.*.                                                                     00010000
.*$MACRO=INUSE      DATE=05/10/80       SOURCE=UCLA(JCJ)                00020000
.*  THIS IS AN COPY OF OACINUSE, UNDER A DIFFERENT NAME, AND WITH THE   00030000
.*       OACGBLS AND OACPRTPM REMOVED/INTEGRATED                        00040000
.*                                                                      00050000
.*  THIS MACRO WILL LIST OUT THE USING TABLE WHICH IS MAINTAINED BY     00060000
.*       'USNGX' AND 'DROPX'.                                           00070000
.*.                                                                     00080000
         MACRO                                                          00090000
         INUSE &DUMMY,&PRINT=                                           00100000
         GBLC  &USELAB(16),&USEREG(16)  PGM LABELS,REGISTERS USED       00110000
         LCLA  &UCNT                    USE INDEX                       00120000
         LCLB  &GOTREG                  WHETHER WE FOUND REGS IN USE    00130000
         AIF   (T'&PRINT EQ 'O').GO#USE SKIP PUSH IF NOT NEEDED         00140000
         PUSH  PRINT                                                    00150000
         PRINT &PRINT                                                   00160000
.GO#USE  SPACE 1                                                        00170000
         MNOTE *,'THE FOLLOWING REGISTER(S) ARE IN USE:'                00180000
         SPACE 1                                                        00190000
.USELOOP ANOP                           USED REGS LOOP                  00200000
&UCNT    SETA  &UCNT+1                  UP THE USE INDEX                00210000
         AIF   ('&USEREG(&UCNT)' EQ '').CHKEND THIS ENTRY IN USE?       00220000
         MNOTE *,'       &USELAB(&UCNT),&USEREG(&UCNT)'                 00230000
&GOTREG  SETB  1                        SAY WE FOUND SOMETHING          00240000
.CHKEND  AIF   (&UCNT LT 16).USELOOP    CHECK FOR END OF ARRAY          00250000
         AIF   (&GOTREG).FINI           LEAVE IF WE HAD REGS IN USE     00260000
         MNOTE *,'       *** NONE ****'                                 00270000
.FINI    SPACE 2                                                        00280000
         AIF   (T'&PRINT EQ 'O').EXIT   SKIP POP IF NOT NEEDED          00290000
         POP   PRINT                                                    00300000
.EXIT    MEND                                                           00310000
