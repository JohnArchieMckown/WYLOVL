         MACRO                                                                  
&LBL     PATH  &MYTAG=,&YOURTAG=,&CONTROL=,&BUFSIZE=,&ECB=,            *        
               &FLAG=,&PASS=,&PFX=,&EXCL=                                       
.*                                                                              
         LCLC  &PRF,&MCTL,&MEXC,&MNAM,&TEMPA                                    
         LCLB  &BMYT,&BYOU,&BCON,&BBUF,&BECB,&BFLA,&BPAS,&BGEN                  
.*                                                                              
         AIF   (K'&PFX GT 4).MN0                                                
&PRF     SETC  '&PFX'                                                           
.*                                                                              
&BMYT    SETB  ('&MYTAG' NE '')                                                 
&BYOU    SETB  ('&YOURTAG' NE '')                                               
&BCON    SETB  ('&CONTROL' NE '')                                               
&BBUF    SETB  ('&BUFSIZE' NE '')                                               
&BECB    SETB  ('&ECB' NE '')                                                   
&BFLA    SETB  ('&FLAG' NE '')                                                  
&BPAS   SETB  ('&PASS' NE '')                                                   
.*                                                                              
&MCTL    SETC  '40'                                                             
&MNAM    SETC  '20'                                                             
&MEXC    SETC  '10'                                                             
&BGEN    SETB  (&BMYT OR &BYOU OR &BCON OR &BBUF OR &BECB OR &BFLA     X        
               OR &BPAS )                                                       
         AIF   (&BGEN).NODEFP                                                   
         AIF   ('&PFX' NE '').NODEFP                                            
&PRF     SETC  'PATH'                                                           
.NODEFP  ANOP                                                                   
.*                                                                              
.*  MAKE SURE REQUIRED PARMS ARE PRESENT                                        
.*                                                                              
         AIF   (&BGEN AND NOT(&BMYT AND &BECB AND &BFLA )).MN1                  
.*                                                                              
.*  -----------PROCESS OPTIONAL LABEL------------                               
.*                                                                              
         AIF   ('&LBL' EQ '').NOLBL                                             
&LBL     DC    0F'0'                                                            
.NOLBL   ANOP                                                                   
&PRF     DC    0F'0'                                                            
.*                                                                              
.*  -----------PROCESS MAGIC WORD---------------                                
.*                                                                              
&PRF     DEFINE MGC,@0,4,0        PATH MAGIC WORD FROM INIT CALL                
.*                                                                              
.*  -------------PROCESS DUP SYMBOL JOBNAME POINTER                             
.*                                                                              
&PRF     DEFINE DJNM,@0,4,0        POINTER TO PCBO'S JOBNAME                    
.*                                                                              
.* ------------PROCESS MYTAG PARM---------------                                
.*                                                                              
&PRF     DEFINE MY,&MYTAG,8,0     MY SYMBOLIC TAG                               
.*                                                                              
.*  -----------PROCESS YOURTAG PARM-------------                                
.*                                                                              
&PRF     DEFINE YOU,&YOURTAG,8,FFFFFFFFFFFFFFFF   YOUR SYMBOLIC TAG             
.*                                                                              
.*  -----------PROCESS ECB PARM----------------                                 
.*                                                                              
&PRF     DEFINE ECB,@&ECB,4,0     ECB ADDR FOR BUFFER MGMT POST                 
.*                                                                              
.*  -----------PROCESS PATHMODE FLAGS:  CONTROL PARM----------                  
.*                                                                              
         AIF   ('&CONTROL' NE 'YES' AND '&CONTROL'NE 'NO'AND &BCON).MN4         
&BCON    SETB  ('&CONTROL' EQ 'YES')                                            
&TEMPA   SETC  '&BCON*X''&MCTL''+&BYOU*X''&MNAM'''                              
         ORG   *-4                                                              
&PRF     DEFINE MFL,@&TEMPA,1,0   MODE FLAGS                                    
         ORG                                                                    
         AIF   ('&PRF' EQ '').NOMFL                                             
*                                                                               
&PRF.MCTL EQU  X'&MCTL'      CALLER ASKS TERMINATION CONTROL                    
&PRF.MNAM EQU  X'&MNAM'      CALLER PROVIDES CALLEE NAME                        
&PRF.MDP1 EQU  X'10'       DUPLICATE TAGS FOUND: MY=PCBO, YOU=PCBA              
&PRF.MDP2 EQU  X'08'       DUPLICATE TAGS FOUND: MY=PCBA, YOU=PCBO              
*                                                                               
.NOMFL   ANOP                                                                   
.*                                                                              
.*  -----------PROCESS BUFSIZE PARM------------                                 
.*                                                                              
&PRF     DEFINE BUF,@&BUFSIZE,4,0   MY BUFFER SIZE                              
.*                                                                              
.*  -----------PROCESS FLAG PARM---------------                                 
.*                                                                              
&PRF     DEFINE ECBF,@&FLAG,4,0   FLAG BYTE ADDR: REASON FOR POST               
         AIF   ('&PRF' EQ '').NOPFL                                             
*                                                                               
&PRF.FCLS EQU   X'80'         I AM POSTED BECAUSE HE CLOSED                     
&PRF.FFQE EQU   X'40'         I AM POSTED BECAUSE HIS FULL Q EMPTIED            
&PRF.FNI  EQU   X'20'         I AM POSTED BECAUSE NEW INFO IN MY Q              
&PRF.FDP  EQU   X'10'         I AM POSTED AT HIS DISCRETION                     
&PRF.FOPN EQU   X'08'         I AM POSTED BECAUSE HE OPENED                     
*                                                                               
.NOPFL   ANOP                                                                   
.*                                                                              
.*  -----------PROCESS PASS PARM ------                                         
.*                                                                              
&PRF     DEFINE PASS,&PASS,4,0    PASSWORD FOR INITIALIZATION                   
.*                                                                              
.*  ------------END OF CONTROL BLOCK----------------                            
.*                                                                              
         AIF   ('&PRF' EQ '').NOEND                                             
*                                                                               
&PRF.SIZE EQU  *-&PRF        LENGTH OF PATH BLOCK                               
.NOEND   ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
.*  -----------PROCESS ERRORS------------------                                 
.*                                                                              
.MN0     MNOTE 16,'PFX VALUE MAY BE NO LONGER THAN 4 CHARACTERS.'               
         MEXIT                                                                  
.MN1  MNOTE 16,'MYTAG, ECB AND FLAG OPERANDS MUST BE NON-NULL'                  
         MEXIT                                                                  
.MN4     MNOTE 16,'CONTROL PARM MAY ONLY BE "YES","NO" OR OMITTED.'             
         MEND                                                                   
