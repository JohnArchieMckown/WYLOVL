         MACRO                                                                  
&L       SCOM  &CODE,&PARM,&PCB,&BRANCH=                                        
.*  Modified, 2/98, RAL                                                         
         LCLA  &I                                                               
         LCLC  &SVCCOM                                                          
&SVCCOM  SETC  '251'                   NUMBER ASSIGNED TO SVC                   
         AIF   ('&CODE' EQ '').NOCODE                                           
         AIF   ('&L' EQ '').NOL                                                 
&L       DS    0H                                                               
.NOL     AIF   ('&CODE' EQ 'TABLE'  ).TABLE                                     
         AIF   ('&CODE' EQ 'OPEN'   ).OPEN                                      
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'CLOSE'  ).CLOSE                                     
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'SEND'   ).SEND                                      
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'GET'    ).GET                                       
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'LOCATE' ).LOCATE                                    
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'FREELOC').FREELOC                                   
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'FREE'   ).FREE                                      
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'POST'   ).POST                                      
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'INFO'   ).INFO                                      
&I       SETA  &I+4                                                             
         AIF   ('&CODE' EQ 'SETINFO').SETINFO                                   
         SYSLR 15,&CODE                                                         
         AGO   .DOPCB                                                           
.CLOSE   ANOP                                                                   
.SEND    ANOP                                                                   
.GET     ANOP                                                                   
.LOCATE  ANOP                                                                   
.FREELOC ANOP                                                                   
.FREE    ANOP                                                                   
.POST    ANOP                                                                   
.INFO    ANOP                                                                   
.SETINFO ANOP                                                                   
         SYSLR 15,&I                                                            
.DOPCB   ANOP                                                                   
         SYSLR 0,&PCB,OP=L                                                      
         AGO   .SVC                                                             
.OPEN    ANOP                                                                   
         SYSLR 0,0                                                              
         SYSLR 15,0                                                             
.SVC     ANOP                                                                   
         SYSLR 1,&PARM                                                          
.*                                                                              
         AIF   ('&BRANCH' EQ '').DOSVC                                          
         AIF   ('&BRANCH' EQ 'YES').DOLOADS                                     
         SYSLR 14,&BRANCH,OP=L                                                  
         AGO   .DOBR                                                            
.*                                                                              
.DOLOADS ANOP                                                                   
         L     14,16                   CVT                                      
         L     14,X'0CC'(,14)          SCIP (User) cvt                          
         L     14,X'24'(,14)           Suzan SVC branch entry                   
.DOBR    ANOP                                                                   
         BASSM 14,14                   Branch entry option                      
         MEXIT                                                                  
                                                                                
.DOSVC   ANOP                                                                   
.*                                                                              
.* GENERATE SVC INSTRUCTION                                                     
.*                                                                              
         SVC   &SVCCOM                                                          
         MEXIT                                                                  
.*                                                                              
.TABLE   ANOP                                                                   
OPEN     EQU   0                                                                
CLOSE    EQU   4                                                                
SEND     EQU   8                                                                
GET      EQU   12                                                               
LOCATE   EQU   16                                                               
FREELOC  EQU   20                                                               
FREE     EQU   24                                                               
POST     EQU   28                                                               
INFO     EQU   32                                                               
         MEXIT                                                                  
.NOCODE  ANOP                                                                   
         MNOTE 8,'SCOM REQUEST CODE NOT SPECIFIED'                              
         MEXIT                                                                  
         MEND                                                                   
