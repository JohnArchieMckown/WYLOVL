         MACRO                                                                  
&LBL     PATH  &MYTAG=,&YOURTAG=,&CONTROL=,&BUFSIZE=,&ECB=,            *        
               &EXIT=,&FLAG=,&PASS=,&PFX=,&EXCL=,&VERSION=1                     
.*       Modified, 11/90, Niz (minor)                                           
.*                                                                              
         LCLC  &PRF,&MCTL,&MEXC,&MNAM,&TEMPA,&VER2                              
         LCLB  &BMYT,&BYOU,&BCON,&BBUF,&BECB,&BFLA,&BPAS,&BGEN,&NEW             
.*                                                                              
         AIF   (K'&PFX GT 4).MN0                                                
&PRF     SETC  '&PFX'                                                           
.*                                                                              
&BMYT    SETB  ('&MYTAG' NE '')                                                 
&BYOU    SETB  ('&YOURTAG' NE '')                                               
&BCON    SETB  ('&CONTROL' NE '')                                               
&BBUF    SETB  ('&BUFSIZE' NE '')                                               
&BECB    SETB  ('&ECB' NE '')                                                   
&BFLA    SETB  ('&FLAG' NE '')                                                  
&BPAS    SETB  ('&PASS' NE '')                                                  
.*                                                                              
&VER2    SETC  '80'                                                             
&MCTL    SETC  '40'                                                             
&MNAM    SETC  '20'                                                             
&MEXC    SETC  '10'                                                             
&BGEN    SETB  (&BMYT OR &BYOU OR &BCON OR &BBUF OR &BECB OR &BFLA     X        
               OR &BPAS )                                                       
&NEW     SETB  ('&VERSION' EQ '2')                                              
         AIF   ('&VERSION' EQ '1' OR '&VERSION' EQ '2').MODOK                   
         MNOTE 4,'VERSION VALUE INVALID, VERSION=1 ASSUMED'                     
.MODOK   ANOP  ,                                                                
         AIF   (&BGEN).NODEFP                                                   
         AIF   ('&PFX' NE '').NODEFP                                            
&PRF     SETC  'PATH'                                                           
.NODEFP  ANOP                                                                   
.*                                                                              
.*  MAKE SURE REQUIRED PARMS ARE PRESENT                                        
.*                                                                              
         AIF   (&BGEN AND NOT(&BMYT AND &BECB AND &BFLA )).MN1                  
.*                                                                              
.*  -----------PROCESS OPTIONAL LABEL------------                               
.*                                                                              
         AIF   ('&LBL' EQ '').NOLBL                                             
&LBL     DC    0F'0'                                                            
.NOLBL   ANOP                                                                   
&PRF     DC    0F'0'                                                            
.*                                                                              
.*  -----------PROCESS MAGIC WORD---------------                                
.*                                                                              
&PRF     DEFINE MGC,@0,4,0        PATH MAGIC WORD FROM INIT CALL                
.*                                                                              
.*  -------------PROCESS DUP SYMBOL JOBNAME POINTER                             
.*                                                                              
&PRF     DEFINE DJNM,@0,4,0       POINTER TO PCBO'S JOBNAME                     
.*                                                                              
.* ------------PROCESS MYTAG PARM---------------                                
.*                                                                              
&PRF     DEFINE MY,&MYTAG,8,0     MY SYMBOLIC TAG                               
.*                                                                              
.*  -----------PROCESS YOURTAG PARM-------------                                
.*                                                                              
&PRF     DEFINE YOU,&YOURTAG,8,FFFFFFFFFFFFFFFF   YOUR SYMBOLIC TAG             
         AIF   (&NEW).V2A                                                       
.*                                                                              
.*  -----------PROCESS ECB PARM----------------                                 
.*                                                                              
&PRF     DEFINE ECB,@&ECB,4,0     ECB ADDR FOR BUFFER MGMT POST                 
         ORG   *-4                                                              
.V2A     ANOP  ,                                                                
.*                                                                              
.*  -----------PROCESS PATHMODE FLAGS:  CONTROL PARM----------                  
.*                                                                              
         AIF   ('&CONTROL' NE 'YES' AND '&CONTROL'NE 'NO'AND &BCON).MN4         
&BCON    SETB  ('&CONTROL' EQ 'YES')                                            
&TEMPA   SETC  '&NEW*X''&VER2''+&BCON*X''&MCTL''+&BYOU*X''&MNAM'''              
&PRF     DEFINE MFL,@&TEMPA,1,0   MODE FLAGS                                    
         AIF   ('&PRF' EQ '').NOMFL                                             
*                                                                               
&PRF.VER2 EQU  X'&VER2'      CALLER IS USING VERSION 2                          
&PRF.MCTL EQU  X'&MCTL'      CALLER ASKS TERMINATION CONTROL                    
&PRF.MNAM EQU  X'&MNAM'      CALLER PROVIDES CALLEE NAME                        
&PRF.MDP1 EQU  X'&MEXC'      DUPLICATE TAGS FOUND: MY=PCBO, YOU=PCBA            
&PRF.MDP2 EQU  X'08'         DUPLICATE TAGS FOUND: MY=PCBA, YOU=PCBO            
*                                                                               
.NOMFL   ANOP                                                                   
         AIF   (&NEW).V2B                                                       
         ORG   ,                                                                
         AGO   .OECBOK                                                          
.V2B     ANOP  ,                                                                
.*                                                                              
.*  -----------PROCESS OLD ECB PARM----------------                             
.*                                                                              
&PRF     DEFINE OECB,@0,3,0       ECB ADDR FOR BUFFER MGMT POST                 
.OECBOK  ANOP                                                                   
.*                                                                              
.*  -----------PROCESS BUFSIZE PARM------------                                 
.*                                                                              
&PRF     DEFINE BUF,@&BUFSIZE,4,0   MY BUFFER SIZE                              
.*                                                                              
.*  -----------PROCESS FLAG PARM---------------                                 
.*                                                                              
&PRF     DEFINE ECBF,@&FLAG,4,0   FLAG BYTE ADDR: REASON FOR POST               
         AIF   ('&PRF' EQ '').NOPFL                                             
*                                                                               
&PRF.FCLS EQU   X'80'         I AM POSTED BECAUSE HE CLOSED                     
&PRF.FFQE EQU   X'40'         I AM POSTED BECAUSE HIS FULL Q EMPTIED            
&PRF.FNI  EQU   X'20'         I AM POSTED BECAUSE NEW INFO IN MY Q              
&PRF.FDP  EQU   X'10'         I AM POSTED AT HIS DISCRETION                     
&PRF.FOPN EQU   X'08'         I AM POSTED BECAUSE HE OPENED                     
&PRF.FCLC EQU   X'04'         I AM POSTED BECAUSE MY CLOSE IS COMPLETE          
*                                                                               
.NOPFL   ANOP                                                                   
.*                                                                              
.*  -----------PROCESS PASS PARM ------                                         
.*                                                                              
&PRF     DEFINE PASS,&PASS,4,0    PASSWORD FOR INITIALIZATION                   
         AIF   (NOT &NEW).DONE                                                  
.*                                                                              
.*  -----------PROCESS ECB PARM----------------                                 
.*                                                                              
&PRF     DEFINE ECB,@&ECB,4,0     ECB ADDR FOR BUFFER MGMT POST                 
.*                                                                              
.*  -----------PROCESS EXIT PARM----------------                                
.*                                                                              
&PRF     DEFINE EXIT,@&EXIT,4,0   EXIT ROUTINE ADDRESS                          
.*                                                                              
.*  -----------RESERVED AREA-------------------                                 
.*                                                                              
&PRF     DEFINE PINP,,4,0         PINFO ptr (or zero) for Open                  
.*                                                                              
&PRF     DEFINE RESV,,4,0         RESERVED FOR FUTURE USE                       
         AIF   ('&PRF' EQ '').DONE     11/90, Niz                               
*                                                                               
*  VERSION 2 RETURN CODES                                                       
*                                                                               
&PRF.ROK  EQU  0                       COMPLETE OK                              
&PRF.RIFC EQU  4                       INVALID FUNCTION CODE                    
&PRF.RPMW EQU  8                       INVALID PATH MAGIC WORD                  
&PRF.RBAF EQU  12                      PARTNER'S BUFFER AREA FULL               
&PRF.RNOP EQU  16                      PARTNER NOT THERE                        
&PRF.RBOM EQU  20                      BUFFER OVER PARTNER'S MAXIMUM            
&PRF.RIBS EQU  24                      RECEIVE BUFFER TOO SMALL                 
&PRF.RBPI EQU  28                      INVALID BUFFER POINTER                   
&PRF.RIAD EQU  32                      INVALID ADDRESS                          
&PRF.RES1 EQU  36                      RESERVED                                 
&PRF.RES2 EQU  40                      RESERVED                                 
&PRF.RES3 EQU  44                      RESERVED                                 
&PRF.RES4 EQU  48                      RESERVED                                 
.DONE    ANOP                                                                   
.*                                                                              
.*  ------------END OF CONTROL BLOCK----------------                            
.*                                                                              
         AIF   ('&PRF' EQ '').NOEND                                             
*                                                                               
&PRF.SIZE EQU  *-&PRF        LENGTH OF PATH BLOCK                               
.NOEND   ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
.*  -----------PROCESS ERRORS------------------                                 
.*                                                                              
.MN0     MNOTE 16,'PFX VALUE MAY BE NO LONGER THAN 4 CHARACTERS.'               
         MEXIT                                                                  
.MN1     MNOTE 16,'MYTAG, ECB AND FLAG OPERANDS MUST BE NON-NULL'               
         MEXIT                                                                  
.MN4     MNOTE 16,'CONTROL PARM MAY ONLY BE "YES","NO" OR OMITTED.'             
         MEND                                                                   
