         MACRO                                                          00010000
         USNGX &PARMS                                                   00020000
.*.                                                                     00030000
.*$MACRO=USNGX      DATE=05/10/80       SOURCE=UCLA(JCJ)                00040000
.*  THIS IS AN COPY OF OACUSING, UNDER A DIFFERENT NAME                 00050000
.*                                                                      00060000
.*  THIS MACRO IS USED TO DO A 'USING' AND ADD THE INFORMATION TO A     00070000
.*       TABLE WHICH IS PRINTED OUT BY THE 'INUSE' MACRO.  THE 'DROPX'  00080000
.*       MACRO MUST BE USED TO REMOVE ENTRIES FROM THIS TABLE.          00090000
.*.                                                                     00100000
         GBLC  &USELAB(16),&USEREG(16)  PGM LABELS,USE REGISTERS        00110000
         LCLA  &RCNT,&PNDX,&UNDX        # OF OPERANDS,PARM/USE INDICES  00120000
         LCLA  &TP1,&HOLE               TEMP WORK,1ST UNUSED SAVE ENTRY 00130000
         LCLB  &GOTREG                  WHETHER TO DO USING OR NOT      00140000
         LCLC  &CHR1,&CHR2,&C(16)       TEMPORARY WORK CHARS,COMMAS     00150000
         LCLC  &R(16),&S                REGS                            00160000
         LCLC  &U,&V,&W                 R(15),C(16),R(16)               00170000
&PNDX    SETA  1                        SET UP PARM STRING INDEX        00180000
&RCNT    SETA  N'&SYSLIST               GET NUMBER OF OPERANDS          00190000
         AIF   (&RCNT LE 1).ERR01       TOO FEW INPUT PARAMETERS        00200000
         AIF   (&RCNT LE 17).SYS1#CK    CONTINUE IF LOW ENOUGH          00210000
         MNOTE 4,'TOO MANY INPUT PARAMETERS. ONLY 17 PROCESSED'         00220000
&RCNT    SETA  17                       SET MAX ALLOWABLE               00230000
.SYS1#CK ANOP                                                           00240000
&S       SETC  '&SYSLIST(1)'            SET FOR SHORTER STRING BELOW    00250000
.COMLOOP ANOP                                                           00260000
&PNDX    SETA  &PNDX+1                  INCREMENT INPUT STRING INDEX    00270000
&UNDX    SETA  0                        INITIALIZE USE INDEX            00280000
&HOLE    SETA  0                        INITIALIZE UNUSED ENTRY INDEX   00290000
.USELOOP AIF   ('&SYSLIST(&PNDX)' EQ '').CHK#END BR IF NULL STRING      00300000
&UNDX    SETA  &UNDX+1                  UP THE USE INDEX                00310000
         AIF   ('&USEREG(&UNDX)' EQ '&SYSLIST(&PNDX)').DO#USE PREVUSE?  00320000
         AIF   ((&HOLE NE 0) OR ('&USEREG(&UNDX)' NE '')).CHKEND        00330000
.* WE BRANCH TO CHKEND IF WE ALREADY HAVE A HOLE OR NO HOLE EXISTS HERE 00340000
&HOLE    SETA  &UNDX                    KEEP THE SAVE TABLE FREE INDEX  00350000
.CHKEND  AIF   (&UNDX LT 16).USELOOP    LOOP FOR ALL ENTRIES            00360000
&UNDX    SETA  &HOLE                    GET FREE ENTRY INDEX IN CASE    00370000
         AIF   (&HOLE NE 0).DO#USE1     BRANCH IF SPACE FOR ENTRY       00380000
         MNOTE 4,'USNGX SAVE TABLE FULL. OPERAND TRUNCATED'             00390000
         AGO   .USINGGO                 GO DO JUST WHAT WE CAN          00400000
.DO#USE  ANOP                                                           00410000
&CHR1    SETC  '********'(1,K'&SYSLIST(&PNDX)) STARS FOR THE REG        00420000
&CHR2    SETC  '****************'(1,K'&USELAB(&UNDX)) MAX 16 CHAR LABEL 00430000
         MNOTE *,'*********&CHR1*********************&CHR2'             00440000
         MNOTE *,'REGISTER &SYSLIST(&PNDX) PREVIOUSLY BASED ON &USELAB(@00450000
               &UNDX)'                                                  00460000
         MNOTE *,'*********&CHR1*********************&CHR2'             00470000
.DO#USE1 ANOP                           LOG THE REG USE HERE            00480000
&CHR1    SETC  '&SYSLIST(1)'            GET A DEFAULT LABEL             00490000
         AIF   (&PNDX EQ 2).SETLAB      IF FIRST REG, NO +NNNN TO ADD   00500000
&TP1     SETA  (&PNDX-2)*4096           ELSE GET ADDED AMOUNT           00510000
&CHR1    SETC  '&CHR1'.'+'.'&TP1'       APPEND THE +NN FOR EXTRA REGS   00520000
.SETLAB  ANOP                           MOVE IN THE SAVE LABELS HERE    00530000
&USELAB(&UNDX) SETC '&CHR1'             SET SAVE TABLE DATA             00540000
&USEREG(&UNDX) SETC '&SYSLIST(&PNDX)'   GET THE REG VALUE IN SAVEAREA   00550000
&R(&PNDX-1) SETC '&SYSLIST(&PNDX)'      GET THE REG VALUE FOR USING     00560000
&C(&PNDX-1) SETC ','                    AND A LEADING COMMA ALSO        00570000
&GOTREG  SETB  1                        SAY WE CAN DO USING             00580000
.CHK#END AIF   (&PNDX LT &RCNT).COMLOOP LOOP FOR ALL ENTRIES            00590000
.USINGGO AIF   (NOT &GOTREG).EXIT       NOW TELL ASSEMBLER IF WE CAN    00600000
&U       SETC  '&R(15)'                 SO WE HAVE A SHORTER STRING     00610000
&V       SETC  '&C(16)'                 SO WE HAVE A SHORTER STRING     00620000
&W       SETC  '&R(16)'                 SO WE HAVE A SHORTER STRING     00630000
         USING &S&C(1)&R(1)&C(2)&R(2)&C(3)&R(3)&C(4)&R(4)&C(5)&R(5)&C(6@00640000
               )&R(6)&C(7)&R(7)&C(8)&R(8)&C(9)&R(9)&C(10)&R(10)&C(11)&R@00650000
               (11)&C(12)&R(12)&C(13)&R(13)&C(14)&R(14)&C(15)&U&V&W     00660000
         MEXIT                                                          00670000
.ERR01   MNOTE 4,'MISSING INPUT PARAMETERS'                             00680000
         MEXIT                                                          00690000
.EXIT    MEND                                                           00700000
