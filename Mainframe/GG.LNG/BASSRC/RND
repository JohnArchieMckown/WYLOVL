RND      TITLE 'RANDOM NO. ROUTINE'
RND      CSECT
         USING *,R15
         SAVE  (14,12),,*
         USING AREA,USER
         L     R10,AFNC
         LA    R10,0(R10,USER)
         SRL   R10,3
         SLL   R10,3
         USING WORK,R10
*
         LE    F2,FLRAN1
         STE   F2,FLRAN
         LTER  F0,F0
         BM    MARG               USE RANDOM SEED AS ARG.
         BP    COMPUTE            USE PASSED SEED AS ARG.
         L     WK1,RAND
         B     GENER
MARG     SR    WK1,WK1            GET RAND. NO. FROM TIMER
         IC    WK1,83
         SLL   WK1,7
         LA    WK1,1(WK1)
         B     GENER
COMPUTE  STE   F0,TEMP            PUT USERS NO. IN TEMP
         L     WK1,TEMP
         LA    WK2,1
         OR    WK1,WK2
GENER    M     WK2,CONST          FORM NEW RAND. NO.
         LR    WK2,WK1
         SLL   WK2,1
         SRL   WK2,1
         ST    WK2,TEMP2
         ST    WK2,0(R1)
         LD    F0,FLOAT           FLOAT RESULT
         AD    F0,FLRAN
         ME    F0,FLOATCON
DONE     LM    R14,R12,12(R13)
         B     4(R14)
F0       EQU   0
R1       EQU   1
F2       EQU   2
WK2      EQU   8                  WK2,WK1 EVEN-ODD PAIR
WK1      EQU   9
R10      EQU   10
USER     EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DS    0D
FLOAT    DC    X'4E',XL7'00'
FLRAN1   DC    X'4E000000'
RAND     DC    F'987654321'       STORED SEED
CONST    DC    F'65539'
FLOATCON DC    E'0.4656613E-9'       2**-31
WORK     DSECT
FLRAN    DS    F
TEMP2    DS    F
TEMP     DS    F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
