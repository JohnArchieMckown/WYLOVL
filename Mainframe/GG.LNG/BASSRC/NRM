NRM      TITLE 'NRM ROUTINE  -  MATRIX COLUMN NORMALIZATION'
NRM      CSECT
*
*        BASIC STATEMENT:
*
*              MAT A=NRM(B)
*
*        ARGUMENTS:
*              0(R1)  -  ADDRESS OF INPUT ARRAY - B
*              4(R1)  -  ADDRESS OF OUTPUT ARRAY - A
*              8(R1)  -  ROW-SIZE (M)
*             12(R1)  -  COL-SIZE (N)
*
*        RETURNS TO 4(R14) FOR NORMAL RETURN AND
*        RETURNS TO 0(R14) FOR ERROR RETURN (WITH R1 BEING THE
*          ERROR MESSAGE NUMBER FROM THE SQR ROUTINE.
*
*        BASIC PROGRAM TO NORMALIZE MATRIX BY COLUMNS:
*
*        10  MAT INPUT B(M,N)
*        20  FOR J= 1 TO N
*        30    S1=0
*        40    FOR I= 1 TO M
*        50      S1 = S1 + B(I,J)**2
*        60    NEXT I
*        70
*        80    S2 = SQR (S1)
*        90    FOR I= 1 TO M
*       100      A(I,J) = B(I,J)/S2
*       110    NEXT I
*       120  NEXT J
*       130
*
***********************************************************************
         EJECT
*        REGISTER USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2                  -BASE OF INPUT ARRAY B
R3       EQU   3                  -BASE OF OUTPUT ARRAY A
R4       EQU   4                  -ROW SIZE
M        EQU   R4                   "
R5       EQU   5                  -COL SIZE
N        EQU   R5                   " (MAIN LOOP INDEX)
R6       EQU   6
I        EQU   R6                 -INDEX TO ROWS
R7       EQU   7                  -NUMBER OF BYTES IN A ROW
*                                  (N*4)
R8       EQU   8                  -CURRENT ELEMENT OF B
R9       EQU   9                  -CURRENT ELEMENT OF A
R10      EQU   10
R11      EQU   11
R12      EQU   12                 -BASE
R13      EQU   13                 -SAVE AREA
R14      EQU   14                 -RETURNS
R15      EQU   15                 -ENTRY TO SQR
*
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
S1       EQU   0
S2       EQU   2
*
         SAVE  (14,12),,*
         BALR  R12,0
         USING *,R12
         LA    R2,72(R13)         -NEXT SAVE AREA
         ST    R13,4(R2)          -SAVE EXECS SAVE AREA
         ST    R2,8(R13)          -COMPLETE CHAIN
         LR    R13,R2
*
         LM    R2,R5,0(R1)        -PICK UP ARGUMENTS
*
         LR    R7,N               -LET R7 BE THE NUMBER OF
         SLL   R7,2                IN THE ROW, TO TRAVEL
*                                  DOWN THE COLUMNS.
*
***********************************************************************
         EJECT
*
*        NORMALIZE MATRIX BY COLUMNS WHERE
*              R8 IS THE CURRENT INPUT ELEMENT
*              R9 IS THE CURRENT OUTPUT ELEMENT
*              S1 IS THE SUM OF THE SQUARES OF EACH COLUMN
*              S2 IS THE SQUARE ROOT OF S1
*
LOOP     EQU   *
*                                 20  FOR J=1 TO N
         SER   S1,S1              30   S1=0
         LR    R8,R2
         LR    I,M                40   FOR I= 1 TO M
LOOP1    EQU   *
         LE    F2,0(R8)
         MER   F2,F2
         AER   S1,F2              50    S1 = S1 + B(I,J)**2
         AR    R8,R7
         BCT   I,LOOP1            60   NEXT I
*
         L     R15,VSQR           80  S2= SQR(S1)
         BALR  R14,R15
         B     ERROR
         LER   S2,S1
*
         LR    R8,R2
         LR    R9,R3
*
         LR    I,M                90  FOR I= 1 TO M
LOOP2    EQU   *
         LE    F0,0(R8)
         DER   F0,S2
         STE   F0,0(R9)           100  A(I,J) = B(I,J)/S2
         AR    R8,R7
         AR    R9,R7
         BCT   I,LOOP2            110 NEXT I
*
         LA    R2,4(R2)
         LA    R3,4(R3)
*
         BCT   N,LOOP             120 NEXT J
*
***********************************************************************
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         B     4(R14)
*
ERROR    EQU   *
         L     R13,4(R13)
         LM    R14,R15,12(R13)
         LM    R2,R12,28(R13)
         BR    R14
*
VSQR     DC    V(SQR)
*
         END
