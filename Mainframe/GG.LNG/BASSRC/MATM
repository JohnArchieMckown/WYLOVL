MATM     TITLE 'BASIC MAT MULT. ROUTINE'
MATM     CSECT
         FSTART  AMLTL
         LM    R2,R7,0(R1)
*
****
****
*
*   MAT C = B * A
*
*   R2= ADDRESS OF B
*   R3= ADDRESS OF A
*   R4= ADDRESS OF C
*   R5= COLS OF B
*   R6= COLS OF C
*   R7= ROWS OF C
*
****
         LR    R0,R13
         LR    L,R5
         LR    M,R6
         LR    N,R7
         LA    J1,4
         STH   L,LT
         LR    I1,M
         SLL   I1,2
         LR    I2,I1
         MH    I2,LT
         SR    I2,I1
         LR    J2,I1
         SR    J2,J1
         LR    N1,N
         STH   M,MT
         MH    N1,MT
         SLL   N1,2
         DROP  11
         LA    K,0
         LR    KK,K
L0       LA    J,0
L1       LA    I,0
         LR    IK,KK
         SER   F6,F6
L2       LR    IJ,I
         AR    IJ,J
         LE    F0,0(IJ,R3)
         ME    F0,0(IK,R2)
         LA    IK,4(IK)
         AER   F6,F0
         BXLE  I,I1,L2
         STE   F6,0(K,R4)
         LA    K,4(K)
         BXLE  J,J1,L1
         LR    KK,K
         CR    KK,N1
         LR    KK,IK
         BL    L0
****
         LR    R13,R0
         FEXIT
*
****
*
*   EQU'S AND CONSTANTS
*
KK       EQU   11
L        EQU   5
F0       EQU   0
R0       EQU   0
R1       EQU   1
N1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
I        EQU   5
R5       EQU   5
I1       EQU   6
R6       EQU   6
F6       EQU   6
I2       EQU   7
R7       EQU   7
J1       EQU   8
J2       EQU   9
M        EQU   10
J        EQU   10
R11      EQU   11
R12      EQU   12
N        EQU   12
K        EQU   12
R13      EQU   13
IJ       EQU   13
R14      EQU   14
IK       EQU   14
R15      EQU   15
*
AMLTL    DSECT
LT       DS    H
MT       DS    H
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
