SIN      TITLE 'SIN AND COS ROUTINES'
SINCOS   CSECT
*
*        SINE-COSINE FUNCTIONS (SHORT)
*              1. DIVIDE MAGNITUDE OF ARG BY PI/4 TO FIND OCTANT
*                 AND FRACTION
*              2. IF COSINE CRANK OCTANT NUMBER BY 2
*                 IF SINE FOR NEGATIVE ARG, CRANK OCTANT NUMBER BY 4
*              3. COMPUTE SINE OR COSINE OF FRACTION*PI/4 DEPENDING
*                 ON THE OCTANT
*              4. IF OCTANT NUMBER IS FOR LOWER PLANE, MAKE SIGN -.
*
         ENTRY COS
         ENTRY SIN
*
GR0      EQU   0               SCRATCH REGISTERS
GR1      EQU   1
GRA      EQU   1               ARGUMENT POINTER
GRS      EQU   13              SAVE AREA POINTER
GRR      EQU   14              RETURN REGISTER
GRL      EQU   15              LINK REGISTER
FR0      EQU   0               ANSWER REGISTER
FR2      EQU   2               SCRATCH REGISTERS
FR4      EQU   4
COS      EQU   *
         USING *,15
         SAVE  (14,12),,*
         USING AREA,11
         LA    2,EINT
         L     3,AINT
         ST    2,0(3,11)
         LA    10,4(11)
         A     10,AFNC
         SRL   10,3            ALIGN TO DBL. WD. BDRY
         SLL   10,3
         USING ASICOL,10
         STE    FR0,TEMP
         LE    FR4,CRANK1
         STE   FR4,CRANK
         MVI   CRANK+3,X'02'   FOR COSINE, OCTANT CRANK IS 2
         LA    GR1,TEMP      COS(X) = SIN(PI/2+X)
         LA    GRL,SIN-COS(GRL)   ADJUST BASE REGISTER
         USING SIN,GRL         AND JOIN MAIN CIRCUIT
         BC    15,MERGE
*
SIN      SAVE  (14,12),,*
         USING AREA,11
         LA    2,EINT
         L     3,AINT
         ST    2,0(3,11)
         LA    10,4(11)
         A     10,AFNC
         SRL   10,3          ALIGN TO DBL. WD. BDRY
         SLL   10,3
         USING ASICOL,10
         STE   FR0,TEMP
         LE    FR4,CRANK1
         STE   FR4,CRANK
         MVI   CRANK+3,X'00'   FOR SINE, OCTANT CRANK IS 0 IF +ARG
         LA    GR1,TEMP      OCTANT CRANK IS 4 IF -ARG
         TM    0(GR1),X'80'    SIN(-X) = SIN(PI+X)
         BC    8,*+8
         MVI   CRANK+3,X'04'
MERGE    LA    2,72(13)
         ST    13,4(2)
         ST    2,8(13)
         LR    13,2
         SDR   FR0,FR0         CLEAR FR0 DOUBLE
         SDR   FR2,FR2         CLEAR FR2 DOUBLE
         LE    FR0,TEMP      OBTAIN ARGUMENT
         LPER  FR0,FR0         CONSIDER ARGUMENT TO BE POSITIVE
         CE    FR0,MAX
         BC    10,ERROR        ERROR IF /X/ GRT THAN OR = PI*22**18
         MD    FR0,FOVPI       MULTIPLY BY 4/PI (LONG FORM)
         CE    FR0,ONE
         BC    4,SMALL         IF PRODUCT LESS THAN 1, JUMP
         AW    FR0,CH46        GIVE PROD CHAR OF 46, UNNORMALIZED
         LER   FR2,FR0         INTEGER PART OF PROD TO FR2, UNNORM.
         SDR   FR0,FR2         FRACTION PART OF PROD TO FR0,NORM.
SMALL    AU    FR2,CRANK       ADD OCTANT CRANK TO FR2, UNNORM.
         STE   FR2,OCTNT       SAVE IT.  LAST 3 BITS ARE MODIF. OCTANT
         TM    OCTNT+3,X'01'   IF ODD OCTANT, TAKE COMPLEMENT
         BC    8,EVEN          OF FRACTION TO OBTAIN THE MODIFIED
         SE    FR0,ONE         FRACTION R
         LPER  FR0,FR0
EVEN     SR    GR1,GR1         GR1 = 0 FOR COSINE POLYNOMIAL
         TM    OCTNT+3,X'03'   THIS IS FOR OCTANT 2,3,6, OR 7
         BC    4,*+8           IF OCTANT 1,4,5, OR 8, USE SINE
         LA    GR1,4           POLYNOMIAL.  GR1 = 4
         LER   FR4,FR0
         MER   FR0,FR0         COMPUTE SIN OR COS OF MODIFIED
         LER   FR2,FR0         FRACTION USING PROPER CHEBYSHEV
         ME    FR0,C3(GR1)     INTERPOLATION POLYNOMIAL
         AE    FR0,C2(GR1)
         MER   FR0,FR2
         AE    FR0,C1(GR1)
         MER   FR0,FR2
         AE    FR0,C0(GR1)     SIN(R)/R OR COS(R) READY
         LTR   GR1,GR1
         BC    8,*+6
         MER   FR0,FR4         IF SINE POLYNOMIAL, MULTIPLY R
         TM    OCTNT+3,X'04'
         BC    8,*+6           IF MODIFIED OCTANT IS IN
         LNER  FR0,FR0         LOWER PLANE, SIGN IS NEGATIVE
         FEXIT Y
ERROR    FEXIT Y,5008
EINT     FEXIT Y,5017
         DS    0D
FOVPI    DC    X'41145F306DC9C830'
CH46     DC    X'4600000000000000'
CRANK1   DC    X'46000000'
MAX      DC    X'45C90FDB'
C3       DC    X'BE14E5E0'     -0.00031888   C3
         DC    X'BD25B368'     -0.00003595   S3
C2       DC    X'3F40EBD6'      0.01584991   C2
         DC    X'3EA32F62'      0.00249001   S2
C1       DC    X'C04EF4E5'     -0.30842425   C1 + FUDGE 1
         DC    X'C014ABBC'     -0.08074543   S1
C0       DC    X'41100000'      1.0          C0
         DC    X'40C90FDB'      0.78539816   S0
ONE      EQU   C0
ASICOL   DSECT
OCTNT    DS    F
CRANK    DS    F
TEMP     DS    F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
