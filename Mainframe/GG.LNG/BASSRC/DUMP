*
*   THIS PROGRAM IS CALLED:
*       1 - BY THE CONTROL PROGRAM WHEN THE 'SHOW DATA' COMMAND IS
*           TYPED BY THE USER.
*       2 - BY THE EXECUTIONER WHEN THE DUMP STATEMENT IS
*           ENCOUNTERED DURING EXECUTION OF A BASIC PROGRAM.
*
*   THIS PROGRAM DUMPS ALL INITIALIZED VALUES IN
*   THE USERS DATA AREA.  ARRAY ELEMENTS WHICH ARE ZERO ARE NOT
*   PRINTED
*
*   IT CALLS THE PRINT ROUTINE FOR CONVERSION AND PRINTING OF VALUES.
*   ARG LIST FOR PRINT ROUTINE
*     WORD 1   LOC OF # OR STRING
*     WORD 2   0 FOR #, 1 FOR STRING
*     WORD 3   0 FOR BLANK, 1 FOR ,   2 FOR ;
*
*   THE TIME-SHARED VERSION WILL DISCONTINUE PRINT-OUT AND RETURN TO
*   THE CALLER IF THE USER HITS ATTENTION.
*
*
*        BASIC DUMP STATEMENT OUTPUT FORMAT
*
*   1)   FOR SIMPLE VARIABLES OF THE TYPE A0 TO Z9
*
*   CC1
*     |
*     A0=S___VALUE____  A1=S___VALUE____  A2=S___VALUE____   ETC
*
*   SEVEN OF THESE VALUES ARE PRINTED PER LINE.
*   UNINITIALIZED VALUES ARE NOT PRINTED.
*   .
*   .
*   .
*
*
*   2)   FOR SIMPLE VARIABLES OF THE TYPE A TO Z
*
*   CC1
*     |
*     A= S___VALUE____  B= S___VALUE____  C= S___VALUE____   ETC
*
*   SEVEN OF THESE VALUES ARE PRINTED PER LINE.
*   UNINITIALIZED VALUES ARE NOT PRINTED.
*
*
*
*     3) FOR SINGLE DIM. VARIABLES OF THE TYPE A TO Z
*
*     CC1
*       |
*       A(XXXX)=S___VALUE____  A(XXXX)=S___VALUE____  ETC
*
*   FIVE OF THESE VALUES ARE PRINTED PER LINE.  ZEROES AND
*   UNINITIALIZED VALUES ARE NOT PRINTED.
*          .
*          .
*          .
*
*
*    4) FOR DOUBLE DIM. VARIABLES OF THE TYPE A TO Z
*
*    CC1
*      |
*      A(XXXX,XXXX)=S___VALUE____  A(XXXX,XXXX)=S___VALUE____
*
*    FOUR OF THESE VALUES ARE PRINTED PER LINE.  ZEROES AND
*    UNINITIALIZED VALUES ARE NOT PRINTED.
*         .
*         .
*         .
*
*
*   5)   FOR SIMPLE STRING VARIABLES OF THE TYPE A$ TO Z$
*
*   CC1
*     |
*     A$=CCCCCCCCCCCCCCC
*     B$=CCCCCCCCCCCCCCC
*
*    6)  FOR STRING ARRAYS OF THE TYPE A$ TO Z$
*
*    CC1
*      |
*      A$(XXXX)=CCCCCCCCCCCCCCC
*      B$(XXXX)=CCCCCCCCCCCCCCC
*      .
*      .
*
*
*   VALUES ARE PRINTED AS DECIMAL NUMBERS  IN A 15 CHARACTER FIELD.
*   THE NUMBER MAY BE PRINTED AS AN INTEGER, A FRACTIONAL NUMBER, OR
*   AN E TYPE NUMBER, DEPENDING ON THE SIZE OF THE NUMBER.
*        THE CCCCCCCCCCCCCCC REPRESENTS THE STRING
*
DUMP     TITLE 'DUMP ROUTINE'
DUMP     CSECT
         SAVE  (14,12),,*         STANDARD BASIC SAVE
         LA    R1,72(R13)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
*
         BALR  R12,0              R12 FOR ADDRESSABILITY
         USING *,R12
*
         USING AREA,USER          R11=USER=USER AREA DSECT
         L     R10,AFNC
         LA    R10,4(R10,USER)     DOUBLE WORD ALIGNMENT
         SRL   R10,3
         SLL   R10,3
         USING WORK,R10
*
*   SET CONSTANTS IN CALLING SEQUENCES FOR PRINT ROUTINE
*
         SR    R0,R0
         STC   R0,PFLAG
         ST    R0,ARGU2            0 FOR #
         ST    R0,ARGU8            0 FOR #
         ST    R0,ARGU9            0 FOR SPACE TO NEW LINE
         ST    R0,ARGU12           0 FOR SPACE TO NEW LINE
         LA    R0,1
         ST    R0,ARGU5            1 FOR STRING
         ST    R0,ARGU11           1 FOR STRING
         ST    R0,ARGU3            1 FOR 15 CHAR OUTPUT STRING
         LA    R0,2
         ST    R0,ARGU6            2 FOR ; CONCATENATE OUTPUT FIELD
         L     R15,VPRINT          PICK UP LOC OF PRINT ROUTINE
         LCR   2,0               NEG VALUE FOR PRINT ARG LST
         STCM  2,8,MOPAD3
         STCM  2,8,MOPAD6
         STCM  2,8,MOPAD9
         STCM  2,8,MOPAD12
         TITLE 'DUMP OF SIMPLE VARIABLES A0 TO Z9'
*
*      THIS CODE DUMPS THE SIMPLE VARIABLES A0 THRU Z9
*
*   IT DUMPS THE VALUES, 7 PER LINE, IN THE FORM
*      A0=S___VALUE____  A1=S___VALUE____  ETC
*
*
         MVI   BUFFER+2,C'='       PUT = IN OUTPUT BUFFER
         MVI   LENGTH,X'03'        SET LENGTH OF OUTPUT BUFFER
         LA    PTR,A0TOZ9          PICK UP POINTER TO A0-Z9 DATA AREA
         SR    LP,LP               SET LETTER POINTER TO START AT A
         LA    R0,X'F9'            SET UPPER LIMIT ON NUMBER COUNTER
         LA    MAXL,25             SET REGISTERS TO LOOP 26 TIMES FOR
         LA    INCL,1              A THRU Z
         LA    R9,7                SET MAX # OF VALUES PER LINE
LOOP1    LA    NP,X'F0'            SET INITIAL VALUE OF NUMBER TO 0
LOOP2    L     R14,M0              HAS THIS VALUE BEEN INITIALIZED?
         C     R14,0(PTR)
         BE    NEXTNO              NO-THEN DON'T DUMP IT
         LA    R0,X'F9'            SET UPPER LIMIT ON NUMBER COUNTER
         IC    R5,LETTER(LP)       PICK UP VARIABLE LETTER
         STC   R5,BUFFER           PUT IT IN BUFFER
         STC   NP,BUFFER+1         PUT VARIABLE # IN BUFFER
         LA    R5,BUFFER           PUT ADDRESS OF CHAR STRING IN
         STCM  R5,7,LENGTH+1    STORE POINTER FOR PRINT
         LA    R5,LENGTH        NEXT POINTER
         ST    R5,ARGU4            ARG LIST
         LA    R1,ARGU4            PICK UP LOCATION OF ARG LIST
         BALR  R14,R15             GO TO PRINT ROUTINE
         B     ERROR
         LA    R5,0(PTR)           PICK UP LOC OF NUMBER OF BE PRINTED
         BCT   R9,PR2              IS THIS THE LAST # THE LINE WILL
         LA    R9,7                HOLD?
         BAL   R8,PR7              YES
PR2      BAL   R8,PRB
NEXTNO   LA    NP,1(NP)            BUMP TO NEXT VAR #
         LA    PTR,4(PTR)          BUMP PTR TO A TO Z DATA AREA
         CR    NP,R0               ARE WE PAST 9?
         BNH   LOOP2               NO
         BXLE  LP,INCL,LOOP1       ARE WE TO LETTER 'Z' YET?
*
         LA    R1,7
         BAL   R8,LINEOUT1
         TITLE 'DUMP OF SIMPLE VARIABLES  A TO Z'
*   THIS CODE DUMPS THE SIMPLE VARIABLES A TO Z
*
*
*   IT DUMPS THE VALUES, 7 PER LINE, IN THE FORM,
*      A= S___VALUE____  B= S___VALUE____  ETC
*
         MVI   BUFFER+1,C'='       INITIALIZE OUTPUT BUFFER
         MVI   BUFFER+2,C' '
         SR    NP,NP               PICK UP CONSTANT 0 FOR CHECKING
         LA    R9,7                SET MAX # OF VALUES PER LINE
         L     R0,M0               PICK UP MINUS 0 CONSTANT FOR CHECK
         SR    LP,LP               SET LETTER POINTER TO START AT 'A'
         LA    INCL,16             SIZE OF EACH ENTRY IN ATOZ TABLE
         LA    MAXL,400            DISP TO LAST ENTRY IN ATOZ
         USING ATOZDSCT,PTR
         LA    PTR,ATOZ            PTR LOOPS THROUGH ATOZ DSECT
         AR    MAXL,PTR            COMPUTE DISP PAST USER OF LAST
*                                  ENTRY IN ATOZ TABLE
LOOP3    C     NP,ARRAYLFT         IF LFT=0 ITS A SIMPLE VARIABLE WITH
         BNE   INLOOP3             RGT CONTAINING THE VALUE
         C     R0,ARRAYRGT         IS VALUE = -0?
         BE    INLOOP3             YES-VAR HAS NOT BEEN INITIALIZED
         IC    R5,LETTER(LP)       PICK UP LETTER
         STC   R5,BUFFER           MOVE TO BUFFER
         LA    R5,BUFFER           PICK UP LOC  OF BUFFER TO BE PRINTED
         STCM  R5,7,LENGTH+1    STORE POINTER FOR PRINT
         LA    R5,LENGTH        NEXT POINTER
         ST    R5,ARGU4            PLACE IN ARG LIST
         LA    R1,ARGU4            PICK UP ADDR OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         LA    R5,8(PTR)           PICK UP LOC OF #
         BCT   R9,PR8              IS THIS THE LAST (7TH) VALUE TO BE
*                                  PRINTED ON THIS LINE
         LA    R9,7                RESET MAX # OF VALUES/LINE
         BAL   R8,PR7              YES
PR8      BAL   R8,PRB
INLOOP3  LA    LP,1(LP)            BUMP LETTER PTR.
         BXLE  PTR,INCL,LOOP3      ARE WE TO LETTER 'Z' YET?
*
         LA    R1,7
         BAL   R8,LINEOUT1         GO DUMP LAST LINE
         TITLE 'DUMP OF SINGLE DIM. VARS. A TO Z'
*
*   THIS CODE DUMPS SINGLY DIMED VARIABLES A TO Z
*
*   IT DUMPS THE VALUES, 5 PER LINE, IN THE FORM
*       A(XXXX)=S___VALUE____  A(XXXX)=S___VALUE____
*
*
         MVI   BUFFER1+1,C'('      PUT ( IN OUTPUT BUFFER
         MVI   BUFFER1+15,X'08'    SET LENGTH OF OUTPUT BUFFER
         LA    PTR,ATOZ            SET UP BXLE THROUGH A-Z
         LA    R9,5                SET MAX # OF VALUES PER LINE
         SR    NP,NP               SET PTR TO LETTER
         B     LOOP4X
LOOP4    LA    NP,1(NP)            BUMP LETTER PNTR TO NEXT LETTER
LOOP4X   LA    LP,1                LP CONTAINS SUBSCRIPT
         SR    R0,R0
         C     R0,ARRAYLFT         IF LFT~=0 AND RGT=0 THEN
         BZ    ENDLOOP4            ITS A SINGLE DIM. ARRAY
         CH    R0,ARRAYRGT+2
         BNZ   ENDLOOP4
         LH    R5,ARRAYIDX         COMPUTE THE LOCATION OF THE FIRST
         LA    R5,0(R5,USER)       ELEMENT OF THIS ARRAY
         A     R5,ARRAYBEG
         IC    R0,LETTER(NP)       PICK UP VARIABLE NAME
         STC   R0,BUFFER1          PUT IT IN BUFFER
BACK4    SR    R0,R0               ZERO NEEDED FOR TESTS
         L     R1,M0               MINUS ZERO NEEDED FOR TESTS
LOOP5    C     R1,0(R5)            IS THIS ELEMENT OF THE ARRAY = 0?
         BNE   INLOOP5             NO
SKIP     LA    LP,1(LP)            YES-THEN BUMP SUBSCRIPT BY 1
         CH    LP,ATOZALOC+2       DONE?
         BH    OUT                 GO TO PRINT PARTIAL LINE
         LA    R5,4(R5)            NOT DONE- GO TO NEXT ITEM
         B     LOOP5
INLOOP5  C     R0,0(R5)            IS THIS ELEMENT =0?
         BE    SKIP                YES-THEN DON'T PRINT
         CVD   LP,FIELD            CONVERT SUBS. TO EBCDIC
         UNPK  BUFFER1+2(6),FIELD+5(4) PUT IN OUTPUT BUFFER
         MVI   BUFFER1+6,C')'      PUT ) IN OUTPUT BUFFER
         MVI   BUFFER1+7,C'='      PUT = IN OUTPUT BUFFER
         LA    R0,BUFFER1          PICK UP ADDR OF BUFFER
         STCM  R0,7,BUFFER1+16  STORE POINTER FOR PRINT
         LA    R0,BUFFER1+15    NEXT  POINTER
         ST    R0,ARGU4            STORE IN ARG LIST
         LA    R1,ARGU4            PICK UP LOC OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         BCT   R9,PR11             IS THIS THE LAST (5TH) VALUE TO BE
*                                  PRINTED ON THIS LINE?
         LA    R9,5                RESET MAX # OF VALUES/LINE
         BAL   R8,PR7              YES
PR11     BAL   R8,PRB
         SR    R0,R0               PICK UP CONSTANTS 0 AND -0
         L     R1,M0
         B     SKIP
OUT      LA    R1,5                HAS THIS LINE ALREADY BEEN PRINTED?
         CR    R9,R1
         BE    ENDLOOP4            YES
         BAL   R8,LINEOUT          GO DUMP LINE
         LA    R9,5                RESET # OF VALUES/LINE REGISTER
ENDLOOP4 BXLE  PTR,INCL,LOOP4      GO ON TO NEXT ARRAY
*
         LA    R1,5
         BAL   R8,LINEOUT1         GO DUMP LAST LINE
         TITLE 'DUMP OF DOUBLE DIM. VARS. A TO Z'
*
*   THIS CODE DUMPS DOUBLY DIMED VARIABLES A TO Z
*
*   IT DUMPS VALUES, 4 PER LINE, IN THE FORM
*   A(XXXX,XXXX)=S___VALUE____   ETC
*
*
*
         MVI   BUFFER2+1,C'('      PUT ( IN OUTPUT BUFFER
         MVI   BUFFER2+15,X'0D'    SET LENGTH OF OUTPUT BUFFER
         LA    PTR,ATOZ            SET UP BXLE THROUGH A-Z
         LA    R9,4                SET MAX # OF VALUES PER LINE
         SR    R1,R1               INITIALIZE POINTER TO LETTER
         ST    R1,TEMP1
         B     LOOP6X
LOOP6    L     R1,TEMP1            PICK UP CURRENT LETTER POINTER
         LA    R1,1(R1)            BUMP TO NEXT
         ST    R1,TEMP1            SAVE IT
LOOP6X   LA    LP,1                INITIALIZE LEFT SUB TO 1
         LR    NP,LP               INITIALIZE RIGHT SUB TO 1
         SR    R0,R0
         C     R0,ARRAYLFT         IF RGT,LFT=0 THEN
         BZ    ENDLOOP6            ITS NOT A  DOUBLE DIM. ARRAY
         CH    R0,ARRAYRGT+2
         BZ    ENDLOOP6
         LH    R5,ARRAYIDX         COMPUTE THE LOCATION OF THE FIRST
         LA    R5,0(R5,USER)       ELEMENT OF THIS ARRAY
         A     R5,ARRAYBEG
         IC    R0,LETTER(R1)       PICK UP VARIABLE NAME
         STC   R0,BUFFER2          STORE IN BUFFER
BACK6    SR    R0,R0               ZERO NEEDED FOR TESTS
         L     R1,M0               MINUS ZERO NEEDED FOR TESTS
LOOP7    C     R1,0(R5)            IS THIS ELEMEMT = -0?
         BNE   INLOOP7             NO
SKIP2    LA    NP,1(NP)            BUMP RIGHT SUBSCRIPT BY 1
         C     NP,ARRAYRGT         HAVE WE REACHED MAX VALUE
         BNH   LOOP7X              NO
         LA    R1,4                HAS THIS LINE BEEN PRINTED?
         CR    R1,R9
         BE    LOOP7Y
         BAL   R8,LINEOUT          NO-GO DUMP IT
         LA    R9,4                RESET MAX # OF VALUES/LINE
LOOP7Y   LA    NP,1                YES-RESET RIGHT SUB BACK TO 1
         LA    LP,1(LP)            BUMP LEFT SUBSCRIPT BY 1
         C     LP,ARRAYLFT         HAVE WE REACHED MAX VALUE ON LEFT?
         BH    OUT2                YES
LOOP7X   LA    R5,4(R5)            NO-BUMP ARRAY PNTR TO NEXT VALUE
         SR    R0,R0               PICK UP CONSTANTS 0 AND -0
         L     R1,M0
         B     LOOP7
INLOOP7  C     R0,0(R5)            IS THIS ELEMENT OF THE ARRAY = 0?
         BE    SKIP2               YES-THEN DON'T PRINT IT
         CVD   LP,FIELD            CONVERT LEFT SUB TO EBCDIC
         UNPK  BUFFER2+2(6),FIELD+5(4) PUT IT IN BUFFER
         MVI   BUFFER2+6,C','      PUT , IN BUFFER
         CVD   NP,FIELD            CONVERT RIGHT SUB TO EBCDIC
         UNPK  BUFFER2+7(6),FIELD+5(4)  AND PLACE IN BUFFER
         MVI   BUFFER2+11,C')'     PUT )= IN BUFFER
         MVI   BUFFER2+12,C'='
         LA    R1,BUFFER2          PICK UP ADDR OF BUFFER
         STCM  R1,7,BUFFER2+16  STORE POINTER FOR PRINT
         LA    R1,BUFFER2+15    NEXT  POINTER
         ST    R1,ARGU4            PLACE IN ARG LIST
         LA    R1,ARGU4            PICK UP LOC OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         BCT   R9,PR20             IS THIS THE LAST (4TH) VALUE TO BE
*                                  PRINTED ON THIS LINE
         LA    R9,4                RESET MAX # OF VALUES/LINE
         BAL   R8,PR7
PR20     BAL   R8,PRB
         B     SKIP2
OUT2     LA    R1,4                HAS THIS LINE BEEN PRINTED?
         CR    R1,R9
         BE    ENDLOOP6            YES
         BAL   R8,LINEOUT          GO DUMP LAST LINE
         LA    R9,4                RESET # OF VALUES/LINE REGISTER
ENDLOOP6 BXLE  PTR,INCL,LOOP6      HAVE WE PRINTED LAST ARRAY?
         LA    R1,4
         BAL   R8,LINEOUT1         GO DUMP LAST LINE
         TITLE 'DUMP OF SIMPLE STRING VARS A$ TO Z$'
*
*   THIS CODE DUMPS UNDIMENSIONED STRING VARIABLES A$ TO Z$
*
*   IF DUMPS STRINGS, ONE VALUE PER LINE, IN THE FORM
*       A$=CCCCCCCCCCCCCCC
*
*
         DROP  PTR
         MVI   BUFFER+1,C'$'       PUT $= IN OUTPUT BUFFER
         MVI   BUFFER+2,C'='
*
         LA    PTR,ATOZSTRG        SET UP BXLE THROUGH A$ TO Z$
         USING ASTGDSCT,PTR
         LA    INCL,8              SIZE OF EACH ENTRY IN ATOZSTRG
         LA    MAXL,200(PTR)       DISP TO LAST ENTRY IN ATOZSTRG
         SR    R9,R9               ZERO NEEDED FOR TESTS
         L     NP,M0               MINUS ZERO NEEDED FOR TESTS
         SR    LP,LP               LP PTS TO LETTER
LOOP8    CH    R9,ASTRGDIM         IS IT A DIM. VAR. ?
         BNE   INLOOP8             YES
         LH    R5,ASTRGIDX         NO-COMPUTE THE LOCATION OF THIS
         LA    R5,0(R5,USER)       STRING
         A     R5,ARRAYBEG
         C     NP,0(R5)            HAS THE STRING BEEN INITIALIZED?
         BE    INLOOP8             NO-THEN DON'T PRINT IT
PR26     IC    R0,LETTER(LP)       PICK UP VARIABLE NAME
         STC   R0,BUFFER           PLACE IN BUFFER
         LA    R1,BUFFER           PICK UP LOC OF BUFFER
         OI    PFLAG,X'01'
         STCM  R1,7,LENGTH+1    STORE POINTER FOR PRINT
         LA    R1,LENGTH        NEXT POINTER
         BAL   R8,PR32
INLOOP8  LA    LP,1(LP)            BUMP LETTER PNTR TO NEXT VAR NAME
ENDLOOP8 BXLE  PTR,INCL,LOOP8      ARE WE FINISHED?
*
         TM    PFLAG,X'01'         HAVE WE OUTPUT ANY STRINGS?
         BZ    PR27                NO
         BAL   R8,LINEOUT          YES GO SPACE A LINE
         TITLE 'DUMP OF ARRAY  STRING VARIABLES A$ TO Z$'
*
*   THIS CODE DUMPS DIMENSIONED STRING ARRAYS A$ TO Z$
*
*   IT DUMPS STRING ARRAYS, ONE PER LINE, IN THE FORM:
*       A$(XXXX)=CCCCCCCCCCCCCCC
*
*
PR27     MVI   BUFFER3+1,C'$'      PUT $( IN BUFFER
         MVI   BUFFER3+2,C'('
         MVI   BUFFER3+15,X'09'    SET BUFFER LENGTH
*
         L     R9,M0               MINUS ZERO NEEDED FOR TESTS
         LA    PTR,ATOZSTRG        SET UP BXLE THROUGH A$ TO Z$
         SR    NP,NP               INITIALIZE PNTR TO LETTER
         B     LOOP9X
LOOP9    LA    NP,1(NP)            BUMP LETTER POINTER BY ONE
LOOP9X   LA    LP,1                LP CONTAINS SUBSCRIPT
         SR    R0,R0
         CH    R0,ASTRGDIM         IS THIS VARIABLE DIMENSIONED?
         BZ    ENDLOOP9            NO
         LH    R5,ASTRGIDX         YES-COMPUTE THE LOCATION OF THIS
         LA    R5,0(R5,USER)
         A     R5,ARRAYBEG         ARRAY
         IC    R0,LETTER(NP)       PICK UP VARIABLE NAME
         STC   R0,BUFFER3          STORE IN BUFFER
LOOP10   C     R9,0(R5)            IS THIS ELEMENT -0?
         BNE   PR31                NO
PR36     LA    LP,1(LP)            BUMP SUBSCRIPT BY ONE
         C     LP,ASTRGLFT         ARE WE AT END OF ARRAY?
         BH    ENDLOOP9            YES
         LA    R5,4(R5)            NO-BUMP TO NEXT STRING IN ARRAY
         B     LOOP10
PR31     CVD   LP,FIELD            CONVERT SUBS TO EBCDIC
         UNPK  BUFFER3+3(6),FIELD+5(4)  PLACE IN BUFFER
         MVI   BUFFER3+7,C')'      PUT )= IN BUFFER
         MVI   BUFFER3+8,C'='
         LA    R1,BUFFER3          PICK UP LOC OF BUFFER
         STCM  R1,7,BUFFER3+16  STORE POINTER FOR PRINT
         LA    R1,BUFFER3+15    NEXT  POINTER
         BAL   R8,PR32
         SR    R0,R0               PICK UP CONSTANTS 0 AND -0
         B     PR36
ENDLOOP9 BXLE  PTR,INCL,LOOP9      ARE WE FINISHED?
*
*
*******       FINISHED       *********
*
HOME     L     R13,4(R13)          RESTORE REGISTERS
         LM    R14,R12,12(R13)
         BR    R14                 RETURN TO CALLER
*
LINEOUT  SR    R1,R1               NO-SET FLAG IN ARG LIST TO TELL
         ST    R1,ARGU1            PRINT TO DUMP LINE
         LA    R1,ARGU1            PICK UP LOC OF ARG LIST
LINEOUT2 BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
*
CAT      TM    AFLAGS,ASYNTAX      CHECK FOR ATTENTION POSTED
         BO    HOME                IF IT HAS STOP DUMP AND RETURN
         BR    R8                  IF NOT-CONTINUE DUMP
*
*
LINEOUT1 TM    PFLAG,X'01'
         BO    LINEOUT4
         BR    R8
LINEOUT4 NI    PFLAG,X'00'
         CR    R1,R9               HAS THIS LINE ALREADY BEEN OUTPUT?
         BE    LINEOUT             YES
         SR    R1,R1               NO-SET FLAG IN ARG LIST TO TELL
         ST    R1,ARGU1            PRINT TO DUMP LINE
         LA    R1,ARGU1            PICK UP LOC OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         B     LINEOUT2
*
ERROR    LA    R1,MESGE            PRINT ERROR MESSAGE
         LA    R0,L'MESGE
         L     R15,VPUTIT
         BALR  R14,R15
         B     HOME
*
PR7      LA    R1,ARGU7            PICK UP ADDR OF ARG LIST
         ST    R5,ARGU7            STORE LOC OF # TO BE PRINTED IN
*                                  ARG LIST
         LA    R8,4(R8)            BUMP RETURN REGISTER
         B     PRA
PRB      LA    R1,ARGU1            PICK UP ADDR OF ARG LIST(NO PRINT)
         ST    R5,ARGU1            PUT LOC OF # IN ARG LIST
PRA      BALR  R14,R15             CALL THE PRINT ROUTINE
         B     ERROR
         OI    PFLAG,X'01'
         B     CAT                 GO CHECK FOR ATTENTION POSTED
*
PR32     ST    R1,ARGU4            STORE LOC OF BUFFER IN ARG LIST
         LA    R1,ARGU4            PICK UP LOC OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         ST    R5,ARGU10           STORE LOC OF STRING IN ARG LIST
         LA    R1,ARGU10           PICK UP LOC OF ARG LIST
         BALR  R14,R15             CALL PRINT ROUTINE
         B     ERROR
         B     CAT                 GO CHECK FOR ATTENTION POSTED
         TITLE 'EQUS AND CONSTANTS'
*
*
R0       EQU   0
R1       EQU   1
INCL     EQU   2
MAXL     EQU   3
NP       EQU   4
R5       EQU   5
PTR      EQU   6
LP       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
USER     EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         DS    0F
*
MESGE    DC    C' DUMP OR SHOW DATA ERROR'
LETTER   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DS    0F
M0       DC    X'80000000'
VPRINT   DC    V(PRINT)
VPUTIT   DC    V(PUTIT)
WORK     DSECT
FIELD    DS    1D
TEMP1    DS    F
*   ARG LIST FOR ADDING A # TO PRINT BUFFER IN 15 CHAR FIELD
ARGU1    DS    F                   LOC OF NUMBER
ARGU2    DC    F'0'                O FOR #
ARGU3    DC    F'1'                1 FOR ,
MOPAD3   DS    F
*   ARG LIST FOR ADDING A STRING TO PRINT BUFFER IN A FIELD THE EXACT
*   LENGTH OF THE STRING
ARGU4    DS    F                   LOC OF STRING
ARGU5    DC    F'1'                1 FOR STRING
ARGU6    DC    F'2'                2 FOR ;
MOPAD6   DS    F
*   ARG LIST FOR ADDING A # TO PRINT BUFFER AND THEN PRINTING THE LINE
ARGU7    DS    F                   LOC OF #
ARGU8    DC    F'0'                0 FOR #
ARGU9    DC    F'0'                0 FOR BLANK-SPACE TO NEW LINE
MOPAD9   DS    F
*   ARG LIST FOR ADDING A STRING THE THE PRINT BUFFER AND THEN
*   PRINTING THE LINE.
ARGU10   DS    F
ARGU11   DC    F'1'                1 FOR STRING
ARGU12   DC    F'0'                O - SPACE TO NEW LINE
MOPAD12  DS    F
         DC    X'00'                PAD
BUFFER   DS    15C
LENGTH   DC    X'03000000'
         DC    X'00'                PAD
BUFFER1  DC    C' (XXXX)=       '
         DC    X'08000000'
         DC    X'00'                PAD
BUFFER2  DC    C' (XXXX,XXXX)=  '
         DC    X'0E000000'
         DC    X'00'                PAD
BUFFER3  DC    C' $(XXXX)=      '
         DC    X'0C000000'
PFLAG    DC    X'00'
         TITLE 'USER AREA DSECT'
AREA     DSECT
         COPY  AREA
         END
