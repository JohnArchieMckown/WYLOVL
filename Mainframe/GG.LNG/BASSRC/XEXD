*
*        BASIC DUMP STATEMENT OUTPUT FORMAT
*
*   1)   FOR SIMPLE VARIABLES OF THE TYPE A0 TO Z9
*
*   CC1
*     |
*            0        1        2        3        4        5 ...  9
*      A  XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*      B  XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*   .
*   .
*   .
*      Z  XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*
*
*   2)   FOR SIMPLE VARIABLES OF THE TYPE A TO Z
*
*   CC1
*     |
*
*      A  XXXXXXXX
*      B  XXXXXXXX
*      F  XXXXXXXX
*      Z  XXXXXXXX
*
*
*
*     3) FOR SINGLE DIM. VARIABLES OF THE TYPE A TO Z
*
*     CC1
*       |
*               001      003      004      005      008     ...
*        A   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX    ...
*               011      014      015      016      020     ...
*        A   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX    ...
*               001      002      003      004      006     ...
*        B   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX    ...
*          .
*          .
*          .
*
*
*    4) FOR DOUBLE DIM. VARIABLES OF THE TYPE A TO Z
*
*    CC1
*      |
*            001,001  001,002  001,004  001,008  002,001 ...
*       A   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*            002,002  002,003  002,005  003,001  003,002 ...
*       A   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*            001,001  001,002  001,003  001,004  001,005 ...
*       B   XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX XXXXXXXX  ...
*         .
*         .
*         .
*
*
*   5)   FOR SIMPLE STRING VARIABLES OF THE TYPE A$ TO Z$
*
*   CC1
*     |
*      A$  CCCCCCCCCCCCCCC
*      B$  CCCCCCCCCCCCCCC
*      E$  CCCCCCCCCCCCCCC
*      Z$  CCCCCCCCCCCCCCC
*
*    6)  FOR STRING ARRAYS OF THE TYPE A$ TO Z$
*
*    CC1
*      |
*                 001            002               003            ...
*       A$  CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC
*                 007            009               010            ...
*       A$  CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC
*                 001            002               003            ...
*       B$  CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC CCCCCCCCCCCCCCC
*         .
*         .
*         .
*
*
*        THE XXXXXXXX REPRESENTS THE NUMBER WRITTEN IN HEX.
*        THE CCCCCCCCCCCCCCC REPRESENTS THE STRING
*
XEXD     TITLE 'DUMP ROUTINE'
HEXDUMP  CSECT
         SAVE  (14,12),,*         STANDARD BASIC SAVE
         LA    R2,72(R13)
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
*
         BALR  R12,0              R12 FOR ADDRESSABILITY
         USING *,R12
*
         USING AREA,USER          R11=USER=USER AREA DSECT
         L     R10,AFNC           R10=WORK AREA DSECT
         AR    R10,USER
         LA    R10,4(R10)         PUT R10 ON DBL. WRD BNDRY
         SRL   R10,3
         SLL   R10,3
         USING WORK,R10
*
         TITLE 'DUMP OF SIMPLE VARIABLES A0 TO Z9'
*
         MVI   BUFFER,C' '
         MVC   BUFFER+1(94),BUFFER     BLANK OUT BUFFER
         LA    PTR,A0TOZ9         PTR MOVES THROUGH A0 TO Z9
         L     R15,VOUT
         LA    R0,L'MESG1         LENGTH
         LCR   R0,R0
         LA    R1,MESG1
         BALR  R14,R15            THE OUTPUT ROUTINE IS CALLED WITH R1
         BAL   R14,CAT            GO CHECK FOR INTERRUPT
*                                 POINTING TO THE MESSAGE
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R0,L'TOPLIN
         LA    R1,TOPLIN
         BALR  R14,R15
         BAL   R14,CAT
*
         SR    LP,LP              LP   PTS TO LETTER A-Z
         LA    INCL,1             SET UP LP BXLE
         LA    MAXL,25
         LA    INCV,9             SET UP VP BXLE, VP MOVES ACROSS
         LA    MAXV,81            LINE TO BE PRINTED
         LA    R1,BUFFER          LEAVE R1 POINTING TO BUFFER
         LA    R0,L'BUFFER
*
LOOP1    IC    WK1,LETTER(LP)
         STC   WK1,BUFFER+2       MOVE LETTER TO BUFFER
         SR    VP,VP              START VP
         SR    R0,R0              R0 USED TO TEST BLANK LINE
LOOP2    L     R14,M1
         C     R14,0(PTR)               PUT IN BLANK IF -0.0
         BNE   CTUE
         MVC   FIELD(8),BLANK
         B     PUT
CTUE     UNPK  FIELD(9),0(5,PTR)  COVERT NO. TO EBCDIC
         TR    FIELD(8),TRTB-240
         LR    R0,R1              SET R0 TO NON-ZERO :NON-BLANK LINE
PUT      LA    R14,BUFFER+4(VP)        MOVE VALUE TO BUFFER
         MVC   0(8,R14),FIELD
         LA    PTR,4(PTR)         BUMP TO NEXT VALUE
         BXLE  VP,INCV,LOOP2
         LTR   R0,R0
         BZ    ENDLOOP1           R0=0 : EMPTY LINE
         LA    R0,L'BUFFER
         BALR  R14,R15            PRINT A LINE
         BAL   R14,CAT
ENDLOOP1 BXLE  LP,INCL,LOOP1
*
         TITLE 'DUMP OF SIMPLE VARIABLES  A TO Z'
*
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R0,L'MESG2
         LA    R1,MESG2           SEND SECOND TITLE
         BALR  R14,R15
         BAL   R14,CAT
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER          RESET R1 TO PT. TO BUFFER
         MVI   BUFFER,C' '
         MVC   BUFFER+1(94),BUFFER     BLANK OUT BUFFER
*
         SR    WK1,WK1            SET UP BXLE           THROUGH A - Z
         L     INCV,M1
         SR    LP,LP
         LA    INCL,16
         LA    MAXL,400
         USING ATOZDSCT,PTR
         LA    PTR,ATOZ           PTR LOOPS THROUGH ATOZ DSECT
         AR    MAXL,PTR
         LA    R0,L'BUFFER
*
LOOP3    C     WK1,ARRAYLFT       IF LFT=0 ITS A SIMPLE VARIABLE WITH
         BNZ   INLOOP3            RGT CONTAINING THE VALUE
         C     INCV,ARRAYRGT
         BZ    INLOOP3
         UNPK  FIELD(9),ARRAYRGT(5)    TRANSLATE NO. TO EBCDIC
         TR    FIELD(8),TRTB-240
         IC    VP,LETTER(LP)      PICK UP LETTER
         STC   VP,BUFFER+2        MOVE TO BUFFER
         MVC   BUFFER+4(8),FIELD  MOVE VALUE TO BUFFER
         BALR  R14,R15            PRINT
         BAL   R14,CAT
INLOOP3  LA    LP,1(LP)           BUMP LETTER PTR.
         BXLE  PTR,INCL,LOOP3
*
         TITLE 'DUMP OF SINGLE DIM. VARS. A TO Z'
*
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,MESG3           SEND THIRD MESSAGE
         LA    R0,L'MESG3
         BALR  R14,R15
         BAL   R14,CAT
*
         LA    PTR,ATOZ           SET UP BXLE THROUGH A-Z
         LA    INCV,9             SET UP BXLE THROUGH LINE
         LA    MAXV,81
         SR    R1,R1              SAVE PTR TO LETTER
         ST    R1,TEMP1
         B     LOOP4X
LOOP4    L     R1,TEMP1
         LA    R1,1(R1)
         ST    R1,TEMP1
LOOP4X   LA    LP,1               LP CONTAINS PROPER SUBSCRIPT
         SR    R0,R0
         C     R0,ARRAYLFT        IF LFT~=0 AND RGT=0 THEN
         BZ    ENDLOOP4           ITS A SINGLE DIM. ARRAY
         CH    R0,ARRAYRGT+2
         BNZ   ENDLOOP4
         LH    WK1,ARRAYIDX       WK1 PTS TO VALUE IN ARRAY
         LA    WK1,0(WK1,USER)
         A     WK1,ARRAYBEG
         IC    R0,LETTER(R1)
         STC   R0,BUFFER+1
BACK4    MVI   BUFFER,C' '  ZERO OUT BUFFERS EXCEPT THE ARRAY NAME
         MVI   BUFFER+2,C' '
         MVI   BUFFER2,C' '
         MVC   BUFFER+3(92),BUFFER+2
         MVC   BUFFER2+1(94),BUFFER2
         SR    R0,R0              ZERO NEEDED FOR TESTS
         L     R1,M1              MINUS ZERO NEEDED FOR TESTS
         SR    VP,VP              VP IS INDEX FOR INNER BXLE
LOOP5    C     R1,0(WK1)
         BNE   INLOOP5            IF NOT -0 GO ON
SKIP     LA    LP,1(LP)
         CH    LP,ATOZALOC+2      DONE?
         BH    OUT                GO TO PRINT PARTIAL LINE
         LA    WK1,4(WK1)         NOT DONE- GO TO NEXT ITEM
         B     LOOP5
INLOOP5  C     R0,0(WK1)          IF 0 SKIP
         BE    SKIP
         UNPK  FIELD(9),0(5,WK1)  CONVERT TO EBCDIC
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER+5(VP)   MOVE TO BUFFER
         MVC   0(8,R14),FIELD
         CVD   LP,FIELD2          CONVERT SUBS. TO EBCDIC
         L     R14,FIELD2+4
         SRL   R14,4
         ST    R14,FIELD2
         UNPK  FIELD(9),FIELD2(5)
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER2+8(VP)  MOVE SUBS. TO BUFFER2
         MVC   0(3,R14),FIELD+5
         LA    LP,1(LP)           BUMP SUB
         CH    LP,ATOZALOC+2      DONE?
         BH    OUT                GO TO PRINT PARTIAL LINE
         LA    WK1,4(WK1)
         BXLE  VP,INCV,LOOP5
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R0,L'BUFFER        PRINT BUFFER LINE OF SUBS
         LA    R1,BUFFER2
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER          PRINT VALUES
         BALR  R14,R15
         BAL   R14,CAT
         B     BACK4
OUT      SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER2         FINISHED CURRENT ARRAY,
         LA    R0,L'BUFFER        PRINT PARTIAL LINES
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER
         BALR  R14,R15
         BAL   R14,CAT
ENDLOOP4 BXLE  PTR,INCL,LOOP4          GO ON TO NEXT ARRAY
*
         TITLE 'DUMP OF DOUBLE DIM. VARS. A TO Z'
*
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,MESG4           SEND FOURTH MESSAGE
         LA    R0,L'MESG4
         BALR  R14,R15
         BAL   R14,CAT
*
         LA    PTR,ATOZ           SET UP BXLE THROUGH A-Z
         SR    R1,R1              SAVE PTR TO LETTER
         ST    R1,TEMP1
         B     LOOP6X
LOOP6    L     R1,TEMP1
         LA    R1,1(R1)
         ST    R1,TEMP1
LOOP6X   LA    LP,1               LP=LFT SUBS.
         LR    R15,LP             R15=RGT SUB.
         SR    R0,R0
         C     R0,ARRAYLFT        IF RGT,LFT=0 THEN
         BZ    ENDLOOP6           ITS A DOUBLE DIM. ARRAY
         CH    R0,ARRAYRGT+2
         BZ    ENDLOOP6
         LH    WK1,ARRAYIDX       WK1 PTS TO VALUE IN ARRAY
         LA    WK1,0(WK1,USER)
         A     WK1,ARRAYBEG
         IC    R0,LETTER(R1)
         STC   R0,BUFFER+1
BACK6    MVI   BUFFER,C' '  ZERO OUT BUFFERS EXCEPT THE ARRAY NAME
         MVI   BUFFER+2,C' '
         MVI   BUFFER2,C' '
         MVC   BUFFER+3(92),BUFFER+2
         MVC   BUFFER2+1(94),BUFFER2
         SR    R0,R0              ZERO NEEDED FOR TESTS
         L     R1,M1              MINUS ZERO NEEDED FOR TESTS
         SR    VP,VP              VP IS INDEX FOR INNER BXLE
LOOP7    C     R1,0(WK1)
         BNE   INLOOP7            IF NOT -0 THEN GO ON
SKIP2    LA    R15,1(R15)
         C     R15,ARRAYRGT       DONE RGT ?
         BNH   LOOP7X
         LA    R15,1
         LA    LP,1(LP)
         C     LP,ARRAYLFT        DONE LFT ?
         BH    OUT2
LOOP7X   LA    WK1,4(WK1)
         B     LOOP7
INLOOP7  C     R0,0(WK1)          IF 0 SKIP
         BE    SKIP2
         UNPK  FIELD(9),0(5,WK1)  CONVERT TO EBCDIC
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER+5(VP)   MOVE TO BUFFER
         MVC   0(8,R14),FIELD
         CVD   LP,FIELD2          CONVERT SUBS. TO EBCDIC
         L     R14,FIELD2+4
         SRL   R14,4
         ST    R14,FIELD2
         UNPK  FIELD(9),FIELD2(5)
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER2+6(VP)  MOVE SUBS. TO BUFFER2
         MVC   0(3,R14),FIELD+5
         CVD   R15,FIELD2         CONVERT SUBS. TO EBCDIC
         L     R14,FIELD2+4
         SRL   R14,4
         ST    R14,FIELD2
         UNPK  FIELD(9),FIELD2(5)
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER2+9(VP)      MOVE SUBS. TO BUFFER
         MVI   0(R14),C','        SEPERATE BY ','
         LA    R14,1(R14)
         MVC   0(3,R14),FIELD+5
         LA    R15,1(R15)
         C     R15,ARRAYRGT       DONE RGT ?
         BNH   GOGO
         LA    R15,1
         LA    LP,1(LP)
         C     LP,ARRAYLFT        DONE LFT ?
         BH    OUT2
GOGO     LA    WK1,4(WK1)
         BXLE  VP,INCV,LOOP7
         LR    VP,R15
         L     R15,VOUT
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R0,L'BUFFER
         LA    R1,BUFFER2
         BALR  R14,R15            PRINT
         BAL   R14,CAT
         LA    R1,BUFFER
         BALR  R14,R15            PRINT
         BAL   R14,CAT
         LR    R15,VP
         B     BACK6
OUT2     L     R15,VOUT
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER2
         LA    R0,L'BUFFER
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER
         BALR  R14,R15
         BAL   R14,CAT
ENDLOOP6 BXLE  PTR,INCL,LOOP6
*
         TITLE 'DUMP OF SIMPLE STRING VARS A$ TO Z$'
*
         DROP  PTR
         L     R15,VOUT
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,MESG5           SEND FIFTH MESSAGE
         LA    R0,L'MESG5
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER
         LA    R0,L'BUFFER
*
         LA    PTR,ATOZSTRG       SET UP BXLE THROUGH A$ TO Z$
         USING ASTGDSCT,PTR
         LA    INCL,8
         LA    MAXL,200(PTR)
         SR    WK1,WK1            ZERO NEEDED FOR TESTS
         L     INCV,M1            MINUS ZERO NEEDED FOR TESTS
         SR    LP,LP              LP PTS TO LETTER
LOOP8    CH    WK1,ASTRGDIM       IS IT A DIM. VAR. ?
         BNE   INLOOP8
         MVI   BUFFER,C' '
         MVC   BUFFER+1(94),BUFFER     BLANK OUT BUFFER
         LH    R14,ASTRGIDX       PT. R14 TO STRING
         LA    R14,0(R14,USER)
         A     R14,ARRAYBEG
         C     INCV,0(R14)
         BE    INLOOP8            SKIP UN-INITIALIZED
         IC    VP,LETTER(LP)      MOVE LETTER TO BUFFER
         STC   VP,BUFFER+1
         MVI   BUFFER+2,C'$'
         MVC   BUFFER+4(15),0(R14)     MOVE VALUE TO BUFFER
         BALR  R14,R15
         BAL   R14,CAT
INLOOP8  LA    LP,1(LP)
ENDLOOP8 BXLE  PTR,INCL,LOOP8
*
         TITLE 'DUMP OF ARRAY  STRING VARIABLES A$ TO Z$'
*
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,MESG6           SEND SIXTH MESSAGE
         LA    R0,L'MESG6
         BALR  R14,R15
         BAL   R14,CAT
*
         LA    PTR,ATOZSTRG       SET UP BXLE THROUGH A$ TO Z$
         LA    INCV,16            SET UP BXLE ACROSS LINE
         LA    MAXV,70
         SR    R1,R1
         ST    R1,TEMP1           SAVE PTR TO LETTER
         B     LOOP9X
LOOP9    L     R1,TEMP1
         LA    R1,1(R1)
         ST    R1,TEMP1
LOOP9X   LA    LP,1               LP CONTAINS PROPER SUBSCRIPT
         SR    R0,R0
         CH    R0,ASTRGDIM        IF DIM~=0 ITS AN ARRAY
         BZ    ENDLOOP9
         LH    WK1,ASTRGIDX       WK1 PTS TO VALUE IN ARRAY
         LA    WK1,0(WK1,USER)
         A     WK1,ARRAYBEG
         IC    R0,LETTER(R1)
         STC   R0,BUFFER+1
         MVI   BUFFER+2,C'$'
BACK9    MVI   BUFFER,C' '        ZERO BUFFERS EXCEPT ARRAY NAME
         MVI   BUFFER+3,C' '
         MVI   BUFFER2,C' '
         MVC   BUFFER+4(91),BUFFER+3
         MVC   BUFFER2+1(94),BUFFER2
         SR    R0,R0              ZERO NEEDED FOR TESTS
         L     R1,M1              MINUS ZERO NEEDED FOR TESTS
         SR    VP,VP              VP IS INDEX OF INNER BXLE
LOOP10   C     R1,0(WK1)
         BNE   INLOOP10           IF NOT -0 GO ON
         LA    LP,1(LP)
         C     LP,ASTRGLFT        DONE ?
         BH    OUT3
         LA    WK1,16(WK1)
         B     LOOP10
INLOOP10 LA    R14,BUFFER+5(VP)
         MVC   0(15,R14),0(WK1)
         CVD   LP,FIELD2          CONVERT SUBS TO EBCDIC
         L     R14,FIELD2+4
         SRL   R14,4
         ST    R14,FIELD2
         UNPK  FIELD(9),FIELD2(5)
         TR    FIELD(8),TRTB-240
         LA    R14,BUFFER2+11(VP)  MOVE SUBS TO BUFFER
         MVC   0(3,R14),FIELD+5
         LA    LP,1(LP)
         C     LP,ASTRGLFT        DONE ?
         BH    OUT3               GO PRINT PARTIAL LINE
         LA    WK1,16(WK1)
         BXLE  VP,INCV,LOOP10
         SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R0,L'BUFFER
         LA    R1,BUFFER2
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER
         BALR  R14,R15
         BAL   R14,CAT
         B     BACK9
OUT3     SR    R0,R0
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER2
         LA    R0,L'BUFFER
         BALR  R14,R15
         BAL   R14,CAT
         LA    R1,BUFFER
         BALR  R14,R15
         BAL   R14,CAT
ENDLOOP9 BXLE  PTR,INCL,LOOP9
*
*
*******       FINISHED       *********
*
HOME     L     R13,4(R13)
         LM    R14,R12,12(R13)
         BR    R14
CAT      TM    AFLAGS,ASYNTAX     CHECK FOR ATTENTION POSTED
         BO    HOME               IF IT HAS STOP THIS AND GO HOME
         BR    R14                IF NOT GO BACK WHENCE WE CAME
*
         TITLE 'EQUS AND CONSTANTS'
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
INCL     EQU   2
MAXL     EQU   3
INCV     EQU   4
MAXV     EQU   5
PTR      EQU   6
LP       EQU   7
VP       EQU   8
WK1      EQU   9
R10      EQU   10
USER     EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
TOPLIN   DC    C'       0        1        2        3        4        5 C
                      6        7        8        9    '
MESG1    DC    C' SIMPLE VARIABLES A0 TO Z9 '
MESG2    DC    C' SIMPLE VARIABLES A TO Z '
MESG3    DC    C' SINGLE DIM. VARIABLES A TO Z '
MESG4    DC    C' DOUBLE DIM. VARIABLES A TO Z '
MESG5    DC    C' SIMPLE STRING VARIABLES A$ TO Z$ '
MESG6    DC    C' STRING ARRAY VARIABLES A$ TO Z$ '
TRTB     DC    C'0123456789'      'LETTER' MUST FOLLOW TRTB
LETTER   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DS    0F
M1       DC    X'80000000'
BLANK    DC    C'        '
VOUT     DC    V(PUTIT)
WORK     DSECT
FIELD    DS    9X'00'
         DS    0D
FIELD2   DS    9X'00'
TEMP1    DS    F
BUFFER   DS    CL95
BUFFER2  DS    CL95
         TITLE 'USER AREA DSECT'
AREA     DSECT
         COPY  AREA
         END
