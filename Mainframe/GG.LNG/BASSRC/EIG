EIG      TITLE 'EIGEN---EIGENVALUE EVALUATION'
EIG      CSECT
*
*       BASIC STATEMENT:
*
*             MAT R=EIG(A)
*
*
*       ARGUMENTS:
*             0(R1)  -  ADDRESS OF INPUT ARRAY A
*             4(R1)  -  ADDRESS OF OUTPUT ARRAY R
*             8(R1)  -  SIZE OF MATRIX
*
*       RETURNS:   4(R1) - NORMAL RETURN
*                   0(R1) - ERROR RETURN FROM SQR (R1=MESG. NO.)
*
*
*       COPY OF SSP SOURCE ROUTINE USED FOR TRANSLITERATION
*
*C
*C   ..................................................................
*C
*C      SUBROUTINE EIGEN
*C
*C      PURPOSE
*C         COMPUTE EIGENVALUES AND EIGENVECTORS OF A REAL SYMMETRIC
*C         MATRIX
*C
*C      USAGE
*C         CALL EIGEN(A,R,N,MV)
*C
*C      DESCRIPTION OF PARAMETERS
*C         A - ORIGINAL MATRIX (SYMMETRIC), DESTROYED IN COMPUTATION.
*C             RESULTANT EIGENVALUES ARE DEVELOPED IN DIAGONAL OF
*C             MATRIX A IN DESCENDING ORDER.
*C         R - RESULTANT MATRIX OF EIGENVECTORS (STORED COLUMNWISE,
*C             IN SAME SEQUENCE AS EIGENVALUES)
*C         N - ORDER OF MATRICES A AND R
*C         MV- INPUT CODE
*C                 0   COMPUTE EIGENVALUES AND EIGENVECTORS
*C                 1   COMPUTE EIGENVALUES ONLY (R NEED NOT BE
*C                     DIMENSIONED BUT MUST STILL APPEAR IN CALLING
*C                     SEQUENCE)
*C
*C      REMARKS
*C         ORIGINAL MATRIX A MUST BE REAL SYMMETRIC (STORAGE MODE=1)
*C         MATRIX A CANNOT BE IN THE SAME LOCATION AS MATRIX R
*C
*C      SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
*C         NONE
*C
*C      METHOD
*C         DIAGONALIZATION METHOD ORIGINATED BY JACOBI AND ADAPTED
*C         BY VON NEUMANN FOR LARGE COMPUTERS AS FOUND IN 'MATHEMATICAL
*C         METHODS FOR DIGITAL COMPUTERS', EDITED BY A. RALSTON AND
*C         H.S. WILF, JOHN WILEY AND SONS, NEW YORK, 1962, CHAPTER 7
*C
*C   ..................................................................
*C
*     SUBROUTINE EIGEN(A,R,N,MV)
*     DIMENSION A(1),R(1)
*C
*C      ...............................................................
*C
*C      IF A DOUBLE PRECISION VERSION OF THIS ROUTINE IS DESIRED, THE
*C      C IN COLUMN 1 SHOULD BE REMOVED FROM THE DOUBLE PRECISION
*C      STATEMENT WHICH FOLLOWS.
*C
*C   DOUBLE PRECISION A,R,ANORM,ANRMX,THR,X,Y,SINX,SINX2,COSX,
*C  1                 COSX2,SINCS,RANGE
*C
*C      THE C MUST ALSO BE REMOVED FROM DOUBLE PRECISION STATEMENTS
*C      APPEARING IN OTHER ROUTINES USED IN CONJUNCTION WITH THIS
*C      ROUTINE.
*C
*C      THE DOUBLE PRECISION VERSION OF THIS SUBROUTINE MUST ALSO
*C      CONTAIN DOUBLE PRECISION FORTRAN FUNCTIONS.  SQRT IN STATEMENTS
*C      40, 68, 75, AND 78 MUST BE CHANGED TO DSQRT.  ABS IN STATEMENT
*C      62 MUST BE CHANGED TO DABS. THE CONSTANT IN STATEMENT 5 SHOULD
*C      BE CHANGED TO 1.0D-12.
*C
*C      ...............................................................
*C
*C      GENERATE IDENTITY MATRIX
*C
*   5 RANGE=1.0E-6
*     IF(MV-1) 10,25,10
*  10 IQ=-N
*     DO 20 J=1,N
*     IQ=IQ+N
*     DO 20 I=1,N
*     IJ=IQ+I
*     R(IJ)=0.0
*     IF(I-J) 20,15,20
*  15 R(IJ)=1.0
*  20 CONTINUE
*C
*C      COMPUTE INITIAL AND FINAL NORMS (ANORM AND ANRMX)
*C
*  25 ANORM=0.0
*     DO 35 I=1,N
*     DO 35 J=I,N
*     IF(I-J) 30,35,30
*  30 IA=I+(J*J-J)/2
*     ANORM=ANORM+A(IA)*A(IA)
*  35 CONTINUE
*     IF(ANORM) 165,165,40
*  40 ANORM=1.414*SQRT(ANORM)
*     ANRMX=ANORM*RANGE/FLOAT(N)
*C
*C      INITIALIZE INDICATORS AND COMPUTE THRESHOLD, THR
*C
*     IND=0
*     THR=ANORM
*  45 THR=THR/FLOAT(N)
*  50 L=1
*  55 M=L+1
*C
*C      COMPUTE SIN AND COS
*C
*  60 MQ=(M*M-M)/2
*     LQ=(L*L-L)/2
*     LM=L+MQ
*  62 IF( ABS(A(LM))-THR) 130,65,65
*  65 IND=1
*     LL=L+LQ
*     MM=M+MQ
*     X=0.5*(A(LL)-A(MM))
*  68 Y=-A(LM)/ SQRT(A(LM)*A(LM)+X*X)
*     IF(X) 70,75,75
*  70 Y=-Y
*  75 SINX=Y/ SQRT(2.0*(1.0+( SQRT(1.0-Y*Y))))
*     SINX2=SINX*SINX
*  78 COSX= SQRT(1.0-SINX2)
*     COSX2=COSX*COSX
*     SINCS =SINX*COSX
*C
*C      ROTATE L AND M COLUMNS
*C
*     ILQ=N*(L-1)
*     IMQ=N*(M-1)
*     DO 125 I=1,N
*     IQ=(I*I-I)/2
*     IF(I-L) 80,115,80
*  80 IF(I-M) 85,115,90
*  85 IM=I+MQ
*     GO TO 95
*  90 IM=M+IQ
*  95 IF(I-L) 100,105,105
* 100 IL=I+LQ
*     GO TO 110
* 105 IL=L+IQ
* 110 X=A(IL)*COSX-A(IM)*SINX
*     A(IM)=A(IL)*SINX+A(IM)*COSX
*     A(IL)=X
* 115 IF(MV-1) 120,125,120
* 120 ILR=ILQ+I
*     IMR=IMQ+I
*     X=R(ILR)*COSX-R(IMR)*SINX
*     R(IMR)=R(ILR)*SINX+R(IMR)*COSX
*     R(ILR)=X
* 125 CONTINUE
*     X=2.0*A(LM)*SINCS
*     Y=A(LL)*COSX2+A(MM)*SINX2-X
*     X=A(LL)*SINX2+A(MM)*COSX2+X
*     A(LM)=(A(LL)-A(MM))*SINCS+A(LM)*(COSX2-SINX2)
*     A(LL)=Y
*     A(MM)=X
*C
*C      TESTS FOR COMPLETION
*C
*C      TEST FOR M = LAST COLUMN
*C
* 130 IF(M-N) 135,140,135
* 135 M=M+1
*     GO TO 60
*C
*C      TEST FOR L = SECOND FROM LAST COLUMN
*C
* 140 IF(L-(N-1)) 145,150,145
* 145 L=L+1
*     GO TO 55
* 150 IF(IND-1) 160,155,160
* 155 IND=0
*     GO TO 50
*C
*C      COMPARE THRESHOLD WITH FINAL NORM
*C
* 160 IF(THR-ANRMX) 165,165,45
*C
*C      SORT EIGENVALUES AND EIGENVECTORS
*C
* 165 IQ=-N
*     DO 185 I=1,N
*     IQ=IQ+N
*     LL=I+(I*I-I)/2
*     JQ=N*(I-2)
*     DO 185 J=I,N
*     JQ=JQ+N
*     MM=J+(J*J-J)/2
*     IF(A(LL)-A(MM)) 170,185,185
* 170 X=A(LL)
*     A(LL)=A(MM)
*     A(MM)=X
*     IF(MV-1) 175,185,175
* 175 DO 180 K=1,N
*     ILR=IQ+K
*     IMR=JQ+K
*     X=R(ILR)
*     R(ILR)=R(IMR)
* 180 R(IMR)=X
* 185 CONTINUE
*     RETURN
*     END
*
*
*       SAVE AREA SET-UP
*
         USING *,15
START    B     8(0,15) BRANCH AROUND ID
         DC    AL1(3)
         DC    CL3'EIG' IDENTIFIER
         STM   14,12,12(13) SAVE REGISTERS
         LA    2,72(13)
         ST    13,4(2)
         ST    2,8(13)
         LR    13,2
         LA    12,START
         DROP  15
         USING START,12
         USING AREA,11
         LR    R4,11
         A     R4,AFNC
         USING AEIGL,R4
         LA    R4,4(R4)           GUARENTEE DBL. WD. BDRY.
         SRL   R4,3
         SLL   R4,3
         LM    R2,R3,0(R1)        PICK UP ARGS.
         L     R1,8(R1)
         ST    R1,N
*                                 DOWN COLUMNS
         LD    F6,IZEROS          F6=FLOAT(N)
         STD   F6,TEMP
         MVI   TEMP,X'4E'
         ST    R1,TEMP+4
         LD    F6,TEMP
         AD    F6,IZEROS
         STE   F6,FLTN
*
**********************************************************************
*
*      CONVERT FROM GENERAL MATRIX FORM TO SSP SYMETRIC FORM
*
         LA    I,1                DO 5 I=1,N
X1       LA    J,1                DO 5 J=1,I
X2       LR    R15,I              IJ=(I*I-I)/2+J
         SR    R14,R14
         MR    R14,I
         SR    R15,I
         SRL   R15,1
         AR    R15,J
         LR    IJ,R15
         LR    R15,I              R15=(I-1)*N+J
         BCTR  R15,0
         SR    R14,R14
         M     R14,N
         AR    R15,J
         BCTR  IJ,0           C   CONVERT TO MACHINE ADDRESSES
         SLL   IJ,2
         BCTR  R15,0
         SLL   R15,2
L5       LE    F0,0(R15,R2)     5 A(IJ)=A(R15)
         STE   F0,0(IJ,R2)
L5A      LA    J,1(J)
         CR    J,I
         BNH   X2
L5B      LA    I,1(I)
         C     I,N
         BNH   X1
*
*
*      GENERATE IDENTITY MATRIX
*
*
L10      L     IQ,N               10 IQ=-N
         LCR   IQ,IQ
         LE    F0,ZERO
         LE    F2,ONE
         LA    J,1                   DO 20 J=1 TO N
B1       A     IQ,N                  IQ=IQ+N
         LA    I,1                   DO 20 I=1 TO N
B2       LR    IJ,IQ                 IJ=IQ+I
         AR    IJ,I
         BCTR  IJ,0            C  CONVERT TO MACHINE ADDR.
         SLL   IJ,2
         STE   F0,0(IJ,R3)           R(IJ)=0.0
         CR    I,J                   IF(I-J) 20,15,20
         BNE   L20A
L15      STE   F2,0(IJ,R3)           R(IJ)=1.0
L20A     LA    I,1(I)             20 CONTINUE
         C     I,N
         BNH   B2
L20B     LA    J,1(J)
         C     J,N
         BNH   B1
*
*  COMPUTE INITIAL AND FINAL FORMS (ANORM AND ANRMX)
*
L23      LE    F0,ZERO               ANORM=0.0
         LA    I,1                   DO 35 I=1,N
B3       LR    J,I                   DO 35 J=I,N
B4       CR    I,J                   IF(I-J) 30,35,30
         BE    L35A
L30      LR    IA,I                  IA=I+(J*J-J)/2
         LR    R8,J
         SRDA  R8,32
         MR    R8,J
         SR    R9,J
         SRL   R9,1
         AR    IA,R9
         BCTR  IA,0            C CONVERT TO MACHINE ADDR.
         SLL   IA,2
         LE    F2,0(IA,R2)           ANORM=ANORM+A(IA)*A(IA)
         MER   F2,F2
         AER   F0,F2
L35A     LA    J,1(J)             35 CONTINUE
         C     J,N
         BNH   B4
L35B     LA    I,1(I)
         C     I,N
         BNH   B3
         LTER  F0,F0                 IF(ANORM) 165,165,40
         BNH   L165
L40      L     R15,VSQR           40 ANORM=1.414*SQRT(ANORM)
         BALR  R14,R15
         B     ERROR
         ME    F0,CON2
         LE    F4,RANGE
         DE    F4,FLTN               ANRMX=ANORM*RANGE/FLOAT(N)
         LER   F2,F0           C  SAVE ANORM TEMPORARILY
         MER   F2,F4
         STE   F2,ANRMX
         LA    R8,0                  IND=0
         ST    R8,IND
         STE   F0,THR                THR=ANORM
L45      LE    F0,THR             45 THR=THR/FLOAT(N)
         DE    F0,FLTN
         STE   F0,THR
L50      LA    L,1                50 L=1
L55      LA    M,1(L)             55 M=L+1
*
*    COMPUTE SIN AND COS
*
L60      LR    MQ,M               60 MQ=(M*M-M)/2
         STH   MQ,CMQ
         MH    MQ,CMQ
         SR    MQ,M
         SRL   MQ,1
         STH   MQ,CMQ
         LR    LQ,L                  LQ=(L*L-L)/2
         STH   LQ,CLQ
         MH    LQ,CLQ
         SR    LQ,L
         SRL   LQ,1
         STH   LQ,CLQ
         LR    LM,L                  LM=L+MQ
         AR    LM,MQ
         BCTR  LM,0            C   CONVERT TO MACHINE ADDR.
         SLL   LM,2
L62      LE    F4,0(LM,R2)           IF( ABS(A(LM))-THR) 130,65,65
         LPER  F0,F4
         CE    F0,THR
         BL    L130
L65      LA    R1,1                  IND=1
         ST    R1,IND
         AR    LL,L                  LL=L+LQ
         AR    MM,M                  MM=M+MQ
         BCTR  LL,0            C  CONVERT TO MACHINE ADDRS.
         SLL   LL,2
         BCTR  MM,0
         SLL   MM,2
         LE    F0,0(LL,R2)           X=0.5*(A(LL)-A(MM))
         SE    F0,0(MM,R2)
         ME    F0,CON3
         STE   F0,X
L68      MER   F0,F0              68 Y=-A(LM)/SQRT(A(LM)*A(LM)+X*X)
         MER   F4,F4
         AER   F0,F4
         L     R15,VSQR
         BALR  R14,R15
         B     ERROR
         LE    F2,0(LM,R2)
         LCER  F2,F2
         DER   F2,F0
         STE   F2,Y
         LE    F0,X                  IF(X) 70,75,75
         LTER  F0,F0
         BNL   L75
L70      LCER  F2,F2              70 Y=-Y
         STE   F2,Y
L75      MER   F2,F2          75 SINX=Y/SQRT(2.0*(1.0+(SQRT(1.0-Y*Y)))
         LE    F0,ONE
         SER   F0,F2
         L     R15,VSQR
         BALR  R14,R15
         B     ERROR
         AE    F0,ONE
         ME    F0,TWO
         L     R15,VSQR
         BALR  R14,R15
         B     ERROR
         LE    F2,Y
         DER   F2,F0
         STE   F2,SINX
         MER   F2,F2                 SINX2=SINX*SINX
         STE   F2,SINX2
L78      LE    F0,ONE             78 COSX=SQRT(1.0-SINX2)
         SER   F0,F2
         L     R15,VSQR
         BALR  R14,R15
         B     ERROR
         STE   F0,COSX
         MER   F0,F0                 COSX2=COSX*COSX
         STE   F0,COSX2
         LE    F0,SINX               SINCS=SINX*COSX
         ME    F0,COSX
         STE   F0,SINCS
         STM   LL,LM,CLL       C  SAVE LL,MM,LM FOR LATER USE
*
*   ROTATE L AND M COLUMNS
*
         LR    R8,L                  ILQ=N*(L-1)
         BCTR  R8,0
         SRDA  R8,32
         M     R8,N
         STH   R9,CILQ
         LR    R8,M                  IMQ=N*(M-1)
         BCTR  R8,0
         SRDA  R8,32
         M     R8,N
         STH   R9,CIMQ
         LA    I,1                   DO 125 I=1,N
B5       LR    R8,I                  IQ=(I*I-I)/2
         SRDA  R8,32
         MR    R8,I
         LR    IQ,R9
         SR    IQ,I
         SRL   IQ,1
         CR    I,L                   IF(I-L) 80,120,80
         BE    L120
L80      CR    I,M                80 IF(I-M) 85,120,90
         BE    L120
         BH    L90
L85      LH    IM,CMQ             85 IM=I+MQ
         AR    IM,I
         B     L95                   GO TO 95
L90      LR    IM,IQ              90 IM=M+IQ
         AR    IM,M
L95      CR    I,L                95 IF(I-L) 100,105,105
         BNL   L105
L100     LH    IL,CLQ            100 IL=I+LQ
         AR    IL,I
         B     L110                  GO TO 110
L105     LR    IL,IQ             105 IL=L+IQ
         AR    IL,L
L110     BCTR  IL,0           C   CONVERT TO MACHINE ADDRS.
         SLL   IL,2
         BCTR  IM,0
         SLL   IM,2
         LE    F0,0(IL,R2)           X=A(IL)*COSX-A(IM)*SINX
         LER   F4,F0
         ME    F0,COSX
         LE    F2,0(IM,R2)
         LER   F6,F2
         ME    F2,SINX
         SER   F0,F2
         STE   F0,X
         ME    F4,SINX               A(IM)=A(IL)*SINX+A(IM)*COSX
         ME    F6,COSX
         AER   F4,F6
         STE   F4,0(IM,R2)
         STE   F0,0(IL,R2)           A(IL)=X
L120     LH    ILR,CILQ          120 ILR=ILQ+I
         AR    ILR,I
         LH    IMR,CIMQ              IMQ=IMQ+I
         AR    IMR,I
         BCTR  ILR,0          C  CONVERT TO MACHINE ADDRS.
         SLL   ILR,2
         BCTR  IMR,0
         SLL   IMR,2
         LE    F0,0(ILR,R3)          X=R(ILR)*COSX-R(IMR)*SINX
         LER   F4,F0
         ME    F0,COSX
         LE    F2,0(IMR,R3)
         LER   F6,F2
         ME    F2,SINX
         SER   F0,F2
         STE   F0,X
         ME    F4,SINX               R(IMR)=R(ILR)*SINX+R(IMR)*COSX
         ME    F6,COSX
         AER   F4,F6
         STE   F4,0(IMR,R3)
         STE   F0,0(ILR,R3)          R(ILR)=X
L125     LA    I,1(I)            125 CONTINUE
         C     I,N
         BNH   B5
         LM    LL,LM,CLL      C  PICK LL,MM,LM BACK UP
         LE    F0,0(LM,R2)           X=2.0*A(LM)*SINCS
         ME    F0,SINCS
         ME    F0,TWO
         STE   F0,X
         LE    F0,0(LL,R2)           Y=A(LL)*COSX2+A(MM)*SINX2-X
         LER   F4,F0
         ME    F0,COSX2
         LE    F2,0(MM,R2)
         LER   F6,F2
         ME    F2,SINX2
         AER   F0,F2
         SE    F0,X
         STE   F0,Y
         ME    F4,SINX2              X=A(LL)*SINX2+A(MM)*COSX2+X
         ME    F6,COSX2
         AER   F4,F6
         AE    F4,X
         STE   F4,X
         LE    F0,0(LL,R2)           A(LM)=(A(LL)-A(MM))*SINCS+
         SE    F0,0(MM,R2)          1A(LM)*(COSX2-SINX2)
         ME    F0,SINCS
         LE    F2,COSX2
         SE    F2,SINX2
         ME    F2,0(LM,R2)
         AER   F0,F2
         STE   F0,0(LM,R2)
         LE    F0,Y                  A(LL)=Y
         STE   F0,0(LL,R2)
         LE    F0,X                  A(MM)=X
         STE   F0,0(MM,R2)
*
*   TESTS FOR COMPLETION
*
*       TEST FOR M = LAST COLUMN
*
L130     C     M,N               130 IF(M-N) 135,140,135
         BE    L140
L135     AH    M,=H'1'           135 M=M+1
         B     L60                   GO TO 60
*
*       TEST FOR L = SECOND FROM LAST COLUMN
*
L140     L     R8,N              140 IF(L-(N-1)) 145,150,145
         BCTR  R8,0
         CR    L,R8
         BE    L150
L145     LA    L,1(L)            145 L=L+1
         B     L55                   GO TO 55
L150     L     R8,IND            150 IF(IND-1) 160,155,160
         BCTR  R8,0
         LTR   R8,R8
         BNE   L160
L155     LA    R8,0              155 IND=0
         ST    R8,IND
         B     L50                   GO TO 50
*
*    COMPARE THRESHOLD WITH FINAL FORM
*
L160     L     R8,THR            160 IF(THR-ANRMX) 165,165,45
         C     R8,ANRMX
         BH    L45
*
*    SORT EIGENVALUES AND EIGENVECTORS
*
L165     L     IQ,N                  IQ=-N
         LCR   IQ,IQ
         LA    I,1                   DO 185 I=1,N
B6       A     IQ,N                  IQ=IQ+N
         LR    R0,I                  L=I+(I*I-I)/2
         SRDA  R0,32
         MR    R0,I
         SR    R1,I
         SRL   R1,1
         AR    R1,I
         LR    L,R1
         SR    R14,R14            JQ=N*(I-2)
         LR    JQ,I
         BCTR  JQ,0
         BCTR  JQ,0
         M     R14,N
         BCTR  L,0            C CONVERT TO MACHINE ADDRS.
         SLL   L,2
         LR    J,I                   DO 185 J=I,N
B7       A     JQ,N                  JQ=JQ+N
         LR    R14,R1         C  MUST SAVE J=R1 TEMPORARILY
         SR    R0,R0                 MM=J+(J*J-J)/2
         MR    R0,R14
         SR    R1,R14
         SRL   R1,1
         AR    R1,R14
         LR    MM,R1
         LR    J,R14
         BCTR  MM,0
         SLL   MM,2
         LE    F0,0(L,R2)            IF(A(L)-A(MM)) 170,185,185
         LE    F2,0(MM,R2)
         CER   F0,F2
         BNL   L185
L170     STE   F2,0(L,R2)        170 X=A(L) ;    A(L)=A(MM)
         STE   F0,0(MM,R2)           A(MM)=X
L175     LA    K,1                   DO 180 K=1,N
B8       LR    R14,IQ                R14=IQ+K
         AR    R14,K
         LR    IMR,JQ                IMR=JQ+K
         AR    IMR,K
         BCTR  R14,0          C   CONVERT TO MACHINE ADDRS.
         SLL   R14,2
         BCTR  IMR,0
         SLL   IMR,2
         LE    F0,0(R14,R3)          X=R(R14)
         LE    F2,0(IMR,R3)          R(R14)=R(IMR)
         STE   F0,0(IMR,R3)          R(IMR)=X
         STE   F2,0(R14,R3)
L180     AH    K,=H'1'           180 CONTINUE
         C     K,N
         BNH   B8
L185     EQU   *
L185A    LA    J,1(J)            185 CONTINUE
         C     J,N
         BNH   B7
L185B    LA    I,1(I)
         C     I,N
         BNH   B6
*
*        CONVERT BACK TO DIAG. MATRIX
*
         L     I,N               DO 200 I=N,1,-1 (CHEATING FORTRAN)
X3       LR    R15,I                 IJ=I*(I+1)/2
         LA    R15,1(R15)
         SR    R14,R14
         MR    R14,I
         SRL   R15,1
         LR    IJ,R15
         BCTR  IJ,0          C   CONVERT TO MACHINE ADDR.
         SLL   IJ,2
         LR    R15,I           R15=(I-1)*N+I
         BCTR  R15,0
         SR    R14,R14
         M     R14,N
         AR    R15,I
         BCTR  R15,0         C   CONVERT TO MACHINE ADDR.
         SLL   R15,2
L200     LE    F0,0(IJ,R2)       200 A(R15)=A(IJ)
         STE   F0,0(R15,R2)
L200A    BCT   I,X3
*
*        ZERO NON-DIAG. ELEMENTS
*
         LE    F0,ZERO            F0=0
         L     IJ,N                  IJ=N-1
         BCTR  IJ,0
         LA    I,1                   DO 300 I=1,IJ
X4       LA    J,1                   DO 300 J=1,N
X5       LR    R15,I                 R15=N*(I-1)+I+J
         BCTR  R15,0
         SR    R14,R14
         M     R14,N
         AR    R15,I
         AR    R15,J
         BCTR  R15,0          C   CONVERT TO MACHINE ADDR.
         SLL   R15,2
L300     STE   F0,0(R15,R2)      300 A(R15)=F0
L300A    LA    J,1(J)
         C     J,N
         BNH   X5
L300B    LA    I,1(I)
         CR    I,IJ
         BNH   X4
*
*                                    RETURN,END
*
**********************************************************************
*
         L     R13,4(R13)         -RESET CHAIN AND RETURN
         LM    R14,R12,12(R13)
         B     4(R14)
*
ERROR    L     R13,4(R13)         -FOR ERROR RETURN
         LM    R14,R15,12(R13)
         LM    R2,R12,28(R13)
         BR    R14
*
VSQR     DC    V(SQR)             LINK FOR SQR ROUTINE
*
*       REGISTER USAGE:
*
R0       EQU   0
M        EQU   0
K        EQU   0
R1       EQU   1
IL       EQU   1
ILR      EQU   1
J        EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
L        EQU   5
IA       EQU   5
R6       EQU   6
I        EQU   6
R7       EQU   7
IM       EQU   7
IMR      EQU   7
R8       EQU   8
IQ       EQU   8
LL       EQU   8
LQ       EQU   8
R9       EQU   9
MM       EQU   9
MQ       EQU   9
R10      EQU   10
LM       EQU   10
IJ       EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
JQ       EQU   15
*
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
*
IZEROS   DC    D'0'
ZERO     DC    E'0.0'
ONE      DC    E'1.0'
TWO      DC    E'2.0'
RANGE    DC    E'1.0E-6'
CON2     DC    E'1.414'
CON3     DC    E'0.5'
*
AEIGL    DSECT
TEMP     DS    D
N        DS    F
FLTN     DS    F
IND      DS    F
THR      DS    F
SINX     DS    F
COSX     DS    F
SINX2    DS    F
COSX2    DS    F
SINCS    DS    F
ANRMX    DS    F
X        DS    F
Y        DS    F
CLL      DS    F                  THIS AND NEXT TWO WORDS MUST BE
CMM      DS    F                   CONSECUTICE
CLM      DS    F
CMQ      DS    H
CLQ      DS    H
CILQ     DS    H
CIMQ     DS    H
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
