XUTI   TITLE  'PUTIT ROUTINE'
*DATE OF LAST UPDATE 06/01/69  22:47:49
PUTIT  CSECT
***********************************************************************
*                                                                     *
*                   WYLBUR/BASIC PUTIT PROGRAM                        *
*                      TIME-SHARED VERSION                            *
*                                                                     *
***********************************************************************
*
*REGISTER USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                 BASE FOR 'CONTROL' WORK AREA
R11      EQU   11                 BASE OF USER AREA
R12      EQU   12                 BASE FOR PUTIT
R13      EQU   13                 POINTER TO SAVE AREA
R14      EQU   14
R15      EQU   15
*
BASER    EQU   12                 BASE FOR PUTIT
*
*
         XAVE  PUTIT
*
*SETUP FOR CONTROL PROGRAMS WORK AREA
*
         LR    R3,R0
         LR    R4,R1
         SENSE 2
         L     1,0(1)             DISPL. FOR LOAD TEXT
         LR    R10,R1
         USING TWORK,10           TELL ASSEMBLER ABOUT TWORK
*
*
*THERE IS A WINDOW HERE THE TIMER MAY HAVE ALREADY FIRED
*AND THOUGH WE ARE GOING TO DO I/O THE USER WILL STILL GET THE
*TIMER INTERRUPT.  A LITTLE SLOPPY BUT I'M IN A HURRY.
*
*TEST THE TIMER FOR RESETING SINCE WE ARE ABOUT TO DO I/O
*
         L     R1,BREKTIME        GET USERS INTERVAL TIME
         LTR   R1,R1              TEST FOR ANTHING TO SET
         BZ    GOON               SKIP SETTING IF ZERO
         SET   4,(1)              RESTART THE TIMER
*
*RESTORE THE REGISTERS
*
GOON     LR    R0,R3
         LR    R1,R4
*
*BRANCH ON TYPE OF ACTION REQUESTED
*
         LTR   R0,R0              WHAT IS THE ACTION REQUESTED
         BL    PAGE               WANTS A SKIP TO TOP OF PAGE
         BE    BLANK              WANTS A BLANK LINE NO MSG
*
***************PROCESS MESSAGE TO OUTPUT BUFFER************************
*
SPACE    C     R0,=F'130'         CHECK THAT MESSAGE IS NOT TOO LONG
         BNH   OUT1               IF NOT SKIP OVER FORCEED LENGTH
         LA    R0,130(R0,R0)      FORCE MESSAGE TO MAXIMUM LENGTH
OUT1     BCTR  R0,R0              BRING DOWN TO MACHINE MESSAGE SIZE
         LR    R2,R0              PUT IN REG THAT CAN BE USED AS INDEX
         EX    R2,DMOVE1          MOVE MESSAGE TO SYSOUT BUFFER
*
*SQUISH TRAILING BLANKS OUT OF OUTPUT BUFFER
*
         LTR   R2,R2              TEST IF WE SHOULD SQUISH
         BZ    OUT3               IF ZERO ONLY ONE CHARACTER
OUT2     LA    R1,SYSOUT(R2)      GET ADDRESS OF LAST CHAR
         CLI   0(R1),X'40'        SEE IF IT IS A BLANK
         BNE   OUT3               IF NOT GO WRITE BUFFER
         BCT   R2,OUT2            LOWER COUNT AND TEST AGAIN
*
*SETUP TO WRITE OUT THE BUFFER
*
OUT3     LA    R14,1(R0,R2)       BRING IT BACK UP
         LA    R1,SYSOUT          ADDRESS OF LINE TO BE TYPED
*
*********************OUTPUT BY CALLING PUTLINE*************************
*
OUTPUT   WRITE TI,(1),(14)
         BZ    FF
         PANIC 6
FF       WAIT  TI
         BM    OUTPUT
         B     RETURN
*
*********************MVC FOR MOVING MESSAGE TO OUTPUT BUFFER***********
*
DMOVE1   MVC   SYSOUT(0),0(R1)    MOVE MESSAGE TO SYSOUT BUFFER
*
*********************PREPROCESS SKIP TO TOP OF PAGE********************
*
PAGE     CONTROL TI,1
         BZ    FFF
         PANIC 7
FFF      WAIT  TI
         BM    PAGE
         LR    R0,R3              PUT IT BACK WHERE IT BELONGS
         LR    R1,R4              DITTO
         LCR   R0,R0              MAKE A POSITIVE MSG LENGTH
         B     SPACE              GO MOVE AND WRITE THE MSG
*
***************ALL THAT IS WANTED IS A BLANK LINE**********************
*
BLANK    MVI   SYSOUT,X'40'
         LA    R1,SYSOUT
         LA    R14,1(R0,R0)
         B     OUTPUT
*
*RETURN TO CALLER
*
RETURN   L     13,4(0,13)         PUT ADDR OF CALLERS SAVE AREA BACK
         LM    14,12,12(13)       RESTORE CALLERS REGISTERS
         BCR   15,14              RETURN TO CALLER
*
         LTORG
*
         EJECT
TWORK    DSECT
         COPY TWORK
         EJECT
         END
