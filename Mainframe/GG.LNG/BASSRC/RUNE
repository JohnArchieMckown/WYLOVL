RUNE     TITLE  'RUNERR     --   MESSAGE DISPATCHER'
RUNERR   CSECT
***********************************************************************
*                                                                     *
*                  ERROR MESSAGE PROCESSOR                            *
*                                                                     *
***********************************************************************
*
*        RUNERR IS CALLED WITH A ERROR MESSAGE NUMBER IN REGISTER 1   *
*        AND A BASIC LINE NUMBER IN REGISTER 0 (IN BINARY FORM)       *
*                                                                     *
*        AFTER SOME SORTING OUT LINE NUMBER AND ERROR NUMBER          *
*        ARE MOVED INTO AND ERROR MESSAGE BUFFER IN THE CONTROL       *
*        PROGRAM WORK AREA.  THE MESSAGE ITSELF IS THEN MOVED         *
*        INTO THE SAME BUFFER.                                        *
*                                                                     *
*        A CALL TO PUTLINE IS THEN MADE WITH REGISTER 1 NOW           *
*        POINTING AT THE NEWLY PREPARED BUFFER IN THE CONTROL         *
*        PROGRAM WORK AREA.  REGISTER 0 IS NOT USED ON THIS CALL      *
*        BUT IS SET TO ZERO SO PUTLINE WILL NOT GET CONFUSED.         *
*                                                                     *
*        RUNERR DOES NOT EXPECT AN ERROR RETURN FROM PUTLINE NOR      *
*        DOES IT PASS ONE TO ITS CALLER                               *
*                                                                     *
*        RUNERR IS REENTRANT.  ONLY THE STATEMENTS IN THE AREA        *
*        'SET UP AND LINK TO PUTLINE' SHOULD HAVE TO BE CHANGED       *
*        TO MAKE IT WORK WITH THE TIME SHARING MONITOR.  THIS         *
*        CHANGE WILL PROBABLY INVOLVE LEAVING THE LENGTH IN           *
*        REGISTER 0.
*                                                                     *
***************************START OF OS ENTRY LINKAGE*******************
         SAVE  (14,12),,*
         BALR  2,0                ESTABLISH ADDRESSABILITY FOR THIS ROU
         USING *,2                *
         LR    3,13               COPY CALLERS SAVE AREA ADDRESS
         LA    13,72(0,13)        LOAD ADDRESS OF THIS ROUTINES SAVE AR
         ST    13,8(0,3)          PUT THIS PROGRAMS SAVE AREA ADDRESS
*                                 IN CALLERS SAVE AREA
         ST    3,4(0,13)          PUT CALLING PROGRAMS SAVE AREA ADDRES
*                                 IN THIS PROGRAMS SAVE AREA
*
**********************GET ADDRESSABILITY TO CWORK**********************
*
         L     10,ACWORK
         USING CWORK,10
**********************SORT OUT ON THE ERROR CODE IN REG 1**************
         LA    15,CONVERT         SET UP ADDRESS OF CONVERT ROUTINE
         LTR   1,1                CHECK LOW BOUNDS
         BL    ERROR
         CL    1,TH10             CHECK HIGH BOUNDS
         BH    ERROR
         CL    1,TH5
         BL    BA04
         CL    1,TH7
         BL    BA56
         CL    1,TH8
         BL    A7
         CL    1,TH9
         BL    A8
         B     A9
BA56     CL    1,TH6
         BL    A5
         B     A6
BA04     CL    1,TH2
         BL    BA01
         CL    1,TH3
         BL    A2
         CL    1,TH4
         BL    A3
         B     A4
BA01     CL    1,TH1
         BL    A0
         B     A1
**************ELIMINATE HIGH DIGIT EFFECT******************************
ERROR    SR    1,1                MAKE A 0000 ERROR CODE
A0       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         LA    3,TAB0             GET ADDRESS OF START OF TRANSFER
*                                 VECTOR FOR THIS CODE
         LA    4,TAB1-2           GET ADDRESS OF END OF TRANSFER
*                                 VECTOR FOR THIS CODE
         B     F
A1       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH1              TAKE HIGH ORDER DIGIT OUT
         LA    3,TAB1             START OF TRANSFER VECTOR THIS CODE
         LA    4,TAB2-2           END OF TRANSFER VECTOR FOR THIS CODE
         B     F
A2       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH2
         LA    3,TAB2
         LA    4,TAB3-2
         B     F
A3       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH3
         LA    3,TAB3
         LA    4,TAB4-2
         B     F
A4       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH4
         LA    3,TAB4
         LA    4,TAB5-2
         B     F
A5       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH5
         LA    3,TAB5
         LA    4,TAB6-2
         B     F
A6       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH6
         LA    3,TAB6
         LA    4,TAB7-2
         B     F
A7       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH7
         LA    3,TAB7
         LA    4,TAB8-2
         B     F
A8       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH8
         LA    3,TAB8
         LA    4,TAB9-2
         B     F
A9       BALR  14,15              BRANCH TO CONVERT ERR AND STM NUMBER
         S     1,TH9
         LA    3,TAB9
         LA    4,TAB10-2
*******************CHECKS RANGE AND GETS ADDRESS OF MESSAGE************
F        SR    4,3                GET TRANSFER VECTOR LENGTH FOR
*                                 THIS CODE
         SLL   1,1(0)             CALLERS CODE TO HALF WORD DISPL
         CR    1,4                IS DISPL WITHIN VECTOR LENGTH
         BNH   FF
         SR    1,1
FF       LH    3,0(1,3)           FETCH DISPL TO MESSAGE
********************SET UP AND LINK TO PUTLINE*************************
         MVI   ERRMESS,X'40'      STICK IN CARRIAGE CONTROL
         MVI   ERRMS,X'40'        TO SET UP MVC
         MVC   ERRMS+1(99),ERRMS  BLANK OUT THE BUFFER
         LA    1,TAB(3)           GET ADDRESS OF LENGTH BYTE OF MESSAGE
         SR    5,5                GET A CLEAN REGISTER
         IC    5,0(0,1)           GET LENGTH OF MESSAGE
         CLI   5,X'64'            TEST IF MSG TOO LONG
         BNH   FFF                SKIP IF IT IS NOT
         LA    5,64(0,0)          FORCE A SHORTER LINE
FFF      BCTR  5,0                TAKE AWAY 1 FOR MVC
         EX    5,DMOVE            XEQ MVC WITH LENGTH IN REG 5
         LA    1,ERRMESS          PUT ADDRESS OF OUTPUT BUFFER IN REG 1
         SR    0,0                CLEAR REGISTER 0  (LR  0,5)
         L     15,APUTLINE        LOAD ADDRESS OF OUTPUT DRIVER
         BALR  14,15              GO TO OUTPUT DRIVER
*
*******************ERROR TEST SHOULD GO HERE***************************
*
*********************RETURN TO CALLER**********************************
         MVC   ERRMESS+1(4),LINE
         L     13,4(0,13)         PUT ADDRESS OF CALLING PROGRAMS
*                                 SAVE AREA BACK IN REGISTER 13
         LM    14,12,12(13)       RESTORE CALLERS REGISTERS
         BCR   15,14              RETURN TO CALLER
*
**************MOVE MESSAGE PORTION TO OUTPUT BUFFER********************
*
DMOVE    MVC   ERRMS(0),1(1)      MOVE MESSAGE TO OUTPUT BUFFER
*
*CONVERT STATEMENT AND ERROR NUMBER INTO THE ERROR BUFFER
*
CONVERT  CVD   1,PAREA            MAKE ERROR NUMBER PACKED DECIMAL
         MVC   ERRNO(6),MASK      PUT EDIT MASK INTO POSITION
         ED    ERRNO(6),PAREA+5   CONVERT BY EDITING
         LTR   0,0
         BZ    BLANKOUT
         L     4,TENTHOU
         CR    4,0
         BE    BLANKOUT
         CVD   0,PAREA            MAKE STMT # A PACKED DECIMAL NUMBER
         MVC   ERRLN(6),MASK      PUT EDIT MASK INTO POSITION
         ED    ERRLN(6),PAREA+5   CONVERT BY EDITING
CONVERT1 MVI   ERRMESS+14,C'('    PUT IN THE )
         BCR   15,14              GO BACK
BLANKOUT MVI   ERRMESS+1,C' '
         MVC   ERRMESS+2(12),ERRMESS+1
         B     CONVERT1
*******************HIGH DIGIT CONSTANT TABLE***************************
TH1      DC    1F'1000'
TH2      DC    1F'2000'
TH3      DC    1F'3000'
TH4      DC    1F'4000'
TH5      DC    1F'5000'
TH6      DC    1F'6000'
TH7      DC    1F'7000'
TH8      DC    1F'8000'
TH9      DC    1F'9000'
TH10     DC    1F'9999'
TENTHOU  DC    1F'10000'
LINE     DC    C'LINE'
*******************MASK TO EDIT OUTPUT BUFFER**************************
MASK     DC    X'402020202120'
******************FOR EXTERNAL CALL FROM THIS ROUTINE******************
APUTLINE DC    V(PUTLINE)
ACWORK   DC    V(CWORK)
*******************TRANSFER VECTOR OF DISPLACEMENTS********************
TAB0     DC    AL2(ER0000-1-TAB)
         DC    AL2(ER0001-1-TAB)
TAB1     DC    AL2(ER1000-1-TAB)
         DC    AL2(ER1001-1-TAB)
         DC    AL2(ER1002-1-TAB)
         DC    AL2(ER1003-1-TAB)
         DC    AL2(ER1004-1-TAB)
         DC    AL2(ER1005-1-TAB)
         DC    AL2(ER1006-1-TAB)
         DC    AL2(ER1007-1-TAB)
         DC    AL2(ER1008-1-TAB)
         DC    AL2(ER1009-1-TAB)
         DC    AL2(ER1010-1-TAB)
         DC    AL2(ER1011-1-TAB)
         DC    AL2(ER1012-1-TAB)
         DC    AL2(ER1013-1-TAB)
         DC    AL2(ER1014-1-TAB)
         DC    AL2(ER1015-1-TAB)
         DC    AL2(ER1016-1-TAB)
         DC    AL2(ER1017-1-TAB)
         DC    AL2(ER1018-1-TAB)
         DC    AL2(ER1019-1-TAB)
         DC    AL2(ER1020-1-TAB)
         DC    AL2(ER1021-1-TAB)
         DC    AL2(ER1022-1-TAB)
         DC    AL2(ER1023-1-TAB)
         DC    AL2(ER1024-1-TAB)
         DC    AL2(ER1025-1-TAB)
         DC    AL2(ER1026-1-TAB)
         DC    AL2(ER1027-1-TAB)
         DC    AL2(ER1028-1-TAB)
         DC    AL2(ER1029-1-TAB)
         DC    AL2(ER1030-1-TAB)
         DC    AL2(ER1031-1-TAB)
         DC    AL2(ER1032-1-TAB)
         DC    AL2(ER1033-1-TAB)
         DC    AL2(ER1034-1-TAB)
         DC    AL2(ER1035-1-TAB)
         DC    AL2(ER1036-1-TAB)
         DC    AL2(ER1037-1-TAB)
         DC    AL2(ER1038-1-TAB)
         DC    AL2(ER1039-1-TAB)
         DC    AL2(ER1040-1-TAB)
         DC    AL2(ER1041-1-TAB)
TAB2     DC    AL2(ER2000-1-TAB)
         DC    AL2(ER2001-1-TAB)
         DC    AL2(ER2002-1-TAB)
         DC    AL2(ER2003-1-TAB)
         DC    AL2(ER2004-1-TAB)
         DC    AL2(ER2005-1-TAB)
         DC    AL2(ER2006-1-TAB)
TAB3     DC    AL2(ER3000-1-TAB)
         DC    AL2(ER3001-1-TAB)
TAB4     DC    AL2(ER4000-1-TAB)
         DC    AL2(ER4001-1-TAB)
         DC    AL2(ER4002-1-TAB)
         DC    AL2(ER4003-1-TAB)
         DC    AL2(ER4004-1-TAB)
         DC    AL2(ER4005-1-TAB)
         DC    AL2(ER4006-1-TAB)
         DC    AL2(ER4007-1-TAB)
         DC    AL2(ER4008-1-TAB)
         DC    AL2(ER4009-1-TAB)
         DC    AL2(ER4010-1-TAB)
         DC    AL2(ER4011-1-TAB)
         DC    AL2(ER4012-1-TAB)
         DC    AL2(ER4013-1-TAB)
         DC    AL2(ER4014-1-TAB)
         DC    AL2(ER4015-1-TAB)
         DC    AL2(ER4016-1-TAB)
         DC    AL2(ER4017-1-TAB)
         DC    AL2(ER4018-1-TAB)
         DC    AL2(ER4019-1-TAB)
         DC    AL2(ER4020-1-TAB)
         DC    AL2(ER4021-1-TAB)
         DC    AL2(ER4022-1-TAB)
         DC    AL2(ER4023-1-TAB)
         DC    AL2(ER4024-1-TAB)
         DC    AL2(ER4025-1-TAB)
TAB5     DC    AL2(ER5000-1-TAB)
         DC    AL2(ER5001-1-TAB)
         DC    AL2(ER5002-1-TAB)
         DC    AL2(ER5003-1-TAB)
         DC    AL2(ER5004-1-TAB)
         DC    AL2(ER5005-1-TAB)
         DC    AL2(ER5006-1-TAB)
         DC    AL2(ER5007-1-TAB)
         DC    AL2(ER5008-1-TAB)
         DC    AL2(ER5009-1-TAB)
         DC    AL2(ER5010-1-TAB)
         DC    AL2(ER5011-1-TAB)
         DC    AL2(ER5012-1-TAB)
         DC    AL2(ER5013-1-TAB)
         DC    AL2(ER5014-1-TAB)
         DC    AL2(ER5015-1-TAB)
         DC    AL2(ER5016-1-TAB)
         DC    AL2(ER5017-1-TAB)
         DC    AL2(ER5018-1-TAB)
TAB6     DC    AL2(ER6000-1-TAB)
         DC    AL2(ER6001-1-TAB)
         DC    AL2(ER6002-1-TAB)
         DC    AL2(ER6003-1-TAB)
         DC    AL2(ER6004-1-TAB)
         DC    AL2(ER6005-1-TAB)
         DC    AL2(ER6006-1-TAB)
TAB7     DC    AL2(ER7000-1-TAB)
         DC    AL2(ER7001-1-TAB)
TAB8     DC    AL2(ER8000-1-TAB)
TAB9     DC    AL2(ER9000-1-TAB)
TAB10    EQU   *
***********************TABLE OF MESSAGES*******************************
TAB      EQU   *
         DC    AL1(L'ER0000)
ER0000   DC    C'CONTROL PROGRAM UNDEFINED ERROR'
         DC    AL1(L'ER0001)
ER0001   DC    C'COMPILATION TERMINATED. TOO MANY CONSTANTS/DATA'
         DC    AL1(L'ER1000)
ER1000   DC    C'SYNTAX PROCESSOR UNDEFINED ERROR'
         DC    AL1(L'ER1001)
ER1001   DC    C'STATEMENT ENDS UNEXPECTEDLY'
         DC    AL1(L'ER1002)
ER1002   DC    C'MISSING OR INCORRECT STATEMENT TYPE'
         DC    AL1(L'ER1003)
ER1003   DC    C' '
         DC    AL1(L'ER1004)
ER1004   DC    C'MISSING OR INCORRECT FUNCTION IDENTIFIER'
         DC    AL1(L'ER1005)
ER1005   DC    C'MISSING OR INCORRECT PARAMETER'
         DC    AL1(L'ER1006)
ER1006   DC    C'MISSING EQUALS SIGN'
         DC    AL1(L'ER1007)
ER1007   DC    C'MISSING "THEN"'
         DC    AL1(L'ER1008)
ER1008   DC    C'MISSING OR INCORRECT "FOR-VARIABLE"'
         DC    AL1(L'ER1009)
ER1009   DC    C'MISSING "TO"'
         DC    AL1(L'ER1010)
ER1010   DC    C'INCORRECT STEP'
         DC    AL1(L'ER1011)
ER1011   DC    C'INCORRECT CONSTANT'
         DC    AL1(L'ER1012)
ER1012   DC    C'MISSING OR INCORRECT QUANTITY'
         DC    AL1(L'ER1013)
ER1013   DC    C'NO CLOSE QUOTE FOR PRINT STRING'
         DC    AL1(L'ER1014)
ER1014   DC    C'MISSING PUNCTUATION OR BAD EXPRESSION'
         DC    AL1(L'ER1015)
ER1015   DC    C'ILLEGAL WORD FOLLOWS "MAT"'
         DC    AL1(L'ER1016)
ER1016   DC    C' '
         DC    AL1(L'ER1017)
ER1017   DC    C'IMPROPER MATRIX FUNCTION'
         DC    AL1(L'ER1018)
ER1018   DC    C'INCORRECT PARAMETER LIST'
         DC    AL1(L'ER1019)
ER1019   DC    C'MAY NOT TRANSPOSE MATRIX INTO SELF'
         DC    AL1(L'ER1020)
ER1020   DC    C' '
         DC    AL1(L'ER1021)
ER1021   DC    C' '
         DC    AL1(L'ER1022)
ER1022   DC    C'MATRIX MAY NOT BE BOTH OPERAND AND RESULT OF MATRIX MUX
               LT.'
         DC    AL1(L'ER1023)
ER1023   DC    C'MISSING LEFT PARENTHESIS'
         DC    AL1(L'ER1024)
ER1024   DC    C'MISSING RIGHT PARENTHESIS'
         DC    AL1(L'ER1025)
ER1025   DC    C'ELEMENT NOT RECOGNIZED'
         DC    AL1(L'ER1026)
ER1026   DC    C'BAD SUBSCRIPT'
         DC    AL1(L'ER1027)
ER1027   DC    C'MISSING ARRAY IDENTIFIER'
         DC    AL1(L'ER1028)
ER1028   DC    C'MISSING OR BAD INTEGER OR LINE NUMBER'
         DC    AL1(L'ER1029)
ER1029   DC    C'NON-BLANK CHARACTER FOLLOWING STATEMENT''S LOGICAL ENDX
               '
         DC    AL1(L'ER1030)
ER1030   DC    C' '
         DC    AL1(L'ER1031)
ER1031   DC    C'MISSING SIMPLE VARIABLE NAME'
         DC    AL1(L'ER1032)
ER1032   DC    C'OPERATOR CANNOT DIRECTLY FOLLOW OPERATOR'
         DC    AL1(L'ER1033)
ER1033   DC    C'STRING CONSTANT CANNOT EXCEED 15 CHARACTERS'
         DC    AL1(L'ER1034)
ER1034   DC    C'MISPLACED LEFT PARENTHESIS'
         DC    AL1(L'ER1035)
ER1035   DC    C'MISPLACED RIGHT PARENTHESIS'
         DC    AL1(L'ER1036)
ER1036   DC    C'MISSING OR INCORRECT STRING VARIABLE OR STRING CONSTANX
               T'
         DC    AL1(L'ER1037)
ER1037   DC    C'MISPLACED QUOTE'
         DC    AL1(L'ER1038)
ER1038   DC    C'MISPLACED COMMA OR SEMICOLON'
         DC    AL1(L'ER1039)
ER1039   DC    C' '
         DC    AL1(L'ER1040)
ER1040   DC    C'MISSING RELATION OPERATOR'
         DC    AL1(L'ER1041)
ER1041   DC    C'"FOR", "NEXT", && NON-EXECUTABLE STMTS ILLEGAL AFTER "X
               THEN"'
         DC    AL1(L'ER2000)
ER2000   DC    C'COMPILER UNDEFINED ERROR'
         DC    AL1(L'ER2001)
ER2001   DC    C'VARIABLE ALREADY DIMENSIONED. STATEMENT IGNORED'
         DC    AL1(L'ER2002)
ER2002   DC    C'FUNCTION PREVIOUSLY DEFINED. STATEMENT IGNORED'
         DC    AL1(L'ER2003)
ER2003   DC    C'PSEUDO-CODE TABLE FULL. COMPILATION TERMINATED'
         DC    AL1(L'ER2004)
ER2004   DC    C'LINES TABLE FULL. COMPILATION TERMINATED'
         DC    AL1(L'ER2005)
ER2005   DC    C'TOO MANY CONSTANTS/DATA. COMPILATION TERMINATED'
         DC    AL1(L'ER2006)
ER2006   DC    C'"FOR-NEXT" TABLE FULL. COMPILATION TERMINATED'
         DC    AL1(L'ER3000)
ER3000   DC    C'LOADER UNDEFINED ERROR'
         DC    AL1(L'ER3001)
ER3001   DC    C'NO MORE CORE TO ALLOCATE ARRAYS'
         DC    AL1(L'ER4000)
ER4000   DC    C'STOP EXECUTED'
         DC    AL1(L'ER4001)
ER4001   DC    C'PAUSE EXECUTED'
         DC    AL1(L'ER4002)
ER4002   DC    C'END OF EXECUTION'
         DC    AL1(L'ER4003)
ER4003   DC    C'TOO MANY NESTED GOSUBS'
         DC    AL1(L'ER4004)
ER4004   DC    C'TOO MANY NESTED DEFINED FUNCTIONS'
         DC    AL1(L'ER4005)
ER4005   DC    C'TOO MANY NESTED FOR LOOPS'
         DC    AL1(L'ER4006)
ER4006   DC    C'NO CORRESPONDING FOR STATEMENT'
         DC    AL1(L'ER4007)
ER4007   DC    C'NO CORRESPONDING NEXT STATEMENT'
         DC    AL1(L'ER4008)
ER4008   DC    C'DEF FN MISSING'
         DC    AL1(L'ER4009)
ER4009   DC    C'MATRIX RE-DEFINITION EXCEEDS ALLOCATED STORAGE'
         DC    AL1(L'ER4010)
ER4010   DC    C'INCONSISTENT USE OF SUBSCRIPTS'
         DC    AL1(L'ER4011)
ER4011   DC    C'STATEMENT MISSING'
         DC    AL1(L'ER4012)
ER4012   DC    C'INCOMPATIBLE DIMENSIONS IN A MATRIX COMPUTATION'
         DC    AL1(L'ER4013)
ER4013   DC    C'SUBSCRIPT OUT OF RANGE'
         DC    AL1(L'ER4014)
ER4014   DC    C'EXECUTING A SYNTACTICALLY INCORRECT STATEMENT'
         DC    AL1(L'ER4015)
ER4015   DC    C'NO MORE DATA'
         DC    AL1(L'ER4016)
ER4016   DC    C'PRINT ERROR'
         DC    AL1(L'ER4017)
ER4017   DC    C'READ ERROR'
         DC    AL1(L'ER4018)
ER4018   DC    C'WRITE ERROR'
         DC    AL1(L'ER4019)
ER4019   DC    C'VARIABLE MAY NOT BE BOTH SIMPLE AND SUBSCRIPTED'
         DC    AL1(L'ER4020)
ER4020   DC    C'NO CORRESPONDING GOSUB STATEMENT '
         DC    AL1(L'ER4021)
ER4021   DC    C'NO MORE CORE TO ALLOCATE ARRAYS'
         DC    AL1(L'ER4022)
ER4022   DC    C'NOTHING TO EXECUTE'
         DC    AL1(L'ER4023)
ER4023   DC    C'ARGUMENT TO RND NOT A SIMPLE VARIABLE'
         DC    AL1(L'ER4024)
ER4024   DC    C'INVALID STRING DATA IN A DATA STATEMENT'
         DC    AL1(L'ER4025)
ER4025   DC    C'ATTN'
         DC    AL1(L'ER5000)
ER5000   DC    C'ARITHMETIC ROUTINE UNDEFINED ERROR'
         DC    AL1(L'ER5001)
ER5001   DC    C'ARITHMETIC OVERFLOW-NUMBER TOO LARGE'
         DC    AL1(L'ER5002)
ER5002   DC    C'ARITHMETIC UNDERFLOW-NUMBER TOO SMALL'
         DC    AL1(L'ER5003)
ER5003   DC    C'DIVIDE CHECK-DO NOT DIVIDE BY ZERO'
         DC    AL1(L'ER5004)
ER5004   DC    C'ATTEMPT TO RAISE NEGATIVE NUMBER OR ZERO TO FLOATING PX
               OINT POWER'
         DC    AL1(L'ER5005)
ER5005   DC    C'ATTEMPT TO TAKE "LOG" OF ZERO OR NEGATIVE NUMBER'
         DC    AL1(L'ER5006)
ER5006   DC    C'"EXP" GIVEN TOO LARGE AN ARGUMENT'
         DC    AL1(L'ER5007)
ER5007   DC    C'ATTEMPT TO TAKE SQUARE ROOT OF A NEGATIVE NUMBER'
         DC    AL1(L'ER5008)
ER5008   DC    C'ARGUMENT OF "SIN" OR "COS" TOO LARGE'
         DC    AL1(L'ER5009)
ER5009   DC    C'ARGUMENT OF TAN TOO LARGE'
         DC    AL1(L'ER5010)
ER5010   DC    C'ATTEMPT TO INVERT A SINGULAR MATRIX'
         DC    AL1(L'ER5011)
ER5011   DC    C'NUMBER OUT OF RANGE IN INVERSION PROCESS'
         DC    AL1(L'ER5012)
ER5012   DC    C'NUMBER OUT OF RANGE IN RAISING NO. TO A POWER'
         DC    AL1(L'ER5013)
ER5013   DC    C'NUMBER OUT OF RANGE IN LOG FUNCTION'
         DC    AL1(L'ER5014)
ER5014   DC    C'NUMBER OUT OF RANGE IN EXP FUNCTION'
         DC    AL1(L'ER5015)
ER5015   DC    C'NUMBER OUT OF RANGE IN SQR FUNCTION'
         DC    AL1(L'ER5016)
ER5016   DC    C'NUMBER OUT OF RANGE IN ATN FUNCTION'
         DC    AL1(L'ER5017)
ER5017   DC    C'NUMBER OUT OF RANGE IN SIN OR COS FUNCTION'
         DC    AL1(L'ER5018)
ER5018   DC    C'NUMBER OUT OF RANGE IN TAN FUNCTION'
         DC    AL1(L'ER6000)
ER6000   DC    C'END OF FILE ON INPUT'
         DC    AL1(L'ER6001)
ER6001   DC    C'INCORRECTLY SPECIFIED STRING'
         DC    AL1(L'ER6002)
ER6002   DC    C'NO INPUT COULD BE FOUND'
         DC    AL1(L'ER6003)
ER6003   DC    C'STRING INCORRECTLY POSITIONED OR NOT PRESENT'
         DC    AL1(L'ER6004)
ER6004   DC    C'STRING LONGER THAN 15 CHARACTERS'
         DC    AL1(L'ER6005)
ER6005   DC    C'NUMERIC DATA INCORRECTLY POSITIONED'
         DC    AL1(L'ER6006)
ER6006   DC    C'NUMERIC DATA INCORRECTLY SPECIFIED'
         DC    AL1(L'ER7000)
ER7000   DC    C'UNDEFINED PROCESSOR UNDEFINED ERROR'
         DC    AL1(L'ER7001)
ER7001   DC    C'STATEMENT IGNORED - NOT IMPLEMENTED YET'
         DC    AL1(L'ER8000)
ER8000   DC    C'UNDEFINED PROCESSOR UNDEFINED ERROR'
         DC    AL1(L'ER9000)
ER9000   DC    C'UNDEFINED PROCESSOR UNDEFINED ERROR'
         EJECT
CWORK    DSECT
         COPY  CWORK
         END
