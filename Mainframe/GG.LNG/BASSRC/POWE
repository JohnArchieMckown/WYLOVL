POWE     TITLE ' COMPUTE A**B'
POWER    CSECT
         FSTART  APWRL,10,EINT
         LA    10,4(10)
         SRL   10,3               TO FORCE DOUBLE WORD ALLIGNMENT
         SLL   10,3
         LTER  2,2
         BC    4,FRXPS            BRANCH IF B < 0
         BC    8,FRXPW            BRANCH IF B = 0
         SDR   4,4
         LER   4,2
         SDR   6,6
         LE    6,PZERO
         AER   6,4
         CER   6,4
         BE    FRXPU              BRANCH IF YES
FRXPS    LTER  0,0
         BC    2,FRXPV            BRANCH IF A > 0
         FEXIT Y,5004
FRXPW    LE    0,ONE
         B     EXIT
FRXPU    LR    0,2                 SAVE GPR2
         SR    2,2
         AW    6,P2ZERO
         STD   6,FRXPB
         L     2,FRXPB+4
         LER   6,0
         LE    0,ONE
FRXPX    MER   0,6                MULTIPLY
         BCT   2,FRXPX            REPEAT
         LR    2,0                 RESTORE GPR2
EXIT     FEXIT Y                 NORMAL RETURN
FRXPV    LR    0,14
         STD   2,FRXPB            -SAVE F2
         L     15,VLOG
         BALR  14,15              LOG(A)
         B     EINT               POSSIBLE ERROR RETURN
         LR    14,0
         LD    2,FRXPB             -RESTORE F2
         MER   0,2                 B*LOG(A)
         L     13,4(13)
         LM    14,12,12(13)
         L     15,VEXP
         BR    15           EXP(B*LOG(A))=A**B  NO RETURN NECESSARY
EINT     FEXIT Y,5012
ONE      DC    E'1'
PZERO    DC    X'47000000'
RZERO    DC    D'0'
P2ZERO   DC    X'4E00000000000000'
VLOG     DC    V(LOG)
VEXP     DC    V(EXP)
APWRL    DSECT
FRXPB    DS    D
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
