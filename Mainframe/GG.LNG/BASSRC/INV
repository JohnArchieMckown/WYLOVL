INV      TITLE 'BASIC INVERSE ROUTINE'
INV      CSECT
*
*   ROUTINE FROM IBM360 SCIENTIFIC SUBROUTINE PACKAGE - PAGE 118
*
*         SUBROUTINE MINV(A,N,D,L,M)
*
*               A - INPUT MATRIX, DESTROYED IN COMPUATION AND REPLACED
*                   BY INVERSE
*               N - ORDER OF MATRIX A
*               D - RESULTANT DETERMINANT
*               L - WORK VECTOR OF LENGTH N
*               M - WORK VECTOR OF LENGTH N
*
*         METHOD
*
*               THE STANDARD GAUSS-JORDAN METHOD.
*               IF D=0, THEN THE MATRIX WAS SINGULAR.
*
*         DIMENSION A(1),L(1),M(1)
*
*  SEARCH FOR THE LARGEST ELEMENT
*
*         D=1.0
*         NK=-N
*         DO 80 K=1,N
*         NK=NK+N
*         L(K)=K
*         M(K)=K
*         KK=NK+K
*         BIGA=A(KK)
*
*         DO 20 J=1,N
*         IZ=N*(J-1)
*               DO 20 I=K,N
*               IJ=IZ+1
*   10          IF(ABS(BIGA) - ABS(A(IJ))) 15,20,20
*   15          BIGA=A(IJ)
*               L(K)=I
*               M(K)=J
*   20    CONTINUE
*
*   INTERCHANGE ROWS
*
*         J=L(K)
*         IF (J-K)35,35,25
*   25    KI=K-N
*         DO 30 I=1,N
*         KI=KI+N
*         HOLD=-A(KI)
*         JI=KI-K+J
*         A(KI)=A(JI)
*   30    A(JI)=HOLD
*
*   INTERCHANGE COLUMNS
*
*   35    I=M(K)
*         IF(1-K) 45,45,38
*   38    JP=N*(I-1)
*         DO 40 J=I,N
*         JK=NK+J
*         JI=JP+J
*         HOLD = -A(JK)
*         A(JK)= A(JI)
*   40    A(JI)=HOLD
*
*   DIVIDE COLUMN BY MINUS PIVOT ELEMENT (BIGA)
*
*   45    IF(BIGA) 48,46,48
*   46    D=0.
*         RETURN
*   48    DO 55 I=1,N
*         IF(I-K)50,55,50
*   50    IK=NK+I
*         A(IK)=A(IK)/(-BIGA)
*   55    CONTINUE
*
*   REDUCE MATRIX
*
*         DO 65 I=1,N
*         IK=NK+I
*         HOLD=A(IK)
*         IJ=I-N
*               DO 65 J=1,N
*               IJ=IJ+N
*               IF (I-K) 60,65,60
*   60          IF (J-K) 62,65,62
*   62          KJ=IJ-I+K
*               A(IJ) = HOLD*A(KJ)+A(IJ)
*   65    CONTINUE
*
*   DIVIDE ROW BY PIVOT
*
*         KJ=K-N
*         DO 75 J=1,N
*         KJ=KJ+N
*         IF (J-K) 70,75,70
*   70    A(KJ) = A(KJ)/BIGA
*   75    CONTINUE
*
*   PRODUCT OF PIVOTS (DETERMINANT)
*
*         D = D*BIGA
*
*   REPLACE PIVOT BY RECIPROCAL
*
*         A(KK)=1.0/BIGA
*
*   80    CONTINUE
*
*   FINAL ROW AND COLUMN INTERCHANGE
*
*         K=N
*   100   K=(K-1)
*         IF (K) 150,150,105
*   105   I=L(K)
*         IF (I-K) 120,120,108
*   108   JQ=N*(K-1)
*         JR=N*(I-1)
*         DO 110 J=1,N
*         JK=JQ+J
*         HOLD=A(JK)
*         JI=JR+J
*         A(JK)=-A(JI)
*   110   A(JI)=HOLD
*
*   120   J=M(K)
*         IF (J-K) 100,100,125
*   125   KI=K-N
*         DO 130 I=1,N
*         KI=KI+N
*         HOLD=A(KI)
*         JI=KI-K+J
*         A(KI)=-A(JI)
*   130   A(JI)=HOLD
*         GO TO 100
*   150   RETURN
*         END
*
         FSTART  AINVL,,EINT
         LM    R2,R4,0(R1)
*
****
****
*
*   MAT A=INV(B)
*
*   R2= ADDRESS OF B
*   R3= ADDRESS OF A
*   R4= ROWS OF B
*
*
*       RETURNS:
*
*          4(R14) IF MATRIX WAS NON-SINGULAR, WITH F4=F6=DETERMINANT
*
*          0(R14) IF MATRIX WAS SINGULAR, WITH F4=F6=0 AND
*                 R1=5010 FOR 'SINGULAR MESSAGE'
*
*          0(R14) IF A NUMBER WAS OUT OF RANGE IN THE
*                 DIVISION, WITH F4~=F6 AND
*                 R1=5011 FOR 'OUT OF RANGE' MESSAGE.
*
*       THE MATRIX TO BE INVERTED MAY BE THE SAME AS THE RESULTANT
*       MATRIX.
*
****
****
*
         LE    F6,ONE                  -F6 IS THE DETERMINANT
         LA    T,256
         LR    T2,R3
         LR    R0,R4
         LR    R5,R4
         MR    R4,R5
         SLL   R5,2
MV       CR    R5,T
         BNH   FNLMV
         MVC   0(256,R3),0(R2)
         AR    R2,T
         AR    R3,T
         SR    R5,T
         B     MV
MVCOP    MVC   0(1,R3),0(R2)
FNLMV    BCTR  R5,0
         EX    R5,MVCOP
         USING AX,R3
         LR    R3,T2
*
         LR    N,R0
         LA    INC,1
         LNR   NK,N
         LA    K,1
S80      AR    NK,N
         LR    T,K
         SR    T,INC
         SLL   T,1
         STH   K,L(T)
         STH   K,M(T)
         LR    KK,NK
         AR    KK,K
         SR    KK,INC
         SLL   KK,2
         LE    F0,A(KK)
         STE   F0,BIGA
         STH   KK,TKK
         LR    J,K
S20      LR    IZ,J
         SR    IZ,INC
         STH   N,TH
         MH    IZ,TH
         LR    I,K
S20A     LR    IJ,IZ
         AR    IJ,I
         SR    IJ,INC
         SLL   IJ,2
         LE    F0,A(IJ)
         LPER  F2,F0
         LE    F4,BIGA
         LPER  F4,F4
L10      CER   F4,F2
         BNL   L20
L15      STE   F0,BIGA
         LR    T2,K
         SR    T2,INC
         SLL   T2,1
         STH   I,L(T2)
         STH   J,M(T2)
L20      BXLE  I,INC,S20A
         BXLE  J,INC,S20
         LR    T,K
         SR    T,INC
         SLL   T,1
         LH    J,L(T)
         CR    J,K
         BNH   L35
L25      LR    KI,K
         SR    KI,N
         LA    I,1
S30      AR    KI,N
         LR    T2,KI
         SR    T2,INC
         SLL   T2,2
         LE    F0,A(T2)
         LCER  F2,F0
         LR    JI,KI
         SR    JI,K
         AR    JI,J
         SR    JI,INC
         SLL   JI,2
         LE    F0,A(JI)
         STE   F0,A(T2)
         STE   F2,A(JI)
L30      BXLE  I,INC,S30
L35      LR    T,K
         SR    T,INC
         SLL   T,1
         LH    I,M(T)
         CR    I,K
         BNH   L45
L38      LR    JP,I
         SR    JP,INC
         STH   N,TH
         MH    JP,TH
         LA    J,1
S40      LR    JK,NK
         AR    JK,J
         LR    JI,JP
         AR    JI,J
         SR    JK,INC
         SLL   JK,2
         SR    JI,INC
         SLL   JI,2
         LE    F0,A(JK)
         LCER  F2,F0
         LE    F0,A(JI)
         STE   F0,A(JK)
         STE   F2,A(JI)
L40      BXLE  J,INC,S40
L45      LE    F0,BIGA
         LTER  F0,F0
         BZ    OUTSNG
L48      LA    I,1
S55      CR    I,K
         BZ    L55
L50      LR    IK,NK
         AR    IK,I
         SR    IK,INC
         SLL   IK,2
         LE    F0,BIGA
         LCER  F0,F0
         LE    F2,A(IK)
         DER   F2,F0
         STE   F2,A(IK)
L55      BXLE  I,INC,S55
         LA    I,1
S65      LR    IK,NK
         AR    IK,I
         SR    IK,INC
         SLL   IK,2
         LE    F2,A(IK)
         LR    IJ,I
         SR    IJ,N
         LA    J,1
S65A     AR    IJ,N
         CR    I,K
         BZ    L65
L60      CR    J,K
         BZ    L65
L62      LR    KJ,IJ
         SR    KJ,I
         AR    KJ,K
         SR    KJ,INC
         SLL   KJ,2
         LR    T,IJ
         SR    T,INC
         SLL   T,2
         LE    F0,A(KJ)
         MER   F0,F2
         AE    F0,A(T)
         STE   F0,A(T)
L65      BXLE  J,INC,S65A
         BXLE  I,INC,S65
         LR    KJ,K
         SR    KJ,N
         LA    J,1
S75      AR    KJ,N
         CR    J,K
         BZ    L75
L70      LR    T,KJ
         SR    T,INC
         SLL   T,2
         LE    F0,A(T)
         DE    F0,BIGA
         STE   F0,A(T)
L75      BXLE  J,INC,S75
*
         ME    F6,BIGA                 -DETERMINANT
         LE    F0,ONE
         DE    F0,BIGA
         LH    KK,TKK
         STE   F0,A(KK)
L80      BXLE  K,INC,S80
         LR    K,N
L100     SR    K,INC
         LTR   K,K
         BZ    OUTANS
         LR    T,K
         SR    T,INC
         SLL   T,1
L105     LH    I,L(T)
         CR    I,K
         BNH   L120
L108     LR    JQ,K
         SR    JQ,INC
         STH   N,TH
         MH    JQ,TH
         LR    JR,I
         SR    JR,INC
         MH    JR,TH
         LA    J,1
S110     LR    JK,JQ
         AR    JK,J
         SR    JK,INC
         SLL   JK,2
         LE    F0,A(JK)
         LR    JI,JR
         AR    JI,J
         SR    JI,INC
         SLL   JI,2
         LE    F2,A(JI)
         LCER  F2,F2
         STE   F2,A(JK)
         STE   F0,A(JI)
L110     BXLE  J,INC,S110
L120     LR    T,K
         SR    T,INC
         SLL   T,1
         LH    J,M(T)
         CR    J,K
         BNH   L100
L125     LR    KI,K
         SR    KI,N
         LA    I,1
S130     AR    KI,N
         LR    T2,KI
         SR    T2,INC
         SLL   T2,2
         LE    F2,A(T2)
         LR    JI,KI
         SR    JI,K
         AR    JI,J
         SR    JI,INC
         SLL   JI,2
         LE    F0,A(JI)
         LCER  F0,F0
         STE   F0,A(T2)
         STE   F2,A(JI)
L130     BXLE  I,INC,S130
         B     L100
*
OUTSNG   EQU   *
*
*        SINGULAR MATRIX
*        SET R1=5010, AND
*        F6=F4=0
*
         SER   F6,F6
         LER   F4,F6
         FEXIT N,5010
*
OUTANS   EQU   *
*
*        NORMAL RETURN
*        SET R1 = 0, AND
*        F6=F4=DETERMINANT
*
         LER   F4,F6
         FEXIT N
*
EINT     EQU   *
*
*        NUMBER OUT OF RANGE.
*        SET R1=5011, AND
*        F6~=F4
*
         SER   F6,F6
         LE    F4,ONE
         FEXIT N,5011
*
****
*
*   EQU'S AND CONSTANTS
*
*
R0       EQU   0
F0       EQU   0
R1       EQU   1
I        EQU   1
R2       EQU   2
F2       EQU   2
J        EQU   2
R3       EQU   3
R4       EQU   4
F4       EQU   4
INC      EQU   4
R5       EQU   5
N        EQU   5
R6       EQU   6
K        EQU   6
F6       EQU   6
R7       EQU   7
NK       EQU   7
JR       EQU   7
R8       EQU   8
T        EQU   8
KK       EQU   8
IZ       EQU   8
KI       EQU   8
JP       EQU   8
IK       EQU   8
R9       EQU   9
IJ       EQU   9
JK       EQU   9
R10      EQU   10
JI       EQU   10
KJ       EQU   10
R11      EQU   11
R12      EQU   12
T2       EQU   12
JQ       EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
ONE      DC    E'1.0'
AINVL    DSECT
BIGA     DS    F
TH       DS    H
L        DS    50H
M        DS    50H
TKK      DS    H
AX       DSECT
A        DS    2500F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
