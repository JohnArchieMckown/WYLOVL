LOG      TITLE 'LOG FUNCTION'
LOG      CSECT
*
*        LOGARITHMIC FUNCTION (SHORT)
*              WRITE X = M*16**P M MANTISSA. PICK A BASE VALUE
*              A DEPENDING ON SIZE OF M, WRITE Z=(M-A)/(M+A).
*              THEN LOG(X) = P*LOG(10)+LOG(A)+LOG((1+Z)/(1-Z)).
*
*
GR0      EQU   0               SCRATCH BEGISTERS
GR1      EQU   1
GR2      EQU   14
GRA      EQU   1               ARGUMENT POINTER
GRS      EQU   13              SAVE AREA POINTER
GRR      EQU   14              RETURN REGISTER
GRL      EQU   15              LINK REGISTER
FR0      EQU   0               ANSWER REGISTER
FR2      EQU   2               SCRATCH REGISTER
FR6      EQU   6
*
         FSTART  ALOGL,10,EINT
         LE    FR6,IPART1
         STE   FR6,IPART
         MVC   TABLE(8),TABLE1
         STE   FR0,ARG
         L     GR0,ARG
         LTR   GR0,GR0
         BC    12,ERROR        IF ARG 0 OR NEGATIVE, ERROR
         SRDL  GR0,24
         SRL   GR1,8
         ST    GR1,ARG         STORE MANTISSA M ALONE
         OI    ARG,X'40'       AND FLOAT IT
         SLL   GR0,2           4*CHAR IN GR0
         STH   GR0,IPART+2     SAVE THIS IN FLOAT FORM
         SR    GR2,GR2         SET GR2 TO 0, 4, OR 8, AND DEFINE
         SRL   GR1,21          A TO BE 1, 1/4, OR 1/16, IF M IS IN
         IC    GR2,TABLE(GR1)  (1/2,1), (1/8,1/2), OR IN (1/16,1/8)
         LE    FR0,ARG         OBTAIN Z = (M-A)/(M+A)
         LER   FR2,FR0
         SE    FR0,ONE(GR2)
         AE    FR2,ONE(GR2)    POSSIBLY ONLY 21 SIGNIF BITS HERE
         DER   FR0,FR2
         STE   FR0,ARG         Z READY. STORE IT AT ARG
         MER   FR0,FR0         Z**2
         LE    FR2,C4          COMPUTE LOG((1+Z)/(1-Z)) USING
         MER   FR2,FR0         CHEEYSHEV INTERPOLATION POLYNOMIAL
         AE    FR2,C3
         MER   FR2,FR0
         AE    FR2,C2
         MER   FR2,FR0
         AE    FR2,C1
         MER   FR2,FR0
         LE    FR0,ARG
         MER   FR2,FR0
         AER   FR2,FR0
         AER   FR2,FR0
         LE    FR0,IPART       4*(P+64)
         SRL   GR2,1           0 OR 2 OR 4 = - LOG(A) BASE 2
         LA    GR2,256(GR2)    4*64-LOG2(A)
         STH   GR2,IPART+2     STORE THIS AND FLOAT IT
         SE    FR0,IPART       4*P+LOG2(A)
         ME    FR0,LOGE2
         ADR   FR0,FR2         NATURAL LOG READY
         FEXIT Y
ERROR    FEXIT Y,5005
EINT     FEXIT Y,5013
         DS    0F
IPART1   DC    X'46000000'
TABLE1   DC    X'0804040400000000'
ONE      DC    X'41100000'   1 FOLLOWING 3 CONSTANTS MUST
         DC    X'40400000'   1/4 BE CONSECUTIVE
         DC    X'40100000'     1/16
LOGE2    DC    X'40B17219'     LOG(2) BASE E + FUDGE 1
C4       DC    X'4048157B'     0.28157778
C3       DC    X'4047973F'     0.27965158
C2       DC    X'40667685'     0.40024595
C1       DC    X'40AAAA71'     0.66666322
ALOGL    DSECT
DUMMY    DS    D
ARG      DS    2F
TABLE    DS    D
IPART    DS    F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
