TAN      TITLE 'TAN ROUTINE'
TAN      CSECT
*        TANGENT-COTANGENT FUNCTION (SHORT)
*              1. DIVIDE MAGNITUDE OF ARG BY PI/4 TO FIND OCTANT AND
*                          FRACTION.  REDUCED ARGUMENT W IS EITHER THIS
*                          FRACTION OR ITS COMPLEMENT. THE MAGNITUDE OF
*                          ANSWER IS EITHER TAN(W*PI/4) OR COT(W*PI/4)
*              2. IF /ARG/ IS EQUAL OR GREATER THAN PI*2**18, ERROR1
*              3. IF COTAN IS WANTED, AND IF /ARG/ IS EQUAL OR SMALLER
*                              THAN 2**(-252), ERROR2
*              4. IF ARG IS SO CLOSE TO ONE OF SINGULARITIES OF THE
*              FUNCTION THAT THE COMBINED EFFECT OF COMPUTATIONAL
*              ERROR AND MINIMAL INPUT ERROR CAN CAUSE RELATIVE
*              ERROR OF 1/5, ERROR2 IS GIVEN
*
*
GR1      EQU   1               SCRATCH
GRA      EQU   1               ARGUMENT POINTER
GRS      EQU   13              SAVE AREA POINTER
GRR      EQU   14              RETURN REGISTER
GRL      EQU   15              LINK REGISTER
FR0      EQU   0               ANSWER REGISTER
FR2      EQU   2               SCRATCH REGISTERS
FR4      EQU   4
*
         FSTART  ATANL,10,EINT
*
MERGE    STE   FR0,TEMP        COMMON SECTION STARTS
         SDR   FR0,FR0
         SDR   FR2,FR2         CLEAR FR0 AND FR2 LONG FORM
         LE    FR0,TEMP
         LE    FR4,QTAN1
         STE   FR4,QTAN
         LPER  FR0,FR0         /ARG/
         CE    FR0,MAX
         BC    10,ERROR1       IF /ARG/ TOO BIG, GIVE ERROR1
         MD    FR0,FOVPI       MULTIPLY /ARG/ BY 4/PI (LONG FORM)
         STE   FR0,OCTNT       I.E. DIVIDE BY PI/4
         MVC   QTAN(1),OCTNT   GIVE CHAR OF QUOTIENT TO TESTING GAUGE
         CE    FR0,ONE
         BC    10,NORML        IF QUOTIENT HAS INTEGER PART, SKIP
         BC    15,JOIN          AND IF TAN ENTRY, SKIP
*
ERROR2   FEXIT Y,5019
*
NORML AW FR0,CH46              IF QUOTIENT HAS INTEGER PART,
         LER   FR2,FR0         SEPARATE IT TO FR2 (UNNORMALIZED)
         SDR   FR0,FR2         AND LEAVE FRACTION PART IN FR0
JOIN     STE   FR2,OCTNT       SAVE INTEGER PART, LOW BITS TELL OCTANT
         TM    OCTNT+3,X'01'
         BC    8,SWICH         IF EVEN OCTANT, MOD ARG W IS READY
         SE    FR0,ONE         IF ODD OCTANT, W=1-FRACTION
         LPER  FR0,FR0
SWICH    TM    OCTNT+3,X'01'
         LER   FR2,FR0
         MER   FR2,FR2
         LER   FR4,FR2
         AE    FR4,B1
         MER   FR4,FR2         COMPUTE TWO POLYNOMIAL FACTORS
         AD    FR4,B0          P(W) = A0*W+A1*W**3
         ME    FR2,A1
         AE    FR2,A0          Q(W) = B0+B1*W**2+W**4
         MER   FR2,FR0         USE DOUBLE PRECISION B0 FOR ACCURACY
         TM    OCTNT+3,X'03'
         BC    4,COTN
         DER   FR2,FR4         IF OCTANT IS 0 OR 3 (MOD 4),
         LER   FR0,FR2         THE ANSWER IS TAN(W*PI/4)=P.W)/Q(W)
         BC    15,SIGN
*
COTN     CE    FR0,QTAN        IF OCTANT IS 1 OR 2 (MOD 4), AND IF
         BC    12,ERROR2       W IS TOO SMALL, SINGULARITY TROUBLE.
         DER   FR4,FR2         OTHERWISE, THE ANSWER IS
         LER   FR0,FR4         COTAN(W*PI/4)=Q(W)/(W)
*
SIGN     TM    OCTNT+3,X'02'   IF OCTANT IS 2 OR 3 (MOD 4),
         BC    8,*+6           CHANGE SIGN OF ANSWER
         LCER  FR0,FR0
         TM    TEMP,X'80'    IF ARGUMENT WAS NEGATIVE,
         BC    8,*+6           CHANGE SIGN OF ANSWER
         LCER  FR0,FR0
         FEXIT Y
ERROR1   FEXIT Y,5009
EINT     FEXIT Y,5018
         DS    0D
FOVPI    DC    X'41145F306DC9C883' 4/PI
CH46     DC    X'4600000000000000' UNNORM ZERO FR0 SCALING
B0       DC    X'4310EAA6DA7C98D7' 270.665736
B1       DC    X'C247A531'     -71.645273
A1       DC    X'C1C8F567'     -12.559912
MAX      DC    X'45C90FDB'     PI*2**18
A0       DC    X'42D49493'     212.58037
MIN      DC    X'02145F31'     (4/PI)*2**-252
ONE      DC    X'41100000'
QTAN1    DC    X'00000008'     WHEN EXPN P IS APPENDED, 2**(4P-21)
ATANL    DSECT
OCTNT    DS    F
         DS    0D
TEMP     DS    F
QTAN     DS    F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
