ATN      TITLE 'ATN ROUTINE'
ATN      CSECT
*
*        ARCTANGENT FUNCTION  (SHORT, 2 ENTRY POINTS)
*              1. REDUCE THE CASE TO THE 1ST OCTANT BY USING
*              ATAN(-X)=-ATAN(X), ATAN(1/X)=PI/2-ATAN(X)
*              2. REDUCE FURTHER TO THE CASE /X/ LESS THAN TAN(PI/12)
*              BY ATAN(X)=PI/6+ATAN((X*SQRT3-1)/(X+SQRT3)).
*              3. FOR THE BASIC RANGE (X LESS THAN TAN(PI/12)), USE
*              A CONTINUED FRACTION APPROXIMATION
*              4. ATAN2 ENTRY REQUIRES A PAIR OF ARGUMENTS Y AND X
*              AND COMPUTES ATAN(Y/X).  ANSWER IS IN (-PI,PI)
*
*
GR0      EQU   0               SCRATCH REGISTERS
GR1      EQU   1
GR2      EQU   14
GRA      EQU   1               ARGUMENT POINTER
GRS      EQU   13              SAVE AREA POINTER
GRR      EQU   14              RETURN REGISTER
GRL      EQU   15              LINK REGISTER
FR0      EQU   0               ANSWER REGISTER
FR2      EQU   2               SCRATCH REGISTERS
FR4      EQU   4
FR6      EQU   6
*
         FSTART  AATNL,10,EINT
         STE   FR0,SIGN
         LPER  FR0,FR0         MAIN CIRCUIT.  FORCE SIGN POSITIVE
         SR    GR1,GR1         GR1 TO DENOTE THE SECTION TO WHICH
         CE    FR0,ONE         ANSWER BELONGS.  BREAK POINTS ARE
         BC    12,REDUC        TAN(PI/12), TAN(PI/4), TAN(5PI/12)
         LE    FR2,ONE         IF ARG GREATER THAN 1, TAKE INVERSE
         DER   FR2,FR0         AND CRANK GR1 BY 8
         LER   FR0,FR2
         LA    GR1,8
*
REDUC    CE    FR0,TAN15       IF ARG GREATER THAN TAN(PI/12), REDUCE
         BC    12,OK           THE ARG BY USING
         LER   FR2,FR0
         ME    FR0,RT3M1       ATAN(X) = PI/6+ATAN(Y)
         SE    FR0,ONE         WHERE Y = (X*SQRT3-1)/(X+SQRT3)
         AER   FR0,FR2
         AE    FR2,RT3         COMPUTE X*SQRT3-1 AS X(SQRT3-1)-1+X
         DER   FR0,FR2         TO PROTECT SIGNIFICANT DIGITS
         LA    GR1,4(GR1)      CRANK GR1 BY 4
*
OK       LER   FR4,FR0         NOW MAGNITUDE OF REDUCED ARG IS
         MER   FR0,FR0         LESS THAN TAN(PI/12)=0.26795
         LER   FR2,FR0
         ME    FR0,C           COMPUTE ANGLE BY
         LER   FR6,FR0
         AE    FR2,A           ATAN(X)/X = D-C*XSQ+B/(XSQ+A)
         LE    FR0,B
         DER   FR0,FR2
         SER   FR0,FR6
         AE    FR0,D
         MER   FR0,FR4
         C     GR1,EIGHT       DEPENDING ON THE SECTION TO WHICH
         BC    4,*+6           THE ANSWER BELONGS, ADD OR SUBTRACT
         LCER  FR0,FR0         REDUCED ANSWER FROM A BASE ANGLE
         AE    FR0,ZERO(GR1)
         TM    SIGN,X'80'      SIGN OF ANS SHOULD AGREE WITH SIGN
         BC    8,*+6           OF ARG
         LCER  FR0,FR0
EXIT     FEXIT Y
EINT     FEXIT Y,5016
         DS    0D
ONE      DC    X'41100000'
TAN15    DC    X'40449851'     TAN 15 DEGREES
RT3M1    DC    X'40BB67AF'     SQRT3-1
RT3      DC    X'411BB67B'     SQRT3
EIGHT    DC    F'8'
A        DC    X'41168A5E'     1.4087812
B        DC    X'408F239C'     0.55913709
C        DC    X'3FD35F49'     0.051604543
D        DC    X'409A6524'     0.60310579
ZERO     DC    F'0'
         DC    X'40860A92'     PI/6
PIOV2    DC    X'411921FB'     PI/2
         DC    X'4110C152'     PI/3
MASK1    DC    X'7FFFFFFF'
AATNL    DSECT
SIGN     DS    F
AREA     DSECT
         PRINT OFF
         COPY  AREA
         END
