XNVL     TITLE 'CONVERT LINE NUMBER ROUTINE'
CONVERTL CSECT
*********************************************************************
*
*   THIS ROUTINE CONVERTS AN INTERNALLY CODED LINE NUMBER TO EBCDIC
*   FOR PRINT OUT WITH ERROR MESSAGES.
*
*   ON ENTRY TO THIS ROUTINE REGISTER 1 CONTAINS A POINTER TO AN
*   ARGUMENT LIST.
*        1ST ARGUMENT - POINTER TO 2 HALFWORDS WHICH CONTAIN THE
*                       INTEGER AND FRACTIONAL PORTIONS OF THE LINE
*                       NUMBER. THESE NUMBERS ARE IN BINARY.
*        2ND AGRUMENT - POINTER TO THE 10 BYTE AREA WHERE THE CONVERTED
*                       LINE NUMBER (FOLLOWED BY TWO BLANKS) IS TO BE
*                       PLACED.
*
*   THE LINE NUMBER IS CONVERTED TO THE FORM
*          XXXX.XXX BL BL
*   LEADING AND TRAILING ZEROES ARE NOT PRINTED.
*
*   ON ENTRY TO THIS ROUTINE REGISTER 10 POINTS TO TWORK AND REGISTER
*   11 POINTS AT USER AREA.
*
*********************************************************************
BASER    EQU   9                   BASE REGISTER
         XAVE  CONVERTL
         USING TWORK,WORK
         L     LB,0(R1)            PICK UP LOC OF BINARY LINE NO.
         L     LEB,4(R1)           PICK UP LOC WHERE CONVERTED LINE NO.
         L     R6,8(R1)            IS TO GO
         L     AR,0(LB)      PICK UP 1ST HALF OF LINE NO. PRS 12/1/75
         CVD   AR,PAREA            CONVERT TO PACKED DECIMAL
         UNPK  0(7,LEB),PAREA+5(4) UNPACK 5 DIGITS    PRS 12/2/75
         LH    AR,4(LB)      PICK UP 2ND HALF OF LINE NO. PRS 12/1/75
         CVD   AR,PAREA            CONVERT TO PACKED DECIMAL
         UNPK  6(5,LEB),PAREA+6(3) UNPACK FRACTIONAL PART. PRS 12/2/75
         MVI   5(LEB),C'.'         PUT IN THE PERIOD     PRS 12/2/75
         LA    AR,3                SET LEADING ZERO COUNTER
         LR    BR,LEB              SET POINTER TO 1ST DIGIT OF LINE NO.
NEXT     CLI   0(BR),C'0'          IS DIGIT ZERO?
         BNE   LAST0               NO
         MVI   0(BR),C' '          YES - CHANGE TO BLANK
         LA    BR,1(BR)            BUMP POINTER
         BCT   AR,NEXT             ARE THERE MORE?
LAST0    LA    AR,8(LEB)           POINT TO LAST DIGIT OF LINE NO. PRS
NEXT0    CLI   0(AR),C'0'          IS DIGIT 0?
         BNE   BACK                NO-RETURN TO CALLER
         MVI   0(AR),C' '          YES - CHANGE TO BLANK
         BCT   AR,NEXT0            REDUCE TRAILING ZERO POINTER
BACK     MVI   8(LEB),C' '         FILL IN THE LAST 2 BYTES OF THE
         MVI   9(LEB),C' '         OUTPUT BUFFER WITH BLANKS
         MVC   0(9,R6),1(LEB)     MOVE LINE # TO BUFFER. PRS 12/2/75
         MVI   9(R6),C' '         FILL WITH A BLANK.     PRS 12/2/75
         L     13,4(0,13)         GET BACK CONTROLS SAVE AREA
         LM    14,12,12(13)        RESTORE REGISTERS
         BR    14
*
R1       EQU   1                   CONTAINS ADDRESS OF ARG LIST
LB       EQU   2                   POINTER TO BINARY LINE #
LEB      EQU   3                   POINTER TO WHERE CONVERTED LINE #
*                                  IS TO GO
AR       EQU   4                   TEMPORARY REGISTER
BR       EQU   5                   TEMPORARY REGISTER
WORK     EQU   10                  POINTER TO TWORK
SAVER    EQU   13                  SAVE AREA POINTER
R6       EQU  6
         EJECT
TWORK    DSECT
         COPY  TWORK
*
         END
