PUTI   TITLE  'PUTIT ROUTINE'
PUTIT  CSECT
***********************************************************************
*                                                                     *
*                 WYLBUR/BASIC PUTIT PROGRAM                        *
*                       BATCH VERSION                                 *
*                                                                     *
***********************************************************************
*
*REGISTER USAGE
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                 BASE FOR 'CONTROL' WORK AREA
R11      EQU   11                 BASE OF USER AREA
R12      EQU   12                 BASE FOR PUTIT
R13      EQU   13                 POINTER TO SAVE AREA
R14      EQU   14
R15      EQU   15
*
BASER    EQU   12                 BASE FOR PUTIT
*
*OS ENTRY LINKAGE (TYPE 3: DEPENDENT)
*
         SAVE  (14,12),,*
         BALR  BASER,0            ESTABLISH ADDRESSABILITY FOR
         USING *,BASER             CALLED
         LR    3,13               COPY CALLERS SAVE AREA ADDRESS
         LA    13,72(0,13)        LOAD ADDRESS OF CALLEDS SAVE AREA
         ST    13,8(0,3)          PUT CALLEDS SAVE AREA ADDRESS
*                                  IN CALLERS SAVE AREA
         ST    3,4(0,13)          PUT CALLERS SAVE AREA ADDRESS
*                                  IN CALLEDS SAVE AREA
         B     GO                 BRANCH AROUND ADDRESS CONSTANTS
*
*ADDRESS CONSTANTS FOR EXTERNAL CONTROL SECTIONS (V TYPE IF ANY)
*
APUTLINE DC    V(PUTLINE)
ACWORK   DC    V(CWORK)
*
*SET UP FOR CONTROL PROGRAMS WORK AREA
*
GO       EQU   *
         L     R10,ACWORK         SET ADDR TO CWORK
         USING CWORK,10           TELL ASSEMBLER ABOUT CWORK
         USING AREA,11            TELL ASSEMBLER ABOUT AREA
*
*
*
*
*
*
***********************************************************************
*                                                                     *
*                 THE CODE GOES HERE                                  *
*                                                                     *
***********************************************************************
         LTR   R0,R0              WHAT IS THE ACTION REQUESTED
         BL    PAGE               WANTS A SKIP TO TOP OF PAGE
         BE    BLANK              WANTS A BLANK LINE NO MSG
*
***************SET IN CARRIAGE CONTROL FOR SINGLE SPACE****************
*
         MVI   CCOUT,X'40'        SET IN SINGLE SPACE CARRIAGE CONTROL
*
***************PROCESS MESSAGE TO OUTPUT BUFFER************************
*
SPACE    C     R0,=F'130'         CHECK THAT MESSAGE IS NOT TOO LONG
         BNH   OUT1               IF NOT SKIP OVER FORCEED LENGTH
         LA    R0,130(R0,R0)      FORCE MESSAGE TO MAXIMUM LENGTH
OUT1     BCTR  R0,R0              BRING DOWN TO MACHINE MESSAGE SIZE
         LR    R2,R0              PUT IN REG THAT CAN BE USED AS INDEX
         EX    R2,DMOVE1          MOVE MESSAGE TO SYSOUT BUFFER
*
*****************PAD BUFFER TO 130 CHARACTER MSG LENGTH****************
*
         CL    R0,=F'129'         CHECK IF MSG WAS FULL LENGTH
         BE    OUTPUT             IF IT WAS GO OUTPUT, NO PAD NEEDED
         LA    R3,SYSOUT+1(R2)    GET ADDR START OF AREA AFTER MSG
         LA    R4,SYSOUT+129      GET ADDR OF END OF THIS PAD AREA
         SR    R4,R3              NUMBER OF CHARACTERS IN IT - 1
         MVI   0(R3),X'40'        MOVE BLANK INTO FIRST POSITION
         LTR   R4,R4              TEST MORE THAN ONE PAD CHAR NEEDED
         BE    OUTPUT             ONLY 1 BLANK TO PAD SO GO OUTPUT
         EX    R4,DMOVE2          MOVE IN REST OF BLANK PAD
*
*********************OUTPUT BY CALLING PUTLINE*************************
*
OUTPUT   L     R15,APUTLINE       GET PUTLINES ADDR
         LA    R1,CCOUT           PUTLINE WANTS ADDR CARRIAGE CONTROL
         BALR  14,15              GO WRITE IT OUT
         B     RETURN             NOTHING MORE TO DO
*
*********************MVC FOR MOVING MESSAGE TO OUTPUT BUFFER***********
*
DMOVE1   MVC   SYSOUT(0),0(R1)    MOVE MESSAGE TO SYSOUT BUFFER
*
*********************MVC TO PAD REMAINDER OF BUFFER********************
*
DMOVE2   MVC   1(0,R3),0(R3)      PAD REMAINDER WITH BLANKS
*
*********************PREPROCESS SKIP TO TOP OF PAGE********************
*
PAGE     MVI   CCOUT,X'F1'        PUT PAGE SKIP CODE IN CAR CONTRL
         LCR   R0,R0              MAKE A POSITIVE MSG LENGTH
         B     SPACE              GO MOVE AND WRITE THE MSG
***************ALL THAT IS WANTED IS A BLANK LINE**********************
*
BLANK    MVI   CCOUT,X'40'        SET IN SINGLE SPACE
         MVC   SYSOUT(130),CCOUT  MAKE A BLANK MESSAGE
         B     OUTPUT
*
*RETURN TO CALLER
*
RETURN   L     13,4(0,13)         PUT ADDR OF CALLERS SAVE AREA BACK
         LM    14,12,12(13)       RESTORE CALLERS REGISTERS
         BCR   15,14              RETURN TO CALLER
*
         LTORG
*
         EJECT
CWORK    DSECT
         COPY CWORK
         EJECT
AREA     DSECT
         COPY AREA
         END
