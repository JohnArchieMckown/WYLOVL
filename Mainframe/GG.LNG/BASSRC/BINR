BINR     TITLE 'BINARY SEARCH ROUTINE'
*
BINR     CSECT
         USING *,BASE
*
*        THIS ROUTINE IS CALLED WITH REGISTER ZERO CONTAINING A HALF-
*        WORD STATEMENT NUMBER TO BE LOOKED UP IN A TABLE, AND
*        REGISTER ONE CONTAINING A POINTER TO THE 'BEG' POINTER OF
*        THE TABLE TO BE SEARCHED.  THE VALUE OF THIS ROUTINE IS A
*        POINTER STORED IN REGISTER ONE OF THE FOUND ITEM, OR A
*        POINTER TO WHERE THE ITEM SHOULD BE INSERTED WITH A HIGH
*        ORDER 1 BIT ON.
*
*    IF R0 COMES IN NEGATIVE IT IS COMPLETENTED AND THE VALUE
*    WHICH IS NORMALLY A DISPLACEMENT FROM THE USER AREA IS
*    RETURNED AS A DISPLACEMENT FROM THE BEGINNING OF THE TABLE
*    BEING SEARCHED.
*
         SAVE  (14,12),,*
          SR   SW,SW
         SR    CHK,CHK
         LTR   0,0
         BP    ELEN
DORT     LCR   0,0
         L     CHK,0(R1)
ELEN     EQU   *
*                                  PICK UP RANGE ON SEARCH IN REGS.
         LM    LO,HI,0(R1)         LO AND HI (REL. TO USER)
         LA    HI,0(LO,HI)
         LA    MIDN,4
         SR    LO,MIDN
*
*
*        MID=(HI+LO)/2 IS GOTTEN BY ADDING HI AND LO
*        AND THEN DOING A SRL MID,3 THEN A SLL MID,2
*        THIS EFFECTIVELY DIVIDES HI PLUS LO BY TWO AND
*        ROUNDS TO AN EVEN MULTIPLE OF 4.
*
*        LO BEGINS ONE WORD BELOW THE BEGINNING OF THE SEARCH
*        HI BEGINS ONE WORD BEYOND THE END OF THE SEARCH
*        (HI ALREADY 1 WORD ABOVE)
*        THIS IS TO COMPENSATE FOR THE END POINTS OF THE SEARCH
*
TEST     LA    MID,0(LO,HI)
         LA    MIDN,4(LO)
         CR    MIDN,HI
         BE    NOTFOUND
         SRL   MID,3
         SLL   MID,2
CMPR     CH    0,0(MID,USER)
         BL    LESS
         BE    FOUND
         LR    LO,MID
         B     TEST
LESS     LR    HI,MID
         B     TEST
NOTFOUND LR    MID,HI
         L     SW,HI1
FOUND    LR   R1,MID
         SR    R1,CHK
         OR    R1,SW
         LM    14,15,12(13)
         LM    2,12,28(13)
         BR    14
R1       EQU   1
WK1      EQU   2
BASE     EQU   15
LO       EQU   4
HI       EQU   5
MID      EQU   6
MIDN     EQU   7
SW       EQU   9
CHK      EQU   10
USER     EQU   11
         DS    0F
HI1      DC    X'80000000'
         END
