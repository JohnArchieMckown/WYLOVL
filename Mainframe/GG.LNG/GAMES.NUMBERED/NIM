REM WYL.GA.PAB.BASIC.GAMES#NIM, CREATED 04/14/76                           1.
DIM B(50,11),Z(50),D(2)                                                   10.
MAT Z = ZER                                                               20.
MAT B = ZER                                                               30.
MAT D = ZER                                                               40.
Z2=-1                                                                     50.
PRINT                                                                     60.
PRINT "THE CAMPUS FACILITY OF THE STANFORD UNIVERSITY ";                  70.
PRINT "COMPUTATION CENTER WELCOMES YOU TO PLAY 'NIM'."                    75.
PRINT                                                                     80.
PRINT "IF YOU WISH INSTRUCTIONS, TYPE '1'. IF NOT, TYPE '0'."             90.
PRINT "(DO NOT FORGET TO PRESS THE 'RETURN' KEY AFTER THIS ";            100.
PRINT "AND ALL OTHER RESPONSES TO QUESTIONS YOU ARE ASKED.)"             110.
PRINT "WHICH (0 OR 1) FOR INSTRUCTIONS? : ";                             120.
INPUT Z1                                                                 130.
IF Z1=0 THEN 270                                                         140.
PRINT                                                                    150.
PRINT "THE GAME IS PLAYED WITH A NUMBER OF PILES OF OBJECTS. ";          160.
PRINT "ANY NUMBER OF OBJECTS ARE REMOVED FROM ONE PILE BY YOU AND"       170.
PRINT "ME ALTERNATELY. YOU MUST SPECIFY WHETHER WINNING ";               180.
PRINT "IS DEFINED AS TAKING OR NOT TAKING THE LAST OBJECT, THE"          190.
PRINT "NUMBER OF PILES IN THE GAME, AND HOW MANY OBJECTS ARE ";          200.
PRINT "ORIGINALLY IN EACH PILE. EACH PILE MAY CONTAIN A"                 210.
PRINT "DIFFERENT NUMBER OF OBJECTS."                                     220.
PRINT                                                                    230.
PRINT "I WILL SHOW MY MOVE BY LISTING EACH PILE AND THE ";               240.
PRINT "NUMBER OF OBJECTS REMAINING IN THE PILES AFTER EACH OF MY ";      250.
PRINT "MOVES."                                                           260.
PRINT                                                                    270.
PRINT "ENTER WIN OPTION -- 1 TO TAKE LAST, 2 TO AVOID LAST:";            280.
INPUT W                                                                  290.
IF W = 1 THEN 330                                                        300.
IF W<>2 THEN 280                                                         310.
PRINT                                                                    320.
PRINT "ENTER NUMBER OF PILES: ";                                         330.
INPUT N                                                                  340.
IF N>50 THEN 330                                                         350.
IF N<1 THEN 330                                                          360.
IF N<>INT(N) THEN 330                                                    370.
PRINT "ENTER PILE SIZES"                                                 380.
FOR I=1 TO N                                                             390.
PRINT "PILE ";I;"= ";                                                    400.
INPUT A(I)                                                               410.
IF A(I)>1000 THEN 460                                                    420.
IF A(I)<1 THEN 460                                                       430.
IF A(I)<>INT(A(I)) THEN 460                                              440.
GOTO 500                                                                 450.
PRINT                                                                    460.
PRINT "AGAIN, PLEASE . . ."                                              470.
PRINT                                                                    480.
GOTO 400                                                                 490.
NEXT I                                                                   500.
PRINT                                                                    510.
PRINT                                                                    520.
IF W=1 THEN 790                                                          530.
C=0                                                                      540.
FOR I=1 TO N                                                             550.
IF A(I)=0 THEN 600                                                       560.
C=C+1                                                                    570.
IF C=3 THEN 690                                                          580.
D(C)=I                                                                   590.
NEXT I                                                                   600.
IF C=2 THEN 770                                                          610.
IF A(D(1))>1 THEN 660                                                    620.
PRINT "I LOSE. CONGRATULATIONS."                                         630.
PRINT                                                                    640.
STOP                                                                     650.
PRINT "I WIN! PLAY AGAIN SOMETIME."                                      660.
PRINT                                                                    670.
STOP                                                                     680.
C=0                                                                      690.
FOR I=1 TO N                                                             700.
IF A(I)>1 THEN 790                                                       710.
IF A(I)=0 THEN 740                                                       720.
C=C+1                                                                    730.
NEXT I                                                                   740.
IF C/2<>INT(C/2) THEN 630                                                750.
GO TO 790                                                                760.
IF A(D(1))=1 THEN 660                                                    770.
IF A(D(2))=1 THEN 660                                                    780.
FOR I=1 TO N                                                             790.
E=A(I)                                                                   800.
FOR J=0 TO 10                                                            810.
F=E/2                                                                    820.
B(I,J+1)=2*(F-INT(F))                                                    830.
E=INT(F)                                                                 840.
NEXT J                                                                   850.
NEXT I                                                                   860.
FOR J=10 TO 0 STEP -1                                                    870.
C=0                                                                      880.
H=0                                                                      890.
FOR I=1 TO N                                                             900.
IF B(I,J+1)=0 THEN 960                                                   910.
C=C+1                                                                    920.
IF A(I)<=H THEN 960                                                      930.
H=A(I)                                                                   940.
G=I                                                                      950.
NEXT I                                                                   960.
IF C/2<>INT(C/2) THEN 1040                                               970.
NEXT J                                                                   980.
E=INT(N*RND(Z2)+1)                                                       990.
IF A(E)=0 THEN 990                                                      1000.
F=INT(A(E)*RND(Z2)+1)                                                   1010.
A(E)=A(E)-F                                                             1020.
GO TO 1230                                                              1030.
A(G)=0                                                                  1040.
FOR J=0 TO 10                                                           1050.
B(G,J+1)=0                                                              1060.
C=0                                                                     1070.
FOR I=1 TO N                                                            1080.
IF B(I,J+1)=0 THEN 1110                                                 1090.
C=C+1                                                                   1100.
NEXT I                                                                  1110.
A(G)=A(G)+2*(C/2-INT(C/2))*2**J                                         1120.
NEXT J                                                                  1130.
IF W=1 THEN 1230                                                        1140.
C=0                                                                     1150.
FOR I=1 TO N                                                            1160.
IF A(I)>1 THEN 1230                                                     1170.
IF A(I)=0 THEN 1200                                                     1180.
C=C+1                                                                   1190.
NEXT I                                                                  1200.
IF C/2<>INT(C/2) THEN 1230                                              1210.
A(G)=1-A(G)                                                             1220.
PRINT                                                                   1230.
PRINT "PILE SIZE"                                                       1240.
FOR I=1 TO N                                                            1250.
PRINT I;A(I)                                                            1260.
NEXT I                                                                  1270.
IF W=2 THEN 1310                                                        1280.
GOSUB 1490                                                              1290.
IF Z1=1 THEN 660                                                        1300.
PRINT                                                                   1310.
PRINT "YOUR MOVE: PILE, NUMBER TO BE REMOVED = ";                       1320.
INPUT X,Y                                                               1330.
IF X>N THEN 1410                                                        1340.
IF X<1 THEN 1410                                                        1350.
IF X<>INT(X) THEN 1410                                                  1360.
IF Y>A(X) THEN 1410                                                     1370.
IF Y<1 THEN 1410                                                        1380.
IF Y<>INT(Y) THEN 1410                                                  1390.
GOTO 1450                                                               1400.
PRINT                                                                   1410.
PRINT "AGAIN, PLEASE . . ."                                             1420.
PRINT                                                                   1430.
GOTO 1310                                                               1440.
A(X)=A(X)-Y                                                             1450.
GOSUB 1490                                                              1460.
IF Z1=1 THEN 630                                                        1470.
GO TO 530                                                               1480.
Z1=0                                                                    1490.
FOR I=1 TO N                                                            1500.
IF A(I)=0 THEN 1530                                                     1510.
RETURN                                                                  1520.
NEXT I                                                                  1530.
Z1=1                                                                    1540.
RETURN                                                                  1550.
END                                                                     1560.
