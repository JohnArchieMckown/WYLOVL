REM WYL.GT.ATG.LIB1#SPEECH, CREATED 10/30/78                               1.
REMARK THIS PROGRAM CAME FROM INTERFACE AGE                               10.
REMARK OF JANUARY 1978, ARTICLE COMPUTER(ESE) SPEECH WRITER               20.
REMARK BY ASHOK NAGRANI                                                   30.
REMARK                                                                    40.
REMARK EXTENSIVE MODS WERE DONE BECAUSE THE ORIGINAL PROGRAM              50.
REMARK ALLOWED MORE THAN ONE CHARACTER NAMES AND FURTHERMORE              60.
REMARK ALLOWED + TO MEAN CONCATENATION WHEN USED WITH STRINGS             70.
REMARK                                                                    80.
REMARK ALSO THE DEFINITIONS WERE MOVED OUT OF THE GENERATION LOOP         90.
REMARK                                                                   100.
PRINT '************ S P R E G R *********************'                   110.
DIM R$(20),A$(10),B$(10),C$(10),D$(10),E$(10)                            120.
DIM F$(10),G$(10),H$(10),I$(10),J$(10),K$(10)                            130.
DIM L$(10),M$(10),N$(10)                                                 140.
DIM C(6)                                                                 145.
PRINT                                                                    150.
PRINT                                                                    160.
PRINT'PROGRAM GENERATES AUTOMATICALLY A REPORT OR SPEECH'                170.
PRINT'FOR ANY LENGTH AND ANY OCCASION.'                                  180.
PRINT                                                                    190.
PRINT                                                                    200.
PRINT'ALL YOU NEED TO DO IS TO INPUT THE LENGTH OF THE '                 210.
PRINT'REPORT DESIRED AND THE SUBJECT OF THE REPORT'                      220.
REMARK THE FOLLOWING WAS IN THE ORIGNAL ARTICLE BUT I DON'T              230.
REMARK UNDERSTAND SO I HAVE COMMENTED IT OUT                             240.
REMARK FOR I=1 TO 1200                                                   250.
REMARK NEXT I                                                            260.
PRINT                                                                    270.
PRINT                                                                    280.
PRINT'THE PROGRAM WILL MAKE THE TEXT AVAILABLE TO YOU'                   290.
PRINT'FREEING YOU FOR OTHER IMPORTANT MANAGERIAL TASKS.'                 300.
REMARK THE FOLLOWING WAS IN THE ORIGNAL ARTICLE BUT I DON'T              310.
REMARK UNDERSTAND SO I HAVE COMMENTED IT OUT                             320.
REMARK FOR I = 1 TO 600                                                  330.
REMARK NEXT I                                                            340.
PRINT                                                                    350.
DEF FNA(X)=INT(10*RND(X)+1)                                              360.
DEF FNB(X)=INT(10*RND(X)+1)                                              370.
DEF FNC(X)=INT(10*RND(X)+1)                                              380.
DEF FND(X)=INT(5*RND(X)+1)                                               390.
DEF FNE(X)=INT(5*RND(X)+1)                                               400.
DEF FNF(X)=INT(5*RND(X)+1)                                               410.
REMARK A$ DEFINITIONS                                                    420.
A$(1)='ALTHOUGH '                                                        430.
A$(2)='IN THE PAST '                                                     440.
A$(3)='PER PAST PRACTISE '                                               450.
A$(4)='SINCE '                                                           460.
A$(5)='WHILE '                                                           470.
A$(6)='IN SPITE OF THE FACT THAT '                                       480.
A$(7)='IN SPITE OF THE WIDELY HELD BELIEF THAT '                         490.
A$(8)='DUE TO TECHNICAL REASONS '                                        500.
A$(9)='BECAUSE OF THE FACT THAT '                                        510.
A$(10)='BECAUSE OF UNFORESEEABLE CIRCUMSTANCES '                         520.
REMARK B$ DEFINITIONS                                                    530.
B$(1)='INTEGRATED '                                                      540.
B$(2)='TOTAL '                                                           550.
B$(3)='SYSTEMATIZED '                                                    560.
B$(4)='PARALLEL '                                                        570.
B$(5)='FUNCTIONAL '                                                      580.
B$(6)='OPTICAL '                                                         590.
B$(7)='SYNCHRONIZED '                                                    600.
B$(8)='COMPATIBLE '                                                      610.
B$(9)='BALANCED '                                                        620.
B$(10)='RESPONSIVE '                                                     630.
REM C$ DEFINITIONS                                                       640.
C$(1)='MANAGEMENT '                                                      650.
C$(2)='ORGANIZATIONAL '                                                  660.
C$(3)='MONITORED '                                                       670.
C$(4)='RECIPROCAL '                                                      680.
C$(5)='DIGITAL '                                                         690.
C$(6)='LOGIC '                                                           700.
C$(7)='TRANSITIONAL '                                                    710.
C$(8)='INCREMENTAL '                                                     720.
C$(9)='THIRD GENERATION '                                                730.
C$(10)='POLICY '                                                         740.
REM D$ DEFINITIONS                                                       750.
D$(1)='OPTION '                                                          760.
D$(2)='FLEXIBILITY '                                                     770.
D$(3)='CAPACITY '                                                        780.
D$(4)='MOBILITY '                                                        790.
D$(5)='PROGRAMMING '                                                     800.
D$(6)='CONCEPT '                                                         810.
D$(7)='TIME PHASE '                                                      820.
D$(8)='PROJECTION '                                                      830.
D$(9)='HARDWARE '                                                        840.
D$(10)='CONTINGENCY '                                                    850.
REM E$ DEFINITIONS                                                       860.
E$(1)='HAS BEEN '                                                        870.
E$(2)='USED TO BE '                                                      880.
E$(3)='WAS GENERALLY GEARED TO '                                         890.
E$(4)='CONCERNED ITSELF WITH '                                           900.
E$(5)='CONFIGURED ITSELF AROUND '                                        910.
E$(6)='HAS NOT BEEN '                                                    920.
E$(7)='DID NOT CONCERN ITSELF WITH '                                     930.
E$(8)='WAS GENERALLY NOT GEARED TO '                                     940.
E$(9)='WAS TEMPORARILY INEFFECTIVE IN '                                  950.
E$(10)='FINDS ITSELF '                                                   960.
REM F$ DEFINITIONS                                                       970.
F$(1)='ADVANCING THE STATE OF THE ART '                                  980.
F$(2)='LOWERING THE LEARNING CURVE '                                     990.
F$(3)='MAKING FANTASTIC PROGRESS '                                      1000.
F$(4)='GENERATING MANAGEMENT ATTENTION '                                1010.
F$(5)='CREATING REQUIRED AWARENESS '                                    1020.
F$(6)='CREATING THE REQUIRED DATA BASE '                                1030.
F$(7)='RESOLVING PERIPHERAL PROBLEMS '                                  1040.
F$(8)='CREATING NECESSARY TECHNICAL BACKSTOPS '                         1050.
F$(9)='RAISING THE LEVEL OF MANAGEMENT CONSCIOUSNESS '                  1060.
F$(10)='FORMULATING SPECIFIC PLANS '                                    1070.
REM G$ DEFINITIONS                                                      1080.
G$(1)='WILL FUNCTION AS '                                               1090.
G$(2)='WILL NOT FUNCTION AS '                                           1100.
G$(3)='WILL CAUSE '                                                     1110.
G$(4)='WILL REQUIRE '                                                   1120.
G$(5)='WILL NEGATE '                                                    1130.
G$(6)='WILL PROVE '                                                     1140.
G$(7)='WILL SUPPORT '                                                   1150.
G$(8)='WILL COMPLEMENT '                                                1160.
G$(9)='WILL SUPPLEMENT '                                                1170.
G$(10)='WILL ALLEVIATE '                                                1180.
REM H$ DEFINITIONS                                                      1190.
H$(1)='IS A '                                                           1200.
H$(2)='IS NOT A '                                                       1210.
H$(3)='IS LIKELY TO BE A '                                              1220.
H$(4)='WILL NOT PROVE TO BE A '                                         1230.
H$(5)='REMAINS A '                                                      1240.
H$(6)='HAS CAUSED A '                                                   1250.
H$(7)='HAS NOT CAUSED A '                                               1260.
H$(8)='CAN AT SOME TIME BECOME '                                        1270.
H$(9)='WILL NOT REMAIN A '                                              1280.
H$(10)='CANNOT BECOME A '                                               1290.
REM I$ DEFINITIONS                                                      1300.
I$(1)='VIABLE '                                                         1310.
I$(2)='RESPONSIBLE '                                                    1320.
I$(3)='FEASIBLE '                                                       1330.
I$(4)='OPTIMAL '                                                        1340.
I$(5)='MANAGEMENT '                                                     1350.
I$(6)='TECHNICAL '                                                      1360.
I$(7)='ANALOGOUS '                                                      1370.
I$(8)='INTERACTIVE '                                                    1380.
I$(9)='INTERPRETIVE '                                                   1390.
I$(10)='EARLY '                                                         1400.
REM J$ DEFINITIONS                                                      1410.
J$(1)='ALTERNATE '                                                      1420.
J$(2)='SOLUTION '                                                       1430.
J$(3)='RECOGNITION '                                                    1440.
J$(4)='VARIABLE '                                                       1450.
J$(5)='OPTION '                                                         1460.
J$(6)='CONSIDERATION '                                                  1470.
J$(7)='CONCLUSION '                                                     1480.
J$(8)='PATTERN '                                                        1490.
J$(9)='ARGUMENT '                                                       1500.
J$(10)='CONCEPT '                                                       1510.
REM K$ DEFINITIONS                                                      1520.
K$(1)='WE ARE '                                                         1530.
K$(2)='COMPETITION IS '                                                 1540.
K$(3)='INDUSTRY TRENDS ARE '                                            1550.
K$(4)='FOREIGN RESOURCES ARE '                                          1560.
K$(5)='RECENT TECHNOLOGICAL ADVANCES ARE '                              1570.
K$(6)='OUR INNOVATIVE APPROACH IS '                                     1580.
K$(7)='OUR RESEARCH IS '                                                1590.
K$(8)='COMPANY POLICY IS '                                              1600.
K$(9)='UPPER MANAGEMENT IS '                                            1610.
K$(10)='NEAR TERM DEVELOPMENTS ARE '                                    1620.
REM L$ DEFINITIONS                                                      1630.
L$(1)='I-CAM '                                                          1640.
L$(2)='CAM-I-APP '                                                      1650.
L$(3)='U-CAM '                                                          1660.
L$(4)='V-CAM '                                                          1670.
L$(5)='ROM '                                                            1680.
L$(6)='CADAM '                                                          1690.
L$(7)='R2-D2 '                                                          1700.
L$(8)='CP-3O '                                                          1710.
L$(9)='ROBOTICS '                                                       1720.
L$(10)='NAKED MEMORY '                                                  1730.
REM M$ DEFINITIONS                                                      1740.
M$(1)='PATTERNS '                                                       1750.
M$(2)='SOLUTIONS '                                                      1760.
M$(3)='VARIABLES '                                                      1770.
M$(4)='APPROACHES '                                                     1780.
M$(5)='METHODOLOGIES '                                                  1790.
M$(6)='ENGINEERING AIDS '                                               1800.
M$(7)='OPTIONS '                                                        1810.
M$(8)='ARGUMENTS '                                                      1820.
M$(9)='CONCEPTS '                                                       1830.
M$(10)='TECHNOLOGIES '                                                  1840.
REMARK THE ORIGINAL DIDN'T SEEM TO NEED INITIALIZATION BUT I DO         1850.
Z$=''                                                                   1860.
Y$=''                                                                   1870.
X$=''                                                                   1880.
W$=''                                                                   1890.
V$=''                                                                   1900.
S$=CLK(0)                                                               1901.
U$=RST(S$,1)                                                            1902.
A=CHN(U$)-190                                                           1903.
S$=LST(S$,7)                                                            1904.
U$=RST(S$,1)                                                            1905.
B=CHN(U$)-190                                                           1906.
S$=LST(S$,5)                                                            1907.
U$=RST(S$,1)                                                            1908.
C=CHN(U$)-190                                                           1909.
S$=LST(S$,4)                                                            1910.
U$=RST(S$,1)                                                            1911.
D=CHN(U$)-190                                                           1912.
S$=LST(S$,2)                                                            1913.
U$=RST(S$,1)                                                            1914.
E=CHN(U$)-190                                                           1915.
S$=LST(S$,1)                                                            1916.
F=CHN(S$)-190                                                           1917.
PRINT                                                                   1970.
REMARK THE FOLLOWING STATMENT WAS 200 IN THE ORIGINAL                   1980.
 PRINT'INPUT SPEECH LENGTH IN MINUTES OR # OF PARAGRAPHS IN REPORT'     1990.
INPUT X                                                                 2000.
PRINT'INPUT SUBJECT OF REPORT'                                          2010.
INPUT S$                                                                2020.
IF S$='0' THEN GOTO 1990                                                2030.
PRINT                                                                   2040.
PRINT '          ', S$                                                  2050.
PRINT                                                                   2055.
FOR I=1 TO X                                                            2060.
REMARK THE FOLLOWING STATEMENT WAS LABELED 600 IN THE SOURCE            2070.
 R$(1)=Z$                                                               2080.
R$(2)=Y$                                                                2090.
R$(3)=X$                                                                2100.
R$(4)=W$                                                                2110.
R$(5)=V$                                                                2120.
REMARK HIS RANDOM NUMBER GENERATOR AUTOMATICALLY BUMPED BUTS            2130.
REMARK OURS DOESN'T, SIGH                                               2140.
A=RND(A)                                                                2150.
B=RND(A)                                                                2160.
C=RND(A)                                                                2170.
D=RND(A)                                                                2180.
E=RND(A)                                                                2190.
F=RND(A)                                                                2200.
REMARK THIS IS WHERE WE DO THE WORK                                     2210.
U$=C$(FNB(A))                                                           2220.
T$=D$(FNC(C))                                                           2230.
T$=CAT(U$,T$)                                                           2240.
U$=B$(FNA(A))                                                           2250.
N$(1)=CAT(U$,T$)                                                        2260.
REMARK  COMPOSE STRING B9$ IN LINE 1401                                 2270.
U$=C$(FNB(B))                                                           2280.
T$=D$(FNC(A))                                                           2290.
T$=CAT(U$,T$)                                                           2300.
U$=B$(FNA(B))                                                           2310.
N$(2)=CAT(U$,T$)                                                        2320.
REMARK  COMPOSE STRING F9$ IN LINE 1402                                 2330.
U$=C$(FNB(C))                                                           2340.
T$=D$(FNC(B))                                                           2350.
T$=CAT(U$,T$)                                                           2360.
U$=B$(FNA(C))                                                           2370.
N$(3)=CAT(U$,T$)                                                        2380.
REMARK  COMPOSE STRING G9$ IN LINE 1403                                 2390.
U$=C$(FNB(D))                                                           2400.
T$=D$(FNC(C))                                                           2410.
T$=CAT(U$,T$)                                                           2420.
U$=B$(FNA(D))                                                           2430.
N$(4)=CAT(U$,T$)                                                        2440.
REMARK  COMPOSE STRING H9$ IN LINE 1404                                 2450.
U$=C$(FNB(E))                                                           2460.
T$=D$(FNC(D))                                                           2470.
T$=CAT(U$,T$)                                                           2480.
U$=B$(FNA(E))                                                           2490.
N$(5)=CAT(U$,T$)                                                        2500.
REMARK  COMPOSE STRING J9$ IN LINE 1405                                 2510.
U$=C$(FNB(F))                                                           2520.
T$=D$(FNC(E))                                                           2530.
T$=CAT(U$,T$)                                                           2540.
U$=B$(FNA(F))                                                           2550.
N$(6)=CAT(U$,T$)                                                        2560.
REMARK  COMPOSE STRING K9$ IN LINE 1406                                 2570.
U$=C$(FNB(A))                                                           2580.
T$=D$(FNC(F))                                                           2590.
T$=CAT(U$,T$)                                                           2600.
U$=B$(FNA(A))                                                           2610.
N$(7)=CAT(U$,T$)                                                        2620.
REMARK  COMPOSE STRING L9$ IN LINE 1407                                 2630.
REMARK SENTENCE #1                                                      2640.
REMARK BUILD Z$ TO GET THE RESULT SHOWN  IN 1412                        2650.
T$=N$(3)                                                                2660.
U$=CAT(T$,', ')                                                         2670.
T$=G$(FNA(B))                                                           2680.
U$=CAT(T$,U$)                                                           2690.
T$=N$(2)                                                                2700.
T$=CAT(T$,U$)                                                           2710.
U$=CAT('BUT ',T$)                                                       2720.
T$=C$(FNA(C))                                                           2730.
U$=CAT(T$,U$)                                                           2740.
T$=B$(FNA(D))                                                           2750.
U$=CAT(T$,U$)                                                           2760.
T$=N$(1)                                                                2770.
U$=CAT(T$,U$)                                                           2780.
T$=A$(FNA(E))                                                           2790.
Z$=CAT(T$,U$)                                                           2800.
REMARK NOW BUILD SENTENCE NUMBER 2                                      2810.
U$=J$(FNB(B))                                                           2820.
U$=CAT(U$,', ')                                                         2830.
T$=I$(FNB(C))                                                           2840.
U$=CAT(T$,U$)                                                           2850.
T$=H$(FNB(D))                                                           2860.
U$=CAT(T$,U$)                                                           2870.
U$=CAT(' ',U$)                                                          2880.
Y$=CAT(S$,U$)                                                           2890.
REMARK NOW BUILD SENTENCE NUMBER 3                                      2900.
T$=N$(4)                                                                2910.
U$=CAT(T$,', ')                                                         2920.
U$=CAT('DUE TO A ',U$)                                                  2930.
T$=F$(FNC(A))                                                           2940.
U$=CAT(T$,U$)                                                           2950.
T$=K$(FNC(B))                                                           2960.
X$=CAT(T$,U$)                                                           2970.
REMARK NOW BUILD SENTENCE 4                                             2980.
U$=N$(5)                                                                2990.
U$=CAT(U$,', ')                                                         3000.
U$=CAT('ARE ',U$)                                                       3010.
T$=M$(FNA(F))                                                           3020.
U$=CAT(T$,U$)                                                           3030.
T$=L$(FNA(A))                                                           3040.
W$=CAT(T$,U$)                                                           3050.
REMARK NOW BUILD SENTENCE 5                                             3060.
U$=N$(6)                                                                3070.
U$=CAT(U$,', ')                                                         3080.
U$=CAT('BECAUSE OF ',U$)                                                3090.
T$=J$(FNA(B))                                                           3100.
U$=CAT(T$,U$)                                                           3110.
T$=I$(FNC(C))                                                           3120.
U$=CAT(T$,U$)                                                           3130.
U$=CAT('AN ',U$)                                                        3140.
T$=G$(FNB(E))                                                           3150.
U$=CAT(T$,U$)                                                           3160.
T$=L$(FNB(F))                                                           3170.
V$=CAT(T$,U$)                                                           3180.
REMARK CHECK AGAINST DUPLICATE SENTENCES                                3190.
O$=R$(FND(D))                                                           3200.
P$=R$(FNE(E))                                                           3210.
Q$=R$(FNF(F))                                                           3220.
IF O$=P$ THEN GOTO 2080                                                 3230.
IF O$=Q$ THEN GOTO 2080                                                 3240.
IF P$=Q$ THEN GOTO 2080                                                 3250.
REMARK NOW PRINT OUT SOME REPORT                                        3260.
U$=''                                                                   3261.
T$=O$                                                                   3262.
GOSUB 3450                                                              3263.
T$=P$                                                                   3264.
GOSUB 3450                                                              3265.
T$=Q$                                                                   3266.
GOSUB 3450                                                              3267.
PRINT U$                                                                3276.
PRINT                                                                   3280.
NEXT I                                                                  3290.
PRINT                                                                   3300.
PRINT                                                                   3310.
PRINT                                                                   3320.
PRINT'THE ABOVE IS THE REPORT YOU REQUESTED TO BE COMPILED'             3330.
PRINT'FOR ';S$                                                          3340.
PRINT                                                                   3350.
PRINT'DO YOU NEED ANY MORE REPORTS?'                                    3360.
 PRINT'TYPE-YES, OTHERWISE TYPE-OVER'                                   3370.
INPUT S$                                                                3380.
IF S$ = 'YES' THEN GOTO 1990                                            3390.
IF S$='OVER' THEN GOTO 3420                                             3400.
 GOTO 3370                                                              3410.
 PRINT 'HOPE YOU WERE SATISFIED.'                                       3420.
PRINT '              GOODBYE'                                           3430.
STOP                                                                    3440.
REMARK THE FOLLOWING SUBROUTINE RECEIVES AS INPUT U$ AND T$.            3441.
REMARK U$ IS THE BUFFER AND T$ IS THE INPUT. U$ WILL BE FILLED          3442.
REMARK TO A MAXIMUM LENGTH OF 64 CHARACTERS AND THEN PRINTED.           3443.
REMARK WHILE T$ IS LONGER THAN 64 CHARACTERS U$ WILL BE REFILLED.       3444.
REMARK WHEN T$ FALLS BELOW 64 CHARACTERS IT IS MOVED TO U$, A           3445.
REMARK PERIOD IS APPENDED AND WE RETURN                                 3446.
G=LEN(T$)                                                               3450.
H=LEN(U$)                                                               3460.
M=G+H                                                                   3470.
U$=CAT(U$,T$)                                                           3490.
IF M > 64 THEN GOTO 3550                                                3495.
H=LEN(U$)                                                               3500.
U$=LST(U$,H-3)                                                          3510.
U$=CAT(U$,'. ')                                                         3520.
RETURN                                                                  3530.
REMARK   WE CAN PRINT SOMETHING                                         3540.
T$=LST(U$,64)                                                           3550.
R$(12)=T$                                                               3559.
FOR L=1 TO 60                                                           3560.
T$=LST(T$,65-L)                                                         3561.
R$(11)=RST(T$,1)                                                        3562.
IF R$(11) = ' ' THEN GOTO 3600                                          3580.
NEXT L                                                                  3590.
J=65-L                                                                  3600.
T$=R$(12)                                                               3601.
T$=LST(T$,J)                                                            3610.
G=LEN(U$)-J                                                             3630.
U$=RST(U$,G)                                                            3640.
R$(13)=RST(U$,3)                                                        3641.
IF R$(13)=',. ' THEN GOTO 3646                                          3642.
PRINT T$                                                                3643.
T$=U$                                                                   3644.
GOTO 3650                                                               3645.
T$=CAT(T$,',')                                                          3646.
PRINT T$                                                                3647.
G=LEN(U$)-3                                                             3648.
T$=RST(U$,G)                                                            3649.
U$=''                                                                   3650.
GOTO 3450                                                               3660.
END                                                                     3670.
