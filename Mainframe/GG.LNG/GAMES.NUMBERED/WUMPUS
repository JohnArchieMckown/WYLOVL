REM WYL.GT.ATG.LIB1#WUMPUS, CREATED 11/01/78                               1.
REM THIS IS A FIRST ATTEMPT AT                                            10.
REM **************************************************************        20.
REM *                                                            *        30.
REM *                 HUNT THE WUMPUS                            *        40.
REM *                                                            *        50.
REM **************************************************************        60.
REM                                   V. B. HUNT 04/26/76                 70.
REM                                                                       80.
REM INITIALIZE THE CAVE                                                   90.
REM   THE CAVE IS A DODECAHEDRON.  EACH CAVERN IN THE CAVE               100.
REM   CORRESPONDS TO THE VERTEX OF THE DODECAHEDRON SO THAT THERE        110.
REM   ARE 3 TUNNELS OUT OF EACH CAVERN.                                  120.
REM                                                                      130.
DIM C(20,3),P(20),R(5)                                                   140.
DATA   2,5,6,     1,8,3,     2,10,4,     3,12,5                          150.
DATA   4,14,1,    15,7,1,    6,16,8,     7,2,9                           160.
DATA   8,17,10,   9,3,11,    10,18,12,   11,13,4                         170.
DATA   12,19,14,  13,15,5,   14,20,6,    20,7,17                         180.
DATA   16,9,18,   17,11,19,  18,13,20,   19,15,16                        190.
REM                                                                      200.
REM THE ABOVE CODE DEFINES THE DODECAHEDRON FOR THE CAVE.                210.
REM   NOW INITIALIZE THE CAVE                                            220.
REM                                                                      230.
FOR I = 1 TO 20                                                          240.
  READ C(I,1),C(I,2),C(I,3)                                              250.
NEXT I                                                                   260.
REM                                                                      270.
REM NOW INITIALIZE GAME BY ASKING USER IF INSTRUCTIONS ARE               280.
REM   WANTED.                                                            290.
REM                                                                      300.
PRINT 'WELCOME TO HUNT THE WUMPUS'                                       310.
PRINT 'DO YOU WANT TO LEARN HOW TO PLAY?';                               320.
GOSUB 2720                                                               330.
IF Y$ = 'NO' THEN 910                                                    340.
PRINT "THE WUMPUS LIVES IN A CAVE OF 20  ROOMS."                         350.
PRINT "EACH ROOM HAS 3 TUNNELS LEADING TO OTHER"                         360.
PRINT "ROOMS.   (LOOK  AT A DODECAHEDRON TO SEE"                         370.
PRINT "HOW THIS WORKS.  IF YOU DON'T KNOW  WHAT"                         380.
PRINT "A DODECAHEDRON IS ASK SOMEONE!)         "                         390.
PRINT "PRESS CR TO CONTINUE."                                            392.
INPUT Y$                                                                 395.
PRINT "                                        "                         400.
PRINT "HAZARDS :                               "                         410.
PRINT "                                        "                         420.
PRINT "BOTTOMLESS PITS - THERE  ARE  TWO  ROOMS"                         430.
PRINT "WITH BOTTOMLESS PITS IN THEM.  IF YOU GO"                         440.
PRINT "THERE,   YOU  FALL  INTO  THE  PIT  (AND"                         450.
PRINT "LOSE!).                                 "                         460.
PRINT "                                        "                         470.
PRINT "SUPER BATS - TWO OTHER ROOMS HAVE  SUPER"                         480.
PRINT "BATS.   IF  YOU GO THERE A BAT GRABS YOU"                         490.
PRINT "AND TAKES YOU TO SOME OTHER  ROOM.   THE"                         500.
PRINT "ROOM IT TAKES YOU TO COULD CAUSE YOU BIG"                         510.
PRINT "TROUBLE.                                "                         520.
PRINT "                                        "                         530.
PRINT "WUMPUS - THE WUMPUS IS NOT  BOTHERED  BY"                         540.
PRINT "HAZARDS  (HE  HAS  SUCKER FEET AND IS TO"                         550.
PRINT "BIG FOR A BAT TO LIFT).  USUALLY  HE  IS"                         560.
PRINT "ASLEEP.  TWO THINGS WAKE HIM UP.  IF YOU"                         570.
PRINT "ENTER  HIS  ROOM HE WILL WAKE UP AND EAT"                         580.
PRINT "YOU!  IF  YOU  SHOOT  AN  ARROW  AT  THE"                         590.
PRINT "WUMPUS  HE WILL WAKE UP AND MOVE.  IF HE"                         600.
PRINT "MOVES INTO THE SAME ROOM THAT YOU ARE IN"                         610.
PRINT "WATCH OUT!  HE WILL EAT YOU UP.         "                         620.
PRINT "PRESS CR TO CONTINUE."                                            622.
INPUT Y$                                                                 623.
PRINT "                                        "                         630.
PRINT "YOU - EACH TURN YOU MAY MOVE OR SHOOT  A"                         640.
PRINT "CROOKED  ARROW.   IF YOU MOVE YOU CAN GO"                         650.
PRINT "ONLY THROUGH ONE TUNNEL TO  ONE  OF  THE"                         660.
PRINT "ROOMS  CONNECTED TO THE ROOM YOU ARE IN."                         670.
PRINT "IF YOU SHOOT BE CAREFUL.  YOU ONLY  HAVE"                         680.
PRINT "5  ARROWS  TO  SHOOT.   THE  WUMPUS GETS"                         690.
PRINT "HUNGRY TOO.  EACH ARROW CAN GO FROM 1 TO"                         700.
PRINT "FIVE ROOMS.   YOU  AIM  BY  TELLING  THE"                         710.
PRINT "COMPUTER THE ROOMS YOU WANT THE ARROW TO"                         720.
PRINT "GO  TO.   IF THE ARROW CAN'T GO THAT WAY"                         730.
PRINT "BECAUSE THERE IS NOT A TUNNEL  THEN  THE"                         740.
PRINT "ARROW  GOES WILD.  A WILD ARROW MOVES AT"                         750.
PRINT "RANDOM TO THE NEXT ROOM.  IF  THE  ARROW"                         760.
PRINT "HITS  THE  WUMPUS YOU WIN.  IF THE ARROW"                         770.
PRINT "HITS YOU, YOU LOSE.                     "                         780.
PRINT "PRESS CR TO CONTINUE."                                            782.
INPUT Y$                                                                 783.
PRINT "                                        "                         790.
PRINT "WHENEVER YOU ARE ONE ROOM  AWAY  FROM  A"                         800.
PRINT "WUMPUS OR A HAZARD THE COMPUTER SAYS:   "                         810.
PRINT ""                                                                 820.
PRINT "WUMPUS - I SMELL A WUMPUS"                                        830.
PRINT "BATS -   BATS NEARBY!"                                            840.
PRINT "PIT  -   I FEEL A DRAFT"                                          850.
REM NOW INITIALIZE                                                       860.
REM   1.)  THE WUMPUS (1)  (1)                                           870.
REM   2.)  THE BATS   (2)  (2)                                           880.
REM   3.)  THE PITS   (3)  (2)                                           890.
REM   NOTE THAT P(I) = 0 SAYS NOTHING IN CAVERN                          900.
FOR I = 1 TO 20                                                          910.
   LET P(I) = 0                                                          920.
NEXT I                                                                   930.
REM THE ABOVE CLEARS OUT THE POSITIONS OF THE BATS,                      940.
REM THE WUMPUS AND THE BOTTOMLESS PITS IN CASE WE                        950.
REM ARE RESTARTING THE GAME                                              960.
GOSUB 2470                                                               970.
LET W9 = W1                                                              980.
REM W9 IS WHERE THE WUMPUS IS!                                           990.
LET P(W1) = 1                                                           1000.
GOSUB 2470                                                              1010.
LET P(W1) = 2                                                           1020.
GOSUB 2470                                                              1030.
LET P(W1) = 2                                                           1040.
GOSUB 2470                                                              1050.
LET P(W1) = 3                                                           1060.
GOSUB 2470                                                              1070.
LET P(W1) = 3                                                           1080.
GOSUB 2470                                                              1090.
REM NOW THE GAME IS READY TO BEGIN.  REMEMBER THAT                      1100.
REM THE WUMPUS IS 1, THE BATS ARE 2, THE PITS ARE 3.                    1110.
REM                                                                     1120.
LET W8 = 5                                                              1130.
REM W8 IS THE NUMBER OF ARROWS YOU GET AT FIRST                         1140.
PRINT ' '                                                               1150.
IF W8 <= 0 THEN 2600                                                    1160.
GOSUB 1620                                                              1170.
IF W1 < 1 THEN 2610                                                     1180.
GOSUB 1410                                                              1190.
PRINT 'YOU ARE IN ROOM'; W1                                             1200.
PRINT 'TUNNELS LEAD TO'; C(W1,1); C(W1,2); C(W1,3)                      1210.
PRINT ' '                                                               1220.
PRINT 'SHOOT OR MOVE (S-M)? ';                                          1230.
INPUT M$                                                                1240.
IF M$ = 'S' THEN 1890                                                   1250.
PRINT 'WHERE TO? ';                                                     1260.
INPUT W2                                                                1270.
IF W2 > 20 THEN 1330                                                    1280.
IF W2 < 1 THEN 1330                                                     1290.
IF C(W1,1) = W2 THEN 1350                                               1300.
IF C(W1,2) = W2 THEN 1350                                               1310.
IF C(W1,3) = W2 THEN 1350                                               1320.
PRINT 'CAN''T FIND A TUNNEL TO'; W2                                     1330.
GOTO 1150                                                               1340.
LET W1 = W2                                                             1350.
GOTO 1150                                                               1360.
REM                                                                     1370.
REM THE FOLLOWING SUBROUTINE LOOKS AT EACH OF THE                       1380.
REM SURROUNDING CAVERNS TO CHECK FOR WIERD THINGS                       1390.
REM TO WARN THE HUNTER ABOUT.                                           1400.
FOR I = 1 TO 3                                                          1410.
   LET W2 = C(W1,I)                                                     1420.
   IF P(W2) = 0 THEN 1550                                               1430.
   IF P(W2) <> 1 THEN 1470                                              1440.
   PRINT 'I SMELL A WUMPUS'                                             1450.
   GOTO 1550                                                            1460.
IF P(W2) <> 2 THEN 1500                                                 1470.
   PRINT 'BATS NEARBY!'                                                 1480.
   GOTO 1550                                                            1490.
IF P(W2) <> 3 THEN 1530                                                 1500.
   PRINT 'I FEEL A DRAFT.'                                              1510.
   GOTO 1550                                                            1520.
PRINT 'THE BALROG HAS EATEN US!'                                        1530.
STOP                                                                    1540.
NEXT I                                                                  1550.
RETURN                                                                  1560.
REM                                                                     1570.
REM THE FOLLOWING ROUTINE CHECKS OUT THE HUNTERS CURRENT                1580.
REM POSITION TO SEE IF HE GETS EATEN BY THE WUMPUS OR                   1590.
REM FALLS INTO THE BOTTOM LESS PIT OR, GETS SPIRITED                    1600.
REM AWAY BY THE BATS.                                                   1610.
IF P(W1) = 0 THEN 1880                                                  1620.
IF P(W1) <> 1 THEN 1700                                                 1630.
PRINT 'LOOK OUT! LOOK OUT! THE WUMPUS IS HERE!'                         1640.
PRINT ' HE''S GOT YOUR LEG ..............'                              1650.
PRINT '   #$&%*@   CHOMP   #$%&&%$ '                                    1660.
PRINT ' HEH! HEH! HEH! - ANOTHER HUNTER GONE '                          1670.
LET W1 = -1                                                             1680.
RETURN                                                                  1690.
IF P(W1) <> 2 THEN 1740                                                 1700.
PRINT ' ZAP -- SUPER BAT SNATCH! ELSEWHEREVILLE FOR YOU!'               1710.
GOSUB 2470                                                              1720.
RETURN                                                                  1730.
IF P(W1) <> 3 THEN 1860                                                 1740.
FOR I = 1 TO 3                                                          1750.
  PRINT 'H'                                                             1760.
  PRINT ' E'                                                            1770.
  PRINT '  L'                                                           1780.
  PRINT '   P'                                                          1790.
  PRINT '    !'                                                         1800.
NEXT I                                                                  1810.
PRINT ' YOU''VE FALLEN INTO A BOTTOMLESS PIT!'                          1820.
PRINT ' THERE''S NO SAVING YOU NOW.'                                    1830.
LET W1 = -1                                                             1840.
RETURN                                                                  1850.
PRINT 'THE BALROG HAS EATEN US'                                         1860.
STOP                                                                    1870.
RETURN                                                                  1880.
  PRINT 'NO. OF ROOMS (1-5)? ';                                         1890.
         INPUT W7                                                       1900.
         IF W7 < 1 THEN 2350                                            1910.
         IF W7 > 5 THEN 2350                                            1920.
         FOR I = 1 TO W7                                                1930.
           LET R(I) = 0                                                 1940.
         NEXT I                                                         1950.
         FOR I = 1 TO W7                                                1960.
           PRINT 'ROOM #? ';                                            1970.
           INPUT W6                                                     1980.
           LET R(I) = W6                                                1990.
         NEXT I                                                         2000.
         LET W6 = W1                                                    2010.
         FOR I = 1 TO W7                                                2020.
           FOR J = 1 TO 3                                               2030.
             IF C(W6,J) = R(I) THEN 2100                                2040.
           NEXT J                                                       2050.
           REM ARROW GOES WILD HERE                                     2060.
           PRINT " YOUR ARROW HAS GONE WILD! "                          2070.
           GOSUB 2560                                                   2080.
           LET R(I) = W2                                                2090.
   LET W6 = R(I)                                                        2100.
           IF W6 <> W1 THEN 2150                                        2110.
           PRINT ' OUCH! YOU''VE SHOT YOURSELF!'                        2120.
           PRINT ' ITS ALL OVER FOR YOU HUNTER!'                        2130.
           GOTO 2610                                                    2140.
IF W6 <> W9 THEN 2190                                                   2150.
           LET W2 = -1                                                  2160.
           W2 = -1                                                      2170.
           LET W2 = RND(W2)                                             2175.
           IF W2 > .25 THEN 2380                                        2180.
 NEXT I                                                                 2190.
         LET W8 = W8 - 1                                                2200.
 PRINT 'TOO BAD!  YOU MISSED THE WUMPUS.'                               2210.
         REM THE FOLLOWING CODE MOVES THE WUMPUS WHEN HE                2220.
         REM IS SHOT AT                                                 2230.
         FOR I = 1 TO 3                                                 2240.
           LET W2 = -1                                                  2250.
           W2 = -1                                                      2260.
           LET W2 = RND(W2)                                             2265.
           LET W3 = INT(W2*3+1)                                         2270.
           IF P(W3) = 0 THEN 2310                                       2280.
         NEXT I                                                         2290.
         GOTO 1150                                                      2300.
LET P(W9) = 0                                                           2310.
         LET P(W3) = 1                                                  2320.
         LET W9 = W3                                                    2330.
         GOTO 1150                                                      2340.
 PRINT 'TYPE A NUMBER BETWEEN 1 AND 5'                                  2350.
         GOTO 1890                                                      2360.
GOTO 1150                                                               2370.
PRINT ' AHA! YOU GOT THE WUMPUS!'                                       2380.
PRINT 'HEE! HEE! HEE! - THE WUMPUS''LL GETCHA NEXT TIME!!'              2390.
PRINT '!!!!!!!!!!!!!!!!!!!'                              2400.
GOTO 2610                                                               2410.
REM THE FOLLOWING SUBROUTINE RETURNS A NEW POSITION TO INSERT           2420.
REM 1.) THE WUMPUS, 2.) THE BATS 3.) THE BOTTOMLESS PIT                 2430.
REM 4.) THE HUNTER.  IT IS ALSO USED WHEN THE HUNTER                    2440.
REM ENCOUNTERS THE SWARM OF SUPER BATS....                              2450.
REM   ...... TO GET THE NEXT ROOM.                                      2460.
LET W1 = -1                                                             2470.
LET W1 = -1                                                             2480.
LET W1 = RND(W1)                                                        2485.
LET W1 = INT(20*W1+1)                                                   2490.
IF P(W1) <> 0 THEN 2470                                                 2500.
RETURN                                                                  2510.
REM THE FOLLOWING ROUTINE GETS A NEW POSITION                           2520.
REM EVEN IF SOMETHING ELSE IS THERE.  IT IS USED                        2530.
REM BY THE SHOOT COMMAND TO GET A TRACK FOR THE                         2540.
REM ARROW IF IT GOES WILD                                               2550.
LET W2 = -1                                                             2560.
         W2 = -1                                                        2570.
         LET W2 = RND(W2)                                               2575.
         LET W2 = INT(20*W2+1)                                          2580.
         RETURN                                                         2590.
PRINT ' YOU''VE RUN OUT OF ARROWS.  TO BAD...'                          2600.
PRINT 'WANT TO PLAY AGAIN(Y-N)? ';                                      2610.
GOSUB 2720                                                              2620.
IF Y$ = 'NO' THEN 2840                                                  2630.
PRINT 'SAME SETUP(Y-N)? ';                                              2640.
GOSUB 2720                                                              2650.
IF Y$ = 'YES' THEN 1090                                                 2660.
GOTO 910                                                                2670.
REM                                                                     2680.
REM THE FOLLOWING ROUTINE GETS THE RESPONSE TO                          2690.
REM A YES OR NO QUESTION.                                               2700.
REM                                                                     2710.
INPUT Y$                                                                2720.
IF Y$ = 'YES' THEN 2800                                                 2730.
IF Y$ = 'Y' THEN 2800                                                   2740.
IF Y$ = 'OK' THEN 2800                                                  2750.
IF Y$ = 'NO' THEN 2820                                                  2760.
IF Y$ = 'N' THEN 2820                                                   2770.
PRINT 'PLEASE ENTER Y FOR "YES" OR N FOR "NO"';                         2780.
GOTO 2720                                                               2790.
LET Y$ = 'YES'                                                          2800.
RETURN                                                                  2810.
LET Y$ = 'NO'                                                           2820.
RETURN                                                                  2830.
END                                                                     2840.
