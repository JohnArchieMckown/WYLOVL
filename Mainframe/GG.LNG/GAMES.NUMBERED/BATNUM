REM WYL.GA.PAB.BASIC.GAMES#BATNUM, CREATED 04/14/76                        1.
PRINT "THE CAMPUS FACILITY OF THE STANFORD UNIVERSITY ";                  10.
PRINT "COMPUTATION CENTER WELCOMES YOU TO PLAY 'BATTLE OF NUMBERS'."      11.
PRINT                                                                     12.
PRINT "IF YOU NEED INSTRUCTIONS TYPE A 1, OTHERWISE TYPE A 2: ";          20.
INPUT I                                                                   30.
IF I<> 1 THEN 140                                                         40.
PRINT "THE GAME IS PLAYED WITH A PILE OF OBJECTS, SOME OF"                50.
PRINT "WHICH ARE REMOVED ALTERNATELY BY YOU AND THE MACHINE."             60.
PRINT "YOU MUST SPECIFY WHETHER WINNING IS DEFINED AS TAKING"             70.
PRINT "OR NOT TAKING THE LAST OBJECT, THE ORIGINAL NUMBER OF"             80.
PRINT "OBJECTS IN THE PILE, WHO GOES FIRST, AND THE MINIMUM"              90.
PRINT "AND MAXIMUM NUMBER OF OBJECTS WHICH CAN BE REMOVED AT"            100.
PRINT "ONE TIME.  TYPING '0' FOR YOUR MOVE WILL CAUSE A"                 110.
PRINT "FORFEIT, AND TYPING '0' FOR THE PILE SIZE WILL CAUSE"             120.
PRINT "THE TERMINATION OF THE GAME."                                     130.
PRINT "ENTER PILE SIZE: ";                                               140.
INPUT N                                                                  150.
IF N<>0 THEN 180                                                         160.
STOP                                                                     170.
IF N<>INT(N) THEN 140                                                    180.
IF N<1 THEN 140                                                          190.
PRINT "ENTER WIN OPTION - 1 TO TAKE LAST, 2 TO AVOID LAST: ";            200.
INPUT M                                                                  210.
IF M=1 THEN 240                                                          220.
IF M<>2 THEN 200                                                         230.
PRINT "ENTER MIN AND MAX: ";                                             240.
INPUT A,B                                                                250.
IF A>B THEN 240                                                          260.
IF A<1 THEN 240                                                          270.
IF A<>INT(A) THEN 240                                                    280.
IF B<>INT(B) THEN 240                                                    290.
PRINT "ENTER START OPTION - 1 MACHINE FIRST, 2 YOU FIRST: ";             300.
INPUT S                                                                  310.
IF S=1 THEN 340                                                          320.
IF S<>2 THEN 300                                                         330.
C=A+B                                                                    340.
IF S=2 THEN 380                                                          350.
GOSUB 410                                                                360.
IF W=1 THEN 140                                                          370.
GOSUB 620                                                                380.
IF W=1 THEN 140                                                          390.
GO TO 360                                                                400.
Q=N                                                                      410.
IF M=1 THEN 440                                                          420.
Q=Q-1                                                                    430.
IF M=1 THEN 490                                                          440.
IF N>A THEN 530                                                          450.
W=1                                                                      460.
PRINT "MACHINE TAKES";N;"AND LOSES"                                      470.
RETURN                                                                   480.
IF N>B THEN 530                                                          490.
W=1                                                                      500.
PRINT "MACHINE TAKES";N;"AND WINS"                                       510.
RETURN                                                                   520.
P=Q-C*INT(Q/C)                                                           530.
IF P>=A THEN 560                                                         540.
P=A                                                                      550.
IF P<=B THEN 580                                                         560.
P=B                                                                      570.
N=N-P                                                                    580.
PRINT "MACHINE TAKES";P;"AND LEAVES";N                                   590.
W=0                                                                      600.
RETURN                                                                   610.
PRINT "YOUR MOVE: ";                                                     620.
INPUT P                                                                  630.
IFP<>0 THEN 680                                                          640.
PRINT "MACHINE WINS BY FORFEIT"                                          650.
W=1                                                                      660.
RETURN                                                                   670.
IF P<>INT(P) THEN 730                                                    680.
IF P>=A THEN 720                                                         690.
IF P=N THEN 770                                                          700.
GO TO 730                                                                710.
IF P<=B THEN 750                                                         720.
PRINT "ILLEGAL MOVE, REENTER IT: ";                                      730.
GO TO 630                                                                740.
N=N-P                                                                    750.
IF N<>0 THEN 840                                                         760.
IF M=1 THEN 810                                                          770.
PRINT "YOU LOSE"                                                         780.
W=1                                                                      790.
RETURN                                                                   800.
PRINT "YOU WIN"                                                          810.
W=1                                                                      820.
RETURN                                                                   830.
IF N>=0 THEN 870                                                         840.
N=N+P                                                                    850.
GO TO 730                                                                860.
W=0                                                                      870.
RETURN                                                                   880.
END                                                                      890.
