.*                                                                              
.*   written    JAN/91 by ch                                                    
.*                                                                              
.*   NOTE:                                                                      
.*   there are a couple or macros that should be used to pass                   
.*   routine addresses in wylbur.  they are:                                    
.*      SAVERTNP @RX,ROUTINE  - save routine address at loc @reg                
.*      LOADRTNP RX,ROUTINE - save routine address in reg                       
.*      CALLRTNP @RX - call routine pointed to by loc @reg                      
.*      CALLRTNP RX - call routine pointed to by reg                            
.*      DEFRTNP - generates a DS for routine pointer storage                    
.*   these macros are only needed where routine addresses                       
.*   are actually passed and called.  (ie. seldom).  they are                   
.*   needed to implement wtoc stuff.  the register passed                       
.*   routine pointers (ie LOADRTNP and CALLRTNP RX)  are not                    
.*   very portable and should not be used.  the memory location                 
.*   macros are more portable, but still not great.                             
.*                                                                              
.*   NOTE:                                                                      
.*   it may be a portability problem to pass routine addresses.                 
.*   so don't do it if you don't have too. (ie from now on!!)                   
.*   also passing function pointers in registers is the worst.                  
.*   PLEASE DO NOT PASS ROUTINE ADDRESSES IN REGISTERS UNLESS                   
.*   ABSOLUTELY NECESSARY.  (I.E. DON'T DO IT!)                                 
.*                                                                              
.*   &ROUTINE can be a 'NAME' or 'A(NAME)' or 'V(NAME)';  an                    
.*   =A(ROUTINE) or =V(ROUTINE) is generated as is apropriate                   
.*                                                                              
.*                                                                              
         MACRO                                                                  
&L       SAVERTNP &RX,&ROUTINE                                                  
         GBLB  &$PCODE                                                          
         LCLC  &REGX                                                            
         LCLC  &RTNNAME                                                         
&L       LABEL ,                                                                
&RTNNAME SETC  '&ROUTINE'                                                       
         AIF   ('&ROUTINE'(1,2) NE 'V(').CONT                                   
&RTNNAME SETC  '&ROUTINE'(3,K'&ROUTINE-3)                                       
.CONT    ANOP                                                                   
         AIF   ('&ROUTINE'(1,2) NE 'A(').CONT2                                  
&RTNNAME SETC  '&ROUTINE'(3,K'&ROUTINE-3)                                       
.CONT2   ANOP                                                                   
         AIF   ('&RX'(1,1) EQ '@').CONT3                                        
         MNOTE 8,'LOCATION (@RX) MUST BE SPECIFIED.'                            
         MEXIT                                                                  
.CONT3   ANOP                                                                   
         AIF   (&$PCODE).CGEN                                                   
         AIF   ('&ROUTINE'(1,2) EQ 'V(').VTYPE                                  
         MVC   &RX.(4),=A(&ROUTINE)                                             
         MEXIT                                                                  
.VTYPE   ANOP                                                                   
         MVC   &RX.(4),=V(&RTNNAME)                                             
         MEXIT                                                                  
.CGEN    ANOP                                                                   
         AIF   ('&ROUTINE'(1,2) NE 'V(').NOTVTYPE                               
         MNOTE *'C-EXTERN extern void &RTNNAME ();'                             
.NOTVTYPE ANOP                                                                  
&REGX    SETC  '&RX'(2,K'&RX-1)                                                 
         MNOTE *,'C-CODE RTN_PTR1 = &RTNNAME; '                                 
         MNOTE *,'C-CODE memcpy((char *) &REGX,'                                
         MNOTE *,'C-CODE   (char *) && RTN_PTR1, sizeof(RTN_PTR1));'            
         MEND                                                                   
