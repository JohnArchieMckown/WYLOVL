         MACRO                                                                  
&L       XTRACE &NAME,&DATA,&TYPE=,&REGS=ALL                                    
.*  Modified, 4/85, "Niz"    2/91 CH                                            
         GBLA  &$REG#                                                           
         GBLB  &$OK                                                             
         GBLB  &$PCODE                                                          
         LCLA  &TPFLGS,&NUM                                                     
         LCLB  &FDATA,&FKAPUT,&FDUMP,&FNREST,&FMSG,&RGMSK(16)                   
         LCLC  &RGTXT                                                           
&L       LABEL                                                                  
         AIF   (NOT &$PCODE).NOPCODE                                            
         MEXIT                                                                  
.NOPCODE ANOP                                                                   
.NOLBL   AIF   ('&DATA' EQ '').LINK                                             
&FDATA   SETB  1                   data given                                   
         AIF   ('&DATA' EQ '*').LINK                                            
         MVC   CVTRCDAT,&DATA      move data                                    
.LINK    STM   R0,R15,CVLNKRGS     save registers                               
         BAS   R14,CVTRC           Trace                                        
         AIF   ('&TYPE(1)' NE 'KAPUT').NOTKAP                                   
&FKAPUT  SETB  1                                                                
&T2      SETC  '&TYPE(2)'                                                       
&T3      SETC  '&TYPE(3)'                                                       
&T4      SETC  '&TYPE(4)'                                                       
&FDUMP   SETB  ('&T2' EQ 'DUMP' OR '&T3' EQ 'DUMP' OR '&T4' EQ 'DUMP')          
&FNREST  SETB  ('&T2' EQ 'NRST' OR '&T3' EQ 'NRST' OR '&T4' EQ 'NRST')          
&FMSG    SETB  ('&T2' EQ 'MSG' OR '&T3' EQ 'MSG' OR '&T4' EQ 'MSG')             
.NOTKAP  ANOP                                                                   
&NUM     SETA  0                                                                
         AIF   ('&REGS' EQ 'NONE').DONE                                         
.LOOP    ANOP                                                                   
&NUM     SETA  &NUM+1                                                           
         AIF   (&NUM GT N'&REGS).DONE                                           
         AIF   ('&REGS(&NUM)' NE 'ALL').SPEC                                    
&RGMSK(1)  SETB 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1                                 
         AGO   .LOOP                                                            
.SPEC    ANOP                                                                   
        $RGLKUP &REGS(&NUM)                                                     
         AIF   (&$OK).REGDEF                                                    
&$REG#   SETA  &REGS(&NUM)                                                      
.REGDEF  ANOP                                                                   
&RGMSK(&$REG#+1) SETB 1                                                         
         AGO   .LOOP                                                            
.DONE    ANOP                                                                   
&TPFLGS  SETA  &FDATA*X'80'+&FKAPUT*X'40'+&FDUMP*X'20'+&FMSG*X'10'+&FNR*        
               EST*X'08'                                                        
&RGTXT SETC  '&RGMSK(1)&RGMSK(2)&RGMSK(3)&RGMSK(4)&RGMSK(5)&RGMSK(6)'           
&RGTXT SETC  '&RGTXT&RGMSK(7)&RGMSK(8)&RGMSK(9)&RGMSK(10)&RGMSK(11)'            
&RGTXT SETC  '&RGTXT&RGMSK(12)&RGMSK(13)&RGMSK(14)&RGMSK(15)&RGMSK(16)'         
         DC    AL1(&TPFLGS),AL1(0),B'&RGTXT',CL8'&NAME'                         
         MEND                                                                   
