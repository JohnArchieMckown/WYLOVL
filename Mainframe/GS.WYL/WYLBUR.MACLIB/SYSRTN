         MACRO                                                                  
&L       SYSRTN &NAME,&ROUTINE                                                  
.*                                                                              
.*  this macro generates a table entry.  the table is used to                   
.*  match (map) names (character format names) to routine                       
.*  addresses.  this macro is used to create the table of system                
.*  variables, system functions and system procedures.  each name               
.*  is associated with a routine that returns the specified                     
.*  or requested value(s).                                                      
.*                                                                              
.*  note/CAUTION:                                                               
.*  varible/function/procedure max name length is a hard                        
.*  coded 16 chars long.  Yukko.                                                
.*                                                                              
.*                                                                              
         GBLB  &$PCODE             C or assembly language switch                
         GBLC  &$CSTR              EBCDIC to ASCII conversion string            
         LCLC  &TOK1,&TOK2                                                      
.*                                                                              
.* validity checks                                                              
.*                                                                              
         AIF   ('&NAME' NE '').CONT1                                            
         MNOTE 8,'Missing variable/function/procedure name.'                    
         MEXIT                                                                  
.CONT1   ANOP                                                                   
         AIF   ('&ROUTINE' NE '').CONT2                                         
         MNOTE 8,'Missing routine name.'                                        
         MEXIT                                                                  
.CONT2   ANOP                                                                   
.*                                                                              
.*       Generate the assembly language version                                 
.*                                                                              
         AIF   (&$PCODE).CGEN                                                   
&L       DC    0F'0'                                                            
         DC    CL16'&NAME'                                                      
         AIF   ('&ROUTINE' EQ 'NULL').NULRTN                                    
         DC    A(&ROUTINE)                                                      
         MEXIT                                                                  
.NULRTN  ANOP                                                                   
         DC    A(0)                                                             
         MEXIT                                                                  
.*                                                                              
.*       Generate C version                                                     
.*                                                                              
.CGEN    ANOP                                                                   
         AIF   ('&L' EQ '').NOLABEL                                             
         MNOTE *,'C-DATA static struct sysrtn &L = {'                           
         AGO   .GENNAME                                                         
.NOLABEL ANOP                                                                   
         MNOTE *,'C-DATA static struct sysrtn RTN&SYSNDX = {'                   
.GENNAME ANOP                                                                   
&TOK1    SETC  '&NAME        '(1,8)                                             
&TOK2    SETC  '&NAME                '(9,8)                                     
        $CSTR  '&TOK1',8                                                        
         MNOTE *,'C-DATA &$CSTR,'                                               
        $CSTR  '&TOK2',8                                                        
         MNOTE *,'C-DATA &$CSTR,'                                               
         AIF   ('&ROUTINE' EQ 'NULL').NULCRTN                                   
         MNOTE *,'C-DATA &ROUTINE };'                                           
         MEXIT                                                                  
.NULCRTN ANOP                                                                   
         MNOTE *,'C-DATA 0 }; '                                                 
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
