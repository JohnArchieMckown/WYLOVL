         MACRO                                                                  
&L       OSCSAVE &T                                                             
.*                                                                              
.**  macro to save scan pointers in specified save area                         
.**    format of parameter may be any of the following forms:                   
.**     name   symbolic name with addressability                                
.**     n(r)   rx form - offset n from register r                               
.**     (r)    register contains address                                        
.**     null   default value                                                    
.**                                                                             
.**  written by esr 9/23/70                                                     
.**  last modified by esr 9.23/70                                               
.*                                                                              
         LCLA  &LNT,&PTR,&CNT,&SUB                                              
         LCLC  &C(8)                                                            
&LNT     SETA  K'&T                length of operand                            
&CNT     SETA  1                   work counter                                 
         AIF   (&LNT EQ 0).DEFLT   CHECK NULL PARM                              
         AIF   ('&T'(1,1) EQ '(').REG  CHECK REG FORM                           
.SCHLP   ANOP                      SEARCH for left paren for rx form            
&CNT     SETA  &CNT+1                                                           
         AIF   (&CNT GT &LNT).GEN  CHECK END OF OPERAND                         
         AIF   ('&T'(&CNT,1) NE '(').SCHLP                                      
.*                                                                              
.**  found rx form                                                              
.*                                                                              
&PTR     SETA  1                   set pointer to start of parm                 
&SUB     SETA  1                   init subscript                               
.LOOP    ANOP                      SAVE part of parm in 8 char groups           
         AIF   (&CNT LE 8).HVOP    CHECK LAST GROUP REQ                         
&C(&SUB) SETC  '&T'(&PTR,8)        8 char group                                 
&SUB     SETA  &SUB+1              bump subscript                               
&PTR     SETA  &PTR+8              bump scan pointer                            
&CNT     SETA  &CNT-8              reduce length                                
         AGO   .LOOP                                                            
.*  have last group                                                             
.HVOP    ANOP                                                                   
&C(&SUB) SETC  '&T'(&PTR,&CNT)     save last group                              
&PTR     SETA  &PTR+&CNT           bump past processed stuff                    
&CNT     SETA  &LNT-((&SUB-1)*8)-&CNT  remaining length                         
         AIF   (&PTR GT &LNT).GO   BR IF SECOND PART OF RX PARM                 
.*  just processed first part of rx form                                        
&C(&SUB+1) SETC '6,'               put in explicit length                       
&SUB     SETA  &SUB+2                                                           
         AGO   .LOOP                                                            
.*  finished with rx form                                                       
.GO      ANOP                                                                   
&L       MVC   &C(1)&C(2)&C(3)&C(4)&C(5)&C(6)&C(7)&C(8),CPOSCAN                 
         MEXIT                                                                  
.*                                                                              
.**  default value to be used                                                   
.*                                                                              
.DEFLT   ANOP                                                                   
&L       MVC   CPTELL(6),CPOSCAN                                                
         MEXIT                                                                  
.*                                                                              
.**  symbolic name specified - not in rx form                                   
.*                                                                              
.GEN     ANOP                                                                   
&L       MVC   &T.(6),CPOSCAN                                                   
         MEXIT                                                                  
.*                                                                              
.**  register form used                                                         
.*                                                                              
.REG     ANOP                                                                   
&C(1)    SETC  '&T'(2,&LNT-2)                                                   
&L       MVC   0(6,&C(1).),CPOSCAN                                              
         MEND                                                                   
