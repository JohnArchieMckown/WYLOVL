         MACRO                                                                  
&L       KAPUT &NAME,&MSG,&LENGTH,&DUMP=YES,&RESTART=YES,&REGS=ALL              
.*       added, 12/76, by JDN; modified, 11/89, Niz                             
         GBLB  &$PCODE             Assembly language or C switch                
.*                                                                              
         GBLA  &VLTNO,&VLTPTR                                                   
         GBLC  &VLTMSG(0)                                                       
         LCLA  &LEN,&NUM,&WORK                                                  
         LCLC  &LABEL,&BC,&ELBL                                                 
         LCLC  &TDUMP,&TREST,&TMSG                                              
.*                                                                              
         AIF   (&$PCODE).CGEN                                                   
.*                                                                              
.*       Assemble language version.                                             
.*                                                                              
&FMSG    SETB  ('&MSG' NE '')                                                   
&FDUMP   SETB  ('&DUMP' EQ 'YES')                                               
&FREST   SETB  ('&RESTART' EQ 'YES')                                            
         AIF   (NOT &FDUMP).NOTDUMP                                             
&TDUMP   SETC  'DUMP'                                                           
.NOTDUMP AIF   (&FREST).REST                                                    
&TREST   SETC  'NRST'                                                           
.REST    AIF   (NOT &FMSG).NOTMSG                                               
&TMSG    SETC  'MSG'                                                            
.NOTMSG  ANOP                                                                   
.*                                                                              
         AIF   (&FMSG OR ((NOT &FMSG) AND ('&LENGTH' EQ ''))).MOK               
         MNOTE 8,'LENGTH=&LENGTH SPECIFIED AND NO MESSAGE GIVEN'                
.MOK     ANOP                                                                   
.*                                                                              
&LNAME   SETC  'KAPUT'             default descriptor                           
         AIF   ('&NAME' EQ '').DFLNAME                                          
&LNAME   SETC  '&NAME'                                                          
.DFLNAME ANOP                                                                   
.*                                                                              
&L       XTRACE &LNAME,REGS=&REGS,TYPE=(KAPUT,&TDUMP,&TREST,&TMSG)              
         AIF   (NOT &FMSG).MSGEND                                               
         AIF   ('&MSG'(1,1) EQ '''').STR                                        
         DC    AL1(&LENGTH),AL3(&MSG)                                           
         AGO   .MSGEND                                                          
.*                                                                              
.STR     ANOP                                                                   
&LEN     SETA  K'&MSG-2                                                         
         AIF   ('&LENGTH' EQ '').NOLEN                                          
&LEN     SETA  &LENGTH                                                          
.NOLEN   ANOP                                                                   
&NUM     SETA  1                                                                
.LOOP    AIF   (&NUM GT &VLTPTR).NEW                                            
         AIF   ('&MSG' EQ '&VLTMSG(&NUM)').OLD                                  
&NUM     SETA  &NUM+1                                                           
         AGO   .LOOP                                                            
.NEW     ANOP                                                                   
&VLTPTR  SETA  &NUM                                                             
&VLTMSG(&VLTPTR)  SETC '&MSG'                                                   
.OLD     ANOP                                                                   
         DC    AL1(&LEN),AL3($$$V&VLTNO&NUM)                                    
.MSGEND  AIF   ('&ELBL' EQ '').DONE                                             
&ELBL    DS    0H                                                               
         MEXIT                                                                  
.*                                                                              
.*       C version.                                                             
.*                                                                              
.CGEN    AIF   ('&MSG' EQ '').NOMSG                                             
         AIF   ('&MSG '(1,1) EQ '''').CQOK                                      
         MNOTE 8,'KAPUT message not properly quoted'                            
         MEXIT                                                                  
.NOMSG   ANOP                                                                   
         MNOTE *,'C-CODE fail("Wylbur Failed.")'                                
         MEXIT                                                                  
.CQOK    ANOP                                                                   
&MCHARS  SETC  '&MSG'(2,K'&MSG-2)                                               
         MNOTE *,'C-CODE fail("&MCHARS");'                                      
.DONE    ANOP                                                                   
         MEND                                                                   
