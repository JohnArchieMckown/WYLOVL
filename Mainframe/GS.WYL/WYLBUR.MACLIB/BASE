         MACRO                                                                  
&L       BASE  &ARG1,&ARG2,&USING=USING,&TRACE=NO,&TRACEID=                     
.*  Added, 11/85, Niz      (Special WYLBUR version)                             
.*  Modified, 11/89, Niz   (C support)                                          
         GBLB  &$PCODE             C or assembly language switch                
         GBLA  &$REG#                                                           
         GBLB  &$OK                                                             
.*                                                                              
&TRC     SETC  'NO'                                                             
         AIF   (&$PCODE).TCOM      C-style always has TRACE=NO                  
&TRC     SETC  '&TRACE'                                                         
.TCOM    ANOP                                                                   
.*                                                                              
&ADDR    SETC  '&ARG2'                                                          
         AIF   ('&ARG1&ARG2' EQ '').DEFLT                                       
        $RGLKUP &ARG1                                                           
         AIF   (&$OK).GOTREG                                                    
&ADDR    SETC  '&ARG1'                                                          
        $RGLKUP &ARG2                                                           
         AIF   (&$OK).GOTREG                                                    
         AIF   (N'&SYSLIST GE 2).BADARGS                                        
.DEFLT  $RGLKUP BR                                                              
         AIF   (NOT &$OK).BADREG                                                
.GOTREG  AIF   ('&ADDR' EQ '' OR '&ADDR' EQ '*').HERE                           
&L       L     &$REG#,=A(&ADDR)                                                 
        &USING (&ADDR,&$REG#)                                                   
         MEXIT                                                                  
.HERE    AIF   (('&ARG1&ARG2' EQ '') AND ('&TRC' NE 'NO')).TRC                  
&L       BASR  &$REG#,0                                                         
        &USING (*,&$REG#)                                                       
         MEXIT                                                                  
.*                                                                              
.TRC     ANOP                                                                   
&L       BAS   BR,CVENT            Special WYLBUR Routine                       
         DC    CL8'&TRACEID '      Trace id                                     
        &USING (*,&$REG#)                                                       
         MEXIT                                                                  
.*                                                                              
.BADARGS ANOP                                                                   
&N       SETA  N'&SYSLIST                                                       
         MNOTE 8,'Too many parameters (NPARMS=&N)'                              
         MEXIT                                                                  
.*                                                                              
.BADREG  MNOTE 8,'Missing or invalid register.'                                 
         MEND                                                                   
