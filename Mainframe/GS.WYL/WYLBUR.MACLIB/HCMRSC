         MACRO                                                                  
         HCMRSC &FORM=DSECT,&NUMBER=,&ANCHOR=0                                  
.*  Add MVS support, 2/81, 12/81; MVS/XA 4/85  "Niz"                            
HCMRSC   RECORD BEGIN                                                           
         RSCHDR PFX=HCM,FORM=DSECT                                              
HCMPATH  PATH  VERSION=2,PFX=HCMP,ECB=HCM$ECB,FLAG=HCMSFLG,            *        
               MYTAG=SUBSYSTM,YOURTAG=HASPCONS,PASS=HXCI                        
HCMFLG   DC    X'00'               Local copy of Suzan flag                     
HCMSFLG  DC    X'00'               Flag for Suzan post info                     
         DC    X'00'               Zero for MVC mask                            
*                                                                               
HCMWFLG  FLAG  ,                   WYLBUR's path flags                          
         FLAG  HCMFOPN             - JES opened path                            
         FLAG  HCMFPND             - JES open pending                           
         FLAG  HCMFSENT            - (LOCAL) Command sent to hasp               
*                                                                               
HCMPTRS  DS    0D                                                               
HCMLOC   DS    F          [Order]  Location of text in suzan buf                
HCMLEN   DS    F          [Order]  Length of text in suzan buf                  
HCMINDEX DS    H                   Index into fetch buffer                      
HCMWA    EQU   HCMPTRS,*-HCMPTRS,C'X'                                           
         DS    0D                                                               
         END                                                                    
         AIF   ('&FORM' EQ 'DSECT').EXIT                                        
HCMFRST  EQU   HCM1                                                             
HCMLAST  EQU   HCM&NUMBER                                                       
&I       SETA  0                                                                
HCM0     EQU   &ANCHOR             Links in resource group                      
&V       SETA  &NUMBER+1                                                        
HCM&V    EQU   &ANCHOR             Links in resource group                      
.*                                                                              
.PLOOP   AIF   (&I GE &NUMBER).EXIT                                             
&I       SETA  &I+1                                                             
&P       SETA  &I-1                                                             
&N       SETA  &I+1                                                             
HCM&I    RSCHDR FORM=CODE,PREV=HCM&P,NEXT=HCM&N                                 
HCMPATH&I PATH VERSION=2,FLAG=HCMSFLG&I,MYTAG=?,YOURTAG=HASPCONS,      *        
               ECB=(HCM&I+(HCM$ECB-HCMRSC)),PASS=HXCI                           
         GEN   'DC X"00"; HCMSFLG&I: DC X"00"; DC X"00"'  Suzan flgs            
         DC    X'00'               WYLBUR path flags                            
         DS    0D                                                               
         DS    (L'HCMWA)X          Path work area                               
         DS    0D                                                               
         AGO   .PLOOP                                                           
.EXIT    MEND                                                                   
