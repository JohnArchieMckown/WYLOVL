         MACRO                                                                  
&NAME    VOLUME &VOLNAME,&TYPE=IO,&PRESRES=YES,&DSECT=NO,&AVAIL=YES,   *        
               &PRIV=NO,&SHOW=YES                                               
         LCLB  &PRBIT              INDICATES VALUE OF PRESRES PARM              
         LCLB  &AVAILBT            INDICATES VOLUME AVAILABILITY                
         LCLB  &SHOWBIT            INDICATES VOLUME SHOWABILITY                 
         LCLC  &VTYPE              HOLDS VOLUME TYPE (2 CHARS)                  
.*                                                                              
         AIF   ('&DSECT' EQ 'YES').DODSECT                                      
         AIF   ('&VOLNAME' EQ '').ERROR1  ERROR - NO VOLUME NAME                
         AIF   (K'&VOLNAME GT 6).ERROR2   ERROR - VOLNAME > 6 CHARS             
.*                                                                              
.*       now check the volume type                                              
.*                                                                              
         AIF   (('&TYPE' EQ 'IO') OR ('&TYPE' EQ 'SAVE')).TYPSAV                
         AIF   (('&TYPE' EQ 'TM') OR ('&TYPE' EQ 'SCRATCH')).TYPSCR             
         AIF   (('&TYPE' EQ 'DD') OR ('&TYPE' EQ 'USE')).TYPUSE                 
         AGO   .ERROR3             ERROR - BAD VOLUME TYPE                      
.*                                                                              
.*       encode volume type                                                     
.*                                                                              
.TYPSAV  ANOP                                                                   
&VTYPE   SETC  'IO'                VOLUME IS USE AND SAVE                       
         AGO   .GEN                                                             
.*                                                                              
.TYPSCR  ANOP                                                                   
&VTYPE   SETC  'TM'                VOLUME IS TEMP                               
         AGO   .GEN                                                             
.*                                                                              
.TYPUSE  ANOP                                                                   
&VTYPE   SETC  'DD'                VOLUME IS USE ONLY                           
.*       ago   .gen                                                             
.*                                                                              
.GEN     ANOP                                                                   
         AIF   (('&PRESRES' NE 'YES') AND ('&PRESRES' NE 'NO')).ERROR4          
&PRBIT   SETB  ('&PRESRES' EQ 'YES')     ENCODE PRESRES SETTING                 
         AIF   (('&AVAIL' NE 'YES') AND ('&AVAIL' NE 'NO')).ERROR5              
&AVAILBT SETB  ('&AVAIL' EQ 'YES')                                              
         AIF   (('&PRIV' NE 'YES') AND ('&PRIV' NE 'NO')).ERROR6                
&PRIVBT  SETB  ('&PRIV' EQ 'YES')                                               
         AIF   (('&SHOW' NE 'YES') AND ('&SHOW' NE 'NO')).ERROR7                
&SHOWBIT SETB  ('&SHOW' EQ 'YES')                                               
.*                                                                              
.*       generate the volume table entry                                        
.*                                                                              
&NAME    DC    CL2'&VTYPE'         VOLUME TYPE CODE                             
         AIF   ('&VOLNAME' EQ 'SPARE').NOTBLK                                   
         DC    CL6'&VOLNAME'       VOLUME NAME                                  
         AGO   .NOTBLK1                                                         
.NOTBLK  DC    CL6' '              VOLUME NAME                                  
.NOTBLK1 AIF   ('&VOLNAME' EQ 'SPARE').SPARE1                                   
         DC    AL1(128*&PRBIT+64*&AVAILBT+32*&PRIVBT) VOL CHAR                  
.SPARE1  AIF   ('&VOLNAME' NE 'SPARE').NSPARE1                                  
         DC    AL1(128*&PRBIT)     VOLUME CHARACTERISTICS FLAGS                 
.NSPARE1 ANOP  ,                                                                
         DC    AL1(0)              SPARE FLAG BYTE                              
         DC    AL1(128*&SHOWBIT)               MORE CHARACTERISTICS             
         DC    XL5'00'             Back-online time                     **ISU** 
         DC    H'0'                NUMBER OF OPENS TO THIS VOLUME               
         DC    F'0'                NUMBER OF READS TO THIS VOLUME               
         DC    F'0'                NUMBER OF WRITES TO THIS VOLUME              
         DC    H'0'                AMOUNT OF SPACE ON VOLUME (TRKS)             
         DC    H'0'                NUMBER OF EXTENTS ON VOLUME                  
         DC    H'0'                MAX SPACE AVAILABLE ON VOLUME                
         DC    H'0'                % FREE SPACE WANTED                          
         DC    A(0)                UCB ADDRESS                                  
         DC    A(0)                OWNING IOWA ADDRESS                          
         DC    A(0)                TIOT ENTRY ADDRESS                           
         DC    AL1(0)              VOLUME OWNER FLAGS 1                         
         DC    AL1(0)              VOLUME OWNER FLAGS 2                         
         DC    CL16' '             VOLUME ACCESSOR LIST                         
         DC    0F'0'               ROUND UP TO FULLWORD BOUNDARY                
         MEXIT                                                                  
.DODSECT ANOP                                                                   
&NAME    DSECT                                                                  
VTYPE    DS    CL2                 VOLUME TYPE CODE                             
VNAME    DS    CL6                 VOLUME NAME                                  
*                                                                               
VFLG1    DS    X                   VOLUME CHARACTERISTICS FLAG                  
*                                                                               
VF1PRES  EQU   X'80'               VOLUME IS PERM. RES OR RESERVED              
VF1AVAIL EQU   X'40'               VOLUME IS AVAILABLE FOR USE                  
VF1PRIV  EQU   X'20'               VOLUME IS ACCESS RESTRICTED                  
VF1PDA   EQU   X'10'               VOLUME IS PENDING DEALLOCATION               
VF1CLR   EQU   X'08'               CLR VOLIST ENTRY AFTER DEALLOCATION          
VF1AJCL  EQU   X'04'               VOLUME OVERRIDDEN IN JCL      BH6023         
VF1MNTD  EQU   X'02'               VOLUME MOUNTED BY OPERATOR    BH6023         
VF1ON    EQU   X'01'               VOLUME IS "ONLINE"            BH6036         
*                                                                               
VFLG2    DS    X                   SECOND FLAG BYTE              BH6023         
*                                                                               
VF2ALLO  EQU   X'80'               ALLOCATE WHEN YOU HAVE VOL    BH6023         
VF2DALLO EQU   X'40'               DEALLOCATE WHEN HAVE VOL      BH6023         
VF2QUIET EQU   X'20'               DO QUIET DEALLOCATION         BH6036         
VF2ADACT EQU   X'10'               ALLO/DALLO ACTIVE             BH6062         
VF2MOUNT EQU   X'08'               mount may be issued to fill req              
VFLG3    DS    X                   THIRD FLAG BYTE                              
*                                                                               
VF3SHOW  EQU   X'80'               - DISPLAY IN "SHOW VOL" OUTPUT               
VF3BKUP  EQU   X'40'               - Volume being backed up             **ISU** 
*                                                                       **ISU** 
VOLONTIM DS    CL5                 - Back-online time                   **ISU** 
*        STATISTICS SECTION                                                     
*                                                                               
VOLOPNS  DS    H                   NUMBER OF OPENS                              
VOLREADS DS    F                   NUMBER OF READS                              
VOLWRITS DS    F                   NUMBER OF WRITES                             
VOLSPACE DS    H                   AMOUNT OF SPACE ON VOLUME                    
VOLEXTN  DS    H                   NUMBER OF EXTENTS                            
VOLMAXSP DS    H                   MAX SPACE AVAILABLE                          
VOLPCFRE DS    H                   PERCENT FREE SPACE WANTED                    
VOLJUNK  EQU   VOLOPNS,*-VOLOPNS   DEFINE VOLUMES AREA           BH6036         
*-                                                                              
*-       CONTROL ADDRESS SECTION                                                
*-                                                                              
VOLUCBAD DS    A                   UCB ADDRESS                                  
VOLUSECT DS    H                   USE COUNT OF VOLUME           BH6036         
         DS    H                   EXTRA HALF WORD               BH6036         
         DS    A                   TIOT POINTER (DO NOT USE)                    
*-                                                                              
*-       ACCESS CONTROL SECTION                                                 
*-                                                                              
VOLOWNF1 DS    X                   VOLUME OWNER FLAGS 1                         
VOLOWNF2 DS    X                   VOLUME OWNER FLAGS 2                         
VOLOWNER DS    CL16                VOLUME ACCESSOR LIST                         
         DS    0F                  ROUND UP TO FULLWORD                         
VOLOWNR  EQU   VOLOWNF1,*-VOLOWNF1 DEFINE VOLUME ACCESS SECTION  BH6036         
         MEXIT                                                                  
.*                                                                              
.*       error messages                                                         
.*                                                                              
.ERROR1  MNOTE 8,'*** VOLUME NAME MISSING ***'                                  
         MEXIT                                                                  
.ERROR2  MNOTE 8,'*** VOLUME NAME GREATER THAN 6 CHARACTERS ***'                
         MEXIT                                                                  
.ERROR3  MNOTE 8,'*** VOLUME TYPE NOT IO,DD,TM,USE,SAVE OR SCRATCH ***'         
         MEXIT                                                                  
.ERROR4  MNOTE 8,'*** INVALID PRESRES VALUE, MUST BE YES|NO ***'                
         MEXIT                                                                  
.ERROR5  MNOTE 8,'*** INVALID AVAIL VALUE, MUST BE YES|NO ***'                  
         MEXIT                                                                  
.ERROR6  MNOTE 8,'*** INVALID PRIV VALUE, MUST BE "YES/NO" ***'                 
         MEXIT                                                                  
.ERROR7  MNOTE 8,'*** INVALID SHOW VALUE, MUST BE "YES/NO" ***'                 
         MEND                                                                   
