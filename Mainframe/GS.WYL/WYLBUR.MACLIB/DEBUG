         MACRO                                                                  
&L       DEBUG                                                                  
.*                                                                              
.*  The DEBUG macro allows statements like:                                     
.*       DEBUG 'CPLENGTH set to ',CPLENGTH,' width ',CPWIDTH                    
.*                                                                              
.*  The current "debugging monitor" session will display the                    
.*  DEBUG lines.  If there is no debugging monitor, then the                    
.*  text is thrown away.                                                        
.*                                                                              
.*  There is also a "NOBUG" macro which is a NOOP version of                    
.*  debug.  A statement like "NOBUG 'testing'" will generate no                 
.*  code -- it's a convenient way of leaving in DEBUG statements.               
.*                                                                              
.*  Added, 11/89, Niz                                                           
.*                                                                              
         GBLB  &$OK                Return code from HIBAL macros                
         LCLA  &I                                                               
&LQ      SETC  'L'''                                                            
.*                                                                              
&L       LABEL                                                                  
.*                                                                              
.*       Go through the operands and check for D:, X:, etc.                     
.*                                                                              
.LOOP    AIF   (&I GE N'&SYSLIST).SEGWR  All done, write it                     
&I       SETA  &I+1                                                             
&ITEM    SETC  '&SYSLIST(&I,1)'    Operand                                      
&WLEN    SETC  '&SYSLIST(&I,2)'    Explicit length (optional)                   
.*                                                                              
         AIF   ('&ITEM '(1,1) NE '''').D                                        
         SEG   &ITEM,&WLEN,.L:CVDEBUGP                                          
         AGO   .LOOP                                                            
.*                                                                              
.D       AIF   ('&ITEM  '(1,2) NE 'D:').X                                       
&SEG     SETC  'SEGDC'                                                          
         AGO   .DXCOM                                                           
.*                                                                              
.X       AIF   ('&ITEM  '(1,2) NE 'X:').A                                       
&SEG     SETC  'SEGHX'                                                          
.DXCOM   ANOP                                                                   
&ITEM    SETC  '&ITEM'(3,K'&ITEM-2)                                             
        $RGLKUP &ITEM              Is it a register?                            
         AIF   (NOT &$OK).DXNREG   No, handle as addr                           
         &SEG  (&ITEM),,.L:CVDEBUGP                                             
         AGO   .LOOP                                                            
.*                                                                              
.DXNREG  AIF   ('&WLEN' NE '').DXLEN                                            
&LL      SETA  L'&ITEM                                                          
         AGO   .DXCONT                                                          
.DXLEN   ANOP                                                                   
&LL      SETA  &WLEN                                                            
.DXCONT  ANOP                                                                   
&OP      SETC  'LC:'                                                            
         AIF   (&LL EQ 1).DXSEG                                                 
&OP      SETC  'L2:'                                                            
         AIF   (&LL EQ 2).DXSEG                                                 
&OP      SETC  'L3:'                                                            
         AIF   (&LL EQ 3).DXSEG                                                 
&OP      SETC  'L:'                                                             
&WLEN    SETC  '4'                                                              
.DXSEG   &SEG  &OP&ITEM,,.L:CVDEBUGP                                            
         AGO   .LOOP                                                            
.*                                                                              
.A       AIF   ('&ITEM  '(1,2) NE 'A:').LA                                      
&ITEM    SETC  '&ITEM'(3,K'&ITEM-2)                                             
         AIF   ('&WLEN' NE '').ASEG                                             
&WLEN    SETC  '4'                                                              
.ASEG    SEGHX L:&ITEM,&WLEN,.L:CVDEBUGP                                        
         AGO   .LOOP                                                            
.*                                                                              
.LA      AIF   ('&ITEM '(1,3) NE 'LA:').STR                                     
&ITEM    SETC  '&ITEM'(4,K'&ITEM-3)                                             
         AIF   ('&WLEN' NE '').LASEG                                            
&WLEN    SETC  '4'                                                              
.LASEG   SEGHX LA:&ITEM,&WLEN,.L:CVDEBUGP                                       
         AGO   .LOOP                                                            
.*                                                                              
.STR    $RGLKUP &ITEM              Is it a register?                            
         AIF   (NOT &$OK).NREG     No, handle as string                         
         SEGDC (&ITEM),,.L:CVDEBUGP                                             
         AGO   .LOOP                                                            
.NREG    SEG   &ITEM,&WLEN,.L:CVDEBUGP                                          
         AGO   .LOOP                                                            
.*                                                                              
.SEGWR   SEGWR ,,.L:CVDEBUGP                                                    
         MEND                                                                   
