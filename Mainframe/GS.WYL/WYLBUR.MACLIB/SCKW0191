         MACRO                                                                  
&L       SCKW  &TOKEN,&ROUTINE,&OPTIONS,&PARM1,&PARM2                           
.*                                                                              
.*       New scanner, 1/90,  Niz/Chaz                                           
.*                                                                              
         GBLB  &$PCODE             C or assembly language switch                
         GBLA  &$COUNT                                                          
         GBLC  &$CSTR                                                           
         GBLA  &SCKWCNT                                                         
.*                                                                              
         LCLB  &TFLG,&P1FLG,&P2FLG,&MATFLG,&RTNFLG                              
         LCLB  &A,&A1,&S                                                        
         LCLB  &PARM,&I,&PI,&SI,&LN,&SYMLN                                      
         LCLB  &PUSH,&NULL                                                      
         LCLB  &END,&DONTBL                                                     
         LCLB  &RTNGEN                                                          
         LCLA  &TOKLEN,&COUNT                                                   
         LCLA  &P1VAL,&P2VAL,&V1,&V2,&V3,&V4                                    
         LCLA  &KWFLG1,&KWFLG2,&KWFLG3,&KWFLG4,&KWFLG5,&KWFLG6                  
         LCLC  &MATTYPE,&MATLAB,&RTNTYPE,&RTNLAB,&RTNCODE                       
         LCLC  &TOK1,&TOK2,&OPT                                                 
.*                                                                              
.*       PROCESS INPUT ARGS A BIT                                               
.*                                                                              
&TFLG    SETB  ('&TOKEN' NE '')                                                 
&TOKLEN  SETA  K'&TOKEN                                                         
.*                                                                              
&P1FLG   SETB  ('&PARM1' NE '')                                                 
&P2FLG   SETB  ('&PARM2' NE '')                                                 
         AIF   (NOT &P1FLG).NOP1                                                
&P1VAL   SETA  &PARM1              (Must be numeric)                            
.NOP1    ANOP                                                                   
         AIF   (NOT &P2FLG).NOP2                                                
&P1VAL   SETA  &PARM2              (Must be numeric)                            
.NOP2    ANOP                                                                   
.*                                                                              
.*       PROCESS OPTIONS FIELD                                                  
.*                                                                              
.OLOOP   AIF   (&COUNT GE N'&OPTIONS).ODONE                                     
&COUNT   SETA  &COUNT+1                                                         
&OPT     SETC  '&OPTIONS(&COUNT)'                                               
         AIF   ('&OPT' EQ '').OLOOP                                             
         AIF   ('&OPT' EQ 'A').A                                                
         AIF   ('&OPT' EQ 'A1').A1                                              
         AIF   ('&OPT' EQ 'S').S                                                
         AIF   ('&OPT' EQ 'P').P                                                
         AIF   ('&OPT' EQ 'I').I                                                
         AIF   ('&OPT' EQ 'PI').PI                                              
         AIF   ('&OPT' EQ 'SI').SI                                              
         AIF   ('&OPT' EQ 'LN').LN                                              
         AIF   ('&OPT' EQ 'SYMLN').SYMLN                                        
         AIF   ('&OPT  '(1,2) EQ 'A(').AMATCH                                   
         AIF   ('&OPT  '(1,2) EQ 'V(').VMATCH                                   
         AIF   ('&OPT' EQ 'NULL').NULL                                          
         AIF   ('&OPT' EQ 'PUSH').PUSH                                          
         AIF   ('&OPT' EQ 'END').END                                            
         AIF   ('&OPT' EQ 'DONTBLANK').DONTBL                                   
         MNOTE 8,'&OPT is an unknown SCKW option'                               
         MEXIT                                                                  
         AGO   .OLOOP                                                           
.*                                                                              
.A       ANOP                                                                   
&A       SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.A1      ANOP                                                                   
&A1      SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.S       ANOP                                                                   
&S       SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.P       ANOP                                                                   
&PARM    SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.I       ANOP                                                                   
&I       SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.PI      ANOP                                                                   
&PI      SETB  1                                                                
&I       SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.SI      ANOP                                                                   
&SI      SETB  1                                                                
&I       SETB  1                                                                
.*                                                                              
.LN      ANOP                                                                   
&LN      SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.SYMLN   ANOP                                                                   
&SYMLN   SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.AMATCH  ANOP                                                                   
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'A'                                                              
&MATLAB  SETC  '&OPT'(3,K'&OPT-3)                                               
         AGO   .OLOOP                                                           
.*                                                                              
.VMATCH  ANOP                                                                   
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  '&OPT'(3,K'&OPT-3)                                               
         AGO   .OLOOP                                                           
.*                                                                              
.NULL    ANOP                                                                   
&NULL    SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.PUSH    ANOP                                                                   
&PUSH    SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.END     ANOP                                                                   
&END     SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.DONTBL  ANOP                                                                   
&DONTBL  SETB  1                                                                
         AGO   .OLOOP                                                           
.*                                                                              
.ODONE   ANOP                                                                   
.*                                                                              
.*  PROCESS ROUTINE FIELD                                                       
.*                                                                              
.DORTN   ANOP                                                                   
         AIF   ('&ROUTINE' EQ '').RNULL                                         
         AIF   ('&ROUTINE '(1,1) EQ '''').RCODE                                 
         AIF   ('&ROUTINE '(1,2) EQ 'V(').RV                                    
         AGO   .RNORMAL                                                         
.RNULL   ANOP                                                                   
&RTNGEN  SETB  0                                                                
&RTNFLG  SETB  0                                                                
&RTNTYPE SETC  'A'                                                              
&RTNLAB  SETC  '0'                                                              
         AGO   .RDONE                                                           
.RCODE   ANOP                                                                   
&RTNGEN  SETB  1                                                                
&RTNFLG  SETB  1                                                                
&RTNTYPE SETC  'A'                                                              
&RTNLAB  SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&RTNCODE SETC  '&ROUTINE'                                                       
         AGO   .RDONE                                                           
.RV      ANOP                                                                   
&RTNGEN  SETB  0                                                                
&RTNFLG  SETB  1                                                                
&RTNTYPE SETC  'V'                                                              
&RTNLAB  SETC  '&ROUTINE'(3,K'&ROUTINE-3)                                       
         AGO   .RDONE                                                           
.RNORMAL ANOP                                                                   
&RTNGEN  SETB  0                                                                
&RTNFLG  SETB  1                                                                
&RTNTYPE SETC  'A'                                                              
&RTNLAB  SETC  '&ROUTINE'                                                       
         AGO   .RDONE                                                           
.RDONE   ANOP                                                                   
         MNOTE *,'DEBUG: &RTNGEN &RTNFLG &RTNTYPE &RTNLAB ' , DEBUG             
.*                                                                              
.*       Do a couple of obvious inconsistency checks.                           
.*                                                                              
.*                                                                              
.VCHK1   ANOP                                                                   
         AIF   (NOT (&PARM AND NOT &TFLG)).VCHK2                                
         MNOTE 8,'Must have TOKEN when P option is used'                        
         MEXIT                                                                  
.*                                                                              
.VCHK2   ANOP                                                                   
         AIF   (NOT &PUSH).VCHK3                                                
         AIF   (&TFLG).ERRPUSH                                                  
         AIF   (&A OR &A1 OR &S OR &I OR &PI OR &SI).ERRPUSH                    
         AIF   (&LN OR &SYMLN).ERRPUSH                                          
.*                                                                              
.VCHK3   ANOP                                                                   
         AIF   (NOT (NOT &TFLG AND (&PARM OR &A OR &A1))).VCHK4                 
         MNOTE 8,'Inconsistent SCKW options'                                    
         MEXIT                                                                  
.*                                                                              
.VCHK4   ANOP                                                                   
         AIF   (&RTNFLG).VCHK5                                                  
         AIF   (&TFLG OR &I OR &PI OR &SI OR &LN OR &SYMLN).MISSRTN             
         AIF   (NOT &END).MISSRTN                                               
.VCHK5   ANOP                                                                   
.*                                                                              
.*       PROCESS, SET UP MATCH ROUTINE                                          
.*                                                                              
.DOMAT   ANOP                                                                   
         AIF   (&MATFLG).MMAT                                                   
         AIF   (&PUSH).MPUSH                                                    
         AIF   (&NULL).MNULL                                                    
         AIF   (&PARM AND &I).MPINT                                             
         AIF   (&PARM AND &LN).MPLN                                             
         AIF   (&PARM).MPARM                                                    
         AIF   (&I).MINT                                                        
         AIF   (&LN).MLN                                                        
         AIF   (&SYMLN).MSYMLN                                                  
         AIF   (&TFLG).MTOKEN                                                   
         AIF   (NOT &RTNFLG).MNONE                                              
         AIF   (NOT &TFLG).MALL                                                 
         MNOTE 8,'Match routine not specified.'                                 
         MEXIT                                                                  
.MMAT    ANOP                                                                   
         AIF   (&PARM).CONFLCT                                                  
         AIF   (&PUSH).CONFLCT                                                  
         AIF   (&I OR &PI OR &SI OR  &LN  OR  &SYMLN).CONFLCT                   
         AGO   .MDONE                                                           
.MPUSH   ANOP                                                                   
&MATFLG  SETB  0                                                                
&MATTYPE SETC  'A'                                                              
&MATLAB  SETC  '0'                                                              
         AGO   .MDONE                                                           
.MNULL   ANOP                                                                   
         AIF   (&TFLG).CONFLCT                                                  
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&I OR &PI OR &SI OR  &LN  OR  &SYMLN).CONFLCT                   
&MATFLG  SETB   1                                                               
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MNULL'                                                          
         AGO .MDONE                                                             
.MPINT   ANOP                                                                   
         AIF   (NOT &TFLG).MISSTOK                                              
         AIF   (&LN OR &SYMLN).CONFLCT                                          
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MPINT'                                                          
         AGO   .MDONE                                                           
.MPLN    ANOP                                                                   
         AIF   (NOT &TFLG).MISSTOK                                              
         AIF   (&I OR &PI OR &SI OR &SYMLN).CONFLCT                             
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MPLNO'                                                          
         AGO   .MDONE                                                           
.MPARM   ANOP                                                                   
         AIF   (NOT &TFLG).MISSTOK                                              
         AIF   (&I OR &PI OR &SI OR  &LN  OR  &SYMLN).CONFLCT                   
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MPARM'                                                          
         AGO   .MDONE                                                           
.MINT    ANOP                                                                   
         AIF   (&TFLG).BADTOK                                                   
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&LN  OR  &SYMLN).CONFLCT                                        
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MINT'                                                           
         AGO   .MDONE                                                           
.MLN     ANOP                                                                   
         AIF   (&TFLG).BADTOK                                                   
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&I OR &PI OR &SI OR  &SYMLN).CONFLCT                            
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MLNO'                                                           
         AGO   .MDONE                                                           
.MSYMLN  ANOP                                                                   
         AIF   (&TFLG).BADTOK                                                   
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&I OR &PI OR &SI OR  &LN).CONFLCT                               
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MSYMLN'                                                         
         AGO   .MDONE                                                           
.MTOKEN  ANOP                                                                   
         AIF   (NOT &TFLG).MISSTOK                                              
         AIF   (&PARM).CONFLCT                                                  
         AIF   (&I OR &PI OR &SI OR  &LN  OR  &SYMLN).CONFLCT                   
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MTOKEN'                                                         
         AGO   .MDONE                                                           
.MNONE   ANOP                                                                   
         AIF   (NOT &END).MISSRTN                                               
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&I OR &PI OR &SI OR &LN OR &SYMLN).CONFLCT                      
&MATFLG  SETB  0                                                                
&MATTYPE SETC  'A'                                                              
&MATLAB  SETC  '0'                                                              
         AGO   .MDONE                                                           
.MALL    ANOP                                                                   
         AIF   (&A OR &A1 OR &S OR &PARM).CONFLCT                               
         AIF   (&I OR &PI OR &SI OR &LN OR &SYMLN).CONFLCT                      
&MATFLG  SETB  1                                                                
&MATTYPE SETC  'V'                                                              
&MATLAB  SETC  'MALL'                                                           
&END     SETB  1                   IF ALL SET END FLAG TOO                      
         AGO   .MDONE                                                           
.MDONE   ANOP                                                                   
.*                                                                              
.*       SET UP FLAGS                                                           
.*                                                                              
&KWFLG1  SETA  &P1FLG*X'80'+&P2FLG*X'40'+&MATFLG*X'20'+&RTNFLG*X'10'            
&KWFLG2  SETA  &PUSH*X'80'+&END*X'40'                                           
&KWFLG3  SETA  &A*X'80'+&A1*X'40'+&S*X'20'+&DONTBL*X'10'                        
&KWFLG4  SETA  &PI*X'80'+&SI*X'40'                                              
&KWFLG5  SETA  0                                                                
&KWFLG6  SETA  0                                                                
.*                                                                              
.*                                                                              
         AIF   (&$PCODE).CGEN                                                   
.*                                                                              
.*       Generate the assembly language version.                                
.*                                                                              
&L       DS    0F                                                               
         DC    CL16'&TOKEN '       Token                                        
         DC    AL2(&TOKLEN)        Token length                                 
         DC    AL1(&KWFLG1)        Flags 1                                      
         DC    AL1(&KWFLG2)              2                                      
         DC    AL1(&KWFLG3)              3                                      
         DC    AL1(&KWFLG4)              4                                      
         DC    AL1(&KWFLG5)              5                                      
         DC    AL1(&KWFLG6)              6                                      
         DC    A(&P1VAL)           Parm 1                                       
         DC    A(&P2VAL)           Parm 2                                       
         DC    &MATTYPE.(&MATLAB)  Match routine address                        
         DC    &RTNTYPE.(&RTNLAB)  Routine address                              
.*                                                                              
         AGO   .CODEGEN                                                         
.*                                                                              
.*       Generate the C version.                                                
.*                                                                              
.CGEN    ANOP                                                                   
         AIF   ('MATTYPE' NE 'V').CGEN2                                         
         MNOTE *,'C-EXTERN extern void *&MATLAB ();'                            
.CGEN2   ANOP                                                                   
         AIF   ('RTNTYPE' NE 'V').CGEN3                                         
         MNOTE *,'C-EXTERN extern void *&RTNLAB ();'                            
.CGEN3   ANOP                                                                   
.*                                                                              
         AIF   ('&L' EQ '').CGEN4                                               
         MNOTE *,'C-SCKW struct sckw &L = {'                                    
         AGO   .CGEN5                                                           
.CGEN4   ANOP                                                                   
&SCKWCNT SETA  &SCKWCNT+1                                                       
         MNOTE *,'C-SCKW struct sckw sckw&SCKWCNT = {'                          
.CGEN5   ANOP                                                                   
&TOK1    SETC  '&TOKEN        '(1,8)                                            
&TOK2    SETC  '&TOKEN                '(8,8)                                    
        $CSTR  '&TOK1',8                                                        
         MNOTE *,'C-SCKW &$CSTR,'                                               
        $CSTR  '&TOK2',8                                                        
         MNOTE *,'C-SCKW &$CSTR,'                                               
         MNOTE *,'C-SCKW 0,&TOKLEN,&KWFLG1,&KWFLG2,'                            
         MNOTE *,'C-SCKW &KWFLG3,&KWFLG4,&KWFLG5,&KWFLG6,'                      
&V1      SETA  &P1VAL/X'1000000'                                                
&V1      SETA  &V1-(&V1/X'100'*X'100')                                          
&V2      SETA  &P1VAL/X'10000'                                                  
&V2      SETA  &V2-(&V2/X'100'*X'100')                                          
&V3      SETA  &P1VAL/X'100'                                                    
&V3      SETA  &V3-(&V3/X'100'*X'100')                                          
&V4      SETA  &P1VAL-(&P1VAL/X'100'*X'100')                                    
         MNOTE *,'C-SCKW &V1, &V2, &V3, &V4,'                                   
&V1      SETA  &P2VAL/X'1000000'                                                
&V1      SETA  &V1-(&V1/X'100'*X'100')                                          
&V2      SETA  &P2VAL/X'10000'                                                  
&V2      SETA  &V2-(&V2/X'100'*X'100')                                          
&V3      SETA  &P2VAL/X'100'                                                    
&V3      SETA  &V3-(&V3/X'100'*X'100')                                          
&V4      SETA  &P2VAL-(&P2VAL/X'100'*X'100')                                    
         MNOTE *,'C-SCKW &V1, &V2, &V3, &V4,'                                   
         MNOTE *,'C-SCKW &MATLAB, &RTNLAB };'                                   
.*                                                                              
.CODEGEN ANOP                                                                   
         AIF   (NOT &RTNGEN).EXIT                                               
&RTNLAB  DATA  BEGIN                                                            
***      DPROC &RTNLAB                                                          
         GEN   &ROUTINE                                                         
         CLEAR R15                                                              
         BR    RAR                                                              
***      DPROCEND                                                               
         END                                                                    
.EXIT    MEXIT                                                                  
.*                                                                              
.*  Errors.                                                                     
.*                                                                              
.ERRPUSH MNOTE 8,'PUSH option inconsistent with other options.'                 
         MEXIT                                                                  
.MISSTOK MNOTE 8,'Missing TOKEN specification.'                                 
         MEXIT                                                                  
.BADTOK  MNOTE 8,'TOKEN must not be specified.  Conflicting options.'           
         MEXIT                                                                  
.MISSRTN MNOTE 8,'Missing SCKW ROUTINE specification.'                          
         MEXIT                                                                  
.MISSMAT MNOTE 8,'Missing MATCH ROUTINE specification.'                         
         MEXIT                                                                  
.CONFLCT MNOTE 8,'Conflicting SCKW options specified.'                          
         MEXIT                                                                  
         MEND                                                                   
