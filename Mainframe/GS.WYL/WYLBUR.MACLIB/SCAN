         MACRO                                                                  
&L       SCAN  &SCKWS,&TABLES=PUSH                                              
.*                                                                              
.*       New scanner, 1/90                                                      
.*       Last update, 6/90                                                      
.*                                                                              
.* THE &TABLE OPTION WORKS AS FOLLOWS:                                          
.*    PUSH/NOPUSH MEANS PUSH AND POP THE PREVIOUS SKIP/STOP/SCKWS               
.*       TABLES.                                                                
.*    SET MEANS SET THE SKIP/STOP/SCKWS TABLES.  SKIP/STOP                      
.*       TABLES ARE SET TO THE DEFAULT. THE SCKW IS SET FROM &SCKW              
.*    NOSET MEANS DO NOT SET (IE. RESET) THE SKIP/STOP/SCKW TABLES              
.*       USE THE SETSKIP,SETSTOP,SETSCKWS TO SET THESE TABLES                   
.*    THESE OPTIONS ARE SPECIFIED AS FOLLOWED:                                  
.*       PUSH/SET -- USE TABLES=PUSH (THE DEFAULT)                              
.*       PUSH/NOSET -- ?? NONSENSE (WHY PUSH WHEN NOT RESET)                    
.*       NOPUSH/SET -- USE TABLES=NOPUSH (RARELY NEVER? USED)                   
.*       NOPUSH/NOSET -- USE TABLES=NOSET (USER SETS BY SELF)                   
.*                                                                              
.*                                                                              
         GBLB  &$PCODE             Assembly language or C flag                  
.*                                                                              
.*                                                                              
&L       SCCBADR R1                                                             
         CLEAR R0                                                               
.*                                                                              
.* DO NOSET,                                                                    
         AIF   ('&TABLES' NE 'NOSET').DOSET                                     
         VCALL NSCAN                                                            
         AGO   .MDONE                                                           
.DOSET    ANOP                                                                  
.*                                                                              
.* DO PUSH                                                                      
         AIF   ('&TABLES' NE 'PUSH').NOPUSH                                     
         SCPUSH TABLES                                                          
         ST    R0,NSCNSKIP-NSCNCB(R1)                                           
         ST    R0,NSCNSTOP-NSCNCB(R1)                                           
.NOPUSH  ANOP                                                                   
.*                                                                              
.* SET SCKWS IF ANY                                                             
         AIF   ('&SCKWS' EQ '').NOKWS                                           
         AIF   (&$PCODE).CGEN                                                   
         L     R14,=A(&SCKWS)                                                   
         ST    R14,NSCNKWS-NSCNCB(R1)                                           
         AGO   .KWSDONE                                                         
.CGEN    ANOP                                                                   
         MNOTE *,'C-CODE R14 = (long int)((char *)( && &SCKWS )); '             
         ST    R14,NSCNKWS-NSCNCB(R1)                                           
         AGO   .KWSDONE                                                         
.NOKWS   ANOP                                                                   
         ST    R0,NSCNKWS-NSCNCB(R1)                                            
.KWSDONE ANOP                                                                   
.*                                                                              
.*                                                                              
         VCALL NSCAN                                                            
.*                                                                              
.* DO POP                                                                       
         AIF   ('&TABLES' NE 'PUSH').NOPOP                                      
         SCPOP TABLES                                                           
         LTR   R15,R15                                                          
.NOPOP   ANOP                                                                   
.MDONE   ANOP                                                                   
         MEND                                                                   
