//WINGRX JOB
// EXEC ASMCLG
//ASM.SYSLIB DD DSN=WYL.GA.MCL.IOPROGM.MACLIB
WINGRX   CSECT
         REGISTER
         ENTER BASE=12
         SR    R1,R1
         MCALL MSGOPEN             Open SYSPRINT
         SPACE
         RACROUTE REQUEST=EXTRACT,TYPE=EXTRACT,CLASS='DATASET',        *
               FIELDS=RFIELDS,RELEASE=1.9,ENTITY=DSNAME,GENERIC=YES,   *
               WORKA=WORKA,MATCHGN=YES,MF=S
         LR    R10,R0              Save R0
         LR    R11,R15             RC
         LA    R4,0(R1)            ->returned data (lo 3 bytes)
         L     R5,0(R4)            L'data
         ST    R0,X1
         ST    R15,X2
         MVC   X3(8),IHB0004A         codes from RACF
         LA    R1,X1               ->regs
         ST    R1,DUMPARMS
         LA    R1,DUMPARMS
         MCALL IOPDUMPR
         ST    R4,DUMPARMS         ->dat for dumper
         STA   R5,DUMLEN           L'dat (lo 3 bytes)
         ST    R4,DUMOFF           show actual address
         EDIT  MSRC,(R15),DW
         MVC   MSDSN,DSNAME        Copy (modified?) dsn
         SYSPRINT MSG1
         LTR   R4,R4               ->data if any
         BZ    OUT
         LA    R1,DUMPARMS
         MCALL IOPDUMPR
         MCALL MSGCLOSE
         LR    R0,R5               L'area
         LR    R1,R4               ->area
         FREEMAIN R,LV=(0),A=(1)   free the area
OUT      EXIT  RC=(R11)
         EJECT
DW       DC    D'0'
DSNAME   DC    CL44'WYL.GA.MCL.WINGS.LIB'
MSG1     MSG   ' DSN='
MSDSN    DC    CL44' ',C' RC was'
MSRC     DC    CL4' ',C' RACF data follows:'
         MSGEND
X1       DC    F'0'
X2       DC    F'0'
X3       DC    2F'0'
         SPACE
*  Field names for RACF
         SPACE
RFIELDS  DC    A(#RF)
RF0      DC                 CL8'CREADATE',CL8'AUTHOR',CL8'LREFDAT'
         DC    CL8'LCHGDAT',CL8'UNIVACS',CL8'GROUPNM',CL8'DSTYPE'
         DC    CL8'NOTIFY',CL8'USERID',CL8'USERACS'
#RF      EQU   (*-RF0)/8
         SPACE
*  Parms for dumper
         SPACE
DUMPARMS DC    A(0)                ->data
DUMLEN   DC    F'16'               L'data
DUMOFF   DC    A(0)                Offset for dump label
DUMFLG   DC    AL1(2),AL3(132)     flags, line length
DUM@PRT  DC    V(IOPSYSPR)         V(sysprint writer)
DUM@TRN  DC    A(1)                ->translate table
         SPACE
WORKA    DS    XL512
         END
//LKED.SYSLMOD DD DSN=WYL.GA.MCL.LINKLIB(WINGRX),DISP=SHR,SPACE=
//LKED.OBJECT DD DSN=WYL.GA.MCL.IOPROGM.OBJECT,DISP=SHR
//LKED.SYSIN DD *
 INCLUDE OBJECT(IOPSYSPR)
 INCLUDE OBJECT(IOPDUMPR)
 SETCODE AC(1)
//GO.SYSPRINT DD SYSOUT=*
//GO.SYSUDUMP DD SYSOUT=C
