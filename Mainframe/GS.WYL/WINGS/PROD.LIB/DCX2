//ASMDCX JOB MCL$GA
// EXEC ASMCLG
//SYSLIB   DD  DSN=SYS4.MACLIB,DISP=SHR
//         DD  DSN=WYL.GG.SYS.MACLIB,DISP=SHR
//         DD  DSN=SYS3.IS.MACLIB,DISP=SHR
//         DD  DSN=SYS3.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,UNIT=DISK,DISP=SHR
FRED     CSECT
         REGISTER
         ENTER BASE=12
*         SR    R2,R2
*         L     R3,=X'7FFFFFFF'
*         M     R2,=F'1000000000'
*         AL    R3,=F'999999999'
*         QSNAP (R2,R3)
         LA    R11,#N              Loop counter
         LA    R10,N1              ->First entry
LOOP     LM    R0,R1,0(R10)        Load an entry
         CLC   0(8,R10),BIGNUM     check for too big
         BNH   NUMOK
* here if too big
         MVC   FLD1,=C'****************'
         MVC   FLD0,=C'****************'
         B     DONE
         SPACE
NUMOK    EQU   *
         D     R0,=F'1000000000'
         CVD   R1,DW1              (quotient) hi part
         CVD   R0,DW0              (remainder) lo part
         MVC   FLD1,PTNH
         MVC   FLD0,PTNB
         LTR   R1,R1               Any high part
         BZ    NOHI                No...
         MVC   FLD0,PTN0           have high part, fix low part
NOHI     EQU   *
         ED    FLD1,DW1
         ED    FLD0,DW0+3
DONE     QSNAP FLD1,32
         LA    R10,8(R10)          ->next
         BCT   R11,LOOP
         EXIT
         DC    0D'0'
FLD1     DC    CL16' '
FLD0     DC    CL10' '
PTNB     DC    X'40202020202020202120'  Pattern for leading blnks
PTN0     DC    X'F0212020202020202020'  ...leading zeros
PTNH     DC    X'40202020202020202020202020202020'
DW0      DC    D'0'
DW1      DC    D'0'
N1       DC    F'0',F'987654321'
N2       DC    F'0',F'2147483647'
N3       DC    F'0',X'80000000'
N4       DC    F'0',X'FFFFFFFF'
N10      DC    F'1',F'0'
N1F      DC    F'1',X'FFFFFFFF'
         DC    X'0000000F',X'FFFFFFFF'
         DC    X'000000FF',X'FFFFFFFF'
         DC    X'000001FF',X'FFFFFFFF'
         DC    X'000003FF',X'FFFFFFFF'
         DC    X'000007FF',X'FFFFFFFF'
         DC    X'00000FFF',X'FFFFFFFF'
NFF      DC    X'00038D7E',X'A4C67FFF'  99,999,999,999,999
NXF      DC    X'00038D7E',X'A4C68000' 100,000,000,000,000
         DC    X'07FFFFFF',X'FFFFFFFF'
**       DC    X'1DCD64FF',X'C4653600' (2**31-1)*(10**9)
BIGNUM   DC    X'1DCD64FF',X'FFFFFFFF' above plus 999999999
         DC    X'1DCD6500',X'FFFFFFFF' a bit too far...
         DC    X'FFFFFFFF',X'FFFFFFFF'
#N       EQU   (*-N1)/8            No. entries
         DC    2F'0'
E9       DC    F'1000000000'
E6       DC    F'1000000'
         END
//GO.SYSUDUMP DD SYSOUT=A
