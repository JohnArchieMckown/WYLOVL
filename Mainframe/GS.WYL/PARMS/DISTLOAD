;
;   DISTLOAD exec file.  (This is WYL.GS.WYL.PARMS#DISTLOAD.)
;
;   This exec file builds a public record group with the name of
;   all mail distribution lists.  Also see
;   WYL.GS.WYL.LIB#MAKEDIST.
;
;   It is called from the initialization exec.
;
;   Written 10/87, Niz
;
;   Modifications:
;     10/22/87  Niz   Add whois names.
;     12/02/87  jws   Comment out whois stuff
;     12/14/87  Niz   Add mail.aliases.
;      1/09/89  Niz   Minor changes.
;      8/30/91  jws   SUH aliases load
;

xproc () begin
   on error cmd 'xreturn error errmsg="Error in distload exec." '

   open
   rg = rec_open('public')


;------------------------------------------
;  On SYSA, load SUH mail aliases

   if (mach eq 'SYSA') begin
      open
      try use WYL.GS.WYL.SUH.ALIASES
      try delete ~'C1'1 and ~'CV'1 and ~'AZ'1
      if (lines ne 0) begin
         try rec_load nodup into &(abs(rg)) xpublic  ; load it up
      end
      close
   end
;------------------------------------------

;------------------------------------------
;  Now load EMS distribution list names (overriding whois if
;      necessary.

   if (machine eq 'SYSA') begin
      clear active
      try use wyl.gs.wyl.distlist ; list of distribution lists
      if (lines ne 0) begin
         delete ';'1 or '#'1 or '' nolist
         rec_load nodup into &(abs(rg)) xpublic  ; load it up
      end
   end
;------------------------------------------

;------------------------------------------
;  Load mail aliases list last to allow them to override.

   clear active
   try use wyl.gs.wyl.mail.aliases
   if (lines ne 0) begin
      delete ';'1 or '#'1 or '' nolist
      rec_load nodup into &(abs(rg)) xpublic  ; load it up
   end
;------------------------------------------


   old_distno = 0
   if (vartype('pub.mail_distno') ne 'UNDEFINED') \
      old_distno = pub.mail_distno

   pub.mail_distno = rg            ; new record group takes effect!

   if (old_distno ne 0) begin
      write 'New distlist record group is ' || rg \
         || ', ' || rec_count(rg) || ' records.'
      write 'Old distlist record group was ' || old_distno \
         || ', ' || rec_count(old_distno) || ' records.'
      wait 30                      ; paranoia t.t.m.
      rc = rec_close(old_distno)
      write '(Closed, rc=' || rc || ').'
   end

   close
end
