;
;   WCharge table exec file.  (This is WYL.GS.WYL.PARMS#WCHARGE.)
;
;   This exec file builds a public record group of identifiers
;   for processes that take on special WCHARGE values.  It is used
;   by processors like Prism and Folio to translate internal
;   application identifiers into proper SET WCHARGE values.
;
;   The source data for this exec is WYL.GQ.DBS.WCHARGE.SOURCE.
;
;   It is called from the initialization exec.
;
;   Written 11/89, ltm
;
;   Modifications:
;

xproc () begin
   on error cmd 'xreturn error errmsg="Error in wcharge exec." '
   set exec auth

   if (machine eq 'RLG') xreturn

   open
   rg = rec_open('public')


;------------------------------------------
;  Now load wcharge table

   try use wyl.gq.dbs.wcharge.source     ; table of wcharge values
   if (lines ne 0) begin
      delete ';'1 or '#'1 or '' nolist
      ch 44/230 to '' nolist
      squash nolist
      ch '= ' to '=' nolist
      rec_load nodup into &(abs(rg)) xpublic  ; load it up
   end

;------------------------------------------


   old_wcharge_id = 0
   if (vartype('pub.wcharge_id') ne 'UNDEFINED') \
      old_wcharge_id = pub.wcharge_id

   pub.wcharge_id = rg            ; new record group takes effect!

   if (old_wcharge_id ne 0) begin
      write 'New wcharge list record group is ' || rg \
         || ', ' || rec_count(rg) || ' records.'
      write 'Old wcharge list record group was ' || old_wcharge_id \
         || ', ' || rec_count(old_wcharge_id) || ' records.'
      wait 10                      ; paranoia t.t.m.
      rc = rec_close(old_wcharge_id)
      write '(Closed, rc=' || rc || ').'
   end

   close
end
