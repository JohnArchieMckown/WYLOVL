;
;  BATMAIL EXEC FILE.
;
;     This exec file is run as a BATWYL job to process mail from
;     another machine.  The mail is passed as "terminal" input
;     ending with a line which contains only a ".".
;
;
;  Written 9/84; NIZ
;
;  Modifications:
;     12/17/84 Niz  For SYSA/RLG split.
;     09/16/85 Niz  For RLG's SYSB/SYSE split.
;     06/16/86 Niz  For RLG's SYSB/SYSE join.
;     11/13/86 Niz  For long titles (WYLBUR changed too).
;

;**********************
;*  Initialization    *
;**********************
;
define $goto as "point ';" append " '1(1) exec nolist" replace
;
set escape &
clear values
clear active
title = ''

;******************************************************************
;*                                                                *
;*  Read mail from the input stream.                              *
;*                                                                *
;******************************************************************
;READMAIL
read string s0 prompt '. '
if (s0 eq '.') $goto SENDMAIL
if (sub(s0,1,7) eq 'NOTIFY:') set val s8 sub(s0,8) ; notify acct
then end &s8
then change '@SYSB' to '@RLG' in last nolist
then read string s8 using last delete
then $goto READMAIL
if (sub(s0,1,6) ne 'TITLE:') $goto NOT_TITLE
title = strip(sub(s0,8)) ; title
if (index(title,'"') eq 0) title = '"'||title||'"'
then $goto READMAIL
if (index(title,"'") eq 0) title = "'"||title||"'"
then $goto READMAIL
title = '"'||translate(title,"'",'"')||'"'
$goto READMAIL
;NOT_TITLE
if (sub(s0,1,8) eq 'COMMAND:') set val s9 sub(s0,9) ; command text
then $goto READMAIL
end &(sub(s0,2))
$goto READMAIL

;******************************************************************
;*                                                                *
;*  Now send the mail.                                            *
;*                                                                *
;******************************************************************
;
;SENDMAIL
if (title ne '') s9 = s9||' title '||title
if (lines eq 0) putend,
write 'Do: '||s9
if (s9 ne '') try &s9 collect 10001
;
10000,--- Mail command results ---
list 10000/last unn
delete 'Note: ' in 10000/last nolist
delete '' in 10000/last nolist
delete 'Mail for '1 in 10000/last nolist
number 10000/last
if (last eq 10000) $goto DONE
if (last ne 10001) $goto ERROR
if (s8 ne '') try $send &s8 From &(machine) mailer: &(line(10001))
;
;DONE
clear active
logoff clear
;
;ERROR
write '--- Batmail error ---'
write ''
write 'Variables...'
write '  S0 = "'||s0||'"'
write '  S8 = "'||s8||'"'
write '  S9 = "'||s9||'"'
write ''
write 'Active file...'
list
clear active exec
