;
;  makedist -- Make Wylbur mail distribution list table from
;     EMS data base.
;
;     Modifications:
;
;        9/14/89  Niz  Update for new EMS.
;        7/10/90  Niz  Change dist-name to dist.name.
;        7/09/91  jws  add better distlist.raw check for SYSA
;        5/03/94  jnk  add set delta for SPIRES
;        5/17/94  jnk  added another set delta to reset afterwards
;       12/17/03  slp  comment out reasonableness check

xproc ()  begin
;   on error cmd 'xreturn error'
   if (lines ne 0) open

;  First get the information from Spires.

   call spires 'bw' clear
   /set delta 0.01
   set nolist
   select ems distribution
   set element dist.name, account
   for subfile
   try in active clear display all
   endfor
   exit
;  check for reasonable sized result...
;  if (lines lt 100) pcall panic('ems distribution error')
   if (lines lt 1) putend ;
;  if (mach eq 'SYSA') begin
;     p 'DIST.NAME = Z' n
;     if (* le 0)  pcall panic('ems distribution error')
;  end
   scr wyl.gs.wyl.distlist.raw
   save wyl.gs.wyl.distlist.raw replace

;  This is highly dependent on the format of the unloaded EMS
;  distribution list obtained from Spires.  Unfortunately, it
;  also uses some tricky Wylbur change commands to re-format the
;  data.  If you are trying to figure out what is going on here,
;  look at an example of the before and after data.

   squash all
   change ' '1 to '' nolist
   delete '****                  '1 nolist
   change ';                     ' to '' nolist
   change 'ACCOUNT = '1  to '' nolist
   change 'DIST.NAME = '1 to ' ' nolist
   align all length 100
   putend
   ; change 200 to '<<>>' in ~' '1 and ~'' nolist
   ; squash all
   change '    '(1) to ' <<>>' in ~' '1 and ~'' nolist
   if (line(first) eq '') delete first  ; delete leading blank lines
   if (line(first) eq '') delete first
;
   lno = first
   while (lno le last) begin
      ltext = upper(strip(line(lno)))
      delete &(lno)
      lno = lno_first(lno)
      if (lno lt 0) goto finished
      point '' (1) in &(lno)/last nolist
      lend = lno_prev(*)
      if (lno le lend) begin
         change 1 to \
            "&(ltext) = (" in &(lno) nolist
         if (lno lt lend) \
            change 1 to \
            "&(ltext)-#<<def>> = (" \
            in &(lno+.001)/&(lend) nolist
         change " <<>>" to ')' in &(lend) nolist
         change " <<>>" \
            to " &(ltext)-#<<num>>)" \
            in &(lno)/&(lend) nolist
         change '<<num>>' to '002'+1 \
            in &(lno)/&(lend) nolist
         change "<<def>>" to '002'+1 \
            in &(lno)/&(lend) nolist
      end

      lno = lno_next(lno_next(lend))
   end
finished:
   if (lines lt 1) putend ;
   save wyl.gs.wyl.distlist replace
   close
   set delta 1
end


;  Panic -- Something bad happened.  Return an error to the
;     caller.

proc panic(msg) begin
   xreturn error errmsg = '&(msg)'
end

