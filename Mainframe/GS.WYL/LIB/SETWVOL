;  setwvol - select best volumes for wylbur wvol list
;
;  This xproc is invoked nightly but I haven't yet found out how.
;
;  Modifications:
;
;  07/13/95  slp  Clean up which volume series are chosen on which
;                 system; remove PUB1 and PUBT from SYSA list.
;
;  01/26/04  slp  Add documentation and clean up select list.
;                 Also message with clues to who's running this.

xproc () begin
   dcl str s1,s2
   send gg.slp JOBNAME &(vjobname) ACCT &(acct) NAME &(lname)
   write 'Select best volumes'
   write ''
   if (lines ne 0) read prompt 'Enter Save for ACTIVE? '
   clear active
;
   dump volumes
   align all len 1 even space
   ch 'PUBLIC' to '' n
   delete 'RLGPAK'1
   if (mach eq 'SYSA') delete ~'PUB'1
   if (mach eq 'SYSA') delete 'PUB1'1
   if (mach eq 'SYSA') delete 'PUBT'1
   if (mach eq 'SYSA') delete 'PUBC'1
   if (mach eq 'SYSB') delete ~'PUB'1
   if (mach eq 'SYSB') delete ~'RLG'1
   if (mach ne 'SYSC') delete 'PUBC'1
   number
;
   w0=first
   w1=last
lx13:
   dump tracks on &(line(w0))
   w0=w0+1
   if (w0 le w1) goto lx13
;
   delete f/&(w1)
   number
   100,-------------
   s0=''
   n1=0
lx19:
   p 'PUB'1 or 'RLG'1 (1) in 1/100 n
   if (* lt 0) goto lx100
   rea string s1 using * delete
   if (siz(s1) gt 6) goto lx19
   rea string s2 using *+1 delete
   n0=sub(s2,1,index(s2,' ')-1)                                            ; no. of tracks available
   &(99999-n0).&(n1) &(s1)
   n1=n1+1
   goto lx19


lx100:
   delete f/100
   align f/&(99999-700) len 235 eve spa
   s0=lin(first)                                                           ; all volumes with gt 500 tracks
   if (size(s0) gt 7*5) s0=sub(s0,1,7*5)
;
   set wvolumes &(s0)
end
