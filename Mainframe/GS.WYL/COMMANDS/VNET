;WYL.GA.PUB.COMMANDS#VNET
;   created on 12/08/83 by Jim Stosick, ITS Systems
;   rewritten on 08/26/87 by Lincoln Ong, DC Operations/OTS
;   updated on 02/01/91  Version 2.07
;This EXEC file implements a pseudo WYLBUR command, VNET.   All options and messages are
;in WYLBUR format.
;
;   VNET message
;
;Revision History:
;   12/83/83 GG.JWS V1.00  Initial release.
;   08/26/87 GG.LSO V2.00  Rewritten.
;   02/19/88 GG.LSO V2.06  Added additional error handling.
;   02/21/91 GG.LSO V2.07  Miscellaneous cleanup.
;
xproc () begin
   on attn command='pcall ATTN_Exit'
   on error command='pcall Error_Exit'

   declare string X.ExecDsn            ;EXEC file data set name.
   declare string X.ExecName           ;EXEC file name.
   declare string X.ExecBanner         ;EXEC file banner.
   declare string X.ExecVersion        ;EXEC file version.
   declare string X.Contact            ;Contact person.
   declare string X.DebugAcct          ;Maintenance account.
   declare string X.PCmdName           ;Psuedo command name.
   declare boolean X.Debug             ;Debug mode [not used].

   X.ExecDsn = 'WYL.GA.PUB.COMMANDS#VNET'
   X.ExecName = 'VNET'
   X.ExecBanner = 'VNET  Revised 21 February 1991  Version 2.07'
   X.ExecVersion = 'V2.07 02/21/91'
   X.Contact = 'Lincoln Ong, DC Operations/OTS'
   X.DebugAcct = 'GG.LSO'
   X.PCmdName = 'VNET'

   if (strip(parm_string) ne '') try $cmd VNET &(parm_string)
   else xreturn error errmsg='Missing &(X.PCmdName) options.'
end



;<.PAGE>
proc ATTN_Exit() begin
;Process any ATTNs entered during the execution of the EXEC file.
;
   comment ...
   xreturn
end


;<.PAGE>
proc Clean_Up() begin
;Close any Active files and restore user to his original Active file.
;
   if (actno ne X.OrigActNum) begin
      close
      pick &(X.OrigActNum)
   end
end


;<.PAGE>
proc Error_Exit() begin
;Display the error condition and current variable settings when an error condition arises.
;
   declare number SLine                ;Starting line line number for variable list.

   write s'2F'||'An error has occurred in processing the &(X.PCmdName) command.'
   open ErrorLog title='&(X.ExecName) EXEC file error log' temporary
   putline end &(X.ExecName) EXEC file
   putline end &(X.ExecDsn)
   putline end &(X.ExecVersion)
   putline end
   putline end Debugging information:
   putline end    errmsg = '&(errmsg)'
   putline end    errinfo = '&(errinfo)'
   putline end
   putline end    Variables:
   SLine = end

   dump variables type X               ;Display global variables.
   putline end
   dump variables type error           ;Display variables at time of error.
   change 1 to '   ' in &(SLine)/last nolist

   if (xaccount ne X.DebugAcct) begin
      try mail to &(X.DebugAcct) subject='&(X.ExecName) EXEC file error' quiet
      close
      write s'15'||'The programming staff has been notified about the problem.  Processing terminated.'
      pcall Clean_Up
      xreturn                          ;Exit the EXEC file.
   end
   xpause msg='[DEBUG mode; error condition occurred.  Execution pause.]'
   clear error                         ;Reset error environment.
   on error reenable
   close
end


