;
;;  FINGER -- Finger command.
;;
;;    This FINGER exec is designed to work the same as the
;;    Unix and TOPS "WHOIS" programs.
;;
;;    Take a look at the REQUEST subroutine called below which
;;    really does all the work in handling the network FINGER
;;    processing.
;;
;;
;;
;;  Written, 2/87, Niz
;;
;;  Modifications:
;;    3/29/87  Niz   Use EXECCMD proc instead of %command.
;;    7/15/87  Niz   Change to DUMP LINES to avoid ATTN problem
;;                     with "try show lines" command.
;;    1/11/88  Niz   Minor bug fix for "finger xxx-yyy" type
;;                     command.
;;    4/10/88  Niz   New version--now uses direct TCP interface.
;



xproc () begin
   x.origact = actno
   x.attn = 0                         ; attn counter (see attnexit)

   x.pgm = 'FINGER'                   ; program name
   x.maint = '(netexec-maint)'               ; maintenance people

   x.pno = 0

   on error cmd 'xcall wyl.gs.wyl.net#\
      _error(x.pgm, x.maint)'
   on attn  cmd 'xcall wyl.gs.wyl.net#\
      _attn(x.pgm, x.maint, x.attn)'

   port = 79                          ; finger tcp port number

   ttl = 'finger ' || squash(tran(parm_string,'"',"'"))
   open finger title '&(ttl)' temp

;
;  Handle non-network FINGER command.
;

   atpos = index(parm_string, '@')

   if (atpos eq 0) begin
      if (parm_string eq '') begin
         try dump lines
      end
      else begin
         try dump line &(parm_string)
      end
   end

;
;  Handle network FINGER command.
;

   else begin
      username = strip(substr(parm_string, 1, atpos-1))
      hostname = strip(substr(parm_string, atpos+1))

      rc = ''
      xcall wyl.gs.wyl.net#_request(x.pgm, \
         hostname, port, username, rc)
   end

   try list unn

   close

   pcall cleanup('')
end



; <.page>
;
;  cleanup -- This routine is always called to return to the
;    user.  It handles restoring the user state.
;

proc cleanup(opt) begin
   pick &(x.origact)
   if (opt eq 'error') \
      opt = "error errmsg=''"
   xreturn &(opt)
end
