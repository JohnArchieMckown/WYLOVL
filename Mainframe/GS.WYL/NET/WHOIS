;
;;  WHOIS -- Network name lookup service.
;;
;;    This WHOIS exec is designed to work the same as the
;;    Unix and TOPS "WHOIS" programs.
;;
;;    The REQUEST subroutine really does all the work.
;;
;;
;;  Created, 3/29/87, Niz
;;
;;  Modifications:
;;     8/08/87  Niz     Add SYSC check.
;;     4/10/88  Niz     Second version, uses a direct TCP
;;                        connection.
;;     5/04/89  Niz     Add %%%%DUMP for "dump whois" command.
;;     3/26/90  Niz     Allow whois "set of words" syntax.
;;     5/30/90  Niz     Change host name back to "argus".
;



xproc () begin
   x.origact = actno
   x.attn = 0                         ; attn counter (see attnexit)

   x.pgm = 'WHOIS'                    ; program name
   x.maint = '(netexec-maint)'               ; maintenance people

   x.pno = 0

   on error cmd 'xcall wyl.gs.wyl.net#\
      _error(x.pgm, x.maint, errid, errmsg, errinfo)'
   on attn  cmd 'xcall wyl.gs.wyl.net#\
      _attn(x.pgm, x.maint, x.attn)'

   nicname = 'directory.stanford.edu'  ; whois server host name
   port = 43                          ; whois tcp port number

;
;  Right now SYSC doesn't have a SUNet interface.
;

   if (machine eq 'SYSC') begin
      write x.pgm || ' can not be executed on this machine.'
      write 'The ' || machine \
         || ' machine does not have a direct SUNet connection.'
      xreturn
   end

;
;  Process %%%%DUMP prefix (which means it is a "dump whois"
;    command).
;
   pstring = parm_string

   if (left(pstring,9) eq '%%%%DUMP ') begin
      pstring = substr(pstring,10)
      col_opts = 'collect'
      try syscall collect_scan(col_opts)
      ; if not ok to clear, cleanup and exit
      if (tryattn or tryerr) begin
         pcall cleanup('error')
      end
   end

;
;  Scan WHOIS parameters (big deal).
;

   whoname = strip(pstring)

   if (whoname eq '') begin
      write 'Missing "name" after &(x.pgm).  Type \
         "HELP &(x.pgm)" for more information.'
      pcall cleanup('error')
   end

   if (left(whoname,1) eq '"' or left(whoname,1) eq '''') begin
       try whoname = &(whoname)
   end
;
;  Issue the network request (responses will come back in the
;    current Active file).
;

   open whois title 'whois &(tran(whoname,'"',"'"))' temp

   rc = ''
   xcall wyl.gs.wyl.net#_request (x.pgm, \
      nicname, port, whoname, rc)

   if (lines gt 0) try list unn
   else try write 'No entries like "' || whoname || '" found.'

   close

   pcall cleanup('')
end



; <.page>
;
;  cleanup -- This routine is always called to return to the
;    user.  It handles restoring the user state.
;

proc cleanup(opt) begin
   pick &(x.origact)
   if (opt eq 'error') \
      opt = "error errmsg=''"
   xreturn &(opt)
end
