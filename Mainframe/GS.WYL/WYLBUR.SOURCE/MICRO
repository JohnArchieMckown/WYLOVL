MICRO    TITLE 'WYLBUR''s Micro-computer Commands'                              
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLMICRO CSECT                                                                  
         SPACE 2                                                                
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
         SYSDEFN ,                 Define installation params                   
*                                                                               
MICR     IDENT 2025                11:49:25 01/25/02  (SLP)                     
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL             Copy CV/CP                                   
         POP   DSECTS                                                           
         TITLE 'UPLOAD Command'                                                 
*box                                                                            
*                                                                               
*  UPLOAD -- UPLOAD Command.                                                    
*                                                                               
UPWA     RECORD BEGIN                                                           
UPFLAG   FLAG                                                                   
         FLAG  UPFDATTN            - Disallow read attns                        
*                                                                               
UPPRLEN  DS    H                   Prompt text length                           
UPPROMPT DS    CL(&LINESZ)         Prompt text                                  
*                                                                               
UPTMOUT  DS    F                   Timeout value (sec*100)                      
*                                                                               
UPPUTCB  PUTCB PFX=UP              PUTBYTES options                             
*-                                                                              
*-       Be careful about increasing UPBUFSZ#.  The ALF front-ends              
*-         currently will not accept larger input buffer                        
*-         specifications than 2K for TRANSPARENT reads                         
*-         (ask Mike Durket to explain it).                                     
*-                                                                              
UPBUFSZ# EQU   2048,,C'N'          Maximum input buffer size                    
         END                                                                    
*-                                                                              
UPLOAD   XPROC UPWA                                                             
         LA    R6,UPWA                                                          
         USING UPWA,R6                                                          
         CLEAR UPWA                                                             
         ST    WAR,CPCWA           Save UPWA ptr                                
*                                                                               
         CLEAR R0,R1               Initialize RC_UPLOAD to null                 
         LA    R2,=CL16'SYS.RC_UPLOAD'                                          
         VCALL SAVEDATA            Set variable                                 
*                                                                               
         VCALL ENDLNO              Get END lineno                               
         ST    R0,UPLNO            Save default starting point                  
*                                                                               
         MVC   UPMAXLN,=H'230'     Max line length before split                 
*                                                                               
         SCAN  UPLPRT              Scan UPLOAD options                          
         SCANCHK                                                                
*-                                                                              
*-       Set terminal options for upload.                                       
*-                                                                              
         SET   CPFCLEAN,CPFTRAN    Clean, transparent                           
         IF    UPFNECHO,'SET CPFNECHO'  Noecho                                  
         IF    UPFDATTN,'SET CPFDATTN'  Disallow input attns                    
         MVC   CPTRMIN,=AL2(UPBUFSZ#)  Maximum input buffer size                
*                                                                               
         L     R15,CVASCTBL        EBCDIC to ASCII translate table              
         TR    UPPROMPT,@R15       Convert to ASCII                             
*-                                                                              
*-       Get first/next line from terminal.                                     
*-                                                                              
         LOOP  BEGIN                                                            
         TREAD UPPROMPT,LH:UPPRLEN,TIMEOUT=L:UPTMOUT  Get more input            
         BM    CVABORT             Logging off, scram                           
         IF    POS,BEGIN           Attn/timeout...                              
         SETMSG 'ATTN'                                                          
         IF    (R15,EQ,101),'SETMSG "TIMEOUT"'                                  
         B     UPLRSET                                                          
         END                                                                    
*                                                                               
         SETMSG (R1),(R0)           Buffer                                      
         LA    R15,UPPUTCB         PUTCB                                        
         VCALL PUTBYTES            Save buffer in the Active file               
         IF    NZ,EXIT             Time to quit                                 
         END                                                                    
*                                                                               
         SETMSG 'OK'                                                            
UPLRSET  LA    R2,=CL16'SYS.RC_UPLOAD'                                          
         VCALL SAVEDATA                                                         
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
*-                                                                              
*-       UPLOAD command options.                                                
*-                                                                              
UPLPRT   SCKW  COUNT,UPXCOUNT,(A,P,PI)                                          
         SCKW  PROMPT,UPXPRMT,(A,P)                                             
         SCKW  STOP,UPXSTOP,(A,P)                                               
         SCKW  WRAP,UPXWRAP,(A,P,PI)                                            
         SCKW  SPLIT,UPXSPLIT,(A,P,PI)                                          
         SCKW  TIMEOUT,UPXTMOUT,(A,P,PI)                                        
         SCKW  TABS,UPXTABS,A                                                   
         SCKW  TRANSPARENT,UPXTRAN,A                                            
         SCKW  FORCE,UPXFORCE,A                                                 
         SCKW  BUFFERED,UPXBUF,A                                                
         SCKW  DATTN,UPXDATN,A                                                  
         SCKW  NODATTN,UPXNDATN,A                                               
         SCKW  ECHO,UPXECHO,A                                                   
         SCKW  NOECHO,UPXNECHO,A                                                
         SCKW  ,V(INVALID)                                                      
*                                                                               
UPXCOUNT PROC                                                                   
         ST    R0,UPCOUNT                                                       
         PEND                                                                   
*                                                                               
UPXPRMT  PROC                                                                   
         SCDQUOTE UPPROMPT                                                      
         STH   R0,UPPRLEN                                                       
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
UPXSTOP  PROC                                                                   
         SET   UPFSTOP                                                          
         SCDQUOTE UPSTOP                                                        
         STH   R0,UPSTLEN                                                       
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
UPXWRAP  PROC                                                                   
         SET   UPFWRAP                                                          
         CEIL  R0,&LINESZ                                                       
         STH   R0,UPMAXLN                                                       
         PEND                                                                   
*                                                                               
UPXSPLIT PROC                                                                   
         CEIL  R0,&LINESZ                                                       
         STH   R0,UPMAXLN                                                       
         PEND                                                                   
*                                                                               
UPXTMOUT PROC                                                                   
         MH    R0,=H'100'         We want .01 second increments                 
         ST    R0,UPTMOUT                                                       
         PEND                                                                   
*                                                                               
UPXTABS  PROC                                                                   
         SET   UPFTABS                                                          
         PEND                                                                   
*                                                                               
UPXTRAN  PROC                                                                   
         SET   UPFTRAN                                                          
         PEND                                                                   
*                                                                               
UPXFORCE PROC                                                                   
         SET   CPFFORCE                                                         
         PEND                                                                   
*                                                                               
UPXBUF   PROC                                                                   
         SET   CPFBUFIO                                                         
         PEND                                                                   
*                                                                               
UPXECHO  PROC                                                                   
         CLEAR UPFNECHO                                                         
         PEND                                                                   
*                                                                               
UPXNECHO PROC                                                                   
         SET   UPFNECHO                                                         
         PEND                                                                   
*                                                                               
UPXDATN  PROC                                                                   
         SET   UPFDATTN                                                         
         PEND                                                                   
*                                                                               
UPXNDATN PROC                                                                   
         CLEAR UPFDATTN                                                         
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XMODEM Packet Description                                                    
*                                                                               
XMODPACK RECORD BEGIN                                                           
XMODSOH  DS    X                   Start of header (ASCII SOH char)             
XMODSEQ  DS    X                   Sequence number                              
XMODCSQ  DS    X                   Complimented sequence number                 
XMODDATA DS    XL128               128 bytes of data                            
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  XMODUP -- XMODEM Upload.  *** Experimental ***                               
*                                                                               
XMODWA   RECORD BEGIN                                                           
XMODTRM  DS    XL(L'CPTRM)         Terminal I/O settings                        
         END                                                                    
*-                                                                              
XMODUP   PROC  XMODWA                                                           
*                                                                               
         TSEG  '(The XModem UPLOAD command isn''t working yet!)',,CR            
         TCR                                                                    
         TSEG  'This is a test of UPLOAD...',,CR                                
         TWRITE                                                                 
         BNZ   CVABORT                                                          
*-                                                                              
*-       XMODEM Receive loop.                                                   
*-                                                                              
         MVC   XMODTRM,CPTRM       Save terminal I/O settings                   
*                                                                               
         SET   CPFNONL,CPFCLEAN,CPFTRAN+CPFRUND                                 
         MVC   CPTRMIN,=AL2(L'XMODPACK)                                         
*                                                                               
         TSEG  XNAK                                                             
*-                                                                              
*-       Read an XMODEM packet.                                                 
*-                                                                              
LPLPLP   TREAD                                                                  
*                                                                               
         TSEG  XACK                Acknowlege packet                            
         BNZ   CVABORT                                                          
         B     LPLPLP                                                           
*-                                                                              
*-       Restore terminal I/O settings.                                         
*-                                                                              
         MVC   CPTRM,XMODTRM       Restore terminal I/O settings                
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
XSOH     DC    X'01'               ASCII SOH character                          
XEOT     DC    X'04'               ASCII EOT character                          
XACK     DC    X'06'               ASCII ACK character                          
XNAK     DC    X'15'               ASCII NAK character                          
XCAN     DC    X'18'               ASCII CAN character                          
         TITLE 'DOWNLOAD Command'                                               
*box                                                                            
*                                                                               
*  DOWNLOAD -- DOWNLOAD Command.                                                
*                                                                               
DOWNWA   RECORD BEGIN                                                           
DOWNFLG  FLAG                                                                   
         FLAG  DOWNFST             - Processed the first line                   
         FLAG  DOWNFTXT            - Last line had text on it                   
         END                                                                    
*-                                                                              
DOWNLOAD XPROC DOWNWA                                                           
         CLEAR DOWNWA                                                           
         LA    R6,DOWNWA                                                        
         ST    R6,CPWK4            Save DOWNWA ptr                              
*                                                                               
         VCALL SCNEXFR             Allow active/exec/from dsname                
         SET   CPLFLG1.CPFALL      Default is ALL                               
         VCALL NDETRNG             Scan range                                   
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL MKFFINFO                                                         
         SCAN  DOWNPRTX                                                         
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  DOWNPRT                                                          
         SCANCHK                                                                
         END                                                                    
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         END                                                                    
*                                                                               
         COMMENT                   READ RANGE                                   
         IF    CPFRDEXE,BEGIN      Exec                                         
         RDRNG DOWNRTN,(DESRTRN+LEXATRTN+UNPRST+DESABORT)                       
         END                                                                    
         ELSEIF    CPFRDEXT,BEGIN       External                                
         RDRNG DOWNRTN,(DESRTRN+LXCATRTN+UNPRST+DESABORT)                       
         END                                                                    
         ELSE  BEGIN                       ACTIVE FILE                          
         RDRNG DOWNRTN,(DESRTRN+LOCATRTN+UNPRST+DESABORT)                       
         END                                                                    
         TWRITE                                                                 
*                                                                               
         CLEAR CPFNONL,CPFCLEAN,CPFSLOW,CPFNMOD,CPFNPAU                         
         TCR                                                                    
         TCR                                                                    
         TSEG  X'2F'                                                            
         SETMSG 'DOWNLOAD completed.'                                           
         B     CVNXTMSG                                                         
*                                                                               
         PEND                                                                   
*                                                                               
*                                                                               
DOWNPRTX SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
DOWNPRT  SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DOWNRTN -- Co-routine to list lines from the range processor                 
*    in a format suitable to be captured by a micro-computer.                   
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
DOWNRTN  PROC                                                                   
         L     R6,CPWK4            DOWNWA ptr                                   
         WITH  (DOWNWA,R6)                                                      
*                                                                               
         LR    R3,R1               Save line ptr                                
         LR    R2,R0                                                            
*                                                                               
         IF    ^DOWNFST,BEGIN      First time...                                
         SET   CPFNONL,CPFCLEAN,CPFSLOW,CPFNMOD,CPFNPAU                         
*                                                                               
         TREAD 'Press RETURN to begin download... '                             
         BNZ   CVNOACTN            Forget it                                    
*                                                                               
         SET   DOWNFST             All set now                                  
         END                                                                    
*-                                                                              
*-       Write the text.                                                        
*-                                                                              
         IF    (R2,NZ),BEGIN       Write text line...                           
         IF    DOWNFTXT,BEGIN      We had text before...                        
         IF    (@R3,NE,' '),'TSEG CVBLANKS,1'                                   
         ELSE  'TCR'                                                            
         END                                                                    
*                                                                               
         TSEG  (R3),(R2)           Write line text                              
         IF    (@R3,EQ,' '),'TCR'  Not text, add the CR                         
         ELSE  'SET DOWNFTXT'      It's just text                               
         END                                                                    
*                                                                               
         ELSE  BEGIN               Blank line...                                
         IF    DOWNFTXT,'TCR'      End last text line                           
         TCR                                                                    
         CLEAR DOWNFTXT                                                         
         END                                                                    
*                                                                               
         TMARK                                                                  
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
