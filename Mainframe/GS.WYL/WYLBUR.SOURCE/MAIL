MAIL     TITLE 'WYLBUR''s Mail Facility'                                        
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
*                                                                               
*  Modification history:                                                        
*                                                                               
*     13-April-1995  M. Durket                                                  
*                                                                               
*     Changed the message: '<userid> is not accepting mail, mail not            
*     sent', to 'Mail not sent because mail is not being accepted               
*     by <userid>'. The reason is that in such messages, the                    
*     <userid> portion is concatenated with the SET MAIL 'message'              
*     field (via an ACALL SEGMID), resulting in some weird looking              
*     messages like: 'Prism general access <HQ.PRM> -- Please                   
*     respond to originator is not accepting mail, mail not sent.'              
*                                                                               
         HIBAL                                                                  
*                                                                               
WYLMAIL  CSECT                                                                  
MAIL     IDENT 4072                10:30:07 03/12/04  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         SYSDEFN ,                 Define installation params                   
*                                                                               
         GBLA  &NAMESZ,&HOSTSZ,&USERSZ                                          
&NAMESZ  SETA  80                  Maximum length of a personal name            
&HOSTSZ  SETA  80                  Maximum length of a host name                
&USERSZ  SETA  80                  Maximum length of a userid                   
         SPACE 2                                                                
         PUSH  DSECTS                                                           
*                                                                               
         COPY  CONTROL                                                          
*                                                                               
         COPY  SYSDSNS                                                          
*                                                                               
         COPY  UTYPDEFN                                                         
*                                                                               
         COPY  RTNCODES                                                         
         TITLE 'DSECTS'                                                         
KWR      RECORD 'KWR'                                                           
*                                                                               
SIN      RECORD BEGIN                                                           
         COPY  SIN                                                              
         END                                                                    
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
SEGCB    RECORD 'SEGCB'                                                         
*                                                                               
         POP   DSECTS                                                           
         TITLE 'SET WHOSTS Command'                                             
*box                                                                            
*                                                                               
*  Host Name Alias Information                                                  
*                                                                               
HAIL     RECORD BEGIN                                                           
HAILLINK DS    A                   Next HAIL ptr                                
HAILOLEN DS    X                   Old name length byte                         
HAILOLD  DS    0C                  Old name                                     
HAILNLEN DS    0X                  New name length byte                         
HAILNEW  DS    0C                  New name                                     
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  Host Information                                                             
*                                                                               
HOST     RECORD BEGIN                                                           
HOSTLINK DS    A                   Next HOST ptr                                
*                                                                               
HOSTFLAG FLAG                                                                   
         FLAG  HOSTFBAT            - Batch job mail                             
         FLAG  HOSTFBIT            - Bitnet mail                                
         FLAG  HOSTFGTE            - Gateway mail                               
         FLAG  HOSTFBSMTP          - BSMTP header required                      
         FLAG  HOSTFSUNMAIL        - SUNMAIL varient of BSMTP                   
         FLAG  HOSTFTST            - Test address, hold mail                    
         FLAG  HOSTFERR            - Error text is present                      
*                                                                               
HOSTFLG2 FLAG                                                                   
         FLAG  HOSTFNDM            Don't use major domain name                  
*                                                                               
HOSTNAME DS    CL(&HOSTSZ)         Host name                                    
*                                                                               
HOSTGATE DS    CL35                Address of gateway for host                  
HOSTRELY DS    CL35                Address of relay node                        
HOSTACCT DS    CL5                 Mailbox account (UUUGG)                      
*                                                                               
HOSTTEXT DS    CL80                Error/warning text (when needed)             
         END                                                                    
         EJECT                                                                  
SHOSTWA  RECORD BEGIN                                                           
SHOSTFLG FLAG                                                                   
*                                                                               
SHAILQH  DS    A                   New HAIL queue head ptr                      
SHAILQT  DS    A                   New HAIL queue tail ptr                      
*                                                                               
SHOSTQH  DS    A                   New HOST queue head ptr                      
SHOSTQT  DS    A                   New HOST queue tail ptr                      
*                                                                               
SHAILOLD DS    2A                  Old alias name text len, loc                 
SHAILNEW DS    2A                  New alias name text len, loc                 
*                                                                               
SHOST    DS    XL(L'HOST)          Working HOST entry                           
*                                                                               
SHOSTLN  DS    CL(&LINESZ)         Working line buffer                          
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SETWHOST -- SET WHOSTS Command.  (Priv'd.)                                   
*                                                                               
*    Note:  This routine is also called after a successful save                 
*           of "HOSTS ACCOUNT GS.WYL".                                          
*                                                                               
SETWHOST GENTER                                                                 
         IF    (CPCMNM,NE,'W'),BEGIN  Entered after SAVE...                     
         VCALL INITCMD             Initialize                                   
         END                                                                    
*                                                                               
         ELSE  BEGIN               Command entry...                             
         IF    ^CPSFSPR,CVNVALID   Sorry pal                                    
         END                                                                    
*                                                                               
         XPUSH ,,L'SHOSTWA,PTR=R6                                               
         USING SHOSTWA,R6                                                       
         ST    R6,CPCWA                                                         
*                                                                               
         CLEAR SHOSTWA                                                          
         LA    R15,SHOSTQH-(HOSTLINK-HOST)                                      
         ST    R15,SHOSTQT         Dummy queue tail                             
         LA    R15,SHAILQH-(HAILLINK-HAIL)                                      
         ST    R15,SHAILQT         Dummy queue tail                             
*                                                                               
         LA    R0,SDSNHOSA         Want the SYSA hosts dsn                      
         IF    CVFRLG,'LA R0,SDSNHOSR'  RLG hosts dsn                           
         IF    (CVMACHID,EQ,'SYSC '),'LA R0,SDSNHOSC'  SYSC hosts               
         IF    (CVMACHID,EQ,'SYSD '),'LA R0,SDSNHOSD'  SYSD hosts               
         IF    (CVMACHID,EQ,'SYSH '),'LA R0,SDSNHOSH'  SYSH hosts               
         VCALL EXIT0               LOCAL exit - format system dsname            
         OSCINIT (R1),(R0)                                                      
         VCALL ZODSNM                                                           
         OSCAN L:=V(DSNPRT)                                                     
         VCALL ZSNFIN              Set up DSNWA                                 
*                                                                               
         SET   CPLFLG1.CPFALL                                                   
         SET   CPFRDEXT                                                         
         VCALL ZDETRNG                                                          
*                                                                               
         VCALL EXTOPEN             Open the file                                
         IF    (R15,NZ),CVNEXT                                                  
*                                                                               
         L     R15,SHOSTWRK                                                     
         VCALL DESPOT              (Co-routine is SHOSTRTN)                     
*                                                                               
         VCALL EXTCLOSE            All done                                     
*-                                                                              
*-       Now do post-processing.                                                
*-                                                                              
         IF    (SHOSTQH,Z),BEGIN   Nothing there...                             
         IF    CVFINIT,EXIT                                                     
         TCCR                                                                   
         TSEG  'No hosts defined.',,CR                                          
         END                                                                    
*-                                                                              
*-       Make new host table the system default.                                
*-                                                                              
         L     R5,CVHOSTQH         Save old table ptr                           
         MVC   CVHOSTQH,SHOSTQH    Make new version available                   
*-                                                                              
*-       Release the old host table.                                            
*-                                                                              
         WHILE (R5,NZ),BEGIN       Freemain each entry...                       
         WITH  (HOST,R5)                                                        
         L     R4,HOSTLINK                                                      
*                                                                               
         CLEAR HOST                (Neatness)                                   
         FREEMAIN R,A=(R5),LV=L'HOST  Release host table entry                  
*                                                                               
         LR    R5,R4                                                            
         END                                                                    
*-                                                                              
*-       Make new host alias table the system default.                          
*-                                                                              
         L     R5,CVHAILQH         Save old table ptr                           
         MVC   CVHAILQH,SHAILQH    Make new version available                   
*-                                                                              
*-       Release the old host alias table.                                      
*-                                                                              
         WHILE (R5,NZ),BEGIN       Freemain each entry...                       
         WITH  (HAIL,R5)                                                        
         L     R4,HAILLINK                                                      
*                                                                               
         CLEAR HAIL                (Neatness)                                   
         FREEMAIN R,A=(R5),LV=L'HAIL  Release host alias table entry            
*                                                                               
         LR    R5,R4                                                            
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         DROP  SHOSTWA,BR                                                       
*                                                                               
         DS    0F                                                               
SHOSTWRK DC    AL1(DESRTRN+LXCATRTN+UNPRST),AL3(SHOSTRTN)                       
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOSTRTN -- Co-routine to process lines from the HOSTS file.                 
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
SHOSTRTN XENTER R2,R8,,LOCAL                                                    
         L     R6,CPCWA                                                         
         USING SHOSTWA,R6                                                       
*                                                                               
         LA    R5,SHOST                                                         
         USING HOST,R5                                                          
*                                                                               
         SET   CPSCNFLG.CPFRTNER                                                
         MVC   CPSCNERT,=A(SHOSTXIT)  Go here if a scan error                   
*                                                                               
         CEIL  R0,L'SHOSTLN                                                     
         LR    R15,R0                                                           
         MOVE  R15,SHOSTLN,@R1                                                  
         OSCINIT SHOSTLN,(R0)      Input line                                   
         SET   CPFSNSEP            Set scan flag to allow "-"s                  
*                                                                               
SHOSTSCN OSCAN SHOSTPRT            Look for options                             
         B     SHOSTXIT                                                         
*                                                                               
*                                                                               
SAILNM   VCALL DEQUOTE             Dequote string if quoted                     
         STM   R0,R1,SHAILOLD      Save old name len, loc                       
         CEIL  R0,&HOSTSZ          Not too long                                 
         LR    R2,R0                                                            
         OSCAN                                                                  
         VCALL DEQUOTE             Dequote string if quoted                     
         CEIL  R0,&HOSTSZ          Not too long                                 
         STM   R0,R1,SHAILNEW      Save new name len, loc                       
         AR    R2,R0                                                            
         LA    R2,@R2+4+1+1        Length of HAIL entry                         
         GETMAIN R,LV=(R2)         Get a new HAIL entry                         
         LR    R3,R1                                                            
         WITH  (HAIL,R3),BEGIN                                                  
         CLEAR HAIL                                                             
*                                                                               
         L     R15,SHAILQT                                                      
         WITH  (HAIL,R15),'ST R3,HAILLINK'  Link up new entry                   
*                                                                               
         ST    R3,SHAILQT          Save new queue tail                          
*                                                                               
         LM    R1,R2,SHAILOLD                                                   
         CEIL  R1,255              Not too long                                 
         STC   R1,HAILOLEN         Save old host name length                    
         MOVE  R1,HAILOLD,@R2      Save old host name text                      
*                                                                               
         LA    R15,HAILOLD                                                      
         A     R15,SHAILOLD        New host name text starts here               
         LM    R1,R2,SHAILNEW                                                   
         CEIL  R1,255                                                           
         STC   R1,@R15             Save new host name length                    
         MOVE  R1,@R15+1,@R2       Save new host name text                      
         END                                                                    
         B     SHOSTSCN                                                         
*                                                                               
*                                                                               
SHOSTNM  CLEAR HOST                                                             
         MVC   HOSTNAME,CVBLANKS                                                
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTNAME                                                   
         MOVE  R15,HOSTNAME,@R1    Save host name                               
         BR    RAR                                                              
*                                                                               
SHOSTGTE SET   HOSTFGTE            Gateway defined                              
         MVC   HOSTGATE,CVBLANKS                                                
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTGATE                                                   
         MOVE  R15,HOSTGATE,@R1    Save gateway address                         
         BR    RAR                                                              
*                                                                               
SHOSTRLY LABEL ,                                                                
         MVC   HOSTRELY,CVBLANKS   Clear relay address                          
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTRELY                                                   
         MOVE  R15,HOSTRELY,@R1    Save relay address                           
         BR    RAR                                                              
*                                                                               
*                                                                               
SHOSTACC IF    ((R0,EQ,6),AND,(@R1+2,EQ,'.')),BEGIN  Valid acct...              
         MVC   HOSTACCT(3),@R15+3  uuu                                          
         MVC   HOSTACCT+3(2),@R15  gg                                           
         END                                                                    
         BR    RAR                                                              
*                                                                               
SHOSTBAT SET   HOSTFBAT            Batch job mail                               
         BR    RAR                                                              
*                                                                               
SHOSTBIT SET   HOSTFBIT            Bitnet mail                                  
         BR    RAR                                                              
*                                                                               
SHOSTBSM SET   HOSTFBSMTP          BSMTP header required                        
         BR    RAR                                                              
*                                                                               
SHOSTNFL SET   HOSTFBSMTP+HOSTFSUNMAIL  BSMTP and SUNMAIL                       
         BR    RAR                                                              
*                                                                               
SHOSTTST SET   HOSTFTST            Test address                                 
         BR    RAR                                                              
*                                                                               
SHOSTNDM SET   HOSTFNDM            Strip major domain name from addr            
         BR    RAR                                                              
*                                                                               
SHOSTERR LENTER                                                                 
         SET   HOSTFERR            This is an error                             
         VCALL DEQUOTE             Dequote string                               
         MVC   HOSTTEXT,CVBLANKS   Initialize command area                      
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTTEXT                                                   
         MOVE  R15,HOSTTEXT,@R1    Save command text                            
         LEXIT                                                                  
*                                                                               
SHOSTEND GETMAIN R,LV=L'HOST                                                    
         LR    R2,R1               Newly getmained HOST entry ptr               
         MOVEL @R2,SHOST,L'HOST    Save new HOST entry                          
         L     R15,SHOSTQT                                                      
         WITH  (HOST,R15),'ST R2,HOSTLINK'  Link us to last entry               
         ST    R2,SHOSTQT                                                       
         B     SHOSTSCN                                                         
*                                                                               
SHOSTXIT CLEAR CPSCNFLG.CPFRTNER                                                
         XEXIT                                                                  
         DROP  HOST,SHOSTWA,BR                                                  
         SPACE 2                                                                
SHOSTPRT OSCKW ALIAS,SAILNM,(A,P)                                               
         OSCKW HOST,SHOSTNM,(A,P)                                               
         OSCKW BATMAIL,SHOSTBAT,A                                               
         OSCKW BITMAIL,SHOSTBIT,A                                               
         OSCKW GATEMAIL,SHOSTGTE,(A,P)                                          
         OSCKW RELAY,SHOSTRLY,(A,P)                                             
         OSCKW BSMTP,SHOSTBSM                                                   
         OSCKW SUNMAIL,SHOSTNFL                                                 
         OSCKW TEST,SHOSTTST                                                    
         OSCKW NODOMAIN,SHOSTNDM                                                
         OSCKW ACCOUNT,SHOSTACC,(A,P)                                           
         OSCKW ERROR,SHOSTERR,(A,P)                                             
         OSCKW END,SHOSTEND                                                     
         OSCKW                                                                  
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ALIASCHK -- Routine to convert an alias for a host to the actual             
*    host name.                                                                 
*                                                                               
*    On entry:                                                                  
*      R15 - host name ptr (&HOSTSZ bytes long)                                 
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - no match (not an alias)                                              
*      4 - it was an alias; the host name passed on entry is updated            
*            with the alias name.                                               
*                                                                               
ALIASWA  RECORD BEGIN                                                           
ALIASNM  DS    CL(&HOSTSZ)         Working host name                            
         END                                                                    
*-                                                                              
ALIASCHK PROC  ALIASWA                                                          
         LR    R5,R15              Host name ptr                                
*-                                                                              
*-       Change host name if it is an alias for something else.                 
*-                                                                              
         LA    R6,CVHAILQH-(HAILLINK-HAIL)                                      
*                                                                               
         LOOP  BEGIN                                                            
         WITH  (HAIL,R6)                                                        
         WHILE ('LT R6,HAILLINK',NZ)  Go through host alias list...             
         LC    R15,HAILOLEN                                                     
         MVC   ALIASNM,CVBLANKS                                                 
         MOVE  R15,ALIASNM,HAILOLD  Old host name                               
*                                                                               
         IF    (@R5,EQ,ALIASNM),BEGIN  Matched...                               
         LA    R1,HAILOLD+1(R15)   New host name starts here                    
         MVC   @R5(L'ALIASNM),CVBLANKS                                          
         LC    R15,@R1             New host name length                         
         MOVE  R15,@R5,@R1+1       Replace with new host name                   
         LA    R15,4               Host name was an alias rc                    
         B     ALIASCEX                                                         
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 No match rc                                  
*                                                                               
ALIASCEX PEND                                                                   
         EJECT                                                                  
HOSTCWA  RECORD BEGIN                                                           
HOSTCVLN DS    A                   Number of chars matched                      
HOSTCVP  DS    A                   HOST entry ptr for closest match             
*                                                                               
HOSTCNSP DS    A                   Ptr to HOSTCNM                               
HOSTCNEP DS    A                   Ptr to last char+1 of HOSTCNM                
*                                                                               
HOSTCNM  DS    CL(&HOSTSZ)         Working host name                            
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  HOSTCHK -- Routine to check the host name to see if it's one we              
*    know about.                                                                
*                                                                               
*    On entry:                                                                  
*      R15 - host name ptr (&HOSTSZ bytes long)                                 
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - unknown host name                                                    
*      nz- host name is known (R15=HOST entry loc)                              
*                                                                               
HOSTCHK  PROC  HOSTCWA                                                          
         CLEAR HOSTCWA                                                          
*                                                                               
         MVC   HOSTCNM,@R15        Save host name                               
         L     R15,CVUPPTBL                                                     
         TR    HOSTCNM,@R15        Convert host name to caps                    
*                                                                               
         SETMSG HOSTCNM                                                         
         VCALL LRTRIM                                                           
         ST    R1,HOSTCNSP         Start of working host name                   
         AR    R1,R0                                                            
         ST    R1,HOSTCNEP         End of working host name                     
*-                                                                              
*-       Lookup name in host table.                                             
*-                                                                              
         LA    R6,CVHOSTQH-(HOSTLINK-HOST)                                      
*                                                                               
         LOOP  BEGIN                                                            
         WITH  (HOST,R6)                                                        
         WHILE ('LT R6,HOSTLINK',NZ)  Go through host list...                   
*                                                                               
         IF    (HOSTNAME,EQ,'*'),BEGIN  Match ending chars...                   
         SETMSG HOSTNAME+1,L'HOSTNAME-1  Host name (without *)                  
         VCALL LRTRIM                                                           
         L     R2,HOSTCNEP         End of working host name                     
         SR    R2,R0                                                            
         IF    (R2,LT,HOSTCNSP),EXIT  Stem is larger than name, scram           
*                                                                               
         LTR   R15,R0              Just "*"?                                    
         IF    NP,BEGIN            Yes, matches everything...                   
         IF    (HOSTCVLN,Z),'ST R6,HOSTCVP'  Save HOST entry ptr                
         END                                                                    
*                                                                               
         ELSE  BEGIN               Match ending suffix...                       
         CMPR  R15,@R2,@R1         Does the suffix match?                       
         IF    EQ,BEGIN            Matched...                                   
         IF    (R0,GT,HOSTCVLN),BEGIN  Closest match so far...                  
         ST    R0,HOSTCVLN         Save number of chars matched                 
         ST    R6,HOSTCVP          Save HOST entry ptr                          
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               Match entire host name...                    
         IF    (HOSTCNM,EQ,HOSTNAME),BEGIN  Matched...                          
         LR    R15,R6                                                           
         B     HOSTCXIT                                                         
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         L     R15,HOSTCVP         Closest match HOST ptr (or zero)             
*                                                                               
HOSTCXIT PEND                                                                   
         QLTORG                                                                 
         TITLE 'MAIL and SEND Commands'                                         
MID      RECORD BEGIN                                                           
MIDLINK  DS    A                   Next MID ptr (or zero)                       
*                                                                               
MIDXFLAG FLAG                                                                   
         FLAG  MIDXFBAT            - Send this item as a batch job              
         FLAG  MIDXFBIT            - Send this item via Bitnet                  
         FLAG  MIDXFGTE            - Send this item via gateway                 
         FLAG  MIDXFBSMTP          - Include BSMTP header for item              
         FLAG  MIDXFSUNMAIL        - SUNMAIL varient of BSMTP                   
         FLAG  MIDXFTST            - Test address, hold outgoing items          
         FLAG  MIDXFSKP            - Don't process this item                    
*                                                                               
MIDXFLG2 FLAG                                                                   
         FLAG  MIDXFCC             - 'cc:' address                              
         FLAG  MIDXFHDR            - Put address in mail header                 
         FLAG  MIDXFSNT            - BSMTP mail sent flag                       
*                                                                               
MIDXNAME DS    CL(&NAMESZ)         Personal name                                
*                                                                               
MIDXUSNC DS    CL(&USERSZ)         Some nets are case sensitive                 
MIDXHONC DS    CL(&HOSTSZ)         ...case sensitive again                      
*                                                                               
MIDXUSER DS    CL(&USERSZ)         Mail userid name                             
MIDXHOST DS    CL(&HOSTSZ)         Mail host name                               
MIDXKEY  EQU   MIDXUSER,*-MIDXUSER,C'X'                                         
*                                                                               
MIDXGATE DS    CL(L'HOSTGATE)      Gateway address                              
MIDXRELY DS    CL(L'HOSTRELY)      Relay node address                           
*                                                                               
MIDXMTXT DS    CL(L'KWRMTXT)       Mail information text                        
*                                                                               
MIDXACCT DS    CL5                 Send mail to this account (uuugg)            
MIDX     EQU   MIDXFLAG,*-MIDXFLAG,C'X'                                         
*                                                                               
         END                                                                    
         EJECT                                                                  
MAILWA   RECORD BEGIN                                                           
MWAFLAG  FLAG                                                                   
         FLAG  MWAFSOME            - We have a least one address                
         FLAG  MWAFCC              - Processing 'cc'                            
*                                                                               
MWAFLAG2 FLAG                                                                   
         FLAG  MWAFNTFY            - Notify                                     
         FLAG  MWAFSIL             - Silent                                     
         FLAG  MWAFEMS             - EMS is doing this command                  
         FLAG  MWAFNHDR            - NOHDR option                               
         FLAG  MWAFUHDR            - UHEADER option                             
         FLAG  MWAFMHDR            - MHEADER option                             
         FLAG  MWAFDRYRUN          - DRYRUN option                              
         FLAG  MWAFMINF            - MAILINFO option                            
*                                                                               
MWAFLAG3 FLAG                                                                   
         FLAG  MWAFFWD             - We're forwarding a message                 
*                                                                               
MWALMAX# EQU   1000,,C'N'          Maximum allowed number of Add's              
MWALCNT  DS    F                   ADDITEM loop counter                         
MWANEST  DS    F                   ADDLIST nesting level                        
MWARCPTN DS    F                   Header recipient address count               
*                                                                               
MWAMIDQH DS    A                   First MID ptr (or zero if none)              
MWAMIDQT DS    A                   MID queue tail                               
*                                                                               
MWATIME  DS    CL18                hh:mm day mm/dd/yy                           
MWADATE  DS    CL17                hh:mm:ss mm/dd/yy                            
MWAMSGID DS    CL22                Message ID                                   
MWAFROM  DS    CL200               Sender's userid                              
MWAFROMH DS    CL200               Sender's userid with host name               
MWARET   DS    CL200               Return mail address                          
MWANAME  DS    CL(&NAMESZ)         Sender's personal name                       
MWATITLE DS    CL200               Title                                        
*                                                                               
MWARNAME DS    CL(&NAMESZ)         Recipient's name (network)                   
*                                                                               
MWALINE  DS    XL(&PRESSZ)         Line work area                               
*                                                                               
MWAMKEY  DS    CL16                MAIL_INFO record group key                   
*                                                                               
MWANLINS DS    F                   No. of lines for new mail                    
MWANCNT  DS    F                   No. of bytes for new mail                    
*                                                                               
MWALINES DS    F                   Total no. of lines for mail                  
MWACOUNT DS    F                   Total no. of bytes for mail                  
*                                                                               
MWAREGS  DS    9A                  Saved regs for DESPOT abort                  
MWASCAN  DS    XL(L'CPOSCAN)       Saved scanner info                           
*                                                                               
MWAWMIDP DS    A                   Working MID ptr                              
MWAWLNO  DS    F                   Working line-number                          
*                                                                               
MWAWDIR  DS    H                   Working dir offset                           
MWAWOFFS DS    H                   Working page offset                          
MWAWHCNT DS    H                   Working header line count                    
*                                                                               
MWANEW#  EQU   30                  Max no of new mail pages                     
MWAMNEW  DS    H           [Order] Maximum offset allowed                       
MWANNEW  DS    H           [Order] Current offset from MWANEW                   
MWANEW   DS    (MWANEW#)H  [Order] New mail directory                           
*                                                                               
MWADIR#  EQU   60                  Max no of old mail pages                     
MWAMDIR  DS    H           [Order] Maximum offset allowed                       
MWANDIR  DS    H           [Order] Current offset from MWADIR                   
MWADIR   DS    (MWADIR#)H  [Order] Old mail directory                           
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAIL -- MAIL and SEND commands.                                              
*                                                                               
MAIL     GENTER L'MAILWA                                                        
         LR    R6,WAR                                                           
         USING MAILWA,R6                                                        
         ST    R6,CPCWA                                                         
         CLEAR MAILWA              Init work area                               
*-                                                                              
*-       Account for Wylbur mail processing separately.                         
*-                                                                              
         IF    (CPWCHAR,EQ,'GENERAL'),BEGIN                                     
         MVC   CPLWCHAR(8),=C'WYLMAIL_'  Prefix                                 
         MVC   CPLWCHAR+8(8),CPWCHAR  Set suffix                                
         VCALL BILLCHK             Switch charging components                   
         END                                                                    
*-                                                                              
*-       Check to see if MAIL/SEND GBL is defined.                              
*-                                                                              
         SETMSG 'SES.MAIL_MAIL'     MAIL command                                
         IF    (CPCMNM,EQ,'SEN'),'SETMSG "SES.MAIL_SEND"'  SEND cmd             
         VCALL GBLCMD              Execute GBL command instead                  
*-                                                                              
*-       Temporary limitation because we use halfword pagenos.                  
*-                                                                              
         IF    CVFMEMPAGE,BEGIN    Trouble...                                   
         ABORT 'MAIL/SEND commands not available in this Wylbur.'               
         END                                                                    
*-                                                                              
*-       Regular MAIL/SEND command.                                             
*-                                                                              
         LA    R15,MWAMIDQH-(MIDLINK-MID)  Dummy first link                     
         ST    R15,MWAMIDQT        Make tail                                    
*                                                                               
         SET   MWAFNTFY            Notify is the default                        
*                                                                               
         COMMENT                   Make MWADATE hh:mm:ss mm/dd/yy               
         VCALL LOCALTOD            Current time/date                            
         XPUSH ,,32,PTR=R15                                                     
         VCALL FMTCLCK                                                          
         MVC   MWADATE(15),@R1     Save current time/date                       
         MVC   MWADATE+15(2),17(R1)  but ignore century part for now            
         XPOP  ,,32                                                             
*                                                                               
         COMMENT                   Make MWATIME hh:mm day mm/dd/yy              
         MVC   MWATIME,CVBLANKS                                                 
         MVC   MWATIME(5),MWADATE  Move hh:mm                                   
         MVC   MWATIME+10(8),MWADATE+9  Move  mm/dd/yy                          
         VCALL LOCALTOD                                                         
         VCALL FMTDAY                                                           
         MVC   MWATIME+6(3),@R1    Move day                                     
*-                                                                              
*-       Make a unique message id.                                              
*-                                                                              
         MVC   MWAMSGID,=C'A0000-0000-00000:00000'                              
*                                                                               
         L     R3,CVCURJCB                                                      
         WITH  (JCB,R3),BEGIN                                                   
         L     R2,JCBSINP                                                       
         WITH  (SIN,R2),BEGIN                                                   
         IF    (SIN,EQ,'SIN'),BEGIN                                             
         MVC   MWAMSGID(16),SINSTAG                                             
         MVI   MWAMSGID+16,C':'                                                 
         INCR  R15,CPNMAIL         Count number of mail commands                
         AL    R15,=A(100000)      Force leading zeros                          
         BTD   MWAMSGID+17,5,(R15)  Add message count                           
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Build sender's userid.  MWAFROM is just the userid; and                
*-         MWAFROMH is the userid with our host name included.                  
*-                                                                              
         MVC   MWAFROM,CVBLANKS                                                 
         IF    (CPUID,NZ),BEGIN    Use primary userid...                        
         MVC   MWAFROM(L'CPUID),CPUID                                           
         END                                                                    
         ELSE  BEGIN               If no ID, then use GG.UUU...                 
         MVC   MWAFROM(2),CPSGRP                                                
         MVI   MWAFROM+2,C'.'                                                   
         MVC   MWAFROM+3(3),CPSUSER                                             
         END                                                                    
*                                                                               
         MVC   MWAFROMH,MWAFROM                                                 
         SETMSG MWAFROMH                                                        
         VCALL RTRIM                                                            
         AR    R1,R0                                                            
         MVI   @R1,C'@'                                                         
         MVC   @R1+1(L'CVINSTAL),CVINSTAL                                       
         IF    (CVINSTAL,EQ,'STANFORD'),BEGIN                                   
         IF    CVFRLG,BEGIN  Stanford/RLG...                                    
         MVC   @R1+1(25),=CL25'RLG.ORG'                                         
         END                                                                    
         ELSE  BEGIN               Stanford/Forsythe...                         
         MVC   @R1+1(25),=CL25'Forsythe.Stanford.EDU'                           
         END                                                                    
         END                                                                    
*                                                                               
         MVC   MWANAME,CVBLANKS                                                 
         MVC   MWANAME(L'CPNAME),CPNAME  User name                              
         IF    (CPNAME,Z),'CLEAR MWANAME'                                       
*                                                                               
         MVC   MWAMDIR,=AL2(MWADIR#*2)                                          
         MVC   MWAMNEW,=AL2(MWANEW#*2)                                          
*-                                                                              
*-       Scan SEND command.                                                     
*-                                                                              
         IF    (CPCMNM,EQ,'SEN'),BEGIN  SEND command...                         
SENDSCAN ACALL MTOKEN              Get next token                               
         IF    NZ,'ERROR "Missing account(s) for SEND.",,NOSNDACC'              
         IF    ((R0,EQ,2),AND,((@R1,EQ,'TO'),OR,                       *        
               (@R1,EQ,'to'))),BEGIN                                            
         TSEG  'TO ignored.',,CR                                                
         B     SENDSCAN                                                         
         END                                                                    
*                                                                               
         IF    CPOFNPAS,BEGIN      For SPIRES...                                
         IF    ((@R1,EQ,'MAI '),OR,(@R1,EQ,'MAIL '),OR,                *        
               (@R1,EQ,'SPI '),OR,(@R1,EQ,'SPIR'),OR,                  *        
               (@R1,EQ,'mai '),OR,(@R1,EQ,'mail '),OR,                 *        
               (@R1,EQ,'spi '),OR,(@R1,EQ,'spir')),'VCALL ORVCMD'               
         END                                                                    
*                                                                               
         ACALL ADDLIST                                                          
*                                                                               
         OSCTELL ,                 Remaining text                               
         VCALL LRTRIM                                                           
         IF  (R0,NP),'ERROR "Missing message after account.",,NOSNDMSG'         
         MVC   MWATITLE,CVBLANKS                                                
         CEIL  R0,L'MWATITLE                                                    
         LR    R15,R0                                                           
         MOVE  R15,MWATITLE,@R1    Move in title                                
         END                                                                    
*-                                                                              
*-       Scan MAIL command.                                                     
*-                                                                              
         ELSE  BEGIN               Mail command...                              
*-                                                                              
*-       We need to pre-scan the command for range information.                 
*-         The following piece of uglyness copies the text we are               
*-         about to scan to a temporary buffer and scans it from                
*-         there; this is to avoid converting the text to upper                 
*-         case.                                                                
*-                                                                              
         MVC   MWASCAN,CPOSCAN      Save scanner info                           
*                                                                               
         OSCTELL                                                                
         CEIL  R0,L'MWALINE        Not too much                                 
         LR    R15,R0                                                           
         MOVE  R15,MWALINE,@R1                                                  
         OSCSET MWALINE,(R0)       Scan alternate copy                          
*                                                                               
         SET   CPLFLG1.CPFALL      Default range is ALL                         
         VCALL SCNEX               Allow from exec                              
         VCALL ZDETRNG             Scan range (if any)                          
*                                                                               
         L     R1,CPSCNLOC                                                      
         LA    R15,MWALINE                                                      
         SR    R1,R15              Offset in to text                            
         LH    R0,CPSCNLEN         Remaining length                             
         MVC   CPOSCAN,MWASCAN      Restore scanner info                        
         AL    R1,CPSCNLOC                                                      
         OSCSET (R1),(R0)          Adjust scan ptrs                             
*-                                                                              
*-       Now scan for MAIL command options.                                     
*-                                                                              
MAILSCAN OSCAN MAILPRT             Mail command options                         
*                                                                               
         IF    MWAFMINF,BEGIN      If MAILINFO option,                          
         IF    MWAFSOME,BEGIN      Err if already have recipients,              
         ERROR 'MAILINFO invalid with TO or CC'                                 
         END                                                                    
         ACALL MGETMINF            Go read MAIL_INFO record group               
         END                                                                    
*                                                                               
         IF    ^MWAFSOME,BEGIN                                                  
         SETMSG 'SES.MAIL_MAILDFLT'                                             
         VCALL GBLCMD                                                           
         ERROR 'Missing "TO account" on MAIL command.',,NOMAIACC                
         END                                                                    
         END                                                                    
*-                                                                              
*-       Process the SEND or MAIL command.                                      
*-                                                                              
         XPUSH ,,L'SEGCB,PTR=R5                                                 
         WITH  (SEGCB,R5),BEGIN                                                 
         SEGINIT MWALINE,&LINESZ,SEGCB                                          
*                                                                               
         COMMENT                   DO OLD MAIL FIRST LINE                       
         IF    CVFRLG,BEGIN        RLG uses OLD MAIL hdr (grrr)...              
         SEG   '*N'                Header line starts with "*n"                 
         SEGB  '????????'          No subject yet                               
         SEGB  MWADATE             "hh:mm:ss mm/dd/yy"                          
         SEGB  'FROM'                                                           
*-                                                                              
*-       Temporarily use the "gg.uuu" instead of our userid so that             
*-         EMS will correctly process the mail header.                          
*-                                                                              
         IF    (CPUID,EQ,MWAFROM),BEGIN  It's our userid...                     
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN               Otherwise, use what is given...              
         SEGT  MWAFROM                                                          
         END                                                                    
*                                                                               
         IF    (MWANAME,NZ),BEGIN                                               
         SEG   ' "'                                                             
         SEGT  MWANAME                                                          
         SEG   '"'                                                              
         END                                                                    
         SEGB  ':'                                                              
         END                                                                    
*                                                                               
         COMMENT                   DO NEW MAIL FIRST LINE                       
         ELSE  BEGIN                                                            
         SEG   '*N'                Header line starts with "*n"                 
         SEGB  '????????'          No subject yet                               
         SEGB  MWATIME             "hh:mm:ss mm/dd/yy"                          
         SEGB  'From'                                                           
         IF    (MWANAME,NZ),BEGIN                                               
         SEGT  MWANAME                                                          
         SEG   ' '                                                              
         END                                                                    
         SEG   '<'                                                              
         SEGT  MWAFROM                                                          
         SEG   '>'                                                              
         SEGB  ':'                                                              
         END                                                                    
*                                                                               
         IF    (MWATITLE,NZ),'SEGT MWATITLE'  Title (if any)                    
*                                                                               
         CLEAR R15                 Dummy lineno (ignored later)                 
         SETMSG MWALINE,L:SEGCBLENF                                             
         END                                                                    
*                                                                               
         XPOP  ,,L'SEGCB                                                        
         VCALL PRESS               Convert to pressed format                    
         LR    R15,R1              Pressed line ptr                             
         INCR  R1,MWANLINS         Kick line count                              
         L     R1,MWANCNT          Byte count                                   
         LA    R1,@R1+5            Plus lineno & len byte                       
         AR    R1,R0               Plus pressed text                            
         ST    R1,MWANCNT                                                       
*                                                                               
         LA    R1,MWAMNEW          Point to dir area for BUFRLINE               
         ACALL BUFRLINE            Buffer line                                  
         FAIL  POS,MAILERRX                                                     
*                                                                               
         IF    (CPCMNM,EQ,'MAI'),BEGIN  Add range to mail item...               
         L     R15,MAILWRK                                                      
         IF    CPFRDEXE,'O R15,=AL1(LEXATRTN,0,0,0)'  Exec                      
         VCALL DESPOT              (Co-routine is MAILRTN)                      
         IF    NZ,'TCLEAR; B CVNOACTN'                                          
         IF    ^CPFSOME,CVNEXT     Nothing                                      
         END                                                                    
*-                                                                              
*-       Delete duplicate id's.                                                 
*-                                                                              
         L     R5,MWAMIDQH                                                      
         WHILE (R5,NZ),BEGIN       Go through all items...                      
         WITH  (MID,R5)                                                         
*-                                                                              
*-       Search through the entire item list for each item in the               
*-         list.  This is an "n by m" search which might start to               
*-         get expensive if there are many items.                               
*-                                                                              
         IF    ^MIDXFSKP,BEGIN                                                  
         L     R15,MWAMIDQH                                                     
         WHILE (R15,NZ),BEGIN                                                   
         WITH  (MID,R15)                                                        
         CLC   MIDXKEY,MIDXKEY-MID(R5)                                          
         IF    EQ,BEGIN            Same id as current...                        
         IF    (R15,EQ,R5),EXIT    It is the current, scram                     
         SET   MIDXFSKP            Skip the duplicate id                        
         END                                                                    
*                                                                               
         L     R15,MIDLINK         Next mail item                               
         END                                                                    
         END                                                                    
*                                                                               
         L     R5,MIDLINK          Next mail item                               
         END                                                                    
*-                                                                              
*-       Send the mail.                                                         
*-                                                                              
         L     R5,MWAMIDQH                                                      
         WHILE (R5,NZ),BEGIN       Send all items...                            
         WITH  (MID,R5)                                                         
*                                                                               
         IF    ^MIDXFSKP,BEGIN     Process this item...                         
         LA    R15,MID                                                          
         ACALL SENDMAIL            Send this mail item                          
         END                                                                    
*                                                                               
         L     R5,MIDLINK          Next mail item                               
         END                                                                    
*-                                                                              
*-       Finish up command.                                                     
*-                                                                              
         IF    (CPCMNM,EQ,'MAI'),BEGIN                                          
         IF    (CPFABORT+CPFERROR,Z),'CLEAR CPACHCT'                            
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         DS    0F                                                               
MAILWRK  DC    AL1(DESRTRN+LOCATRTN+PREST+DESABORT)                             
         DC    AL3(MAILRTN)                                                     
         EJECT                                                                  
***                                                                             
***  MAIL command options.                                                      
***                                                                             
MAILPRT  OSCKW TO,MTO                                                           
         OSCKW CC,MCC                                                           
         OSCKW MAILINFO,MMINFO                                                  
         OSCKW SENDER,MSENDER,(A,P)                                             
         OSCKW NORETURN,MNORET,A                                                
         OSCKW RETURN,MRETURN,(A,P)                                             
         OSCKW NOHDR,MNOHDR,A                                                   
         OSCKW NOHEADER,MNOHDR,A                                                
         OSCKW DATE,MDATE,(A,P)                                                 
         OSCKW NAME,MNAME,(A,P)                                                 
         OSCKW TITLE,MTITLE,(A,P)                                               
         OSCKW SUBJECT,MTITLE,(A,P)                                             
         OSCKW RECIPIENT,MRECIP,(A,P)                                           
         OSCKW NOTIFY,MNOTIFY,A                                                 
         OSCKW NONOTIFY,MNONOTFY,A                                              
         OSCKW SILENT,MSILENT,A                                                 
         OSCKW NOSILENT,MNOSIL,A                                                
         OSCKW MHEADER,MMHDR,A                                                  
         OSCKW MHDR,MMHDR                                                       
         OSCKW UHEADER,MUHDR,A                                                  
         OSCKW UHDR,MUHDR                                                       
         OSCKW DRYRUN,MDRYRUN,A                                                 
         OSCKW EMSFLAG,MEMSFLAG                                                 
         OSCKW FORWARD,MFWDFLAG                                                 
         OSCKW EXECUTE,,A          (Handled elsewhere)                          
         OSCKW ,V(COLLPRT),PUSH                                                 
         OSCKW ,V(QUIETPRT),PUSH                                                
         OSCKW ,V(INVALID)                                                      
*                                                                               
MCC      SET   MWAFCC              Flag as 'cc'                                 
         B     MTO2                                                             
*                                                                               
MTO      SET   MWAFSOME                                                         
MTO2     ACALL MTOKEN              Get mail id (or list)                        
         IF    NZ,'ERROR "Missing mail-id(s) after TO."'                        
         ACALL ADDLIST                                                          
         CLEAR MWAFCC              Reset 'cc' flag                              
         B     MAILSCAN                                                         
*                                                                               
MDATE    IF    ^CPSFSPR,CVNVALID   Restricted                                   
         VCALL DEQUOTE                                                          
         CEIL  R0,L'MWADATE                                                     
         MVC   MWADATE,CVBLANKS                                                 
         LR    R15,R0                                                           
         MOVE  R15,MWADATE,@R1                                                  
         B     MAILSCAN                                                         
*                                                                               
MSENDER  IF    ^CPSFSPR,CVNVALID   Restricted                                   
         VCALL DEQUOTE                                                          
         VCALL RTRIM                                                            
         IF    (R0,NP),MAILSCAN                                                 
         CEIL  R0,L'MWAFROM                                                     
         MVC   MWAFROM,CVBLANKS                                                 
         LR    R15,R0                                                           
         MOVE  R15,MWAFROM,@R1     Save from address                            
*                                                                               
         MVC   MWAFROMH,MWAFROM    Save "from with hostname" addr               
*                                                                               
         CLEAR R15                 Flag: no @ found                             
         CLEAR R1                  Addr of first non-blank char                 
         LA    R2,MWAFROMH         Start of from name                           
         LA    R3,MWAFROMH+L'MWAFROMH-32-2  End of from name                    
*                                                                               
         WHILE (R3,GE,R2),BEGIN    Back scan name...                            
         IF    (@R3,EQ,'@'),'LA R15,1'  Flag: @ found                           
         IF    ((R1,Z),AND,(@R3,NE,' ')),'LR R1,R3'  Save last pos              
         DECR  R3                  Back up a position                           
         END                                                                    
*                                                                               
         IF    (R1,Z),'MVI MWAFROMH,C"?"; LA R1,MWAFROMH'                       
*                                                                               
         IF    (R15,Z),BEGIN       Add the "@stanford" suffix...                
         MVI   @R1+1,C'@'                                                       
         MVC   @R1+2(L'CVINSTAL),CVINSTAL                                       
*                                                                               
         IF    (CVINSTAL,EQ,'STANFORD'),BEGIN                                   
         IF    CVFRLG,BEGIN  Stanford/RLG...                                    
         MVC   @R1+2(32),=CL32'RLG.ORG'                                         
         END                                                                    
         ELSE  BEGIN               Stanford/Forsythe...                         
         MVC   @R1+2(32),=CL32'Forsythe.Stanford.EDU'                           
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         B     MAILSCAN                                                         
*                                                                               
MNORET   MVC   MWARET,CVBLANKS                                                  
         B     MAILSCAN                                                         
*                                                                               
MRETURN  VCALL DEQUOTE                                                          
         CEIL  R0,L'MWARET                                                      
         MVC   MWARET,CVBLANKS                                                  
         LR    R15,R0                                                           
         MOVE  R15,MWARET,@R1                                                   
         B     MAILSCAN                                                         
*                                                                               
MNOHDR   SET   MWAFNHDR                                                         
         BR    RAR                                                              
*                                                                               
MMINFO   SET   MWAFMINF                                                         
         BR    RAR                                                              
*                                                                               
MNAME    VCALL DEQUOTE                                                          
         BM    CVNVALID                                                         
         MVC   MWANAME,CVBLANKS                                                 
         CEIL  R0,L'MWANAME                                                     
         LR    R15,R0                                                           
         MOVE  R15,MWANAME,@R1                                                  
         IF    (MWANAME,EQ,CVBLANKS),'CLEAR MWANAME'                            
         B     MAILSCAN                                                         
*                                                                               
MRECIP   VCALL DEQUOTE                                                          
         BM    CVNVALID                                                         
         MVC   MWARNAME,CVBLANKS                                                
         CEIL  R0,L'MWARNAME                                                    
         LR    R15,R0                                                           
         MOVE  R15,MWARNAME,@R1                                                 
         IF    (MWARNAME,EQ,CVBLANKS),'CLEAR MWARNAME'                          
         B     MAILSCAN                                                         
*                                                                               
MTITLE   VCALL DEQUOTE                                                          
         BM    CVNVALID                                                         
         MVC   MWATITLE,CVBLANKS                                                
         CEIL  R0,L'MWATITLE                                                    
         LR    R15,R0                                                           
         MOVE  R15,MWATITLE,@R1                                                 
         IF    (MWATITLE,EQ,CVBLANKS),'CLEAR MWATITLE'                          
         B     MAILSCAN                                                         
*                                                                               
MNOTIFY  SET   MWAFNTFY                                                         
         BR    RAR                                                              
*                                                                               
MNONOTFY CLEAR MWAFNTFY                                                         
         BR    RAR                                                              
*                                                                               
MSILENT  SET   MWAFSIL                                                          
         BR    RAR                                                              
*                                                                               
MNOSIL   CLEAR MWAFSIL                                                          
         BR    RAR                                                              
*                                                                               
MDRYRUN  SET   MWAFDRYRUN                                                       
         BR    RAR                                                              
*                                                                               
MMHDR    SET   MWAFMHDR                                                         
         BR    RAR                                                              
*                                                                               
MUHDR    SET   MWAFUHDR                                                         
         BR    RAR                                                              
*                                                                               
MEMSFLAG SET   MWAFEMS             Being done from EMS                          
         BR    RAR                                                              
MFWDFLAG SET   MWAFFWD             This is a forwarded message                  
         BR    RAR                                                              
*                                                                               
         DROP  BR                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MGETMINF -- Get mail recipient list from record                              
*    group created by SYSCALL MAIL_INFO.                                        
*                                                                               
MGETMINF PROC  (R2,LSR)                                                         
         CLEAR R4                  Init loop counter                            
         LOOP  BEGIN                                                            
         LA    R15,CPRG16                                                       
         LA    R0,CP#MAIL          Our RG number                                
         VCALL XSETSET                                                          
*                                                                               
         SETMSG MWALINE             Area for data                               
         LA    R2,MWAMKEY          Key area                                     
         LA    R15,CPRG16                                                       
         IF    (R4,Z),BEGIN        If first iteration,                          
         CLEAR MWAMKEY             Clear key field                              
         VCALL XGETFRST            Call for first record                        
         END                                                                    
         ELSE  BEGIN               Else not first iteration                     
         VCALL XGETNEXT            Get next record                              
         END                                                                    
         IF    (R15,NZ),EXIT       Exit if no more                              
*                                                                               
         IF    (MWAMKEY,EQ,'RCPT'),BEGIN  If recipient entry,                   
         ACALL ADDLIST             Add to recipient list                        
         SET   MWAFSOME            Show have recipient                          
         END                                                                    
*                                                                               
         INCR  R4                  Incr loop count                              
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MTOKEN -- Routine to scan a mail id or a parenthesized list.                 
*    This is a little awkward because we don't use the "regular"                
*    scanner here.  Instead we use the new scanner, which can                   
*    handle tokens with special characters in them.                             
*                                                                               
*    On entry:                                                                  
*       CPOSCAN - pointing to the text to scan                                  
*                                                                               
*    On exit:                                                                   
*       CPOSCAN - scan ptrs updated                                             
*       R1,R0 - token loc, len (if nothing, R0=0)                               
*       R15 (and cc) - 0 = ok                                                   
*                      4 = no more tokens                                       
*                                                                               
MTOKEN   PROC  (R2,LSR)                                                         
         OSCTELL                                                                
*-                                                                              
*-       Trim leading blanks and/or commas.                                     
*-                                                                              
         WHILE ((R0,POS),AND,((@R1,EQ,' '),OR,(@R1,EQ,','))),BEGIN              
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         IF    (R0,POS),BEGIN      We have something...                         
*-                                                                              
*-       Use the old scanner to isolate a parenthesized list.                   
*-                                                                              
         IF    (@R1,EQ,'('),BEGIN  Parenthesized list...                        
         OSCAN                                                                  
         B     MTOKFIN                                                          
         END                                                                    
*-                                                                              
*-       Use the new scanner to isolate a single token.                         
*-                                                                              
         LS    R2,R3,' ,'          Scan skip string                             
         LS    R4,R5,' ,;'         Scan stop string                             
         VCALL SCANSTR             Isolate mail id                              
*                                                                               
         XPUSH R0,R1                                                            
         OSCSET (R2),(R3)          Update scan ptrs                             
         XPOP  R0,R1                                                            
         END                                                                    
*                                                                               
MTOKFIN  CLEAR R15                 Assume ok                                    
         IF    (R0,NP),'LA R15,4'  Nothing found                                
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ADDLIST -- Routine to add mail id(s) to the mailing list.                    
*                                                                               
*    On entry:                                                                  
*      R1,R0 - id loc, len                                                      
*                                                                               
ADDLIST  PROC                                                                   
         INCR  R2,MWANEST          Incr nesting level                           
*-                                                                              
*-       Add a single mail id.                                                  
*-                                                                              
         IF    (@R1,NE,'('),BEGIN                                               
         ACALL ADDITEM             Only one mail id                             
         B     ADDLEXIT            All done                                     
         END                                                                    
*-                                                                              
*-       Add a parenthesized list of mail ids separated by blanks or            
*-         commas (e.g., (aaa,bbb,ccc)).                                        
*-                                                                              
         LA    R1,@R1+1            Skip past "("                                
         SH    R0,=H'2'            Deduct len of "(" and ")"                    
         BNP   ADDLEXIT            Nothing here, forget it                      
*-                                                                              
*-     Use "SCANSTR" instead of the original scanner because we want            
*-         to allow special characters and we only want to stop on              
*-         blanks and commas.                                                   
*-                                                                              
         LOOP  BEGIN                                                            
         LS    R2,R3,' ,'          Scan skip string                             
         LS    R4,R5,' ,'          Scan stop string                             
         VCALL SCANSTR             Isolate mail id                              
         IF    (R0,Z),ADDLEXIT     All done, scram                              
*                                                                               
         ACALL ADDITEM             Add this mail id                             
*                                                                               
         LR    R1,R2               Set new loc from remaining loc               
         LR    R0,R3               Ditto for length                             
         END                                                                    
*                                                                               
ADDLEXIT DECR  R2,MWANEST          Decr nesting level                           
         PEND                                                                   
         EJECT                                                                  
ADDIWA   RECORD BEGIN                                                           
ADDIPTRS DS    2A                  Id loc, len ptrs                             
ADDIOLDM DS    A                   Saved "working MID" ptr                      
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  ADDITEM -- Local routine to add a single mail id to the mailing              
*    list.                                                                      
*                                                                               
*    On entry:                                                                  
*      R1,R0 - id loc, len                                                      
*                                                                               
ADDITEM  PROC  ADDIWA                                                           
         VCALL DEQUOTE             Dequote, if quoted                           
         STM   R0,R1,ADDIPTRS      Save id loc, len                             
         MVC   ADDIOLDM,MWAWMIDP   Save working MID ptr                         
*-                                                                              
*-       Check for too many (probably recursive) id definitions.                
*-                                                                              
         VCALL SLICECHK            Keep others happy                            
*                                                                               
         INCR  R15,MWALCNT         Count number of Add's                        
         IF    (R15,GT,MWALMAX#),BEGIN  Something's wrong...                    
         XPUSH R0,R1                                                            
         TSEG  'Mail not sent to '                                              
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         ERROR ' -- Can''t resolve mail address'                                
         END                                                                    
*-                                                                              
*-       Allocate MID entry on the heap for the mail id information.            
*-                                                                              
         LA    R0,L'MID                                                         
         VCALL GETHEAP             Get a MID entry for this mail id             
         LR    R5,R1                                                            
         WITH  (MID,R5)                                                         
         CLEAR MID                                                              
         ST    R5,MWAWMIDP         Save working MID ptr                         
*                                                                               
         IF    MWAFCC,'SET MIDXFCC'  Flag if this is a 'cc' addr                
*                                                                               
         IF    (MWANEST,EQ,1),BEGIN  If command line address,                   
         SET   MIDXFHDR            Flag as address for header                   
         INCR  R0,MWARCPTN         Incr header address count                    
         END                                                                    
*-                                                                              
*-       Process electronic mail address.                                       
*-                                                                              
         LM    R0,R1,ADDIPTRS      Id loc, len                                  
*                                                                               
         LA    R15,MID                                                          
         ACALL SCANITEM            Parse the mail id                            
*                                                                               
         LA    R15,MID                                                          
         ACALL NETEMA              Process network mail address                 
         IF    NZ,ADDIEXIT         Skip mailing if error                        
*                                                                               
         LA    R15,MID                                                          
         ACALL LOCALEMA            Process local mail address                   
         IF    (R15,EQ,4),ADDIEXIT    Skip mailing if error                     
         IF    (R15,EQ,8),BEGIN    If forwarding address found,                 
         SET   MIDXFSKP            Skip this entry when mailing                 
         END                                                                    
*-                                                                              
*-       Link up MID entry on the queue of mail ids.                            
*-                                                                              
         L     R15,MWAMIDQT        Last MID entry                               
         WITH  (MID,R15),'ST R5,MIDLINK'  Link up the new MID entry             
         ST    R5,MWAMIDQT                                                      
*                                                                               
ADDIEXIT MVC   MWAWMIDP,ADDIOLDM   Restore working MID ptr                      
         PEND                                                                   
         EJECT                                                                  
DISTWA   RECORD BEGIN                                                           
DISTRG01 DS    2A                  Working registers                            
DISTCB   SEGCB                                                                  
DISTBUF  DS    CL256               Working seg buffer                           
*                                                                               
DISTRES  DS    CL256               Distribution list goes here                  
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DISTITEM -- Local routine to process a distribution list.                    
*                                                                               
*    On entry:                                                                  
*      R1,R0 - id loc, len                                                      
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - we processed the distribution list                                   
*      4 - not a distribution list; no action taken                             
*                                                                               
DISTITEM PROC  DISTWA                                                           
*-                                                                              
*-  Build an expression whose result is the text of the distribution            
*-         list (if it IS a distribution list).                                 
*-                                                                              
*-       Editorial:                                                             
*-       This is a deliberate departure from previous implementation            
*-          techniques used in WYLBUR.  The idea is that execs will             
*-     maintain and manipulate the distribution lists; WYLBUR simply            
*-          references them.  This provides power and simplicity and            
*-        exploits the extended exec enviornment.               7/86            
*-                                                                              
         STM   R0,R1,DISTRG01                                                   
         SEGINIT DISTBUF,,DISTCB                                                
*-                                                                              
*-       First check for a public distribution list with this name.             
*-                                                                              
         SEG   'REC_GET(PUB.MAIL_DISTNO,UPPER("'                                
         LM    R0,R1,DISTRG01      Restore item loc, len                        
         SEG   (R1),(R0)                                                        
         SEG   '"))'                                                            
*                                                                               
         TPUSH                                                                  
*                                                                               
         LA    R2,DISTRES          Put result here                              
         LA    R3,L'DISTRES                                                     
         SETMSG L:DISTCBLOC,L:DISTCBLENF                                        
         VCALL EVALSTR             Is the "id" a dist list?                     
*                                                                               
         LR    R4,R15                                                           
         TPOP  JUNK                                                             
         LTR   R15,R4                                                           
*                                                                               
         IF    NP,BEGIN            Yep...                                       
         SETMSG (R2),(R3)                                                       
         VCALL LRTRIM                                                           
         IF    (R0,POS),BEGIN      There is some text...                        
         ACALL ADDLIST             Add dist list id's (recursive)               
         CLEAR R15                 We processed the dist list                   
         B     DISTEXIT            Scram                                        
         END                                                                    
         END                                                                    
*-                                                                              
*- Not a public distribution list, now check for a user distribution            
*-         list with this name.                                                 
*-                                                                              
         SEGCLR                                                                 
         SEG   'REC_GET(SES.MAIL_DISTNO,UPPER("'                                
         LM    R0,R1,DISTRG01      Restore item loc, len                        
         SEG   (R1),(R0)                                                        
         SEG   '"))'                                                            
*                                                                               
         TPUSH                                                                  
*                                                                               
         LA    R2,DISTRES          Put result here                              
         LA    R3,L'DISTRES                                                     
         SETMSG L:DISTCBLOC,L:DISTCBLENF                                        
         VCALL EVALSTR             Is the "id" a dist list?                     
*                                                                               
         LR    R4,R15                                                           
         TPOP  JUNK                                                             
         LTR   R15,R4                                                           
*                                                                               
         IF    NP,BEGIN            Yep...                                       
         SETMSG (R2),(R3)                                                       
         VCALL LRTRIM                                                           
         IF    (R0,POS),BEGIN      There is some text...                        
         ACALL ADDLIST             Add dist list id's (recursive)               
         CLEAR R15                 We processed the dist list                   
         B     DISTEXIT            Scram                                        
         END                                                                    
         END                                                                    
*                                                                               
         LA    R15,4               Id is not ours                               
DISTEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SCANITEM -- Local routine to break a mail id into user and host              
*    components.                                                                
*                                                                               
*    On entry:                                                                  
*      R15 - MID entry ptr                                                      
*      R1,R0 - id loc, len                                                      
*                                                                               
*    On exit:                                                                   
*      MID - filled in with user and host                                       
*                                                                               
SCANITEM PROC                                                                   
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
******** CLEAR MIDX                Initialize working information               
*-                                                                              
*-       Allow mail id in angle brackets.                                       
*-                                                                              
         IF    (R0,POS),BEGIN      Check for <text>...                          
         IF    (@R1,NE,'<'),EXIT                                                
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         DECR  R15                                                              
         IF    (@R15,NE,'>'),EXIT                                               
*                                                                               
         LA    R1,@R1+1            Strip angle brackets                         
         SH    R0,=H'2'                                                         
         END                                                                    
*                                                                               
         LR    R4,R1               Save id ptr                                  
         LR    R3,R0                                                            
*-                                                                              
*-       Check for "user@host" notation.                                        
*-                                                                              
         LR    R1,R4                                                            
         AR    R1,R3                                                            
         DECR  R1                  Last char of mail id                         
         CLEAR R15                                                              
         WHILE (R15,LT,R3),BEGIN   Backscan id for "@"...                       
         IF    (@R1,EQ,'@'),BEGIN  User@host...                                 
         SR    R3,R15                                                           
         DECR  R3                  Adjust len for just "user"                   
*                                                                               
         IF    (R15,GT,L'MIDXHOST),BEGIN  Host name is too long...              
         TSEG  (R1),(R0)                                                        
         ERROR ' is too long for a host name, mail not sent.'                   
         END                                                                    
         MVC   MIDXHONC,CVBLANKS                                                
         MOVE  R15,MIDXHONC,@R1+1  Save host name                               
         B     SCNXUSER                                                         
         END                                                                    
*                                                                               
         DECR  R1                  Backup another character                     
         LA    R15,@R15+1                                                       
         END                                                                    
*                                                                               
SCNXUSER IF    (R3,GT,L'MIDXUSER),BEGIN  Userid is too long...                  
         TSEG  (R1),(R0)                                                        
         ERROR ' is too long for a mail address, mail not sent.'                
         END                                                                    
*                                                                               
         IF    (R3,NP),BEGIN       No user id supplied?                         
         ERROR 'Mail not sent, no user id in address.'                          
         END                                                                    
*                                                                               
         MVC   MIDXUSNC,CVBLANKS                                                
         MOVE  R3,MIDXUSNC,@R4     Save userid                                  
         MVC   MIDXUSER,MIDXUSNC   Save upper case copy                         
         MVC   MIDXHOST,MIDXHONC   Ditto                                        
         L     R15,CVUPPTBL                                                     
         TR    MIDXHOST,@R15       Make upper case                              
         TR    MIDXUSER,@R15       Ditto                                        
*                                                                               
         CLEAR R15                 (Neatness)                                   
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LOCALEMA -- Local routine to process a local mail address.                   
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - id is OK (or not ours)                                               
*      4 - error (a message has aleady been written)                            
*                                                                               
LOCEMWA  RECORD BEGIN                                                           
LOCEMPTR DS    2F                                                               
         END                                                                    
*                                                                               
LOCALEMA PROC  LOCEMWA                                                          
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         CLEAR R15                 Return code--assume OK                       
         IF    (MIDXHOST,NZ),LOCEEXIT  Not a local address, scram               
*                                                                               
         SETMSG MIDXUSER            User name (account)                         
         VCALL LRTRIM                                                           
         STM   R0,R1,LOCEMPTR      Save len and addr                            
*-                                                                              
*-       Check for distribution list                                            
*-                                                                              
         ACALL CHKITEM             Check for userid/dist conflict               
         IF    NZ,BEGIN            If error (msg written),                      
         LA    R15,4               Set RC=4                                     
         B     LOCEEXIT            Return                                       
         END                                                                    
*                                                                               
         LM    R0,R1,LOCEMPTR      Refresh len and addr                         
         ACALL DISTITEM            Check for dist list                          
         IF    Z,BEGIN             If entry was dist list,                      
         SET   MIDXFSKP            Skip entry when mailing                      
         B     LOCEEXIT            Return with RC=0                             
         END                                                                    
*                                                                               
         XPUSH ,,L'KWR,PTR=R4                                                   
         WITH  (KWR,R4)                                                         
         CLEAR KWR                                                              
*-                                                                              
*-       Just "uuu" is a special case.  We have to first check to               
*-         see if "GG.uuu" exists, and if it does then use that.                
*-         If not, we want to fall through and do the user-id                   
*-         lookup.                                                              
*-                                                                              
         LM    R0,R1,LOCEMPTR      Refresh len and addr                         
         IF    (R0,EQ,3),BEGIN     Possibly "uuu" of "GG.uuu"...                
         MVC   KWRGRP,CPSGRP       Logged on group code                         
         MVC   KWRUSER,@R1         "uuu"                                        
         KREAD KWR                                                              
         IF    ^KWRIFL.KWRIFVAL,EXIT  No such GG.uuu, scram                     
         MVC   MIDXACCT,KWRACCT    Save "uuugg" account                         
         MVC   MIDXUSER(2),MIDXACCT+3                                           
         MVI   MIDXUSER+2,C'.'                                                  
         MVC   MIDXUSER+3(3),MIDXACCT                                           
         B     LOCEVAL                                                          
         END                                                                    
*-                                                                              
*-       Look up the userid and get the account.                                
*-                                                                              
         LM    R0,R1,LOCEMPTR      Refresh len and addr                         
         LA    R15,MIDXACCT        Put "uuugg" account here                     
         VCALL GETACCT             Get account from userid                      
         IF    NZ,BEGIN            Local userid doesn't exist...                
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TSEG  ' is an unknown local address, mail not sent.',,WR               
         IF    (R2,NZ),'SET CPFQUIET'                                           
         LA    R15,4               Set error return code                        
         B     LOCEEXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Make sure the local account exists.                                    
*-                                                                              
         CLEAR KWR                                                              
         MVC   KWRACCT,MIDXACCT                                                 
         KREAD KWR                                                              
         IF    ^KWRIFL.KWRIFVAL,BEGIN                                           
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  'Account '                                                       
         TSEG  KWRGRP                                                           
         TSEG  '.'                                                              
         TSEG  KWRUSER                                                          
         TSEG  ' does not exist, mail not sent.',,WR                            
         BNZ   CVABORT                                                          
         IF    (R2,NZ),'SET CPFQUIET'                                           
         LA    R15,4               Set error return code                        
         B     LOCEEXIT            Skip this user                               
         END                                                                    
*-                                                                              
*-       We now have a valid account, so now get the primary                    
*-         userid and fill that in.                                             
*-                                                                              
LOCEVAL  LA    R15,MIDXACCT                                                     
         SETMSG MIDXUSER                                                        
         VCALL GETUID              Get primary userid                           
*                                                                               
         IF    (KWRNAME,NZ),BEGIN  User name is available...                    
         MVC   MIDXNAME,CVBLANKS                                                
         MVC   MIDXNAME(L'KWRNAME),KWRNAME  Save the user's name                
         END                                                                    
*                                                                               
         IF    (KWRMTXT,NZ),BEGIN                                               
         MVC   MIDXMTXT,CVBLANKS                                                
         MVC   MIDXMTXT(L'KWRMTXT),KWRMTXT  Save the user's mail text           
         END                                                                    
*                                                                               
         IF    ^KWROFL.KWRFMAIL,BEGIN                                           
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  'Mail not sent '                                                 
         TSEG  'because mail is not being accepted by '                         
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TSEG  '.',,WR                                                          
*        TSEG  ' is not accepting mail, mail not sent.',,WR                     
         BNZ   CVABORT                                                          
         IF    (R2,NZ),'SET CPFQUIET'                                           
         LA    R15,4               Set error return code                        
         B     LOCEEXIT            Skip this user                               
         END                                                                    
*-                                                                              
*-       Handle mail forwarding.                                                
*-                                                                              
         IF    (KWRMFOR,NZ),BEGIN  Forwarding account...                        
         SETMSG KWRMFOR             Forwarding mail-id                          
         VCALL LRTRIM                                                           
         ACALL ADDLIST             Add forwarding address                       
         LA    R15,8               We handled the orig mail-id                  
         B     LOCEEXIT                                                         
         END                                                                    
*-                                                                              
*-       Handle automatically BATMAILing mail if appropriate.                   
*-         For example, if XX.YYY's primary machine is SYSC, then               
*-         treat USERID as USERID@SYSC.                                         
*-                                                                              
         LC    R2,CVMACH           Our machine flag bit                         
         EX    R2,'TM KWRMPRIM,0'  Recipient on this machine?                   
         IF    Z,BEGIN             No, BATMAIL it...                            
*!!!!!!!!!! needs work (can't assume "GG.UUU" anymore) !!!!!!!!                 
* for now we just check if account is gg.uuu form, if not we do                 
* not send it to primary machine.  but hey what does a userid on                
* sysc or sysb mean to us.                                                      
         IF    (MIDXUSER+6,EQ,' '),BEGIN                                        
         IF    KWRMPRIM.KWRFSYSA,'MVC MIDXUSER+6(5),=C"@SYSA"'                  
         IF    KWRMPRIM.KWRFSYSB,'MVC MIDXUSER+6(5),=C"@SYSB"'                  
******** IF    KWRMPRIM.KWRFSYSC,'MVC MIDXUSER+6(5),=C"@SYSC"'                  
*                                                                               
         IF    (MIDXUSER+6,NE,' '),BEGIN                                        
         SETMSG MIDXUSER,11         "gg.uuu@SYSx"                               
         ACALL ADDLIST             Add this mail-id                             
         LA    R15,4               We handled the orig mail-id                  
         B     LOCEEXIT                                                         
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
         CLEAR KWR                                                              
         XPOP  ,,L'KWR                                                          
*                                                                               
         CLEAR R15                 Set good rc                                  
*                                                                               
LOCEEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETEMA -- Local routine to process a network mail address.                   
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - address is OK                                                        
*      4 - error (a message has aleady been written)                            
*                                                                               
NETEMA   PROC                                                                   
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         IF    (MIDXHOST,NZ),BEGIN  Network mail address...                     
         LA    R15,MIDXHOST        Host part of address                         
         ACALL ALIASCHK            Get real host name                           
         IF    NZ,BEGIN            Host name has changed...                     
         MVC   MIDXHONC,MIDXHOST   Move in new host name                        
         END                                                                    
*-                                                                              
*-  This hack can be deleted once this version of MAIL is                       
*-  running in all copies of Wylbur, and SYSC is aliases to ''                  
*-  in the host table.                                                          
*-                                                                              
         IF    (MIDXHOST,EQ,'SYSC '),BEGIN                                      
         MVC   MIDXHOST,CVBLANKS                                                
         END                                                                    
*                                                                               
         IF    (MIDXHOST,EQ,CVBLANKS),BEGIN  If local host alias,               
         CLEAR MIDXHOST                      Clear host names                   
         CLEAR MIDXHONC                                                         
         END                                                                    
*                                                                               
         IF    (MIDXHOST,Z),'CLEAR R15; B NETAEXIT'  Scram                      
*                                                                               
         LA    R15,MIDXHOST        Host name                                    
         ACALL HOSTCHK             Do we know about this host?                  
         IF    Z,BEGIN             Nope...                                      
         TSEG  MIDXHOST,,T                                                      
         TSEG  ' is an unknown host address, mail not sent.',,CR                
         LA    R15,4                                                            
         B     NETAEXIT                                                         
         END                                                                    
*                                                                               
         LR    R4,R15                                                           
         WITH  (HOST,R4)                                                        
         IF    HOSTFERR,BEGIN                                                   
         TSEG  'Mail not sent to '                                              
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TSEG  ', '                                                             
         IF    (HOSTTEXT,NZ),'TSEG HOSTTEXT,,T'  Error text                     
         LA    R15,4                                                            
         B     NETAEXIT                                                         
         END                                                                    
*                                                                               
         IF    HOSTFBIT,BEGIN      Bitnet mail...                               
         SET   MIDXFBIT            This is a Bitmail address                    
         END                                                                    
*                                                                               
         IF    HOSTFGTE,BEGIN      Gateway mail...                              
         SET   MIDXFGTE            This message goes to a gateway               
         END                                                                    
*                                                                               
         IF    HOSTFTST,'SET MIDXFTST'  Test host address                       
         IF    HOSTFBSMTP,'SET MIDXFBSMTP'  BSMTP header                        
         IF    HOSTFSUNMAIL,'SET MIDXFSUNMAIL'  SUNMAIL option                  
         IF    HOSTFBAT,'SET MIDXFBAT'  BATMAIL                                 
*                                                                               
         MVC   MIDXACCT,HOSTACCT   Save account                                 
         MVC   MIDXGATE,HOSTGATE   Gateway address                              
         MVC   MIDXRELY,HOSTRELY   Relay node address                           
*-                                                                              
*-       If target host cannot handle major domains, strip them off.            
*-                                                                              
         IF    HOSTFNDM,BEGIN      Domain suppression                           
         SETMSG MIDXHONC                                                        
         AR    R1,R0                                                            
         DECR  R1                  Point to last char in node name              
         CLEAR R15                                                              
DELDMN   WHILE (R15,LE,R0),BEGIN                                                
         IF    (@R1,EQ,'.'),BEGIN  Found domain?                                
         EX    R15,'MVC @R1(0),CVBLANKS'  Kill major domain                     
         EXIT  DELDMN                                                           
         END                                                                    
         LA    R15,@R15+1                                                       
         DECR  R1                                                               
         END                                                                    
         END                                                                    
*                                                                               
         END                                                                    
         CLEAR R15                 Set good rc                                  
*                                                                               
NETAEXIT PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  CHKITEM - Check for UserID/Distribution List Conflict                        
*                                                                               
*                                                                               
*  On Entry                                                                     
*    R1,R0 - id/account/list loc, len                                           
*                                                                               
*  On Exit                                                                      
*    R15 - zero all ok                                                          
*    R15 - non-zero, conflict, message given                                    
*                                                                               
*                                                                               
*                                                                               
CHKITEM  PROC                                                                   
*                                                                               
         COMMENT                   CHECK FOR DISTRIBUTION LIST                  
         ACALL CHKDLIST                                                         
         IF    NZ,BEGIN            NOT FOUND, ALL OK                            
         B     CHKIOK                                                           
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR ACCOUNT/USERID                     
         ACALL CHKACCID                                                         
         IF    NZ,BEGIN            NOT FOUND, ALL OK                            
         B     CHKIOK                                                           
         END                                                                    
         ELSE  BEGIN               BOTH ACCT/USRID AND LIST                     
         XPUSH R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid mail address, mail not sent. '                        
         TSEG  'UserID/Distribution list conflict. ',,WR                        
         TSEG  'Mail not sent to '                                              
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         TSEG  '.',,WR                                                          
         LA    R15,4                                                            
         B     CHKIDONE                                                         
         END                                                                    
*                                                                               
CHKIOK   LABEL ,                                                                
         CLEAR R15                                                              
*                                                                               
CHKIDONE LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  CHKDLIST - Check for Distribution List                                       
*                                                                               
*                                                                               
*  On Entry                                                                     
*    R1,R0 - id/account/list loc, len                                           
*                                                                               
*  On Exit                                                                      
*    R15 - zero if found                                                        
*    R15 - non-zero if not found                                                
*                                                                               
*                                                                               
*  Note:                                                                        
*  This code is all from DISTITEM routine. See more                             
*  detailed comments there.                                                     
*                                                                               
CHKDLIST PROC  DISTWA                                                           
*                                                                               
         COMMENT                   IS IT DISTRIBUTION LIST                      
         COMMENT                   CHECK IT OUT                                 
         STM   R0,R1,DISTRG01                                                   
         SEGINIT DISTBUF,,DISTCB                                                
*-                                                                              
*-       First check for a public distribution list with this name.             
*-                                                                              
         SEG   'REC_GET(PUB.MAIL_DISTNO,UPPER("'                                
         LM    R0,R1,DISTRG01      Restore item loc, len                        
         SEG   (R1),(R0)                                                        
         SEG   '"))'                                                            
*                                                                               
         TPUSH                                                                  
*                                                                               
         LA    R2,DISTRES          Put result here                              
         LA    R3,L'DISTRES                                                     
         SETMSG L:DISTCBLOC,L:DISTCBLENF                                        
         VCALL EVALSTR             Is the "id" a dist list?                     
*                                                                               
         LR    R4,R15                                                           
         TPOP  JUNK                                                             
         LTR   R15,R4                                                           
*                                                                               
         IF    NP,BEGIN            Yep...                                       
         SETMSG (R2),(R3)                                                       
         VCALL LRTRIM                                                           
         IF    (R0,POS),BEGIN      There is some text...                        
         B     GOTLIST                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*- Not a public distribution list, now check for a user distribution            
*-         list with this name.                                                 
*-                                                                              
         SEGCLR                                                                 
         SEG   'REC_GET(SES.MAIL_DISTNO,UPPER("'                                
         LM    R0,R1,DISTRG01      Restore item loc, len                        
         SEG   (R1),(R0)                                                        
         SEG   '"))'                                                            
*                                                                               
         TPUSH                                                                  
*                                                                               
         LA    R2,DISTRES          Put result here                              
         LA    R3,L'DISTRES                                                     
         SETMSG L:DISTCBLOC,L:DISTCBLENF                                        
         VCALL EVALSTR             Is the "id" a dist list?                     
*                                                                               
         LR    R4,R15                                                           
         TPOP  JUNK                                                             
         LTR   R15,R4                                                           
*                                                                               
         IF    NP,BEGIN            Yep...                                       
         SETMSG (R2),(R3)                                                       
         VCALL LRTRIM                                                           
         IF    (R0,POS),BEGIN      There is some text...                        
         B     GOTLIST                                                          
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   WE DID NOT FIND LIST                         
         LA    R15,4                                                            
         B     CHKDDONE                                                         
*                                                                               
GOTLIST  LABEL ,                                                                
         CLEAR R15                                                              
         B     CHKDDONE                                                         
*                                                                               
CHKDDONE LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  CHKACCID - Check for ACCOUNT/ID list                                         
*                                                                               
*                                                                               
*  On Entry                                                                     
*    R1,R0 - id/account/list loc, len                                           
*                                                                               
*  On Exit                                                                      
*    R15 - zero if found                                                        
*    R15 - non-zero if not found/account does not exist                         
*                                                                               
*                                                                               
*                                                                               
CHKAIWA  RECORD BEGIN                                                           
CHKAACCT DS    XL128                                                            
         END                                                                    
*                                                                               
CHKACCID PROC  CHKAIWA                                                          
*                                                                               
         COMMENT                   DO IT TO IT                                  
         LA    R15,CHKAACCT        Put account in "uuugg" form here             
         VCALL CHKACCT             Get/check account                            
         COMMENT                   Returns R15 set appropriately                
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SEGMID -- Local routine to seg the current mail id.                          
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
SEGMID   PROC                                                                   
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         LA    R2,CVBLANKS                                                      
         SETMSG MIDXNAME            Recipient name -- non-gateway               
         IF    MIDXFGTE+MIDXFBIT,'SETMSG MWARNAME'  Rec. nme. gate/bit          
         IF    (@R1,NE,0),'TSEG (R1),(R0),TB; LA R2,=C"<>"'                     
*                                                                               
         TSEG  @R2,1,T             <                                            
         SETMSG MIDXUSNC                                                        
         IF    (MIDXHONC,Z),'SETMSG MIDXUSER'  Caps vers if local addr          
         TSEG  (R1),(R0),T         Userid                                       
         SETMSG MIDXHOST            Host name                                   
         LR    R3,R0               Save                                         
         LR    R4,R1                                                            
         IF    (@R1,NE,0),'TSEG "@"; TSEG (R4),(R3),T'                          
         TSEG  @R2+1,1,T           >                                            
*                                                                               
         IF    (MIDXMTXT,NZ),BEGIN  Show mail message, if any...                
         TSEG  ' -- '                                                           
         TSEG  MIDXMTXT,,T                                                      
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
SENWSEC# EQU   5                   Number of seconds to wait if busy            
SENWMAX# EQU   5                   Maximum number of MAILDEFQ files             
*                                                                               
SENDWA   RECORD BEGIN                                                           
SENDFLAG FLAG                                                                   
         FLAG  SENFWAIT            - We've tried waiting a few secs             
*                                                                               
SENDQCNT DS    H                   Number of the MAILDEFQ data sets             
*                                                                               
SENDSMID DS    A                   Saved working MID ptr                        
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SENDMAIL -- Local routine to send a mail item.                               
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
SENDMAIL PROC  SENDWA                                                           
         CLEAR SENDWA                                                           
*                                                                               
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         MVC   SENDSMID,MWAWMIDP                                                
         ST    R5,MWAWMIDP         Set current MID ptr                          
*                                                                               
         CLEAR MWANDIR,MWADIR                                                   
         MVC   MWALINES,MWANLINS   Line count                                   
         MVC   MWACOUNT,MWANCNT    Byte count                                   
*-                                                                              
*-       Gather a few statistics.                                               
*-                                                                              
         INCR  R15,CVMSENT         Total number of messages sent                
         IF    MWAFEMS,'INCR R15,CVMEMS'  Number of EMS messages                
         IF    (CPCMNM,EQ,'SEN'),'INCR R15,CVMSEND'  No. SENDs                  
         IF    MIDXFBIT,'INCR R15,CVMBIT'  No. of Bitnet mails                  
*-                                                                              
*-       Dryrun version.                                                        
*-                                                                              
         IF    MWAFDRYRUN,BEGIN    DRYRUN...                                    
         TSEG  'Mail would have been sent to '                                  
         LA    R15,MID                                                          
         ACALL SEGMID              Userid                                       
         TWRITE                                                                 
         B     SENDDONE                                                         
         END                                                                    
*-                                                                              
*-       Send Bitnet mail.                                                      
*-                                                                              
         IF    MIDXFBIT,BEGIN      Bitnet mail...                               
         ACALL TRANNAME            Get rid of chars forbidden                   
         LA    R15,MID                                                          
         ACALL SENDBIT             Send Bitnet mail item                        
         B     SENDDONE                                                         
         END                                                                    
*-                                                                              
*-       Send mail to gateway.                                                  
*-                                                                              
         IF    MIDXFGTE,BEGIN      Gateway processing...                        
         ACALL TRANNAME            Get rid of chars forbidden                   
         LA    R15,MID                                                          
         ACALL SENDGTE             Send item to gateway                         
         B     SENDDONE                                                         
         END                                                                    
*-                                                                              
*-       Send batch mail.                                                       
*-                                                                              
         IF    MIDXFBAT,BEGIN      Batch job mail...                            
         LA    R15,MID                                                          
         ACALL SENDBAT             Send batch job mail item                     
         B     SENDDONE                                                         
         END                                                                    
*-                                                                              
*-       Send local mail.                                                       
*-                                                                              
MAILBS   CLEAR (CPDSNWA,DSAVSIZ)   Init DSNWA                                   
         MVC   CPSEQFLD(4),=H'-1,8'  Init seqflds                               
         MVI   CPLRCL,X'80'        Init file type                               
         SET   DSNDTYPE.DSNDDISK   It's a disk                                  
         SET   DSNWAF1.DSNFSTD+DSNFMYDS                                         
         MVC   DSNWADSN,=CL44'WYL.gg.uuu.MAIL'                                  
         MVC   DSNWADSN+4(2),MIDXACCT+3  Group                                  
         MVC   DSNWADSN+7(3),MIDXACCT  User                                     
         MVC   DSNWANL,=H'15'      L'WYL.gg.uuu.MAIL                            
*                                                                               
MAILBSLP SET   DSNFMAIL            Mail command (checked in DISK)               
         CLEAR DSNON               No special volume                            
         CLEAR CPFNOCAT            Use the catalog                              
*-                                                                              
*-       If the user's MAIL data set is in use, then switch to the              
*-         alternate MAILDEFQ data sets and try again.                          
*-                                                                              
         LA    R15,1               Temporary enq                                
         VCALL DENQ                Enqueue on MAIL data set                     
         IF    NZ,BEGIN            Data set in use...                           
         MVC   DSNWADSN,=CL44'WYL.gg.uuu.MAIL.DEFQnnnn'                         
         MVC   DSNWADSN+4(2),MIDXACCT+3  Group                                  
         MVC   DSNWADSN+7(3),MIDXACCT  User                                     
         INCR  R15,SENDQCNT        Kick MAILDEFQ file counter                   
         A     R15,=A(100000)      Force leading zeros for BTD                  
         BTD   DSNWADSN+20,4,(R15)  Add queue number                            
         MVC   DSNWANL,=H'24'      L'WYL.gg.uuu.MAIL.DEFQnnnn                   
*-                                                                              
*- After we have tried and failed to ENQ on many MAILDEFQ data sets;            
*-       try waiting a few seconds and then start over again.  If we            
*-         still can't ENQ on a MAILDEFQ after that then give up.               
*-                                                                              
         IF    (SENDQCNT,GT,SENWMAX#),BEGIN  Too many MAILDEFQs...              
         IF    ^SENFWAIT,BEGIN     Try waiting a few secs...                    
         LA    R15,SENWSEC#*100                                                 
         VCALL WAITER              Wait a bit                                   
         BNZ   CVABORT                                                          
         SET   SENFWAIT                                                         
         CLEAR SENDQCNT            Reset DEFQ counter                           
         B     MAILBS              Try it again                                 
         END                                                                    
*                                                                               
         TSEG  'Mail not sent to '                                              
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TSEG  '.  Mailbox is unavailable.'                                     
         MVC   CPERRID,=C'MLNOTSNT'                                             
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         IF    (R2,NZ),'SET CPFQUIET'                                           
         SET   CPFERROR                                                         
         B     SENDDONE            Try next account (if any)                    
         END                                                                    
*                                                                               
         B     MAILBSLP            Try adding mail to this file                 
         END                                                                    
*-                                                                              
*-       Open MAIL/MAILDEFQ data set.                                           
*-                                                                              
         VCALL EXTOPEN             Open data set, etc.                          
         LR    R2,R15                                                           
*                                                                               
         IF    (R2,NZ),BEGIN       Error...                                     
         IF    (R2,GT,4),BEGIN     Worse than just not found...                 
         TSEG  'Mail not sent to '                                              
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         ACALL MLOGERR             Log the error                                
         CLEAR R0                                                               
         IF   (R2,EQ,RTNNOACS),'SETMSG ".  Access not permitted."'              
         IF (R2,EQ,RTNVOLNA),'SETMSG ". Mailbox volume not available."'         
         IF   (R2,EQ,RTNBDVOL),'SETMSG ".  Invalid volume."'                    
         IF   (R0,Z),BEGIN         Display error code...                        
         TSEG  '.  Error code '                                                 
         TNUM  (R2)                                                             
         SETMSG '.'                                                             
         END                                                                    
*                                                                               
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  (R1),(R0)                                                        
         MVC   CPERRID,=CL8'MLNOTSNT'                                           
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         IF    (R2,NZ),'SET CPFQUIET'                                           
         SET   CPFERROR                                                         
         B     SENDDONE            Try next account (if any)                    
         END                                                                    
         END                                                                    
         ELSE  BEGIN               Process old data...                          
         LR    R0,R13                                                           
         LA    R1,SENDDONE                                                      
         STM   R0,R8,MWAREGS       Save regs in case of abort                   
         CLEAR CPFRDRNG                                                         
         SET   CPFEXPRG            Set explicit range                           
         SET   CPFSOME             (Avoids "No lines" message)                  
         CLEAR CPFSLN              Set range                                    
         MVC   CPLSLN,CVMAXLNO     ... to ALL                                   
         L     R15,MAILWRKX                                                     
         VCALL DESPOT              (Co-routine is MAILWRKX)                     
         IF    NZ,'TCLEAR; B CVNOACTN'                                          
         SET   CPFNOCAT            Don't look in the catalog again              
         END                                                                    
         IF    CPRFEXT,'VCALL EXTCLOSE'  (Enq will remain)                      
*                                                                               
MAILREWR SET   CPLFLG2.CPFSCRTC    Ok to replace                                
         CLEAR CPLRCL              Edit format                                  
         DOPEN 0,=A(MAILSIZE),SAVE  Now open for output                         
         IF    NZ,BEGIN                                                         
         LR    R2,R15              Save return code                             
*-                                                                              
*-       Disk is out of space, move the mail file.                              
*-                                                                              
         IF    (R2,EQ,RTNNOSPC),BEGIN  No space...                              
         IF    (DSNBADV,Z),'MVC DSNBADV,DSNON6'  Save bad disk name             
         INCR  R15,DSNVOLCT        Count the number of attempts                 
         IF    (R15,GT,5),EXIT     Too many retries, forget it                  
         CLEAR DSNON               Try again                                    
         CLEAR CPFNOCAT            We must use catalog                          
         SET   CPFRECAT            Recatalog                                    
         B     MAILREWR                                                         
         END                                                                    
*-                                                                              
*-       Write out "Mail not sent" error message.                               
*-                                                                              
         TCLEAR                                                                 
         TSEG  'Mail not sent to '                                              
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         ACALL MLOGERR             Log the error                                
         CLEAR R0                                                               
         IF    (R2,NEG),'SETMSG ".  Mail not edit format."'                     
         IF  (R2,EQ,RTNOVSP),'SETMSG ".  User''s disk space exceeded."'         
         IF    (R2,EQ,RTNNOACS),'SETMSG ".  Access not permitted."'             
         IF (R2,EQ,RTNVOLNA),'SETMSG ". Mailbox volume not available."'         
         IF    (R2,EQ,RTNNOSPC),'SETMSG ".  No space after scratch."'           
         IF    (R2,EQ,RTNBDVOL),'SETMSG ".  Invalid volume."'                   
         IF   (R0,Z),BEGIN         Display error code...                        
         TSEG  '.  Error code '                                                 
         TNUM  (R2)                                                             
         SETMSG '.'                                                             
         END                                                                    
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  (R1),(R0),CR                                                     
         BNZ   CVABORT                                                          
         IF    (R2,NZ),'SET CPFQUIET'                                           
         SET   CPFERROR                                                         
         B     SENDDONE            Try next account (if any)                    
         END                                                                    
*                                                                               
         LA    R2,2                Assume edit save                             
         L     R3,CPLRCL           Get lrecl                                    
         LTR   R3,R3               Is it edit or fixed                          
         BZ    ROPENN2             Br if edit                                   
         DECR  R3                  Set lrecl for ex moves                       
         ST    R3,CPNREC           Put in nrec                                  
         CLEAR R2                  Set displacement for fixed                   
ROPENN2  ST    R1,CPDRED           Save external I/O buff addr                  
         ST    R0,CPRCSZ           Save output record size                      
         AR    R1,R2               Point to addr for first text byte            
         ST    R1,CPNXAD           Save                                         
         ST    R2,CPNWCT           Into current count                           
*                                                                               
         CLEAR MWAWLNO             Init lineno                                  
*                                                                               
         LA    R1,MWAMDIR          Old mail dir                                 
         CLEAR R15                                                              
         ACALL SAVEDIR                                                          
*                                                                               
         LA    R1,MWAMNEW          New mail dir                                 
         LA    R15,L'SDIRFADD                                                   
         ACALL SAVEDIR                                                          
*                                                                               
*                                  FINAL WRITE REMOVED BY CH, 05/91             
*        CLEAR R15                 Do final write                               
*        VCALL EXTWRITE                                                         
*                                                                               
         VCALL EXTCLOSE                                                         
         IF    (R15,NZ),BEGIN      Error doing close...                         
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  'Mail not sent, error mailing to '                               
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TWRITE                                                                 
         IF    (R2,NZ),'SET CPFQUIET'                                           
         B     SENDDONE            Try next account (if any)                    
         END                                                                    
         VCALL DDEQ                                                             
*                                                                               
         XPUSH ,,L'KWR,PTR=R4                                                   
         USING KWR,R4                                                           
         CLEAR KWR                                                              
         MVC   KWRGRP,MIDXACCT+3   gg                                           
         MVC   KWRUSER,MIDXACCT    uuu                                          
         KREAD KWR                                                              
         LA    R3,10               Max KWRITE retry loop count                  
         WHILE ((R3,POS),AND,KWRIFL.KWRIFVAL),BEGIN                             
*  Quick check to see if proper status bits are already set...                  
         IF    (KWRSFL.KWRSFMAI,AND,(SENDQCNT,Z)),EXIT                          
*                                                                               
         SET   KWRSFL.KWRSFMAI     New mail waiting                             
         IF    (SENDQCNT,NZ),'SET KWRSFL.KWRSFMLQ'  Mail in Q-file              
         KWRITE KWR                                                             
         IF    KWRHFL.KWRHFUOK,EXIT                                             
         DECR  R3                                                               
         END                                                                    
         CLEAR KWR                                                              
         XPOP  ,,L'KWR                                                          
         DROP  KWR                                                              
*                                                                               
         LA    R3,100              Dummy MILSERV rc -- nonotify                 
         IF    MWAFNTFY,BEGIN      Notify...                                    
         IF    ((CPSGRP,EQ,MIDXACCT+3),AND,                            *        
               (CPSUSER,EQ,MIDXACCT)),EXIT  It's us, nonotify                   
         TCLEAR                                                                 
         TSEG  'Mail received from '                                            
*OLD-                                                                           
*OLD-    Temporarily use the "gg.uuu" instead of our userid to                  
*OLD-      avoid confusion.                                                     
*OLD-                                                                           
*OLD     IF    (CPUID,EQ,MWAFROM),BEGIN  It's our userid...                     
*OLD     IF    (MWANAME,NZ),'TSEG MWANAME,,TB; TSEG "<"'                        
*OLD     TSEG  CPSGRP                                                           
*OLD     TSEG  '.'                                                              
*OLD     TSEG  CPSUSER                                                          
*OLD     IF    (MWANAME,NZ),'TSEG ">"'                                          
*OLD     END                                                                    
*OLD                                                                            
*OLD     ELSE  BEGIN               Otherwise, use what is given...              
         SETMSG MWAFROM             userid                                      
         VCALL LRTRIM                                                           
         IF    (MWANAME,NZ),BEGIN                                               
         XPUSH R0,R1                                                            
         TSEG  MWANAME,,TB                                                      
         TSEG  '<'                                                              
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         SETMSG '>'                                                             
         END                                                                    
         TSEG  (R1),(R0)                                                        
*OLD     END                                                                    
*                                                                               
         IF    (MWATITLE,NZ),BEGIN  Subject...                                  
         TSEG  ': '                                                             
         TSEG  MWATITLE,,T                                                      
         END                                                                    
         L     R2,MWANLINS                                                      
         DECR  R2                                                               
         IF    (R2,POS),BEGIN                                                   
         TSEG  '; '                                                             
         TNUM  (R2)                                                             
         TSEG  ' lines'                                                         
         END                                                                    
*                                                                               
         L     R15,CPSEGLENF                                                    
         LR    R0,R15                                                           
         L     R14,CPSEGLOC                                                     
         MOVE  R15,MWALINE,@R14                                                 
         XPUSH R0                                                               
         TCLEAR                                                                 
*                                                                               
         XPOP  R0                                                               
         SETMSG MWALINE,(R0)                                                    
         LA    R15,MIDXACCT                                                     
         VCALL SENDMSG             Send TO messages                             
         LR    R3,R15                                                           
         END                                                                    
*                                                                               
         IF    (CPCMNM,EQ,'MAI'),BEGIN                                          
         TSEG  'Mail sent to '                                                  
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         CLEAR R0                                                               
         IF    (R3,Z),'SETMSG " (User notified)"'                               
         IF    (R3,EQ,4),'SETMSG " (User logged on but not notified)"'          
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
SENDDONE TWRITE                                                                 
         PFREE PAR                                                              
         PFREE PBR                                                              
*                                                                               
         IF    CPRFEXT,BEGIN                                                    
         VCALL EXTCLOSE                                                         
         VCALL DDEQ                                                             
         END                                                                    
*                                                                               
         CLEAR R2                                                               
         WHILE (R2,LT,MWANDIR),BEGIN                                            
         LA    R15,MWADIR(R2)                                                   
         PJUNK L2:@R15,TEMP                                                     
         LA    R2,@R2+2                                                         
         END                                                                    
         CLEAR MWANDIR,MWADIR                                                   
         MVC   MWAWMIDP,SENDSMID                                                
         PEND                                                                   
*                                                                               
         DS    0F                                                               
MAILWRKX DC    AL1(DESRTRN+LXCATRTN+PREST+DESABORT)                             
         DC    AL3(MAILRTNX)                                                    
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SENDBAT -- Routine to mail the current item as a batch job.                  
*    (This is how intercpu mail is handled.)                                    
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
SBATWA   RECORD BEGIN                                                           
SBATFLAG FLAG                                                                   
         FLAG  SBATFQ              - QUIET originally specified                 
*                                                                               
SBATCB   SEGCB                                                                  
*                                                                               
SBATBUF  DS    XL256               Local SEG buffer                             
         END                                                                    
*-                                                                              
SENDBAT  PROC  SBATWA                                                           
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         CLEAR SBATFLAG                                                         
*                                                                               
         IF    CPFQUIET,'SET SBATFQ'  Remember QUIET                            
         SET   CPFQUIET                                                         
*                                                                               
         SEGINIT SBATBUF,,SBATCB                                                
         SEG   'HOLD NONOTIFY '                                                 
*                                                                               
         SEG   'SYSAFF '                                                        
         SEGT  MIDXRELY            Send to relay host                           
*                                                                               
         CLEAR MWAWDIR,MWAWOFFS,MWAWHCNT  Init working dir ptrs                 
*                                                                               
         OSCINIT L:SBATCBLOC,L:SBATCBLENF  RUN options                          
         LA    R1,SBATHDR          (RUNMSG header subroutine)                   
         LA    R15,SBATRTN         (RUNMSG co-routine)                          
         VCALL RUNMSG              Send message to JES                          
*                                                                               
         IF    ^SBATFQ,'CLEAR CPFQUIET'  Restore non-QUIET                      
*                                                                               
         IF    (CPCMNM,EQ,'MAI'),BEGIN                                          
         TSEG  'Mail sent to '                                                  
         LA    R15,MID                                                          
         ACALL SEGMID              Userid@Host                                  
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SBATHDR -- Local routine to seg Batmail header lines.                        
*                                                                               
*  On entry:                                                                    
*    R15 - SEGCB ptr                                                            
*                                                                               
SBATHDR  PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
*                                                                               
         L     R5,MWAWMIDP                                                      
         WITH  (MID,R5)                                                         
*                                                                               
         LR    R4,R15              SEGCB ptr                                    
         SEGDEF (R4)                                                            
*-                                                                              
*-       Sender information.                                                    
*-                                                                              
         IF    (MWAFROMH,NZ),BEGIN                                              
         SEG   ';SENDER:'                                                       
         SEGT  MWAFROMH                                                         
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Sender's name.                                                         
*-                                                                              
         IF    (MWANAME,NZ),BEGIN                                               
         SEG   ';NAME:'                                                         
         SEGT  MWANAME                                                          
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Notify information.                                                    
*-                                                                              
         IF    ^MWAFSIL,BEGIN      Not silent...                                
         SEG   ';NOTIFY:'                                                       
         SEGT  MWAFROM             Our userid                                   
         SEG   '@'                                                              
         SEGT  CVMACHID            Machine name                                 
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Mail title (subject).                                                  
*-                                                                              
         IF    (MWATITLE,NZ),BEGIN  Title...                                    
         SEG   ';TITLE:'                                                        
         SEGT  MWATITLE                                                         
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Mail recipient.                                                        
*-                                                                              
         SEG   ';TO:'                                                           
         SEGT  MIDXUSNC            Userid                                       
         IF    (MIDXHONC,NZ),'SEG "@"; SEGT MIDXHONC'  @Host                    
         SEGWR                                                                  
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SBATRTN -- This routine gets the next line in the new mail                   
*    directory and returns it (unpressed).  R1 is zero if there                 
*    aren't any more lines.                                                     
*                                                                               
*    This is a RUNMSG co-routine.                                               
*                                                                               
SBATRTN  PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
*                                                                               
         LH    R4,MWAWDIR          Working dir offset                           
SBATLP   IF    (R4,GE,MWANNEW),BEGIN  All done...                               
         CLEAR R1                  End of file                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN               Get next line...                             
         LA    R15,MWANEW(R4)                                                   
         PGET  PAR,L2:@R15         Get page                                     
*                                                                               
         LH    R3,MWAWOFFS         Working page offset                          
         LH    R15,@PAR                                                         
         IF    (R3,GE,R15),BEGIN   Next page...                                 
         LA    R4,@R4+2                                                         
         STH   R4,MWAWDIR                                                       
         CLEAR MWAWOFFS            Reset page offset                            
         B     SBATLP                                                           
         END                                                                    
*                                                                               
         LA    R15,@PAR+2(R3)      Pressed line ptr                             
         SH    R15,=H'4'           Backup for fake lineno                       
*                                                                               
         LC    R2,@PAR+2(R3)       Pressed line length                          
         LA    R3,@R2+1(R3)        Offset for the next line                     
         STH   R3,MWAWOFFS         Save new page offset                         
*                                                                               
         LA    R1,MWALINE          Put unpressed line here                      
         VCALL UNPRESS                                                          
         IF    (@R1,EQ,'*N'),SBATLP  Skip mail header                           
         IF    (@R1,EQ,'**'),'LA R1,@R1+1; DECR R0'                             
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         LR    R15,R1              Set CC for caller                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SENDBIT -- Routine to mail the current item as a PRINT job for               
*    JES.  This is how Bitnet mail is handled.                                  
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
SBITWA   RECORD BEGIN                                                           
SBITFLAG FLAG                                                                   
         FLAG  SBITFQ              - QUIET originally specified                 
*                                                                               
SBITCB   SEGCB                                                                  
*                                                                               
SBITBUF  DS    XL256               Local SEG buffer                             
         END                                                                    
*-                                                                              
SENDBIT  PROC  SBITWA                                                           
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         CLEAR SBITFLAG                                                         
*                                                                               
         IF    CPFQUIET,'SET SBITFQ'  Remember QUIET                            
         SET   CPFQUIET                                                         
*                                                                               
         SEGINIT SBITBUF,,SBITCB                                                
         IF    MWAFSIL,'SEG "FORMS=QUIET "'                                     
         SEG   'WRAP=80 SYSOUT=E TO "'                                          
         SEGT  MIDXUSER            Userid                                       
         SEG   '@'                 @                                            
         SETMSG MIDXHOST            Host                                        
         VCALL LRTRIM                                                           
         IF    (R0,GE,8),BEGIN     Check for ".BITNET#" suffix...               
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         SH    R15,=H'8'           Last 8 chars                                 
         IF    (@R15,EQ,'.BITNET#'),'DECR R0'  Just use ".BITNET"               
         END                                                                    
         SEG   (R1),(R0)                                                        
         SEG   '" SENDER "'                                                     
*-                                                                              
*-       Quote the sender string.                                               
*-                                                                              
         SETMSG MWAFROMH            Sender                                      
         VCALL RTRIM                                                            
         LR    R3,R1                                                            
         LR    R2,R0                                                            
*                                                                               
         WHILE (R2,POS),BEGIN                                                   
         SEG   @R3,1                                                            
         IF    (@R3,EQ,'"'),'SEG @R3,1'  Double the quote                       
         LA    R3,@R3+1                                                         
         DECR  R2                                                               
         END                                                                    
*                                                                               
         SEG   '"'                                                              
*                                                                               
         CLEAR MWAWDIR,MWAWOFFS,MWAWHCNT  Init working dir ptrs                 
*                                                                               
         OSCINIT L:SBITCBLOC,L:SBITCBLENF  PRINT command options                
         CLEAR R0                  (Options)                                    
         LA    R1,SBITHDR          (Header subroutine)                          
         LA    R15,SBITRTN         (Co-routine)                                 
         VCALL PRINTMSG            Send message to JES                          
*                                                                               
         IF    ^SBITFQ,'CLEAR CPFQUIET'  Restore non-QUIET                      
*                                                                               
         SETMSG 'Mail sent to '                                                 
         IF    CPFBDEST,'SETMSG "Mail not sent to "; CLEAR CPFBDEST'            
         TSEG  (R1),(R0)                                                        
         LA    R15,MID                                                          
         ACALL SEGMID              Userid@Host                                  
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SBITHDR -- Local routine to seg Bitnet mail header lines.                    
*                                                                               
*    On entry:                                                                  
*      R15 - SEGCB ptr                                                          
*                                                                               
SBITHDR  PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
*                                                                               
         L     R5,MWAWMIDP                                                      
         WITH  (MID,R5)                                                         
*                                                                               
         LR    R4,R15                                                           
         SEGDEF (R4)                                                            
*                                                                               
         IF    MWAFNHDR,SBHEXIT    Don't send our mail header                   
*-                                                                              
*-       DATE line.                                                             
*-                                                                              
         SEG   '1Date:      '                                                   
         XPUSH ,,32,PTR=R15                                                     
         VCALL LOCALTOD            Current date                                 
         VCALL FMTXDATE                                                         
         SEG   (R1),(R0)           Formatted date                               
         XPOP  ,,32                                                             
         SEGWR                                                                  
*-                                                                              
*-       TO line.                                                               
*-                                                                              
         SEG   ' To:        '                                                   
*                                                                               
         CLEAR R3                  Flag: no name                                
         IF    (MWARNAME,NZ),BEGIN                                              
         SEG   '"'                                                              
         SEGT  MWARNAME            Recipient's name                             
         SEG   '" <'                                                            
         LA    R3,1                Flag: name                                   
         END                                                                    
*                                                                               
         SEGT  MIDXUSER            Userid                                       
         SEG   '@'                 @                                            
         SETMSG MIDXHOST                                                        
         VCALL LRTRIM                                                           
         SH    R0,=H'7'            Get rid of ".BITNET"                         
         SEG   (R1),(R0)           Host                                         
         IF    (R3,NZ),'SEG ">"'   Ending bracket                               
         SEGWR                                                                  
*-                                                                              
*-       FROM line.                                                             
*-                                                                              
         SEG   ' From:      '                                                   
*                                                                               
         CLEAR R3                  Flag: no name                                
         IF    (MWANAME,NZ),BEGIN                                               
         SEG   '"'                                                              
         SEGT  MWANAME             Sender's name                                
         SEG   '" <'                                                            
         LA    R3,1                Flag: name                                   
         END                                                                    
*-                                                                              
*-       The following code converts "user@forsythe.stanford.edu"               
*-         back to "user@STANFORD.BITNET".                                      
*-                                                                              
         XPUSH ,,L'MWAFROMH,PTR=R2                                              
         MVC   @R2(L'MWAFROMH),MWAFROMH                                         
         SETMSG (R2),L'MWAFROMH                                                 
         VCALL LRTRIM                                                           
         ACALL BITFIX                                                           
         SEGT  (R1),(R0)           User@host                                    
         XPOP  ,,L'MWAFROMH                                                     
*                                                                               
         IF    (R3,NZ),'SEG ">"'                                                
         SEGWR                                                                  
*-                                                                              
*-       SUBJECT line.                                                          
*-          (Wrap text so that no lines are over 80 characters.)                
*-                                                                              
         IF    (MWATITLE,NZ),BEGIN  We have a title...                          
         LA    R2,MWATITLE+L'MWATITLE-1   Text end address                      
         LA    R3,MWATITLE                Text start address                    
         WHILE ((R2,GT,R3),AND,(@R2,EQ,' ')),'DECR R2'  Trim                    
         SR    R2,R3               Get trimmed                                  
         INCR  R2                    text length                                
         SEG   ' Subject: '        Start header line                            
         WHILE (R2,P),BEGIN                                                     
         SEG   '  '                Leading blanks                               
         LR    R0,R2               Copy text length                             
         LR    R1,R3               Copy text address                            
         CEIL  R0,69               Truncate length                              
         SR    R2,R0               Decr remaining text length                   
         AR    R3,R0               Incr remaining text pointer                  
         SEGWR (R1),(R0)           Write line                                   
         END                                                                    
         END                                                                    
*                                                                               
         SEGWR ' '                                                              
*                                                                               
SBHEXIT  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SBITRTN -- This routine gets the next line in the new mail                   
*    directory and returns it (unpressed).  R1 is zero if there                 
*    aren't any more lines.                                                     
*                                                                               
*    This is a PRINTMSG co-routine.                                             
*                                                                               
SBITRTN  PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
*                                                                               
         LH    R4,MWAWDIR          Working dir offset                           
SBITLP   IF    (R4,GE,MWANNEW),BEGIN  All done...                               
         CLEAR R1                  End of file                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN               Get next line...                             
         LA    R15,MWANEW(R4)                                                   
         PGET  PAR,L2:@R15         Get page                                     
*                                                                               
         LH    R3,MWAWOFFS         Working page offset                          
         LH    R15,@PAR                                                         
         IF    (R3,GE,R15),BEGIN   Next page...                                 
         LA    R4,@R4+2                                                         
         STH   R4,MWAWDIR                                                       
         CLEAR MWAWOFFS            Reset page offset                            
         B     SBITLP                                                           
         END                                                                    
*                                                                               
         LA    R15,@PAR+2(R3)      Pressed line ptr                             
         SH    R15,=H'4'           Backup for fake lineno                       
*                                                                               
         LC    R2,@PAR+2(R3)       Pressed line length                          
         LA    R3,@R2+1(R3)        Offset for the next line                     
         STH   R3,MWAWOFFS         Save new page offset                         
*                                                                               
         LA    R1,MWALINE          Put unpressed line here                      
         VCALL UNPRESS                                                          
         IF    (@R1,EQ,'*N'),SBITLP  Skip mail header                           
         IF    (@R1,EQ,'**'),'LA R1,@R1+1; DECR R0'                             
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         LR    R15,R1              Set CC for caller                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SENDGTE -- Routine to mail the current item as a PUNCH job for               
*    JES.  This is how mail routed through gateways is handled.                 
*                                                                               
*    On entry:                                                                  
*      R15 - MID ptr                                                            
*                                                                               
SGTEWA   RECORD BEGIN                                                           
SGTEFLAG FLAG                                                                   
         FLAG  SGTEFQ              - QUIET originally specified                 
*                                                                               
SGTECB   SEGCB                                                                  
*                                                                               
SGTEBUF  DS    XL64                Local SEG buffer                             
         END                                                                    
*-                                                                              
SENDGTE  PROC  SGTEWA                                                           
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
         LR    R5,R15                                                           
         WITH  (MID,R5)                                                         
*                                                                               
         CLEAR SGTEFLAG                                                         
*                                                                               
         IF    CPFQUIET,'SET SGTEFQ'  Remember QUIET                            
         SET   CPFQUIET                                                         
*                                                                               
         SEGINIT SGTEBUF,,SGTECB                                                
*-                                                                              
*-       If test is set, make outgoing stuff held.                              
*-                                                                              
         IF    MIDXFTST,'SEG "HOLD "'                                           
*                                                                               
         IF    MWAFSIL,'SEG "FORMS=QUIET "'                                     
         SEG   'SYSOUT=E TO '                                                   
         SEGT  MIDXGATE            Gateway address                              
*                                                                               
         CLEAR MWAWDIR,MWAWOFFS,MWAWHCNT  Init working dir ptrs                 
*                                                                               
         OSCINIT L:SGTECBLOC,L:SGTECBLENF  PUNCH command options                
         LA    R0,2                Option is PUNCH                              
         IF    MIDXFBSMTP,'LA R0,3'  Option=3 if BSMTP                          
         IF    MIDXFSUNMAIL,'LA R0,4'  Option=4 if SUNMAIL                      
         LA    R1,SGTEHDR          (Header subroutine)                          
         LA    R15,SGTERTN         (Co-routine)                                 
         VCALL PRINTMSG            Send message to JES                          
*                                                                               
         IF    ^SGTEFQ,'CLEAR CPFQUIET'  Restore non-QUIET                      
*                                                                               
         TSEG  'Mail sent to '                                                  
         LA    R15,MID                                                          
         ACALL SEGMID              Userid@Host                                  
         TWRITE ,                                                               
*-                                                                              
*-       If BSMTP/SUNMAIL there may have been multiple recipients               
*-       processed with mail item.  Look thru MID queue for flagged             
*-       entries and write messages for them.                                   
*-                                                                              
         IF    MIDXFBSMTP,BEGIN    If BSMTP/SUNMAIL                             
         WHILE ('LT R5,MIDLINK',NZ),BEGIN                                       
         IF    MIDXFSNT,BEGIN      If sent with ths mail,                       
         CLEAR MIDXFSNT            Reset flag                                   
         TSEG  'Mail sent to '                                                  
         LA    R15,MID                                                          
         ACALL SEGMID                                                           
         TWRITE ,                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SGTEHDR -- Local routine to seg BSMTP and ARPA header lines for              
*    gateway mail transmission.                                                 
*                                                                               
*    On entry:                                                                  
*      R15 - SEGCB ptr                                                          
*                                                                               
SGTEHWA  RECORD BEGIN                                                           
         DS    F                   Reserved                                     
SGTERCPT DS    F                   Recipient counter                            
SGTEHWAF FLAG                                                                   
         FLAG  SGTEHFLN            'To' and 'cc' temp flag                      
SGTEHWTO DS    CL2                 'To' or 'cc' text                            
SGTEHWAH DS    CL24                Local host name                              
         END                                                                    
*-                                                                              
SGTEHDR  PROC  SGTEHWA                                                          
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
         L     R5,MWAWMIDP                                                      
         WITH  (MID,R5)                                                         
*                                                                               
         LR    R4,R15                                                           
         SEGDEF (R4)                                                            
*-                                                                              
*-       Set up local host name in appropriate format                           
*-                                                                              
         MVC   SGTEHWAH,=CL(L'SGTEHWAH)'Forsythe.Stanford.EDU'                  
         IF    CVFRLG,BEGIN                                                     
         MVC   SGTEHWAH,=CL(L'SGTEHWAH)'RLG.ORG'                                
         END                                                                    
*                                                                               
         IF    MIDXFBSMTP,BEGIN    BSMTP headers...                             
*-                                                                              
*-       Envelope lines                                                         
*-                                                                              
         SEG   ' HELO '                                                         
         SEGWR SGTEHWAH                                                         
*                                                                               
         SEG   ' MAIL FROM:<'                                                   
*-                                                                              
*-       Use specific return address if one was specified.                      
*-                                                                              
         IF    (MWARET,NZ),BEGIN   Use specific return address...               
         SETMSG MWARET              Return address                              
         VCALL LRTRIM                                                           
         IF    (R0,GT,64),BEGIN    It's too long...                             
         IF    MIDXFSUNMAIL,EXIT   SUNet can handle long addresses              
         CLEAR R0                  Too long, use null addr                      
         END                                                                    
*                                                                               
         SEG   (R1),(R0)                                                        
         END                                                                    
*-                                                                              
*-       Use our from address, being careful to change                          
*-         "Forsythe.Stanford.EDU" to "STANFORD.BITNET" so we don't             
*-         confuse other Bitnet mailers.  This host name change is              
*-         not done for our local 'sunmail' style of BSMTP.                     
*-                                                                              
         ELSEIF ^MIDXFSUNMAIL,BEGIN  BSMTP only...                              
         XPUSH ,,L'MWAFROMH,PTR=R2                                              
         MVC   @R2(L'MWAFROMH),MWAFROMH                                         
         SETMSG (R2),L'MWAFROMH                                                 
         VCALL LRTRIM                                                           
         IF    (R0,GT,64),'CLEAR R0'  It's too long, use null addr              
*                                                                               
         ACALL BITFIX                                                           
         SEGT  (R1),(R0)           User@host                                    
         XPOP  ,,L'MWAFROMH                                                     
         END                                                                    
*-                                                                              
*-       For Sunmail, just use our from address.                                
*-                                                                              
         ELSE  BEGIN               Sunmail...                                   
         SEGT  MWAFROMH            User@host                                    
         END                                                                    
*                                                                               
         SEGWR '>'                                                              
*-                                                                              
*-       Generate 'RCPT TO' lines for each recipient with the same host         
*-                                                                              
         LR    R3,R5               Save current MID pointer                     
         CLEAR SGTERCPT            Clear recipient count                        
         WHILE (R5,NZ),BEGIN       Loop through rest of MID queue               
         IF    (^MIDXFSKP,AND,                     If not skip,        *        
               (MIDXHONC,EQ,MIDXHONC-MID(R3))),BEGIN  and host match,           
         INCR  R0,SGTERCPT         Incr recipient counter                       
         SEG   ' RCPT TO:<'        Internet address                             
         SEGT  MIDXUSNC                                                         
         SETMSG '%'                                                             
         IF    (MIDXRELY,Z),'SETMSG "@"'  If not through relay...               
         SEG   (R1),(R0)                                                        
         SEGT  MIDXHONC                                                         
         IF    (MIDXRELY,NZ),BEGIN   Need to add on relay node?                 
         SEG   '@'                                                              
         SEGT  MIDXRELY            Set relay name                               
         END                                                                    
         SEGWR '>'                                                              
         SET   MIDXFSKP            Flag as already processed                    
         SET   MIDXFSNT            FLag 'mail sent' msg needed                  
         END                                                                    
         IF    (MWAFEMS,AND,^MWAFMINF),EXIT  ** Only 1 addr if EMS              
         IF    (SGTERCPT,EQ,100),EXIT   ** LIMIT TO 100 RECIPIENTS **           
         L     R5,MIDLINK          Chain to next id                             
         END                                                                    
*                                                                               
         SEGWR ' DATA'                                                          
         END                                                                    
*-                                                                              
*-       Now produce ARPA standard message header.                              
*-                                                                              
         IF    MWAFNHDR,SGHEXIT    Don't send our mail header                   
*                                                                               
         SEG   ' Date:     '                                                    
         XPUSH ,,32,PTR=R15                                                     
         VCALL LOCALTOD            Current date                                 
         VCALL FMTXDATE                                                         
         B     SKIPSQZ             For patching                                 
         MVC   12(15,R1),14(R1)    Squeeze out century                          
         S     R0,=F'2'                                                         
SKIPSQZ  LABEL ,                                                                
         SEG   (R1),(R0)           Formatted date                               
         XPOP  ,,32                                                             
         SEGWR                                                                  
*-                                                                              
*-       'From:' line                                                           
*-                                                                              
         L     R5,MWAWMIDP         Get MID address                              
         IF    MIDXFSUNMAIL,BEGIN  SUNMAIL style...                             
         SEG   ' From:     '                                                    
         CLEAR R3                  Flag: no name                                
         IF    (MWANAME,NZ),BEGIN                                               
         SEG   '"'                                                              
         SEGT  MWANAME             Sender's name                                
         SEG   '"'                                                              
         SEGCOL 30                                                              
         SEG   '<'                                                              
         LA    R3,1                Flag: name                                   
         END                                                                    
*                                                                               
         SEGT  MWAFROMH            User@host                                    
         IF    (R3,NZ),'SEG ">"'                                                
         SEGWR                                                                  
         END                                                                    
         ELSE  BEGIN               Batch style...                               
         SEG   ' From:     '                                                    
         IF    (MWANAME,NZ),BEGIN                                               
         SEG   '"'                                                              
         SEGT  MWANAME             Sender's name                                
         SEG   '"  '                                                            
         END                                                                    
         SEG   '<'                                                              
*-                                                                              
*-       Copy host name and convert to Bitnet format if needed                  
*-                                                                              
         XPUSH ,,L'MWAFROMH,PTR=R2                                              
         MVC   @R2(L'MWAFROMH),MWAFROMH                                         
         SETMSG (R2),L'MWAFROMH                                                 
         VCALL LRTRIM                                                           
         ACALL BITFIX                                                           
         SEGT  (R1),(R0)           User@host                                    
         XPOP  ,,L'MWAFROMH                                                     
*                                                                               
         SEG   '>'                                                              
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Build 'To: and 'cc:' lines                                             
*-                                                                              
         MVC   SGTEHWTO,=C'To'     Init header text                             
*                                                                               
SGTEHRCP LOOP  BEGIN                                                            
*                                                                               
         CLEAR SGTEHFLN            Clear first line sent flg                    
         L     R5,MWAMIDQH         Get MID queue head                           
*-                                                                              
*-       Special ugly EMS hacks (hopefully temporary).                          
*-                                                                              
*-       If EMS mail with MAILINFO option, and there are                        
*-       more than 50 recipients just build a 'To' line of                      
*-                                                                              
*-          To:   "Distribution list":;                                         
*-                                                                              
*-       to avoid a long recipient list.                                        
*-                                                                              
*-       If EMS mail without MAILINFO, we are sending the mail                  
*-       one recipient at a time.  Just put this recipient in the               
*-       mail header.                                                           
*-                                                                              
         IF    MWAFEMS,BEGIN       If EMS,                                      
         IF    (MWAFMINF,AND,      If MAILINFO option used,            *        
               (MWARCPTN,GT,50)),BEGIN   and over 50 recipients,                
         SEGWR ' To:       "Distribution list":;'                               
         EXIT  SGTEHRCP                                                         
         END                                                                    
*                                                                               
         IF    ^MWAFMINF,BEGIN     If MAILINFO not used,                        
         L     R5,MWAWMIDP         Only do current entry                        
         SET   MIDXFHDR            Force header display                         
         END                                                                    
         END                                                                    
*                                                                               
         WHILE (R5,NZ),BEGIN                                                    
         IF    (MIDXFHDR,AND,                                          *        
               (((SGTEHWTO,EQ,'To'),AND,^MIDXFCC),OR,                  *        
               ((SGTEHWTO,EQ,'cc'),AND,MIDXFCC))),BEGIN                         
         IF    ^SGTEHFLN,BEGIN     If first line not done,                      
         SET   SGTEHFLN            Flag first line done                         
         SEG   ' '                                                              
         SEG   SGTEHWTO            Header type ('To' or 'cc')                   
         SEG   ':       '                                                       
         END                                                                    
         ELSE  BEGIN               Else continuation line,                      
         SEG   ','                 Add ',' to current line                      
         SEGWR ,                     and write it                               
         SEG   '           '       Spacer for new line                          
         END                                                                    
*                                                                               
         CLEAR R3                  Flag: no name                                
         IF    (MWARNAME,NZ),BEGIN                                              
         SEG   '"'                                                              
         SEGT  MWARNAME            Recipient's name                             
         SEG   '"'                                                              
         SEGCOL 30                                                              
         SEG   '<'                                                              
         LA    R3,1                Flag: name                                   
         END                                                                    
         ELSEIF (MIDXHONC,LE,' '),BEGIN   Else if local userid,                 
         IF    (MIDXNAME,GT,' '),BEGIN    If have user name,                    
         SEG   '"'                                                              
         SEGT  MIDXNAME                                                         
         SEG   '"'                                                              
         SEGCOL 30                                                              
         SEG   '<'                                                              
         LA    R3,1                Flag: name                                   
         END                                                                    
         END                                                                    
*                                                                               
         IF    ((MIDXUSER,GE,' '),AND,                                 *        
               (MIDXHONC,LE,' ')),BEGIN  If have local userid,                  
         SEGT  MIDXUSER                 Use it,                                 
         END                                                                    
         ELSE  BEGIN                    Else use command line uid               
         SEGT  MIDXUSNC                 Userid                                  
         END                                                                    
*                                                                               
         SETMSG '@'                                                             
*                                                                               
         IF    (^MIDXFBSMTP,AND,(MIDXRELY,NZ)),'SETMSG "%"'                     
         SEG   (R1),(R0)           Seg separator                                
         IF    (MIDXHONC,LE,' '),BEGIN   If local mail,                         
         SEGT  SGTEHWAH                  Add local host id                      
         END                                                                    
         ELSE  BEGIN                                                            
         SEGT  MIDXHONC                                                         
         END                                                                    
*                                                                               
         IF    (^MIDXFBSMTP,AND,(MIDXRELY,NZ)),BEGIN                            
         SEG   '@'                                                              
         SEGT  MIDXRELY            Finish up with relay host                    
         END                                                                    
*-                                                                              
*-       Make host name fully qualified                                         
*-                                                                              
         SETMSG MIDXHONC                                                        
         IF    (MIDXRELY,GT,' '),'SETMSG MIDXRELY'                              
         IF    (@R1,GT,' '),BEGIN                                               
         VCALL LRTRIM                                                           
         WHILE (R0,P),BEGIN                                                     
         IF    (@R1,EQ,'.'),EXIT                                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    (R0,NP),BEGIN                                                    
*        IF    CVFRLG,BEGIN  RLG...                                             
*        SEG   '.RLG.ORG'                                                       
*        END                                                                    
*        ELSE  BEGIN                                                            
         SEG   '.Stanford.EDU'                                                  
*        END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         IF    (R3,NZ),'SEG ">"'                                                
         END                                                                    
*-                                                                              
*-       Rest of EMS hack...if old-style EMS mail without                       
*-       MAILINFO, exit loop after adding one recipient                         
*-                                                                              
         IF    (MWAFEMS,AND,^MWAFMINF),EXIT                                     
*                                                                               
         L     R5,MIDLINK          Chain to next entry                          
         END                                                                    
*                                                                               
         IF    SGTEHFLN,BEGIN      If generated a line,                         
         SEGWR ,                   Write final line                             
         END                                                                    
*-                                                                              
*-       Another EMS hack...don't need to do 'cc' check                         
*-                                                                              
         IF    MWAFEMS,EXIT                                                     
*                                                                               
         IF    (SGTEHWTO,EQ,'cc'),EXIT  Done if this is cc                      
         MVC   SGTEHWTO,=C'cc'     Set up for 'cc' entries                      
         END                                                                    
*-                                                                              
*-       SUBJECT line...wrap long lines if Bitnet mail                          
*-                                                                              
         IF    (MWATITLE,NZ),BEGIN  We have a subject...                        
         IF    MIDXFSUNMAIL,BEGIN   If Internet mail,                           
         SEG   ' Subject:  '        Just write out subject line                 
         SEGT  MWATITLE                                                         
         SEGWR                                                                  
         END                                                                    
         ELSE  BEGIN                Else Bitnet mail,                           
         LA    R2,MWATITLE+L'MWATITLE-1   Text end address                      
         LA    R3,MWATITLE                Text start address                    
         WHILE ((R2,GT,R3),AND,(@R2,EQ,' ')),'DECR R2'  Trim                    
         SR    R2,R3               Get trimmed                                  
         INCR  R2                    text length                                
         SEG   ' Subject:'         Start header line                            
         WHILE (R2,P),BEGIN                                                     
         SEG   '  '                Leading blanks                               
         LR    R0,R2               Copy text length                             
         LR    R1,R3               Copy text address                            
         CEIL  R0,69               Truncate length                              
         SR    R2,R0               Decr remaining text length                   
         AR    R3,R0               Incr remaining text pointer                  
         SEGWR (R1),(R0)           Write line                                   
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
* If this is a forwarded mail message, add MIME information                     
*                                                                               
* Forwarding has problems because of the way EMS deals with                     
* message replies. Until we can come up with a better solution                  
* we must ignore the FORWARD keyword from EMS.                                  
*                                                                               
* Additional change for SLP. If EMS is not set, allow forward,                  
* so that Eudora mail sent to Forsythe can be forwarded                         
* correctly with attachments intact.                                            
*                                                                               
* (We came up with a better solution, so removed block.)                        
*                                                                               
         IF    (MWAFFWD),BEGIN                                                  
         SEGWR ' Mime-Version: 1.0'                                             
         SEGWR ' Content-Type: message/rfc822'                                  
         END                                                                    
*                                                                               
         SEGWR ' '                 Blank line after header                      
*                                                                               
SGHEXIT  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SGTERTN -- This routine gets the next line in the new mail                   
*    directory and returns it (unpressed).  After user's message                
*    lines are exhausted, this routine returns the final lines                  
*    for the SMTP envelope.  It then returns a zero in R1 to                    
*    indicate no more lines.                                                    
*                                                                               
*    This is a PRINTMSG co-routine.                                             
*                                                                               
SGTERTN  PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (MAILWA,R6)                                                      
         L     R5,MWAWMIDP                                                      
         WITH  (MID,R5)                                                         
*                                                                               
         LH    R4,MWAWDIR          Working dir offset                           
*                                                                               
SGTELP   CLEAR R1                                                               
         IF    (R4,LT,MWANNEW),BEGIN  If not done...                            
         LA    R15,MWANEW(R4)                                                   
         PGET  PAR,L2:@R15         Get page                                     
         LH    R3,MWAWOFFS         Working page offset                          
         LH    R15,@PAR                                                         
         IF    (R3,GE,R15),BEGIN   Next page...                                 
         LA    R4,@R4+2                                                         
         STH   R4,MWAWDIR                                                       
         CLEAR MWAWOFFS            Reset page offset                            
         B     SGTELP                                                           
         END                                                                    
*                                                                               
         LA    R15,@PAR+2(R3)      Pressed line ptr                             
         SH    R15,=H'4'           Backup for fake lineno                       
*                                                                               
         LC    R2,@PAR+2(R3)       Pressed line length                          
         LA    R3,@R2+1(R3)        Offset for the next line                     
         STH   R3,MWAWOFFS         Save new page offset                         
*                                                                               
         LA    R1,MWALINE          Put unpressed line here                      
         VCALL UNPRESS                                                          
         IF    (@R1,EQ,'*N'),SGTELP  Skip mail header                           
         IF    (@R1,EQ,'**'),'LA R1,@R1+1; DECR R0'                             
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         LR    R15,R1              Set CC for caller                            
         PEND                                                                   
         DROP  MAILWA,BR                                                        
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TRANNAME -- This routine eliminates the characters quote ("),                
*    back slash (\), and carriage return from the name strings used             
*    for BITNET and gateway mail.  The name strings are included as             
*    quoted-strings in the ARPA headers and the above characters are            
*    forbidden by the syntax (RFC-822) without a lot of mucking                 
*    around.                                                                    
*                                                                               
TRANNAME PROC                                                                   
         L     R6,CPCWA            Establish mail work area                     
         WITH  (MAILWA,R6),BEGIN                                                
         TR    MWANAME,SPECIAL                                                  
         TR    MWARNAME,SPECIAL                                                 
         END                                                                    
         PEND                                                                   
*                                                                               
SPECIAL  DC    256AL1(*-SPECIAL)   Translate table                              
         ORG   SPECIAL+C'"'                                                     
         DC    C''''               Replace " with '                             
         ORG   SPECIAL+C'\'                                                     
         DC    C' '                Replace \ with blank                         
         ORG   SPECIAL+X'0D'                                                    
         DC    C' '                Replace <CR> with blank                      
         ORG                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  BITFIX - Fix up from address for Bitnet mail                                 
*                                                                               
*  On entry:                                                                    
*    R0,R1 - address text len and addr                                          
*                                                                               
*  On exit:                                                                     
*    R0,R1 - len and addr of updated string                                     
*                                                                               
BITFIX   PROC                                                                   
****     IF    (R0,GE,22),BEGIN                                                 
****     LR    R15,R0                                                           
****     AR    R15,R1                                                           
****     SH    R15,=Y(22)                                                       
****     IF    (@R15,EQ,'@Forsythe.Stanford.EDU'),BEGIN                         
****     MVC   @R15(22),=CL22'@STANFORD.BITNET'                                 
****     SH    R0,=Y(6)                                                         
****     PRETURN (R0)                                                           
****     B     BITFIXR                                                          
****     END                                                                    
****     END                                                                    
****                                                                            
****     IF    (R0,GE,17),BEGIN                                                 
****     LR    R15,R0                                                           
****     AR    R15,R1                                                           
****     SH    R15,=Y(17)                                                       
****     IF    (@R15,EQ,'@RLG.Stanford.EDU'),BEGIN                              
****     MVC   @R15(17),=CL17'@RLG.BITNET'                                      
****     SH    R0,=Y(6)                                                         
****     PRETURN (R0)                                                           
****     END                                                                    
****     END                                                                    
*                                                                               
BITFIXR  PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
SDIRWA   RECORD BEGIN                                                           
         XSA   R2,R8                                                            
*                                                                               
SDIRFLAG FLAG                                                                   
         FLAG  SDIRFADD            - Add ROUTE info to item                     
SDIRLINE DS    XL(&LINESZ+256)                                                  
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SAVEDIR -- Routine to save pages from directory.                             
*                                                                               
*    On entry:                                                                  
*      R1 - page dir ptr                                                        
*      R15- option flags (see SDIRFLAG)                                         
*                                                                               
SAVEDIR  XENTER R2,R8,L'SDIRWA                                                  
         USING SDIRWA,WAR                                                       
         STC   R15,SDIRFLAG        Save flag byte                               
*                                                                               
         USING MAILWA,R6           (Entry assumption)                           
         LR    R5,R1                                                            
         USING BUFRDIR,R5                                                       
         CLEAR R4                  Init offset in dir                           
         WHILE (R4,LT,BUFRCUR),BEGIN  Write out old mail...                     
         LA    R15,BUFRPG(R4)                                                   
         PGET  PAR,L2:@R15                                                      
         LH    R3,@PAR             Text length                                  
         CLEAR R2                                                               
         WHILE (R2,LT,R3),BEGIN    Go thru lines in page...                     
         LA    R15,1000            Incr for linenos is 1.000                    
         A     R15,MWAWLNO                                                      
         ST    R15,MWAWLNO                                                      
*                                                                               
         LA    R1,@PAR+2(R2)                                                    
         MVC   MWALINE(4),MWAWLNO  Lineno                                       
         LC    R15,@R1             Size of pressed line                         
         LA    R15,@R15+1          Including the length byte                    
         MOVE  R15,MWALINE+4,@R1   Copy pressed line                            
         LA    R15,MWALINE                                                      
         XPUSH R2                                                               
         LA    R1,SDIRLINE                                                      
         VCALL UNPRESS             EXTWRITE wants unpress, CH 5/91              
         LR    R2,R15                                                           
         VCALL EXTWRITE            Buffer/write this line                       
         XPOP  R2                                                               
         LC    R15,@PAR+2(R2)                                                   
         LA    R2,@R15+1(R2)                                                    
*                                                                               
         IF    SDIRFADD,BEGIN      Add ROUTE info...                            
         CLEAR SDIRFADD            (Only the first time)                        
         L     R15,MWAWMIDP        Working current MID entry                    
         FAIL  (R15,Z),MAILERR                                                  
         XPUSH R0,R3                                                            
         L     R3,MWAWMIDP                                                      
         WITH  (MID,R3)                                                         
         TCLEAR                                                                 
*                                                                               
***OLD   IF    ^CVFRLG,BEGIN       RLG can't hack new headers...                
*-                                                                              
*-       MESSAGE-ID line.                                                       
*-                                                                              
         TSEG  '*::Message-ID: <'                                               
         TSEG  MWAMSGID,,T                                                      
         TSEG  '>'                                                              
         ACALL ADDLINE             Buffer/write header line                     
*-                                                                              
*-       Add the following lines if the user hasn't provided                    
*-         his own headers.                                                     
*-                                                                              
         IF    ^MWAFMHDR,BEGIN     No mail headers in file...                   
*-                                                                              
*-       DATE line.                                                             
*-                                                                              
         TSEG  '*::Date: '                                                      
         XPUSH ,,32,PTR=R15                                                     
         VCALL LOCALTOD            Current date                                 
         VCALL FMTXDATE                                                         
         MVC   12(15,R1),14(R1)    Squeeze out century                          
         S     R0,=F'2'                                                         
         TSEG  (R1),(R0)           Formatted date                               
         XPOP  ,,32                                                             
*                                                                               
         ACALL ADDLINE             Buffer/write header line                     
*-                                                                              
*-       TO line.                                                               
*-                                                                              
         TSEG  '*::To:   '                                                      
*                                                                               
         CLEAR R2                  Flag: no name                                
         IF    (MIDXNAME,NZ),BEGIN                                              
         TSEG  '"'                                                              
         TSEG  MIDXNAME,,T         Recipient's name                             
         TSEG  '" <'                                                            
         LA    R2,1                Flag: name                                   
         END                                                                    
*                                                                               
         TSEG  MIDXUSER,,T         Userid                                       
         IF    (MIDXHOST,NZ),BEGIN                                              
         SETMSG '@'                                                             
         IF    (^MIDXFBSMTP,AND,(MIDXRELY,NZ)),'SETMSG "%"'                     
         TSEG  (R1),(R0)           Seg separator                                
         TSEG  MIDXHONC,,T                                                      
         IF    (^MIDXFBSMTP,AND,(MIDXRELY,NZ)),BEGIN                            
         TSEG  '@'                                                              
         TSEG  MIDXRELY,,T         Finish up with relay host                    
         END                                                                    
         END                                                                    
         IF    (R2,NZ),'TSEG ">"'                                               
*                                                                               
         ACALL ADDLINE             Buffer/write header line                     
*-                                                                              
*-       FROM line.                                                             
*-                                                                              
         TSEG  '*::From: '                                                      
*                                                                               
         CLEAR R2                  Flag: no name                                
         IF    (MWANAME,NZ),BEGIN                                               
         TSEG  '"'                                                              
         TSEG  MWANAME,,T          Sender's name                                
         TSEG  '" <'                                                            
         LA    R2,1                Flag: name                                   
         END                                                                    
*                                                                               
         TSEG  MWAFROM,,T          Userid                                       
         IF    (R2,NZ),'TSEG ">"'                                               
         ACALL ADDLINE             Buffer/write header line                     
*-                                                                              
*-       X-FROM-NAME line.                                                      
*-                                                                              
         IF    (MWANAME,NZ),BEGIN  We have a name...                            
         TSEG  '*::X-From-Name: '                                               
         TSEG  MWANAME,,T                                                       
         ACALL ADDLINE             Buffer/write header line                     
         END                                                                    
*-                                                                              
*-       X-FROM-USERID line.                                                    
*-                                                                              
         TSEG  '*::X-From-Userid: '                                             
         TSEG  MWAFROM,,T          Userid                                       
         ACALL ADDLINE             Buffer/write header line                     
*-                                                                              
*-       X-FROM-ACCOUNT line.                                                   
*-                                                                              
         IF    (CPUID,EQ,MWAFROM),BEGIN  It's our userid...                     
         TSEG  '*::X-From-Account: '                                            
         TSEG  CPSGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPSUSER                                                          
*                                                                               
         ACALL ADDLINE             Buffer/write header line                     
         END                                                                    
*-                                                                              
*-       SUBJECT line.                                                          
*-                                                                              
         IF    (MWATITLE,NZ),BEGIN  We have a subject...                        
         TSEG  '*::Subject: '                                                   
         TSEG  MWATITLE                                                         
*                                                                               
         ACALL ADDLINE             Buffer/write header line                     
         END                                                                    
         END                                                                    
***OLD   END                                                                    
*                                                                               
         XPOP  R0,R3                                                            
         END                                                                    
         END                                                                    
*                                                                               
         LA    R4,@R4+2                                                         
         END                                                                    
         PFREE PAR                                                              
         XEXIT                                                                  
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ADDLINE -- Local routine to add the TSEG buffer to the MAIL                  
*    data set.                                                                  
*                                                                               
ADDLINE  LENTER                                                                 
         L     R15,CPSEGLENF                                                    
         LR    R0,R15                                                           
         L     R14,CPSEGLOC                                                     
         MOVE  R15,MWALINE,@R14                                                 
         XPUSH R0                                                               
         TCLEAR                                                                 
         XPOP  R0                                                               
         SETMSG MWALINE,(R0)                                                    
         LA    R15,1000                                                         
         A     R15,MWAWLNO                                                      
         ST    R15,MWAWLNO                                                      
         LR    R2,R15              R1,R0-text loc,len, R2-line no               
         VCALL EXTWRITE            Save special line                            
         LEXIT                                                                  
*                                                                               
         DROP  BUFRDIR,MAILWA,SDIRWA,BR                                         
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAILSIZE -- Routine called by DOPEN to determine the size of the             
*    data.                                                                      
*                                                                               
*    On entry:                                                                  
*      R15 - blocksize for save                                                 
*                                                                               
MAILSIZE XENTER R2,R8,,LOCAL                                                    
         L     R6,CPCWA            Mailwa ptr                                   
         USING MAILWA,R6                                                        
         ST    R15,CPNWCT          Save blocksize                               
         MVC   DSAVCNT,MWALINES+2  No. of lines                                 
         L     R3,MWACOUNT                                                      
         AH    R3,=H'1024'         Slop, for "*:" lines                         
         STH   R3,DSAVSPA          Pressed byte count                           
         CLEAR R2                                                               
         SH    R15,=AL2(2+&PRESSZ-1)  Smallest buffer                           
         AR    R3,R15                                                           
         DR    R2,R15              No. of blocks needed (worst case)            
         STH   R3,DSAVBLK          Save block count                             
         LTR   BR,BR               Set nz cc for caller                         
         XEXIT                                                                  
         DROP  MAILWA,BR                                                        
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAILRTN -- Routine to save the active file range in a temporary              
*    text page (MWAPG).  It is entered from the range processor                 
*    with the pressed line loc in R15.                                          
*                                                                               
*  PREST option removed 5/91 by CH.  We press line on entry for                 
*  backwards compatibility.                                                     
*                                                                               
*                                                                               
MAILRTN  PROC                                                                   
*                                                                               
         COMMENT                   BE BACKWARDS COMPAT, PRESS LINE              
         L     R15,CPLCNO                                                       
         VCALL PRESS                                                            
         LR    R15,R1                                                           
*                                                                               
         L     R6,CPCWA            Mailwa ptr                                   
         WITH  (MAILWA,R6)                                                      
         LR    R5,R15                                                           
*-                                                                              
*-       The following checks for lines beginning with "*" and                  
*-         changes it to "**".                                                  
*-                                                                              
         IF    (@R5+4,GE,X'02'),BEGIN  Non-null line...                         
         LC    R15,@R5+5           Get first control byte                       
         SRL   R15,4               Blank count                                  
         IF    (R15,NZ),EXIT       Leading blanks, scram                        
         IF    (@R5+6,NE,'*'),EXIT  Not "*", scram                              
         IF    ((@R5+7,EQ,'::'),AND,MWAFMHDR),EXIT  Mail hdr, scram             
         IF    ((@R5+7,EQ,':'),AND,(@R5+8,NE,':'),AND,MWAFUHDR),EXIT            
         LR    R15,R5                                                           
         MVI   MWALINE,C'*'                                                     
         LA    R1,MWALINE+1                                                     
         VCALL UNPRESS                                                          
         LA    R1,MWALINE                                                       
         A     R0,CVONE                                                         
         CEIL  R0,&LINESZ                                                       
         VCALL PRESS                                                            
         LR    R5,R1                                                            
         END                                                                    
         INCR  R15,MWANLINS        Kick line count                              
         L     R15,MWANCNT         Old byte count                               
         LC    R1,@R5+4                                                         
         LA    R15,@R1+5(R15)                                                   
         ST    R15,MWANCNT         Total byte count                             
*                                                                               
         LR    R15,R5              Pressed line ptr                             
         LA    R1,MWAMNEW          New dir ptr                                  
         ACALL BUFRLINE                                                         
         IF    POS,BEGIN           Won't fit...                                 
         TSEG  'Mail not sent.  '                                               
         TSEG  'Too much mail, starting at line '                               
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         ERROR (R1),(R0),MUCHMAIL                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAILRTNX -- Routine to save the old mail data set in temporary               
*    text pages (MWADIR).  It is entered from the range processor               
*    with the pressed line loc in R15.                                          
*                                                                               
*  PREST option removed 5/91 by CH.  We press line on entry for                 
*  backwards compatibility.                                                     
*                                                                               
*                                                                               
MAILRTNX PROC                                                                   
*                                                                               
         COMMENT                   BE BACKWARDS COMPAT, PRESS LINE              
         L     R15,CPLCNO                                                       
         VCALL PRESS                                                            
         LR    R15,R1                                                           
*                                                                               
         L     R6,CPCWA            Mailwa ptr                                   
         USING MAILWA,R6                                                        
         LR    R5,R15                                                           
         INCR  R15,MWALINES        Kick line count                              
         L     R15,MWACOUNT                                                     
         LC    R1,@R5+4                                                         
         LA    R15,@R1+5(R15)                                                   
         ST    R15,MWACOUNT        Total byte count                             
*                                                                               
         LR    R15,R5              Pressed line ptr                             
         LA    R1,MWAMDIR          Mail dir                                     
         ACALL BUFRLINE                                                         
         IF    POS,BEGIN           Won't fit...                                 
         CLEAR R2                                                               
         IF    CPFQUIET,'LA R2,1; CLEAR CPFQUIET'                               
         TSEG  'Mail not sent to '                                              
         L     R3,MWAWMIDP                                                      
         WITH  (MID,R3),BEGIN                                                   
         TSEG  MIDXUSER,,T                                                      
         END                                                                    
         TSEG  '.  Mailbox is full.',,WR                                        
         IF    (R2,NZ),'SET CPFQUIET'                                           
         MVC   CPERRID,=C'MAILFULL'                                             
         SET   CPFERROR                                                         
         LM    R0,R8,MWAREGS                                                    
         LR    R13,R0                                                           
         BR    R1                  (Returns to SENDDONE)                        
         END                                                                    
         PEND  ,                                                                
         DROP  MAILWA                                                           
*                                                                               
         QLTORG                                                                 
         TITLE 'SHOW MAIL Command'                                              
*box                                                                            
*                                                                               
*  SHOWMAIL -- SHOW MAIL command.                                               
*                                                                               
SHOWMAIL XPROC KWR                                                              
         CLEAR KWR                                                              
         MVC   CPCACCT,CPSACCT     Default is logged on user's acct             
         SET   CPFVER              Verify assumed                               
*-                                                                              
*-       Account for Wylbur mail processing separately.                         
*-                                                                              
         IF    (CPWCHAR,EQ,'GENERAL'),BEGIN                                     
         MVC   CPLWCHAR(8),=C'WYLMAIL_'  Prefix                                 
         MVC   CPLWCHAR+8(8),CPWCHAR  Set suffix                                
         VCALL BILLCHK             Switch charging components                   
         END                                                                    
*-                                                                              
*-       Scan for SHOW MAIL options.                                            
*-                                                                              
         CLEAR R3,R4               Subject ptr, option flags                    
         SCAN  SHMPRT                                                           
         SCANCHK                                                                
         LR    R5,R3               R5 options                                   
         LR    R6,R4               R6 subject ptr                               
*                                                                               
         IF    CPFDUMP,'CLEAR CPFVER'                                           
*                                                                               
         IF    CPFVER,BEGIN        Verify...                                    
         ST    R5,CPDOUB           Flag byte                                    
         TM    CPDOUB+3,L'SHMFDEL                                               
         IF    Z,EXIT                                                           
         N     R5,=A(-1-L'SHMFDEL)                                              
         TSEG  'DELETE ignored, NOVERIFY must also be specified.',,CR           
         BNZ   CVABORT                                                          
         END                                                                    
*                                                                               
         INCR  R15,CVMSHML         Count the SHOW MAIL                          
         IF    CPFDUMP,'INCR R15,CVMSHMLD'  Count DUMP MAIL                     
*-                                                                              
*-       Temporary limitation because we use halfword pagenos.                  
*-                                                                              
         IF    CVFMEMPAGE,BEGIN    Trouble...                                   
         ABORT 'SHOW MAIL command not available in this Wylbur.'                
         END                                                                    
*-                                                                              
*-       Niz's famous SHOW MAIL experiment, started 6/27/86.                    
*-                                                                              
         TPUSH                                                                  
         XPUSH ,,L'SEGCB,PTR=R4                                                 
         SEGINIT CPCMD,,(R4)                                                    
         XPUSH ,,L'CPCMD,PTR=R2                                                 
         ST    R5,CPWK7            Option flags                                 
*                                                                               
         LA    R3,L'CPCMD                                                       
         SETMSG 'SES.MAIL_SHOWMAIL'  Var containing SHOW MAIL cmd               
         VCALL EVALSTR             Get contents of var                          
         IF    NP,BEGIN            Variable is defined...                       
         IF    (R3,NP),EXIT        Null string means don't do it                
         SEG   (R2),(R3)           Command text                                 
         SEG   '/'                 Parms                                        
         IF    (CPSACCT,NE,CPCACCT),BEGIN  Different account...                 
         SEG   CVBLANKS,1                                                       
         SEG   CPCGRP                                                           
         SEG   '.'                                                              
         SEG   CPCUSER                                                          
         END                                                                    
         TM    CPWK7+3,L'SHMFALL                                                
         IF    NZ,'SEG " ALL"'                                                  
         TM    CPWK7+3,L'SHMFKEPT                                               
         IF    NZ,'SEG " KEPT"'                                                 
         TM    CPWK7+3,L'SHMFCHR                                                
         IF    NZ,'SEG " CHRON"'                                                
*                                                                               
         IF    CPFDUMP,EXIT        Can't handle this                            
         IF    CPFINT,EXIT                                                      
         TM    CPWK7+3,L'SHMFSUM                                                
         IF    NZ,EXIT             Can't handle SHOW MAIL SUMMARY               
         TM    CPWK7+3,L'SHMFSTAT                                               
         IF    NZ,EXIT             Can't handle SHOW MAIL STATUS                
*                                                                               
         WITH  (SEGCB,R4),'MVC CPCMDLEN,SEGCBLENF+2'  Set cmd len               
         CLEAR CPFTYPED            Don't log the command                        
         SETMSG CPCMD,LH:CPCMDLEN   Command text                                
         VCALL EDTCOM              Go execute command                           
         DC    H'0'                (No return)                                  
         END                                                                    
*                                                                               
         XPOP  ,,L'CPCMD                                                        
         XPOP  ,,L'SEGCB                                                        
         TPOP  JUNK                                                             
*-                                                                              
*-       Regular SHOW MAIL.                                                     
*-                                                                              
         MVC   KWRUSER(5),CPCACCT                                               
         LH    R0,=H'-2'           Even priv'd must supply password             
         LA    R1,KWR                                                           
         VCALL GETKWR              Read kwr, prompt for kw, etc.                
*                                                                               
         LA    R15,KWR             Kwr ptr                                      
         LR    R0,R5               Option flags                                 
         LR    R1,R6               Subject ptr (or zero)                        
         ACALL MAILDISP                                                         
         BM    CVNOACTN            Mail aborted by attn                         
         CLEAR KWR                                                              
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
SHMPRT   SCKW  ALL,SHMLALL                                                      
         SCKW  KEPT,SHMLKEPT,A                                                  
         SCKW  SUBJECT,SHMLSUB,(A,P)                                            
         SCKW  SUMMARY,SHMLSUM,A                                                
         SCKW  INTERNAL,SHMLINT,A                                               
         SCKW  CHRONOLOGICAL,SHMLCHR,A                                          
         SCKW  NOCHRONOLOGICAL,SHMLNCHR,A                                       
         SCKW  DELETE,SHMLDEL,A                                                 
         SCKW  NODELETE,SHMLNDEL,A                                              
         SCKW  MHEADER,SHMLMHDR,A                                               
         SCKW  MHDR,SHMLMHDR                                                    
         SCKW  UHEADER,SHMLUHDR,A                                               
         SCKW  UHDR,SHMLUHDR                                                    
         SCKW  STATUS,SHMLSTAT,A                                                
         SCKW  EMSFLAG,SHMLEMS                                                  
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(SCNNOOP),V(MATUID)  ALLOW USERID TOO                          
         SCKW  ,V(VERPSH),PUSH                                                  
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE ,                                                                
SHMLSUB  PROC                                                                   
         SCUPTOK R1                                                             
         MVC   CPWK8(8),@R1                                                     
         LA    R4,CPWK8                                                         
         O     R3,=A(L'SHMFKEPT)                                                
         PRETURN (R3,R4)                                                        
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHMLALL  PROC                                                                   
         O     R3,=A(L'SHMFALL)                                                 
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLKEPT PROC                                                                   
         O     R3,=A(L'SHMFKEPT)                                                
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLSUM  PROC                                                                   
         O     R3,=A(L'SHMFSUM)                                                 
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLCHR  PROC                                                                   
         O     R3,=A(L'SHMFCHR)                                                 
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLNCHR PROC                                                                   
         N     R3,=A(-1-L'SHMFCHR)                                              
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLDEL  PROC                                                                   
         O     R3,=A(L'SHMFDEL)                                                 
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLNDEL PROC                                                                   
         N     R3,=A(-1-L'SHMFDEL)                                              
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLMHDR PROC                                                                   
         O     R3,=A(L'SHMFMHDR)                                                
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLUHDR PROC                                                                   
         O     R3,=A(L'SHMFUHDR)                                                
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLSTAT PROC                                                                   
         O     R3,=A(L'SHMFSTAT)                                                
         PRETURN (R3)                                                           
         PEND                                                                   
*                                                                               
SHMLEMS  PROC  ,              (Nothing for now)                                 
         PEND                                                                   
*                                                                               
SHMLINT  PROC  ,                                                                
         SET   CPFINT                                                           
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
SHMWSEC# EQU   5,,C'N'             Number of seconds to wait                    
SHMDMAX# EQU   SENWMAX#,,C'N'      Maximum no. of MAILDEFQ files                
         SPACE 2                                                                
SHMWA    RECORD BEGIN                                                           
SHMFLAG  FLAG                                                                   
         FLAG  SHMFKEEP            - Don't scratch mail ds                      
         FLAG  SHMFCHNG            - At least one change made                   
         FLAG  SHMFSKIP            - Skip entire item                           
         FLAG  SHMFMLQ             - Mail queue files exist                     
         FLAG  SHMFWAIT            - We've tried waiting a few secs             
*                                                                               
SHMFOPT  FLAG                                                                   
         FLAG  SHMFALL             - Display all mail                           
         FLAG  SHMFKEPT            - Display "kept" mail                        
         FLAG  SHMFCHR             - Display in chronological order             
         FLAG  SHMFDEL             - Delete displayed mail                      
         FLAG  SHMFSUM             - Summary                                    
         FLAG  SHMFSTAT            - Display mailbox status                     
         FLAG  SHMFMHDR            - Display mail headers                       
         FLAG  SHMFUHDR            - Display user headers                       
*                                                                               
SHMQCNT  DS    H                   Number of MAIL data sets found               
*                                                                               
SHMSUBJ  DS    CL8                 Subject                                      
*-                                                                              
*-       SHMBOXES is an array of the mailboxes we are using.  If the            
*-     byte is non-zero then we are using that mailbox.  BOXES[0] is            
*-       the MAIL data set, BOXES[1] is MAIL.DEFQ0001, etc.                     
*-                                                                              
SHMBOXES DS    XL(1+SHMDMAX#)      One byte per possible mailbox                
*                                                                               
SHMNCNT  DS    H                   No. of new items                             
SHMKCNT  DS    H                   No. of kept items                            
*                                                                               
SHMLINES DS    F                   Total no. of mail lines                      
SHMCOUNT DS    F                   Total no. of mail bytes                      
*                                                                               
SHMKWRP  DS    A                   KWR pointer                                  
*                                                                               
SHMLNO   DS    F                   Work line-number                             
*                                                                               
SHMMAST  DS    H                   Master directory page                        
*                                                                               
SHMDIR#  EQU   MWADIR#+MWANEW#     Max no of pages                              
SHMMDIR  DS    H           [Order] Max offset allowed                           
SHMNDIR  DS    H           [Order] Current offset from SHMDIR                   
SHMDIR   DS    (SHMDIR#)H  [Order] Mail directory                               
*                                                                               
SHMIFLAG FLAG                                                                   
         FLAG  SHMFATTN            - Skip remainder of item (attn)              
         FLAG  SHMFRETY            - Item being retyped                         
*                                                                               
SHMISTAT DS    C                   Status char                                  
SHMISUBJ DS    CL8                 Subject                                      
SHMPTRS  DS    3F                  Current dir ptrs                             
SHMIPTRS DS    3F                  Item start dir ptrs                          
SHMCPTRS DS    3F                  Init cont. dir ptrs                          
SHMPPTRS DS    3F                  Print dir ptrs                               
SHMLCNT  DS    H                   Item line count                              
SHMBCNT  DS    H                   Item byte count                              
SHMSCNT  DS    H                   Item skip count                              
SHMITEM  EQU   SHMIFLAG,*-SHMIFLAG,C'X'                                         
*                                                                               
SHMLINE  DS    CL(&PRESSZ)                                                      
         DS    CL(100)             Slop space (in case bad UNPRESS)             
SHMLINE2 DS    CL(&LINESZ)                                                      
         DS    CL(256)             Slop                                         
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAILDISP -- Routine to display mail.  This routine is either                 
*    called at logon (if auto mail is in effect) or by the                      
*    SHOW MAIL command.                                                         
*                                                                               
*      On entry:                                                                
*        R15- KWR ptr                                                           
*        R0 - flag byte (see "SHMFOPT")                                         
*        R1 - address of 8 char subject (or zeros)                              
*                                                                               
*      On exit, R15 (and CC):                                                   
*        -4 - attn hit, mail not displayed                                      
*         0 - ok                                                                
*         4 - some sort of error occured                                        
*                                                                               
         SPACE 2                                                                
MAILDISP XENTER                                                                 
         XPUSH ,,L'SHMWA,PTR=R6                                                 
         ST    R6,CPCWA            Save shmwa ptr                               
         USING SHMWA,R6                                                         
         CLEAR SHMWA               Init work area                               
         MVC   SHMMDIR,=AL2(SHMDIR#*2)                                          
         STC   R0,SHMFOPT          Save option flags                            
         IF    (R1,NZ),'MVC SHMSUBJ,@R1'  Save subject                          
*                                                                               
         LR    R5,R15              Kwr ptr                                      
         WITH  (KWR,R5),BEGIN                                                   
         ST    R5,SHMKWRP          Save ptr to kwr                              
*                                                                               
         IF    SHMFSTAT,BEGIN      Give mailbox status (only)...                
         IF    (CPCACCT,NE,CPSACCT),BEGIN                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         TSEG  ' - '                                                            
         END                                                                    
         LA    R1,KWR                                                           
         VCALL MBOXDISP            (Give mailbox status, in ACCT)               
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         LA    R15,KWR                                                          
         ACALL MAILCHK             Give user mailbox info                       
*                                                                               
         IF    (KWRSFL.KWRSFMAI+KWRSFKEP,Z),BEGIN                               
         SETMSG 'No mail.'                                                      
         MVC   CPERRID,=CL8'NOMAIL'                                             
         B     SHMLKMSG                                                         
         END                                                                    
         IF    ^SHMFALL,BEGIN                                                   
         IF    ^SHMFKEPT,BEGIN                                                  
         IF    KWRSFL.KWRSFMAI,SHMLMACX                                         
         SETMSG 'No new mail.'                                                  
         MVC   CPERRID,=CL8'NONEWML'                                            
         B     SHMLKMSG                                                         
         END                                                                    
         IF    KWRSFL.KWRSFKEP,SHMLMACX                                         
         SETMSG 'No kept mail.'                                                 
         MVC   CPERRID,=CL8'NOKEPTML'                                           
SHMLKMSG IF    ^CPFINT,'TSEG (R1),(R0),CR'                                      
         CLEAR R15                                                              
         B     SHMLEXIT                                                         
         END                                                                    
*-                                                                              
*-       Check to see if we can display mail on this machine.                   
*-                                                                              
SHMLMACX LC    R15,CVMACH                                                       
         EX    R15,'TM KWRMPRIM,0'  Only SHOW MAIL on primary...                
         IF    Z,BEGIN             Not primary machine...                       
         TSEG  'Mail for '                                                      
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         TSEG  ' cannot be displayed on '                                       
         TSEG  CVMACHID,,T                                                      
         TSEG  ', try '                                                         
         XPUSH ,,64,PTR=R1                                                      
         SETMSG (R1),(R0)           Work area                                   
         LC    R15,KWRMPRIM                                                     
         VCALL GETMACH                                                          
         TSEG  (R1),(R0)                                                        
         XPOP  ,,64                                                             
         TSEG  ' instead.'                                                      
         MVC   CPERRID,=CL8'NOMLMACH'                                           
         CLEAR R15                                                              
         B     SHMLEXIT                                                         
         END                                                                    
*-                                                                              
*-       MAILDEFQ files exist.                                                  
*-                                                                              
         IF    KWRSFL.KWRSFMLQ,BEGIN  MailQ files exist...                      
         LA    R3,10               Max KWR retry count                          
         WHILE ((R3,POS),AND,KWRIFL.KWRIFVAL),BEGIN                             
         CLEAR KWRSFL.KWRSFMLQ     Must reset before checking files             
         KWRITE KWR                                                             
         IF    KWRHFL.KWRHFUOK,EXIT                                             
         DECR  R3                                                               
         END                                                                    
*                                                                               
         SET   SHMFMLQ             Mail defq files may exist                    
         SET   SHMFCHNG            Mailbox must be rewritten                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Main SHOW MAIL item processing.                                        
*-                                                                              
         PFLIP ,                                                                
         VCALL PGETNEW             Get new page in PAR                          
         IF    NZ,'VCALL NOMORPG'                                               
         VCALL PTEMP               Make it a temporary page                     
         PFLIP ,                   New into PBR                                 
         STH   R0,SHMMAST          Save pageno                                  
         CLEAR (@PBR,2)                                                         
         PMARK PBR                                                              
*-                                                                              
*-   Read in all the MAIL items.  This means reading the contents of            
*-         the MAIL and MAILDEFQ files.                                         
*-                                                                              
SHMLRDLP CLEAR (CPDSNWA,DSAVSIZ)   Init dsnwa                                   
         MVC   CPSEQFLD(4),=H'-1,8'  Init seqflds                               
         MVI   CPLRCL,X'80'        Init file type                               
         SET   DSNDTYPE.DSNDDISK   It's a disk                                  
         SET   DSNWAF1.DSNFSTD+DSNFMYDS                                         
         CLEAR DSNON               No special volume                            
         CLEAR CPFNOCAT            Use the catalog                              
*                                                                               
         LA    R2,15               L'WYL.gg.uuu.MAIL                            
         MVC   DSNWADSN,=CL44'WYL.gg.uuu.MAIL'                                  
         MVC   DSNWADSN+4(2),CPCGRP  Group                                      
         MVC   DSNWADSN+7(3),CPCUSER  User                                      
*                                                                               
         LTH   R15,SHMQCNT                                                      
         IF    (R15,NZ),BEGIN      Build ".DEFQnnnn" suffix...                  
         MVC   DSNWADSN+15(9),=C'.DEFQnnnn'                                     
         A     R15,=A(100000)      High order zeros                             
         BTD   DSNWADSN+20,4,(R15)                                              
         LA    R2,24               L'WYL.gg.uuu.MAIL.DEFQnnnn                   
         END                                                                    
         STH   R2,DSNWANL          Save data set name length                    
*                                                                               
         SET   DSNFSML             Show mail (checked in DISK)                  
*-                                                                              
*-    ENQ on MAIL data set name.  If the data set is busy, then wait            
*-         a few seconds and then try it again.                                 
*-                                                                              
SHMLENQ  LA    R15,1               Temporary enq                                
         VCALL DENQ                Enq on data set                              
         IF    NZ,BEGIN            Can't enqueue...                             
         IF    SHMFWAIT,BEGIN      We've already tried waiting...               
         TSEG  'Unable to SHOW MAIL.  '                                         
         TSEG  'Your mailbox is currently in use.',,CR                          
         MVC   CPERRID,=CL8'MLINUSE'                                            
         SET   CPFERROR                                                         
         LH    R15,=H'-4'                                                       
         B     SHMLFREE                                                         
         END                                                                    
*                                                                               
         SET   SHMFWAIT            Only do this once                            
         LA    R15,SHMWSEC#*100    Number of seconds to wait                    
         VCALL WAITER              Wait a few seconds                           
         BZ    SHMLENQ             Try it again                                 
*                                                                               
         LH    R15,=H'-4'                                                       
         B     SHMLFREE            Break, forget it                             
         END                                                                    
*                                                                               
         CLEAR SHMFWAIT            Reset wait flag                              
*-                                                                              
*-       Open the MAIL file and read it's contents.                             
*-                                                                              
         VCALL EXTOPEN             Open data set, etc.                          
         LTR   R2,R15              Return code                                  
         IF    NZ,BEGIN            Error...                                     
         IF    (R2,EQ,4),BEGIN     File not found...                            
         IF    CPRFEXT,'VCALL EXTCLOSE'  Tidy up                                
         IF    (SHMQCNT,NZ),'VCALL DDEQ'  Dequeue on DEFQ DSNAME                
         B     SHMLNBOX            Go check next                                
         END                                                                    
*                                                                               
         CLEAR CPFDUMP                                                          
         IF   (R2,EQ,RTNVOLNA),'TSEG "Mailbox volume not available. "'          
         TSEG  'Unable to display mail.',,CR                                    
         SET   CPFABORT                                                         
         LH    R15,=H'-4'                                                       
         B     SHMLFREE                                                         
         END                                                                    
*                                                                               
         LH    R15,SHMQCNT                                                      
         LA    R1,SHMBOXES(R15)    MAIL data set list                           
         MVI   @R1,X'FF'           We are using this MAIL data set              
*-                                                                              
*-       Process all the lines in this mail file.                               
*-                                                                              
         CLEAR CPFRDRNG                                                         
         SET   CPFEXPRG            Set explicit range                           
         CLEAR CPFSLN              Range (all)                                  
         MVC   CPLSLN,CVMAXLNO                                                  
         L     R15,SHMLWRK                                                      
         VCALL DESPOT              (Co-routine is SHMLRTN)                      
         IF    NZ,'TCLEAR; LH R15,=H"-4"; B SHMLFREE'                           
         IF    CPRFEXT,'VCALL EXTCLOSE'  (Enq will remain)                      
         LH    R15,=AL2(MWADIR#*2)  Offset for full mailbox                     
         IF    ((R15,LT,SHMNDIR),AND,^CPFINT),BEGIN                             
         TSEG  'WARNING -- This mailbox is FULL.',,CR                           
         TSEG  'No additional mail can be received until "NEW" or "KEPT*        
               " mail is deleted by you.',,WR                                   
         END                                                                    
*-                                                                              
*-       Go back and read more MAILDEFQ files if necessary.                     
*-                                                                              
SHMLNBOX IF    ^SHMFMLQ,SHMLSHOW   Don't look for MailQ files                   
         IF    (SHMQCNT,GE,SHMDMAX#),SHMLSHOW  No more MailQ files              
         INCR  R15,SHMQCNT         Set up for next MailQ file                   
         B     SHMLRDLP            Go look for more mail files                  
         EJECT                                                                  
***                                                                             
***  We have now read in all the items from the MAIL/MAILDEFQ files.            
***  Now process them item by item.                                             
***                                                                             
SHMLSHOW PGET  PBR,L2:SHMMAST                                                   
*                                                                               
         IF    SHMFCHR,BEGIN       Chron style...                               
         LH    R5,@PBR                                                          
         CLEAR R4                                                               
         WHILE (R4,LT,R5),BEGIN    Go thru items...                             
         LH    R0,@R4+2(PBR)       Offset in dir                                
         LH    R1,@R4+4(PBR)       Offset in page                               
         ACALL SMITEM                                                           
         BNZ   SHMLFIN                                                          
         LA    R4,@R4+4            Next item                                    
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               Latest item first...                         
         LH    R5,@PBR                                                          
         LR    R4,R5                                                            
         WHILE (R4,POS),BEGIN      Go thru items...                             
         SH    R4,=H'4'            Back up                                      
         LH    R0,@R4+2(PBR)       Offset in dir                                
         LH    R1,@R4+4(PBR)       Offset in page                               
         ACALL SMITEM                                                           
         BNZ   SHMLFIN                                                          
         END                                                                    
         END                                                                    
*                                                                               
SHMLFIN  IF    ^SHMFCHNG,BEGIN     Nothing changed...                           
         IF    ^CPFFIRST,BEGIN     Nothing...                                   
         IF    CPFINT,SHMLFREE                                                  
         TSEG  'No '                                                            
         IF    ^SHMFALL,BEGIN                                                   
         IF    SHMFKEPT,'TSEG "Kept "'                                          
         IF    (SHMSUBJ,NZ),BEGIN                                               
         TSEG  '"'                                                              
         SETMSG SHMSUBJ                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  '" '                                                             
         END                                                                    
         END                                                                    
         TSEG  'mail exists.',,CR                                               
         END                                                                    
         CLEAR R15                                                              
         B     SHMLFREE                                                         
         END                                                                    
*-                                                                              
*-       Make sure we update the primary MAIL data set.                         
*-                                                                              
         CLEAR CPFNOCAT            Use the catalog                              
         CLEAR DSNON               (We don't know the volume)                   
         MVC   DSNWADSN,=CL44'WYL.gg.uuu.MAIL'                                  
         MVC   DSNWADSN+4(2),CPCGRP  Group                                      
         MVC   DSNWADSN+7(3),CPCUSER  User                                      
         MVC   DSNWANL,=H'15'      L'WYL.gg.uuu.MAIL                            
*                                                                               
*  This code used to scratch the MAIL data set if all the mail                  
*  has been read.  In order to reduce the number of times mailboxes             
*  are scratched and re-allocated, the code is never executed and               
*  WYLBUR just keeps a null (allocated) mail data set.                          
*                                                                               
*PERF    IF    ((SHMNCNT,Z),AND,(SHMKCNT,Z)),BEGIN  Scratch...                  
*PERF    IF    SHMFKEEP,EXIT       Don't scratch it after all                   
*PERF    DSCRATCH                                                               
*PERF    IF    ('LTR R2,R15',NZ),BEGIN  Scratch didn't work...                  
*PERF    TSEG  'Mail not updated.  Scratch failed.',,CR                         
*PERF    SET   CPFABORT                                                         
*PERF    LH    R15,=H'-4'                                                       
*PERF    B     SHMLFREE                                                         
*PERF    END                                                                    
*PERF    CLEAR R15                 All mail has been read                       
*PERF    B     SHMLFREE                                                         
*PERF    END                                                                    
*                                                                               
*  Must re-write mail data set because some items have been read.               
*                                                                               
         SET   CPLFLG2.CPFSCRTC    Ok to replace                                
         SET   DSNWAF3.DSNFCHT     Ok to change type to edit                    
         CLEAR CPLRCL              Edit format                                  
         DOPEN 0,=A(SHMLSIZE),SAVE  Open for output                             
         IF    NZ,BEGIN                                                         
         LR    R2,R15              Save return code                             
         TSEG  'Mail not updated.  '                                            
         CLEAR R0                                                               
         IF    (R2,EQ,RTNNOSPC),'SETMSG "No space after scratch."'              
         IF    (R2,EQ,RTNNOACS),'SETMSG "Access not permitted."'                
         IF   (R2,EQ,RTNVOLNA),'SETMSG "Mailbox volume not available."'         
         TSEG  (R1),(R0),CR                                                     
         LH    R15,=H'-4'                                                       
         B     SHMLFREE                                                         
         END                                                                    
*                                                                               
         LA    R2,2                Assume edit save                             
         L     R3,CPLRCL           Get lrecl                                    
         LTR   R3,R3               Is it edit or fixed                          
         BZ    SHMLED              Br if edit                                   
         DECR  R3                  Set lrecl for ex moves                       
         ST    R3,CPNREC           Put in nrec                                  
         SR    R2,R2               Set displacement for fixed                   
SHMLED   ST    R1,CPDRED           Save external I/O buff addr                  
         ST    R0,CPRCSZ           Save output record size                      
         AR    R1,R2               Point to addr for first text byte            
         ST    R1,CPNXAD           Save                                         
         ST    R2,CPNWCT           Into current count                           
*                                                                               
         CLEAR SHMLNO              Init lineno                                  
         CLEAR SHMFSKIP                                                         
*                                                                               
         CLEAR R4                  Init offset in dir                           
         WHILE (R4,LT,SHMNDIR),BEGIN                                            
         LA    R15,SHMDIR(R4)                                                   
         PGET  PAR,L2:@R15                                                      
         LH    R3,@PAR                                                          
         CLEAR R2                                                               
         WHILE (R2,LT,R3),BEGIN    Go thru lines in page...                     
         LA    R1,@PAR+2(R2)                                                    
         IF    (@R1,NE,X'00'),BEGIN  Non-null...                                
         IF    (@R1+1.X'F0',NZ),EXIT  leading blanks, scram                     
         IC    R15,@R1+1                                                        
         N     R15,=A(X'F')        Char count                                   
         IF    (R15,LT,2),EXIT     Scram                                        
         IF    (@R1+2,EQ,'*'),BEGIN  Special line...                            
         IF    (@R1+3,EQ,'*'),EXIT                                              
         IF    (@R1+3,EQ,':'),EXIT                                              
         CLEAR SHMFSKIP                                                         
         IF    (@R1+3,EQ,'D'),'SET SHMFSKIP'                                    
         END                                                                    
         END                                                                    
         IF    ^SHMFSKIP,BEGIN                                                  
         LA    R15,1000            Lineno increment                             
         A     R15,SHMLNO                                                       
         ST    R15,SHMLNO          Save incremented lineno                      
*                                                                               
         MVC   SHMLINE(4),SHMLNO   Move in lineno                               
         LC    R15,@R1             Size of pressed line                         
         LA    R15,@R15+1          Including the length byte                    
         MOVE  R15,SHMLINE+4,@R1   Copy pressed line                            
         LA    R15,SHMLINE                                                      
         XPUSH R2                                                               
         LA    R1,SHMLINE2                                                      
         VCALL UNPRESS             EXTWRITE wants unpress.  CH 5/91             
         LR    R2,R15              R1,R0-text loc,len R2-line no                
         VCALL EXTWRITE            Buffer/write this line                       
         XPOP  R2                                                               
         END                                                                    
         LC    R15,@PAR+2(R2)                                                   
         LA    R2,@R15+1(R2)                                                    
         END                                                                    
         PFREE PAR                                                              
         LA    R15,SHMDIR(R4)                                                   
         PJUNK L2:@R15,TEMP                                                     
         LA    R4,@R4+2            Next dir entry                               
         END                                                                    
*                                                                               
*        COMMENT                   FINAL WRITE REMOVED BY CH 05/91              
*        CLEAR R15                                                              
*        VCALL EXTWRITE            Do final write                               
*                                                                               
         CLEAR SHMFMLQ             We've processed our DEFQ files               
*                                                                               
SHMLERR  LA    R15,4               Some sort of error                           
SHMLFREE LR    R2,R15                                                           
         IF    CPRFEXT,BEGIN       Close and DEQ dsn...                         
         VCALL EXTCLOSE            Close                                        
         VCALL DDEQ                Dequeue                                      
         END                                                                    
*                                                                               
         IF    ((R2,^NEG),AND,SHMFCHNG),BEGIN  Scr MAILQ files...               
         ACALL SCRMAILQ            Scratch any MAILDEFQ files                   
         END                                                                    
*                                                                               
         L     R5,SHMKWRP                                                       
         WITH  (KWR,R5),BEGIN                                                   
         LA    R3,10               Max kwrite retry count                       
         WHILE ((R3,POS),AND,KWRIFL.KWRIFVAL),BEGIN                             
*                                                                               
         IF    (R2,^NEG),BEGIN                                                  
         CLEAR KWRSFL.KWRSFMAI+KWRSFKEP                                         
         IF    (SHMNCNT,NZ),'SET KWRSFL.KWRSFMAI'                               
         IF    (SHMKCNT,NZ),'SET KWRSFL.KWRSFKEP'                               
         END                                                                    
*                                                                               
         IF    SHMFMLQ,'SET KWRSFL.KWRSFMLQ'                                    
         IF    KWRSFL.KWRSFMLQ,'SET KWRSFL.KWRSFMAI'  (Important!)              
         KWRITE KWR                                                             
         IF    KWRHFL.KWRHFUOK,EXIT                                             
         DECR  R3                                                               
         END                                                                    
         END                                                                    
         LR    R15,R2              Set rc                                       
*                                                                               
SHMLEXIT LR    R2,R15              Save rc                                      
         IF    CPRFEXT,'VCALL EXTCLOSE'  Tidy up                                
         CLEAR CPFNOCAT            Tidy up                                      
         CLEAR (CPDSNWA,DSAVSIZ)   Tidy up also                                 
         LR    R15,R2              Restore rc                                   
*                                                                               
         XEXIT ,,LTR                                                            
*                                                                               
SHMLEMSG TSEG  (R1),(R0),CR                                                     
         B     SHMLERR                                                          
         SPACE 2                                                                
         DS    0F                                                               
SHMLWRK  DC    AL1(DESRTRN+LXCATRTN+PREST+DESABORT)                             
         DC    AL3(SHMLRTN)                                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SCRMAILQ -- Local routine to scratch all the MAILDEFQ files                  
*    processed.                                                                 
*                                                                               
*    On entry:                                                                  
*      R6 - SHMWA ptr (assumed)                                                 
*                                                                               
SCRMAILQ PROC                                                                   
         LA    R2,1                                                             
         WHILE (R2,LE,SHMDMAX#),BEGIN  We need to scratch...                    
         LA    R15,SHMBOXES(R2)                                                 
         LC    R0,@R15                                                          
         IF    (R0,NZ),BEGIN       We are using this MAILDEF file...            
         CLEAR CPFNOCAT                                                         
         CLEAR DSNON                                                            
         MVC   DSNWADSN,=CL44'WYL.gg.uuu.MAIL.DEFQnnnn'                         
         MVC   DSNWADSN+4(2),CPCGRP  Group                                      
         MVC   DSNWADSN+7(3),CPCUSER  User                                      
         LR    R15,R2                                                           
         A     R15,=A(100000)      Force leading zeros for BTD                  
         BTD   DSNWADSN+20,4,(R15)                                              
         MVC   DSNWANL,=H'24'      L'WYL.gg.uuu.MAIL.DEFQnnnn                   
         DSCRATCH                                                               
         IF    (R15,NZ),BEGIN                                                   
         TSEG  'Minor mailbox problem -- unable to scratch '                    
         TSEG  DSNWADSN,LH:DSNWANL                                              
         TCR                                                                    
         END                                                                    
         VCALL DDEQ                Dequeue dsname                               
         END                                                                    
*                                                                               
         LA    R2,@R2+1            Next MAILDEFQ file                           
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
SMIWA    RECORD BEGIN                                                           
         XSA   R2,R8                                                            
*                                                                               
SMIPBRNO DS    F                   PBR pageno (on entry)                        
SMIPARNO DS    F                   PAR pageno (PARSAVE/PARREST)                 
*                                                                               
SMPRXPAR DS    F                   PAR pageno on entry to SMPROMPT              
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SMITEM -- Routine to display mail item.                                      
*                                                                               
*    On entry:                                                                  
*      R0 - offset in dir                                                       
*      R1 - offset in page                                                      
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - ok                                                                   
*      4 - attn                                                                 
*                                                                               
SMITEM   XENTER R2,R8,L'SMIWA,LOCAL                                             
         USING SMIWA,WAR                                                        
*                                                                               
         SET   CPFNPT              Don't allow p/p text                         
         LR    R5,R0               Dir offset                                   
         LR    R3,R1               Page offset                                  
*                                                                               
         CLEAR R0                                                               
         IF    (PBR,NZ),'PNUM PBR'                                              
         ST    R0,SMIPBRNO         Save PBR pageno                              
         PFREE PBR                                                              
*                                                                               
         LA    R15,SHMDIR(R5)                                                   
         PGET  PAR,L2:@R15                                                      
         LH    R4,@PAR             Page len                                     
*                                                                               
         CLEAR SHMITEM             Reset item info                              
         STM   R3,R5,SHMIPTRS      Save pos in dir                              
*                                                                               
SMILOOP  LA    R15,@R3+2(PAR)                                                   
         SH    R15,=H'4'           Back up for mythical lineno                  
         LA    R1,SHMLINE                                                       
         VCALL UNPRESS                                                          
*                                                                               
         IF    (@R1,EQ,'*'),BEGIN  Control line...                              
         IF    (@R1+1,EQ,'*'),EXIT                                              
         IF    (@R1+1,EQ,':'),EXIT                                              
         B     SMIHDR                                                           
         END                                                                    
*                                                                               
         IF    SHMFRETY,SHMITX     It's ok, if retyping item                    
         ACALL SHMTSEG             TSEG MIXED                                   
         ACALL PARSAVE             ((((((((                                     
         TCR                                                                    
         TSEG  'Mail logic problem.',,CR                                        
         ACALL PARREST             ))))))))                                     
         LA    R15,4                                                            
         B     SMIEXIT                                                          
*                                                                               
SMIHDR   MVC   SHMISTAT(9),@R1+1   Save stat char & subj                        
         CLEAR R15                 Preset rc                                    
         IF    ^SHMFALL,BEGIN                                                   
         IF    ((SHMISTAT,EQ,'K'),AND,^SHMFKEPT),SMIEXIT                        
         IF    ((SHMISTAT,NE,'K'),AND,SHMFKEPT),SMIEXIT                         
         END                                                                    
*                                                                               
         IF    ((SHMSUBJ,NZ),AND,(SHMSUBJ,NE,SHMISUBJ)),SMIEXIT                 
*                                                                               
         SET   CPFFIRST            At least one item found                      
*                                                                               
         XPUSH R0,R1                                                            
         IF    CPFINT,BEGIN        Internal format...                           
         IF    (@R1+2,EQ,'????????'),'MVC @R1+2(8),CVBLANKS'                    
         ACALL PARSAVE             ((((((((                                     
         TSEG  (R1),11                                                          
         ACALL PARREST             ))))))))                                     
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         IF    ((SHMSUBJ,NZ),OR,(@R1+2,EQ,'????????')),EXIT                     
         LR    R2,R1                                                            
         ACALL PARSAVE             ((((((((                                     
         TSEG  '('                                                              
         TSEG  @R2+2,8,T                                                        
         TSEG  ') '                                                             
         ACALL PARREST             ))))))))                                     
         END                                                                    
         XPOP  R0,R1                                                            
*                                                                               
         LA    R1,@R1+11                                                        
         SH    R0,=H'11'                                                        
         IF    NEG,'CLEAR R0'                                                   
*                                                                               
         IF    SHMFSUM,BEGIN       Summary...                                   
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         DECR  R15                                                              
         IF    (@R15,EQ,':'),'DECR R0'                                          
         ACALL SHMTSEG                                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         ACALL SHMTSEG                                                          
         ACALL PARSAVE             ((((((((                                     
         TCR                                                                    
         ACALL PARREST             ))))))))                                     
         IF    NZ,'STM R3,R5,SHMCPTRS; SET SHMFATTN'                            
         END                                                                    
*                                                                               
*                                                                               
         LC    R0,@R3+2(PAR)                                                    
         IF    ^SHMFRETY,BEGIN     Keep counts accurate...                      
         LR    R15,R0                                                           
         LA    R15,@R15+5          True pressed len                             
         AH    R15,SHMBCNT                                                      
         STH   R15,SHMBCNT                                                      
         END                                                                    
         AR    R3,R0                                                            
         LA    R3,@R3+1            Next line on page                            
         IF    (R3,GE,R4),'LA R5,@R5+2; CLEAR R3'                               
*-                                                                              
*-       Now type text of item.                                                 
*-                                                                              
SHMITX   WHILE (R5,LT,SHMNDIR),BEGIN  Go through dir...                         
         LA    R15,SHMDIR(R5)                                                   
         PGET  PAR,L2:@R15                                                      
         LH    R4,@PAR             Page len                                     
         WHILE (R3,LT,R4),BEGIN    Go thru lines in page...                     
         LA    R15,@R3+2(PAR)                                                   
         SH    R15,=H'4'           Backup for mythical lineno                   
         LA    R1,SHMLINE                                                       
         VCALL UNPRESS                                                          
*                                                                               
         IF    (@R1,EQ,'*'),BEGIN  Header...                                    
         IF    (@R1+1,EQ,'::'),BEGIN  Mail header...                            
         IF    ^SHMFMHDR,SMINEXT   Skip it                                      
         END                                                                    
*                                                                               
         ELSEIF ((@R1+1,EQ,':'),AND,(@R1+2,NE,':')),BEGIN                       
         IF    ^SHMFUHDR,SMINEXT   Skip it                                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               Not a header...                              
         IF    (@R1+1,NE,'*'),SMIPR  End of item                                
         IF    ^CPFINT,'LA R1,@R1+1; DECR R0'                                   
         END                                                                    
         END                                                                    
*                                                                               
         INCR  R15,SHMLCNT         Kick item line count                         
         IF    SHMFSKIP,SMIEXIT    Skip item entirely                           
         IF    SHMFATTN,'INCR R15,SHMSCNT; B SMINEXT'                           
         IF    SHMFSUM,SMINEXT                                                  
*                                                                               
         ACALL SHMTSEG                                                          
         ACALL PARSAVE             ((((((((                                     
         TCR                                                                    
         ACALL PARREST             ))))))))                                     
         IF    NZ,'STM R3,R5,SHMCPTRS; SET SHMFATTN'                            
*                                                                               
SMINEXT  LC    R0,@R3+2(PAR)                                                    
         IF    ^SHMFRETY,BEGIN     Keep counts accurate...                      
         LR    R15,R0                                                           
         LA    R15,@R15+5          True pressed len                             
         AH    R15,SHMBCNT                                                      
         STH   R15,SHMBCNT                                                      
         END                                                                    
         AR    R3,R0                                                            
         LA    R3,@R3+1            Next line on page                            
         END                                                                    
*                                                                               
         LA    R15,@R5+2                                                        
         IF    (R15,GE,SHMNDIR),SMIPR  last item, don't kick ptrs               
         CLEAR R3,R4                                                            
         LA    R5,@R5+2            Next page in dir                             
         END                                                                    
*                                                                               
SMIPR    ACALL SMPROMPT            Prompt for action                            
         BM    SMILOOP             Retype item                                  
*                                                                               
SMIEXIT  CLEAR CPFNPT              Reset                                        
         XPUSH R15                                                              
         PFREE PAR                                                              
         PFREE PBR                                                              
         IF    ('LT R0,SMIPBRNO',NZ),'PGET PBR,(R0)'                            
         XPOP  R15                                                              
         XEXIT ,,LTR                                                            
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SMPROMPT -- Local routine to ask user what to do about item just             
*    read.                                                                      
*                                                                               
*    On entry:                                                                  
*      R3,R4,R5 - current mail dir ptrs                                         
*                                                                               
*    On exit, R15 (and CC):                                                     
*      -4 - ptrs set for new item                                               
*       0 - prompt done                                                         
*       4 - attn response to prompt                                             
*                                                                               
SMPROMPT LENTER R0,R2                                                           
         PNUM  PAR                                                              
         ST    R0,SMPRXPAR         Save PAR pageno on entry                     
*                                                                               
         COMMENT                   Nothing to scan yet                          
         CLEAR R0                                                               
         CLEAR R1                                                               
         OSCINIT (R1),(R0)                                                      
*                                                                               
         IF    SHMFSKIP,SMPOK                                                   
*                                                                               
         STM   R3,R5,SHMPTRS       Save current ptrs                            
*                                                                               
         IF    SHMFSUM,BEGIN       Summary...                                   
         IF    (SHMLCNT,NZ),BEGIN                                               
         ACALL PARSAVE             ((((((((                                     
         TSEG  '; '                                                             
         TNUM  LH:SHMLCNT                                                       
         TSEG  ' lines'                                                         
         ACALL PARREST             ))))))))                                     
         END                                                                    
*                                                                               
         ACALL PARSAVE             ((((((((                                     
         TCR                                                                    
         ACALL PARREST             ))))))))                                     
         IF    NZ,'LA R15,4; B SMEXIT'  Attn                                    
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (SHMSCNT,NZ),BEGIN                                               
         CLEAR CPFDUMP                                                          
         IF    ^CPFVER,BEGIN                                                    
         ACALL PARSAVE             ((((((((                                     
         TSEG  'Mail not updated.',,CR                                          
         ACALL PARREST             ))))))))                                     
         CLEAR SHMFCHNG                                                         
         B     SMATTN                                                           
         END                                                                    
         END                                                                    
*                                                                               
         IF    ^CPFVER,BEGIN                                                    
         IF    SHMFDEL,SMDEL                                                    
         B     SMPOK                                                            
         END                                                                    
*                                                                               
*                                                                               
SMREAD   TWRITE                                                                 
*                                                                               
         IF    CPLSTOWD,BEGIN      Lines have been stowed...                    
         CLEAR CPLSTOWD,CPFDUMP                                                 
*                                                                               
         SET   (CPLFLG5.CPFNLST,EQ)                                             
         CLEAR CPLFLG1.CPFTODFT    Assume TO not defaulted                      
         IF    ^CPLCOLXP,'SET CPLFLG1.CPFTODFT'                                 
         L     R15,CPDMPLST        Next line                                    
         SL    R15,CPDMPDLT        Minus delta for last                         
         VCALL LASTLINE            Do last line msg                             
         END                                                                    
*                                                                               
SMLOOP   L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         TREAD 'Delete',,Q                                                      
         BM    SMATTN                                                           
         IF    POS,BEGIN                                                        
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'  No exec break                        
         L     R15,SHMKWRP                                                      
         WITH  (KWR,R15),BEGIN                                                  
         IF    KWRTFL2.KWRTFMNOATTN,BEGIN                                       
         TSEG  'Type Q to quit the SHOW MAIL command.',,CR                      
         B     SMLOOP                                                           
         END                                                                    
         END                                                                    
         B     SMATTN                                                           
         END                                                                    
         CLEAR CPLFLG5.CPFNLST     Make sure NOLIST is reset                    
         OSCINIT (R1),(R0)                                                      
*                                                                               
         LA    R15,SHMDIR(R5)                                                   
         L2    R0,@R15             Pageno                                       
         IF    (R0,NZ),'PGET PAR,(R0)'  Get current page                        
         ELSE  'PFREE PAR'                                                      
*                                                                               
         SET   CPSCNFLG.CPFRTNER                                                
         MVC   CPSCNERT,=A(SMINVRTN)                                            
         OSCAN SMPRT                                                            
         END                                                                    
         B     SMPOK                                                            
*                                                                               
SMLPRINT LA    R0,1                LPRINT wanted                                
         B     SMPRX                                                            
*                                                                               
SMPRINT  CLEAR R0                  No special print options                     
SMPRX    MVC   SHMPPTRS(3*4),SHMIPTRS  Save pos in dir                          
         CLEAR R1                  (No header subroutine)                       
         LA    R15,PRINTRTN                                                     
         VCALL PRINTMSG                                                         
         B     SMREAD                                                           
*                                                                               
SMDUMP   OSCBACK                                                                
         OSCAN L:=V(COLLPRT)       Allow collect options                        
*                                                                               
SMLIST   LM    R3,R5,SHMIPTRS                                                   
         LA    R15,SHMDIR(R5)                                                   
         L2    R0,@R15                                                          
         ST    R0,SMPRXPAR         Set new PAR register on exit                 
         CLEAR SHMITEM                                                          
         STM   R3,R5,SHMIPTRS                                                   
         LH    R15,=H'-4'                                                       
         B     SMEXIT                                                           
*                                                                               
SMKEEP   OSCAN                                                                  
         LR    R2,R15              Subject name                                 
         IF    Z,'CLEAR R2'        No subject                                   
         B     SMDK                                                             
*                                                                               
SMDEL    OSCAN                                                                  
         IF    NZ,BEGIN                                                         
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid DELETE option.',,CR                                   
         B     SMLOOP                                                           
         END                                                                    
*                                                                               
         LH    R2,=H'-1'                                                        
SMDK     L     R15,SHMIPTRS+8      Dir offset                                   
         LA    R15,SHMDIR(R15)                                                  
         PGET  PAR,L2:@R15                                                      
         L     R15,SHMIPTRS        Page offset                                  
         LA    R15,@PAR+2(R15)                                                  
         IF    (R2,M),BEGIN        Delete...                                    
         L     R1,SHMLINES                                                      
         DECR  R1                                                               
         SH    R1,SHMLCNT                                                       
         ST    R1,SHMLINES         Adjust line count                            
*                                                                               
         L     R1,SHMCOUNT                                                      
         SH    R1,SHMBCNT                                                       
         ST    R1,SHMCOUNT         Adjust byte count                            
*                                                                               
         IF    (@R15+3,EQ,'K'),'DECR R1,SHMKCNT'  decr kept item count          
         ELSE  'DECR R1,SHMNCNT'   Decr new item count                          
         MVI   @R15+3,C'D'         Flag as deleted                              
         END                                                                    
*                                                                               
         ELSE  BEGIN               Keep...                                      
         IF    (@R15+3,NE,'K'),'DECR R1,SHMNCNT; INCR R1,SHMKCNT'               
         MVI   @R15+3,C'K'         Flag as kept                                 
         IF    ((R2,NZ),AND,(@R15+1,EQ,X'0A')),'MVC @R15+4(8),@R2'              
         END                                                                    
         PMARK PAR                                                              
         SET   SHMFCHNG            Re-write needed                              
SMPOK    CLEAR R15                                                              
*                                                                               
SMEXIT   LR    R2,R15                                                           
         PGET  PAR,L:SMPRXPAR      Restore PAR                                  
         LR    R15,R2                                                           
*                                                                               
         LEXIT R0,R2,LTR                                                        
*                                                                               
SMATTN   LA    R15,4                                                            
         B     SMEXIT                                                           
*                                                                               
SMCANCEL TSEG  'All actions cancelled '                                         
         ABORT '(except printing).'                                             
         SPACE 2                                                                
SMPRT    OSCKW D,SMDEL                                                          
         OSCKW DELETE,SMDEL,A                                                   
******** OSCKW DUMP,SMDUMP,A                                                    
         OSCKW K,SMKEEP                                                         
         OSCKW KEEP,SMKEEP,A                                                    
         OSCKW L,SMLIST                                                         
         OSCKW LIST,SMLIST,A                                                    
         OSCKW COLLECT,SMDUMP,A                                                 
         OSCKW PRINT,SMPRINT,A                                                  
         OSCKW LPRINT,SMLPRINT,A                                                
         OSCKW Y,SMDEL                                                          
         OSCKW YES,SMDEL                                                        
         OSCKW OK,SMDEL                                                         
         OSCKW N,SMPOK                                                          
         OSCKW NO,SMPOK                                                         
         OSCKW QUIT,SMATTN,A                                                    
         OSCKW Q,SMATTN                                                         
         OSCKW CANCEL,SMCANCEL,A                                                
         OSCKW ?,SMQUEST                                                        
         OSCKW ,SMINV                                                           
*                                                                               
* This routine is entered whenever there's a bad scan of the                    
* reply to the Delete? prompt on SHOW MAIL. This fixes a bug                    
* whereby a response of a single apostrophe to the Delete?                      
* prompt, while in WYLBUR READ EDIT mode from a SPIRES processor                
* causes WYLBUR to send the SHOW MAIL command to SPIRES instead                 
* of the apostrophe. This is because the old scanner (which we've               
* really got to get rid of) sends all scan errors to CVNVALID.                  
*                                                                               
SMINVRTN LM    R0,R1,CPSAVE                                                     
         CLEAR CPSCNFLG.CPFRTNER                                                
*                                                                               
SMINV    TSEG  (R1),(R0)                                                        
         TSEG  ' is unrecognized.',,CR                                          
SMQUEST  TSEG  'Expecting YES, NO, DELETE, KEEP, '                              
         TSEG  'LIST, COLLECT, LPRINT, PRINT, CANCEL or QUIT.',,CR              
         B     SMREAD                                                           
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHMTSEG -- Local routine to do a TSEG for mail item text.                    
*    Note this will write "LIST MIXED" form output if the responses             
*    are going to the terminal.                                                 
*                                                                               
*    On entry:                                                                  
*      R1,R0 - text loc, length                                                 
*                                                                               
SHMTSEG  LENTER R15,R2                                                          
         ACALL PARSAVE             ((((((((                                     
         IF    CPFDUMP,'TSEG (R1),(R0)'  Regular format                         
         ELSE  'VCALL MIXFMT'      Mixed format                                 
         ACALL PARREST             ))))))))                                     
         LEXIT R15,R2,LTR                                                       
         SPACE 2                                                                
         QLTORG                                                                 
*box                                                                            
*                                                                               
*  PARSAVE -- Local routine to save PAR page-number.                            
*                                                                               
PARSAVE  LENTER R15,R2                                                          
         PNUM  PAR                                                              
         ST    R0,SMIPARNO         Save PAR pageno                              
         LEXIT R15,R2,LTR                                                       
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  PARREST -- Local routine to restore PAR page-number.                         
*                                                                               
PARREST  LENTER R15,R2                                                          
         IF    (SMIPARNO,Z),'PFREE PAR'                                         
         ELSE  'PGET PAR,L:SMIPARNO'                                            
         LEXIT R15,R2,LTR                                                       
*                                                                               
         DROP  SHMWA,SMIWA,BR                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTRTN -- PRINTMSG co-routine.  This routine gets the next                 
*    line in the mail dir and returns it (unpressed).  R1 is zero               
*    if there aren't any more lines.                                            
*                                                                               
PRTNWA   RECORD BEGIN                                                           
PRTNFLAG FLAG                                                                   
         FLAG  PRTNFSKIP           - Don't display this line                    
         END                                                                    
*-                                                                              
PRINTRTN PROC  (R2,LSR),PRTNWA                                                  
         CLEAR PRTNWA                                                           
*                                                                               
         L     R6,CPCWA            Shmwa ptr                                    
         WITH  (SHMWA,R6)                                                       
*                                                                               
PRTNLOOP LM    R3,R5,SHMPPTRS      Print dir ptrs                               
         CLEAR R1                                                               
         IF    (R5,GT,SHMPTRS+8),PREXIT                                         
         IF    ((R5,EQ,SHMPTRS+8),AND,(R3,EQ,SHMPTRS)),PREXIT                   
         LA    R15,SHMDIR(R5)                                                   
         PGET  PAR,L2:@R15                                                      
         LH    R4,@PAR                                                          
         LA    R15,@R3+2(PAR)                                                   
         SH    R15,=H'4'                                                        
         LA    R1,SHMLINE                                                       
         VCALL UNPRESS                                                          
         IF    (@R1,EQ,'*'),BEGIN  Special line...                              
         LA    R2,11                                                            
         IF    (@R1+1,EQ,'*'),'LA R2,1'                                         
*                                                                               
         IF    (@R1+1,EQ,'::'),BEGIN                                            
         IF    ^SHMFMHDR,'SET PRTNFSKIP'  Don't display this line               
         CLEAR R2                                                               
         END                                                                    
*                                                                               
         IF    ((@R1+1,EQ,':'),AND,(@R1+2,NE,':')),BEGIN                        
         IF    ^SHMFUHDR,'SET PRTNFSKIP'  Don't display this line               
         CLEAR R2                                                               
         END                                                                    
*                                                                               
         AR    R1,R2                                                            
         SR    R0,R2                                                            
         IF    NEG,'CLEAR R0'                                                   
         END                                                                    
*                                                                               
         LC    R2,@R3+2(PAR)                                                    
         LA    R3,@R2+1(R3)                                                     
         IF    (R3,GE,R4),BEGIN                                                 
         CLEAR R3,R4                                                            
         LA    R5,@R5+2                                                         
         END                                                                    
         STM   R3,R5,SHMPPTRS                                                   
         IF    PRTNFSKIP,'CLEAR PRTNFSKIP; B PRTNLOOP'  Loop back               
PREXIT   LR    R15,R1              Set cc by R1                                 
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHMLSIZE -- Routine called by DOPEN to determine the size of the             
*    data.  On entry: R15 - blocksize for save                                  
*                                                                               
SHMLSIZE XENTER R2,R8,,LOCAL                                                    
         L     R6,CPCWA            Shmwa ptr                                    
         USING SHMWA,R6                                                         
         ST    R15,CPNWCT          Save blocksize                               
         MVC   DSAVCNT,SHMLINES+2  No. of lines                                 
         L     R3,SHMCOUNT                                                      
         STH   R3,DSAVSPA          Pressed byte count                           
         CLEAR R2                                                               
         SH    R15,=AL2(2+&PRESSZ-1)  Smallest buffer                           
         AR    R3,R15                                                           
         DR    R2,R15              No. of blocks needed (worst case)            
         STH   R3,DSAVBLK          Save block count                             
         LTR   BR,BR               Set nz cc for caller                         
         XEXIT                                                                  
         DROP  SHMWA,BR                                                         
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHMLRTN -- Routine to save the mail data set in temporary                    
*    text pages (SHMDIR).  It is entered from the range processor               
*    with the pressed line loc in R15.                                          
*                                                                               
*  PREST option removed 5/91 by CH.  We press line on entry for                 
*  backwards compatibility.                                                     
*                                                                               
*                                                                               
SHMLRTN  PROC                                                                   
*                                                                               
         COMMENT                   BE BACKWARDS COMPAT, PRESS LINE              
         L     R15,CPLCNO                                                       
         VCALL PRESS                                                            
         LR    R15,R1                                                           
*                                                                               
         L     R6,CPCWA            Shmwa ptr                                    
         WITH  (SHMWA,R6)                                                       
         LR    R5,R15                                                           
         INCR  R15,SHMLINES        Kick line count                              
         L     R15,SHMCOUNT                                                     
         LC    R1,@R5+4                                                         
         LA    R15,@R1+5(R15)                                                   
         ST    R15,SHMCOUNT        Total byte count                             
*                                                                               
         IF    (@R5+4,GE,X'02'),BEGIN  Non-null line...                         
         IF    (@R5+5.X'F0',NZ),EXIT  Leading blanks, scram                     
         LC    R15,@R5+5           First len byte                               
         IF    ((R15,LT,2),OR,(@R5+6,NE,'*')),EXIT                              
         IF    (@R5+7,EQ,'*'),EXIT                                              
         IF    (@R5+7,EQ,':'),EXIT                                              
         IF    (@R5+7,EQ,'K'),'INCR R15,SHMKCNT'                                
         ELSE  'MVI @R5+7,C"N"; INCR R15,SHMNCNT'                               
*                                                                               
         PGET  PBR,L2:SHMMAST                                                   
         LH    R4,@PBR             Len                                          
         IF    (R4,LGT,CVPSZ-4-2),BEGIN  Overflow...                            
         TSEG  'Account '                                                       
         TSEG  DSNWADSN+4,6        gg.uuu                                       
         ABORT ' mailbox is full -- too many items.'                            
         END                                                                    
         LR    R15,R5              Pressed line ptr                             
         LA    R1,SHMMDIR          Mail dir                                     
         ACALL BUFRLINE                                                         
         BNZ   SHMLNR              No room                                      
         STH   R0,@R4+2(PBR)       Offset in dir                                
         STH   R1,@R4+4(PBR)       Offset in page                               
         LA    R4,@R4+4                                                         
         STH   R4,@PBR                                                          
         PMARK PBR                                                              
         B     SHMREXIT                                                         
         END                                                                    
*                                                                               
         LR    R15,R5              Pressed line ptr                             
         LA    R1,SHMMDIR          Mail dir                                     
         ACALL BUFRLINE                                                         
         IF    POS,BEGIN           Won't fit...                                 
SHMLNR   TSEG  'Account '                                                       
         TSEG  DSNWADSN+4,6        gg.uuu                                       
         ABORT ' mailbox is full.'                                              
         END                                                                    
*                                                                               
SHMREXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BUFRLINE -- Routine to put pressed line (w/o lineno) in text                 
*    page.                                                                      
*                                                                               
*      On entry:                                                                
*        R1 - page dir ptr                                                      
*        R15- pressed line ptr                                                  
*                                                                               
*      On exit, R15 (and CC):                                                   
*        0 - ok (R0 - offset in dir, R1 - offset in page)                       
*        4 - new line won't fit                                                 
*                                                                               
         SPACE                                                                  
BUFRDIR  RECORD BEGIN                                                           
BUFRMAX  DS    H                   Max dir offset                               
BUFRCUR  DS    H                   Current dir offset                           
BUFRPG   DS    0H                  Pagenos                                      
         END                                                                    
         SPACE 2                                                                
BUFXWA   RECORD BEGIN                                                           
         XSA   R2,R8                                                            
BUFXLINE DS    XL(&PRESSZ)         Pressed line area                            
         END                                                                    
         SPACE 2                                                                
BUFRLINE XENTER R2,R8,L'BUFXWA,LOCAL                                            
         USING BUFXWA,WAR                                                       
         LC    R2,@R15+4           Pressed line length                          
         LA    R2,@R2+5            Include dummy lineno and len byte            
         MOVE  R2,BUFXLINE,@R15    Save line text                               
         LA    R6,BUFXLINE         Pressed line ptr                             
*                                                                               
         LR    R5,R1               Dir ptr                                      
         USING BUFRDIR,R5                                                       
         LTH   R4,BUFRCUR          Current dir offset                           
         IF    Z,BEGIN             First page...                                
         VCALL PGETNEW                                                          
         IF    NZ,'VCALL NOMORPG'                                               
         VCALL PTEMP                                                            
         STH   R0,BUFRPG           Save first pageno                            
         LA    R4,2                                                             
         STH   R4,BUFRCUR          Save current dir offset                      
         CLEAR (@PAR,2)                                                         
         PMARK PAR                                                              
         END                                                                    
         ELSE  BEGIN               Old page...                                  
         LA    R15,BUFRPG-2(R4)                                                 
         L2    R3,@R15             Pageno                                       
         FAIL  (R3,Z),MAILERRB                                                  
         PGET  PAR,(R3)                                                         
         END                                                                    
*                                                                               
BUFRRTRY LH    R3,@PAR                                                          
         LR    R2,R3                                                            
         LC    R15,@R6+4           Len byte for pressed line                    
         LA    R15,@R15+1                                                       
         AR    R2,R15              New buffer len                               
         IF    (R2,GT,CVPSZ-2),BEGIN  Overflow...                               
         IF    (R4,GE,BUFRMAX),'LA R15,4; B BUFREXIT'  Dir full                 
         VCALL PGETNEW                                                          
         IF    NZ,'VCALL NOMORPG'                                               
         VCALL PTEMP                                                            
         STH   R0,BUFRPG(R4)       Save pageno                                  
         LA    R4,@R4+2            Kick dir offset                              
         STH   R4,BUFRCUR                                                       
         CLEAR (@PAR,2)                                                         
         PMARK PAR                                                              
         B     BUFRRTRY                                                         
         END                                                                    
         STH   R2,@PAR             Save new page len                            
*                                                                               
         LA    R1,@R3+2(PAR)                                                    
         MOVE  R15,@R1,@R6+4                                                    
         PMARK PAR                                                              
*                                                                               
         LR    R0,R4                                                            
         SH    R0,=H'2'            Offset in dir                                
         LR    R1,R3               Offset in page                               
         CLEAR R15                 Rc                                           
BUFREXIT XEXIT ,,LTR                                                            
         DROP  BUFRDIR,BUFXWA,BR                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MLOGERR  -- Log mail I/O error                                               
*                                                                               
*      On entry:                                                                
*        R2 - Return code from DISK module                                      
*                                                                               
*                                                                               
         SPACE                                                                  
MLOGWA   RECORD BEGIN                                                           
MLOGCB   SEGCB                                                                  
MLOGBUF  DS    CL256               Working seg buffer                           
         END                                                                    
         SPACE 2                                                                
MLOGERR  PROC  MLOGWA                                                           
         SEGINIT MLOGBUF,,MLOGCB                                                
         VCALL LOGCMD                                                           
         SEG   'Return code '                                                   
         SEGDC (R2)                                                             
         SEG   ' for '                                                          
         SEG   DSNWADSN,LH:DSNWANL                                              
         SEG   ' on '                                                           
         SEG   DSNON6,L'DSNON6                                                  
         SETMSG L:MLOGCBLOC,L:MLOGCBLENF                                        
         LA    R15,=CL8'MAILERR'                                                
         VCALL LOGEVENT                                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAILCHK -- Routine to check if the logged on user is allowing                
*    mail.  This routine is called by MAIL/SEND (to issue a                     
*    warning to the user if he isn't going to accept mail) and                  
*    by the SHOW MAIL command.                                                  
*                                                                               
*      On entry:                                                                
*        R15 - KWR ptr                                                          
*                                                                               
MAILCHK  XENTER                                                                 
         IF    CPFINT,MCHKEXIT     Don't mess with INTERNAL format              
*                                                                               
         LR    R5,R15                                                           
         WITH  (KWR,R5),BEGIN                                                   
         IF    KWRIFL.KWRIFVAL,BEGIN  Valid account...                          
         IF    ^KWROFL.KWRFMAIL,BEGIN  Nomail...                                
         IF    (CPCMNM,NE,'SHO'),'TSEG "Note:",,B'                              
         SETMSG 'Your account is not receiving new mail.'                       
         B     MCHKMSG                                                          
         END                                                                    
*                                                                               
         IF    (KWRMFOR,NZ),BEGIN  Mail being forwarded...                      
         IF    (CPCMNM,NE,'SHO'),'TSEG "Note:",,B'                              
         TSEG  'Your new mail is being forwarded to '                           
         SETMSG KWRMFOR                                                         
         B     MCHKMSG                                                          
         END                                                                    
*                                                                               
         IF    (KWRMTXT,NZ),BEGIN                                               
         IF    (CPCMNM,EQ,'SHO'),EXIT                                           
         TSEG  'Your account''s mail message is: '                              
         TSEG  KWRMTXT,,T                                                       
         B     MCHKFIN                                                          
         END                                                                    
         EXIT                                                                   
*                                                                               
MCHKMSG  TSEG  (R1),(R0),TB                                                     
         IF    (KWRMTXT,NZ),BEGIN                                               
         TSEG  '-- '                                                            
         TSEG  KWRMTXT,,T                                                       
         END                                                                    
MCHKFIN  IF    (CPCMNM,NE,'SEN'),'TSEG X"15"'                                   
         TWRITE                                                                 
         END                                                                    
         END                                                                    
MCHKEXIT XEXIT                                                                  
         DROP  BR                                                               
*                                                                               
         QLTORG                                                                 
         VLTORG                                                                 
         END   .                                                                
