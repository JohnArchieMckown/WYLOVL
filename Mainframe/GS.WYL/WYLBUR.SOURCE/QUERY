QUERY    TITLE 'WYLBUR''s Query Commands'                                       
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
*                                                                               
*                                                                               
*  Modification history:                                                        
*                                                                               
*    21-April-1997 - M. Durket                                                  
*                                                                               
*    Fixed a bug in the OPENSETU routine. If the server RPC EXEC                
*    fails to initialize at startup, then WYLBUR will subsequently              
*    undergo periodic failsofts in SERVPOST. This is because the                
*    UNETSVIB field fails to get cleared if the server doesn't                  
*    initialize. The SVIBUNET field is cleared, though, which                   
*    triggers the failsoft. One would think that the UNETSVIB                   
*    field would get closed, because NPATCLS is being called,                   
*    but there's a call in NPATCLS to check for Suzan posts on                  
*    the UNET, prior to freeing it - and this calls SERVPOST when               
*    it finds that there's a TCP_EOF in the queue.                              
*                                                                               
WYLQUERY CSECT                                                                  
*                                                                               
QUER     IDENT 2193                15:11:48 07/12/02  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
KWR      RECORD 'KWR'                                                           
*                                                                               
UPATH    RECORD BEGIN                                                           
UPAT     UPATH                                                                  
         END                                                                    
*                                                                               
UNET     RECORD BEGIN                                                           
UNET     UNET                                                                   
         END                                                                    
*                                                                               
SEGCB    RECORD 'SEGCB'                                                         
*                                                                               
         COPY  VNENTRY                                                          
*                                                                               
         POP   DSECTS                                                           
         EJECT                                                                  
*box                                                                            
*                                                                               
*        Field Descriptor Entry.                                                
*                                                                               
FDEMAX#  EQU   100,,C'N'           Maximum number of FDEs                       
*                                                                               
FDE      RECORD BEGIN                                                           
FDETYPE  FLAG                                                                   
         FLAG  (FDETLEFT,1,EQ)     - Left centered field                        
         FLAG  (FDETRIGHT,2,EQ)    - Right centered field                       
         FLAG  (FDETCENTER,3,EQ)   - Middle centered field                      
*                                                                               
         DS    X                   Reserved                                     
*                                                                               
FDELEN   DS    H                   Field length                                 
FDENEXT  EQU   *,,C'X'             Next FDE goes here                           
         END                                                                    
*                                                                               
FDEMAP   RECORD BEGIN                                                           
         DS    (FDEMAX#)XL(L'FDE)  Field descriptor entries                     
FDEMAPE  DS    XL(L'FDE)           Zeroed FDE (end of map)                      
         END                                                                    
*box                                                                            
*                                                                               
*        INENTRY:  A line of input from the Query server.                       
*                                                                               
INENTRY  RECORD BEGIN                                                           
INLINK   DS    A                   Ptr to next INENTRY (or zero)                
INMAXL#  EQU   4000,,C'N'          Maximum line length                          
INLEN    DS    H                   Length of data following                     
INTEXT   EQU   *,,C'X'             Line data                                    
         END                                                                    
SVIBTRC# EQU   50,,C'X'            Max number of trace entries                  
*box                                                                            
*                                                                               
*  STRC -- Server trace entry                                                   
*                                                                               
STRC     RECORD BEGIN                                                           
STRCSTRT DS    CL4'STRC'           Self identification                          
STRCLINK DS    A                   Next STRC ptr (or zero)                      
STRCCLCK DS    D                   Time stamp                                   
*                                                                               
STRCFLAG FLAG                                                                   
         FLAG  STRCFSHOWN          - This entry has been displayed              
         FLAG  STRCFKEEP           - Don't delete this entry                    
*                                                                               
STRCLNO  DS    H                   Milten line number                           
STRCACCT DS    CL5                 Session account (uuugg)                      
*                                                                               
STRCTYPE DS    CL3                 Trace type                                   
*                                                                               
STRCDLEN DS    H                   Data length                                  
STRCHDR  EQU   STRCSTRT,*-STRCSTRT,C'X'                                         
*                                                                               
STRCMXDL EQU   200,,C'N'           Max allowed trace data length                
STRCDATA DS    0X                  Data starts here (variable len)              
         END                                                                    
*box                                                                            
*                                                                               
*  QINFO -- Query information.                                                  
*                                                                               
*    Each user has a linked list of QINFOs.  This is the control                
*      block that resolves references to "*" as the current query               
*      server number.                                                           
*                                                                               
QINFO    RECORD BEGIN                                                           
QINFLINK DS    A                   Next QINFO ptr                               
QINFNO   DS    F                   Our server number                            
         END                                                                    
         EJECT                                                                  
*-                                                                              
*-       Universal constants.                                                   
*-                                                                              
$TAB     EQU   X'05',,C'N'         EBCDIC tab character                         
*-                                                                              
*-       Well known sockets.                                                    
*-                                                                              
SQLPORT# EQU   201,,C'N'           RSQL socket number                           
         SPACE 2                                                                
         COPY  SVIB                                                             
*box                                                                            
*                                                                               
*  QMW -- QMONITOR wait entry.                                                  
*                                                                               
QMW      RECORD BEGIN                                                           
QMWLINK  DS    A                   Next QMW ptr                                 
QMWNO    DS    F                   Server number (filled in at post)            
QMWNAME  DS    CL(L'SVIBNAME)      Waiting for this server name                 
QMWJCBP  DS    A                   Waiter's JCB ptr                             
         END                                                                    
         TITLE 'SET QUERY Command'                                              
*box                                                                            
*                                                                               
*  SETQUERY -- SET QUERY Command.                                               
*                                                                               
SETQWA   RECORD BEGIN                                                           
SETQFLG1 FLAG                                                                   
         FLAG  SETQFDEL            - DELETE                                     
         FLAG  SETQFCRE            - CREATE                                     
         FLAG  SETQFPUBLIC         - PUBLIC                                     
*                                                                               
SETQFLG2 FLAG                                                                   
         FLAG  SETQFREUSE          - REUSE                                      
         FLAG  SETQFNOREUSE        - NOREUSE                                    
*                                                                               
SETQFLG3 FLAG                                                                   
         FLAG  SETQFTRACE          - TRACE                                      
         FLAG  SETQFNOTRACE        - NOTRACE                                    
         FLAG  SETQFTCLEAR         - TCLEAR                                     
*                                                                               
SETQFOPT FLAG                                                                   
         FLAG  SETQFSOME           - We processed something                     
         FLAG  SETQFPDEF           - Private defn overrides public              
*                                                                               
SETQRDB  DS    CL32                Remote database name                         
SETQRHOST DS   CL(L'UNETRNAME)     Remote host name                             
SETQRPORT DS   F                   Remote port number                           
SETQRUSER DS   CL32                Remote user name                             
SETQRPASS DS   CL32                Remote user password                         
SETQMAX  DS    F                   Maximum number of servers allowed            
*                                                                               
SETQSVIB DS    XL(L'SVIB)          Our working SVIB                             
         END                                                                    
*                                                                               
SETQUERY XPROC SETQWA                                                           
         CLEAR SETQWA                                                           
         LA    R6,SETQWA                                                        
         WITH  (SETQWA,R6)                                                      
*-                                                                              
*-       Scan for server name.                                                  
*-                                                                              
         LA    R5,SETQSVIB                                                      
         WITH  (SVIB,R5)                                                        
         CLEAR SVIB                                                             
         MVC   SVIB(4),=CL4'SVIB'                                               
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVSCANPROTO       Scan server name                             
         BNZ   CVERROR                                                          
*-                                                                              
*-       Scan SET QUERY options.                                                
*-                                                                              
         MVC   SVIBMAX,=F'1'       Default MAXSERVERS                           
         SET   SVIBFTRC            Default is TRACE                             
*                                                                               
         SCAN  SETQPRT             SET QUERY options                            
         SCANCHK ,                 Complain if scanning error                   
*-                                                                              
*-       Make model SVIB from options given.                                    
*-                                                                              
         MVC   SVIBRHOST,SETQRHOST                                              
         MVC   SVIBRPORT,SETQRPORT                                              
         MVC   SVIBRUSER,SETQRUSER                                              
         MVC   SVIBRPASS,SETQRPASS                                              
         MVC   SVIBRDB,SETQRDB                                                  
         IF    (SETQMAX,NZ),'MVC SVIBMAX,SETQMAX'                               
         IF    SETQFREUSE,'SET SVIBFREUSE'                                      
         IF    SETQFNOREUSE,'CLEAR SVIBFREUSE'                                  
         IF    SETQFTRACE,'SET SVIBFTRC'                                        
         IF    SETQFNOTRACE,'CLEAR SVIBFTRC'                                    
*-                                                                              
*-       SET QUERY CREATE.                                                      
*-                                                                              
         IF    SETQFCRE,BEGIN      SET QUERY CREATE...                          
         ACALL QSCREATE            Create a new server                          
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       SET QUERY DELETE.                                                      
*-                                                                              
         IF    SETQFDEL,BEGIN      SET QUERY DELETE...                          
         ACALL QSDELETE            Delete existing server                       
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       SET QUERY number <new options>.                                        
*-                                                                              
         IF    ('LT R15,SVIBNO',NZ),BEGIN  Server number...                     
         ACALL SERVGET             Get SVIB ptr                                 
         IF    NZ,'LCALL QSUPDATE'  Update server info                          
         END                                                                    
*-                                                                              
*-       SET QUERY name <new options>.                                          
*-                                                                              
         ELSE  BEGIN               Server name...                               
QUPDLOOP LOOP  BEGIN                                                            
         IF    SETQFPUBLIC,BEGIN   Search public servers...                     
         LA    R15,SVIB                                                         
         ACALL SERVFINDPUBLIC      Find next matching public server             
         IF    NZ,EXIT,QUPDLOOP    No more entries found                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               Otherwise, search user servers...            
         LA    R15,SVIB                                                         
         ACALL SERVFINDPRIVATE     Find next matching private server            
         IF    NZ,BEGIN            No more entries found...                     
         IF    SETQFSOME,EXIT,QUPDLOOP  We are done now                         
         SET   SETQFPUBLIC         Nothing found, look through pubs             
         LA    R5,SETQSVIB         Our working SVIB                             
         B     QUPDLOOP                                                         
         END                                                                    
*                                                                               
         LR    R5,R1               Set our new current SVIB ptr                 
*                                                                               
         LA    R15,SVIB            SVIB ptr                                     
         LCALL QSUPDATE            Update server info                           
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Server entry not found.                                                
*-                                                                              
         IF    ^SETQFSOME,BEGIN    No match...                                  
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' does not exist; '                                              
         TSEG  'use CREATE option to create new server.'                        
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QSCREATE -- Local routine to create a new server.                            
*    (part of the SET QUERY command).                                           
*                                                                               
QSCREATE PROC                                                                   
         WITH  (SVIB,R5),(SETQWA,R6)                                            
         IF    (SVIBRHOST,Z),BEGIN  Set default host name...                    
         ERROR 'Missing "ON hostname" on SET QUERY command.'                    
         END                                                                    
*                                                                               
         IF    (SVIBRDB,Z),BEGIN                                                
         ERROR 'Missing "AS dbname" on SET QUERY command.'                      
         END                                                                    
*                                                                               
         IF    (SVIBRPORT,Z),BEGIN  Set default port...                         
         MVC   SVIBRPORT,=A(SQLPORT#)  Set default port number                  
         END                                                                    
*                                                                               
         IF    SETQFPUBLIC,BEGIN   Public server...                             
         LA    R15,SVIB                                                         
         ACALL SERVFINDPUBLIC      Find matching public server                  
         BZ    QSEXISTS            Error if one already exists                  
         END                                                                    
*                                                                               
         ELSE  BEGIN               User server...                               
         LA    R15,SVIB                                                         
         ACALL SERVFINDPRIVATE     Find matching private server                 
         BZ    QSEXISTS            Error if one already exists                  
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVFINDPUBLIC      Is it also a public server?                  
         IF    Z,'SET SETQFPDEF'   We are overriding public server              
         END                                                                    
*                                                                               
         SET   SVIB#MASTER         This is the master definition                
*                                                                               
         CLEAR R0                  Private server                               
         IF    SETQFPUBLIC,BEGIN   Public server...                             
         IF    ^CPSFSPR,'ERROR "Privileged option."'                            
         LA    R0,1                Set public                                   
         END                                                                    
         LA    R15,SVIB                                                         
         ACALL SERVALLO            Allocate new server info                     
*                                                                               
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' has been created'                                              
         IF    SETQFPDEF,'TSEG " (overrides public server)"'                    
         SETMSG '.'                                                             
         EXIT  QSCREATE                                                         
*-                                                                              
*-       Server already exists error message.                                   
*-                                                                              
QSEXISTS TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' already exists, no action taken.'                              
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QSDELETE -- Local routine to delete the server specified                     
*    (part of the SET QUERY command).                                           
*                                                                               
QSDELETE PROC                                                                   
         WITH  (SVIB,R5),(SETQWA,R6)                                            
*-                                                                              
*-       Delete a server by number.                                             
*-                                                                              
         LT    R15,SVIBNO          Was server number specified?                 
         IF    NZ,BEGIN            Yes...                                       
         ACALL SERVGET             Get SVIB ptr                                 
         ACALL SERVCHK             Make sure we can use it                      
*                                                                               
         LR    R2,R15                                                           
         MOVEL SVIB,@R2            Save SVIB contents                           
*                                                                               
         IF    SVIBFPUBLIC,BEGIN   Public server...                             
         IF ^CPSFSPR,'ERROR "Public server operation is privileged."'           
         END                                                                    
*                                                                               
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
*-                                                                              
*-       Delete a busy server.                                                  
*-                                                                              
         IF    SVIB#BUSY,BEGIN     Busy...                                      
         TSEG  ' is busy (will delete server when current'                      
         TSEG  ' operation has completed).',,CR                                 
*                                                                               
         WITH  (SVIB,R2),'SET SVIBFCLOSING'  Close connection when done         
         END                                                                    
*-                                                                              
*-       Delete an idle server.                                                 
*-                                                                              
         ELSE  BEGIN               Delete server...                             
         TSEG  ' will be deleted.',,CR                                          
*                                                                               
         LR    R15,R2                                                           
         ACALL SERVDEL             Delete server entry                          
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       Delete all servers with the name specified.                            
*-                                                                              
QDELLOOP LOOP  BEGIN                                                            
*-                                                                              
*-       Find first/next server.                                                
*-                                                                              
         IF    SETQFPUBLIC,BEGIN   Search public servers...                     
         LA    R15,SVIB                                                         
         ACALL SERVFINDPUBLIC      Find next matching public server             
         IF    NZ,EXIT,QDELLOOP    No more entries found                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               Otherwise, search user servers...            
         LA    R15,SVIB                                                         
         ACALL SERVFINDPRIVATE     Find next matching private server            
         IF    NZ,BEGIN            No more entries found...                     
         IF    SETQFSOME,EXIT,QDELLOOP  We are done now                         
         SET   SETQFPUBLIC         Nothing found, look through pubs             
         B     QDELLOOP                                                         
         END                                                                    
         END                                                                    
*                                                                               
         LR    R2,R1                                                            
         MOVEL SVIB,@R2            Save SVIB (for next SERVFIND)                
*                                                                               
         IF    SVIBFPUBLIC,BEGIN   Public server...                             
         IF ^CPSFSPR,'ERROR "Public server operation is privileged."'           
         END                                                                    
*                                                                               
         SET   SETQFSOME           We found something                           
*                                                                               
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
*-                                                                              
*-       Delete a busy server.                                                  
*-                                                                              
         IF    SVIB#BUSY,BEGIN     Busy...                                      
         TSEG  ' is busy (will delete server when current'                      
         TSEG  ' operation has completed).',,CR                                 
*                                                                               
         SET   SVIBFCLOSING        Close connection when done                   
         END                                                                    
*-                                                                              
*-       Delete an idle server.                                                 
*-                                                                              
         ELSE  BEGIN               Delete server...                             
         TSEG  ' will be deleted.',,CR                                          
*                                                                               
         LR    R15,R2                                                           
         ACALL SERVDEL             Delete server entry                          
         END                                                                    
         END                                                                    
*                                                                               
         IF    ^SETQFSOME,BEGIN    No such server found...                      
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QSUPDATE -- Local routine to update this SVIB with the SET QUERY             
*    parameters specified.                                                      
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
QSUPDATE PROC                                                                   
         WITH  (SETQWA,R6)                                                      
*                                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),QUPDSERV,'Bad SVIB ptr'                         
*                                                                               
         IF    SVIBFPUBLIC,BEGIN   Public server...                             
         IF ^CPSFSPR,'ERROR "Public server operation is privileged."'           
         END                                                                    
*                                                                               
         CLEAR R4                  Flag: nothing updated                        
*-                                                                              
*-       Only update this information in the master copy.                       
*-                                                                              
         IF    SVIB#MASTER,BEGIN                                                
         IF    (SETQRDB,NZ),'MVC SVIBRDB,SETQRDB; LA R4,1'                      
         IF    (SETQRHOST,NZ),'MVC SVIBRHOST,SETQRHOST; LA R4,1'                
         IF    (SETQRPORT,NZ),'MVC SVIBRPORT,SETQRPORT; LA R4,1'                
         IF    (SETQRUSER,NZ),'MVC SVIBRUSER,SETQRUSER; LA R4,1'                
         IF    (SETQRPASS,NZ),'MVC SVIBRPASS,SETQRPASS; LA R4,1'                
         IF    (SETQMAX,NZ),'MVC SVIBMAX,SETQMAX; LA R4,1'                      
         END                                                                    
*-                                                                              
*-       Update server information.                                             
*-                                                                              
         IF    SETQFREUSE,'SET SVIBFREUSE; LA R4,1'                             
         IF    SETQFNOREUSE,'CLEAR SVIBFREUSE; LA R4,1'                         
         IF    SETQFTRACE,'SET SVIBFTRC; LA R4,1'                               
         IF    SETQFNOTRACE,'CLEAR SVIBFTRC; LA R4,1'                           
*                                                                               
         IF    SETQFTCLEAR,BEGIN   Clear the trace table...                     
         WHILE ('LT R1,SVIBTRCQH',NZ),BEGIN  Free trace entries...              
         LA    R15,SVIB                                                         
         ACALL FREESTRC            Free this entry                              
         END                                                                    
         LA    R4,1                                                             
         END                                                                    
*                                                                               
         IF    (R4,NZ),BEGIN       Something was updated...                     
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' information has been updated.',,CR                             
         SET   SETQFSOME           Something was updated                        
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       SET QUERY Options                                                      
*-                                                                              
SETQPRT  SCKW  AS,SSVAS,P                                                       
         SCKW  ON,SSVON,P                                                       
         SCKW  PORT,SSVPORT,(A,P,PI),99999                                      
         SCKW  USER,SSVUSER,(A,P)                                               
         SCKW  PASSWORD,SSVPASS,(A,P)                                           
         SCKW  CREATE,SSVCRE,A                                                  
         SCKW  DELETE,SSVDEL,A                                                  
         SCKW  PUBLIC,SSPUBLIC,A                                                
         SCKW  MAXSERVERS,SSMAX,(A,P,PI)                                        
         SCKW  REUSE,SSVREUSE,A                                                 
         SCKW  NOREUSE,SSVNOREU,A                                               
         SCKW  REUSABLE,SSVREUSE,A                                              
         SCKW  NOREUSABLE,SSVNOREU,A                                            
         SCKW  NOTRACE,SSVNTRC,A                                                
         SCKW  TRACE,SSVTRC,A                                                   
         SCKW  NOTRACE,SSVNTRC,A                                                
         SCKW  TCLEAR,SSVTCLR,A                                                 
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
SSVAS    PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         MVC   SETQRDB,CVBLANKS                                                 
         VCALL DEQUOTE                                                          
         LR    R15,R0                                                           
         CEIL  R15,L'SETQRDB                                                    
         MOVE  R15,SETQRDB,@R1                                                  
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SSVON    PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         MVC   SETQRHOST,CVBLANKS                                               
         VCALL DEQUOTE                                                          
         LR    R15,R0                                                           
         CEIL  R15,L'SETQRHOST                                                  
         MOVE  R15,SETQRHOST,@R1                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SSVPORT  PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         ST    R0,SETQRPORT                                                     
         PEND                                                                   
*                                                                               
SSVUSER  PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         MVC   SETQRUSER,CVBLANKS                                               
         VCALL DEQUOTE                                                          
         LR    R15,R0                                                           
         CEIL  R15,L'SETQRUSER                                                  
         MOVE  R15,SETQRUSER,@R1                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SSVPASS  PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         MVC   SETQRPASS,CVBLANKS                                               
         VCALL DEQUOTE                                                          
         LR    R15,R0                                                           
         CEIL  R15,L'SETQRPASS                                                  
         MOVE  R15,SETQRPASS,@R1                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SSVCRE   PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFCRE                                                         
         PEND                                                                   
*                                                                               
SSVDEL   PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFDEL                                                         
         PEND                                                                   
*                                                                               
SSPUBLIC PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFPUBLIC                                                      
         PEND                                                                   
*                                                                               
SSMAX    PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         ST    R0,SETQMAX                                                       
         PEND                                                                   
*                                                                               
SSVREUSE PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFREUSE,(SETQFNOREUSE,OFF)                                    
         PEND                                                                   
*                                                                               
SSVNOREU PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFNOREUSE,(SETQFREUSE,OFF)                                    
         PEND                                                                   
*                                                                               
SSVTRC   PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFTRACE,(SETQFNOTRACE,OFF)                                    
         PEND                                                                   
*                                                                               
SSVNTRC  PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFNOTRACE,(SETQFTRACE,OFF)                                    
         PEND                                                                   
*                                                                               
SSVTCLR  PROC                                                                   
         WITH  (SETQWA,R6)                                                      
         SET   SETQFTCLEAR                                                      
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'SHOW QUERY Command'                                             
*box                                                                            
*                                                                               
*  SHOWQRY -- SHOW QUERY Command.                                               
*                                                                               
SHOSWA   RECORD BEGIN                                                           
SHOSFLAG FLAG                                                                   
         FLAG  SHOSFDEBUG          - SHOW QUERY DEBUG                           
         FLAG  SHOSFTRACE          - SHOW QUERY TRACE                           
         FLAG  SHOSFALL            - SHOW QUERY ALL                             
         FLAG  SHOSFSOME           - We found some servers                      
*                                                                               
SHOSNO   DS    F                   Server number                                
*                                                                               
SHOSMLEN DS    H                   Match string length                          
SHOSMAT  DS    CL(L'SVIBNAME)      String to match                              
*                                                                               
SHOSVIB  DS    XL(L'SVIB)          Working SVIB                                 
         END                                                                    
*-                                                                              
SHOWQRY  XPROC SHOSWA                                                           
         LA    R6,SHOSWA                                                        
         WITH  (SHOSWA,R6)                                                      
         CLEAR SHOSWA                                                           
*-                                                                              
*-       Scan SHOW QUERY Options.                                               
*-                                                                              
         SCAN  SHOSPRT                                                          
*-                                                                              
*-       Show a specific server.                                                
*-                                                                              
         IF    ('LT R15,SHOSNO',NZ),BEGIN   Show it...                          
         ACALL SERVGET             Get SVIB ptr                                 
         IF    NZ,BEGIN                                                         
         LR    R5,R15              Save SVIB ptr                                
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Check authority if he wants to see the trace table.                    
*-                                                                              
         IF    SHOSFTRACE,BEGIN    Trace option..                               
         IF    (CPSGRP,NE,'G'),BEGIN  Disallow...                               
         IF    (CPSGRP,EQ,'AN'),EXIT  Allow AN for now                          
         ERROR 'TRACE option is privileged.'                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Display server information.                                            
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVDISP                                                         
*-                                                                              
*-       Add debugging information.                                             
*-                                                                              
         IF    SHOSFDEBUG,BEGIN    Debugging information...                     
         TSEG  'Debug info: SVIB at '                                           
         THEX  LA:SVIB,8                                                        
         TSEG  ', UNET at '                                                     
         THEX  L:SVIBUNET,8                                                     
         TCR                                                                    
         END                                                                    
*-                                                                              
*-       Display trace table.                                                   
*-                                                                              
         IF    SHOSFTRACE,BEGIN    TRACE...                                     
         LA    R15,SVIB                                                         
         ACALL SERVDTRC            Display trace table                          
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               No such server...                            
         TSEG  'Server '                                                        
         TNUM  L:SHOSNO                                                         
         TSEG  ' does not exist.'                                               
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       Show user servers.                                                     
*-                                                                              
         L     R0,CVUSTAB          Start of SVIT                                
         L     R1,CVUSENDP         End of SVIT                                  
         LCALL DISPSVIT            Show user servers                            
*-                                                                              
*-       Show public servers.                                                   
*-                                                                              
         L     R0,CVPSTAB          Start of SVIT                                
         L     R1,CVPSENDP         End of SVIT                                  
         LCALL DISPSVIT            Show user servers                            
*                                                                               
         IF    ^SHOSFSOME,BEGIN    Nada...                                      
         TSEG  'No query servers.'                                              
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       SHOW QUERY Options.                                                    
*-                                                                              
SHOSPRT  SCKW  LIKE,SHSLIKE,(A,P)                                               
         SCKW  DEBUG,SHSDEBUG,A                                                 
         SCKW  TRACE,SHSTRACE,A                                                 
         SCKW  ALL,SHSALL,A                                                     
         SCKW  ,SHSNUM,PI                                                       
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(SCNINVAL)                                                     
*-                                                                              
SHSDEBUG PROC                                                                   
         WITH  (SHOSWA,R6)                                                      
         SET   SHOSFDEBUG                                                       
         PEND                                                                   
*                                                                               
SHSTRACE PROC                                                                   
         WITH  (SHOSWA,R6)                                                      
         SET   SHOSFTRACE                                                       
         PEND                                                                   
*                                                                               
SHSALL   PROC                                                                   
         WITH  (SHOSWA,R6)                                                      
         SET   SHOSFALL                                                         
         PEND                                                                   
*                                                                               
SHSLIKE  PROC                                                                   
         WITH  (SHOSWA,R6)                                                      
         MVC   SHOSMAT,CVBLANKS                                                 
         LR    R15,R0                                                           
         CEIL  R15,L'SHOSMAT                                                    
         STH   R15,SHOSMLEN                                                     
         MOVE  R15,SHOSMAT,@R1                                                  
         L     R2,CVUPPTBL                                                      
         TEX   R15,'TR SHOSMAT,@R2'  Force caps                                 
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHSNUM   PROC                                                                   
         WITH  (SHOSWA,R6)                                                      
         ST    R0,SHOSNO           Save server number                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DISPSVIT -- Local routine to display server information.                     
*                                                                               
*    On entry:                                                                  
*      R0 - SVIT ptr                                                            
*      R1 - END of SVIT ptr                                                     
*      R6 - SHOSWA ptr (for SHOW QUERY formatting options)                      
*                                                                               
DSWA     RECORD BEGIN                                                           
DSSVITP  DS    F                   Start of SVIT ptr                            
DSENDP   DS    F                   End of SVIT ptr                              
*                                                                               
DSSERV   DS    CL(L'SVIB)          Server record                                
*                                                                               
DSCLCK   DS    D                   Working clock                                
         END                                                                    
*-                                                                              
DISPSVIT PROC  DSWA                                                             
         CLEAR DSWA                                                             
*                                                                               
         WITH  (SHOSWA,R6)                                                      
*                                                                               
         ST    R0,DSSVITP          Start of SVIT ptr                            
         ST    R1,DSENDP           End of SVIT ptr                              
*                                                                               
         LT    R5,DSSVITP          SVIT ptr                                     
         IF    Z,EXIT              No servers at all, scram                     
*-                                                                              
*-       Go through server table and display active entries.                    
*-                                                                              
         WHILE (R5,LT,DSENDP),BEGIN  Go through servers...                      
         IF    ('LT R4,@R5',NZ),BEGIN  Active server...                         
         WITH  (SVIB,R4)                                                        
*-                                                                              
*-       Only display private server we created unless ALL is                   
*-         specified.                                                           
*-                                                                              
         IF    (SVIBCRJCB,NZ),BEGIN  Private server...                          
         IF    (SVIBCRJCB,EQ,CVCURJCB),EXIT  Ours, fine                         
         IF    ^SHOSFALL,DSVNEXT   Skip this one                                
         END                                                                    
*-                                                                              
*-       Display server information.                                            
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVDISP                                                         
*-                                                                              
*-       Add debugging information.                                             
*-                                                                              
         IF    SHOSFDEBUG,BEGIN    Debugging information...                     
         TSEG  'Debug info: SVIB at '                                           
         THEX  LA:SVIB,8                                                        
         TSEG  ', UNET at '                                                     
         THEX  L:SVIBUNET,8                                                     
         TCR                                                                    
         END                                                                    
*-                                                                              
*-       Display trace table.                                                   
*-                                                                              
         IF    SHOSFTRACE,BEGIN    TRACE...                                     
         LA    R15,SVIB                                                         
         ACALL SERVDTRC            Display trace table                          
         END                                                                    
*                                                                               
         SET   SHOSFSOME           We displayed something                       
         END                                                                    
*                                                                               
DSVNEXT  LA    R5,@R5+4            Next SVIT entry                              
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'QUERY Command'                                                  
*box                                                                            
*                                                                               
*  QUERY -- QUERY Command.                                                      
*                                                                               
QWA      RECORD BEGIN                                                           
QTOKPTR  DS    2A                  QUERY token ptrs                             
*                                                                               
QFLAG    FLAG                                                                   
         FLAG  QFOPEN              - Network connection is open                 
         FLAG  QFABORT             - ABORT sent                                 
         FLAG  QFENDTAB            - End of table reached                       
*                                                                               
QUSG     DS    A                   Network output SEGCB ptr                     
*                                                                               
QSVIB    DS    XL(L'SVIB)          Server information                           
         END                                                                    
*-                                                                              
QUERY    XPROC QWA                                                              
         CLEAR QWA                                                              
         STM   R0,R1,QTOKPTR       Save scanned token loc, len                  
*                                                                               
         LA    R5,QSVIB                                                         
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Scan for server name.                                                  
*-                                                                              
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,Z),BEGIN        No options...                                
*-                                                                              
*-       This special hack is to try and give a message which                   
*-         isn't too confusing when the user accidently types "Q"               
*-         with no other options as a command.                                  
*-                                                                              
         LM    R0,R1,QTOKPTR       Pick up original verb                        
         IF    (R0,EQ,1),BEGIN     One character verb ("Q" form)...             
         TSEG  (R1),(R0)                                                        
         TSEG  ': unrecognized command '                                        
         TSEG  '(or QUERY command with a missing server name).'                 
         END                                                                    
*                                                                               
         ELSE  'TSEG "Missing server name on the QUERY command."'               
*                                                                               
         ERROR                                                                  
         END                                                                    
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVSETPROTO        Set server name                              
*-                                                                              
*-       If server number was specified then we are sending a query             
*-         to a server which we have previously opened.                         
*-                                                                              
         IF    (SVIBNAME,Z),BEGIN  No server name specified...                  
         L     R15,SVIBNO          Server number                                
         ACALL SERVGET             Get SVIB ptr                                 
         IF    Z,BEGIN             No such server...                            
         TSEG  'Server '                                                        
         TNUM  L:SVIBNO                                                         
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LR    R5,R15              SVIB ptr                                     
*-                                                                              
*-       Make sure server has previously been opened.                           
*-                                                                              
         IF    ^SVIB#BUSY,BEGIN    Error of some sort...                        
         IF    ((SVIBCRJCB,NZ),AND,(SVIBCRJCB,NE,CVCURJCB)),BEGIN               
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' is not yours.'                                                 
         END                                                                    
*                                                                               
         IF    SVIB#MASTER,BEGIN   Prototype definition...                      
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' is a definition, not a server.'                                
         END                                                                    
*                                                                               
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' has not been previously opened.'                               
         END                                                                    
*-                                                                              
*-       Make sure server is connected to us and not someone else.              
*-                                                                              
         IF    (SVIBJCBP,NE,CVCURJCB),BEGIN  Not our server...                  
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' is already busy with line '                                    
         L     R2,SVIBJCBP                                                      
         WITH  (JCB,R2)                                                         
         TNUM  LH:JCBSEQ                                                        
         TSEG  ' '                                                              
         TSEG  JCBGRP                                                           
         TSEG  '.'                                                              
         TSEG  JCBUSER                                                          
         ERROR '.'                                                              
         END                                                                    
         END                                                                    
*-                                                                              
*-       If a server name was given then open the connection                    
*-         and set a flag so we will close it when we are done.                 
*-                                                                              
         ELSE  BEGIN               Server name specified...                     
         LA    R1,SVIB             SVIBNAME or SVIBNO filled in                 
         ACALL SERVOPEN            Open user server                             
         IF    NZ,BEGIN            Can't open...                                
         BM    QNETERR             Network error, scram                         
         IF    (R15,EQ,1),QERRSEG  Query error, call it quits                   
*                                                                               
         IF    ('LT R15,SVIBNO',NZ),'TNUM (R15)'                                
         ELSE  'TSEG SVIBNAME,,T'                                               
         TSEG  ' is an unknown server'                                          
         TSEG  ' (type SHOW QUERY for a'                                        
         ERROR ' list of servers).'                                             
         END                                                                    
*                                                                               
         SET   QFOPEN              Connection is now open                       
         LR    R5,R1               Set our SVIB ptr                             
         END                                                                    
*-                                                                              
*-       Connection to a query server has been established,                     
*-         now send the command and fetch the responses.                        
*-                                                                              
         CLEAR SVIBLOCALF                                                       
*                                                                               
         SCINFO                                                                 
         VCALL LRTRIM                                                           
         LA    R15,SVIB                                                         
         ACALL SERVCMDL            Send command                                 
         BNZ   QNETERR             Trouble, scram                               
*                                                                               
         SET   SVIBFBUFFER         Buffer responses                             
*-                                                                              
*-       Process responses and format them as table(s).                         
*-                                                                              
         LOOP  BEGIN                                                            
         LA    R15,SVIB                                                         
         ACALL SERVGETL            Get first/next response line                 
         BNZ   QNETERR             Trouble, scram                               
*                                                                               
         LA    R15,SVIB                                                         
         SETMSG (R1),(R0)                                                       
         LA    R2,CPSEG                                                         
         ACALL SERVFMTL            Format response line                         
*                                                                               
         IF    (@R1,EQ,'2'),EXIT   Normal completion                            
         IF    (@R1,EQ,'4'),EXIT   Error completion                             
         IF    (@R1,EQ,'5'),EXIT   Error completion                             
         END                                                                    
*-                                                                              
*-       Close server connection.                                               
*-                                                                              
         IF    QFOPEN,BEGIN        If we opened connection...                   
         L     R15,SVIBNO          Server number                                
         ACALL SERVCLS             Close connection                             
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*-                                                                              
*-       Error handling.                                                        
*-                                                                              
QNETERR  IF    (@R1,EQ,'RESET'),BEGIN                                           
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' is not currently accepting requests.',,CR                      
         B     QERR                                                             
         END                                                                    
*                                                                               
         IF    (@R1,EQ,'TIMEOUT'),BEGIN                                         
         TSEG  'No response from server '                                       
         TSEG  SVIBNAME,,T                                                      
         TSEG  '.',,CR                                                          
         B     QERR                                                             
         END                                                                    
*                                                                               
         XPUSH R0,R1                                                            
         TSEG  'Can''t connect to server '                                      
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ': '                                                             
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),,CR       Reason                                       
         B     QERR                                                             
*                                                                               
QERRSEG  XPUSH R0,R1                                                            
         TSEG  'Query error completion: '                                       
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  (R1),(R0),CR        Write out error message                      
         XPOP  R0,R1                                                            
*                                                                               
QERR     IF    CPSTVIRT,BEGIN      Be persistent if virtual term...             
         TSEG  '(Will retry server operation in 30 seconds.)',,WR               
         BNZ   CVABORT                                                          
*                                                                               
         LA    R15,30*100                                                       
         VCALL WAITER              Wait for 30 seconds                          
         B     CVABORT             ???????                                      
*******  B     ???????                                                          
         END                                                                    
         B     CVABORT                                                          
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         SEGDEF DUMMY                                                           
         TITLE 'QUERY Function'                                                 
*box                                                                            
*                                                                               
*  QUERYFC -- QUERY function call.  This is called by the QUERY                 
*    function; so execute a simple query on the server specified.               
*    All errors should be returned to the caller.                               
*                                                                               
*    On entry:                                                                  
*      R1,R0 - command text (loc, len)                                          
*      R3,R2 - server name (loc, len)                                           
*      R5,R4 - response buffer area                                             
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok;    R1,R0 = first data line loc, len                              
*      nz- error; R1,R0 = error message loc, len                                
*                                                                               
QFCWA    RECORD BEGIN                                                           
QFCFLAG  FLAG                                                                   
         FLAG  QFCFOPEN            - Server has been opened                     
QFCCMDP  DS    2A                  Query command ptrs                           
QFCBUFP  DS    2A                  Buffer area ptrs                             
QFCSVIB  DS    XL(L'SVIB)          Working SVIB                                 
         END                                                                    
*-                                                                              
QUERYFC  XPROC QFCWA                                                            
         CLEAR QFCWA                                                            
         STM   R0,R1,QFCCMDP       Save working command ptrs                    
         STM   R4,R5,QFCBUFP       Save response buffer ptrs                    
*-                                                                              
*-       Parse server name or number.                                           
*-                                                                              
         LA    R5,QFCSVIB                                                       
         WITH  (SVIB,R5)                                                        
*                                                                               
         LA    R15,SVIB                                                         
         SETMSG (R3),(R2)                                                       
         ACALL SERVSETPROTO        Build working SVIB                           
         IF    NZ,BEGIN            Bad server name/number...                    
         TCLEAR ,                  Get rid of error text                        
         SETMSG 'BAD SERVER NAME'                                               
         B     QFCERR                                                           
         END                                                                    
*-                                                                              
*-       If server number was specified then we are sending a query             
*-         to a server which we have previously opened.                         
*-                                                                              
         IF    (SVIBNAME,Z),BEGIN  No server name specified...                  
         L     R15,SVIBNO          Server number                                
         ACALL SERVGET             Get SVIB ptr                                 
         IF    Z,BEGIN             No such server...                            
         SETMSG 'NO SUCH SERVER'                                                
         B     QFCERR                                                           
         END                                                                    
*                                                                               
         LR    R5,R15              SVIB ptr                                     
*-                                                                              
*-       Make sure server has previously been opened.                           
*-                                                                              
         IF    ^SVIB#BUSY,BEGIN    Error of some sort...                        
         SETMSG 'SERVER NOT PREVIOUSLY OPENED'                                  
         B     QFCERR                                                           
         END                                                                    
*-                                                                              
*-       Make sure server is connected to us and not someone else.              
*-                                                                              
         IF    (SVIBJCBP,NE,CVCURJCB),BEGIN  Not our server...                  
         SETMSG 'SERVER NOT YOURS'                                              
         B     QFCERR                                                           
         END                                                                    
         END                                                                    
*-                                                                              
*-       If a server name was given then open the connection                    
*-         and set a flag so we will close it when we are done.                 
*-                                                                              
         ELSE  BEGIN               Server name specified...                     
         LA    R1,SVIB             SVIBNAME or SVIBNO filled in                 
         ACALL SERVOPEN            Open user server                             
         BNZ   QFCERR              Error, scram                                 
*                                                                               
         SET   QFCFOPEN            Connection is now open                       
         LR    R5,R1               Set our SVIB ptr                             
         END                                                                    
*-                                                                              
*-       Connection to a query server has been established,                     
*-         now send the command and fetch the responses.                        
*-                                                                              
         CLEAR SVIBLOCALF                                                       
*                                                                               
         LM    R0,R1,QFCCMDP       Get command ptrs                             
         LA    R15,SVIB                                                         
         ACALL SERVCMDL            Send command                                 
         BNZ   QFCERR              Trouble, scram                               
*-                                                                              
*-       Keep processing responses until we get a data line.                    
*-                                                                              
         LOOP  BEGIN                                                            
         LA    R15,SVIB                                                         
         ACALL SERVGETL            Get first/next response line                 
         BNZ   QFCERR              Trouble, scram                               
*                                                                               
         IF    (@R1,EQ,$TAB),QFCOK  We have a data line, scram                  
*                                                                               
         IF    (@R1,EQ,'2'),'CLEAR R1,R0; B QFCOK'  Normal                      
         IF    (@R1,EQ,'4'),EXIT   Error completion                             
         IF    (@R1,EQ,'5'),EXIT   Error completion                             
         END                                                                    
*-                                                                              
*-       Error completion.                                                      
*-                                                                              
QFCERR   LA    R15,4                                                            
         B     QFCDONE                                                          
*-                                                                              
*-       Normal completion.                                                     
*-                                                                              
QFCOK    CLEAR R15                 A-ok                                         
QFCDONE  LM    R2,R3,QFCBUFP       Response buffer loc, len                     
         LR    R14,R0                                                           
         CEIL  R14,R2              Not too long now                             
         MOVE  R14,@R3,@R1         Copy response                                
         SETMSG (R3),(R0)           Our response (now in caller's buf)          
*                                                                               
         XPUSH R15,R1              Save return code info                        
*-                                                                              
*-       Close server connection.                                               
*-                                                                              
         IF    QFCFOPEN,BEGIN      If we opened connection...                   
         L     R15,SVIBNO          Server number                                
         ACALL SERVCLS             Close connection                             
         END                                                                    
*                                                                               
         XPOP  R15,R1                                                           
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         TITLE 'QOPEN Command'                                                  
*box                                                                            
*                                                                               
*  QOPEN -- QOPEN Command.                                                      
*                                                                               
QOWA     RECORD BEGIN                                                           
QOSVIB   DS    XL(L'SVIB)          Server information                           
         END                                                                    
*-                                                                              
QOPEN    XPROC QOWA                                                             
         CLEAR QOWA                                                             
*                                                                               
         LA    R5,QOSVIB                                                        
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Scan for server name.                                                  
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVSCANPROTO       Scan server name                             
*                                                                               
         IF    (SVIBNAME,Z),BEGIN  Don't allow servno here...                   
         ERROR 'You must QOPEN server by name (not number).'                    
         END                                                                    
*-                                                                              
*-       Scan QOPEN options.                                                    
*-                                                                              
         SCAN  QOPENPRT            QOPEN options                                
         SCANCHK                                                                
*-                                                                              
*-       Try opening server.                                                    
*-                                                                              
         LA    R1,SVIB             SVIBNAME or SVIBNO filled in                 
         ACALL SERVOPEN            Open user server                             
         IF    NZ,BEGIN            Can't open...                                
         BM    SONETERR            Network error, scram                         
         IF    (R15,EQ,1),SOERRSEG  Query error, call it quits                  
*                                                                               
         IF    ('LT R15,SVIBNO',NZ),'TNUM (R15)'                                
         ELSE  'TSEG SVIBNAME,,T'                                               
         TSEG  ' is an unknown server'                                          
         TSEG  ' (use the SHOW QUERY command for a'                             
         ERROR ' list of servers).'                                             
         END                                                                    
*                                                                               
         LR    R5,R1               Set our SVIB ptr                             
*-                                                                              
*-       Save server number in our active query queue.                          
*-                                                                              
         LA    R0,L'QINFO                                                       
         VCALL GETCORE             Get room for QINFO                           
         WITH  (QINFO,R1),BEGIN                                                 
         CLEAR QINFO                                                            
         MVC   QINFLINK,CPQINFOQ   Link everything to us                        
         MVC   QINFNO,SVIBNO       Save server number                           
         ST    R1,CPQINFOQ         We are the new queue head                    
         END                                                                    
*-                                                                              
*-       Connection to a query server has been established.                     
*-                                                                              
         TSEG  'Connection to server '                                          
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' has been established.'                                         
         TCR                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
SONETERR IF    (@R1,EQ,'RESET'),BEGIN                                           
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' is not currently accepting requests.',,CR                      
         LA    R15,=CL8'NOSERVER'                                               
         B     SOERR                                                            
         END                                                                    
*                                                                               
         IF    (@R1,EQ,'TIMEOUT'),BEGIN                                         
         TSEG  'No response from server '                                       
         TSEG  SVIBNAME,,T                                                      
         TSEG  '.',,CR                                                          
         LA    R15,=CL8'TIMEOUT'                                                
         B     SOERR                                                            
         END                                                                    
*                                                                               
         XPUSH R0,R1                                                            
         TSEG  'Can''t connect to server '                                      
         TSEG  SVIBNAME,,T                                                      
         TSEG  ': '                                                             
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),,CR       Reason                                       
         LA    R15,=CL8'NETERROR'                                               
         B     SOERR                                                            
*                                                                               
SOERRSEG XPUSH R0,R1                                                            
         TSEG  'Error completion: '                                             
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         TSEG  '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  @R1,3                                                            
         TSEG  ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         ERROR (R1),(R0)                                                        
*                                                                               
SOERR    IF    CPSTVIRT,BEGIN      Be persistent if virtual term...             
         TSEG  '(Will retry server operation in 30 seconds.)',,WR               
         BNZ   CVABORT                                                          
*                                                                               
         LA    R15,30*100                                                       
         VCALL WAITER              Wait for 30 seconds                          
         B     CVABORT             ???????                                      
*******  B     ???????                                                          
         END                                                                    
*                                                                               
         MVC   CPERRID,@R15        Set error id                                 
         B     CVERROR                                                          
         PEND                                                                   
*-                                                                              
*-       QOPEN Options                                                          
*-                                                                              
QOPENPRT SCKW ,V(SCNINVAL)                                                      
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         TITLE 'QSEND Command'                                                  
*box                                                                            
*                                                                               
*  QSEND -- QSEND Command.                                                      
*                                                                               
QSEND    XPROC                                                                  
*-                                                                              
*-       Scan for server number.                                                
*-                                                                              
         ACALL SERVSCANSVIB        Get SVIB ptr                                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         LA    R15,SVIB                                                         
         SCINFO                                                                 
         VCALL LRTRIM                                                           
         ACALL SERVCMDL            Send command line                            
         BZ    CVNEXT              Normal completion, scram                     
*-                                                                              
*-       Error completion.                                                      
*-                                                                              
QSERRSEG XPUSH R0,R1                                                            
         TSEG  'Error completion: '                                             
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         TSEG  '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  @R1,3                                                            
         TSEG  ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
*debug:  IF    (@R1,EQ,':'),'CLEAR CVFNTRC'                                     
         ERROR (R1),(R0)           Write out error message                      
         PEND                                                                   
         TITLE 'QEXEC Command'                                                  
*box                                                                            
*                                                                               
*  QEXEC -- QEXEC Command.                                                      
*                                                                               
QEXEC    XPROC                                                                  
*-                                                                              
*-       Scan for server number.                                                
*-                                                                              
         ACALL SERVSCANSVIB        Get SVIB ptr                                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         LA    R15,SVIB                                                         
         SCINFO                                                                 
         VCALL LRTRIM                                                           
*-                                                                              
*-       Send the remainder of the command line to the server.                  
*-                                                                              
         IF    (R0,POS),BEGIN                                                   
         ACALL SERVCMDL            Send command line                            
         BNZ   QXERRSEG            Trouble, scram                               
         END                                                                    
*-                                                                              
*-       Start fetching rows but don't wait for a response.                     
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVFET             Start fetching response                      
         BNZ   QXERRSEG            Trouble, scram                               
         B     CVNEXT              All done                                     
*-                                                                              
*-       Error completion.                                                      
*-                                                                              
QXERRSEG XPUSH R0,R1                                                            
         TSEG  'Error completion: '                                             
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         TSEG  '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  @R1,3                                                            
         TSEG  ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         ERROR (R1),(R0)           Write out error message                      
         PEND                                                                   
         TITLE 'QRUN Command'                                                   
*box                                                                            
*                                                                               
*  QRUN -- QRUN Command.                                                        
*                                                                               
QRWA     RECORD BEGIN                                                           
QRSNO    DS    F                   Server number                                
QRSVIBP  DS    A                   SVIB ptr                                     
QRUSG    DS    A                   Network output SEGCB ptr                     
*                                                                               
QRLCNT   DS    F                   Working line count                           
         END                                                                    
*-                                                                              
QRUN     XPROC QRWA                                                             
         LA    R6,QRWA                                                          
         WITH  (QRWA,R6)                                                        
         CLEAR QRWA                                                             
*                                                                               
         ST    R6,CPWK4            Save QRWA ptr (for QRUNRTN)                  
*-                                                                              
*-       Scan line range.                                                       
*-                                                                              
         VCALL SCNEXFR             Allow active/exec/from dsname                
         SET   CPLFLG1.CPFALL      Default is ALL                               
*                                                                               
         VCALL NDETRNG             Scan range                                   
*-                                                                              
*-       Scan QRUN options.                                                     
*-                                                                              
         IF    CPFRDEXT,BEGIN      With dsn options                             
         VCALL MKFFINFO                                                         
         SCAN QRPRTX                                                            
         END                                                                    
         ELSE  BEGIN               Without dsn options                          
         SCAN QRPRT                                                             
         END                                                                    
         SCANCHK                                                                
*-                                                                              
*-       Get SVIB ptr from server number.                                       
*-                                                                              
         LT    R15,QRSNO           Server number                                
         IF    Z,BEGIN             Use default server...                        
         LT    R15,CPQINFOQ        Last query info entry (or zero)              
         IF    NZ,'L R15,QINFNO-QINFO(,R15)'  Current server number             
         IF    (R15,Z),BEGIN       No connection...                             
         ERROR 'There is no current server connection.'                         
         END                                                                    
         ST    R15,QRSNO           Save server number                           
         END                                                                    
*                                                                               
         L     R15,QRSNO           Server number                                
         ACALL SERVGET             Get SVIB ptr from server number              
         IF    Z,BEGIN             No such server...                            
         TSEG  'Server '                                                        
         TNUM  L:QRSNO                                                          
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         ST    R5,QRSVIBP          Save our SVIB ptr                            
*                                                                               
         L     R15,SVIBUNET                                                     
         WITH  (UNET,R15),'LA R1,UNETOUTSG'                                     
         ST    R1,QRUSG            Save output SEGCB ptr                        
         SEGDEF L:QRUSG                                                         
*-                                                                              
*-       Send DATA command if this is the first line of data.                   
*-                                                                              
         IF    ^SVIBSFDATA,BEGIN   Data...                                      
         SEG   'DATA'                                                           
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'354'          Wait for intermediate response               
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get "go ahead" response                      
         BNZ   QRERRSEG                                                         
*                                                                               
         SET   SVIBSFDATA          DATA command has been sent                   
         END                                                                    
*-                                                                              
*-       Send range of lines.                                                   
*-                                                                              
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         END                                                                    
*                                                                               
         IF    CPFRDEXE,BEGIN  Exec...                                          
         RDRNG QRUNRTN,(LEXATRTN+DESRTRN+LOCATRTN+UNPRST+DESABORT)              
         END                                                                    
*                                                                               
         ELSEIF CPFRDEXT,BEGIN  External...                                     
         RDRNG QRUNRTN,(LXCATRTN+DESRTRN+LOCATRTN+UNPRST+DESABORT)              
         END                                                                    
*                                                                               
         ELSE  'RDRNG QRUNRTN,(DESRTRN+LOCATRTN+UNPRST+DESABORT)'               
*                                                                               
         SEGWR ,                   Force lines to be sent                       
*-                                                                              
*-       Normal completion.                                                     
*-                                                                              
         IF    (QRLCNT,NZ),BEGIN   Status message...                            
         TNUM  L:QRLCNT                                                         
         SETMSG ' lines'                                                        
         IF    (QRLCNT,EQ,1),'DECR R0'  lines/line                              
         TSEG  (R1),(R0)                                                        
         TSEG  ' sent to server '                                               
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*-                                                                              
*-       Error completion.                                                      
*-                                                                              
QRERRSEG XPUSH R0,R1                                                            
         TSEG  'Error completion: '                                             
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         TSEG  '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  @R1,3                                                            
         TSEG  ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         ERROR (R1),(R0)           Write out error message                      
         PEND                                                                   
*-                                                                              
*-       QRUN Options.                                                          
*-                                                                              
QRPRTX   SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(FNAMPSHF),PUSH                                                
QRPRT    SCKW  SERVER,QRSERV,(A,P)                                              
         SCKW  ,V(COLSPSH),PUSH    Allow COLUMNS=n[/m]                          
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(SCNINVAL)                                                     
*-                                                                              
QRSERV   PROC                                                                   
         WITH  (QRWA,R6)                                                        
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),'CLEAR R15'                         
         ELSE  BEGIN               Number...                                    
         DTB   (R1),(R0)           Convert to number                            
         BNZ   CVNVALID            Not a server number                          
         END                                                                    
         ST    R15,QRSNO           Save server number                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QRUNRTN -- Range processor co-routine to send the lines selected             
*    to the query server.                                                       
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
QRUNRTN  PROC                                                                   
         L     R6,CPWK4            QRWA ptr                                     
         WITH  (QRWA,R6)                                                        
*-                                                                              
*-       Only select specified columns (see COLSPSH in OPT).                    
*-                                                                              
         CEIL  R0,CPLCOL           Don't go past last col                       
         AH    R1,CPFCOL                                                        
         SH    R0,CPFCOL                                                        
         IF    NEG,'CLEAR R0'                                                   
*                                                                               
         SEG   (R1),(R0)           Write line                                   
         SEGCR                                                                  
*                                                                               
         INCR  R15,QRLCNT          Count number of lines sent                   
*-                                                                              
*-       Check for incoming responses from the server (this normally            
*-         should not happen, since the server is supposed to wait              
*-         until we are done with the input before complaining).                
*-                                                                              
         L     R15,QRSVIBP         Get SVIB ptr                                 
         ACALL SERVCHKL            Check for incoming responses                 
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'QFETCH Command'                                                 
*box                                                                            
*                                                                               
*  QFETCH -- QFETCH Command.                                                    
*                                                                               
QFETWA   RECORD BEGIN                                                           
QFFLAG   FLAG                                                                   
         FLAG  QFFDISPLAY          - DISPLAY option                             
         FLAG  QFFAPPEND           - APPEND option                              
         FLAG  QFFUNFORMATTED      - UNFORMATTED option                         
*                                                                               
QFRC     DS    CL3                 Return code information                      
QFNLINES DS    F                   Number of lines fetched                      
QFNTBLS  DS    F                   Number of tables fetched                     
*                                                                               
QFWRAP   DS    F                   WRAP length                                  
QFTOLNO  DS    F                   TO line-number                               
QFBYLNO  DS    F                   BY delta                                     
QFLASTLN DS    F                   Last line-number added                       
*                                                                               
QFSEGCB  SEGCB ,                   Output SEGCB                                 
QFSEGBUF DS    XL512               Working output buffer                        
         END                                                                    
*-                                                                              
QFETCH   XPROC QFETWA                                                           
         LA    R6,QFETWA                                                        
         WITH  (QFETWA,R6)                                                      
         CLEAR QFETWA                                                           
*                                                                               
         MVC   QFTOLNO,=F'-1'      Calculate TO lineno                          
         MVC   QFBYLNO,CPDELTA     Use default DELTA                            
         MVC   QFLASTLN,=F'-1'     Last lineno added                            
         MVC   QFWRAP,=F'230'      Default wrap length                          
*-                                                                              
*-       Scan for server number.                                                
*-                                                                              
         ACALL SERVSCANSVIB        Get SVIB ptr                                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Scan QFETCH options.                                                   
*-                                                                              
         SCAN  QFETPRT             QFETCH options                               
         SCANCHK                                                                
*-                                                                              
*-       First clear the Active file.                                           
*-                                                                              
         IF    (^QFFDISPLAY,AND,^QFFAPPEND),BEGIN                               
         VCALL CLRTEXT             Clear/Keep                                   
         MVC   CPANAME,=CL8'QFETCH'   Set Active name                           
         MVC   CPATITL,=CL(L'CPATITL)'Query results'                            
         SET   CPLFLG5.CPFNLST     (In case LSLNMSGA abort)                     
         END                                                                    
*-                                                                              
*-       Calculate starting line-number and delta.                              
*-                                                                              
         IF    ^QFFDISPLAY,BEGIN   Not DISPLAY...                               
         IF    ('LT R1,QFTOLNO',NEG),BEGIN  Figure it out...                    
         L     R0,CPALLNO          Last line-number                             
         L     R1,QFBYLNO          Delta                                        
         VCALL ADDER               Get next lineno                              
         IF    NEG,BEGIN           Trouble...                                   
         ABORT 'Bad line-number, can''t APPEND.'                                
         END                                                                    
         ST    R0,QFTOLNO          Save TO lineno                               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Initialize our output seg buffer.                                      
*-                                                                              
         SEGINIT QFSEGBUF,,QFSEGCB,RTN=QFETSEG                                  
         ST    R6,QFSEGCBUSR       Save QFETWA ptr in SEGCB                     
*-                                                                              
*-       Display the response lines.                                            
*-                                                                              
         CLEAR SVIBLOCALF          Reset local flags                            
         IF    ^QFFUNFORMATTED,'SET SVIBFBUFFER'  Must buffer                   
*                                                                               
QFETLOOP LOOP  BEGIN                                                            
         LA    R15,SVIB                                                         
         ACALL SERVGETL            Get first/next response line                 
         IF    NZ,EXIT             Trouble, scram                               
*                                                                               
         IF    (@R1,EQ,'101'),'INCR R15,QFNTBLS'  Count table                   
*-                                                                              
*-       Output unformatted response line.                                      
*-                                                                              
         IF    QFFUNFORMATTED,BEGIN  Unformatted...                             
         XPUSH R0,R1                                                            
         LR    R3,R1                                                            
         LR    R2,R0                                                            
         WHILE (R2,GT,QFWRAP),BEGIN  Wrap long response lines...                
         SEG   @R3,L:QFWRAP        Add text segment                             
         SEGCR '\'                 Plus wrap indicator                          
         A     R3,QFWRAP                                                        
         S     R2,QFWRAP                                                        
         END                                                                    
*                                                                               
         SEG   (R3),(R2)           Unformatted response line                    
         SEGCR ';'                 End line with semicolon                      
         BNZ   CVABORT             Trouble, scram                               
         XPOP  R0,R1                                                            
         END                                                                    
*-                                                                              
*-       Output formatted response line.                                        
*-                                                                              
         ELSE  BEGIN               Formatted...                                 
         LA    R15,SVIB                                                         
         SETMSG (R1),(R0)                                                       
         LA    R2,QFSEGCB                                                       
         ACALL SERVFMTL            Format response line                         
         END                                                                    
*-                                                                              
*-       Check for completion of response stream.                               
*-                                                                              
         IF    (@R1,EQ,'2'),EXIT                                                
         IF    (@R1,EQ,'4'),EXIT                                                
         IF    (@R1,EQ,'5'),EXIT                                                
         END                                                                    
*-                                                                              
*-       Write out completion message.                                          
*-                                                                              
         IF    (@R1,EQ,'2'),BEGIN  Normal completion...                         
         IF    ^CPFTYPED,CVNEXT    Non-typed command, scram                     
         END                                                                    
*                                                                               
         XPUSH R0,R1                                                            
         IF    (@R1,EQ,'2'),'TSEG "Normal completion"'                          
         ELSE  'TSEG "Error completion"'                                        
         XPOP  R0,R1                                                            
*                                                                               
         IF    ((@R1,EQ,'2'),OR,(@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN             
         LA    R1,@R1+4            Skip past error number                       
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         VCALL LRTRIM                                                           
         IF    (R0,POS),BEGIN      Write out any message text...                
         XPUSH R0,R1                                                            
         TSEG  ': '                                                             
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         END                                                                    
         TCR                                                                    
*-                                                                              
*-       And finally the summary line.                                          
*-                                                                              
         TNUM  L:QFNTBLS                                                        
         SETMSG ' tables'                                                       
         IF    (QFNTBLS,EQ,1),'DECR R0'                                         
         TSEG  (R1),(R0)                                                        
         TSEG  ' fetched'                                                       
*                                                                               
         IF    (QFLASTLN,NE,-1),BEGIN                                           
         TSEG  ', '                                                             
         L     R0,QFLASTLN                                                      
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  ' is the last line'                                              
         END                                                                    
*                                                                               
         TSEG  '.'                                                              
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       QFETCH Options                                                         
*-                                                                              
QFETPRT  SCKW  DISPLAY,QFETDISP,A                                               
         SCKW  UNFORMATTED,QFETUNFM,A                                           
         SCKW  FORMATTED,QFETFMT,A                                              
         SCKW  WRAP,QFETWRAP,(A,P,PI),234                                       
         SCKW  APPEND,QFETAPP,A                                                 
         SCKW  TO,QFETTO,V(MPSYMLN)                                             
         SCKW  BY,QFETBY,(P,LN)                                                 
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(SCNINVAL)                                                     
*-                                                                              
QFETDISP PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         SET   QFFDISPLAY                                                       
         PEND                                                                   
*                                                                               
QFETUNFM PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         SET   QFFUNFORMATTED                                                   
         PEND                                                                   
*                                                                               
QFETFMT  PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         CLEAR QFFUNFORMATTED                                                   
         PEND                                                                   
*                                                                               
QFETAPP  PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         SET   QFFAPPEND                                                        
         PEND                                                                   
*                                                                               
QFETTO   PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         ST    R0,QFTOLNO                                                       
         PEND                                                                   
*                                                                               
QFETBY   PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         ST    R0,QFBYLNO                                                       
         PEND                                                                   
*                                                                               
QFETWRAP PROC                                                                   
         WITH  (QFETWA,R6)                                                      
         ST    R0,QFWRAP                                                        
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QFETSEG -- Routine to write the contents of our seg buffer to                
*    the Active file (or to the terminal if DISPLAY was specified).             
*                                                                               
*    On entry:                                                                  
*      R15 - SEGCB ptr                                                          
*                                                                               
QFETSEG  PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SEGCB,R5)                                                       
*                                                                               
         L     R6,SEGCBUSR         QFETWA ptr                                   
         WITH  (QFETWA,R6)                                                      
*-                                                                              
*-       SEGCR/SEGWR.                                                           
*-                                                                              
         IF    (SEGCBCR,OR,SEGCBWR),BEGIN  Write a line...                      
*-                                                                              
*-       Display line to terminal.                                              
*-                                                                              
         IF    QFFDISPLAY,BEGIN    DISPLAY...                                   
         TSEG  (R1),(R0),CR                                                     
         BNZ   CVABORT                                                          
         END                                                                    
*-                                                                              
*-       Add line to the Active file.                                           
*-                                                                              
         ELSE  BEGIN               Add to the Active file...                    
         L     R15,QFTOLNO         Get TO lineno                                
         SETMSG (R1),(R0)           Line text                                   
         LA    R2,=C'I...'         Insert only                                  
         VCALL PUTLINEX            Add line to the Active file                  
         IF    NZ,BEGIN            Can't insert...                              
         TSEG  'Line '                                                          
         L     R0,QFTOLNO                                                       
         VCALL CVEXNO              Lineno                                       
         TSEG  (R1),(R0)                                                        
         ERROR ' already in file.'                                              
         END                                                                    
*                                                                               
         L     R15,QFTOLNO         Line-number we just added                    
         ST    R15,QFLASTLN        Save lineno of line just added               
         AL    R15,QFBYLNO         Add delta                                    
         ST    R15,QFTOLNO         Save next line-number to use                 
         END                                                                    
*                                                                               
         CLEAR SEGCBLENF,SEGCBMRKF  Reset seg buffer                            
         END                                                                    
*                                                                               
         PEND                                                                   
         TITLE 'QFETLINE Routine (for SYSCALL QUERY_FETCH)'                     
*box                                                                            
*                                                                               
*  QFETLINE -- Routine to fetch the next response line from the                 
*    server.  We are called by the QUERY_FETCH SYSCALL routine.                 
*                                                                               
*      On entry:                                                                
*        R15 - server number                                                    
*                                                                               
*      On exit, R15 (and cc):                                                   
*        0 - OK; R1,R0 - response line loc, len                                 
*        4 - error; R1,R0 - error msg                                           
*                                                                               
QFETLINE XPROC                                                                  
         ACALL SERVGET             Get SVIB ptr from server no                  
         IF    Z,'SETMSG "NO SUCH SERVER"; LA R15,4; B QFLDONE'                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         IF    (SVIBCRJCB,NZ),BEGIN  Private server...                          
         IF    (SVIBCRJCB,EQ,CVCURJCB),EXIT                                     
         SETMSG 'NOT YOUR SERVER'                                               
         LA    R15,4                                                            
         B     QFLDONE                                                          
         END                                                                    
*-                                                                              
*-       Get next response line from server.                                    
*-                                                                              
         CLEAR SVIBLOCALF          Reset local options                          
         LA    R15,SVIB                                                         
         ACALL SERVGETL            Get first/next response line                 
*                                                                               
QFLDONE  PRETURN (R0,R1)           Return response (or errmsg)                  
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'QCLOSE Command'                                                 
*box                                                                            
*                                                                               
*  QCLOSE -- QCLOSE Command.                                                    
*                                                                               
QCLWA    RECORD BEGIN                                                           
QCLSNO   DS    F                   Server number                                
QCLSNAME DS    CL(L'SVIBNAME)      Server name                                  
         END                                                                    
*-                                                                              
QCLOSE   XPROC QCLWA                                                            
*-                                                                              
*-       Scan for server number.                                                
*-                                                                              
         ACALL SERVSCANSVIB        Get SVIB ptr                                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         MVC   QCLSNO,SVIBNO       Save server number                           
         MVC   QCLSNAME,SVIBNAME   Save server name                             
*-                                                                              
*-       Remove us from our list of active queries.                             
*-                                                                              
         CLEAR R3                  No previous entry ptr                        
         LA    R4,CPQINFOQ-(QINFLINK-QINFO)  Dummy queue head                   
QCLINFO  LOOP  BEGIN                                                            
         WITH  (QINFO,R4)                                                       
         WHILE ('LT R4,QINFLINK',NZ)                                            
         IF    (QCLSNO,EQ,QINFNO),BEGIN  We found us...                         
         IF    (R3,Z),'MVC CPQINFOQ,QINFLINK'  Dequeue                          
         ELSE  'MVC QINFLINK-QINFO(4,R3),QINFLINK'  Dequeue                     
         LA    R1,QINFO                                                         
         VCALL FREECORE            Release QINFO entry                          
         EXIT  QCLINFO                                                          
         END                                                                    
*                                                                               
         LR    R3,R4               Previous QINFO entry ptr                     
         END                                                                    
*-                                                                              
*-       Close our connection to this server.                                   
*-                                                                              
         L     R15,SVIBNO          Server number                                
         ACALL SERVCLS             Close connection                             
*-                                                                              
*-       Give status message.                                                   
*-                                                                              
         TSEG  'Connection to server '                                          
         TNUM  L:QCLSNO                                                         
         TSEG  ' '                                                              
         TSEG  QCLSNAME,,T                                                      
         SETMSG ' has been closed.'                                             
         B     CVNXTMSG                                                         
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         TITLE 'QRESET Command'                                                 
*box                                                                            
*                                                                               
*  QRESET -- QRESET Command.                                                    
*                                                                               
QRESWA   RECORD BEGIN                                                           
QRESSGP  DS    A                   Network SEGCB ptr                            
         END                                                                    
*-                                                                              
QRESET   XPROC QRESWA                                                           
*-                                                                              
*-       Scan for server number.                                                
*-                                                                              
         ACALL SERVSCANSVIB        Get SVIB ptr                                 
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         L     R15,SVIBUNET                                                     
         WITH  (UNET,R15),'LA R1,UNETOUTSG'                                     
         ST    R1,QRESSGP                                                       
         SEGDEF L:QRESSGP                                                       
*                                                                               
         SCAN  QRESPRT             RESET options                                
         SCANCHK                                                                
*-                                                                              
*-       If we are sending data then stop.                                      
*-                                                                              
         IF    SVIBSFDATA,BEGIN    Currently sending data...                    
         SEGCR '.'                 End of data                                  
         SEGWR                                                                  
*                                                                               
         SET   SVIBSFNORMAL        Back to normal                               
*                                                                               
         LA    R1,=C'...'                                                       
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Wait for completion response                 
*  (Don't bother checking for errors here.)                                     
         END                                                                    
*-                                                                              
*-       If we are fetching responses then send abort.                          
*-                                                                              
         IF    SVIBSFFETCH,BEGIN   Currently fetching responses...              
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors here.)                                     
         SET   SVIBSFNORMAL        Reset state to normal                        
         END                                                                    
*-                                                                              
*-       Don't attempt reset if we are in a funny state.                        
*-                                                                              
         IF    ^SVIBSFNORMAL,BEGIN  Trouble...                                  
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' can not be reset (state='                                      
         LC    R15,SVIBSFNORMAL                                                 
         TNUM  (R15)                                                            
         ERROR ').'                                                             
         END                                                                    
*-                                                                              
*-       Finally, send RESET to the server.                                     
*-                                                                              
         SEGCR 'RESET'             RESET server info                            
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'                                                       
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Wait for completion response                 
         BNZ   QRESERR             Trouble, scram                               
         B     CVNEXT                                                           
*-                                                                              
*-       Error completion.                                                      
*-                                                                              
QRESERR  XPUSH R0,R1                                                            
         TSEG  'Error completion: '                                             
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         TSEG  '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  @R1,3                                                            
         TSEG  ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         ERROR (R1),(R0)           Write out error message                      
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*-                                                                              
*-       QRESET Options.                                                        
*-                                                                              
QRESPRT  SCKW  ,V(SCNINVAL)                                                     
         TITLE 'QMONITOR Command'                                               
*box                                                                            
*                                                                               
*  QMONITOR -- QMONITOR Command.                                                
*                                                                               
QMONWA   RECORD BEGIN                                                           
QMONFLAG FLAG                                                                   
         FLAG  QMONFACT            - Add trace to end of Active file            
*                                                                               
QMONSNO  DS    F                   Server number we are monitoring              
*                                                                               
QMONINTX DS    4F                  Always re-display last four entries          
QMONTRCP EQU   QMONINTX,*-QMONINTX,C'X'                                         
*                                                                               
         DS    0F                                                               
QMONSVIB DS    XL(L'SVIB)          Working SVIB                                 
         END                                                                    
*-                                                                              
QMONITOR XPROC QMONWA                                                           
         LA    R6,QMONWA                                                        
         WITH  (QMONWA,R6)                                                      
         CLEAR QMONWA                                                           
*-                                                                              
*-       Scan for server name or number.                                        
*-                                                                              
         LA    R5,QMONSVIB                                                      
         WITH  (SVIB,R5)                                                        
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVSCANPROTO       Get SVIB ptr                                 
         BNZ   CVERROR                                                          
*                                                                               
         SCAN  QMONPRT             QMONITOR options                             
         SCANCHK                                                                
*-                                                                              
*-       Wait for a connection to the named server.                             
*-                                                                              
         IF    (SVIBNO,Z),BEGIN    No server number given...                    
*-                                                                              
*-       Build a QMW entry and wait.                                            
*-                                                                              
         LA    R0,L'QMW                                                         
         VCALL GETCORE                                                          
         LR    R4,R1                                                            
         WITH  (QMW,R4)                                                         
         CLEAR QMW                                                              
         MVC   QMWNAME,SVIBNAME    Save name of server                          
         MVC   QMWJCBP,CVCURJCB    Save our JCB ptr                             
         MVC   QMWLINK,CVQMONQ     Copy old queue head                          
         ST    R4,CVQMONQ          Save new queue head                          
*                                                                               
         TSEG  'Waiting for an open of server '                                 
         TSEG  SVIBNAME,,T                                                      
QMONNWT  TWRITE                                                                 
         BNZ   CVABORT                                                          
*-                                                                              
*-       Wait for someone to open a server.                                     
*-                                                                              
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'SET JCBWWFGEN'  General wait                           
         PAUSE ,                                                                
*                                                                               
         TSTAT                                                                  
         BNZ   CVABORT             Attn means stop                              
*                                                                               
         IF    ('LT R2,QMWNO',Z),BEGIN  This shouldn't happen...                
         TSEG  'Still waiting'                                                  
         B     QMONNWT                                                          
         END                                                                    
*-                                                                              
*-       Dequeue our QMW entry.                                                 
*-                                                                              
         LA    R3,CVQMONQ-(QMWLINK-QMW)  Dummy queue head                       
QMONDQW  LOOP  BEGIN                                                            
         LR    R2,R3               Previous entry                               
         WITH  (QMW,R3)                                                         
         WHILE ('LT R3,QMWLINK',NZ)                                             
         IF    (R3,EQ,R4),BEGIN    Our entry...                                 
         MVC   QMWLINK-QMW(4,R2),QMWLINK  Dequeue                               
         LA    R1,QMW                                                           
         VCALL FREECORE            Release memory                               
         EXIT  QMONDQW                                                          
         END                                                                    
         END                                                                    
*                                                                               
         MVC   SVIBNO,QMWNO        Set server number we want                    
         END                                                                    
*-                                                                              
*-       Get the real SVIB ptr given the server number.                         
*-                                                                              
         L     R15,SVIBNO                                                       
         ACALL SERVGET                                                          
         IF    Z,BEGIN             No server...                                 
         TSEG  'Server '                                                        
         TNUM  L:SVIBNO                                                         
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LR    R5,R15              Real SVIB ptr                                
         MVC   QMONSNO,SVIBNO      Save server number for later                 
*-                                                                              
*-       We can't monitor a server definition.                                  
*-                                                                              
         IF    SVIB#MASTER,BEGIN   Server definition...                         
         TSEG  'You can''t monitor a definition; '                              
         TSEG  'try "QMONITOR '                                                 
         TSEG  SVIBNAME,,T                                                      
         ERROR '" instead.'                                                     
         END                                                                    
*-                                                                              
*-       Only one user can monitor a server.                                    
*-                                                                              
         IF    ('LT R3,SVIBMJCB',NZ),BEGIN  Already monitored...                
         WITH  (JCB,R3)                                                         
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' is already being monitored by line '                           
         TNUM  LH:JCBSEQ                                                        
         ERROR                                                                  
         END                                                                    
*-                                                                              
*-       Check user's authority.                                                
*-                                                                              
         IF    (CPSGRP,NE,'G'),BEGIN  Disallow...                               
         ERROR 'QMONITOR is privileged.'                                        
         END                                                                    
*                                                                               
         SET   SVIBFMON            We are monitoring now                        
         MVC   SVIBMJCB,CVCURJCB   Save our JCB ptr                             
*-                                                                              
*-       Display general server information.                                    
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVDISP            Display server info                          
*-                                                                              
*-       Set the "shown" flag in all but the last four trace                    
*-         table entries.                                                       
*-                                                                              
         CLEAR QMONTRCP            Reset ptrs                                   
         LA    R4,SVIBTRCQH-(STRCLINK-STRC)  Dummy first link                   
         LOOP  BEGIN               Go through trace table...                    
         WITH  (STRC,R4)                                                        
         WHILE ('LT R4,STRCLINK',NZ)                                            
         SET   STRCFSHOWN          Don't display this entry                     
         MVC   QMONTRCP(L'QMONTRCP-4),QMONTRCP+4  Shift                         
         ST    R4,QMONTRCP+L'QMONTRCP-4  Add our STRC ptr                       
         END                                                                    
*                                                                               
         LA    R3,QMONTRCP         First ptr                                    
         LA    R4,QMONTRCP+L'QMONTRCP  End of list                              
         WHILE (R3,LT,R4),BEGIN    Reset last four entries...                   
         LT    R15,@R3             STRC ptr                                     
         IF    NZ,BEGIN            We have one...                               
         WITH  (STRC,R15),'CLEAR STRCFSHOWN'  Reset shown flag                  
         END                                                                    
         LA    R3,@R3+4            Next trace entry                             
         END                                                                    
*-                                                                              
*-       Main loop -- break out when user presses ATTN.                         
*-                                                                              
QMONLOOP LOOP  BEGIN                                                            
         LA    R4,SVIBTRCQH-(STRCLINK-STRC)  Dummy first link                   
         LOOP  BEGIN                                                            
         WITH  (STRC,R4)                                                        
         WHILE ('LT R4,STRCLINK',NZ)                                            
         IF    ^STRCFSHOWN,BEGIN   Not shown yet...                             
*-                                                                              
*-       Put a copy of the trace entry in the Active file.                      
*-                                                                              
         IF    QMONFACT,BEGIN      Copy to the Active file...                   
         TWRITE                                                                 
*                                                                               
         SET   CPFDUMP             Add to active file                           
         LA    R15,STRC                                                         
         ACALL SERVDTRE                                                         
         TWRITE                                                                 
         CLEAR CPFDUMP             Reset                                        
         END                                                                    
*-                                                                              
*-       Display the trace entry.                                               
*-                                                                              
         LA    R15,STRC                                                         
         ACALL SERVDTRE            Display trace entry                          
         SET   STRCFSHOWN          Entry has been displayed                     
         CLEAR STRCFKEEP           Don't keep this any more                     
         END                                                                    
         END                                                                    
*                                                                               
         TWRITE                                                                 
         IF    NZ,EXIT             Attn means stop                              
*-                                                                              
*-       Wait for new trace entries to be added.                                
*-                                                                              
         LT    R15,SVIBTRCQT       Newest trace entry                           
         IF    NZ,BEGIN            We have something...                         
         WITH  (STRC,R15)                                                       
         IF    ^STRCFSHOWN,QMONLOOP  We have new entries, loop back             
         END                                                                    
*                                                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'SET JCBWWFGEN'  General wait                           
         PAUSE ,                                                                
*                                                                               
         TSTAT                                                                  
         IF    NZ,EXIT             Attn means stop                              
*-                                                                              
*-       Make sure the server is still around.                                  
*-                                                                              
         L     R15,QMONSNO         Our server number                            
         ACALL SERVGET             Get SVIB ptr                                 
         IF    Z,BEGIN             No server...                                 
         TSEG  'Server '                                                        
         TNUM  L:SVIBNO                                                         
         ERROR ' connection has been closed.'                                   
         END                                                                    
*                                                                               
         LR    R5,R15              Copy SVIB ptr                                
         END                                                                    
*-                                                                              
*-       Stop monitoring.                                                       
*-                                                                              
         CLEAR SVIBFMON            No longer monitoring                         
         CLEAR SVIBMJCB            Reset our JCB ptr                            
         B     CVNEXT                                                           
         PEND                                                                   
*-                                                                              
*-       QMONITOR Options.                                                      
*-                                                                              
QMONPRT  SCKW  ACTIVE,QMONACT,A                                                 
         SCKW  ,V(SCNINVAL)                                                     
*-                                                                              
QMONACT  PROC                                                                   
         WITH  (QMONWA,R6)                                                      
         SET   QMONFACT                                                         
         PEND                                                                   
         TITLE 'Common Subroutines'                                             
*box                                                                            
*                                                                               
*  GETQINFO -- Routine to return information about the current                  
*    QUERY.                                                                     
*                                                                               
*    On exit:                                                                   
*      R1,R0 - Current server name loc, len                                     
*      R15   - Current server number                                            
*                                                                               
GETQINFO XPROC (R2,LSR)                                                         
         CLEAR R0,R1               Assume no name                               
         LH    R15,=H'-1'          Assume no current server                     
*                                                                               
         LT    R5,CPQINFOQ         Current query info ptr                       
         IF    NZ,BEGIN            We have one...                               
         WITH  (QINFO,R5)                                                       
         L     R15,QINFNO          Server number                                
         ACALL SERVGET             Get SVIB ptr                                 
         IF    NZ,BEGIN            We found the server...                       
         WITH  (SVIB,R15)                                                       
         SETMSG SVIBNAME            Server name                                 
         VCALL RTRIM               Trim trailing blanks                         
         END                                                                    
         ELSE  'CLEAR R0,R1'       No server name (unusual)                     
         L     R15,QINFNO          Server number                                
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GETQSTAT -- Routine to return information about the state                    
*    of the server specified.                                                   
*                                                                               
*    On entry:                                                                  
*      R15   - server number                                                    
*                                                                               
*    On exit:                                                                   
*      R1,R0 - message with state (loc,len)                                     
*                                                                               
GETQSTAT XPROC (R2,LSR)                                                         
         ACALL SERVGET             Get SVIB ptr                                 
*-                                                                              
*-       We found the server.                                                   
*-                                                                              
         IF    NZ,BEGIN            We found the server...                       
         WITH  (SVIB,R15)                                                       
         SETMSG 'ACTIVE'                                                        
         IF    SVIBSFDATA,'SETMSG "SENDING"'                                    
         IF    SVIBSFFETCH,'SETMSG "FETCHING"'                                  
         IF    SVIBFCLOSING,'SETMSG "CLOSING"'                                  
*                                                                               
         LT    R5,SVIBUNET         UNET ptr                                     
         IF    NZ,BEGIN                                                         
         WITH  (UNET,R5)                                                        
         IF    (UNETFINEOF,OR,UNETFINABORT,OR,                         *        
               (UNETSTATE,GE,UNETSFABORT)),BEGIN  Close...                      
         SETMSG 'CLOSING'                                                       
         END                                                                    
         END                                                                    
*                                                                               
         IF    SVIB#MASTER,'SETMSG "DEFINITION"'                                
         END                                                                    
*-                                                                              
*-       No such server.                                                        
*-                                                                              
         ELSE  'SETMSG "NOT FOUND"'                                             
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SEGFLDS -- Routine to SEG an expanded field line from a                      
*    list of field descriptors and tab separated data.                          
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line location, length                                            
*      R2    - FDEMAP ptr                                                       
*      R15   - SEGCB ptr                                                        
*                                                                               
SEGFWA   RECORD BEGIN                                                           
SEGFCBP  DS    A                   SEGCB ptr                                    
SEGFLNP  DS    2A                  Current data line ptrs                       
SEGFFLDP DS    2A                  Current field ptrs                           
         END                                                                    
*-                                                                              
SEGFLDS  PROC  SEGFWA                                                           
         ST    R15,SEGFCBP         Save SEGCB ptr                               
         SEGDEF L:SEGFCBP                                                       
*                                                                               
         STM   R0,R1,SEGFLNP       Save input line ptr                          
*                                                                               
         LR    R6,R2               Copy FDEMAP ptr                              
*-                                                                              
*-       Display each field.                                                    
*-                                                                              
         LOOP  BEGIN                                                            
         WITH  (FDE,R6)                                                         
*                                                                               
         LM    R2,R3,SEGFLNP       Get line ptrs                                
         LCALL GETFIELD            Get next field                               
         IF    NZ,EXIT             All done, scram                              
         STM   R2,R3,SEGFLNP       Save updated line ptrs                       
         STM   R0,R1,SEGFFLDP      Save field len, loc                          
*-                                                                              
*-       Left centered field.                                                   
*-                                                                              
         IF    FDETLEFT,BEGIN      Left centered...                             
         LM    R0,R1,SEGFFLDP                                                   
         SEG   (R1),(R0)           Field data                                   
*                                                                               
         LH    R0,FDELEN                                                        
         S     R0,SEGFFLDP                                                      
         SEGBLANK (R0)             Trailing blank padding                       
         END                                                                    
*-                                                                              
*-       Right centered field.                                                  
*-                                                                              
         ELSEIF FDETRIGHT,BEGIN    Right centered field...                      
         LH    R0,FDELEN                                                        
         S     R0,SEGFFLDP                                                      
         SEGBLANK (R0)             Leading blank padding                        
*                                                                               
         LM    R0,R1,SEGFFLDP                                                   
         SEG   (R1),(R0)           Field data                                   
         END                                                                    
*-                                                                              
*-       Middle centered field.                                                 
*-                                                                              
         ELSEIF FDETCENTER,BEGIN   Centered field...                            
         LH    R0,FDELEN                                                        
         S     R0,SEGFFLDP                                                      
         SLL   R0,1                Divided by two                               
         LR    R2,R0               Save leading blank count                     
         SEGBLANK (R0)             Leading blank padding                        
*                                                                               
         LM    R0,R1,SEGFFLDP                                                   
         SEG   (R1),(R0)           Field data                                   
*                                                                               
         LH    R0,FDELEN                                                        
         SR    R0,R2               Deduct leading blank count                   
         S     R0,SEGFFLDP         Deduct field text count                      
         SEGBLANK (R0)             Trailing blank padding                       
         END                                                                    
*-                                                                              
*-       Unknown field type.                                                    
*-                                                                              
         ELSE  BEGIN               No field description...                      
         SEG   (R1),(R0)                                                        
         END                                                                    
*-                                                                              
*-       Trailing blank.                                                        
*-                                                                              
         SEG   '  '                                                             
*                                                                               
         IF    (FDETYPE,NZ),'LA R6,FDENEXT'  Next FDE                           
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GETFIELD -- Local routine to get the next tab separated field                
*    in the line passed.                                                        
*                                                                               
*    On entry:                                                                  
*      R3,R2 - line location, length                                            
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - field returned:                                                      
*            R3,R2 = remaining line location, length                            
*            R1,R0 = field loc, len                                             
*                                                                               
*      4 - no more fields                                                       
*                                                                               
GETFIELD PROC  (R4,LSR)                                                         
         IF    (R2,NP),'LA R15,4; EXIT GETFIELD'  Nothing left                  
*                                                                               
         LR    R1,R3               Set field loc                                
         CLEAR R0                  Initialize field len                         
         WHILE ((R2,POS),AND,(@R3,NE,$TAB)),BEGIN  Look for TAB...              
         INCR  R0                  Next char                                    
         LA    R3,@R3+1                                                         
         DECR  R2                                                               
         END                                                                    
*                                                                               
         IF    ((R2,POS),AND,(@R3,EQ,$TAB)),BEGIN  Sep char...                  
         LA    R3,@R3+1            Skip sep char                                
         DECR  R2                                                               
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Utility Subroutines'                                            
*box                                                                            
*                                                                               
*  SERVGET -- Routine to get the SVIB given it's server number.                 
*                                                                               
*    On entry:                                                                  
*      R15 - server number                                                      
*                                                                               
*    On exit:                                                                   
*      R15 = SVIB ptr (or zero if no such server)                               
*                                                                               
SERVGET  PROC                                                                   
         LR    R5,R15              Server number                                
*-                                                                              
*-       User server (get SVIB from CVUSTAB).                                   
*-                                                                              
         IF    ((CVUSTAB,NZ),AND,                                      *        
               (R5,GE,CVUSFNO),AND,(R5,LE,CVUSLNO)),BEGIN                       
         S     R5,=A(CVUSFNO)                                                   
         SLL   R5,2                Times 4 for word offset                      
         A     R5,CVUSTAB          Index table entry ptr                        
         L     R15,@R5             Get SVIB ptr (or zero if none)               
         END                                                                    
*-                                                                              
*-       Public server (get SVIB from CVPSTAB).                                 
*-                                                                              
         ELSEIF ((CVPSTAB,NZ),AND,                                     *        
               (R5,GE,CVPSFNO),AND,(R5,LE,CVPSLNO)),BEGIN                       
         S     R5,=A(CVPSFNO)                                                   
         SLL   R5,2                Times 4 for word offset                      
         A     R5,CVPSTAB          Index table entry ptr                        
         L     R15,@R5             Get SVIB ptr (or zero if none)               
         END                                                                    
*-                                                                              
*-       No such server.                                                        
*-                                                                              
         ELSE  BEGIN                                                            
         CLEAR R15                 Sorry guy                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVCHK -- Routine to make sure that the server can be used.                 
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr; 0 means no server (error)                                
*                                                                               
*    Returns only if OK (R15 = SVIB ptr).                                       
*                                                                               
SCWA     RECORD BEGIN                                                           
SCKWR    DS    XL(L'KWR)           Working KWR area                             
         END                                                                    
*-                                                                              
SERVCHK  PROC  SCWA                                                             
*-                                                                              
*-       Make sure we found a server entry.                                     
*-                                                                              
         IF    (R15,Z),BEGIN       No server...                                 
         ERROR 'Server does not exist.'                                         
         END                                                                    
*-                                                                              
*-       Private server belongs to another user.                                
*-                                                                              
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVCHK,'Bad SVIB ptr.'                         
         IF    (SVIBCRJCB,NZ),BEGIN  User server...                             
         IF    (SVIBCRJCB,EQ,CVCURJCB),EXIT  Ours, fine                         
         TSEG  'Server '                                                        
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         TSEG  ' is not yours.',,CR                                             
         IF    (SVIBCRACCT,EQ,CPSACCT),BEGIN  It's us...                        
         SETMSG 'OK'                                                            
         VCALL YESREQ              Get verification                             
         END                                                                    
*                                                                               
         ELSE  BEGIN               Get password...                              
         LA    R1,SCKWR            KWR area                                     
         WITH  (KWR,R1),'MVC KWRACCT,SVIBCRACCT'  Creator's account             
         CLEAR R0                  0 = normal mode                              
         VCALL CHKKW               Make them supply password                    
         END                                                                    
         END                                                                    
*                                                                               
         LA    R15,SVIB            Return SVIB ptr                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVSETPROTO -- Routine to fill in the server name and                       
*    number in the SVIB given a server name or server number.                   
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*      R1,R0 - text loc,len                                                     
*                                                                               
*    On exit, R15 (and cc):                                                     
*       0 - OK; SVIB NAME/NO is filled in                                       
*       nz- error; error message is already TSEG'D                              
*                                                                               
SERVSETPROTO PROC                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         CLEAR SVIBNAME,SVIBNO                                                  
*                                                                               
         IF    ((@R1,EQ,'"'),OR,(@R1,EQ,'''')),BEGIN                            
         TSEG  (R1),(R0)                                                        
         TSEG  ': quoted server name not allowed.'                              
         B     SCNSERR                                                          
         END                                                                    
*                                                                               
         IF    (@R1,EQ,'('),BEGIN                                               
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid server name.'                                         
         B     SCNSERR                                                          
         END                                                                    
*                                                                               
         IF    (R0,GT,L'SVIBNAME),BEGIN  Too too much...                        
         TSEG  (R1),(R0)                                                        
         TSEG  ': server name is too long.'                                     
         B     SCNSERR                                                          
         END                                                                    
*                                                                               
         LR    R3,R1                                                            
         LR    R2,R0                                                            
*-                                                                              
*-       Current server connection.                                             
*-                                                                              
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         LT    R15,CPQINFOQ        Last query info entry (or zero)              
         IF    NZ,'L R15,QINFNO-QINFO(,R15)'  Current server number             
         IF    (R15,Z),BEGIN                                                    
         TSEG  'There is no current server connection.'                         
         B     SCNSERR                                                          
         END                                                                    
         ST    R15,SVIBNO          Save server number                           
         CLEAR R15                 A-OK                                         
         B     SCNSEXIT                                                         
         END                                                                    
*-                                                                              
*-       Server number.                                                         
*-                                                                              
         DTB   (R1),(R0)                                                        
         IF    Z,BEGIN             Numeric (server number)...                   
         ST    R15,SVIBNO          Save server number                           
         END                                                                    
*-                                                                              
*-       Server name.                                                           
*-                                                                              
         ELSE  BEGIN               Non-numeric (server name)...                 
         MVC   SVIBNAME,CVBLANKS                                                
         CEIL  R2,L'SVIBNAME                                                    
         MOVE  R2,SVIBNAME,@R3     Save server name                             
         L     R15,CVUPPTBL                                                     
         TR    SVIBNAME,@R15       Convert to caps                              
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         B     SCNSEXIT                                                         
*                                                                               
SCNSERR  TCR                                                                    
         LA    R15,4               Error                                        
SCNSEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVSCANPROTO -- Routine to scan the next token as a                         
*    server name/number.                                                        
*                                                                               
*    On entry:                                                                  
*       R15 - SVIB ptr                                                          
*                                                                               
*    On exit, R15 (and cc):                                                     
*       0 - OK; SVIB NAME/NO is filled in                                       
*       nz- error; error message is already TSEG'D                              
*                                                                               
SERVSCANPROTO PROC                                                              
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,Z),BEGIN        Nothing...                                   
         TSEG  'Missing server name'                                            
         LA    R15,4                                                            
         B     SSCNEXIT                                                         
         END                                                                    
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVSETPROTO        Set up SVIB NAME/NO                          
SSCNEXIT PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVSCANSVIB -- Routine to scan for a server number and                      
*    then get it's SVIB ptr.  The server must already be                        
*    active and connected to the current user.                                  
*                                                                               
*    On entry:                                                                  
*       Scanner is set up                                                       
*                                                                               
*    On exit, returns only if OK:                                               
*       R15 = SVIB ptr                                                          
*                                                                               
SERVSCANSVIB PROC                                                               
*-                                                                              
*-       Pick off server number ("*" as a server number means                   
*-          use the current server).                                            
*-                                                                              
         SCAN  ,                   Look for optional server number              
         SCANCHK                                                                
         IF    (R0,NP),'ERROR "Missing server number."'                         
         DTB   (R1),(R0)           Convert to number                            
         LR    R2,R15              Save number (if numeric)                     
         IF    NZ,BEGIN            Not a number...                              
         IF    ((R0,NE,1),OR,(@R1,NE,'*')),BEGIN  Not * either...               
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid; expecting server number (or *).'                     
         END                                                                    
         LT    R2,CPQINFOQ         Last query info entry (or zero)              
         IF    NZ,'L R2,QINFNO-QINFO(,R2)'  Current server number               
         END                                                                    
*-                                                                              
*-       Get SVIB ptr given it's server number.                                 
*-                                                                              
         IF    (R2,Z),BEGIN        No default server number...                  
         ERROR 'There is no current server connection.'                         
         END                                                                    
*                                                                               
         LR    R15,R2              Server number                                
         ACALL SERVGET             Get SVIB ptr from server number              
         IF    Z,BEGIN             No such server...                            
         TSEG  'Server '                                                        
         TNUM  (R2)                                                             
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Make sure server has been opened and that it belongs to us.            
*-                                                                              
         IF    (^SVIB#BUSY,OR,(SVIBJCBP,NE,CVCURJCB)),BEGIN                     
         TSEG  'Connection to server '                                          
         LA    R15,SVIB                                                         
         ACALL SERVSEGN                                                         
         ERROR ' has not been opened.'                                          
         END                                                                    
*                                                                               
         LA    R15,SVIB            Return SVIB ptr                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFINDPUBLIC -- Routine to find the first/next matching                    
*    public server entry (SVIB) given a "model" SVIB with                       
*    SVIBNAME set to the server name (zeros means match                         
*    everything) and SVIBNO set to the last matched server                      
*    found.                                                                     
*                                                                               
*    On entry:                                                                  
*      R15 - model SVIB ptr                                                     
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - found next server; R1 = SVIB ptr                                     
*      4 - not found                                                            
*                                                                               
SERVFINDPUBLIC PROC                                                             
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Start looking from the server number in model SVIB.                    
*-                                                                              
         L     R4,SVIBNO           Server number (or zero)                      
         LA    R4,@R4+1            Find next matching server                    
*-                                                                              
*-       Try to match the next public server.                                   
*-                                                                              
         IF    (R4,LT,CVPSFNO),'LA R4,CVPSFNO'  Start at beginning              
*                                                                               
         IF    (R4,LE,CVPSLNO),BEGIN  In range of public servers...             
         IF    (CVPSTAB,Z),EXIT     No public servers, scram                    
*                                                                               
         LR    R3,R4                                                            
         S     R3,=A(CVPSFNO)      Relative to start of index table             
         SLL   R3,2                Times 4 for word offset                      
         A     R3,CVPSTAB          Index table entry ptr                        
*                                                                               
         WHILE (R3,LT,CVPSENDP),BEGIN  Go through index table...                
         LT    R1,@R3              Get SVIB ptr                                 
         IF    NZ,BEGIN            Server defined...                            
         IF    (SVIBNAME,Z),FPUBMAT  Match everything                           
         CLC   SVIBNAME,SVIBNAME-SVIB(R1)  Does name match?                     
         BE    FPUBMAT             Yes, we found it                             
         END                                                                    
*                                                                               
         LA    R3,@R3+4            Next SVIB ptr in index table                 
         END                                                                    
         END                                                                    
*-                                                                              
*-       No match.                                                              
*-                                                                              
         LA    R15,4               No match                                     
         EXIT                                                                   
*                                                                               
FPUBMAT  PRETURN (R1)              Return matching SVIB ptr                     
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFINDPRIVATE -- Routine to find the first/next matching                   
*    private server entry (SVIB) given a "model" SVIB with                      
*    SVIBNAME set to the server name (zeros means match                         
*    everything) and SVIBNO set to the last matched server                      
*    found.                                                                     
*                                                                               
*    On entry:                                                                  
*      R15 - model SVIB ptr                                                     
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - found next server; R1 = SVIB ptr                                     
*      4 - not found                                                            
*                                                                               
SERVFINDPRIVATE PROC                                                            
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Start looking from the server number in model SVIB.                    
*-                                                                              
         L     R4,SVIBNO           Server number (or zero)                      
         LA    R4,@R4+1            Find next matching server                    
*-                                                                              
*-       Try to match the next public server.                                   
*-                                                                              
         IF    (R4,LT,CVUSFNO),'LA R4,CVUSFNO'  Start at beginning              
*                                                                               
         IF    (R4,LE,CVUSLNO),BEGIN  In range of user servers...               
         IF    (CVUSTAB,Z),EXIT     No user servers, scram                      
*                                                                               
         LR    R3,R4                                                            
         S     R3,=A(CVUSFNO)      Relative to start of index table             
         SLL   R3,2                Times 4 for word offset                      
         A     R3,CVUSTAB          Index table entry ptr                        
*                                                                               
         WHILE (R3,LT,CVUSENDP),BEGIN  Go through index table...                
         LT    R1,@R3              Get SVIB ptr                                 
         IF    NZ,BEGIN            Server defined...                            
         CLC   CVCURJCB,SVIBCRJCB-SVIB(R1)  Is it ours?                         
         IF    NE,EXIT             No, ignore it                                
         IF    (SVIBNAME,Z),FUSRMAT  Match everything                           
         CLC   SVIBNAME,SVIBNAME-SVIB(R1)  Does name match?                     
         BE    FUSRMAT             Yes, we found it                             
         END                                                                    
*                                                                               
         LA    R3,@R3+4            Next SVIB ptr in index table                 
         END                                                                    
         END                                                                    
*-                                                                              
*-       No match.                                                              
*-                                                                              
         LA    R15,4               No match                                     
         EXIT                                                                   
*                                                                               
FUSRMAT  PRETURN (R1)              Return matching SVIB ptr                     
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVSEGN -- Routine to seg server number and name.                           
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
SERVSEGN PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         LT    R15,SVIBNO          Server number                                
         IF    NZ,'TNUM (R15); SETMSG " "'                                      
         ELSE  'CLEAR R0'                                                       
*                                                                               
         IF    (SVIBNAME,NZ),BEGIN  Server name...                              
         TSEG  (R1),(R0)                                                        
         TSEG  SVIBNAME,,T                                                      
         END                                                                    
*                                                                               
         IF    ((SVIBNO,Z),AND,(SVIBNAME,Z)),BEGIN  Strange...                  
         TSEG  '(no name)'                                                      
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVDISP -- Routine to format a SHOW QUERY response for the                  
*    server specified.                                                          
*                                                                               
*    On entry:                                                                  
*       R15 - SVIB ptr                                                          
*                                                                               
SDSWA    RECORD BEGIN                                                           
SDSCLCK  DS    D                   Working STCK area                            
         END                                                                    
*-                                                                              
SERVDISP PROC  SDSWA                                                            
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVDISP,'Bad SERVDISP options'                 
*                                                                               
         TSEG  'Server'                                                         
         TNUM  L:SVIBNO,5                                                       
         TSEG  ' '                                                              
         TSEG  (R1),(R0)                                                        
         TSEG  SVIBNAME,,T         Server name                                  
*                                                                               
         IF    (SVIBINFO,NZ),BEGIN                                              
         TCOL  25                                                               
         TSEG  SVIBINFO,,T                                                      
         END                                                                    
         TCR                                                                    
*-                                                                              
*-       Server master definition.                                              
*-                                                                              
         IF    SVIB#MASTER,BEGIN                                                
         TSEG  '  Defined as '                                                  
         TSEG  SVIBRDB,,T                                                       
         IF    (SVIBRHOST,NZ),'TSEG " on "; TSEG SVIBRHOST,,T'                  
         IF    (SVIBRUSER,NZ),'TSEG " user "; TSEG SVIBRUSER,,T'                
         TSEG  ' port '                                                         
         TNUM  L:SVIBRPORT                                                      
*                                                                               
         IF    (SVIBMAX,NZ),BEGIN  Max number of servers allowed...             
         TSEG  ' (Max='                                                         
         TNUM  L:SVIBMAX                                                        
         TSEG  ')'                                                              
         END                                                                    
         END                                                                    
*-                                                                              
*-       Server is waiting for a connection.                                    
*-                                                                              
         ELSEIF SVIB#WAITING,BEGIN                                              
         TSEG  '  Waiting for connection to server'                             
         END                                                                    
*-                                                                              
*-       Server is idle.                                                        
*-                                                                              
         ELSEIF SVIB#IDLE,BEGIN                                                 
         TSEG  '  Idle'                                                         
         END                                                                    
*-                                                                              
*-       Server is busy.                                                        
*-                                                                              
         ELSEIF SVIB#BUSY,BEGIN                                                 
         TSEG  '  Busy:  '                                                      
*                                                                               
         IF    (SVIBCLK,NZ),BEGIN  Display elapsed time busy...                 
         STCK  SDSCLCK                                                          
         L     R15,SDSCLCK                                                      
         S     R15,SVIBCLK                                                      
         IF    (R15,LT,60),BEGIN   Show as seconds...                           
         TNUM  (R15),3                                                          
         TSEG  ' seconds '                                                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               Show as minutes...                           
         CLEAR R14                                                              
         D     R14,=A(60)          Divide by 60 for minutes                     
         IF    (R14,GE,60/2),'INCR R15'  Round                                  
         TNUM  (R15),3                                                          
         TSEG  ' minutes '                                                      
         END                                                                    
         END                                                                    
*                                                                               
         TSEG  'with line '        Display who we are busy with                 
         L     R2,SVIBJCBP                                                      
         WITH  (JCB,R2)                                                         
         TNUM  LH:JCBSEQ                                                        
         TSEG  ' '                                                              
         TSEG  JCBGRP                                                           
         TSEG  '.'                                                              
         TSEG  JCBUSER                                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  'Unknown status'                                                 
         END                                                                    
*-                                                                              
*-       We are looking at another user's private server.                       
*-                                                                              
         IF    ('LT R2,SVIBCRJCB',NZ),BEGIN  Private server...                  
         IF    (R2,EQ,CVCURJCB),EXIT  Ours, fine                                
         WITH  (JCB,R2)                                                         
         TSEG  ' (Owned by line '                                               
         TNUM  LH:JCBSEQ                                                        
         TSEG  ')'                                                              
         END                                                                    
*-                                                                              
*-       Flags.                                                                 
*-                                                                              
         IF    (SVIBSTATE,NZ),BEGIN  Special state...                           
         SETMSG ' (Special state)'                                              
         IF    SVIBSFDATA,'SETMSG " (Sending)"'                                 
         IF    SVIBSFFETCH,'SETMSG " (Fetching)"'                               
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         IF    SVIBFREUSE,'TSEG " (Reusable)"'                                  
         IF    SVIBFCLOSING,'TSEG " (Closing)"'                                 
*                                                                               
         IF    SVIBFMON,BEGIN                                                   
         TSEG  ' (Monitoring'                                                   
         IF    (SVIBMJCB,NE,CVCURJCB),BEGIN                                     
         TSEG  ' by line '                                                      
         L     R2,SVIBMJCB                                                      
         WITH  (JCB,R2),'LH R15,JCBSEQ'                                         
         TNUM  (R15)                                                            
         END                                                                    
         TSEG  ')'                                                              
         END                                                                    
*                                                                               
         TCR                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVDTRC -- Routine to display the server trace table.                       
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
*-                                                                              
SERVDTRC PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),STRCDISP,'Bad SVIB ptr.'                        
*-                                                                              
*-       Complain if trace table is empty.                                      
*-                                                                              
         IF    (SVIBTRCQH,Z),BEGIN   No trace entries...                        
         TSEG  'Trace table is empty.',,CR                                      
         B     STRCEXIT                                                         
         END                                                                    
*-                                                                              
*-       Step through the trace entry queue.                                    
*-                                                                              
         LA    R6,SVIBTRCQH-(STRCLINK-STRC)  Dummy first link                   
         LOOP  BEGIN                                                            
         WITH  (STRC,R6)                                                        
         WHILE ('LT R6,STRCLINK',NZ)  Go through buffers...                     
         LA    R15,STRC                                                         
         LCALL SERVDTRE            Display this trace entry                     
         END                                                                    
*                                                                               
STRCEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVDTRE -- Routine to format a specific trace table entry.                  
*                                                                               
*    On entry:                                                                  
*      R15 - STRC ptr                                                           
*                                                                               
SVDTWA   RECORD BEGIN                                                           
SVDTTYPE DS    CL3                 Working trace type                           
SVDTMSG  DS    XL256               Message work area                            
         END                                                                    
*-                                                                              
SERVDTRE PROC  SVDTWA                                                           
         LR    R6,R15                                                           
         WITH  (STRC,R6)                                                        
*-                                                                              
*-       Format the trace entry.                                                
*-                                                                              
         IF    (STRC,NE,'STRC'),BEGIN  Shucks...                                
         TSEG  'Bad trace entry queue!',,CR                                     
         B     STREEXIT                                                         
         END                                                                    
*                                                                               
         LM    R0,R1,STRCCLCK      Get time stamp                               
         XPUSH ,,11,PTR=R15                                                     
         VCALL FMTTIME             Get "hh:mm:ss.th"                            
         TSEG  (R1),(R0),B                                                      
         TSEG  'Line '                                                          
         TNUM  LH:STRCLNO,4        Session number                               
         TSEG  CVBLANKS,1                                                       
         TSEG  STRCACCT+3,2        gg                                           
         TSEG  '.'                                                              
         TSEG  STRCACCT,3,B          uuu                                        
*                                                                               
         MVC   SVDTTYPE,STRCTYPE                                                
         MVC   SVDTTYPE+2(1),SVDTTYPE+1                                         
         TSEG  SVDTTYPE,,B         Trace type                                   
*                                                                               
         SETMSG STRCDATA,LH:STRCDLEN  Trace data                                
*-                                                                              
*-       Make a copy of the trace data and convert it from                      
*-         ASCII to EBCDIC if necessary.                                        
*-                                                                              
         IF    (STRCTYPE+2,EQ,'A'),BEGIN  ASCII...                              
         LR    R2,R0                                                            
         CEIL  R2,L'SVDTMSG                                                     
         LR    R15,R2                                                           
         MOVE  R15,SVDTMSG,@R1     Copy trace data                              
         L     R3,CVEBCTBL                                                      
         EX    R15,'TR SVDTMSG,@R3'  Convert to EBCDIC                          
         SETMSG SVDTMSG,(R2)        EBCDIC trace data                           
         END                                                                    
*                                                                               
         SETMSG (R1),(R0)           Trace data                                  
         IF    (R0,GE,2),BEGIN     Check for trailing CR,LF...                  
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         SH    R15,=H'2'                                                        
         IF    (@R15,EQ,X'0D25'),'SH R0,=H"2"'  Remove them                     
         END                                                                    
*                                                                               
         VCALL MIXFMT              Write it in mixed format                     
         TCR                                                                    
*                                                                               
STREEXIT PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVALLO -- Routine to allocate an SVIB control block.                       
*                                                                               
*    On entry:                                                                  
*      R0  - 0 = user server                                                    
*            1 = public server                                                  
*      R15 - SVIB data (a new SVIB is created with this data)                   
*                                                                               
*    On exit:                                                                   
*      R15 - New SVIB ptr                                                       
*                                                                               
ALLOWA   RECORD BEGIN                                                           
ALLOFLAG FLAG                                                                   
         FLAG  ALLOFPUB            - Public server                              
*                                                                               
ALLOMODP DS    A                   Model SVIB ptr                               
ALLONO   DS    A                   Server number we allocated                   
*                                                                               
ALLOSVFN DS    F                   First server number                          
ALLOSVLN DS    F                   Last server number                           
ALLOSVIT DS    A                   SVIT ptr                                     
ALLOENDP DS    A                   End of SVIT ptr                              
ALLONXP  DS    A                   Next SVIT entry to use ptr                   
         END                                                                    
*-                                                                              
SERVALLO PROC  ALLOWA                                                           
         CLEAR ALLOWA                                                           
         IF    (R0,NZ),'SET ALLOFPUB'  Public server                            
*                                                                               
         WITH  (SVIB,R15),BEGIN                                                 
         FAIL  (SVIB,NE,'SVIB'),SERVALLO,'Bad SERVALLO options'                 
         FAIL  ^SVIB#MASTER,SERVALLO,'Bad SVIB type for SERVALLO'               
         ST    R15,ALLOMODP        Save model SVIB ptr                          
         END                                                                    
*                                                                               
         IF    ALLOFPUB,BEGIN      Public means allocate from CV...             
         MVC   ALLOSVFN,=A(CVPSFNO)  First server number                        
         MVC   ALLOSVLN,=A(CVPSLNO)  Last server number                         
         MVC   ALLOSVIT,CVPSTAB    SVIT ptr                                     
         MVC   ALLOENDP,CVPSENDP   End of SVIT ptr                              
         MVC   ALLONXP,CVPSNXP     Next SVIT entry to use ptr                   
         END                                                                    
*                                                                               
         ELSE  BEGIN               User means allocate from CP...               
         MVC   ALLOSVFN,=A(CVUSFNO)  First server number                        
         MVC   ALLOSVLN,=A(CVUSLNO)  Last server number                         
         MVC   ALLOSVIT,CVUSTAB    SVIT ptr                                     
         MVC   ALLOENDP,CVUSENDP   End of SVIT ptr                              
         MVC   ALLONXP,CVUSNXP     Next SVIT entry to use ptr                   
         END                                                                    
*-                                                                              
*-       If this is the first server to be allocated then we must               
*-         allocate the server index table (SVIT).                              
*-                                                                              
         IF    (ALLOSVIT,Z),BEGIN  Allocate SVIT...                             
         L     R2,ALLOSVLN                                                      
         S     R2,ALLOSVFN                                                      
         LA    R2,@R2+1            Number of server entries                     
         SLL   R2,2                Times 4                                      
         LR    R0,R2                                                            
         VCALL GETCORE             Get memory for SVIT                          
         ST    R1,ALLOSVIT         Save SVIT ptr                                
         ST    R1,ALLONXP          Next entry ptr                               
         LR    R3,R1                                                            
         ZOT   @R3,(R2)            Zero SVIT                                    
         L     R1,ALLOSVIT                                                      
         AR    R1,R2                                                            
         ST    R1,ALLOENDP         Save end of SVIT ptr                         
         END                                                                    
*                                                                               
         L     R4,ALLONXP                                                       
         LOOP  BEGIN                                                            
         IF    ('LT R2,@R4',Z),EXIT                                             
         LA    R4,@R4+4            Next entry                                   
         IF    (R4,GE,ALLOENDP),'L R4,ALLOSVIT'  Wrap around                    
*                                                                               
         IF    (R4,EQ,ALLONXP),BEGIN  Should be very rare...                    
         ABORT 'No more servers can be allocated.'                              
         END                                                                    
         END                                                                    
*-                                                                              
*-       R4 now points to our SVIT table entry.                                 
*-                                                                              
         LR    R0,R4                                                            
         S     R0,ALLOSVIT         Offset in to table                           
         SRL   R0,2                Divide by 4 (entry number)                   
         A     R0,ALLOSVFN         Add first server number                      
         ST    R0,ALLONO           Save server number we assigned               
*-                                                                              
*-       Update SVIT information.                                               
*-                                                                              
         LA    R2,@R4+4                                                         
         IF    (R2,GE,ALLOENDP),'L R2,ALLOSVIT'  Wrap around                    
         ST    R2,ALLONXP          Save updated new entry ptr                   
*                                                                               
         IF    ALLOFPUB,BEGIN      Update public server info...                 
         MVC   CVPSTAB,ALLOSVIT    Save SVIT ptr                                
         MVC   CVPSENDP,ALLOENDP   Save end of SVIT ptr                         
         MVC   CVPSNXP,ALLONXP     Save next SVIT entry ptr                     
         END                                                                    
*                                                                               
         ELSE  BEGIN               Update private server info...                
         MVC   CVUSTAB,ALLOSVIT    Save SVIT ptr                                
         MVC   CVUSENDP,ALLOENDP   Save end of SVIT ptr                         
         MVC   CVUSNXP,ALLONXP     Save next SVIT entry ptr                     
         END                                                                    
*-                                                                              
*-       Create SVIB from the model passed to us and then save                  
*-         SVIB ptr in SVIT.                                                    
*-                                                                              
         LA    R0,L'SVIB                                                        
         VCALL GETCORE             Get memory for SVIB                          
         LR    R5,R1                                                            
         WITH  (SVIB,R5)                                                        
         MOVEL SVIB,L:ALLOMODP,L'SVIB  Copy SVIB                                
*                                                                               
         IF    ALLOFPUB,'SET SVIBFPUBLIC'  Set public server                    
         ELSE  BEGIN               Private server...                            
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15)                                                        
         ST    R15,SVIBCRJCB       Save creator's JCB ptr                       
         MVC   SVIBCRACCT,JCBACCT  Save creator'a account                       
         END                                                                    
*                                                                               
         MVC   SVIBNO,ALLONO       Set server number                            
*                                                                               
         ST    R5,@R4              Set SVIB ptr in SVIT                         
*                                                                               
         LA    R15,SVIB            Return newly created SVIB ptr                
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFREE -- Routine to free up all user connections.                         
*    This routine is called at the end of a user session.                       
*                                                                               
SERVFREE XPROC                                                                  
*-                                                                              
*-       Delete all our QINFO entries.                                          
*-                                                                              
         WHILE ('LT R5,CPQINFOQ',NZ),BEGIN  Delete QINFO entries...             
         WITH  (QINFO,R5)                                                       
         MVC   CPQINFOQ,QINFLINK   Dequeue this entry                           
         LA    R1,QINFO                                                         
         VCALL FREECORE            Release QINFO entry                          
         END                                                                    
*-                                                                              
*-       Delete all private servers.                                            
*-                                                                              
         IF    ('LT R6,CVUSTAB',NZ),BEGIN   User servers...                     
         WHILE (R6,LT,CVUSENDP),BEGIN  Go through user servers...               
         LT    R5,@R6              Get SVIB ptr                                 
         IF    NZ,BEGIN            We have a server...                          
         WITH  (SVIB,R5)                                                        
         IF    (SVIBCRJCB,EQ,CVCURJCB),BEGIN  We created this one...            
         LA    R15,SVIB                                                         
         ACALL SERVDEL             Delete this server                           
         END                                                                    
         END                                                                    
*                                                                               
         LA    R6,@R6+4            Next entry                                   
         END                                                                    
         END                                                                    
*-                                                                              
*-       Disconnect from public servers.                                        
*-                                                                              
         LT    R6,CVPSTAB           Public server table                         
         IF    NZ,BEGIN                                                         
         WHILE (R6,LT,CVPSENDP),BEGIN  Go through public servers...             
         LT    R5,@R6              Get SVIB ptr                                 
         IF    NZ,BEGIN            We have a server...                          
         WITH  (SVIB,R5)                                                        
         IF    (SVIBJCBP,EQ,CVCURJCB),BEGIN  Belongs to us...                   
         L     R15,SVIBNO          Server number                                
         ACALL SERVCLS             Close connection                             
         END                                                                    
         END                                                                    
*                                                                               
         LA    R6,@R6+4            Next entry                                   
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVOPEN -- Routine to open a query connection to the server                 
*    specified.  This routine takes care of creating a new server               
*    entry if none are idle.                                                    
*                                                                               
*    On entry:                                                                  
*      R1 - model SVIB ptr (with SVIBNAME or SVIBNO filled in)                  
*                                                                               
*    On exit, R15 (and cc):                                                     
*       0 - OK;                    R1 = SVIB ptr                                
*      -1 - network error;         R1,R0 = error msg loc,len                    
*       1 - query error;           R1,R0 = error msg loc,len                    
*       2 - no such server                                                      
*                                                                               
SERVOPEN XPROC (R2,LSR)                                                         
         LR    R5,R1               Save server name ptr                         
         WITH  (SVIB,R5)                                                        
*-                                                                              
*-       Handle opening a server explicitly by number.                          
*-                                                                              
         IF    (SVIBNAME,Z),BEGIN  No name...                                   
         L     R15,SVIBNO          Server number                                
         ACALL SERVGET             Get SVIB ptr                                 
         IF    Z,BEGIN             No such server...                            
         SETMSG 'NO SUCH SERVER'                                                
         LA    R15,2               No such server                               
         B     SOPNEXIT            Scram                                        
         END                                                                    
*                                                                               
         LR    R5,R15              Our SVIB ptr                                 
*                                                                               
         IF    ^SVIB#IDLE,BEGIN    Can't take this one...                       
         SETMSG 'SERVER NOT AVAILABLE'                                          
         LA    R15,2               Server is busy (probably)                    
         B     SOPNEXIT            Scram                                        
         END                                                                    
*                                                                               
         LA    R1,SVIB                                                          
         LCALL OPENSETU            Make SVIB ours                               
         IF    Z,'LA R1,SVIB'      Pass back SVIB ptr for caller                
         B     SOPNEXIT            All done                                     
         END                                                                    
*-                                                                              
*-       Try to open as a user server.                                          
*-                                                                              
         L     R0,CVUSTAB          Start of user SVIT                           
         L     R1,CVUSENDP         End of SVIT                                  
         LA    R15,SVIBNAME        Server name                                  
         LCALL OPENFIND            Try to open a server connection              
         IF    (R15,EQ,2),BEGIN    No such server...                            
*-                                                                              
*-       Try to open as a public server.                                        
*-                                                                              
         L     R0,CVPSTAB          Start of public SVIT                         
         L     R1,CVPSENDP         End of SVIT                                  
         LA    R15,SVIBNAME        Server name                                  
         LCALL OPENFIND            Open a server connection                     
         END                                                                    
*                                                                               
SOPNEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OPENFIND -- Local routine to find a matching idle server from the            
*    server list passed to it.  If there is a master definition                 
*    for the server, but no servers are currently idle then a new               
*    server entry will be created.                                              
*                                                                               
*    On entry:                                                                  
*      R0 - Start of SVIT ptr (or zero)                                         
*      R1 - End of SVIT ptr                                                     
*      R15 - Server name (L'SVIBNAME)                                           
*                                                                               
*    On exit, R15 (and cc):                                                     
*       0 - OK;                    R1 = SVIB ptr                                
*      -1 - network error;         R1,R0 = error msg loc,len                    
*       1 - query error;           R1,R0 = error msg loc,len                    
*       2 - no such server                                                      
*                                                                               
OPENWA   RECORD BEGIN                                                           
OPENSVIT DS    A                   Start of SVIT                                
OPENENDP DS    A                   End of SVIT                                  
*                                                                               
OPENNAME DS    CL(L'SVIBNAME)      Server name                                  
*                                                                               
OPENMASP DS    A                   Master SVIB ptr                              
OPENCURC DS    F                   No. of connections active                    
OPENOURC DS    F                   No. of our connections active                
         END                                                                    
*-                                                                              
OPENFIND PROC  (R2,LSR),OPENWA                                                  
         CLEAR OPENWA                                                           
         MVC   OPENNAME,@R15       Save server name                             
         ST    R0,OPENSVIT         Save start of SVIT ptr                       
         ST    R1,OPENENDP         Save end of SVIT ptr                         
*                                                                               
OPENRTRY CLEAR OPENMASP,OPENCURC,OPENOURC  Reset                                
         LT    R6,OPENSVIT         Get user server index table ptr              
         IF    Z,BEGIN             No servers at all...                         
         SETMSG 'NO SUCH SERVER'                                                
         LA    R15,2               No such server                               
         B     OFINEXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Find all servers with this name.                                       
*-                                                                              
         WHILE (R6,LT,OPENENDP),BEGIN  Go through user servers...               
         LT    R5,@R6              SVIB ptr                                     
         IF    NZ,BEGIN                                                         
         WITH  (SVIB,R5)                                                        
*                                                                               
         IF    ((SVIBCRJCB,NZ),AND,(SVIBCRJCB,NE,CVCURJCB)),EXIT                
*                                                                               
         IF    (OPENNAME,NE,SVIBNAME),EXIT  All done                            
*-                                                                              
*-       Save master server ptr.                                                
*-                                                                              
         IF    SVIB#MASTER,BEGIN   Master definition for server...              
         ST    R5,OPENMASP         Save master SVIB ptr                         
         END                                                                    
*-                                                                              
*-       Idle server.                                                           
*-                                                                              
         ELSEIF SVIB#IDLE,BEGIN    Idle server...                               
         LA    R1,SVIB                                                          
         LCALL OPENSETU            Make SVIB ours                               
         BNZ   OFINEXIT            Trouble, scram                               
         B     OFINDONE            All done                                     
         END                                                                    
*-                                                                              
*-       Busy server.                                                           
*-                                                                              
         ELSEIF SVIB#BUSY,BEGIN    Busy server...                               
         INCR  R15,OPENCURC        Count number of busy servers                 
         IF    (SVIBJCBP,EQ,CVCURJCB),'INCR R15,OPENOURC'  Count                
         END                                                                    
         END                                                                    
*                                                                               
         LA    R6,@R6+4            Next SVIT entry                              
         END                                                                    
*-                                                                              
*-       No available server was found.  If we found the master                 
*-         definition then we can start a new server; otherwise                 
*-         it's an unknown server name.                                         
*-                                                                              
         IF    (OPENMASP,Z),BEGIN  Not found...                                 
         SETMSG 'NO SUCH SERVER'                                                
         LA    R15,2               Not found                                    
         B     OFINEXIT                                                         
         END                                                                    
*-                                                                              
*-       Check to see if we are allowed to open another server                  
*-         connection.  If we already have the maximum number of                
*-         connections active then wait for a connection to free                
*-         up.                                                                  
*-                                                                              
         L     R15,OPENMASP        Master SVIB ptr                              
         WITH  (SVIB,R15),'L R2,SVIBMAX'  Max connections allowed               
         IF    (R2,NZ),BEGIN       There is a limit...                          
         IF    (OPENCURC,LT,R2),EXIT  OK to start a new one                     
         IF    (OPENCURC,EQ,OPENOURC),BEGIN                                     
         TSEG  'You currently have '                                            
         IF    (OPENOURC,EQ,1),'TSEG "an open connection "'                     
         ELSE  'TNUM L:OPENOURC; TSEG " open connections "'                     
         TSEG  'to server '                                                     
         TSEG  OPENNAME,,T                                                      
         TCR                                                                    
         TSEG  'Can''t open any more connections.'                              
         B     CVABORT                                                          
         END                                                                    
*                                                                               
         VCALL LOCALTOD                                                         
         XPUSH ,,32,PTR=R15                                                     
         VCALL FMTTIME                                                          
         TSEG  (R1),5,B            Hh:mm                                        
         XPOP  ,,32                                                             
*                                                                               
         TSEG  'Waiting for '                                                   
         TSEG  OPENNAME,,TB                                                     
         TSEG  'server'                                                         
*                                                                               
         IF    (OPENCURC,EQ,1),BEGIN  Only one connection allowed...            
         TSEG  ' (currently in use by another user)'                            
         END                                                                    
*                                                                               
         ELSE  BEGIN               Many connections but all in use...           
         TSEG  ' ('                                                             
         TNUM  L:OPENCURC                                                       
         TSEG  ' connections currently in use by other users)'                  
         END                                                                    
*                                                                               
         TWRITE                                                                 
         BNZ   CVABORT             Attn aborts                                  
*                                                                               
         L     R15,=A(15*100)      15 seconds                                   
         VCALL WAITER              Wait                                         
         BNZ   CVABORT             Attn aborts                                  
*                                                                               
         B     OPENRTRY            Go try it again                              
         END                                                                    
*-                                                                              
*-       We have the master definition, now create a new idle                   
*-         server entry from that definition.                                   
*-                                                                              
         L     R15,OPENMASP        Copy data from master definition             
         WITH  (SVIB,R15),BEGIN                                                 
         CLEAR R0                  Assume user server                           
         IF    SVIBFPUBLIC,'LA R0,1'  Public server                             
         END                                                                    
*        R0 = server type; R15 = model SVIB ptr                                 
         ACALL SERVALLO            Allocate a new server entry                  
         LR    R5,R15              Newly created SVIB ptr                       
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),OFINERR,'OPENFIND error.'                       
*                                                                               
         SET   SVIB#IDLE           Server is idle                               
*                                                                               
         LA    R1,SVIB                                                          
         LCALL OPENSETU            Make SVIB ours                               
         BNZ   OFINEXIT            Trouble, scram                               
         B     OFINDONE            All done                                     
*-                                                                              
*-       Open error; R15 return code already set.                               
*-                                                                              
OFINNERR LH    R15,=H'-1'          Network error return code                    
OFINERR  B     OFINEXIT                                                         
*                                                                               
OFINDONE LA    R1,SVIB             Return SVIB ptr                              
         CLEAR R15                 A-ok                                         
OFINEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OPENSETU -- Local routine to make the SVIB belong to the current             
*    user.                                                                      
*                                                                               
*    On entry:                                                                  
*      R1 - SVIB ptr                                                            
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*     -1 - network error;       R1,R0 - error msg loc, len                      
*      1 - query error;         R1,R0 - error msg loc, len                      
*      2 - SVIB not available;  R1,R0 - error msg loc, len                      
*                                                                               
OPENSETU PROC  (R2,LSR)                                                         
         LR    R5,R1                                                            
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),OPENSETU,'Bad SVIB ptr.'                        
         FAIL  (SVIBJCBP,NZ),OPENSETU,'Bad SVIB owner.'                         
*                                                                               
         IF    ^SVIB#IDLE,BEGIN    SVIB not available...                        
         SETMSG 'SERVER NOT AVAILABLE'                                          
         LA    R15,2                                                            
         B     SETUEXIT            Scram                                        
         END                                                                    
*                                                                               
         SET   SVIB#BUSY           Server is doing work now                     
         STCK  SVIBCLK             Clock when server became busy                
         MVC   SVIBJCBP,CVCURJCB   Our JCB ptr                                  
*-                                                                              
*-       Allocate a network path.                                               
*-                                                                              
         IF    (SVIBUNET,Z),BEGIN  Allocate network path...                     
         FAIL  SVIBFINIT,OPENSETU,'NPATALLO SETU logic error.'                  
*                                                                               
         CLEAR R0                  Assume user UNET                             
         IF    SVIBFPUBLIC,'LA R0,1'  Global UNET                               
         VCALL NPATALLO            Allocate a network path                      
         IF    NZ,'LH R15,=H"-1"; B SETUEXIT'  Can't allocate, scram            
         ST    R1,SVIBUNET         Save UNET ptr                                
*-                                                                              
*-       Set host name, port number and UNET options.                           
*-                                                                              
         L     R4,SVIBUNET                                                      
         WITH  (UNET,R4),BEGIN                                                  
         ST    R5,UNETSVIB         Save SVIB ptr in UNET                        
         MVC   UNETRNAME,SVIBRHOST Set server host name                         
         MVC   UNETRPORT,SVIBRPORT Set port number                              
         SET   UNETFKEEPTABS       Don't expand tabs                            
         MVC   UNETMAXL,=AL2(UNETBUF#-10)  Maximum allowed - slop               
         CLEAR UNETFWRAP           Truncate very long lines                     
         END                                                                    
         END                                                                    
*-                                                                              
*-       Mark this network connection as belonging to us.                       
*-                                                                              
         L     R4,SVIBUNET                                                      
         WITH  (UNET,R4)                                                        
         SEGDEF LA:UNETOUTSG       Our network seg buffer                       
         IF    SVIBFPUBLIC,'LA R15,UNET; VCALL NPATSETU'  Take it               
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVFBUF            Free any old buffers                         
*-                                                                              
*-       Post anyone waiting to monitor this server.                            
*-                                                                              
         LA    R3,CVQMONQ-(QMWLINK-QMW)  Dummy first entry                      
SETUQMON LOOP  BEGIN                                                            
         WITH  (QMW,R3)                                                         
         WHILE ('LT R3,QMWLINK',NZ)                                             
         IF    (QMWNAME,EQ,SVIBNAME),BEGIN  We matched a server...              
         IF    ('LT R2,QMWJCBP',NZ),BEGIN  Someone is waiting...                
         MVC   QMWNO,SVIBNO        Save our server number in QMW                
         WITH  (JCB,R2)                                                         
         IF    JCBWWFGEN,BEGIN     Waiting for our post...                      
         CLEAR JCBWWFGEN           Reset waiting                                
         IF    ^JCBAPDSP,'SETUP (R2)'  Start up the monitoring user             
         EXIT  SETUQMON            All done                                     
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Open connection and wait for greetings message.                        
*-                                                                              
         IF    ^SVIBFINIT,BEGIN    Initialize query connection...               
         LA    R15,UNET                                                         
         VCALL NPATOPEN            Open network connection                      
         IF    NZ,'LH R15,=H"-1"; B SETUEXIT'  Net trouble, scram               
*                                                                               
         LA    R1,=C'...'          Wait for server ready message                
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT            Trouble, go close connection                 
*                                                                               
         LA    R1,@R1+4            Skip over numeric response code              
         SH    R0,=H'4'                                                         
         IF    POS,BEGIN           We have some text...                         
         MVC   SVIBINFO,CVBLANKS                                                
         CEIL  R0,L'SVIBINFO       Not too long now                             
         LR    R15,R0                                                           
         MOVE  R15,SVIBINFO,@R1    Save server info text                        
         END                                                                    
*-                                                                              
*-       INFO:  TAP command to give server information about us.                
*-                                                                              
         SEG   'INFO '                                                          
         SEG   'Line '                                                          
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'SEGDC LH:JCBSEQ'  Line number                          
         SEG   ' '                                                              
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
         IF    (CPSNAME,NZ),'SEG " "; SEGT CPSNAME'                             
         SEG   ' -- '                                                           
         SEGT  CVMACHID                                                         
         SEG   '/'                                                              
         SEGT  CVWYLSSN                                                         
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT                                                         
*-                                                                              
*-       USER:  TAP command to identify the database userid.                    
*-                                                                              
         IF    (SVIBRUSER,NZ),BEGIN  Userid specified...                        
         SEG   'USER '                                                          
         SEGT  SVIBRUSER                                                        
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT            Trouble, go close connection                 
         END                                                                    
*-                                                                              
*-       PASS:  TAP command to give the password for the userid.                
*-                                                                              
         IF    (SVIBRPASS,NZ),BEGIN  Password specified...                      
         SEG   'PASS '                                                          
         SEGT  SVIBRPASS                                                        
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT            Trouble, go close connection                 
         END                                                                    
*-                                                                              
*-       DATABASE:  TAP command to identify the database name.                  
*-                                                                              
         SEG   'DATABASE '                                                      
         SEGT  SVIBRDB                                                          
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT            Trouble, go close connection                 
*                                                                               
         SET   SVIBFINIT           Connection initialized                       
*                                                                               
         INCR  R15,CVCSCONN        Count the new connection                     
         END                                                                    
*-                                                                              
*-       RESET:  TAP command to reset user controlled options.                  
*-               We send this command in an attempt to cleanup                  
*-               after the previous user of this server.                        
*-                                                                              
         IF    SVIBFREUSE,BEGIN    Must reset if reusable...                    
         SEGCR 'RESET'             Reset the connection                         
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT            Trouble, scram                               
*                                                                               
         SEG   'INFO '                                                          
         SEG   'Line '                                                          
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'SEGDC LH:JCBSEQ'  Line number                          
         SEG   ' '                                                              
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
         IF    (CPSNAME,NZ),'SEG " "; SEGT CPSNAME'                             
         SEG   ' -- '                                                           
         SEGT  CVMACHID                                                         
         SEG   '/'                                                              
         SEGT  CVWYLSSN                                                         
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for positive completion                 
         LA    R15,UNET            UNET ptr                                     
         ACALL SERVREAD            Get response                                 
         BNZ   SETUEXIT                                                         
         END                                                                    
*                                                                               
         INCR  R15,CVCSREQ         Count the server request                     
*                                                                               
         CLEAR R15                 A-ok                                         
*-                                                                              
*-       If we are going to return an error here, we have to clean              
*-         up the connection since SERVCLS will not be called by                
*-         our caller.                                                          
*-                                                                              
SETUEXIT IF    (R15,NZ),BEGIN      Bad return code...                           
         XPUSH R15,R1              Save return code info                        
*-                                                                              
*-       We couldn't connect so make server idle again.                         
*-                                                                              
         SET   SVIB#IDLE           Server is now idle                           
         CLEAR SVIBJCBP            No current owner                             
*                                                                               
         IF    SVIBFPUBLIC,BEGIN   Public server...                             
         L     R15,SVIBUNET                                                     
         VCALL NPATCLRU            Clear owner fields                           
         END                                                                    
*-                                                                              
*-       We didn't even initialize so release the network                       
*-         connection.                                                          
*-                                                                              
         IF    ^SVIBFINIT,BEGIN    We didn't initialize...                      
         CLEAR SVIBUNET                                                         
         CLEAR UNETSVIB            Break this connection, too  MPD              
         MVC   SVIBINFO,=CL(L'SVIBINFO)'No network connection'                  
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATCLS             Close connection (& free UNET)               
         END                                                                    
*-                                                                              
*-       Close our side of the network connection if the other side             
*-         was closed.  We don't actually free the server entry,                
*-         the next time the SVIB is used the network connection                
*-         will be re-allocated.                                                
*-                                                                              
         LT    R2,SVIBUNET         UNET ptr                                     
         IF    NZ,BEGIN            Active network connection...                 
         WITH  (UNET,R2)                                                        
         FAIL  (UNET,NE,'UNET'),SERVCLS,'UNET error.'                           
         IF    (UNETFINEOF,OR,UNETFINABORT,OR,                         *        
               (UNETSTATE,GE,UNETSFABORT)),BEGIN  Close...                      
         CLEAR SVIBUNET            No network connection                        
         CLEAR UNETSVIB            Break this connection, too  MPD              
         CLEAR SVIBFINIT           Reset initialized flag                       
         MVC   SVIBINFO,=CL(L'SVIBINFO)'No network connection'                  
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATCLS             Close our side of net connection             
         END                                                                    
         END                                                                    
*                                                                               
         XPOP  R15,R1              Restore original rc info                     
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVCMDL -- Routine to send a command to the query server.                   
*                                                                               
*    On entry:                                                                  
*      R15   - SVIB ptr                                                         
*      R1,R0 - command loc, len                                                 
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - OK                                                                   
*      nz- error; R1,R0 - error message loc, len                                
*                                                                               
SVCLWA   RECORD BEGIN                                                           
SVCLSGP  DS    A                   Network SEGCB ptr                            
SVCLPTRS DS    2A                  Command len, loc                             
         END                                                                    
*-                                                                              
SERVCMDL PROC  SVCLWA                                                           
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVCMDL,'Bad SVIB ptr.'                        
*                                                                               
         STM   R0,R1,SVCLPTRS      Save command ptrs                            
*                                                                               
         L     R15,SVIBUNET                                                     
         WITH  (UNET,R15),'LA R1,UNETOUTSG'                                     
         ST    R1,SVCLSGP          Network output SEGCB ptr                     
         SEGDEF L:SVCLSGP                                                       
*-                                                                              
*-       First we need to wait for the previous FETCH to complete.              
*-                                                                              
         IF    SVIBSFFETCH,BEGIN   We are fetching now...                       
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors on FETCH completion.)                      
         SET   SVIBSFNORMAL        Reset state to normal                        
         END                                                                    
*-                                                                              
*-       Send TAP DATA command if this is the first line of data.               
*-                                                                              
         IF    ^SVIBSFDATA,BEGIN   Data...                                      
         SEG   'DATA'                                                           
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'354'          Wait for intermediate response               
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get "go ahead" response                      
         IF    NZ,BEGIN            Trouble...                                   
         PRETURN (R0,R1)                                                        
         B     SCMDEXIT                                                         
         END                                                                    
*                                                                               
         SET   SVIBSFDATA          DATA command has been sent                   
         END                                                                    
*-                                                                              
*-       Send the command line.                                                 
*-                                                                              
         LM    R0,R1,SVCLPTRS                                                   
         SEG   (R1),(R0)           Command text                                 
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         CLEAR R15                 A-ok                                         
SCMDEXIT PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFET -- Routine to start fetching the responses from                      
*    the server.  If necessary this routine will end the data                   
*    stream.                                                                    
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      nz- error; R1,R0 = error message                                         
*                                                                               
SERVFET  PROC  (R2,LSR)                                                         
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVFET,'Bad SVIB ptr.'                         
*                                                                               
         L     R15,SVIBUNET                                                     
         WITH  (UNET,R15),'LA R4,UNETOUTSG'                                     
         SEGDEF (R4)                                                            
*-                                                                              
*-       Send end of data if necessary.                                         
*-                                                                              
         IF    SVIBSFDATA,BEGIN    Send end of data sequence...                 
         SEGCR '.'                 End of data                                  
         SEGWR                                                                  
*                                                                               
         SET   SVIBSFNORMAL        Data mode has ended now                      
*                                                                               
SFETLOOP LA    R1,=C'1..'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get "go ahead" response                      
         BNZ   SVFTEXIT            Trouble                                      
*                                                                               
         IF    ((R0,POS),AND,(@R1,EQ,'1')),BEGIN  Info...                       
         IF    (@R1,EQ,'11'),BEGIN  11x messages...                             
         XPUSH R0,R1                                                            
         TSEG  '*** '                                                           
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         B     SFETLOOP                                                         
         END                                                                    
*                                                                               
         IF    (@R1,EQ,'15'),BEGIN  15x messages...                             
         XPUSH R0,R1                                                            
         TSEG  '*** Data info: '                                                
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         B     SFETLOOP                                                         
         END                                                                    
         END                                                                    
*-                                                                              
*-       Now send FETCH ROWS command.                                           
*-                                                                              
         SEGCR 'FETCH ROWS'        Get all rows                                 
         SEGWR                                                                  
*                                                                               
         SET   SVIBSFFETCH         Now doing FETCH ROWS command                 
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
SVFTEXIT PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVGETL -- Routine to get the next line of response data from               
*    the server.                                                                
*                                                                               
*    If SVIBFBUFFER is set then a table worth of data is locally                
*    buffered (if possible) before the first line is returned.                  
*    This is so that we can intelligently format the responses.                 
*                                                                               
*    On entry:                                                                  
*      R15   - SVIB ptr                                                         
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - next response line returned in R1,R0                                 
*      nz- error of some sort; R1,R0 - error message                            
*                                                                               
SVGLWA   RECORD BEGIN                                                           
SVGLSGP  DS    A                   Network SEGCB ptr                            
         END                                                                    
*-                                                                              
SERVGETL PROC  (R2,LSR),SVGLWA                                                  
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVGETL,'Bad SVIB ptr.'                        
*                                                                               
         L     R15,SVIBUNET                                                     
         WITH  (UNET,R15),'LA R1,UNETOUTSG'                                     
         ST    R1,SVGLSGP          Network output SEGCB ptr                     
         SEGDEF L:SVGLSGP                                                       
*-                                                                              
*-       Start fetching the responses now.                                      
*-                                                                              
         IF    ^SVIBSFFETCH,BEGIN  Start fetching rows...                       
         LA    R15,SVIB                                                         
         ACALL SERVFET             Start fetching                               
         BNZ   SVGLEXIT            Trouble, scram                               
         END                                                                    
*-                                                                              
*-       Free top most buffered line (the one we processed the                  
*-         last time this routine was called).                                  
*-                                                                              
         IF    ('LT R1,SVIBINQH',NZ),BEGIN  Take top entry...                   
         WITH  (INENTRY,R1)                                                     
         L     R2,INLINK           Get next INENTRY ptr                         
         MVC   INLINK,=F'-1'       Safety                                       
         VCALL FREECORE            Release entry                                
*                                                                               
         ST    R2,SVIBINQH         Update queue head                            
         IF    (R2,Z),'ST R2,SVIBINQT'  Zero queue tail too                     
*                                                                               
         DECR  R15,SVIBINCT        One less entry now                           
         END                                                                    
*                                                                               
         IF    (SVIBINQH,NZ),SVGLNEXT  Return next line                         
*-                                                                              
*-       Attempt to fetch a line when we don't have any more                    
*-         data to return.                                                      
*-                                                                              
         IF    SVIBSFNORMAL,BEGIN  In normal waiting state...                   
         SETMSG SVGLNDAT            "570 NOTHING TO FETCH"                      
         LA    R15,4                                                            
         B     SVGLEXIT                                                         
         END                                                                    
*-                                                                              
*-       Buffer the response lines from the server.                             
*-                                                                              
         IF    SVIBFBUFFER,BEGIN   Buffered mode...                             
         LOOP  BEGIN                                                            
SVGLATL1 L     R15,SVIBUNET                                                     
         VCALL NPATREAD            Get next line                                
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'ATTN'),BEGIN  Send an ABORT request...                  
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
         B     SVGLATL1            Wait for completion                          
         END                                                                    
*                                                                               
         B     SVGLEXIT            Error of some sort                           
         END                                                                    
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVFMAP            Update field descriptor info                 
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVBUF             Buffer this line                             
         IF    NZ,EXIT             Can't buffer any more, scram                 
*                                                                               
         IF    ((R0,GE,3),AND,(@R1,EQ,'109')),EXIT  End of table                
*                                                                               
         IF    ((R0,POS),AND,(@R1,EQ,'2')),EXIT  Normal end                     
         IF    ((R0,POS),AND,(@R1,EQ,'4')),EXIT  Abnormal end                   
         IF    ((R0,POS),AND,(@R1,EQ,'5')),EXIT  Abnormal end                   
         END                                                                    
*-                                                                              
*-       We are done buffering, return the first line in the                    
*-         buffer.                                                              
*-                                                                              
         IF    (SVIBINQH,NZ),SVGLNEXT  Return next line                         
         END                                                                    
*-                                                                              
*-       Read the next response line (non-buffered mode).                       
*-                                                                              
SVGLATL2 L     R15,SVIBUNET                                                     
         VCALL NPATREAD            Get next line                                
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'ATTN'),BEGIN  Send an ABORT request...                  
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
         B     SVGLATL2            Wait for completion                          
         END                                                                    
         END                                                                    
*                                                                               
         B     SVGLOK              Return this line                             
*-                                                                              
*-       Return a pointer to the next line buffered.                            
*-                                                                              
SVGLNEXT L     R2,SVIBINQH         Take first entry on queue                    
         WITH  (INENTRY,R2),BEGIN                                               
         SETMSG INTEXT,LH:INLEN     Response line                               
         END                                                                    
*                                                                               
SVGLOK   IF    (R0,POS),BEGIN      Check for final response...                  
         IF    ((@R1,EQ,'2'),OR,(@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN             
         SET   SVIBSFNORMAL        Reset state if final response                
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
SVGLEXIT PEND                                                                   
*                                                                               
SVGLNDAX DC    C'570'                                                           
         DC    X'05'                                                            
         DC    C'Nothing to fetch, no command in progress.'                     
SVGLNDAT EQU   SVGLNDAX,*-SVGLNDAX,C'X'                                         
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVCHKL -- Routine to check for responses from the server.  This            
*    routine is called when we are sending a lot of data to the                 
*    server to see if it wants us to stop.                                      
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
SERVCHKL PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVCHKL,'Bad SVIB ptr.'                        
*                                                                               
         L     R4,SVIBUNET                                                      
         WITH  (UNET,R4)                                                        
         FAIL  (UNET,NE,'UNET'),SERVCHKL,'Bad UNET ptr.'                        
*-                                                                              
*-       Read in any input responses.                                           
*-                                                                              
         LOOP  BEGIN                                                            
         SET   UNETFNOWAIT         Don't wait for data                          
         LA    R15,UNET                                                         
         VCALL NPATREAD            Get any response lines                       
         CLEAR UNETFNOWAIT         Reset                                        
         IF    (R15,NZ),EXIT       No new data, fine                            
         XPUSH R0,R1                                                            
         TSEG  '*** Immediate info: '                                           
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         XPOP  R0,R1                                                            
*                                                                               
         IF    (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,'2'),CVNEXT                                              
         IF    (@R1,EQ,'4'),CVERROR                                             
         IF    (@R1,EQ,'5'),CVERROR                                             
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 Neatness                                     
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFMTL -- Routine to format a response line.                               
*                                                                               
*    On entry:                                                                  
*      R15   - SVIB ptr                                                         
*      R2    - SEGCB ptr                                                        
*      R1,R0 - respone line loc, len                                            
*                                                                               
SVFMWA   RECORD BEGIN                                                           
SVFMSGP  DS    A                   SEGCB ptr                                    
         END                                                                    
*-                                                                              
SERVFMTL PROC  SVFMWA                                                           
         CLEAR SVFMWA                                                           
         ST    R2,SVFMSGP          Save SEGCB ptr                               
         SEGDEF L:SVFMSGP                                                       
*                                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVFMTL,'Bad SVIB ptr.'                        
*-                                                                              
*-       Display data line.                                                     
*-                                                                              
         IF    (@R1,EQ,$TAB),BEGIN  Tab separated data line...                  
         LA    R1,@R1+1            Skip past leading TAB char                   
         DECR  R0                                                               
         L     R2,SVIBFMAP         Field descriptor map                         
         SEGLD ,                   Output SEGCB                                 
         ACALL SEGFLDS             Write our fields                             
         SEGCR                                                                  
         IF    NZ,BEGIN            Attn...                                      
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
         END                                                                    
         END                                                                    
*-                                                                              
*-       Intermediate information message.                                      
*-                                                                              
         ELSEIF (@R1,EQ,'1'),BEGIN  Information message...                      
*-                                                                              
*-       Display start of table line.                                           
*-                                                                              
         IF    (@R1,EQ,'100'),BEGIN  Start of table line...                     
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         XPUSH R0,R1                                                            
         SEG   '*** '                                                           
         XPOP  R0,R1                                                            
         VCALL LRTRIM                                                           
         IF    (R0,NP),'SETMSG "Start of table"'                                
         SEGCR (R1),(R0)           Write out line                               
         END                                                                    
*-                                                                              
*-       Display table column headings.                                         
*-                                                                              
         ELSEIF (@R1,EQ,'101'),BEGIN  Table columns line...                     
         IF    SVIBFENDTAB,'XPUSH R0,R1; SEGCR; XPOP R0,R1'                     
*                                                                               
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         IF    POS,BEGIN           We have column titles...                     
         L     R2,SVIBFMAP         Field descriptors                            
         SEGLD ,                   Output SEGCB                                 
         ACALL SEGFLDS             Write fields                                 
         SEGCR                                                                  
*-                                                                              
*-       Generate a dashed line.                                                
*-                                                                              
         LT    R2,SVIBFMAP         Get field descriptor map ptr                 
         IF    NZ,BEGIN            We have some fields...                       
         WITH  (FDE,R2)                                                         
         WHILE (FDETYPE,NZ),BEGIN  Go through fields...                         
*                                                                               
         XPUSH R2                                                               
         LH    R2,FDELEN           Total length for dashed line                 
         WHILE (R2,POS),BEGIN      Write out dashed line...                     
         LR    R0,R2                                                            
         CEIL  R0,64                                                            
         SR    R2,R0                                                            
         SEG   =64C'-',(R0)        Dashed line                                  
         END                                                                    
         XPOP  R2                                                               
*                                                                               
         SEG   '  '                                                             
*                                                                               
         LA    R2,FDENEXT          Next field                                   
         END                                                                    
         END                                                                    
*                                                                               
         SEGCR                                                                  
         END                                                                    
         END                                                                    
*-                                                                              
*-       Display end of table line.                                             
*-                                                                              
         ELSEIF (@R1,EQ,'109'),BEGIN  End of table line...                      
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         XPUSH R0,R1                                                            
         SEG   '*** '                                                           
         XPOP  R0,R1                                                            
         VCALL LRTRIM                                                           
         IF    (R0,NP),'SETMSG "End of table"'                                  
         SEGCR (R1),(R0)           Write out line                               
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVFBUF            Free remaining buffered stuff                
*                                                                               
         SET   SVIBFENDTAB         End of table reached                         
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'15'),BEGIN  Info message...                            
         XPUSH R0,R1                                                            
         SEG   '*** Info: '                                                     
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         SEG   '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         SEG   @R1,3                                                            
         SEG   ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         SEGCR (R1),(R0)                                                        
         END                                                                    
*-                                                                              
*-       All other informational messages.                                      
*-                                                                              
         ELSE  BEGIN               Informational message...                     
         COMMENT ,                 Ignore other info msgs for now               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Normal end of command.                                                 
*-                                                                              
         ELSEIF (@R1,EQ,'2'),BEGIN  Normal completion...                        
         SET   SVIBSFNORMAL        Normal state                                 
         XPUSH R0,R1                                                            
         SEG   '*** Normal completion: '                                        
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         SEG   '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         SEG   @R1,3                                                            
         SEG   ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         SEGCR (R1),(R0)                                                        
         END                                                                    
*-                                                                              
*-       Abnormal end of command.                                               
*-                                                                              
         ELSEIF ((@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN  Error...                  
         SET   SVIBSFNORMAL        Normal state                                 
         XPUSH R0,R1                                                            
         SEG   '*** Error completion: '                                         
         XPOP  R0,R1                                                            
*                                                                               
         IF    (@R1+3,EQ,X'05'),BEGIN  Display as "(nnn) message"...            
         XPUSH R0,R1                                                            
         SEG   '('                                                              
         XPOP  R0,R1                                                            
         XPUSH R0,R1                                                            
         SEG   @R1,3                                                            
         SEG   ') '                                                             
         XPOP  R0,R1                                                            
         LA    R1,@R1+4                                                         
         SH    R0,=H'4'                                                         
         END                                                                    
*                                                                               
         SEGCR (R1),(R0)                                                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               Something unexpected...                      
         XPUSH R0,R1                                                            
         SEG   '*** Unexpected data: '                                          
         XPOP  R0,R1                                                            
         SEGCR (R1),(R0)           Data                                         
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVABRT -- Routine to tell the server to stop sending data                  
*    (i.e., send a TAP ABORT).                                                  
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
SERVABRT PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
*                                                                               
         LT    R15,SVIBUNET                                                     
         IF    NZ,BEGIN                                                         
         WITH  (UNET,R15),'LA R4,UNETOUTSG'                                     
         SEGDEF (R4)               Network output SEGCB ptr                     
*-                                                                              
*-       Only send TAP ABORT the first time.                                    
*-                                                                              
         IF    ^SVIBFABORT,BEGIN   Send an ABORT request...                     
         SEGCR 'ABORT'                                                          
         SEGWR                                                                  
         SET   SVIBFABORT          Remember that we sent this                   
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 Neatness                                     
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFMAP -- Routine to update field descriptors based on the                 
*     data line passed.                                                         
*                                                                               
*     On entry:                                                                 
*       R15   - SVIB ptr                                                        
*       R1,R0 - data line loc, len                                              
*                                                                               
FMAPWA   RECORD BEGIN                                                           
FMAPTOK  DS    CL8                 Working token area                           
         END                                                                    
*-                                                                              
SERVFMAP PROC  FMAPWA                                                           
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVFMAP,'Bad SVIB ptr.'                        
*                                                                               
         LR    R3,R1               Copy original line ptrs                      
         LR    R2,R0                                                            
*-                                                                              
*-       Get field descriptor map addressibility.                               
*-                                                                              
         L     R6,SVIBFMAP         Get FDEMAP ptr                               
         WITH  (FDEMAP,R6)                                                      
*                                                                               
         IF    (R6,Z),BEGIN        No FDEMAP, allocate one...                   
         LA    R0,L'FDEMAP                                                      
         VCALL GETCORE             Get room for FDEMAP                          
         LR    R6,R1                                                            
         ST    R6,SVIBFMAP         Save FDEMAP ptr                              
         CLEAR FDEMAP              Init FDEMAP                                  
         END                                                                    
*-                                                                              
*-       Data line.                                                             
*-                                                                              
         IF    ((R2,POS),AND,(@R3,EQ,$TAB)),BEGIN  Data line...                 
         LA    R3,@R3+1            Skip past leading tab                        
         DECR  R2                  Keep length accurate                         
         LA    R4,FDEMAP                                                        
         WHILE ('ACALL GETFIELD',Z),BEGIN  Step through fields...               
         WITH  (FDE,R4)                                                         
         IF    (FDETYPE,Z),'SET FDETLEFT'  Assume left centered                 
         IF    (R0,GT,FDELEN),'STH R0,FDELEN'  Max length so far                
*                                                                               
         LA    R4,FDENEXT          Next entry                                   
         LA    R15,FDEMAPE                                                      
         IF    (R4,GE,R15),EXIT    Too many fields, scram                       
         END                                                                    
         END                                                                    
*-                                                                              
*-       101: Column names.                                                     
*-                                                                              
         ELSEIF ((R2,GE,3),AND,(@R3,EQ,'101')),BEGIN  Col names...              
         LA    R3,@R3+4            Skip past numeric id                         
         SH    R2,=H'4'            Keep length accurate                         
         LA    R4,FDEMAP                                                        
         WHILE ('ACALL GETFIELD',Z),BEGIN  Step through fields...               
         WITH  (FDE,R4)                                                         
         IF    (FDETYPE,Z),'SET FDETLEFT'  Assume left centered                 
         IF    (R0,GT,FDELEN),'STH R0,FDELEN'  Max length so far                
*                                                                               
         LA    R4,FDENEXT          Next entry                                   
         LA    R15,FDEMAPE                                                      
         IF    (R4,GE,R15),EXIT    Too many fields, scram                       
         END                                                                    
         END                                                                    
*-                                                                              
*-       102: Column types.                                                     
*-                                                                              
         ELSEIF ((R2,GE,3),AND,(@R3,EQ,'102')),BEGIN  Col types...              
         LA    R3,@R3+4            Skip past numeric id                         
         SH    R2,=H'4'            Keep length accurate                         
         LA    R4,FDEMAP                                                        
         WHILE ('ACALL GETFIELD',Z),BEGIN  Step through fields...               
         WITH  (FDE,R4)                                                         
         IF    (FDETYPE,Z),'SET FDETLEFT'  Assume left centered                 
         IF    (R0,GE,6),BEGIN     Type name is 6+ chars long...                
         MVC   FMAPTOK,@R1                                                      
         OC    FMAPTOK,CVBLANKS    Convert to upper case                        
         IF    (FMAPTOK,EQ,'NUMBER'),'SET FDETRIGHT'  Right centered            
         END                                                                    
*                                                                               
         LA    R4,FDENEXT          Next entry                                   
         LA    R15,FDEMAPE                                                      
         IF    (R4,GE,R15),EXIT    Too many fields, scram                       
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVBUF -- Routine to buffer an input line from the query                    
*    server.                                                                    
*                                                                               
*      On entry:                                                                
*        R1,R0 - line location, length                                          
*        R15   - SVIB ptr                                                       
*                                                                               
*      On exit, R15 (and cc):                                                   
*        0 - OK                                                                 
*        4 - OK; but don't buffer any more                                      
*                                                                               
SERVBUF  PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVFMAP,'Bad SVIB ptr.'                        
*                                                                               
         LR    R3,R1                                                            
         LR    R2,R0                                                            
*                                                                               
         AH    R0,=AL2(L'INENTRY)                                               
         VCALL GETCORE                                                          
         LR    R4,R1                                                            
         WITH  (INENTRY,R4)                                                     
         CLEAR INENTRY             Initialize new entry                         
         STH   R2,INLEN            Save text length                             
         MOVEL INTEXT,@R3,(R2)     Copy in data                                 
*                                                                               
         LT    R2,SVIBINQT         Old queue tail                               
         IF    Z,BEGIN             Nothing on queue...                          
         ST    R4,SVIBINQH         We are the queue head                        
         ST    R4,SVIBINQT         ...and the queue tail                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               Add us to the end of the queue...            
         ST    R4,INLINK-INENTRY(,R2)  Link us to old last entry                
         ST    R4,SVIBINQT         We are the new queue tail                    
         END                                                                    
*                                                                               
         INCR  R15,SVIBINCT        Kick count of entries                        
         IF    (R15,GT,SVIBIN#),'LA R15,4'  Too many                            
         ELSE  'CLEAR R15'         OK                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVREAD -- Routine to get the next TAP control line from                    
*    the network path.                                                          
*                                                                               
*    On entry:                                                                  
*      R15- UNET ptr                                                            
*      R1 - "x.."   String (the first character is a code which                 
*                     will complete the read -- see TAP doc).                   
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok;              R1,R0 - data line loc, len                          
*     -1 - network error;   R1,R0 - error msg loc, len                          
*      1 - query error;     R1,R0 - error msg loc, len                          
*                                                                               
READWA   RECORD BEGIN                                                           
READOPT  DS    CL3                 Option code from entry                       
READCOMP EQU   READOPT,1,C'X'      Stop on this numeric code                    
         END                                                                    
*-                                                                              
SERVREAD XPROC (R2,LSR),READWA                                                  
         MVC   READOPT,@R1         Save option code                             
*                                                                               
         LR    R6,R15                                                           
         WITH  (UNET,R6)                                                        
         SEGDEF UNETOUTSG          Network output seg buffer                    
*-                                                                              
*-       Keep reading lines until we get the response code that                 
*-         we are looking for.                                                  
*-                                                                              
RRDLOOP  LOOP  BEGIN                                                            
         LA    R15,UNET                                                         
         VCALL NPATREAD            Get next input line                          
         BNZ   READNERR            Network error, scram                         
*-                                                                              
*-       Return if we get what we were waiting for, or if                       
*-         we get a final completion.                                           
*-                                                                              
         IF    (@R1,EQ,READCOMP),EXIT  We got what we wanted                    
*                                                                               
         IF    (@R1,EQ,'2'),EXIT   Positive completion                          
*-                                                                              
*-       Check out the response line to see if it is a fatal error.             
*-         TAP control messages begin with a three digit code.                  
*-                                                                              
         IF    ((@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN  Error...                   
         LA    R1,@R1+4            Skip past numeric ID                         
         SH    R0,=H'4'                                                         
         IF    NP,'SETMSG "ERROR"'    (No error message text)                   
         LA    R15,1               Query error                                  
         B     READEXIT                                                         
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         B     READEXIT                                                         
*                                                                               
READNERR LH    R15,=H'-1'          Network error return code                    
READEXIT XPUSH R15,R4                                                           
         LR    R2,R15                                                           
         LR    R3,R1                                                            
         LR    R4,R0                                                            
         DEBUG 'SERVREAD returns ',R2,' -- ',(@R3,(R4))                         
         XPOP  R15,R4                                                           
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVCLS -- Routine to close the server specified.  Actually,                 
*    all this routine currently does is send a RESET command to                 
*    the server and then mark the server entry as idle.                         
*                                                                               
*    On entry:                                                                  
*      R15 - server number                                                      
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - closed                                                               
*      4 - server not found                                                     
*                                                                               
SERVCLS  XPROC                                                                  
         ACALL SERVGET             Get SVIB ptr                                 
         IF    Z,'LA R15,4; B SCLSEXIT'  No such server                         
*                                                                               
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVCLS,'SVIB error.'                           
*                                                                               
         FAIL  ^SVIB#BUSY,SERVCLS,'SVIB status error.'                          
         FAIL  (SVIBJCBP,NE,CVCURJCB),SERVCLS,'SVIB JCB error.'                 
*-                                                                              
*-       Free up any remaining buffered lines.                                  
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVFBUF            Free any buffered lines                      
*-                                                                              
*-       Close our side of the network connection if the other side             
*-         was closed.  We don't actually free the server entry,                
*-         the next time the SVIB is used the network connection                
*-         will be re-allocated.                                                
*-                                                                              
         LT    R2,SVIBUNET         UNET ptr                                     
         IF    NZ,BEGIN            Active network connection...                 
         WITH  (UNET,R2)                                                        
         FAIL  (UNET,NE,'UNET'),SERVCLS,'UNET error.'                           
         IF    (UNETFINEOF,OR,UNETFINABORT,OR,                         *        
               (UNETSTATE,GE,UNETSFABORT)),BEGIN  Close...                      
         CLEAR SVIBUNET            No network connection                        
         CLEAR SVIBFINIT           Reset initialized flag                       
         MVC   SVIBINFO,=CL(L'SVIBINFO)'No network connection'                  
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATCLS             Close our side of net connection             
         END                                                                    
         END                                                                    
*-                                                                              
*-       Free server entry if closing or not re-usable.                         
*-         We try to do it the nice way by sending a QUIT                       
*-         rather than just closing the path.                                   
*-                                                                              
         IF    (^SVIBFREUSE,OR,SVIBFCLOSING),BEGIN  Closing...                  
         LT    R4,SVIBUNET                                                      
         IF    NZ,BEGIN            Network path is allocated...                 
         WITH  (UNET,R4)                                                        
*-                                                                              
*-       Terminate sending input data.                                          
*-                                                                              
         IF    SVIBSFDATA,BEGIN    Sending data...                              
         SEGCR '.',,UNETOUTSG      End of data                                  
         SEGWR                                                                  
         SET   SVIBSFNORMAL        Reset to normal                              
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors on QUIT completion.)                       
         END                                                                    
*-                                                                              
*-       Send QUIT and get acknowlegement.                                      
*-                                                                              
         SEGCR 'QUIT',,UNETOUTSG                                                
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors on QUIT completion.)                       
         END                                                                    
*                                                                               
         LA    R15,SVIB                                                         
         ACALL SERVDEL                                                          
*                                                                               
         CLEAR R15                 A-ok                                         
         B     SCLSEXIT                                                         
         END                                                                    
*-                                                                              
*-       Send end of data if necessary.                                         
*-                                                                              
         IF    SVIBSFDATA,BEGIN    Send end of data sequence...                 
         LT    R4,SVIBUNET                                                      
         IF    NZ,BEGIN            Network connection is active...              
         WITH  (UNET,R4)                                                        
         SEGCR '.',,UNETOUTSG      End of data                                  
         SEGWR                                                                  
*                                                                               
         SET   SVIBSFNORMAL        Reset to normal                              
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get "go ahead" response                      
*  (Don't bother checking for errors on DATA completion.)                       
         END                                                                    
         END                                                                    
*-                                                                              
*-       Wait for FETCH to complete.                                            
*-                                                                              
         IF    SVIBSFFETCH,BEGIN   We are fetching now...                       
         LT    R4,SVIBUNET                                                      
         IF    NZ,BEGIN                                                         
         LA    R15,SVIB                                                         
         ACALL SERVABRT            Send ABORT                                   
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors on FETCH completion.)                      
         END                                                                    
         SET   SVIBSFNORMAL        Reset state to normal                        
         END                                                                    
*-                                                                              
*-       Reset the server options so that it can be reused by                   
*-         the next user.                                                       
*-                                                                              
         LT    R4,SVIBUNET                                                      
         IF    NZ,BEGIN            Network path is allocated...                 
         WITH  (UNET,R4)                                                        
         SEGCR 'RESET',,UNETOUTSG                                               
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for completion response                 
         L     R15,SVIBUNET                                                     
         ACALL SERVREAD            Get completion                               
*  (Don't bother checking for errors on RESET completion.)                      
         END                                                                    
*-                                                                              
*-       Network connection no longer belongs to us if this is                  
*-         a public server.                                                     
*-                                                                              
         IF    SVIBFPUBLIC,BEGIN   Public server...                             
         LT    R15,SVIBUNET                                                     
         IF    Z,EXIT              No network path, scram                       
         VCALL NPATCLRU            Clear owner fields                           
         END                                                                    
*-                                                                              
*-       Mark server as idle.                                                   
*-                                                                              
         SET   SVIB#IDLE           Server is now idle                           
         CLEAR SVIBJCBP            Not owned by anyone now                      
*                                                                               
         CLEAR R15                 A-ok                                         
SCLSEXIT PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Safety)                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVTRC -- Routine to queue a trace buffer for debugging                     
*    (if tracing or monitoring has been turned on).                             
*                                                                               
*    On entry:                                                                  
*      R15   - SVIB ptr                                                         
*      R1,R0 - trace data loc, len                                              
*      R2    - address of 3 char trace type                                     
*                                                                               
SERVTRC  XPROC                                                                  
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVTRC,'Bad SVIB ptr'                          
*-                                                                              
*-       Only save the buffer if tracing or monitoring is requested.            
*-                                                                              
         IF    SVIBFTRC+SVIBFMON,BEGIN  Tracing/monitoring...                   
         CEIL  R0,STRCMXDL         Not too long now                             
*                                                                               
         LR    R4,R1                                                            
         LR    R3,R0                                                            
*                                                                               
         AH    R0,=AL2(L'STRCHDR)                                               
         VCALL GETCORE             Get a new trace buffer                       
         LR    R6,R1                                                            
         WITH  (STRC,R6)                                                        
         CLEAR STRCHDR                                                          
         MVC   STRC(4),=C'STRC'    Set self-identification                      
         VCALL LOCALTOD            Time stamp                                   
         STM   R0,R1,STRCCLCK                                                   
*                                                                               
         MVC   STRCTYPE,@R2        Save trace type                              
*                                                                               
         MVC   STRCACCT,=C'?????'  Pre-set unknown account                      
         LT    R15,SVIBJCBP                                                     
         IF    NZ,BEGIN            A user is connected...                       
         WITH  (JCB,R15)                                                        
         MVC   STRCLNO,JCBSEQ      Milten line number                           
         MVC   STRCACCT,JCBACCT    Save account                                 
         END                                                                    
*                                                                               
         CEIL  R3,STRCMXDL         Not too long now                             
         STH   R3,STRCDLEN         Save data length                             
         MOVEL STRCDATA,(R4),(R3)  Save data                                    
*                                                                               
         IF    SVIBFMON,'SET STRCFKEEP'  Must keep until displayed              
*-                                                                              
*-       Link this trace entry to the end of the queue.                         
*-                                                                              
         LT    R2,SVIBTRCQT        Get last trace entry ptr                     
         IF    Z,BEGIN             Empty trace table...                         
         ST    R6,SVIBTRCQH        We are the queue head                        
         ST    R6,SVIBTRCQT        And the queue tail                           
         END                                                                    
*                                                                               
         ELSE  BEGIN               Add us to the end...                         
         ST    R6,STRCLINK-STRC(,R2)  Link us to last                           
         ST    R6,SVIBTRCQT        We are the new queue tail                    
         END                                                                    
*                                                                               
         INCR  R15,SVIBNTRC        Keep count of number of entries              
*-                                                                              
*-       Free oldest entries.                                                   
*-                                                                              
         WHILE (SVIBNTRC,GT,=A(SVIBTRC#)),BEGIN                                 
         L     R1,SVIBTRCQH        Oldest STRC entry                            
         WITH  (STRC,R1)                                                        
         IF    STRCFKEEP,EXIT      We can't delete any more                     
         LA    R15,SVIB                                                         
         LCALL FREESTRC            Free a trace entry                           
         END                                                                    
*-                                                                              
*-       Post user who is monitoring this server.                               
*-                                                                              
         IF    ('LT R4,SVIBMJCB',NZ),BEGIN  Someone is monitoring...            
         WITH  (JCB,R4)                                                         
         IF    JCBWWFGEN,BEGIN     Waiting for our post...                      
         CLEAR JCBWWFGEN           Reset waiting                                
         IF    ^JCBAPDSP,'SETUP (R4)'  Start up the monitoring user             
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Dispatcher Subroutines'                                         
*box                                                                            
*                                                                               
*        The following routines can be called in dispatcher or                  
*        user mode.  As a result, no CP addressability can be                   
*        assumed.                                                               
*                                                                               
         DROP  CP                  No CP addressibility                         
         SPACE 4                                                                
*box                                                                            
*                                                                               
*  SERVPOST -- Routine to handle a noteworthy event on the network              
*    connection for a server connection.  This routine is called                
*    from NPAT in dispatcher mode.                                              
*                                                                               
*    On entry:                                                                  
*       R15 - SERV ptr                                                          
*                                                                               
SERVPOST XPROC                                                                  
         LR    R6,R15                                                           
         WITH  (SVIB,R6)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVPOST,'Bad SVIB ptr.'                        
         L     R5,SVIBUNET         UNET ptr                                     
         WITH  (UNET,R5)                                                        
         FAIL  (UNET,NE,'UNET'),SERVPOST,'Bad UNET ptr.'                        
*-                                                                              
*-       Get rid of a server which has died while no one was                    
*-         connected to it.                                                     
*-                                                                              
         IF    SVIB#IDLE,BEGIN     Server is idle...                            
         IF    (UNETFINEOF,OR,UNETFINABORT,OR,                         *        
               (UNETSTATE,GE,UNETSFABORT)),BEGIN  Close...                      
         LA    R15,SVIB                                                         
         ACALL SERVDEL             Delete the server                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         TITLE 'Dispatcher Common Routines'                                     
*box                                                                            
*                                                                               
*  SERVDEL -- Routine to free the SVIB control block.  This                     
*    also involves freeing control blocks currently attached                    
*    to the SVIB (e.g., UNET).                                                  
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
SERVDEL  PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVDEL,'Bad SVIB ptr'                          
         FAIL  (SVIBNO,Z),SERVDEL,'SERVDEL SVIBNO error'                        
*                                                                               
         L     R4,SVIBNO           Server number                                
         IF    ((R4,GE,CVUSFNO),AND,(R4,LE,CVUSLNO)),BEGIN                      
         S     R4,=A(CVUSFNO)      Entry index from SVIT start                  
         SLL   R4,2                Times 4 for byte index                       
         A     R4,CVUSTAB                                                       
         END                                                                    
*                                                                               
         ELSEIF ((R4,GE,CVPSFNO),AND,(R4,LE,CVPSLNO)),BEGIN                     
         S     R4,=A(CVPSFNO)      Entry index from SVIT start                  
         SLL   R4,2                Times 4 for byte index                       
         A     R4,CVPSTAB                                                       
         END                                                                    
*                                                                               
         ELSE  'KAPUT SIBFREE,"Bad SVIBNO value"'                               
*-                                                                              
*-       R4 now points to our SVIT entry.                                       
*-                                                                              
         L     R15,@R4             Get our SVIB ptr                             
         FAIL  (R15,NE,R5),SVITFREE,'Index table error'                         
*-                                                                              
*-       Remove our entry from the SVIT.                                        
*-                                                                              
         CLEAR (@R4,4)             Remove ourselves from the list               
*-                                                                              
*-       Free any buffered lines.                                               
*-                                                                              
         LA    R15,SVIB                                                         
         ACALL SERVFBUF            Free buffered lines                          
*-                                                                              
*-       Free the trace table.                                                  
*-                                                                              
         WHILE ('LT R1,SVIBTRCQH',NZ),BEGIN  Free trace entries...              
         LA    R15,SVIB                                                         
         ACALL FREESTRC            Free this entry                              
         END                                                                    
*                                                                               
         CLEAR SVIBFTRC            Reset trace option                           
*-                                                                              
*-       Reset the monitoring information.                                      
*-                                                                              
         IF    ('LT R4,SVIBMJCB',NZ),BEGIN  Someone is monitoring...            
         WITH  (JCB,R4)                                                         
         IF    JCBWWFGEN,BEGIN     Waiting for our post...                      
         CLEAR JCBWWFGEN           Reset waiting                                
         IF    ^JCBAPDSP,'SETUP (R4)'  Start up the monitoring user             
         END                                                                    
*                                                                               
         CLEAR SVIBMJCB            Reset trace monitor info                     
         END                                                                    
*-                                                                              
*-       Free SVIB.                                                             
*-                                                                              
         MVC   SVIB(4),=C'OSVB'                                                 
*                                                                               
         LT    R4,SVIBUNET                                                      
         IF    NZ,BEGIN            Network path is allocated...                 
         WITH  (UNET,R4)                                                        
         CLEAR UNETSVIB            Not a server connection now                  
         CLEAR SVIBUNET            No network connection now                    
*                                                                               
         LA    R1,=C'EOF'                                                       
         LA    R15,UNET                                                         
         VCALL NPATCTRL            Send normal EOF                              
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATCLS             Close network path                           
         END                                                                    
*                                                                               
         LA    R1,SVIB                                                          
         VCALL FREECORE            Free SVIB storage                            
*                                                                               
         CLEAR R15                 (Neatness)                                   
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SERVFBUF -- Routine to free buffered lines and field descriptor              
*    map (see FDEMAP dsect).                                                    
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*                                                                               
SERVFBUF PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),SERVFBUF,'Bad SVIB ptr'                         
*-                                                                              
*-       Free field descriptor map.                                             
*-                                                                              
         IF    ('LT R1,SVIBFMAP',NZ),BEGIN  Free FDEMAP...                      
         CLEAR SVIBFMAP            Reset FDEMAP ptr                             
         VCALL FREECORE            Release FDEMAP                               
         END                                                                    
*-                                                                              
*-       Free buffered input lines.                                             
*-                                                                              
         WHILE ('LT R2,SVIBINQH',NZ),BEGIN  Free input lines...                 
         WITH  (INENTRY,R2)                                                     
         L     R15,INLINK                                                       
         ST    R15,SVIBINQH        Save new queue head                          
         IF    (R15,Z),'ST R15,SVIBINQT'  Zero queue tail too                   
*                                                                               
         LA    R1,INENTRY                                                       
         VCALL FREECORE            Release INENTRY                              
         END                                                                    
*                                                                               
         CLEAR SVIBINCT            Reset queued entry count                     
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FREESTRC -- Routine to free a trace entry.                                   
*                                                                               
*    On entry:                                                                  
*      R15 - SVIB ptr                                                           
*      R1  - STRC ptr of entry to free                                          
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - no entries left in trace queue                                       
*      nz- there are still entries in the trace queue                           
*                                                                               
FREESTRC PROC                                                                   
         LR    R5,R15                                                           
         WITH  (SVIB,R5)                                                        
         FAIL  (SVIB,NE,'SVIB'),FREESTRC,'Bad SVIB ptr'                         
*                                                                               
         LR    R6,R1               STRC ptr                                     
         IF    (R6,Z),'CLEAR R15; EXIT FREESTRC'  Nothing left                  
*-                                                                              
*-       Find the previous STRC entry so we can unlink this entry.              
*-                                                                              
         CLEAR R4                  Previous entry ptr                           
*                                                                               
         LA    R3,SVIBTRCQH-(STRCLINK-STRC)  Dummy queue head                   
         WITH  (STRC,R3),BEGIN                                                  
         WHILE ('LT R3,STRCLINK',NZ),BEGIN  Go through entries...               
*                                                                               
         IF    (R3,EQ,R6),FSTRCOM  Matched the entry, scram                     
*                                                                               
         LR    R4,R3               Save previous entry ptr                      
         END                                                                    
         END                                                                    
*                                                                               
         KAPUT STRCERR2,'Free STRC logic error.'                                
*-                                                                              
*-       Dequeue our STRC entry.                                                
*-                                                                              
FSTRCOM  IF    (R4,Z),BEGIN        We are removing the 1st entry...             
         MVC   SVIBTRCQH,STRCLINK-STRC(R6)  Set new queue head                  
         IF    (SVIBTRCQH,Z),'CLEAR SVIBTRCQT'  Nothing on queue                
         END                                                                    
*                                                                               
         ELSE  BEGIN               Dequeue...                                   
         WITH  (STRC,R4)                                                        
         FAIL  (STRC,NE,'STRC'),STRCERR3,'Free STRC logic error.'               
         MVC   STRCLINK,STRCLINK-STRC(R6)  Dequeue our entry                    
         IF    (STRCLINK,Z),'ST R4,SVIBTRCQT'  Update queue tail                
         END                                                                    
*-                                                                              
*-       Free the STRC entry.                                                   
*-                                                                              
         WITH  (STRC,R6),BEGIN                                                  
         FAIL  (STRC,NE,'STRC'),STRCERR4,'Free STRC logic error.'               
         MVC   STRC(4),=C'OTRC'    Neatness                                     
         MVC   STRCLINK,=F'-1'     (Ditto)                                      
*                                                                               
         LA    R1,STRC                                                          
         VCALL FREECORE            Free trace entry                             
*                                                                               
         DECR  R15,SVIBNTRC        Decrement number of trace entries            
         END                                                                    
*                                                                               
         LT    R15,SVIBTRCQH       Set CC for caller                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
