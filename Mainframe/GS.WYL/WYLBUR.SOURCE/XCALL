XCALL    TITLE 'WYLBUR XCALL, PCALL Commands'                                   
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         EJECT                                                                  
WYLXCALL CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
XCAL     IDENT 2025                11:49:39 01/25/02  (SLP)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         EJECT                                                                  
         COPY  FFINFO                                                           
         POP   DSECTS                                                           
WYLXCALL CSECT                                                                  
         DC    C'To the MAX !!!!'                                               
*                                                                               
         EJECT                                                                  
*-                                                                              
*-       CP, CV ADDITIONS                                                       
*-                                                                              
*-                                                                              
         DS    0S(CV#FEXEC-CP#FEXEC-CPMAXXCT)                                   
         EJECT                                                                  
*-                                                                              
*-       COPY DSECTS TO HERE                                                    
*-                                                                              
         EJECT                                                                  
         COPY  XDSNAME                                                          
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-       XFENTRY - EXEC FILE INFORMATION, SWEET, SIMPLE                         
*-                                                                              
*-                                                                              
XFENTRY  RECORD BEGIN                                                           
*                                                                               
* EXEC SET NUMBER NAME                                                          
*                                                                               
XFNAME   DS    XL(L'XDSNAME)       EXEC FILE NAME                               
*-                                                                              
*-       EXEC FILE INFORMATION                                                  
*-                                                                              
XFFLAG1  FLAG  ,                                                                
         FLAG  (XFPUBLIC,1,EQ)     PUBLIC EXEC                                  
         FLAG  (XFUSERX,2,EQ)      USERS EXEC                                   
XFFLAG2  FLAG  ,                                                                
XFFLAG3  FLAG  ,                                                                
XFFLAG4  FLAG  ,                                                                
XF#SETNO DS    F                   EXEC/FLOW SET NUMBER                         
XFRGEXEC DS    A                   ADDRESS OF EXEC RGROUP INFO                  
XFRGFLOW DS    A                   ADDRESS OF FLOW RGROUP INFO                  
XFLDTIME DS    D                   TIME LOADED                                  
XFLTIME  DS    D                   TIME OF LAST ACCESS (NOT KEPT)               
         END                                                                    
         SPACE 5                                                                
*-                                                                              
*-                                                                              
*-       XFXENTRY - EXEC FILE INFORMATION INDEX BY SET, RECORD GROUP            
*-                                                                              
*-                                                                              
XFXENTRY RECORD BEGIN                                                           
XFXNDX   DS    CL(CPRG16NL)        EXEC SET-RGROUP INDEX                        
         ORG   XFXNDX                                                           
XFXSETNO DS    F                   EXEC SET NUMBER                              
XFXRG    DS    F                   EXEC RGROUP INFO ADDR                        
         DS    0S((XFXNDX+L'XFXNDX)-*)                                          
         ORG                                                                    
XFXNAME  DS    XL(L'XDSNAME)       EXEC FILE NAME AREA                          
         END                                                                    
         EJECT                                                                  
         COPY  VNENTRY                                                          
         SPACE 5                                                                
         COPY  VPARM                                                            
         SPACE 5                                                                
*-                                                                              
*-       PARM LIST DEFINITIONS                                                  
*-                                                                              
*-       NLIST - PARAMETER NAME LIST (AS DEFINED ON PROC STATEMENT)             
*-       VLIST - PARAMETER VALUE LIST                                           
*-     PLIST - PARAMETER NAME LIST (AS SPECIFIED ON PCALL STATEMENT)            
*-                                                                              
*-       NOTE: THE DIFFERENCE BETWEEN A NLIST AND A PLIST (IN THIS              
*-       PROGRAM) IS AS FOLLOWS:  A NLIST IS A LIST OF NAMES                    
*-       SPECIFIED ON A PROCEDURE DEFINITION,, THE NAMES MUST                   
*-       SIMPLE VARIABLE NAMES.  THESE VARIABLES WILL BECOME                    
*-       LOCAL VARIABLES OF THE PROCEDURE.  A PLIST IS A LIST                   
*-       OF PARM VARIABLE NAMES THAT ARE PASSED TO A PROCEDURE,, AS             
*-       SOME OF THE PARAMETERS MAY BE EXPRESSIONS OR CONSTANTS                 
*-       NOT ALL THE ENTRIES IN A PLIST EXIST,, ONLY VARIABLE                   
*-       NAME ENTRIES EXIST,,, CONSTANT OR EXPRESSION ENTRIES ARE               
*-       ZERO.                                                                  
*-                                                                              
NLIST    RECORD BEGIN ,            LIST OF PARAMETER NAMES                      
         DS    XL(CPPLIMIT*L'VNDESC)                                            
         END                                                                    
*                                                                               
VLIST    RECORD BEGIN ,            LIST OF PARAMETER VALUES                     
         DS    XL(CPPLIMIT*L'VPARM)                                             
         END                                                                    
*                                                                               
PLIST    RECORD BEGIN ,            LIST OF PARM NAMES                           
         DS    XL(CPPLIMIT*L'VNDESC)                                            
         END                                                                    
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-     SNENTRY - EXEC STATE ENTRY                                               
*-                                                                              
*-     THIS DSECT CONTAINS INFORMATION DESCRIBING THE EXCUTION STATE            
*-     OF AN EXEC.  IT IS SAVED WHEN WE START A NEW EXEC.  IT IS                
*-     RESTORED WHEN WE RESUME EXECUTION.  ALL FIELDS CORRESPOND TO             
*-     EQUIVALENT CP FIELDS.  (NO?)                                             
*-                                                                              
*-     NOTE: THIS STRUCTURE IS USED TO SAVE AND RESOTRE STATE INFO              
*-     FROM XCALLS AND PCALLS.  SOME STATE INFO IS NOT RESTORED                 
*-     UPON RETURN FROM A PCALL.  SEE SETPRET ROUTINE FOR DETAILS.              
*-                                                                              
*-                                                                              
SNENTRY  RECORD BEGIN                                                           
SNFLAG   FLAG                                                                   
         FLAG  SNFEXEC             EXEC MODE IN EFFECT                          
SNFLAG2  FLAG  ,                   MISC FLAGS, ONLY THOSE NECESSARY!            
         FLAG  SNFXTEND            EXTENDED EXEC                                
         FLAG  SNFXOLD             OLD EXEC                                     
         FLAG  SNFXAUTHLIB         AUTHORIZED EXEC LIBRARY                      
         FLAG  SNFXAUTH            Running in authorized mode                   
         FLAG  SNFVCMD             COMMAND SENT TO WYL FROM ORVYL               
SNFLAG3  FLAG                                                                   
         FLAG  SNFTYPED            COMMAND TYPED AT TERMINAL                    
         FLAG  SNFTRY              TRY XCALL, TRY PCALL                         
         FLAG  SNFQUIET            QUIET XCALL                                  
         FLAG  SNFXQT              QUIET XPROC                                  
         FLAG  SNFXDUMP            DUMP XPROC                                   
SNFLAG4  FLAG                                                                   
SNEXLINE DS    F                   EXEC FILE LINE NUMBER                        
SNEXNEXT DS    F                   NEXT EXEC FILE LINE NUMBER                   
SNSCOPE  DS    F                   START OF SCOPE                               
SNSCOPD  DS    F                   END OF SCOPE                                 
SNXSCOPE DS    F                   START OF XPROC SCOPE                         
SNXSCOPD DS    F                   END OF XPROC SCOPE                           
SN#XSET  DS    F                   EXEC FILE SET NUMBER                         
SNRGEXEC DS    F                   EXEC FILE RECORD GROUP POINTER               
SNRGFLOW DS    F                   FLOW RECORD GROUP POINTER                    
SNVSET#  DS    F                   VARIABLE STACK SET #                         
SNXVSET# DS    F                   VARIABLE STACK SET # OF LAST XPROC           
SNESCAPE DS    C                   ESCAPE CHARACTER                             
SNSKIP   DS    C                   SKIP   CHARACTER                             
SNXNAME  DS    CL(L'XDSNAME)       EXEC, XPROC NAME ,,, NOT SET!                
SNPROCNM DS    CL(L'CPPROCNM)      EXEC PROCEDURE NAME                          
SNXPUSH  DS    CL(L'CPXPUSH)       ADDITION EXEC STATE INFO                     
         END                                                                    
         EJECT                                                                  
WYLXCALL CSECT                                                                  
         TITLE 'XCALL Module'                                                   
*box                                                                            
*                                                                               
*                                                                               
*  INITXCAL - INITIALIZE XCALL MODULE                                           
*                                                                               
*  (PER USER INIT -- CALLED BY SIGN MODULE)                                     
*                                                                               
*                                                                               
INITXCAL XPROC ,                                                                
         ACALL XLISTCLR            CLEAR EXEC CACHE LIST                        
         ACALL CLRALIST            CLEAR ACTIVE EXECS LIST                      
         PEND  ,                                                                
         TITLE 'XCALL Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XCALL - CALL EXEC FILE                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINDER OF XCALL COMMAND !                                   
*                                                                               
*  WE DO THE FOLLOWING (MORE OR LESS) !                                         
*       - GET THE NAME OF THE EXEC TO CALL                                      
*       - LOAD THE EXEC TO CALL, IF NOT ALREADY LOADED                          
*       - GET THE PARAMETER STRING                                              
*       - GET THE PARAMETER LIST                                                
*       - GET THE PARAMETER VALUES                                              
*       - GET THE XPROC NAME LIST                                               
*       - PASS PARAMETERS                                                       
*       - SAVE CALLING/CALLED PARM LISTS                                        
*       - SAVE CALLING ROUTINES EXECUTION STATE                                 
*       - ESTABLISH NEW ROUTINES EXECUTION STATE                                
*       - TAKE OFF !                                                            
*                                                                               
* NOTE: CPPLIMIT=8                MAXIMUM NUMBER OF PARAMETERS                  
*                                                                               
*                                                                               
XCLWA    RECORD BEGIN                                                           
XCLFLAG  FLAG  ,                                                                
         FLAG  (XCLDSN,1,EQ)       XCALL <DSN NAME>                             
         FLAG  (XCLACT,2,EQ)       XCALL ACTIVE                                 
XCLFLAG2 FLAG  ,                                                                
         FLAG  (XCLPARMS,1,EQ)     PARM LIST                                    
         FLAG  (XCLFREE,2,EQ)      FREE FORMAT PARM STRING                      
XC#SETNO DS    F                   CALLED EXEC SET NUMBER                       
XCRGEXEC DS    F                   CALLED EXEC RECORD GROUP                     
XCRGFLOW DS    F                   CALLED EXEC FLOW RECORD GROUP                
XCNEWSET DS    F                   CALLED EXEC NEW VAR SET NUMBER               
XCPARM   DS    XL(&LINESZ)         PARAMETER STRING                             
XCPARMLN DS    F                   PARAMETER STRING LENGTH                      
XCPARMCT DS    F                   PARM COUNT                                   
XCLPLIST DS    XL(L'PLIST)         PARM LIST (FROM CALLER)                      
XCLNLIST DS    XL(L'NLIST)         NAME LIST (FROM PROC)                        
XCLVLIST DS    XL(L'VLIST)         VALUE LIST (ACTUAL PARM VALUES)              
XCLXINFO DS    XL(L'XFENTRY)       EXEC FILE INFO DSECT                         
XCLSTATE DS    XL(L'SNENTRY)       EXEC STATE INFO DSECT                        
         END                                                                    
*                                                                               
*                                                                               
XCALL    XPROC XCLWA                                                            
*                                                                               
         COMMENT                   CLEAR WORK AREA                              
         LA    R2,XCLWA+40            (EXCEPT FOR ENTRY REGS)                   
         LA    R3,L'XCLWA-40                                                    
         CLEAR R5                                                               
         MVCL  R2,R4                                                            
*                                                                               
         COMMENT                   GET FILE WORK AREA                           
         VCALL MKFFINFO                                                         
*                                                                               
         COMMENT                   SCAN XCALL OPTIONS                           
         ACALL SCNXCALL                                                         
         IF    (R3,Z),BEGIN                                                     
         SET   XCLACT              XCALL ACTIVE SCANNED                         
         END                                                                    
         ELSE  BEGIN                                                            
         SET   XCLDSN              XCALL DSN SCANNED                            
         END                                                                    
         IF    (R2,Z),BEGIN                                                     
         SET   XCLPARMS            PARM-LIST SCANNED                            
         END                                                                    
         ELSE  BEGIN                                                            
         SET   XCLFREE             FREE-FORMAT PARM STRING SCANNED              
         MVC   XCPARMCT,=F'-1'     SET -1 PARM COUNT, FOR FREE FORMAT           
         END                                                                    
         ST    R0,XCPARMLN                                                      
         L     R3,XCPARMLN         SAVE INPUT PARM LIST/STRING                  
         MOVE  R3,XCPARM,@R1                                                    
*                                                                               
         COMMENT                   IF CALLED FROM TERMINAL                      
         IF    ^CPFEXEC,BEGIN      CLEAR ERROR/ATTN STATE, COMMANDS             
         VCALL INITERR                                                          
         VCALL INITATTN                                                         
         VCALL INITXDMP            TURN OFF EXEC DUMP INFO                      
         CLEAR CPFXQT              CLEAR EXEC QUIET FLAG                        
         MVC   CPLLIMIT,CPLIMIT    LOCAL LIMIT DEFAULTS TO GLOBAL               
         END                                                                    
*                                                                               
         COMMENT                   IF 1ST XCALL, RESET EXEC STACKS              
         IF    (CPFXOLD,OR,^CPFEXEC),BEGIN                                      
         COMMENT                   CLEAR VAR STACK, SET BASE VARS               
         COMMENT                   AND RESET EXEC ALL EXEC POINTERS             
         VCALL EXRESET             TO INITIAL VALUES                            
         END                                                                    
*                                                                               
         COMMENT                   LOAD EXEC FILE, IF NEEDED                    
         IF    XCLDSN,BEGIN                                                     
         LA    R1,XCLXINFO         SET EXEC FILE NAME                           
         ACALL SETXNAME                                                         
         CLEAR R0                                                               
         LA    R1,XCLXINFO                                                      
         LA    R2,XCLXINFO                                                      
         ACALL GETXINFO            CHECK IF EXEC LOADED                         
         IF    NZ,BEGIN            IF NOT LOADED, LOAD                          
         ACALL LOADDSN                                                          
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   LOAD EXEC FILE FROM ACTIVE,                  
         IF    XCLACT,BEGIN          IF ACTIVE SPECIFIED                        
         IF    CPFEXEC,BEGIN                                                    
         ERROR 'XCALL ACTIVE: command invalid from within EXEC file.'           
         END                                                                    
         ACALL LOADACT                                                          
         END                                                                    
*                                                                               
         COMMENT                   GET CALLED EXEC POINTERS                     
         IF    XCLDSN,BEGIN        IF XCALL DSN,                                
         CLEAR R0                                                               
         LA    R1,XCLXINFO                                                      
         LA    R2,XCLXINFO                                                      
         ACALL GETXINFO                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         MVC   XC#SETNO,XF#SETNO-XFENTRY(R2)                                    
         MVC   XCRGEXEC,XFRGEXEC-XFENTRY(R2)                                    
         MVC   XCRGFLOW,XFRGFLOW-XFENTRY(R2)                                    
         END                                                                    
         ELSEIF XCLACT,BEGIN       IF XCALL ACTIVE,                             
         MVC   XC#SETNO,=AL4(CP#XACT)                                           
         LA    R3,CPEXEC                                                        
         ST    R3,XCRGEXEC                                                      
         LA    R3,CPFLOW                                                        
         ST    R3,XCRGFLOW                                                      
         END                                                                    
         ELSE  BEGIN               IF NEITHER, BOMB                             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET NEW VARIABLE SET NUMBER                  
         VCALL GTNEWSET                                                         
         ST    R0,XCNEWSET                                                      
         COMMENT                   CLEAR NEW VARIABLE SET                       
         L     R0,XCNEWSET                                                      
         VCALL CLRVSETS                                                         
         COMMENT                   MARK EXEC ACTIVE                             
         L     R0,XC#SETNO                                                      
         L     R1,XCRGEXEC                                                      
         ACALL MRKALIST                                                         
         IF    NZ,BEGIN                                                         
         ERROR 'Too many (>64) EXEC files active.'                              
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM LIST                               
         IF    XCLPARMS,BEGIN                                                   
         L     R0,XCPARMLN                                                      
         LA    R1,XCPARM                                                        
         LA    R2,XCLPLIST                                                      
         ACALL SCNPLIST                                                         
         ST    R0,XCPARMCT                                                      
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM VALUES                             
         IF    XCLPARMS,BEGIN                                                   
         L     R0,XCPARMLN                                                      
         LA    R1,XCPARM                                                        
         LA    R2,XCLVLIST                                                      
         ACALL SCNVLIST                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CALLED XPROC NAME LIST                   
         L     R0,XC#SETNO                                                      
         L     R1,XCRGFLOW                                                      
         LA    R2,XCLNLIST                                                      
         VCALL GTXNLIST                                                         
*                                                                               
         COMMENT                   PASS PARMS                                   
         L     R0,XCPARMCT                                                      
         LA    R1,XCLVLIST                                                      
         LA    R2,XCLNLIST                                                      
         L     R3,XCNEWSET                                                      
         ACALL PASSPRMS                                                         
*                                                                               
         COMMENT                   PASS PARM_STR, AND PARM_COUNT                
         L     R0,XCPARMLN                                                      
         LA    R1,XCPARM                                                        
         L     R2,XCPARMCT                                                      
         ACALL PASSPARM                                                         
*                                                                               
         COMMENT                   SAVE CALLERS PLIST                           
         LA    R0,L'XCLPLIST                                                    
         LA    R1,XCLPLIST                                                      
         LA    R2,=CL16'SYS*CALLER_PLIST'                                       
         L     R3,XCNEWSET                                                      
         VCALL SAVEVSTK                                                         
         COMMENT                   SAVE CALLED NLIST                            
         LA    R0,L'XCLNLIST                                                    
         LA    R1,XCLNLIST                                                      
         LA    R2,=CL16'SYS*PROC_NLIST  '                                       
         L     R3,XCNEWSET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   PUSH CURRENT ON CONDITION INFO               
         L     R0,XCNEWSET                                                      
         VCALL PUSHON                                                           
         L     R0,XCNEWSET         PUSH CURRENT ERROR/ATTN VARS                 
         VCALL PUSHERR                                                          
*                                                                               
         COMMENT                   GET CURRENT EXEC STATE                       
         LA    R1,XCLSTATE                                                      
         VCALL GETSTATE                                                         
*                                                                               
         COMMENT                   SAVE CURRENT EXEC STATE                      
         LA    R0,L'SNENTRY                                                     
         LA    R1,XCLSTATE                                                      
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,XCNEWSET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   SET UP NEW EXEC STATE                        
         L     R0,XC#SETNO                                                      
         L     R1,XCRGEXEC                                                      
         L     R2,XCRGFLOW                                                      
         LA    R3,XCLSTATE                                                      
         ACALL GTXSTART                                                         
*                                                                               
         COMMENT                   IF DEBUG ACTIVE, CHECK FOR TRAP              
         IF    CPFDEBUG,BEGIN                                                   
         L     R0,XC#SETNO                                                      
         LA    R1,=CL16'XPROC '                                                 
         VCALL DBCALL                                                           
         END                                                                    
*                                                                               
         COMMENT                   START NEW EXEC                               
         LA    R1,XCLSTATE                                                      
         ACALL START               DOES NOT RETURN                              
*                                                                               
         BOMB  ,                   NEVER EXECUTED, WE EXIT ABOVE                
*                                                                               
         CLEAR R15                                                              
         PEND  ,          NEVER EXECUTED, WE EXIT ABOVE                         
         TITLE 'XA - XCALL ACTIVE'                                              
*box                                                                            
*                                                                               
*  XA -- XCALL ACTIVE                                                           
*                                                                               
XCACTWA  RECORD BEGIN                                                           
XCACTCMD DS    XL(&LINESZ+8)                                                    
         END                                                                    
*                                                                               
*                                                                               
XCACTIVE XPROC XCACTWA    SAME AS XCALL ACTIVE                                  
*                                                                               
         COMMENT                   FORMAT NEW COMMAND                           
         MVC   XCACTCMD(8),=CL8'ACTIVE  '                                       
         SCINFO ,                                                               
         LR    R15,R0                                                           
         MOVE  R15,XCACTCMD+8,@R1  TACK ON PARMS                                
*                                                                               
         COMMENT                   PASS NEW COMMAND TO 'XCALL'                  
         A     R0,=F'8'                                                         
         LA    R1,XCACTCMD                                                      
         SCINIT (R1),(R0)                                                       
         ACALL  XCALL              DOES NOT RETURN                              
*                                                                               
         BOMB  ,                                                                
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'XRETURN'                                                        
*box                                                                            
*                                                                               
*                                                                               
*  XRETURN - RETURN FROM XPROC ,                                                
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - XRETURN OPTIONS, OR PARM LIST                                  
*                                                                               
*  ON EXIT:                                                                     
*       THIS ROUTINE DOES NOT RETURN !!                                         
*       THIS ROUTINE EXITS VIA START ROUTIN                                     
*                                                                               
*  NOTE: THIS DESCRIBES THE MANY NAME LISTS:                                    
*       XRRLIST - NAME LIST IN  .. RETURN (<NAME LIST>)                         
*       XRNLIST - NAME LIST IN  .. PROC NAME (<NAME LIST>)                      
*       XRPLIST - PARM LIST IN  .. PCALL NAME (<PARM LIST>)                     
*       XRLIST  - CALCULATED RETURN LIST CONTAINS CALLERS VAR NAMES             
*                                                                               
*                                                                               
XRWA     RECORD BEGIN                                                           
XRCNT    DS    F                   RETURN PARMS COUNT                           
XRRLIST  DS    XL(L'NLIST)         RETURN NAME LIST  (PROCEDURES)               
XRNLIST  DS    XL(L'NLIST)         ENTRY NAME LIST  (PROCEDURES)                
XRPLIST  DS    XL(L'NLIST)         ENTRY NAME LIST  (CALLERS)                   
XRLIST   DS    XL(L'NLIST)         RETURN NAME LIST  (CALLERS)                  
XRVLIST  DS    XL(L'VLIST)         RETURN VALUE LIST                            
XRPARMLN DS    F                   PARM/OPTIONS LENGTH                          
XRPARM   DS    F                   PARM/OPTIONS ADDRESS                         
XRSTATE  DS    XL(L'SNENTRY)       EXEC-STATE DSECT                             
         END                                                                    
*                                                                               
*                                                                               
XRETURN  XPROC XRWA                                                             
*                                                                               
         COMMENT                   CHECK IF XRETURN VALID                       
         IF    (CPFXOLD,OR,(CP#XSET,EQ,=A(CP#XOLD))),BEGIN                      
         ABORT 'Not in procedure.  Command ignored.'                            
         END                                                                    
*                                                                               
         COMMENT                   IF DEBUG ACTIVE, CHECK FOR TRAPS             
         IF    CPFDEBUG,BEGIN                                                   
         L     R0,CP#XSET                                                       
         LA    R1,=CL16'XPROC '                                                 
         VCALL DBRET                                                            
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR XRETURN OPTIONS ...                
         COMMENT                      XRETURN CMD                               
         COMMENT                      XRETURN ERR                               
         COMMENT                      XRETRUN ATTN                              
         SCINFO                                                                 
         VCALL LRTRIM                                                           
         IF    ((@R1,NE,'('),AND,(R0,POS)),BEGIN    IF GOT OPTIONS,,            
         COMMENT                                      PROCESS THEM              
         ACALL XRETOPTS            THIS CALL DOES NOT RETURN ->                 
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM/OPTIONS                            
         ACALL PSCNPARM                                                         
         ST    R0,XRPARMLN                                                      
         ST    R1,XRPARM                                                        
*                                                                               
         IF    ((R0,POS),AND,(CPXVSET#,NE,CPVSET#)),BEGIN                       
         COMMENT                   ALLOW XRETURN TO RETURN PARMS                
         COMMENT                   FROM PROCEDURE LEVEL ??                      
         COMMENT                   , (FOR NOW NO-OP)                            
         END                                                                    
*                                                                               
         COMMENT                   SCAN RETURN LIST VALUES                      
         L     R0,XRPARMLN                                                      
         L     R1,XRPARM                                                        
         LA    R2,XRVLIST                                                       
         ACALL SCNVLIST                                                         
         ST    R0,XRCNT                                                         
*                                                                               
         COMMENT                   SCAN RETURN LIST NAMES                       
         L     R0,XRPARMLN                                                      
         L     R1,XRPARM                                                        
         LA    R2,XRRLIST                                                       
         ACALL SCNNLIST                                                         
*                                                                               
         COMMENT                   GET CALLING PARM LIST (NAMES)                
         LA    R0,L'XRNLIST                                                     
         LA    R1,XRNLIST                                                       
         LA    R2,=CL16'SYS*PROC_NLIST  '                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET CALLING PARM LIST (NAMES)                
         LA    R0,L'XRPLIST                                                     
         LA    R1,XRPLIST                                                       
         LA    R2,=CL16'SYS*CALLER_PLIST'                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   ASSOCIATE RETURN NAMES WITH                  
         COMMENT                   PARM NAMES, POSITION WISE                    
         USING VNENTRY,R1                                                       
         LA    R0,XRLIST                                                        
         LA    R1,L'NLIST                                                       
         LA    R2,XRRLIST                                                       
         LA    R3,L'NLIST                                                       
         MVCL  R0,R2               COPY RETURN LIST                             
         L     R0,XRCNT                                                         
         LA    R1,XRLIST                                                        
         WHILE (R0,POS),BEGIN      LOOP THRU ALL RETURN PARMS                   
*                                                                               
         COMMENT                   FIND ASSOCIATED NAME                         
         LA    R2,XRNLIST                                                       
         LA    R4,XRNLIST                                                       
         LA    R4,L'NLIST(R4)                                                   
         WHILE ((R2,LT,R4),AND,(@R2,NE,VNDESC)),BEGIN                           
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         IF    (R2,GE,R4),BEGIN                                                 
         LR    R2,R1                                                            
         VCALL SEGVNAME                                                         
         ERROR ': not a procedure parameter.  Unable to return value.'          
         END                                                                    
         LA    R4,XRNLIST          CALCULATE NAME OFFSET IN R2                  
         SR    R2,R4                                                            
         LA    R2,XRPLIST(R2)                                                   
         MVC   0(L'VNDESC,R1),0(R2)   SAVE CALLING NAME IN RETURN LIST          
*                                                                               
         COMMENT                   LOOP THRU ALL RETURN VARIABLES               
         LA    R1,L'VNDESC(R1)                                                  
         DECR  R0                                                               
         END                                                                    
         DROP  R1                                                               
*                                                                               
         COMMENT                   RESTORE ON CONDITION COMMANDS                
         L     R0,CPXVSET#                                                      
         VCALL POPON                                                            
         L     R0,CPXVSET#         RESTORE ERROR/ATTN VARS                      
         VCALL POPERR                                                           
*                                                                               
         COMMENT                   GET CALLING EXECUTION STATE                  
         LA    R0,L'XRSTATE                                                     
         LA    R1,XRSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RETURN PASSED PARMS                          
         L     R0,XRCNT                                                         
         LA    R1,XRVLIST                                                       
         LA    R2,XRLIST                                                        
         LA    R3,XRRLIST                                                       
         L     R4,XRSTATE+(SNVSET#-SNENTRY)                                     
         L     R5,XRSTATE+(SNXVSET#-SNENTRY)                                    
         ACALL RETPARMS            RETURN PARMS                                 
*                                                                               
         COMMENT                   DO ANY LAST MINUTE CLEANUP                   
         LA    R1,XRSTATE                                                       
         ACALL XCLEANUP                                                         
*                                                                               
         COMMENT                   START RETURN EXEC                            
         LA    R1,XRSTATE                                                       
         ACALL START               DOES NOT RETURN                              
*                                                                               
*                                                                               
         BOMB                                                                   
*                                                                               
         CLEAR R15                 NEVER EXECUTED, WE EXIT ABOVE                
         PEND  ,                                                                
*$ XRETURN IN PROC (IE. NOT XPROC,,, NEEDS SPECIAL RETURN PARMS                 
*$ ETC...                                                                       
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XRETOPTS -                                                                   
*                                                                               
*  PROCESS:  XRETURN CMD= ...                                                   
*            XRETURN ERR                                                        
*            XRETURN ATTN                                                       
*                                                                               
*                                                                               
*                                                                               
XRETOPTS XPROC ,                                                                
*                                                                               
         SCPUSH ,                                                               
         SCAN  XRETKWS                                                          
         SCANCHK                                                                
         SCPOP ,                                                                
         IF    (R0,Z),BEGIN                                                     
         END                                                                    
         ELSEIF (R15,EQ,4),BEGIN   IF XRETURN CMD = .. , DO IT                  
         ACALL XCOMMAND            DOES NOT RETURN                              
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL XABORT                                                           
         END                                                                    
*                                                                               
         PEND  ,                                                                
*                                                                               
*  XRETURN CMD= ...                                                             
*  XRETURN <ABORT OPTIONS>                                                      
*                                                                               
XRETKWS  SCKW  COMMAND,XCMDRET,A                                                
         SCKW  CMD,XCMDRET,A                                                    
         SCKW  ,XCMDABRT                                                        
*                                                                               
XCMDRET  PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
XCMDABRT PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XABORT - PROCESS THE FOLLOWING:                                              
*                                                                               
*       XRETURN CMD=                                                            
*       XRETURN ERROR                                                           
*       XRETURN ATTENTION                                                       
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - 'XRETURN' SCANNED                                              
*                                                                               
*  ON EXIT:                                                                     
*       THIS ROUTINE DOES NOT RETURN !!                                         
*       THIS ROUTINE EXITS VIA START ROUTINE.                                   
*                                                                               
*                                                                               
XBWA     RECORD BEGIN                                                           
XBFLAG   FLAG                                                                   
         FLAG  XBFERROR            XRETURN ERROR                                
         FLAG  XBFATTN             XRETURN ATTN                                 
         DS    0F                                                               
XBERRID  DS    CL8                 ERROR ID                                     
XBERRMSG DS    CL(&LINESZ)         ERROR MESSAGE TEXT                           
XBERRMLN DS    F                   ERROR MESSAGE LENGTH                         
XBATTNID DS    CL8                 ATTN ID                                      
XBSTATE  DS    XL(L'SNENTRY)       EXEC-STATE DSECT                             
         END                                                                    
*                                                                               
*                                                                               
XABORT   PROC  XBWA                                                             
         LA    R6,XBWA                                                          
         USING XBWA,R6                                                          
*                                                                               
         COMMENT                   SET UP SCANNER,                              
         SCPUSH ,                                                               
         SCAN ,                    (CHECK FOR NON-NULL PARMS)                   
         SCANCHK                                                                
         SCPOP ,                                                                
         IF    (R0,Z),BEGIN                                                     
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SET WORK AREA DEFAULTS                       
         CLEAR XBFLAG                                                           
         MVC   XBERRID,=CL8'ERROR   '                                           
         SETMSG 'Error in XPROC.'                                               
         ST    R0,XBERRMLN                                                      
         LR    R3,R0                                                            
         MOVE  R3,XBERRMSG,@R1                                                  
         MVC   XBATTNID,=CL8'ATTN    '                                          
         CLEAR XBSTATE                                                          
*                                                                               
         COMMENT                   SCAN ERROR/ATTN OPTIONS                      
         SCAN XABRTPRT                                                          
         SCANCHK                                                                
*-                                                                              
*-       IF  XRETURN ERROR ...                                                  
*-                                                                              
         COMMENT                   IF XRETURN ERROR                             
         IF    XBFERROR,BEGIN                                                   
*                                                                               
         COMMENT                   RESTORE ON CONDTION COMMANDS                 
         L     R0,CPXVSET#                                                      
         VCALL POPON                                                            
         L     R0,CPXVSET#         RESTORE ERROR/ATTN VARS                      
         VCALL POPERR                                                           
*                                                                               
         COMMENT                   GET CALLING EXEC STATE                       
         LA    R0,L'XBSTATE                                                     
         LA    R1,XBSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DO ANY LAST MINUTE CLEANUP                   
         LA    R1,XBSTATE                                                       
         ACALL XCLEANUP                                                         
*                                                                               
         COMMENT                   SET ERROR VARIABLES                          
         MVC   CPERRID,XBERRID                                                  
         L     R0,XBERRMLN                                                      
         LA    R1,XBERRMSG                                                      
         IF    (R0,POS),BEGIN                                                   
         TSEG  (R1),(R0),CR                                                     
         END                                                                    
*                                                                               
         COMMENT                   START RETURN EXEC                            
         SET   CPFERROR            WITH ERROR FLAGS SET                         
         SET   CPFABORT                                                         
         LA    R1,XBSTATE                                                       
         ACALL START               DOES NOT RETURN  --->>>                      
         END                                                                    
*-                                                                              
*-       IF XRETURN ATTN                                                        
*-                                                                              
         COMMENT                   IF XRETURN ATTN                              
         IF    XBFATTN,BEGIN                                                    
*                                                                               
         COMMENT                   SET ATTNID                                   
         SET   CPFIDSET            XRETURNED ATTNID SET                         
         LA    R0,L'XBATTNID                                                    
         LA    R1,XBATTNID                                                      
         LA    R2,=CL16'SYS.XRET_ATTNID '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   RESTORE ON CONDTION COMMANDS                 
         L     R0,CPXVSET#                                                      
         VCALL POPON                                                            
         L     R0,CPXVSET#         RESTORE ERROR/ATTN VARS                      
         VCALL POPERR                                                           
*                                                                               
         COMMENT                   GET CALLING EXEC STATE                       
         LA    R0,L'XBSTATE                                                     
         LA    R1,XBSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DO ANY LAST MINUTE CLEANUP                   
         LA    R1,XBSTATE                                                       
         ACALL XCLEANUP                                                         
*                                                                               
         COMMENT                   START RETURN EXEC                            
         L     R2,CVCURJCB         WITH ATTN FLAG SET                           
         USING JCB,R2                                                           
         SET   JCBFATTN                                                         
         DROP  R2                                                               
         LA    R1,XBSTATE                                                       
         ACALL START               DOES NOT RETURN  --->>>                      
         END                                                                    
*                                                                               
         COMMENT                                                                
         BOMB                      NEVER EXECUTED, WE EXIT ABOVE                
         PEND  ,              A HABIT,                                          
         SPACE 5                                                                
*-                                                                              
*-                                                                              
*-       XRETURN ERROR/ATTN OPTIONS                                             
*-                                                                              
*-                                                                              
XABRTPRT SCKW  ERR,XXXERROR                                                     
         SCKW  ERROR,XXXERROR                                                   
         SCKW  ERRID,XXXERRID,P                                                 
         SCKW  ERRMSG,XXXERRMS,P                                                
         SCKW  ATTN,XXXATTN                                                     
         SCKW  ATTENTION,XXXATTN                                                
         SCKW  ATTNID,XXXATNID,P                                                
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 2                                                                
*                                                                               
* ERROR                                                                         
*                                                                               
XXXERROR PROC  ,                                                                
         SET   XBFERROR            SET XRETURN ERROR FLAG                       
         IF    XBFATTN,BEGIN       XRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
* ERRID                                                                         
*                                                                               
XXXERRID PROC  ,                                                                
         SET   XBFERROR            SET XRETURN ERROR FLAG                       
         IF    XBFATTN,BEGIN       XRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPTOK R1                                                             
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),' VCALL NOTVALID '               
         MVC   XBERRID,@R1                                                      
         LC    R3,XBERRID                                                       
         IF    ((R3,LT,C'A'),OR,(R3,GT,C'Z')),' VCALL NOTVALID '                
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
* ERRMSG                                                                        
*                                                                               
XXXERRMS PROC  ,                                                                
         SET   XBFERROR            SET XRETURN ERROR FLAG                       
         IF    XBFATTN,BEGIN       XRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         VCALL DEQUOTE             DEQUOTE ERRMSG STRING                        
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid; quotes required.'                                    
         END                                                                    
         ST    R0,XBERRMLN                                                      
         LR    R3,R0                                                            
         MOVE  R3,XBERRMSG,@R1     SAVE ERROR MSG                               
         CLEAR R15                                                              
         PEND                                                                   
         SPACE 2                                                                
*                                                                               
* ATTENTION                                                                     
*                                                                               
XXXATTN  PROC  ,                                                                
         SET   XBFATTN             SET XRETURN ERROR FLAG                       
         IF    XBFERROR,BEGIN      XRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
* ATTNID                                                                        
*                                                                               
XXXATNID PROC  ,                                                                
         SET   XBFATTN             SET PRETURN ATTN FLAG                        
         IF    XBFERROR,BEGIN      PRETURN ERROR ATTN .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPTOK R1                                                             
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),' VCALL NOTVALID '               
         MVC   XBATTNID,@R1                                                     
         LC    R3,XBATTNID                                                      
         IF    ((R3,LT,C'A'),OR,(R3,GT,C'Z')),' VCALL NOTVALID '                
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*-box                                                                           
*                                                                               
*  XCLEANUP                                                                     
*                                                                               
*  THIS ROUTINE OFFERS US A CHANCE TO DO ANY NECESSARY                          
*  CLEANUP AT THE TIME THAT AN XPROC RETURNS.  THOUGH                           
*  SOMEDAY THIS ROUTINE MAY HAVE SOME MORE GENERAL PURPOSE                      
*  FOR NOW IT SIMPLY CHECKS THE DUMP STATUS INFORMATION,                        
*  AND WRITES A 'LINE NNNN IS LAST LINE' MESSAGE IF                             
*  APPROPRIATE.                                                                 
*                                                                               
*  ON ENTRY:                                                                    
*        R1 - THE NEW STATE FLAGS,                                              
*        CP - CONTAINS THE CURRENT STATE                                        
*                                                                               
*  ON EXIT:                                                                     
*        NO RETURNS,  WILL WRITE 'LINE NNNN IS LAST LINE' IF                    
*        APPROPRIATE DUMP STUFF IS TURNED ON.                                   
*                                                                               
XCLEANUP PROC  ,                                                                
         USING SNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   CHECK TO SEE IF WE                           
         COMMENT                   SHOULD WRITE DUMP MSG                        
         IF    (CPFXDUMP,AND,^SNFEXEC,AND,SNFTYPED),BEGIN                       
         L     R2,CPXDPLN                                                       
         IF    (R2,POS),BEGIN                                                   
         TWRITE                                                                 
         CLEAR CPFXDUMP                                                         
         TSEG  'Line '                                                          
         L     R0,CPXDPLN                                                       
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  ' is the last line.'                                             
         TWRITE                                                                 
         SET   CPFXDUMP                                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XRETURN CMD=                                                                 
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - 'XRETURN' SCANNED                                              
*                                                                               
*  ON EXIT:                                                                     
*       THIS ROUTINE DOES NOT RETURN !!                                         
*       THIS ROUTINE EXITS VIA XRETXCMD (START) ROUTINE.                        
*                                                                               
*                                                                               
XCDWA    RECORD BEGIN                                                           
XCDSTATE DS    XL(L'SNENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
XCOMMAND PROC  XCDWA                                                            
*                                                                               
         COMMENT                   SCAN COMMAND,                                
         COMMENT                      MOVE CMD INTO CP CMD AREA                 
         SCAN  ,                   SCAN 'CMD'                                   
         SCAN  ,                   SCAN START OF <CMD>                          
         SCANCHK                                                                
         IF    (R0,NP),BEGIN                                                    
         ERROR 'Missing command.'                                               
         END                                                                    
         SCBKUP                                                                 
         SCINFO                                                                 
         CEIL  R0,L'CPCMD                                                       
         LR    R2,R0                                                            
         DEX   R2,' MVC CPCMD(0),@R1 '     MOVE COMMAND                         
         STH   R0,CPCMDLEN                                                      
         LA    R1,CPCMD                                                         
*                                                                               
         COMMENT                   GET SET TO RETURN FROM XCALL                 
*                                                                               
         COMMENT                   RESTORE ON CONDTION COMMANDS                 
         L     R0,CPXVSET#                                                      
         VCALL POPON                                                            
         L     R0,CPXVSET#         RESTORE ERROR/ATTN VARS                      
         VCALL POPERR                                                           
*                                                                               
         COMMENT                   GET CALLING EXEC STATE                       
         LA    R0,L'XBSTATE                                                     
         LA    R1,XCDSTATE                                                      
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPXVSET#                                                      
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DO ANY LAST MINUTE CLEANUP                   
         LA    R1,XCDSTATE                                                      
         ACALL XCLEANUP                                                         
*                                                                               
         COMMENT                   START RETURN EXEC, AND ISSUE                 
         COMMENT                   COMMAND AFTER RETURN, YUCKKY POO             
         LA    R1,XCDSTATE                                                      
         ACALL XRETXCMD            DOES NOT RETURN  --->>>                      
*                                                                               
         COMMENT                                                                
         BOMB                      NEVER EXECUTED, WE EXIT ABOVE                
         PEND  ,              A HABIT,                                          
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'PCALL Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PCALL - PROCEDURE CALL                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINDER OF PCALL COMMAND !                                   
*                                                                               
*  WE DO THE FOLLOWING (MORE OR LESS) !                                         
*       - GET THE NAME OF THE PROCEDURE TO CALL                                 
*       - GET THE PARAMETER STRING                                              
*       - GET THE PARAMETER LIST                                                
*       - GET THE PARAMETER VALUES                                              
*       - GET THE PROC NAME LIST                                                
*       - PASS PARAMETERS                                                       
*       - SAVE CALLING/CALLED PARM LISTS                                        
*       - SAVE CALLING ROUTINES EXECUTION STATE                                 
*       - ESTABLISH NEW ROUTINES EXECUTION STATE                                
*       - TAKE OFF !                                                            
*                                                                               
* NOTE: CPPLIMIT=8                MAXIMUM NUMBER OF PARAMETERS                  
*                                                                               
*                                                                               
PCLWA    RECORD BEGIN                                                           
PCLFLAG  FLAG  ,                                                                
         FLAG  (PCLPARMS,1,EQ)     PARM LIST                                    
         FLAG  (PCLFREE,2,EQ)      FREE FORMAT PARM STRING                      
         DS    0F                                                               
PCLNAME  DS    CL(L'VNNAME)        PROCEDURE NAME                               
PCLSTART DS    F                   STARTING LINE NO OF PROC                     
PCLEND   DS    F                   ENDING LINE NO OF PROC                       
PCNEWSET DS    F                   CALLED PROC NEW VAR SET NUMBER               
PCPARM   DS    XL(&LINESZ)         PARAMETER STRING                             
PCPARMLN DS    F                   PARAMETER STRING LENGTH                      
PCPARMCT DS    F                   PARM COUNT                                   
PCLPLIST DS    XL(L'PLIST)         PARM LIST (FROM CALLER)                      
PCLNLIST DS    XL(L'NLIST)         NAME LIST (FROM PROC)                        
PCLVLIST DS    XL(L'VLIST)         VALUE LIST (ACTUAL PARM VALUES)              
PCLSTATE DS    XL(L'SNENTRY)       EXEC STATE INFO DSECT                        
         END                                                                    
*                                                                               
*                                                                               
PCALL    XPROC PCLWA                                                            
*                                                                               
      COMMENT                      PCALL, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'PCALL: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      PCALL, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'PCALL: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS PCALL COMMAND                                                  
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CLEAR WORK AREA                              
         LA    R2,PCLWA+40            (EXCEPT FOR ENTRY REGS)                   
         LA    R3,L'PCLWA-40                                                    
         CLEAR R5                                                               
         MVCL  R2,R4                                                            
*                                                                               
         COMMENT                   SCAN PCALL COMMAND                           
         LA    R2,PCLNAME                                                       
         VCALL SCNPNAME            SCAN PROCEDURE NAME                          
         BNZ   CVERROR                                                          
*                                                                               
         COMMENT                   CHECK TO SEE IF PROC IS DEFINED              
         LA    R1,PCLNAME                                                       
         VCALL CHKPDEF                                                          
         IF    NZ,BEGIN                                                         
         TSEG  PCLNAME,L'PCLNAME,T                                              
         ERROR ': undefined procedure name.'                                    
         END                                                                    
         ST    R0,PCLSTART         *$  NOT SET BY ANYBODY ABOVE ??              
         ST    R1,PCLEND           *$  ??                                       
*                                                                               
         COMMENT                   SCAN PCALL PARM LIST                         
         ACALL SCNPCALL                                                         
         IF    (R2,Z),BEGIN                                                     
         SET   PCLPARMS            PARM-LIST SCANNED                            
         END                                                                    
         ELSE  BEGIN                                                            
         SET   PCLFREE             FREE-FORMAT PARM STRING SCANNED              
         MVC   PCPARMCT,=F'-1'     SET -1 PARM COUNT, FOR FREE FORMAT           
         END                                                                    
         ST    R0,PCPARMLN                                                      
         L     R3,PCPARMLN         SAVE INPUT PARM LIST/STRING                  
         MOVE  R3,PCPARM,@R1                                                    
*                                                                               
         COMMENT                   GET NEW VARIABLE SET NUMBER                  
         VCALL GTNEWSET                                                         
         ST    R0,PCNEWSET                                                      
         COMMENT                   CLEAR NEW VARIABLE SET                       
         L     R0,PCNEWSET                                                      
         VCALL CLRVSETS                                                         
*                                                                               
         COMMENT                   SCAN PARM LIST                               
         IF    PCLPARMS,BEGIN                                                   
         L     R0,PCPARMLN                                                      
         LA    R1,PCPARM                                                        
         LA    R2,PCLPLIST                                                      
         ACALL SCNPLIST                                                         
         ST    R0,PCPARMCT                                                      
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM VALUES                             
         IF    PCLPARMS,BEGIN                                                   
         L     R0,PCPARMLN                                                      
         LA    R1,PCPARM                                                        
         LA    R2,PCLVLIST                                                      
         ACALL SCNVLIST                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CALLED XPROC NAME LIST                   
         LA    R1,PCLNAME                                                       
         LA    R2,PCLNLIST                                                      
         VCALL GTPNLIST                                                         
*                                                                               
         COMMENT                   PASS PARMS                                   
         L     R0,PCPARMCT                                                      
         LA    R1,PCLVLIST                                                      
         LA    R2,PCLNLIST                                                      
         L     R3,PCNEWSET                                                      
         ACALL PASSPRMS                                                         
*                                                                               
         COMMENT                   PASS PARM_STR, AND PARM_COUNT                
         L     R0,PCPARMLN                                                      
         LA    R1,PCPARM                                                        
         L     R2,PCPARMCT                                                      
         ACALL PASSPARM                                                         
*                                                                               
         COMMENT                   SAVE CALLERS PLIST                           
         LA    R0,L'PCLPLIST                                                    
         LA    R1,PCLPLIST                                                      
         LA    R2,=CL16'SYS*CALLER_PLIST'                                       
         L     R3,PCNEWSET                                                      
         VCALL SAVEVSTK                                                         
         COMMENT                   SAVE CALLED NLIST                            
         LA    R0,L'PCLNLIST                                                    
         LA    R1,PCLNLIST                                                      
         LA    R2,=CL16'SYS*PROC_NLIST  '                                       
         L     R3,PCNEWSET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   GET CURRENT EXEC STATE                       
         LA    R1,PCLSTATE                                                      
         VCALL GETSTATE                                                         
*                                                                               
         COMMENT                   SAVE CURRENT EXEC STATE                      
         LA    R0,L'SNENTRY                                                     
         LA    R1,PCLSTATE                                                      
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,PCNEWSET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   SET UP NEW EXEC STATE                        
         LA    R1,PCLNAME                                                       
         LA    R3,PCLSTATE                                                      
         ACALL GTPSTART                                                         
*                                                                               
         COMMENT                   IF DEBUG ACTIVE, CHECK FOR TRAP              
         IF    CPFDEBUG,BEGIN                                                   
         L     R0,CP#XSET                                                       
         LA    R1,PCLNAME                                                       
         VCALL DBCALL                                                           
         END                                                                    
*                                                                               
         COMMENT                   START NEW EXEC                               
         LA    R1,PCLSTATE                                                      
         ACALL START               DOES NOT RETURN                              
*                                                                               
         BOMB  ,                   NEVER EXECUTED, WE EXIT ABOVE                
*                                                                               
         CLEAR R15                                                              
         PEND  ,          NEVER EXECUTED, WE EXIT ABOVE                         
         TITLE 'RETURN from procedure call'                                     
*box                                                                            
*                                                                               
*                                                                               
*  PRETURN - RETURN FROM PROCEDURE                                              
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - RETURN OPTIONS, OR PARM LIST                                   
*                                                                               
*  ONE EXIT:                                                                    
*       THIS ROUTINE DOES NOT RETURN !!                                         
*       THIS ROUTINE EXITS VIA START ROUTINE.                                   
*                                                                               
*                                                                               
*  NOTE: 'RETURN' COMMAND IS REFERRED TO HERE AS 'PRETURN'                      
*                                                                               
*  NOTE2: THIS DESCRIBES THE MANY NAME LISTS:                                   
*       PRRLIST - NAME LIST IN  .. RETURN (<NAME LIST>)                         
*       PRNLIST - NAME LIST IN  .. PROC NAME (<NAME LIST>)                      
*       PRPLIST - PARM LIST IN  .. PCALL NAME (<PARM LIST>)                     
*       PRLIST  - CALCULATED RETURN LIST CONTAINS CALLERS VAR NAMES             
*                                                                               
*                                                                               
PRWA     RECORD BEGIN                                                           
PRCNT    DS    F                   RETURN PARMS COUNT                           
PRRLIST  DS    XL(L'NLIST)         RETURN NAME LIST  (PROCEDURES)               
PRNLIST  DS    XL(L'NLIST)         ENTRY NAME LIST (PROCEDURES)                 
PRPLIST  DS    XL(L'NLIST)         ENTRY NAME LIST (CALLERS)                    
PRLIST   DS    XL(L'NLIST)         RETURN NAME LIST  (CALLERS)                  
PRVLIST  DS    XL(L'VLIST)         RETURN VALUE LIST                            
PRPARMLN DS    F                   PARM/OPTIONS LENGTH                          
PRPARM   DS    F                   PARM/OPTIONS ADDRESS                         
PRSTATE  DS    XL(L'SNENTRY)       EXEC-STATE DSECT                             
         END                                                                    
*                                                                               
*                                                                               
PRETURN  XPROC PRWA                                                             
*                                                                               
         COMMENT                   CHECK IF PRETURN VALID                       
         IF    (CPFXOLD,OR,(CP#XSET,EQ,=A(CP#XOLD))),BEGIN                      
         ERROR 'Not in procedure.  Command ignored.'                            
         END                                                                    
*                                                                               
         COMMENT                   CHECK IF PRETURN VALID                       
         IF    (CPXVSET#,EQ,CPVSET#),BEGIN                                      
         ERROR 'RETURN: invalid within XPROC.  Use XRETURN instead.'            
         END                                                                    
*                                                                               
         COMMENT                   IF DEBUG ACTIVE, CHECK FOR TRAPS             
         IF    CPFDEBUG,BEGIN                                                   
         L     R0,CP#XSET                                                       
         LA    R1,CPPROCNM                                                      
         VCALL DBRET                                                            
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM/OPTIONS                            
         ACALL PSCNPARM                                                         
         ST    R0,PRPARMLN                                                      
         ST    R1,PRPARM                                                        
*                                                                               
         COMMENT                   CHECK FOR                                    
         COMMENT                      PRETURN ERROR ...                         
         COMMENT                      PRETURN ATTN                              
         COMMENT                                                                
         IF    ((@R1,NE,'('),AND,(R0,POS)),BEGIN                                
         COMMENT                   GOT PRETURN ERR/ATTN  (OR ELSE!)             
         ACALL PABORT              DOES NOT RETURN --->>>                       
         END                                                                    
*                                                                               
         COMMENT                   SCAN RETURN LIST VALUES                      
         L     R0,PRPARMLN                                                      
         L     R1,PRPARM                                                        
         LA    R2,PRVLIST                                                       
         ACALL SCNVLIST                                                         
         ST    R0,PRCNT                                                         
*                                                                               
         COMMENT                   SCAN RETURN LIST NAMES                       
         L     R0,PRPARMLN                                                      
         L     R1,PRPARM                                                        
         LA    R2,PRRLIST                                                       
         ACALL SCNNLIST                                                         
*                                                                               
         COMMENT                   GET CALLING PARM LIST (NAMES)                
         LA    R0,L'PRNLIST                                                     
         LA    R1,PRNLIST                                                       
         LA    R2,=CL16'SYS*PROC_NLIST  '                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET CALLING PARM LIST (NAMES)                
         LA    R0,L'PRPLIST                                                     
         LA    R1,PRPLIST                                                       
         LA    R2,=CL16'SYS*CALLER_PLIST'                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   ASSOCIATE RETURN NAMES WITH                  
         COMMENT                   PARM NAMES, POSITION WISE                    
         USING VNENTRY,R1                                                       
         LA    R0,PRLIST                                                        
         LA    R1,L'NLIST                                                       
         LA    R2,PRRLIST                                                       
         LA    R3,L'NLIST                                                       
         MVCL  R0,R2               COPY RETURN LIST                             
         L     R0,PRCNT                                                         
         LA    R1,PRLIST                                                        
         WHILE (R0,POS),BEGIN      LOOP THRU ALL RETURN PARMS                   
*                                                                               
         COMMENT                   FIND ASSOCIATED NAME                         
         LA    R2,PRNLIST                                                       
         LA    R4,PRNLIST                                                       
         LA    R4,L'NLIST(R4)                                                   
         WHILE ((R2,LT,R4),AND,(@R2,NE,VNDESC)),BEGIN                           
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         IF    (R2,GE,R4),BEGIN                                                 
         LR    R2,R1                                                            
         VCALL SEGVNAME                                                         
         ERROR ': not a procedure parameter.  Unable to return value.'          
         END                                                                    
         LA    R4,PRNLIST          CALCULATE NAME OFFSET IN R2                  
         SR    R2,R4                                                            
         LA    R2,PRPLIST(R2)                                                   
         MVC   0(L'VNDESC,R1),0(R2)   SAVE CALLING NAME IN RETURN LIST          
*                                                                               
         COMMENT                   LOOP THRU ALL RETURN VARIABLES               
         LA    R1,L'VNDESC(R1)                                                  
         DECR  R0                                                               
         END                                                                    
         DROP  R1                                                               
*                                                                               
         COMMENT                   GET CALLING EXECUTION STATE                  
         LA    R0,L'PRSTATE                                                     
         LA    R1,PRSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RETURN PASSED PARMS                          
         L     R0,PRCNT                                                         
         LA    R1,PRVLIST                                                       
         LA    R2,PRLIST                                                        
         LA    R3,PRRLIST                                                       
         L     R4,PRSTATE+(SNVSET#-SNENTRY)                                     
         L     R5,PRSTATE+(SNXVSET#-SNENTRY)                                    
         ACALL RETPARMS            RETURN PARMS                                 
*                                                                               
         COMMENT                   RESET SOME PCALL RETURN PARMS                
         LA    R1,PRSTATE                                                       
         ACALL SETPRET                                                          
*                                                                               
         COMMENT                   START RETURN EXEC                            
         LA    R1,PRSTATE                                                       
         ACALL START               DOES NOT RETURN                              
*                                                                               
*                                                                               
         BOMB                                                                   
*                                                                               
         CLEAR R15                 NEVER EXECUTED, WE EXIT ABOVE                
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PABORT - PROCESS THE FOLLOWING:                                              
*                                                                               
*       RETURN ERROR                                                            
*       RETURN ATTENTION                                                        
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF ERROR/ATTN OPTIONS                                       
*       R1 - LOCATION OF ERROR/ATTN OPTIONS AFTER 'RETURN'                      
*            NOTE: OPTIONS LIST IS NON-EMPTY (OR ELSE)                          
*                                                                               
*  ON EXIT:                                                                     
*       THIS ROUTINE DOES NOT RETURN !!                                         
*       THIS ROUTINE EXITS VIA START ROUTINE.                                   
*                                                                               
*                                                                               
PBWA     RECORD BEGIN                                                           
PBFLAG   FLAG                                                                   
         FLAG  PBFERROR            PRETURN ERROR                                
         FLAG  PBFATTN             PRETURN ATTN                                 
         DS    0F                                                               
PBERRID  DS    CL8                 ERROR ID                                     
PBERRMSG DS    CL(&LINESZ)         ERROR MESSAGE TEXT                           
PBERRMLN DS    F                   ERROR MESSAGE LENGTH                         
PBATTNID DS    CL8                 ATTN ID                                      
PBSTATE  DS    XL(L'SNENTRY)       EXEC-STATE DSECT                             
         END                                                                    
*                                                                               
*                                                                               
PABORT   PROC  PBWA                                                             
         LA    R6,PBWA                                                          
         USING PBWA,R6                                                          
*                                                                               
         COMMENT                   SET UP SCANNER,                              
         SCINIT (R1),(R0)                                                       
         SCPUSH                                                                 
         SCAN  ,                   (CHECK FOR NON-NULL PARMS)                   
         SCANCHK ,                                                              
         SCPOP                                                                  
         IF    (R0,Z),BEGIN                                                     
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SET WORK AREA DEFAULTS                       
         CLEAR PBFLAG                                                           
         MVC   PBERRID,=CL8'ERROR   '                                           
         SETMSG 'Error in PROC.'                                                
         ST    R0,PBERRMLN                                                      
         LR    R3,R0                                                            
         MOVE  R3,PBERRMSG,@R1                                                  
         MVC   PBATTNID,=CL8'ATTN    '                                          
         CLEAR PBSTATE                                                          
*                                                                               
         COMMENT                   SCAN ERROR/ATTN OPTIONS                      
         SCAN  PABRTPRT                                                         
         SCANCHK                                                                
*-                                                                              
*-       IF  PRETURN ERROR ...                                                  
*-                                                                              
         COMMENT                   IF PRETURN ERROR                             
         IF    PBFERROR,BEGIN                                                   
*                                                                               
         COMMENT                   SET ERROR VARIABLES                          
         MVC   CPERRID,PBERRID                                                  
         L     R0,PBERRMLN                                                      
         LA    R1,PBERRMSG                                                      
         IF    (R0,NZ),BEGIN                                                    
         TSEG  (R1),(R0),CR                                                     
         END                                                                    
*                                                                               
         COMMENT                   GET CALLING EXEC STATE                       
         LA    R0,L'PBSTATE                                                     
         LA    R1,PBSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RESET SOME PCALL RETURN PARMS                
         LA    R1,PBSTATE                                                       
         ACALL SETPRET                                                          
*                                                                               
         COMMENT                   START RETURN EXEC                            
         SET   CPFERROR            WITH ERROR FLAGS SET                         
         SET   CPFABORT                                                         
         LA    R1,PBSTATE                                                       
         ACALL START               DOES NOT RETURN  --->>>                      
         END                                                                    
*-                                                                              
*-       IF PRETURN ATTN                                                        
*-                                                                              
         COMMENT                   IF PRETURN ATTN                              
         IF    PBFATTN,BEGIN                                                    
*                                                                               
         COMMENT                   SET ATTNID                                   
         SET   CPFIDSET            RETURNED ATTNID SET                          
         LA    R0,L'PBATTNID                                                    
         LA    R1,PBATTNID                                                      
         LA    R2,=CL16'SYS.XRET_ATTNID '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   GET CALLING EXEC STATE                       
         LA    R0,L'PBSTATE                                                     
         LA    R1,PBSTATE                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RESET SOME PCALL RETURN PARMS                
         LA    R1,PBSTATE                                                       
         ACALL SETPRET                                                          
*                                                                               
         COMMENT                   START RETURN EXEC                            
         L     R2,CVCURJCB         WITH ATTN FLAG SET                           
         USING JCB,R2                                                           
         SET   JCBFATTN                                                         
         DROP  R2                                                               
         LA    R1,PBSTATE                                                       
         ACALL START               DOES NOT RETURN  --->>>                      
         END                                                                    
*                                                                               
         COMMENT                                                                
         BOMB                      NEVER EXECUTED, WE EXIT ABOVE                
         PEND  ,              A HABIT,                                          
         SPACE 5                                                                
*-                                                                              
*-                                                                              
*-       PRETURN ERROR/ATTN OPTIONS                                             
*-                                                                              
*-                                                                              
PABRTPRT SCKW  ERR,PPPERROR                                                     
         SCKW  ERROR,PPPERROR                                                   
         SCKW  ERRID,PPPERRID,P                                                 
         SCKW  ERRMSG,PPPERRMS,P                                                
         SCKW  ATTN,PPPATTN                                                     
         SCKW  ATTENTION,PPPATTN                                                
         SCKW  ATTNID,PPPATNID,P                                                
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 2                                                                
*                                                                               
* ERROR                                                                         
*                                                                               
PPPERROR PROC  ,                                                                
         SET   PBFERROR            SET PRETURN ERROR FLAG                       
         IF    PBFATTN,BEGIN       PRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
* ERRID                                                                         
*                                                                               
PPPERRID PROC  ,                                                                
         SET   PBFERROR            SET PRETURN ERROR FLAG                       
         IF    PBFATTN,BEGIN       PRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPTOK R1                                                             
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),' VCALL NOTVALID '               
         MVC   PBERRID,@R1                                                      
         LC    R3,PBERRID                                                       
         IF    ((R3,LT,C'A'),OR,(R3,GT,C'Z')),' VCALL NOTVALID '                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
* ERRMSG                                                                        
*                                                                               
PPPERRMS PROC  ,                                                                
         SET   PBFERROR            SET PRETURN ERROR FLAG                       
         IF    PBFATTN,BEGIN       PRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         VCALL DEQUOTE             DEQUOTE ERRMSG STRING                        
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid; quotes required.'                                    
         END                                                                    
         ST    R0,PBERRMLN                                                      
         LR    R3,R0                                                            
         MOVE  R3,PBERRMSG,@R1     SAVE ERROR MSG                               
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 2                                                                
*                                                                               
* ATTENTION                                                                     
*                                                                               
PPPATTN  PROC  ,                                                                
         SET   PBFATTN             SET PRETURN ERROR FLAG                       
         IF    PBFERROR,BEGIN      PRETURN ATTN ERROR .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         PEND  ,                                                                
*                                                                               
* ATTNID                                                                        
*                                                                               
PPPATNID PROC  ,                                                                
         SET   PBFATTN             SET PRETURN ATTN FLAG                        
         IF    PBFERROR,BEGIN      PRETURN ERROR ATTN .. NOT ALLOWED            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPTOK R1                                                             
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),' VCALL NOTVALID '               
         MVC   PBATTNID,@R1                                                     
         LC    R3,PBATTNID                                                      
         IF    ((R3,LT,C'A'),OR,(R3,GT,C'Z')),' VCALL NOTVALID '                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'SYSCALL Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  SYSCALL - PROCEDURE CALL                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINDER OF SYSCALL COMMAND !                                 
*                                                                               
*  WE DO THE FOLLOWING (MORE OR LESS) !                                         
*       - SCAN COMMAND                                                          
*       - GET ADDRESS OF SYSTEM PROCEDURE                                       
*       - GET THE PARAMETER STRING                                              
*       - GET THE PARAMETER LIST                                                
*       - GET THE PARAMETER VALUES                                              
*       - CALL SYSTEM PROCEDURE                                                 
*       - PASS RETURN VALUES                                                    
*                                                                               
* NOTE: CPPLIMIT=8                MAXIMUM NUMBER OF PARAMETERS                  
*                                                                               
*                                                                               
SCLWA    RECORD BEGIN                                                           
SCLFLAG  FLAG  ,                                                                
         FLAG  (SCLPARMS,1,EQ)     PARM LIST                                    
         FLAG  (SCLFREE,2,EQ)      FREE FORMAT PARM STRING                      
         DS    0F                                                               
SCLNAME  DS    CL(L'VNNAME)        PROCEDURE NAME                               
SCLRTNP  DEFRTNP ,                 SYSTEM PROCEDURE ROUTINE POINTER             
SCPARM   DS    XL(&LINESZ)         PARAMETER STRING                             
SCPARMLN DS    F                   PARAMETER STRING LENGTH                      
SCPARMCT DS    F                   PARM COUNT                                   
SCLPLIST DS    XL(L'PLIST)         PARM LIST NAMES (FROM CALLER)                
SCLVLIST DS    A                   VALUE LIST (ACTUAL PARM VALUES)              
SCLRLIST DS    A                   VALUE LIST (RETURNED FROM PROC)              
* NOTE: BOTH SCLVLIST AND SCLRLIST ARE MANUALLY XPUSHED AND POPPED              
* AS OTHERWISE THE WORK AREA IS LARGER THAN 4K BYTES AND XENTER                 
* MACRO FREAKS OUT.                                                             
         END                                                                    
*                                                                               
*                                                                               
SYSCALL  XPROC SCLWA                                                            
*                                                                               
         COMMENT                   XPUSH SCNVLIST,SCNRLIST                      
         XPUSH ,,L'VLIST,PTR=R1                                                 
         ST    R1,SCLVLIST                                                      
         XPUSH ,,L'VLIST,PTR=R2                                                 
         ST    R2,SCLRLIST                                                      
*                                                                               
         COMMENT                   CLEAR WORK AREA                              
         LA    R4,SCLWA+40            (EXCEPT FOR ENTRY REGS)                   
         L     R5,=AL4(L'SCLWA-40+L'VLIST+L'VLIST)                              
         CLEAR R15                                                              
         MVCL  R4,R14                                                           
         ST    R1,SCLVLIST         MUST RESTORE THESE CLEARED ADDRS             
         ST    R2,SCLRLIST                                                      
*                                                                               
         COMMENT                   SCAN SYSCALL COMMAND                         
         LA    R2,SCLNAME                                                       
         VCALL SCNPNAME            SCAN PROCEDURE NAME                          
         BNZ   CVERROR                                                          
         LA    R0,SCLRTNP                                                       
         LA    R1,SCLNAME                                                       
         VCALL GETPROCP            GET SYSTEM PROC POINTER                      
         IF    (R15,NZ),BEGIN                                                   
         TSEG  SCLNAME,L'SCLNAME,T                                              
         ERROR ': undefined system procedure name.'                             
         END                                                                    
*                                                                               
         COMMENT                   SCAN SYSCALL PARM LIST                       
         ACALL SCNPCALL                                                         
         IF    (R2,Z),BEGIN                                                     
         SET   SCLPARMS            PARM-LIST SCANNED                            
         END                                                                    
         ELSE  BEGIN                                                            
         SET   SCLFREE             FREE-FORMAT PARM STRING SCANNED              
         MVC   SCPARMCT,=F'-1'     SET -1 PARM COUNT, FOR FREE FORMAT           
         ERROR 'Free Format parameter string invalid for SYSCALL.'              
         END                                                                    
         ST    R0,SCPARMLN                                                      
         L     R3,SCPARMLN         SAVE INPUT PARM LIST/STRING                  
         MOVE  R3,SCPARM,@R1                                                    
*                                                                               
         COMMENT                   SCAN PARM LIST                               
         IF    SCLPARMS,BEGIN                                                   
         L     R0,SCPARMLN                                                      
         LA    R1,SCPARM                                                        
         LA    R2,SCLPLIST                                                      
         ACALL SCNPLIST                                                         
         ST    R0,SCPARMCT                                                      
         END                                                                    
*                                                                               
         COMMENT                   SCAN PARM VALUES                             
         IF    SCLPARMS,BEGIN                                                   
         L     R0,SCPARMLN                                                      
         LA    R1,SCPARM                                                        
         L     R2,SCLVLIST                                                      
         ACALL SCNVLIST                                                         
         END                                                                    
*                                                                               
         COMMENT                   CALL SYSTEM PROCEDURE                        
         L     R0,SCPARMCT         R0 - PARM COUNT                              
         L     R1,SCLVLIST         R1 - PARM VALUES                             
         L     R6,SCLRLIST         R6 - RETURN AREA, CONTAINS PROC              
         MVC   0(L'SCLNAME,R6),SCLNAME         NAME ON ENTRY                    
         LA    R14,SCLRTNP         R14 - SYSTEM PROCEDURE ADDR                  
         CALLRTNP @R14             VCALL PROCEDURE                              
         BNZ   CVERROR                                                          
*                                                                               
         COMMENT                   RETURN PROCEDURE VARIABLES                   
         L     R0,SCPARMCT         R0 - PARM COUNT                              
         L     R1,SCLRLIST         R1 - PARM VALUES                             
         LA    R2,SCLPLIST         R2 - PARM NAMES                              
         ACALL RETSYSP             RETURN VALUES                                
*                                                                               
         COMMENT                   EXIT VIA CVNEXT !                            
         B     CVNEXT                                                           
*                                                                               
         XPOP  ,,L'VLIST                                                        
         XPOP  ,,L'VLIST                                                        
         CLEAR R15                                                              
         PEND  ,          NEVER EXECUTED, WE EXIT ABOVE                         
         TITLE 'XLOAD Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XLOAD - LOAD EXEC FILE                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       SCAN CONTROL BLOCK - REMAINDER OF XLOAD COMMAND                         
*                                                                               
*  ON EXIT:                                                                     
*       EXEC FILE LOADED,                                                       
*       EXITS VIA CVNEXT, OR CVERROR                                            
*                                                                               
*                                                                               
XLDWA    RECORD BEGIN                                                           
XLDXDSN  DS    XL(L'XDSNAME)                                                    
         END                                                                    
*                                                                               
*                                                                               
XLOAD    XPROC XLDWA                                                            
*                                                                               
         COMMENT                   SCAN FOR DATA SET NAME OR 'ACTIVE'           
         LA    R0,1                 'FROM' KEYWORD NOT REQUIRED                 
         ACALL SCNDSN                                                           
*                                                                               
         COMMENT                   IF ACTIVE, LOAD ACTIVE FILE                  
         IF    (R15,Z),BEGIN                                                    
         VCALL NOOPTS              NO OPTIONS ALLOWED FOR XA                    
         ACALL LOADACT                                                          
         END                                                                    
*                                                                               
         COMMENT                   IF DSN SPECIFIED, LOAD DSN                   
         ELSEIF (R15,NZ),BEGIN                                                  
         ACALL LOADDSN                                                          
         END                                                                    
*                                                                               
         B     CVNEXT              BYE GUYS !                                   
         PEND  ,                                                                
         TITLE 'PUBLOAD Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  PUBLOAD - LOAD PUBLIC EXEC FILE                                              
*                                                                               
*  ON ENTRY:                                                                    
*       SCAN CONTROL BLOCK - REMAINDER OF PUBLOAD COMMAND                       
*                                                                               
*  ON EXIT:                                                                     
*       EXEC FILE LOADED,                                                       
*       EXITS VIA CVNEXT, OR CVERROR                                            
*                                                                               
*                                                                               
PLDWA    RECORD BEGIN                                                           
PLDXDSN  DS    XL(L'XDSNAME)                                                    
         END                                                                    
*                                                                               
*                                                                               
PUBLOAD  XPROC PLDWA                                                            
*                                                                               
*  The theory was to allow a System priv'd account to PUBLOAD                   
*  new files, and to allow any account to PUBLOAD an existing file              
*  (to update it).                                                              
*                                                                               
*  Right now, we don't bother and will let anyone do a pubload.                 
*                                                                               
*****    COMMENT                   FOR PRIVILEGED ACCOUNTS ONLY                 
*****    IF    ^CPSFSPR,BEGIN                                                   
*****    VCALL NOTVALID                                                         
*****    END                                                                    
*                                                                               
         COMMENT                   SCAN FOR DATA SET NAME OR 'ACTIVE'           
         LA    R0,1                 'FROM' KEYWORD NOT REQUIRED                 
         ACALL SCNDSN                                                           
*                                                                               
         COMMENT                   IF ACTIVE, OH NO !                           
         IF    (R15,Z),BEGIN                                                    
         ERROR 'ACTIVE file cannot be loaded into PUBLIC EXEC file.'            
         END                                                                    
*                                                                               
         COMMENT                   IF DSN SPECIFIED, LOAD DSN                   
         ELSEIF (R15,NZ),BEGIN                                                  
         ACALL LOADPUB                                                          
         END                                                                    
*                                                                               
         B     CVNEXT              BYE GUYS !                                   
*                                                                               
         PEND  ,                                                                
         TITLE 'EXEC LOAD Routines'                                             
*box                                                                            
*                                                                               
*                                                                               
*  LOADDSN - LOAD EXEC FILE FROM EXTERNAL DATA SET.                             
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*      FFINFO DSECT DESCRIBES FILE TO LOAD                                      
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ERRORS WILL EXIT VIA CVERROR.                                      
*                                                                               
*                                                                               
LOADWA   RECORD BEGIN                                                           
LOADXF   DS    XL(L'XFENTRY)       EXEC INFO ENTRY                              
         END                                                                    
*                                                                               
*                                                                               
LOADDSN  PROC  LOADWA                                                           
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LA    R6,LOADXF                                                        
         LA    R5,XFNAME                                                        
*                                                                               
         COMMENT                   INIT WORK AREA                               
         LA    R1,XDSNAME                                                       
         ACALL SETXNAME                                                         
*                                                                               
         COMMENT                   IF IN OLD EXEC, OR NO EXEC                   
         COMMENT                   THEN NO EXTENDED EXEC ARE ACTIVE             
         IF    (CPFXOLD,OR,(CP#XSET,EQ,=A(CP#XOLD))),BEGIN                      
         ACALL CLRALIST            SO CLEAR ACTIVE EXECS LIST                   
         END                                                                    
*                                                                               
         COMMENT                   IF ALREADY LOADED, DELETE EXEC               
         CLEAR R0                                                               
         LA    R1,XFNAME                                                        
         LA    R2,XFENTRY                                                       
         ACALL GETUINFO                                                         
         IF    Z,BEGIN             IF LOADED,                                   
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         ACALL CHKALIST                                                         
         IF    Z,BEGIN             IF ACTIVE, WE CANNOT RELOAD                  
         ERROR 'File is currently active, unable to load.'                      
         END                                                                    
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         ACALL DELUEXEC            DELETE (USER) EXEC                           
         END                                                                    
*                                                                               
         COMMENT                   OPEN EXTERNAL FILE                           
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
*                                                                               
         COMMENT                   IF OPENED OK,                                
         COMMENT                                                                
         ACALL GETSETNO            GET FREE EXEC SET NUMBER                     
         SET   XFUSERX             SET UP EXEC INFO ENTRY                       
         ST    R0,XF#SETNO                                                      
         LA    R3,CPEXEC                                                        
         ST    R3,XFRGEXEC                                                      
         LA    R3,CPFLOW                                                        
         ST    R3,XFRGFLOW                                                      
         VCALL LOCALTOD            Save load time/date                          
         STM   R0,R1,XFLDTIME                                                   
*                                                                               
         COMMENT                   MARK EXEC ACTIVE                             
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         ACALL MRKALIST                                                         
         IF    NZ,BEGIN                                                         
         ERROR 'Too many (>64) EXEC files active.'                              
         END                                                                    
*                                                                               
         COMMENT                   SET LOAD EXEC/FLOW FILES                     
         MVC   CP#XLOAD,XF#SETNO       SET #                                    
         MVC   CPRGLOAD,XFRGEXEC       EXEC RECORD GROUP                        
         MVC   CPRGFC,XFRGFLOW         FLOW RECORD GROUP                        
*                                                                               
         COMMENT                   LOAD EXEC FILE                               
         ACALL LDDSN                                                            
*                                                                               
         COMMENT                   CLOSE EXTERNAL FILE                          
         VCALL EXTCLOSE                                                         
*                                                                               
         COMMENT                   SET EXEC INFOMATION                          
         LA    R2,XFENTRY                                                       
         ACALL SETXINFO                                                         
*                                                                               
         COMMENT                   DO FLOW PROCESSING                           
         VCALL FLOWCTRL                                                         
*                                                                               
         COMMENT                   MARK SET NUMBER (LRU SCHEME)                 
         L     R0,XF#SETNO                                                      
         ACALL XLISTMRK                                                         
*$       ACALL LLISTMRK            COULD EFFECT CALL COUNT !!                   
*$                                 IF IN EXEC BREAK WE CAN IGNORE               
*                                                                               
         COMMENT                   CLEAR LOAD POINTERS                          
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LOADPUB - LOAD PUBLIC EXEC FILE FROM EXTERNAL DATA SET.                      
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*      FFINFO DSECT DESCRIBES FILE TO LOAD                                      
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ERRORS WILL EXIT VIA CVERROR.                                      
*                                                                               
*  EXPLANATION: EXISTING PUBLIC EXECS MAY NOT BE DELETED!                       
*  THIS IS BECAUSE THERE IS NO WAY TO TELL IF THEY ARE IN USE.                  
*  TO RELOAD AN EXISTING PUBLIC EXEC WE MARK THE OLD ONE                        
*  TO USE A NEW NAME.  THEN UPDATE THE EXEC INFO TO POINT                       
*  AT THE NEW RELOADED COPY.  COMPREHEND IT.                                    
*                                                                               
*                                                                               
LDPWA    RECORD BEGIN                                                           
LDPXFOLD DS    XL(L'XFENTRY)       INFO FOR PREVIOUS COPY IF ANY                
LDPXFNEW DS    XL(L'XFENTRY)       INFO FOR CURRENT COPY OF EXEC                
         END                                                                    
*                                                                               
*                                                                               
LOADPUB  PROC  LDPWA                                                            
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LA    R6,LDPXFNEW                                                      
         LA    R5,XFNAME                                                        
*                                                                               
         COMMENT                   PUT NAME IN EXEC INFO ENTRY                  
         CLEAR XFENTRY                                                          
         LA    R1,XDSNAME                                                       
         ACALL SETXNAME                                                         
*                                                                               
         COMMENT                   OPEN EXTERNAL FILE                           
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
*                                                                               
         COMMENT                   IF OPENED OK,                                
         COMMENT                                                                
         L     R0,CV#XFREE         GET FREE EXEC SET NUMBER                     
         A     R0,=F'1'                                                         
         IF    (R0,LT,CV#FEXEC),BEGIN                                           
         LA    R0,CV#FEXEC                                                      
         END                                                                    
         ST    R0,CV#XFREE                                                      
         SET   XFPUBLIC            SET UP EXEC INFO ENTRY                       
         ST    R0,XF#SETNO                                                      
         LA    R3,CVEXEC                                                        
         ST    R3,XFRGEXEC                                                      
         LA    R3,CVFLOW                                                        
         ST    R3,XFRGFLOW                                                      
         VCALL LOCALTOD            Save load time/date                          
         STM   R0,R1,XFLDTIME                                                   
*                                                                               
         COMMENT                   SET LOAD EXEC/FLOW FILES                     
         MVC   CP#XLOAD,XF#SETNO       SET #                                    
         MVC   CPRGLOAD,XFRGEXEC       EXEC RECORD GROUP                        
         MVC   CPRGFC,XFRGFLOW         FLOW RECORD GROUP                        
*                                                                               
         COMMENT                   LOAD EXEC FILE                               
         ACALL LDDSN                                                            
*                                                                               
         COMMENT                   CLOSE EXTERNAL FILE                          
         VCALL EXTCLOSE                                                         
*                                                                               
         COMMENT                   SET EXEC INFOMATION                          
         LA    R2,XFENTRY                                                       
         ACALL MRKNAME             (USE TEMP NAME FOR LOAD)                     
         LA    R2,XFENTRY                                                       
         ACALL SETXINFO                                                         
*                                                                               
         COMMENT                   DO FLOW PROCESSING                           
         VCALL FLOWCTRL                                                         
*                                                                               
         COMMENT                   UNSET TEMP LOAD NAME                         
         LA    R2,XFENTRY                                                       
         ACALL RSETNAME                                                         
*                                                                               
         COMMENT                   SET EXEC INFOMATION                          
         MVC   LDPXFOLD,LDPXFNEW   CHECK IF RELOAD OF PUB EXEC                  
         CLEAR R0                                                               
         LA    R1,LDPXFOLD                                                      
         LA    R2,LDPXFOLD                                                      
         ACALL GETPINFO            GET PREVIOUS EXEC INFO, IF ANY               
         IF    Z,BEGIN                                                          
         LA    R1,LDPXFOLD                                                      
         LA    R2,LDPXFNEW                                                      
         ACALL UPDTINFO            IF RELOAD, MARK/UPDATE INFO                  
         END                                                                    
         ELSE  BEGIN               IF NOT RELOAD, JUST SET INFO                 
         LA    R2,XFENTRY                                                       
         ACALL SETXINFO                                                         
         END                                                                    
*                                                                               
         COMMENT                   CLEAR LOAD POINTERS                          
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LOADACT - LOAD EXEC FILE FROM ACTIVE FILE.                                   
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       NO INPUT PARMS                                                          
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ERRORS WILL EXIT VIA CVERROR.                                      
*                                                                               
*                                                                               
LOADACT  PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK FOR NULL ACTIVE FILE                   
         VCALL ACTCHK                                                           
*                                                                               
         COMMENT                   DELETE ACTIVE EXEC/FLOW LINES                
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XACT                                                       
         LA    R1,CPEXEC                                                        
         VCALL XDELSET                                                          
         LA    R15,CPFLOW                                                       
         LA    R0,CP#XACT                                                       
         LA    R1,CPFLOW                                                        
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   SET LOAD EXEC/FLOW FILE POINTERS             
         MVC   CP#XLOAD,=AL4(CP#XACT)                                           
         LA    R3,CPEXEC                                                        
         ST    R3,CPRGLOAD                                                      
         LA    R3,CPFLOW                                                        
         ST    R3,CPRGFC                                                        
*                                                                               
         COMMENT                   LOAD EXEC FILE                               
         ACALL LDACT                                                            
*                                                                               
         COMMENT                   DO FLOW PROCESSING                           
         VCALL FLOWCTRL                                                         
*                                                                               
         COMMENT                   CLEAR LOAD POINTERS                          
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LOADTEMP - LOAD TEMP EXEC FILE FROM ACTIVE FILE.                             
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       NO INPUT PARMS                                                          
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ERRORS WILL EXIT VIA CVERROR.                                      
*            EXEC FILE / FLOW INFORMATION DELETED AND EXEC                      
*            INFORMATION ENTRY MARKED AS UNLOADED.                              
*                                                                               
*                                                                               
LOADTEMP PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK FOR NULL ACTIVE FILE                   
         VCALL ACTCHK                                                           
*                                                                               
         COMMENT                   DELETE TEMP EXEC/FLOW LINES                  
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XTEMP                                                      
         LA    R1,CPEXEC                                                        
         VCALL XDELSET                                                          
         LA    R15,CPFLOW                                                       
         LA    R0,CP#XTEMP                                                      
         LA    R1,CPFLOW                                                        
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   SET LOAD EXEC/FLOW FILE POINTERS             
         MVC   CP#XLOAD,=AL4(CP#XTEMP)                                          
         LA    R3,CPEXEC                                                        
         ST    R3,CPRGLOAD                                                      
         LA    R3,CPFLOW                                                        
         ST    R3,CPRGFC                                                        
*                                                                               
         COMMENT                   LOAD EXEC FILE                               
         ACALL LDACT                                                            
*                                                                               
         COMMENT                   DO FLOW PROCESSING                           
         VCALL FLOWCTRL                                                         
*                                                                               
         COMMENT                   CLEAR LOAD POINTERS                          
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
* LOAD EXEC WORK AREA                                                           
*                                                                               
* LOAD EXEC LINE WORK AREA.  SEE EXEC LOAD                                      
* CO-ROUTINE.                                                                   
*                                                                               
*                                                                               
LDWA     RECORD BEGIN                                                           
LDLNO    DS    F                   LOAD LINE LINE NUMBER                        
LDLEN    DS    F                   LOAD LINE LENGTH                             
LDLINE   DS    CL(2*&LINESZ)       LOAD LINE TEXT                               
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LDDSN - LOAD DSN (EXTERNAL FILE) INTO EXEC FILE                              
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - SET NUMBER OF EXEC FILE                                      
*     @ CPRGLOAD - RECORD GROUP OF EXEC                                         
*     @ CPRGFC   - RECORD GROUP OF FLOW INFO                                    
*                                                                               
*  ON EXIT:                                                                     
*      EXTERNAL FILE LOADED                                                     
*      ERRORS EXIT VIA LDABORT ROUTINE                                          
*                                                                               
*  NOTE: OLD EXEC FILES ARE LOADED IN EXEC MODULE                               
*  NOTE: CALLS TO OPEN AND CLOSE DSN ARE MADE ELSEWHERE.                        
*                                                                               
*                                                                               
*                                                                               
LDDWA    RECORD BEGIN                                                           
LDDAREA  DS    XL(L'LDWA)                                                       
         END                                                                    
*                                                                               
*                                                                               
LDDSN    PROC  LDDWA                                                            
         USING LDWA,R6                                                          
         LA    R6,LDDAREA                                                       
         ST    R6,CPCWA                                                         
*                                                                               
         COMMENT                   INIT LOAD WORK AREA                          
         CLEAR LDWA                                                             
*                                                                               
         COMMENT                   INIT SOME EXEC FILE STUFF                    
         SET   CPFRDEXT            SELECT EXTERNAL FILE                         
         SET   CPLFLG1.CPFALL                                                   
         SCINIT CVBLANKS,0         NULL RANGE, DEFAULT TO ALL                   
         VCALL NDETRNG             CALL DETRNG TO SET ALL                       
         SET   CPFNCUR             DO NOT RESET CURRENT                         
*                                                                               
         COMMENT                   USE CO-ROUTINE TO PROCESS                    
         RDRNG LOADRTN,(DESRTRN+LXCATRTN+DESABORT+UNPRST)                       
         IF    NZ,' ACALL LDABORT' ABORT IF ATTN (NO RETURN)                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LDACT - LOAD ACTIVE FILE INTO EXEC FILE                                      
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - SET NUMBER OF EXEC FILE                                      
*     @ CPRGLOAD - RECORD GROUP OF EXEC                                         
*     @ CPRGFC   - RECORD GROUP OF FLOW INFO                                    
*                                                                               
*  ON EXIT:                                                                     
*      ACTIVE FILE LOADED                                                       
*      ERRORS EXIT VIA LDABORT ROUTINE                                          
*                                                                               
*  NOTE: OLD EXEC FILES ARE LOADED IN EXEC MODULE                               
*                                                                               
*                                                                               
LDAWA    RECORD BEGIN                                                           
LDAAREA  DS    XL(L'LDWA)                                                       
         END                                                                    
*                                                                               
*                                                                               
LDACT    PROC  LDAWA                                                            
         USING LDWA,R6                                                          
         LA    R6,LDAAREA                                                       
         ST    R6,CPCWA                                                         
*                                                                               
         COMMENT                   INIT LOAD WORK AREA                          
         CLEAR LDWA                                                             
*                                                                               
         COMMENT                   INIT SOME ACTIVE FILE STUFF                  
         SET   CPLFLG1.CPFALL                                                   
         SCINIT CVBLANKS,0         NULL RANGE, DEFAULT TO ALL                   
         VCALL NDETRNG             CALL NDETRNGTO SET ALL                       
         SET   CPFNCUR             DO NOT RESET CURRENT                         
*                                                                               
         COMMENT                   USE CO-ROUTINE TO PROCESS                    
         RDRNG LOADRTN,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                       
         IF    NZ,' ACALL LDABORT' ABORT IF ATTN (NO RETURN)                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LOADRTN - CO-ROUTINE TO LOAD EXEC FILE                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*       CPCWA - LOAD DSECT                                                      
*       CPLCNO - LINE NUMBER                                                    
*                                                                               
*  ON EXIT:                                                                     
*       LINE LOADED INTO EXEC FILE                                              
*                                                                               
*  NOTE, NOTE !!                                                                
*  WE PROCESS LINES AS FOLLOWS DURING LOADING                                   
*       1. DO NOT LOAD BLANK LINES                                              
*       2. DO NOT LOAD COMMENT LINES                                            
*       3. LINES ENDING IN '\' ARE CONTINUED ON NEXT                            
*          LINE.                                                                
*                                                                               
*                                                                               
LOADRTN  PROC  ,                                                                
         USING  LDWA,R6                                                         
         L     R6,CPCWA                                                         
*                                                                               
         COMMENT                   GET LINE                                     
         IF    (LDLEN,EQ,0),BEGIN  NOT CONTINUATION, JUST MOVE                  
         VCALL RTRIM                                                            
         L     R15,CPLCNO                                                       
         ST    R15,LDLNO                                                        
         ST    R0,LDLEN                                                         
         LR    R3,R0                                                            
         MOVE  R3,LDLINE,@R1                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               CONTINUATION, CONCATENATE                    
         VCALL LRTRIM                                                           
         LA    R2,LDLINE                                                        
         A     R2,LDLEN                                                         
         DECR  R2                  STRIP '\' FROM 1ST STRING                    
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         MVCL  R2,R4                                                            
         L     R1,LDLEN                                                         
         AR    R1,R0                                                            
         DECR  R1                                                               
         ST    R1,LDLEN                                                         
         END                                                                    
*-                                                                              
*-       Check for line length overflow.                                        
*-                                                                              
         IF    (LDLEN,GT,&LINESZ),BEGIN                                         
         TSEG  'Error in line '                                                 
         L     R0,LDLNO                                                         
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  ', command line exceeds &LINESZ characters.',,CR                 
         ACALL LDABORT             NO RETURN                                    
         END                                                                    
*-                                                                              
*-       Get the first character in the line.                                   
*-                                                                              
         LA    R1,LDLINE                                                        
         LA    R2,LDLINE                                                        
         A     R2,LDLEN                                                         
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R2)),BEGIN                              
         LA    R1,1(R1)                                                         
         END                                                                    
         DECR  R2                                                               
*-                                                                              
*-       Ignore blank lines.                                                    
*-                                                                              
         IF    (R1,GT,R2),BEGIN    Blank line...                                
         CLEAR LDLEN                                                            
         END                                                                    
*-                                                                              
*-       Line is continued if it ends with "\" character.                       
*-                                                                              
         ELSEIF (@R2,EQ,'\'),BEGIN  Possibly continued...                       
         DECR  R2                  Get 2nd to last char ptr                     
         IF    (R1,LE,R2),BEGIN    At least two chars on line...                
         IF    (@R2,EQ,'\'),BEGIN  Ended with two "\\" chars...                 
         DECR  R0,LDLEN            Get rid of one of the "\" chars              
         B     LDRLINE             Treat it as a regular line                   
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Ignore comment lines.                                                  
*-                                                                              
         ELSEIF (@R1,EQ,';'),BEGIN  Comment line...                             
         CLEAR LDLEN                                                            
         END                                                                    
*-                                                                              
*-       Load the line (it's not a special line).                               
*-                                                                              
         ELSE  BEGIN                                                            
LDRLINE  L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
         L     R15,CPRGLOAD                                                     
         L     R0,LDLEN                                                         
         LA    R1,LDLINE                                                        
         LA    R2,LDLNO                                                         
         VCALL XPUT                                                             
         IF    NEG,BEGIN                                                        
         ERROR 'Too many EXEC files loaded. '                                   
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
         CLEAR LDLEN                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LDABORT - ABORT XLOAD                                                        
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - XLOAD EXEC/FLOW SET NUMBER                                   
*       CPRGLOAD - XLOAD EXEC RGROUP INFO ADDR                                  
*                                                                               
*  THIS ROUTINE HANDLES THE ABORTING OF AN XLOAD.                               
*   1. IT CLEARS EXEC/FLOW LINES                                                
*   2. DELETES XINFO LINE, IF ANY                                               
*   3. IT GIVES MESSAGE                                                         
*   4. IT CLEARS LOAD FILE POINTERS                                             
*   5. IT EXITS VIA CVERROR                                                     
*                                                                               
*  THIS IS THE ERROR EXIT ROUTINE FOR XLOAD,, WE                                
*  DO IT TO IT !                                                                
*                                                                               
*                                                                               
XBTWA    RECORD BEGIN                                                           
XBTXF    DS    XL(L'XFENTRY)       EXEC INFO ENTRY                              
         END                                                                    
*                                                                               
*                                                                               
LDABORT  XPROC XBTWA                                                            
         USING XFENTRY,R6                                                       
         LA    R6,XBTXF                                                         
*                                                                               
         COMMENT                   FIRST GIVE A MESSAGE                         
         COMMENT                                                                
         COMMENT *$                DO NOT GIVE MESSAGE FOR OLD!                 
         COMMENT *$                DO NOT GIVE MESSAGE FOR TEMP!                
         L     R0,CP#XLOAD                                                      
         IF    ((R0,NE,CP#XOLD),AND,(R0,NE,CP#XTEMP)),BEGIN                     
         TSEG  'Command stopped.'                                               
         END                                                                    
*                                                                               
         COMMENT                   CLEAR EXEC/FLOW LINES                        
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XDELSET                                                          
         L     R15,CPRGFC                                                       
         L     R0,CP#XLOAD                                                      
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   MARK EXEC AS NOT LOADED                      
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         LA    R2,XFENTRY                                                       
         ACALL GETXINFO                                                         
         IF    Z,BEGIN             IF EXEC INFO EXISTS, DELETE IT               
         IF    XFUSERX,BEGIN                                                    
         L     R0,XF#SETNO                                                      
         LA    R1,CPEXEC                                                        
         ACALL DELUINFO                                                         
         END                                                                    
         IF    XFPUBLIC,BEGIN                                                   
         L     R0,XF#SETNO                                                      
         LA    R1,CVEXEC                                                        
         ACALL DELPINFO                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CLEAR EXEC LOADING POINTERS                  
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
*                                                                               
         COMMENT                   EXIT VIA CVERROR !!!@#$%&#!!!               
         B     CVERROR             EXIT VIA CVERROR !!!@#$%&#!!!               
*                                                                               
         BOMB                      NOT USED                                     
         PEND  ,              NOT USED                                          
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOW XLOAD - SHOW LOADED EXECS                                               
*                                                                               
*                                                                               
*                                                                               
SHLDWA   RECORD BEGIN                                                           
SHLDTIME DS    CL64                                                             
SHLDXF   DS    XL(L'XFENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
SHXLOAD  XPROC SHLDWA                                                           
         USING XFENTRY,R6                                                       
         LA    R6,SHLDXF                                                        
         CLEAR XFENTRY                                                          
*                                                                               
         COMMENT                   SET PROPER GROUP/SET                         
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   PRINT FRIST LINE                             
         TSEG  'Loaded            ID  EXEC File Name',,CR                       
*                                                                               
         COMMENT                   LOOP READING ENTRIES                         
         LA    R15,CPRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XGETFRST           GET FIRST,,                                   
         WHILE Z,BEGIN             AND LOOP THRU ALL,,                          
*                                                                               
         COMMENT                   PRINT INFO,                                  
         LM    R0,R1,XFLDTIME                                                   
         LA    R15,SHLDTIME                                                     
         VCALL FMTCLCK             GET TIME IN SHOW DATE FORMAT                 
         MVC   SHLDTIME+5(12),SHLDTIME+8                                        
         LA    R1,SHLDTIME+17                                                   
         LA    R0,5                                                             
         L     R15,XF#SETNO                                                     
         BTD   (R1),(R0),(R15)                                                  
         MVC   SHLDTIME+22(2),=CL2'  '   'HH:MM MM/DD/YYYY ---ID  '             
         TSEG  SHLDTIME,24                                                      
         LA    R1,XFNAME                                                        
         ACALL SEGFDSN                                                          
         TCR                                                                    
         BNZ   CVNEXT              EXIT IF ATTN HIT                             
*                                                                               
         COMMENT                   READ NEXT EXEC FILE AND LOOP                 
         LA    R15,CPRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XGETNEXT            GET NEXT,,, AND LOOP                         
         END                                                                    
*                                                                               
         COMMENT                   EXIT, DO NOT RETURN TO CALLER                
         B     CVNEXT              (CALLER IS AN SCKW IN MODE)                  
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         TITLE 'SHOW LOADED EXECS'                                              
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOW PUBLOAD - SHOW LOADED PUB EXECS                                         
*                                                                               
*                                                                               
*                                                                               
SHPBLOAD XPROC SHLDWA                                                           
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LA    R6,SHLDXF                                                        
         LA    R5,XFNAME                                                        
         CLEAR XFENTRY                                                          
*                                                                               
         COMMENT                   SET PROPER GROUP/SET                         
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   PRINT FRIST LINE                             
         TSEG  'Loaded            ID  EXEC File Name',,CR                       
*                                                                               
         COMMENT                   LOOP READING ENTRIES                         
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XGETFRST            GET FIRST,,                                  
         WHILE Z,BEGIN             AND LOOP THRU ALL,,                          
*                                                                               
         COMMENT                   PRINT INFO,                                  
         IF    (XMRK,EQ,'    '),BEGIN  IF TEMP OR RELOADED, SKIP                
         LM    R0,R1,XFLDTIME                                                   
         LA    R15,SHLDTIME                                                     
         VCALL FMTCLCK             GET TIME IN SHOW DATE FORMAT                 
         MVC   SHLDTIME+5(12),SHLDTIME+8                                        
         LA    R1,SHLDTIME+17                                                   
         LA    R0,5                                                             
         L     R15,XF#SETNO                                                     
         BTD   (R1),(R0),(R15)                                                  
         MVC   SHLDTIME+22(2),=CL2'  '   'HH:MM MM/DD/YYYY ---ID  '             
         TSEG  SHLDTIME,24                                                      
         LA    R1,XFNAME                                                        
         ACALL SEGFDSN                                                          
         TCR                                                                    
         BNZ   CVNEXT              EXIT IF ATTN HIT                             
         END                                                                    
*                                                                               
         COMMENT                   READ NEXT EXEC FILE AND LOOP                 
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XGETNEXT            GET NEXT,,, AND LOOP                         
         END                                                                    
*                                                                               
         COMMENT                   EXIT, DO NOT RETURN TO CALLER                
         B     CVNEXT              (CALLER IS AN SCKW IN MODE)                  
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'XPROC Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XPROC Command ---                                                            
*                                                                               
*  Note: XPROC command is never executed.  No siree, an XCALL                   
*    executes the first command after the xproc statment.  The                  
*    XPROC statment is scanned during the flow processing that                  
*    is done when the EXEC is loaded.                                           
*                                                                               
*                                                                               
*                                                                               
XPROC    XPROC ,                                                                
*                                                                               
      COMMENT                      CASES, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'XPROC: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      CASES, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'XPROC: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*                                                                               
      COMMENT                      CHECK FOR EXTENDED EXEC                      
      IF    ^CPFXTEND,BEGIN                                                     
      BOMB                                                                      
      END                                                                       
*                                                                               
      COMMENT                      XPROC SHOULD NEVER BE EXECUTED               
      TSEG  'XPROC: statement invalid.',,CR                                     
      ERROR 'XPROC statement is not executable.  Use XCALL command.'            
*                                                                               
         PEND ,                                                                 
         TITLE 'XSCAN Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XSCAN - SCAN ACTIVE FILE FOR PROPER FLOW CONTROL                             
*                                                                               
*  ON ENTRY:                                                                    
*       SCAN CONTROL BLOCK - REMAINDER OF COMMAND                               
*                                                                               
*  ON EXIT:                                                                     
*       ERRORS EXIT TO CVERROR,  SUCCESS EXITS TO CVNEXT.                       
*                                                                               
*                                                                               
XSCAN    XPROC ,                                                                
*                                                                               
         COMMENT                   XSCAN SUPPORTS NO OPTIONS                    
         VCALL NOOPTS                                                           
*                                                                               
         COMMENT                   CHECK ACTIVE FILE                            
         VCALL ACTCHK                                                           
*                                                                               
         COMMENT                   LOAD ACTIVE FILE INTO TEMP EXEC              
         ACALL LOADTEMP                                                         
*                                                                               
         COMMENT ,                 NOTE   *****                                 
         COMMENT ,                 MUST ADD PASS 2 TO CHECK                     
         COMMENT ,                 GOTO'S, PARMS, ETC...                        
         COMMENT ,                 JUST LIKE C'S LINT PROGRAM IN                
         COMMENT ,                 UNIX                                         
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
         TITLE 'XFORMAT Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  XFORMAT - FORMAT EXTENDED EXEC FILE                                          
*                                                                               
*  ON ENTRY:                                                                    
*       SCAN CONTROL BLOCK - REMAINDER OF COMMAND                               
*                                                                               
*  ON EXIT:                                                                     
*       ACTIVE FILE FORMATTED AS REQUESTED                                      
*       ALL ERRORS EXIT VIA CVERROR                                             
*                                                                               
*  THIS ROUTINE ROUTINES WORKS AS FOLLOWS,  FIRST WE DO THE                     
*  EQUIVALENT OF AN XSCAN,  THIS IS TO INSURE THAT ALL BEGIN/                   
*  END BLOCK ARE SYNTACTICALLY CORRECT.  WE THEN CALL FFORMAT                   
*  (IN FLOW MODULE) TO FORMAT EXEC.  THERE ARE MANY ROUTINES                    
*  FOR PARSING BEGIN-END BLOCKS IN FLOW MODULE, SO THAT IS                      
*  WHY MOST OF THE WORK (FFORMAT ROUTINE) IS DONE THERE.                        
*                                                                               
*                                                                               
*                                                                               
XFWA     RECORD BEGIN                                                           
XFOPTION FLAG  ,                                                                
         FLAG  XFFMARK                                                          
         FLAG  XFFUNFMT                                                         
XFINDENT DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
XFORMAT  XPROC XFWA                                                             
         LA    R6,XFWA                                                          
         USING XFWA,R6                                                          
*                                                                               
         COMMENT                   INITIALIZE WORK AREA W/DEFAULTS              
         CLEAR XFOPTION                                                         
         MVC   XFINDENT,=F'3'                                                   
*                                                                               
         COMMENT                   SCAN OPTIONS                                 
         SCAN  XFORMPRT                                                         
         SCANCHK                                                                
*                                                                               
         COMMENT                   CHECK FOR ACTIVE FILE                        
         VCALL ACTCHK                                                           
*                                                                               
         COMMENT                   LOAD ACTIVE INTO TEMP EXEC                   
         COMMENT ,                 (THIS DOES EQUIVALENT TO XSCAN.)             
         ACALL LOADTEMP                                                         
*                                                                               
         COMMENT                   FORMAT IT                                    
         IF    (XFOPTION,EQ,0),BEGIN                                            
         L     R0,=CL4'    '                                                    
         END                                                                    
         ELSEIF (XFFMARK,AND,^XFFUNFMT),BEGIN                                   
         L     R0,=CL4'M   '                                                    
         END                                                                    
         ELSEIF (XFFMARK,AND,XFFUNFMT),BEGIN                                    
         L     R0,=CL4'MU  '                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'Invalid combination of formatting options.'                     
         END                                                                    
         COMMENT                   R0 - FORMATTING OPTIONS                      
         L     R1,XFINDENT         R1 - INDENT SPACE VALUE                      
         VCALL FFORMAT             FORMAT EXEC FILE                             
*                                                                               
         COMMENT                   BUMP CHANGE COUNT                            
         INCR  R15,CPACHCT                                                      
*                                                                               
         COMMENT                   ALL DONE                                     
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
         SPACE 4                                                                
*-                                                                              
*-       XFORMAT SCAN KEYWORDS, ROUTINES                                        
*-                                                                              
XFORMPRT SCKW  MARK,XFSCMARK,A                                                  
         SCKW  UNFORMATTED,XFSCUNF,A                                            
         SCKW  INDENT,XFSCIND,(A,P,PI),16                                       
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 2                                                                
*                                                                               
XFSCMARK DPROC ,                                                                
         SET   XFFMARK                                                          
         PEND  ,                                                                
*                                                                               
XFSCUNF  DPROC ,                                                                
         SET   XFFUNFMT                                                         
         PEND  ,                                                                
*                                                                               
XFSCIND  DPROC ,                                                                
         ST    R0,XFINDENT                                                      
         PEND  ,                                                                
         SPACE 4                                                                
*                                                                               
         COMMENT ,                 END OF XFORMAT ROUTINE                       
         DROP  R6                                                               
         TITLE 'XCONVERT Command'                                               
XCONVERT XPROC ,                                                                
         ABORT 'Command not yet implemented.'                                   
         PEND  ,                                                                
         TITLE 'XXXEXIT - Reset XPROC Environment and XEXIT'                    
*box                                                                            
*                                                                               
*                                                                               
*  XXXEXIT - RESET XPROC ENVIRONMENT, AND GOTO XPROC LABEL                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE NUMBER OF XPROC LABEL                                         
*                                                                               
*  ON EXIT:                                                                     
*       THIS ROUTINE DOES NOT RETURN.  IT CALLS THE START ROUTINE               
*       TO RESET THE EXEC ENVIRONMENT AND START UP AT THE NEW LINE              
*       NUMBER.  IT DOES NOT RETURN.                                            
*                                                                               
*  NOTE:                                                                        
*       'XEXIT' ENTRY POINT IS IN FLOW MODULE.  WE COME HERE TO                 
*       RESET XPROC ENVIRIONMENT AND START UP XPROC AT LINE NUMBER              
*       PASSED TO US FROM 'XEXIT' ROUTINE. THIS ROUTINE DOES NOT                
*       RETURN.!!                                                               
*                                                                               
*       THIS ROUTINE CHAINS BACK THROUGH ALL THE PROC STATE                     
*       INFORMATION.  WHEN IT REACHES THE XPROC STATE (ENVIRONMENT)             
*       INFORMATION, IT MODIFIES IT TO CONTAIN THE NEW LABEL LINE               
*       NUMBER AS THE NEXT LINE NUMBER TO EXECUTE, AND THEN STARTS              
*       UP THE EXEC WITH THE XPROC ENVIRONMENT !                                
*                                                                               
*                                                                               
*                                                                               
XXITWA   RECORD BEGIN                                                           
XXITLNO  DS    F                   XEXIT LINE NUMBER                            
XXITSTAT DS    XL(L'SNENTRY)       STATE INFO FOR PROCS/XPROC                   
         END                                                                    
*                                                                               
*                                                                               
XXXEXIT  XPROC XXITWA                                                           
         USING SNENTRY,R6                                                       
         LA    R6,XXITSTAT                                                      
*                                                                               
         COMMENT                   INITIALIZE WORK AREA                         
         ST    R0,XXITLNO                                                       
         CLEAR XXITSTAT                                                         
*                                                                               
         COMMENT                   IF IN XPROC, JUST SET NEXT LINENO            
         IF    (CPXVSET#,EQ,CPVSET#),BEGIN                                      
         ST    R0,CPEXNEXT                                                      
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IN PROC, CHAIN BACK TO XPROC                 
         COMMENT                   STATE INFORMATION                            
         LA    R0,L'SNENTRY                                                     
         LA    R1,SNENTRY                                                       
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,CPVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         WHILE (SNXVSET#,NE,SNVSET#),BEGIN    WHILE NOT XPROC INFO,             
         LA    R0,L'SNENTRY                      CHAIN BACK THRU                
         LA    R1,SNENTRY                        STATES                         
         LA    R2,=CL16'SYS*EXEC_STATE  '                                       
         L     R3,SNVSET#                                                       
         VCALL GETVSTK                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   GOT XPROC STATE INFO,,,                      
         MVC   SNEXNEXT,XXITLNO    SET LABEL LINE NUMBER AS NEXT                
         LA    R1,SNENTRY          LINE TO EXEC, AND                            
         ACALL START               START UP EXEC W/XPROC ENVIROMENT             
*                                                                               
         BOMB  ,                   NEVER EXECUTED, WE EXIT ABOVE                
*                                                                               
         CLEAR R15                                                              
         PEND  ,          NEVER EXECUTED, WE EXIT ABOVE                         
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Delete EXEC File Routines'                                      
*box                                                                            
*                                                                               
*                                                                               
*  DELUEXEC - DELETE USER EXEC FILE                                             
*                                                                               
*  THREE THINGS MUST BE DELETED,, THE EXEC FILE LINES, THE FLOW                 
*  FILE LINES,, AND THE EXEC INFO ENTRY.  WE DO NOT CHECK TO SEE                
*  IF THE EXEC EXISTS NOR DO WE CARE,,, USER CAN CLOBBER SELF IF                
*  USER UNLOADS AN EXEC THAT CALLED THE EXEC THAT UNLOADS... THAT               
*  WOULD BE A NO-NO,,, (A RELOAD COULD DO SAME THING,, UCK).                    
*  HEY ! THIS ROUTINES WILL NOT UNLOAD PUBLIC EXECS.                            
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*       R1 - EXEC RECORD GROUP POINTER   (=A(CPEXEC))                           
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, IF IT IS THERE IT IS DELETED                              
*                                                                               
*                                                                               
DELUEXEC PROC  ,                                                                
         LR    R6,R0               R6 - SETNO OF EXEC TO DELETE                 
         LR    R5,R1               R5 - RECORD GROUP POINTER                    
         IF    ((R6,LT,CP#FEXEC),AND,(R6,GT,CP#FEXEC+CPMAXXCT)),BEGIN           
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DELETE EXEC/FLOW LINES                       
         LA    R15,CPEXEC                                                       
         LR    R0,R6                                                            
         VCALL XDELSET                                                          
         LA    R15,CPFLOW                                                       
         LR    R0,R6                                                            
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   DELETE EXEC SET INFO                         
         LR    R0,R6                                                            
         LR    R1,R5                                                            
         ACALL DELUINFO                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
** >>>>> DO NOT CALL THIS ROUTINE !!!! <<<<<<                                   
**                                                                              
**  THIS ROUTINE IS OBSOLETE, SEE UPDTINFO ROUTINE !!!                          
**                                                                              
**  DELPEXEC - DO NOT USE !   (DELETE PUBLIC EXEC)                              
**                                                                              
**  THREE THINGS MUST BE DELETED,, THE EXEC FILE LINES, THE FLOW                
**  FILE LINES,, AND THE EXEC INFO ENTRY.  WE DO NOT CHECK TO SEE               
**  IF THE EXEC EXISTS NOR DO WE CARE,,, USER CAN CLOBBER SELF IF               
**  USER UNLOADS AN EXEC THAT CALLED THE EXEC THAT UNLOADS... THAT              
**  WOULD BE A NO-NO,,, (A RELOAD COULD DO SAME THING,, UCK).                   
**                                                                              
**  ON ENTRY:                                                                   
**       R0 - EXEC SET NUMBER                                                   
**       R1 - EXEC RECORD GROUP POINTER   (=A(CVEXEC))                          
**                                                                              
**  ON EXIT:                                                                    
**      R15 - CC=ZERO, IF IT IS THERE IT IS DELETED                             
**                                                                              
*                                                                               
DELPEXEC PROC  ,                                                                
         LR    R6,R0               R6 - SETNO OF EXEC TO DELETE                 
         LR    R5,R1               R5 - EXEC RECORD GROUP POINTER               
         IF    (R0,LT,CV#FEXEC),BEGIN                                           
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DELETE EXEC/FLOW LINES                       
         LA    R15,CVEXEC                                                       
         LR    R0,R6                                                            
         VCALL XDELSET                                                          
         LA    R15,CVFLOW                                                       
         LR    R0,R6                                                            
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   DELETE EXEC SET INFO                         
         LR    R0,R6                                                            
         LR    R1,R5                                                            
         ACALL DELPINFO                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'EXEC INFO Routines'                                             
*box                                                                            
*                                                                               
*                                                                               
*  GETXIFNO - GET EXEC INFOMATION                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ZERO                                                               
*      @R1 - FILE NAME IN XDSNAME DSECT FORMAT                                  
*      @R2 - EXEC INFO DSECT                                                    
*  OR.. R0 - EXEC SET NUMBER                                                    
*       R1 - EXEC RECORD GROUP POINTER                                          
*      @R2 - EXEC INFO DSECT                                                    
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, GOT INFO                                                  
*            CC=NZ, SET DOES NOT EXIST                                          
*                                                                               
*  NOTE: ON ENTRY, R0,R1 - SPECIFY EXEC FILE, CAN BE SPECIFIED AS               
*        EXEC FILE NAME, OR AS A REC GROUP POINTER AND A SET NUMBER.            
*                                                                               
GETXINFO PROC  ,                                                                
         ACALL GETUINFO            FIRST TRY USERS EXEC INFO                    
         IF    NZ,BEGIN                                                         
         ACALL GETPINFO            THEN TRY PUBLIC EXEC INFO                    
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTUXIFNO - GET USER'S EXEC INFOMATION                                        
*                                                                               
*  NOTE: SEARCHES USER'S EXEC FILES ONLY                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ZERO                                                               
*      @R1 - FILE NAME IN XDSNAME DSECT FORMAT                                  
*      @R2 - EXEC INFO DSECT                                                    
*  OR.. R0 - EXEC SET NUMBER                                                    
*       R1 - EXEC RECORD GROUP POINTER                                          
*      @R2 - EXEC INFO DSECT                                                    
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, GOT INFO                                                  
*            CC=NZ, SET DOES NOT EXIST                                          
*                                                                               
*  NOTE: ON ENTRY, R0,R1 - SPECIFY EXEC FILE, CAN BE SPECIFIED AS               
*        EXEC FILE NAME, OR AS A REC GROUP POINTER AND A SET NUMBER.            
*                                                                               
GETXWA   RECORD BEGIN                                                           
GETXR0   DS    F                                                                
GETXR1   DS    F                                                                
GETXR2   DS    F                                                                
GETXXF   DS    XL(L'XFENTRY)                                                    
GETXXFX  DS    XL(L'XFXENTRY)                                                   
         END                                                                    
*                                                                               
*                                                                               
GETUINFO PROC  GETXWA                                                           
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         LA    R6,GETXXF                                                        
         LA    R5,GETXXFX                                                       
         CLEAR XFENTRY                                                          
         CLEAR XFXENTRY                                                         
         ST    R0,GETXR0                                                        
         ST    R1,GETXR1                                                        
         ST    R2,GETXR2                                                        
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CPRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET NAME OF EXEC FILE                        
         L     R0,GETXR0                                                        
         L     R1,GETXR1                                                        
         IF    (R0,Z),BEGIN        IF NAME, JUST MOVE                           
         MVC   XFNAME,@R1                                                       
         END                                                                    
         IF    (R0,NZ),BEGIN       IF NO NAME, GET FROM NDX FILE                
         STM   R0,R1,XFXENTRY                                                   
         LA    R15,CPRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XGET                                                             
         BNZ   GTUXDONE                                                         
         MVC   XFNAME,XFXNAME                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET EXEC INFO, NAME IS XFNAME                
         LA    R15,CPRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFENTRY                                                       
         VCALL XGET                                                             
         BNZ   GTUXDONE                                                         
*                                                                               
         COMMENT                   GOT INFO, MOVE TO CALLERS DSECT              
         L     R2,GETXR2                                                        
         LA    R3,L'XFENTRY                                                     
         LA    R4,XFENTRY                                                       
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
*                                                                               
GTUXDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETPINFO - GET PUBLIC EXEC INFOMATION                                        
*                                                                               
*  NOTE: SEARCHES PUBLIC EXEC FILES ONLY                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ZERO                                                               
*      @R1 - FILE NAME IN XDSNAME DSECT FORMAT                                  
*      @R2 - EXEC INFO DSECT                                                    
*  OR.. R0 - EXEC SET NUMBER                                                    
*       R1 - EXEC RECORD GROUP POINTER                                          
*      @R2 - EXEC INFO DSECT                                                    
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, GOT INFO                                                  
*            CC=NZ, SET DOES NOT EXIST                                          
*                                                                               
*  NOTE: ON ENTRY, R0,R1 - SPECIFY EXEC FILE, CAN BE SPECIFIED AS               
*        EXEC FILE NAME, OR AS A REC GROUP POINTER AND A SET NUMBER.            
*                                                                               
GETPINFO PROC  GETXWA                                                           
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         LA    R6,GETXXF                                                        
         LA    R5,GETXXFX                                                       
         CLEAR XFENTRY                                                          
         CLEAR XFXENTRY                                                         
         ST    R0,GETXR0                                                        
         ST    R1,GETXR1                                                        
         ST    R2,GETXR2                                                        
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CVRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CVRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET NAME OF EXEC FILE                        
         L     R0,GETXR0                                                        
         L     R1,GETXR1                                                        
         IF    (R0,Z),BEGIN        IF NAME, JUST MOVE                           
         MVC   XFNAME,@R1                                                       
         END                                                                    
         IF    (R0,NZ),BEGIN       IF NO NAME, GET FROM NDX FILE                
         STM   R0,R1,XFXENTRY                                                   
         LA    R15,CVRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XGET                                                             
         BNZ   GTPXDONE                                                         
         MVC   XFNAME,XFXNAME                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET EXEC INFO, NAME IS XFNAME                
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFENTRY                                                       
         VCALL XGET                                                             
         BNZ   GTPXDONE                                                         
*                                                                               
         COMMENT                   GOT INFO, MOVE TO CALLERS DSECT              
         L     R2,GETXR2                                                        
         LA    R3,L'XFENTRY                                                     
         LA    R4,XFENTRY                                                       
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
*                                                                               
GTPXDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETXINFO - SET EXEC INFOMATION                                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R2 - EXEC INFO DSECT,  ALL FIELD FILLED                                 
*                                                                               
*  ON EXIT:                                                                     
*       DONE.                                                                   
*                                                                               
*                                                                               
STXWA    RECORD BEGIN                                                           
STXXFX   DS    XL(L'XFXENTRY)                                                   
         END                                                                    
*                                                                               
*                                                                               
SETXINFO PROC  STXWA                                                            
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         LR    R6,R2                                                            
         LA    R5,STXXFX                                                        
*                                                                               
         COMMENT                   FORMAT INDEX ENTRY                           
         CLEAR XFXENTRY                                                         
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         STM   R0,R1,XFXNDX                                                     
         MVC   XFXNAME,XFNAME                                                   
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CPRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CVRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CVRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   IF PUBLIC EXEC, BEGIN                        
         IF    XFPUBLIC,BEGIN                                                   
         LA    R15,CVRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XPUT                SAVE ENTRY INDEX                             
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFENTRY                                                       
         VCALL XPUT                SAVE ENTRY                                   
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF USERS EXEC, BEGIN                         
         ELSEIF XFUSERX,BEGIN                                                   
         LA    R15,CPRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XPUT                SAVE ENTRY INDEX                             
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
         LA    R15,CPRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFENTRY                                                       
         VCALL XPUT                SAVE ENTRY                                   
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF NOT PUBLIC OR USERS                       
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DELXIFNO - DELETE USER EXEC INFOMATION                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ZERO                                                               
*      @R1 - FILE NAME IN XDSNAME DSECT FORMAT                                  
*  OR.. R0 - EXEC SET NUMBER                                                    
*       R1 - EXEC RECORD GROUP POINTER                                          
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*                                                                               
*  NOTE: ON ENTRY, R0,R1 - SPECIFY EXEC FILE, CAN BE SPECIFIED AS               
*        EXEC FILE NAME, OR AS A REC GROUP POINTER AND A SET NUMBER.            
*                                                                               
DLXWA    RECORD BEGIN                                                           
DLXXF    DS    XL(L'XFENTRY)                                                    
DLXXFX   DS    XL(L'XFXENTRY)                                                   
         END                                                                    
*                                                                               
*                                                                               
DELUINFO PROC  DLXWA                                                            
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         LA    R6,DLXXF                                                         
         LA    R5,DLXXFX                                                        
*                                                                               
         COMMENT                   GET XINFO,                                   
         LA    R2,XFENTRY                                                       
         ACALL GETUINFO                                                         
*                                                                               
         COMMENT                   IF ENTRY EXISTS, DELETE IT                   
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CPRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   DELETE ENTRY                                 
         LA    R15,CPRG64                                                       
         LA    R2,XFENTRY                                                       
         VCALL XDELETE             DELETE ENTRY                                 
         CLEAR XFXENTRY                                                         
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         STM   R0,R1,XFXNDX                                                     
         LA    R15,CPRG16                                                       
         LA    R2,XFXENTRY                                                      
         VCALL XDELETE             DELET INDEX                                  
*                                                                               
         END , OF IF ENTRY TO DELETE                                            
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
** >>>>> DO NOT CALL THIS ROUTINE !!!! <<<<<<                                   
**                                                                              
**  THIS ROUTINE IS OBSOLETE, SEE UPDTINFO ROUTINE !!!                          
**                                                                              
**                                                                              
**  DELXIFNO - DO NOT CALL (DELETE PUBLIC EXEC INFOMATION)                      
**                                                                              
**  ON ENTRY:                                                                   
**       R0 - ZERO                                                              
**      @R1 - FILE NAME IN XDSNAME DSECT FORMAT                                 
**  OR.. R0 - EXEC SET NUMBER                                                   
**       R1 - EXEC RECORD GROUP POINTER                                         
**                                                                              
**  ON EXIT:                                                                    
**      R15 - CC=ZERO                                                           
**                                                                              
**  NOTE: ON ENTRY, R0,R1 - SPECIFY EXEC FILE, CAN BE SPECIFIED AS              
**        EXEC FILE NAME, OR AS A REC GROUP POINTER AND A SET NUMBER.           
**                                                                              
**  CAUTION:  PUBLIC EXECS MAY NOT BE RELOADED !!! (8/86)                       
**                                                                              
*                                                                               
DELPINFO PROC  DLXWA                                                            
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         LA    R6,DLXXF                                                         
         LA    R5,DLXXFX                                                        
*                                                                               
         COMMENT                   GET XINFO,                                   
         LA    R2,XFENTRY                                                       
         ACALL GETXINFO                                                         
*                                                                               
         COMMENT                   IF ENTRY EXISTS, DELETE IT                   
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CVRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CVRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   DELETE ENTRY                                 
         LA    R15,CVRG64                                                       
         LA    R2,XFENTRY                                                       
         VCALL XDELETE             DELETE ENTRY                                 
         CLEAR XFXENTRY                                                         
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         STM   R0,R1,XFXNDX                                                     
         LA    R15,CVRG16                                                       
         LA    R2,XFXENTRY                                                      
         VCALL XDELETE             DELETE INDEX                                 
*                                                                               
         END , OF IF ENTRY TO DELETE                                            
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  UPDTINFO - UPDATE PUBLIC EXEC INFO                                           
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC INFO DSECT (OLD), ALL FIELDS FILLED                           
*      @R2 - EXEC INFO DSECT (NEW), ALL FIELDS FILLED                           
*                                                                               
*  ON EXIT:                                                                     
*       DONE.                                                                   
*                                                                               
*  THIS ROUTINE UPDATES PUBLIC EXEC FILE INFO.  IT IS USED WHEN                 
*  A PUBLIC EXEC IS REPLACED.  THE OLD EXEC CAN NOT BE DELETED                  
*  SOMEONE MAY STILL BE POINTED TO IT.  SO INSTEAD WE MARK IT                   
*  WITH A DIFFERENT NAME AND KEEP IT AROUND.  THIS ROUTINE SETS                 
*  UP BOTH THE NEW AND OLD NAMES AND INDEXES.  CARE IS TAKEN TO                 
*  ORDER THE INFO UPDATES SO THAT THE INFO IS ALWAYS CONSISTENT!                
*  (IE. OLD MARKED ENTRY SAVED, NEW INDEX ENTRY SAVED, OLD INDEX                
*  UPDATED, NEW ENTRY REPLACES OLD ENTRY,,, DIG IT !)                           
*                                                                               
UPPWA    RECORD BEGIN                                                           
UPPXFOLD DS    A                   OLD XINFO DSECT                              
UPPXFNEW DS    A                   NEW XINFO DSECT                              
UPPXFX   DS    XL(L'XFXENTRY)      ENTRY INDEX DSECT                            
         END                                                                    
*                                                                               
*                                                                               
UPDTINFO PROC  UPPWA                                                            
         USING XFENTRY,R6                                                       
         USING XFXENTRY,R5                                                      
         ST    R1,UPPXFOLD                                                      
         ST    R2,UPPXFNEW                                                      
         LA    R5,UPPXFX                                                        
*                                                                               
         COMMENT                   SET XINFO SET NUMBERS                        
         LA    R15,CVRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
         LA    R15,CVRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   MARK OLD EXEC WITH NEW NAME                  
         L     R6,UPPXFOLD                                                      
         LA    R2,XFENTRY                                                       
         ACALL MRKNAME                                                          
*                                                                               
         COMMENT                   SAVE OLD (MARKED) ENTRY                      
         L     R6,UPPXFOLD                                                      
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XPUT                SAVE ENTRY INDEX                             
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   FORMAT NEW ENTRY INDEX                       
         L     R6,UPPXFNEW                                                      
         CLEAR XFXENTRY                                                         
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         STM   R0,R1,XFXNDX                                                     
         MVC   XFXNAME,XFNAME                                                   
*                                                                               
         COMMENT                   SAVE NEW ENTRY INDEX                         
         LA    R15,CVRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XPUT                SAVE ENTRY INDEX                             
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   FORMAT OLD (MARKED) ENTRY INDEX              
         L     R6,UPPXFOLD                                                      
         CLEAR XFXENTRY                                                         
         L     R0,XF#SETNO                                                      
         L     R1,XFRGEXEC                                                      
         STM   R0,R1,XFXNDX                                                     
         MVC   XFXNAME,XFNAME                                                   
*                                                                               
         COMMENT                   SAVE OLD ENTRY INDEX                         
         LA    R15,CVRG16                                                       
         LA    R0,L'XFXENTRY-L'XFXNDX                                           
         LA    R1,XFXENTRY+L'XFXNDX                                             
         LA    R2,XFXENTRY                                                      
         VCALL XPUT                SAVE ENTRY INDEX                             
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE NEW ENTRY                               
         L     R6,UPPXFNEW                                                      
         LA    R15,CVRG64                                                       
         LA    R0,L'XFENTRY-L'XFNAME                                            
         LA    R1,XFENTRY+L'XFNAME                                              
         LA    R2,XFNAME                                                        
         VCALL XPUT                SAVE ENTRY                                   
         IF    NEG,BEGIN                                                        
         KAPUT ,'Too many EXEC files loaded.  Page system overflow.'            
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETXRG - GET EXEC RECORD GROUP DSECT POINTER                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - SET NUMBER                                                         
*                                                                               
*  ON EXIT:                                                                     
*       R1 - EXEC RECORD GROUP POINTER (ZERO IF R15=NZ)                         
*       R15 - CC=Z, VALID SET NUMBER                                            
*             CC=NZ, NON-USED SET NUMBER                                        
*                                                                               
*  NOTE:  THIS ROUTINE WORKS LIKE THIS, AS OF 1/1/87..                          
*       1-2048 (CV#FEXEC) ARE USER EXECS                                        
*       2048+  ARE PUBLIC EXEC NUMBERS                                          
*  THE IDEA IS BY ASSIGNING DIFFERENT RANGES OF SET NUMBERS,                    
*  THE SET NUMBER UNIQUELY DEFINES THE EXEC, PRIOR TO 1/1/87                    
*  BOTH THE SET NUMBER AND EXEC RECORD GROUP NUMBER WERE                        
*  ALWAYS SPECIFIED TOGETHER,,, AND THAT IS THE WAY THEY ARE                    
*  GENERALLY USED WITHIN THIS MODULE.  IN ANY CASE, FOR NOW,                    
*  GIVEN AN EXEC SET NUMBER, THIS ROUTINE WILL RETURN THE                       
*  RECORD GROUP INFO DSECT POINTER. YEOW!                                       
*                                                                               
*  NOTE2: R15 REFLECTS ONLY WEAK TESTING OF SET NUMBER SPACE.                   
*  IT DOES NOT TELL IF AN ACTUAL EXEC EXISTS.                                   
*                                                                               
*                                                                               
GETXRG   PROC  ,                                                                
*                                                                               
         IF    (R0,Z),BEGIN                                                     
         CLEAR R1                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         ELSEIF ((R0,GT,1),AND,(R0,LT,CV#FEXEC)),BEGIN                          
         LA    R1,CPEXEC                                                        
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF (R0,GE,CV#FEXEC),BEGIN                                          
         LA    R1,CVEXEC                                                        
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         CLEAR R1                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETXNAME - GET EXEC NAME                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*       @R1 - EXEC NAME BUFFER (XDSNAME DSECT FORMAT)                           
*                                                                               
*  ON EXIT:                                                                     
*       @R1 - EXEC NAME (IN XDSNAME FORMAT)                                     
*       R15 - CC=Z, EXEC NAME FOUND (IE. EXEC EXISTS)                           
*             CC=NZ, NO EXEC FOR THIS SET NUMBER                                
*                                                                               
*                                                                               
*                                                                               
GXNWA    RECORD BEGIN                                                           
GXNSET   DS    F                   EXEC SET NUMBER                              
GXNRECD  DS    XL(L'XFENTRY)       EXEC FILE INFO RECORD                        
         END                                                                    
*                                                                               
*                                                                               
GETXNAME XPROC GXNWA                                                            
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LA    R6,GXNRECD                                                       
         LR    R5,R1                                                            
*                                                                               
         COMMENT                   INITIALIZE WORK AREA                         
         ST    R0,GXNSET                                                        
*                                                                               
         COMMENT                   IF NO SET,,                                  
         IF    (R0,Z),BEGIN                                                     
         CLEAR R0                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF OLD EXEC,, GET NAME                       
         ELSEIF (R0,EQ,CP#XOLD),BEGIN                                           
         LA    R0,L'GXNRECD                                                     
         LA    R1,GXNRECD                                                       
         LA    R2,=CL16'SYS.EXECNAME    '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         MVC   XDSNAME,XFNAME                                                   
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF XCALLED ACTIVE                            
         ELSEIF (R0,EQ,CP#XACT),BEGIN DUMMY XDSNAME                             
         MVC   XDSNAME,=CL(L'XDSNAME)'(ACTIVE file)'                            
*        VCALL SETXNAME            ACTIVE FILE NAME SET ABOVE                   
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF TEMP, BOMB                                
         ELSEIF (R0,EQ,CP#XTEMP),BEGIN                                          
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF XCALLED <FILENAME>                        
         ELSE  BEGIN ,,                                                         
         L     R0,GXNSET                                                        
         ACALL GETXRG                                                           
         IF    Z,BEGIN                                                          
         COMMENT                   HANDLE STADARD DSN NAME,                     
         COMMENT                   GET XINFO (CONTAINS DSN NAME)                
         LA    R2,XFENTRY                                                       
         ACALL GETXINFO                                                         
         IF    Z,BEGIN                                                          
         MVC   XDSNAME,XFNAME                                                   
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'EXEC LRU Cache and Load List Routines'                          
*box                                                                            
*                                                                               
*                                                                               
*  GETSETNO - GET SET NUMBER FOR NEW EXEC FILE                                  
*                                                                               
*  WE GET A SET NUMBER FOR NEW EXEC FILE FROM EXEC FILE                         
*  CACHE.  SCHEME IS LEAST RECENTLY USED AND IS IMPLEMENTED                     
*  WITH AN ORDERED LIST, MOST CURRENTLY USED IS FIRST.                          
*  A CALL TO 'MRKSETNO' WILL MARK SET NUMBER AS USED.                           
*                                                                               
*  ON EXIT:                                                                     
*       R0 - SET NUMBER TO USE                                                  
*       CPXALIST - EXEC FILE CACHE LRU LIST                                     
*                                                                               
*  NOTE: THIS ROUTINE IS FOR USE ONLY WITH USER EXEC FILES.                     
*        PUBLIC EXEC FILES ARE NEVER DELETED AND ARE ALLOCATED                  
*        NEW SETS IN ORDER, 1 TO 30000.                                         
*                                                                               
*                                                                               
GETSETNO PROC  ,                                                                
*                                                                               
*$ delete xlistdel ,,,, (cannot delete from lru list !)                         
*$ check to see if LRU is in CALLED LIST !! ERROR IF SO !                       
*$   (eg. UNABLE TO LOAD ... ?? hmmm,,, a minor point)                          
         COMMENT                   GET FREE EXEC SET NUMBER                     
         ACALL XLISTGET                                                         
         IF    NZ,BEGIN            IF NONE FREE,                                
         ACALL XLISTLRU            GET/FREE/USE LRU EXEC                        
         COMMENT                   RETURNS R0 - LRU SET NUMBER                  
         LA    R1,CPEXEC           R1 - POINT TO USERS EXEC RGROUP              
         ACALL DELUEXEC                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
**                                                                              
**  XLISTCLR - CLEAR EXEC SET NUMBER LIST, CALLED AT SIGN-ON                    
**                                                                              
**  WE KEEP A LARGE CACHE OF THE MOST RECENTLY USED EXEC FILES                  
**  FOR EACH USER.  THE CACHE WORKS ON A 'LRU' SCHEEME.  THAT                   
**  IS IF THE CACHE FILLS UP WE MOVE THE LRU EXEC FILE.  THE                    
**  LRU SCEEME IS IMPLEMENTED BY AN ORDERED LIST OF EXEC FILES                  
**  WITH THE MOST RECENTLY USED AT THE HEAD OF THE LIST.  ENTRIES               
**  IN THE LIST CAN RANGE FROM 1-256 WITH ZERO AS A NULL ENTRY.                 
**  IF THE CACHE IS FULL THE LRU EXEC IS PURGED AND THE NEW EXEC                
**  TAKES ON ITS 'EXEC SET NUMBER'.  EACH TIME AN EXEC IS LOADED                
**  OR CALLED IT IS MOVED (MARKED) TO THE START OF THE LIST AND                 
**  ALL OTHER ENTRIES ARE MOVED BACK ONE.                                       
**                                                                              
**                                                                              
**  NOTE: THESE ROUTINES ARE FOR USE ONLY WITH USER EXEC FILES.                 
**        PUBLIC EXEC FILES ARE NEVER DELETED AND ARE ALLOCATED                 
**        NEW SETS IN ORDER, 1 TO 30000+.                                       
**                                                                              
**                                                                              
XLISTCNT EQU   256 CPMAXXCT        MAXIMUM ELEMENTS IN LIST                     
XLISTLEN EQU   XLISTCNT*4          LENGTH OF LIST                               
XLFIRST  EQU   CP#FEXEC            FIRST ALLOWABLE EXEC SET NUMBER              
*                                                                               
XLISTWA  RECORD BEGIN ,                                                         
XLSETNO  DS    F                   SET NUMBER                                   
XLLIST   DS    XL(XLISTLEN)        SET LIST                                     
         ORG   XLLIST+XLISTLEN-4                                                
XLISTEND DS    F                   LAST ENTRY (LRU) IN LIST                     
         END                                                                    
*                                                                               
*                                                                               
XLISTCLR XPROC XLISTWA                                                          
         CLEAR XLLIST              CLEAR EXEC LRU LIST                          
*                                                                               
         COMMENT                  SAVE EXEC LRU LIST                            
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XLISTMRK - MARK SET IN LRU SET LIST                                          
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - SET NUMBER                                                         
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, EXEC MARKED AS USED                                      
*                                                                               
*                                                                               
*  THE SET LIST IS A 'LRU' ORDERED LIST OF EXEC FILES. SEE                      
*  COMMENTS AT START OF 'XLISTCLR' ROUTINE FOR MORE INFO.                       
*                                                                               
*                                                                               
XLISTMRK PROC  XLISTWA                                                          
         ST    R0,XLSETNO                                                       
         IF    ((R0,LT,XLFIRST),OR,(R0,GE,XLFIRST+XLISTCNT)),BEGIN              
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET QINFO                                    
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL GETDATA                                                          
         IF    (NZ,OR,(R0,NE,XLISTLEN)),BEGIN                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT   WE LOOP, LOOKING FOR ENTRY TO MARK, ALL PRIOR                
         COMMENT   ENTRIES ARE MOVED DOWN IN THE LIST UNTIL WE FIND             
         COMMENT   THE ONE TO MARK, IT IS PUT AT START OF THE LIST              
         COMMENT                                                                
         L     R0,XLSETNO          R0 - SET NUMBER TO MARK                      
         L     R1,XLLIST           R1 - LIST ENTRY                              
         LA    R2,XLLIST           R2 - LIST INDEX                              
         LA    R3,XLISTEND         R3 - END OF LIST                             
         COMMENT                   WHILE PRIOR ENTRY, LOOP                      
         WHILE ((R1,NZ),AND,(R1,NE,R0),AND,(R2,LT,R3)),BEGIN                    
         LR    R4,R1                                                            
         LA    R2,4(R2)                                                         
         L     R1,0(R2)                                                         
         ST    R4,0(R2)            MOVE IT DOWN                                 
         END                                                                    
         IF    ((R1,NZ),AND,(R1,NE,R0)),BEGIN  IF NO ROOM                       
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE LAST USED EXEC SET NUMBER               
         COMMENT                   AT START OF LIST                             
         ST    R0,XLLIST                                                        
*                                                                               
         COMMENT                   SAVE EXEC LRU LIST                           
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XLISTGET- GET FREE EXEC SET NUMBER                                           
*                                                                               
*  ON EXIT:                                                                     
*       R0 - SET NUMBER TO USE                                                  
*       R15 - CC=ZERO, FREE SET NUMBER EXISTS                                   
*             CC=NZ, ALL SETS USED                                              
*                                                                               
*  THE SET LIST IS A 'LRU' ORDERED LIST OF EXEC FILES. SEE                      
*  COMMENTS AT START OF 'XLISTCLR' ROUTINE FOR MORE INFO.                       
*                                                                               
*                                                                               
XLISTGET PROC  XLISTWA                                                          
*                                                                               
         COMMENT                   GET QINFO                                    
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL GETDATA                                                          
         IF    (NZ,OR,(R0,NE,XLISTLEN)),BEGIN                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         IF    (XLISTEND,NE,0),BEGIN     LIST FULL                              
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF LIST NOT FULL, GET UNUSED SET#            
         ELSE  BEGIN ,                                                          
         XPUSH ,,XLFIRST+XLISTCNT+2,PTR=R6                                      
         LR    R2,R6                                                            
         LA    R3,XLISTCNT+XLFIRST+2                                            
         CLEAR R5                                                               
         MVCL  R2,R4                                                            
         LA    R2,XLLIST           R2 - SET INDEX                               
         LA    R3,XLISTEND                                                      
         WHILE (R2,LE,R3),BEGIN    LOOP MARKING USED SETS                       
         L     R1,0(R2)                                                         
         LA    R4,0(R1,R6)                                                      
         MVI   0(R4),255                                                        
         LA    R2,4(R2)                                                         
         END                                                                    
         LA    R1,XLFIRST(R6)   LOOP TO FIND FIRST UNUSED                       
         WHILE (@R1,NE,0),BEGIN                                                 
         LA    R1,1(R1)                                                         
         END                                                                    
         SR    R1,R6               R1 - FIRST UNUSED SETNO                      
         XPOP  ,,XLFIRST+XLISTCNT+2                                             
         LR    R0,R1               RETURN R0                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XLISTLRU- GET 'LRU' SET NUMBER                                               
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LRU SET NUMBER                                                     
*       R15 - CC=ZERO, LRU EXISTS                                               
*             CC=NZ, NO LRU SET                                                 
*                                                                               
*  THE SET LIST IS A 'LRU' ORDERED LIST OF EXEC FILES. SEE                      
*  COMMENTS AT START OF 'XLISTCLR' ROUTINE FOR MORE INFO.                       
*                                                                               
*                                                                               
XLISTLRU PROC  XLISTWA                                                          
*                                                                               
         COMMENT                   GET QINFO                                    
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL GETDATA                                                          
         IF    (NZ,OR,(R0,NE,XLISTLEN)),BEGIN                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET LRU SET NUMBER                           
         L     R0,XLISTEND                                                      
         IF    (R0,NZ),BEGIN                                                    
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*****$$$$ THIS ROUTINE NEVER USED,,, LRU,,, WILL EVENTUALLY DELETE              
**                                                                              
**  XLISTDEL- DELETE SET FROM SET LIST                                          
**                                                                              
**  ON ENTRY:                                                                   
**       R0 - SET NUMBER                                                        
**                                                                              
**  ON EXIT:                                                                    
**       R15 - CC=ZERO                                                          
**                                                                              
**                                                                              
**  THE SET LIST IS A 'LRU' ORDERED LIST OF EXEC FILES. SEE                     
**  COMMENTS AT START OF 'XLISTCLR' ROUTINE FOR MORE INFO.                      
**                                                                              
*                                                                               
XLISTDEL PROC  XLISTWA                                                          
         ST    R0,XLSETNO                                                       
         IF    ((R0,LT,XLFIRST),OR,(R0,GE,XLFIRST+XLISTCNT)),BEGIN              
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET QINFO                                    
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL GETDATA                                                          
         IF    (NZ,OR,(R0,NE,XLISTLEN)),BEGIN                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   FIND SET TO DELETE                           
         COMMENT                                                                
         L     R0,XLSETNO          R0 - SET NUMBER TO MARK                      
         L     R1,XLLIST           R1 - LIST ENTRY                              
         LA    R2,XLLIST           R2 - LIST INDEX                              
         LA    R3,XLISTEND         R3 - END OF LIST                             
         WHILE ((R1,NE,R0),AND,(R2,LT,R3)),BEGIN                                
         LA    R2,4(R2)                                                         
         L     R1,0(R2)                                                         
         END                                                                    
         IF    (R1,NE,R0),BEGIN    IF NOT FOUND                                 
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   DELETE ITEM                                  
         XPUSH ,,XLISTLEN,PTR=R6                                                
         LA    R0,XLISTEND         CALCULATE LENGTH TO LIST END                 
         SR    R0,R1                                                            
         LR    R2,R6                                                            
         LR    R3,R0                                                            
         LA    R2,4(R1)                                                         
         LR    R5,R0                                                            
         MVCL  R2,R4                                                            
         LR    R2,R6                                                            
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         MVCL  R4,R2               MOVE ALL ENTRIES UP DELETING ITEM            
         XPOP  ,,XLISTLEN                                                       
         CLEAR XLISTEND                                                         
*                                                                               
         COMMENT                   SAVE EXEC LRU LIST                           
         LA    R0,XLISTLEN                                                      
         LA    R1,XLLIST                                                        
         LA    R2,=CL16'SYS.XSETLIST    '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'Active EXECS List Routines'                                     
*box                                                                            
*                                                                               
*                                                                               
*  CLRALIST - CLEAR ACTIVE EXECS LIST                                           
*                                                                               
*  THE ACTIVE EXECS LIST KEEPS TRACK OF ALL ACTIVE EXECS.                       
*  EXECS ARE CONSIDERED ACTIVE IF THEY HAVE BEEN CALLED OR                      
*  OR LOADED AS A RESULT OF AN INITIAL XCALL FROM THE TERMINAL.                 
*  THE NUMBER OF ACTIVE EXEC FILES MUST BE LESS THAT THE                        
*  NUMBER OF EXECS CACHED (LRU SCHEME).  THIS INSURES THAT THE                  
*  XSET NUMBER FOR ALL ACTIVE EXECS WILL NOT BE CHANGED UNTIL                   
*  ALL THE XCALLS HAVE RETURNED AND CONTROL IS GIVEN TO THE                     
*  TERMINAL.                                                                    
*                                                                               
*                                                                               
ALISTCNT EQU   128                 MAXIMUM ELEMENTS IN LIST                     
ALISTLEN EQU   ALISTCNT*4          LENGTH OF LIST                               
* MAXIMUM MUST BE LESS THAN NUMBER OF EXECS IN LRU CASHE/2 !!                   
*                                                                               
CAWA     RECORD BEGIN                                                           
CALIST   DS    XL(ALISTLEN)                                                     
         END                                                                    
*                                                                               
*                                                                               
CLRALIST XPROC CAWA                                                             
*                                                                               
         COMMENT                   CLEAR AND SAVE CALL LIST                     
         CLEAR CALIST                                                           
         LA    R0,L'CALIST                                                      
         LA    R1,CALIST                                                        
         LA    R2,=CL16'SYS.CALLEDLIST  '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  MRKALIST - MARK ACTIVE EXECS LIST                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - XSET NUMBER OF ACTIVE EXEC TO ADD                                  
*      @R1 - EXEC RECORD GROUP INFO BLOCK                                       
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=Z, ALL OK                                                       
*            CC= NZ, TOO MANY EXECS ACTIVE                                      
*                                                                               
*  THE ACTIVE EXECS LIST KEEPS TRACK OF ALL ACTIVE EXECS.                       
*  EXECS ARE CONSIDERED ACTIVE IF THEY HAVE BEEN CALLED OR                      
*  OR LOADED AS A RESULT OF AN INITIAL XCALL FROM THE TERMINAL.                 
*  THE NUMBER OF ACTIVE EXEC FILES MUST BE LESS THAT THE                        
*  NUMBER OF EXECS CACHED (LRU SCHEME).  THIS INSURES THAT THE                  
*  XSET NUMBER FOR ALL ACTIVE EXECS WILL NOT BE CHANGED UNTIL                   
*  ALL THE XCALLS HAVE RETURNED AND CONTROL IS GIVEN TO THE                     
*  TERMINAL.                                                                    
*                                                                               
*  NOTE: ZERO ENTRIES ARE EMPTY ENTRIES, AT LEAST ONE EMPTY                     
*  ENTRY MUST ALWAYS EXIST AT END OF LIST.                                      
*                                                                               
*                                                                               
*                                                                               
MAWA     RECORD BEGIN                                                           
MALIST   DS    XL(ALISTLEN)                                                     
         END                                                                    
*                                                                               
*                                                                               
MRKALIST PROC  MAWA                                                             
         LR    R3,R0               R3 - SET NUMBER TO ADD                       
*                                                                               
         COMMENT                   MARK ONLY PRIVATE EXECS,                     
         COMMENT                   PUBLIC EXECS ARE NOT IN LRU LIST             
         LA    R2,CPEXEC                                                        
         IF    (R1,NE,R2),BEGIN    IF PUBLIC EXEC, DO NOT ADD TO LIST           
         CLEAR R15                                                              
         B     MRKADONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET ACTIVE EXEC LIST                         
         LA    R0,L'MALIST                                                      
         LA    R1,MALIST                                                        
         LA    R2,=CL16'SYS.CALLEDLIST  '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SEE IF ALREADY IN ACTIVE LIST                
         COMMENT ,                 (LAST ENTRY IN LIST ALWAYS ZERO)             
         LA    R1,MALIST           R1 - ACTIVE LIST INDEX                       
         L     R2,0(R1)            R2 - ACTIVE SET NUMBER                       
         COMMENT                   R3 - SET NUMBER TO ADD                       
         COMMENT                   R4 - LAST ENTRY IN LIST                      
         LA    R4,MALIST+(L'MALIST-4)                                           
         WHILE ((R2,NZ),AND,(R2,NE,R3)),BEGIN                                   
         LA    R1,4(R1)                                                         
         L     R2,0(R1)                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF LIST FULL, RETURN CC=NZ                   
         IF    (R1,GE,R4),BEGIN                                                 
         LA    R15,4                                                            
         B     MRKADONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF SET # NOT IN LIST,                        
         IF    (R2,EQ,0),BEGIN                                                  
         ST    R3,0(R1)            ADD TO LIST                                  
         LA    R0,L'MALIST                                                      
         LA    R1,MALIST                                                        
         LA    R2,=CL16'SYS.CALLEDLIST  '                                       
         VCALL SAVEDATA                                                         
         END   ,                                                                
         CLEAR R15                 SET NUMBER IN LIST NOW                       
*                                                                               
MRKADONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKALIST - CHECK ACTIVE EXEC LIST                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - XSET NUMBER OF EXEC TO CHECK                                       
*      @R1 - EXEC RECORD GROUP INFO BLOCK                                       
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=Z, EXEC ALREADY ACTIVE                                          
*            CC= NZ, EXEC NOT ACTIVE                                            
*                                                                               
*  THE ACTIVE EXECS LIST KEEPS TRACK OF ALL ACTIVE EXECS.                       
*  EXECS ARE CONSIDERED ACTIVE IF THEY HAVE BEEN CALLED OR                      
*  OR LOADED AS A RESULT OF AN INITIAL XCALL FROM THE TERMINAL.                 
*  THE NUMBER OF ACTIVE EXEC FILES MUST BE LESS THAT THE                        
*  NUMBER OF EXECS CACHED (LRU SCHEME).  THIS INSURES THAT THE                  
*  XSET NUMBER FOR ALL ACTIVE EXECS WILL NOT BE CHANGED UNTIL                   
*  ALL THE XCALLS HAVE RETURNED AND CONTROL IS GIVEN TO THE                     
*  TERMINAL.                                                                    
*                                                                               
*  NOTE: ZERO ENTRIES ARE EMPTY ENTRIES, AT LEAST ONE EMPTY                     
*  ENTRY MUST ALWAYS EXIST AT END OF LIST.                                      
*                                                                               
*                                                                               
*                                                                               
KAWA     RECORD BEGIN                                                           
KALIST   DS    XL(ALISTLEN)                                                     
         END                                                                    
*                                                                               
*                                                                               
CHKALIST PROC  KAWA                                                             
         LR    R3,R0               SET NUMBER TO CHECK                          
*                                                                               
         COMMENT                   CHECK ONLY PRIVATE EXECS,                    
         COMMENT                   PUBLIC EXECS ARE NOT IN LRU LIST             
         LA    R2,CPEXEC                                                        
         IF    (R1,NE,R2),BEGIN    IF PUBLIC EXEC, DO CHECK                     
         CLEAR R15                                                              
         B     CHKADONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET ACTIVE EXECS LIST                        
         LA    R0,L'KALIST                                                      
         LA    R1,KALIST                                                        
         LA    R2,=CL16'SYS.CALLEDLIST  '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SEE IF ALREADY IN ACTIVE LIST                
         COMMENT ,                 (LAST ENTRY IN LIST ALWAYS ZERO)             
         LA    R1,KALIST           R1 - CALLED LIST INDEX                       
         L     R2,0(R1)            R2 - CALLED SET NUMBER                       
         COMMENT                   R3 - SET NUMBER TO ADD                       
         COMMENT                   R4 - LAST ENTRY IN LIST                      
         LA    R4,KALIST+(L'KALIST-4)                                           
         WHILE ((R2,NZ),AND,(R2,NE,R3)),BEGIN                                   
         LA    R1,4(R1)                                                         
         L     R2,0(R1)                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF IN LIST CC=Z, IF NOT CC=NZ                
         IF    (R2,Z),BEGIN        IF NOT IN LIST                               
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN               IF IN LIST                                   
         CLEAR R15                                                              
         END                                                                    
*                                                                               
CHKADONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'XCALL/PCALL - Routines '                                        
*box                                                                            
*                                                                               
*                                                                               
*  SCNXCALL - SCAN XCALL COMMAND                                                
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINDER OF XCALL COMMAND                                     
*                                                                               
*  ON EXIT:                                                                     
*       R0 - PARM_STRING LENGTH                                                 
*       R1 - PARM_STRING                                                        
*       R2 - R2=0 -- PARM LIST,  R2=NZ -- FREE FORMAT PARM                      
*       R3 - R3=0 -- ACTIVE,  R3=NZ -- REGULAR DSN                              
*                                                                               
*                                                                               
*                                                                               
SCNXWA   RECORD BEGIN                                                           
SCNXFLG1 FLAG  ,                                                                
         FLAG  (SCXFACT,1,EQ)      ACTIVE                                       
         FLAG  (SCXFDSN,2,EQ)      DSN                                          
SCNXFLG2 FLAG  ,                                                                
         FLAG  (SCXFFREE,1,EQ)     FREE FORMAT PARMS                            
         FLAG  (SCXFPARM,2,EQ)     PARM LIST SPECIFIED                          
SCNXPLEN DS    F                   PARM STRING LENGTH                           
SCNXPARM DS    CL(&LINESZ)         PARM STRING                                  
         END                                                                    
*                                                                               
*                                                                               
SCNXCALL PROC  SCNXWA                                                           
         LA    R6,SCNXWA                                                        
         USING SCNXWA,R6                                                        
*                                                                               
         COMMENT                   INIT WORK AREAS                              
         CLEAR SCNXFLG1                                                         
         CLEAR SCNXFLG2                                                         
         CLEAR SCNXPLEN                                                         
*                                                                               
         COMMENT                   CHECK FOR DSN/ACTIVE                         
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCANCHK                                                                
         SCPOP ,                                                                
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing data set name.'                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN XCALL COMMAND                           
         SCAN  SCNXPRT                                                          
         SCANCHK                                                                
         CLEAR R15                                                              
*                                                                               
         L     R0,SCNXPLEN         LOAD UP RETURN PARMS                         
         LA    R1,SCNXPARM                                                      
         CLEAR R2                                                               
         IF    SCXFFREE,BEGIN                                                   
         LA    R2,4                                                             
         END                                                                    
         IF    SCXFACT,BEGIN                                                    
         CLEAR R3                                                               
         END                                                                    
         IF    SCXFDSN,BEGIN                                                    
         LA    R3,4                                                             
         END                                                                    
         CLEAR R15                                                              
         PRETURN (R0,R3)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       SCAN ROUTINES FOR SCAN XCALL                                           
*-                                                                              
SCNXPRT  SCKW  ACTIVE,SXACT                                                     
         SCKW  ,SXDSN                                                           
*                                                                               
SXDSNPRT SCKW  ,V(FNAMPSHF),PUSH    FILE NAME OPTIONS                           
         SCKW  ,SXPARM                                                          
*                                                                               
SXACTPRT SCKW  ,SXPARM             ACTIVE OPTIONS                               
         SPACE 4                                                                
*-                                                                              
*-       SCAN ACTIVE                                                            
*-                                                                              
SXACT    PROC  ,                                                                
         SET   SCXFACT             SET ACTIVE SCANNED                           
         SCAN  SXACTPRT            SCAN FOR PARMS                               
         LA    R15,4                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       SCAN DSN                                                               
*-                                                                              
SXDSN    PROC  ,                                                                
         SET   SCXFDSN             SET DSN SCANNED                              
         SCPUSH ,                                                               
         SCINIT (R1),(R0)                                                       
         VCALL DOFNAME                                                          
         VCALL FILEOPTS             SCAN FILE NAME                              
*        SCAN L:=V(DSNPRT)         SCAN DSNNAME                                 
         SCPOP ,                                                                
         SCAN  SXDSNPRT            SCAN FOR MORE                                
         SCANCHK                                                                
         VCALL FNAMEFIN                                                         
         VCALL NRESOLVE                                                         
         PEND  ,                                                                
*-                                                                              
*-       SCAN PARM                                                              
*-                                                                              
SXPARM   PROC  ,                                                                
         IF    (@R1,EQ,'('),BEGIN  IF () PARMS,,,                               
         SET   SCXFPARM                                                         
         ST    R0,SCNXPLEN                                                      
         L     R3,SCNXPLEN                                                      
         MOVE  R3,SCNXPARM,@R1                                                  
         CLEAR R15                                                              
         END                                                                    
         COMMENT                   IF '' OR "" PARMS                            
         ELSEIF ((@R1,EQ,'"'),OR,(@R1,EQ,'''')),BEGIN                           
         SET   SCXFFREE                                                         
         SCDQUOTE SCNXPARM,L'SCNXPARM                                           
         ST    R0,SCNXPLEN                                                      
         CLEAR R15                                                              
         END                                                                    
         ELSEIF (@R1,EQ,'/'),BEGIN IF / PARMS                                   
         SCINFO                                                                 
         SET   SCXFFREE                                                         
         ST    R0,SCNXPLEN                                                      
         L     R3,SCNXPLEN                                                      
         MOVE  R3,SCNXPARM,@R1                                                  
         SCINIT CVBLANKS,0                                                      
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN               IF NONE OF ABOVE                             
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid',,INVALID                                             
         END                                                                    
*                                                                               
         PEND  ,                   CONTINUE SCANNING                            
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNPCALL - SCAN PCALL COMMAND FOR PARM LIST/STRING                           
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINDER OF PCALL COMMAND                                     
*                (PROC NAME HAS ALREADY BEEN SCANNED)                           
*                                                                               
*  ON EXIT:                                                                     
*       R0 - PARM_STRING LENGTH                                                 
*       R1 - PARM_STRING                                                        
*       R2 - R2=0 -- PARM LIST,  R2=NZ -- FREE FORMAT PARM                      
*                                                                               
*                                                                               
*                                                                               
SNNPWA   RECORD BEGIN                                                           
SNNPFLG  FLAG  ,                                                                
         FLAG  (SCPFFREE,1,EQ)     FREE FORMAT PARMS                            
         FLAG  (SCPFPARM,2,EQ)     PARM LIST SPECIFIED                          
SNNPPLEN DS    F                   PARM STRING LENGTH                           
SNNPPARM DS    CL(&LINESZ)         PARM STRING                                  
         END                                                                    
*                                                                               
*                                                                               
SCNPCALL PROC  SNNPWA                                                           
         LA    R6,SNNPWA                                                        
         USING SNNPWA,R6                                                        
*                                                                               
         COMMENT                   INIT WORK AREAS                              
         CLEAR SNNPFLG                                                          
         CLEAR SNNPPLEN                                                         
*                                                                               
         COMMENT                   SCAN XCALL COMMAND                           
         SCAN  SNNPPRT                                                          
         SCANCHK                                                                
*                                                                               
         L     R0,SNNPPLEN         LOAD UP RETURN PARMS                         
         LA    R1,SNNPPARM                                                      
         CLEAR R2                                                               
         IF    SCPFFREE,BEGIN                                                   
         LA    R2,4                                                             
         END                                                                    
         CLEAR R15                                                              
         PRETURN (R0,R3)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       SCAN ROUTINES FOR SCAN PCALL OPTIONS                                   
*-                                                                              
SNNPPRT  SCKW  ,SPPARM                                                          
         SPACE 4                                                                
*-                                                                              
*-       SCAN PARM                                                              
*-                                                                              
SPPARM   PROC  ,                                                                
         IF    (@R1,EQ,'('),BEGIN  IF () PARMS,,,                               
         SET   SCPFPARM                                                         
         ST    R0,SNNPPLEN                                                      
         L     R3,SNNPPLEN                                                      
         MOVE  R3,SNNPPARM,@R1                                                  
         CLEAR R15                                                              
         END                                                                    
         COMMENT                   IF '' OR "" PARMS                            
         ELSEIF ((@R1,EQ,'"'),OR,(@R1,EQ,'''')),BEGIN                           
         VCALL DEQUOTE                                                          
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': mismatched quotes.',,SYNTAX                                   
         END                                                                    
         SET   SCPFFREE                                                         
         ST    R0,SNNPPLEN                                                      
         L     R3,SNNPPLEN                                                      
         MOVE  R3,SNNPPARM,@R1                                                  
         CLEAR R15                                                              
         END                                                                    
         ELSEIF (@R1,EQ,'/'),BEGIN IF / PARMS                                   
         SCINFO                                                                 
         SET   SCPFFREE                                                         
         ST    R0,SNNPPLEN                                                      
         L     R3,SNNPPLEN                                                      
         MOVE  R3,SNNPPARM,@R1                                                  
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN               IF NONE OF ABOVE                             
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid',,INVALID                                             
         END                                                                    
         PEND  ,                   CONTINUE SCANNING                            
         SPACE 4                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNDSN - SCAN DSN                                                            
*                                                                               
*  SCAN DSN NAME ALLOWING THE FOLLOWING:                                        
*       ACTIVE                                                                  
*       <DSNAME>  <DSN OPTIONS>                                                 
*       FROM <DSNAME>  <DSN OPTIONS>                                            
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - SCAN CONTROL BLOCK SET UP                                      
*       IF R0=0, 'FROM' REQUIRED, IF R0=NZ 'FROM' OPTIONAL                      
*                                                                               
*  ON EXIT:                                                                     
*       FFINFO DXECT CONTAIN FILE NAME, FILE HOST, FILE TYPE                    
*       R15 - CC=Z, IF 'ACTIVE' SCANNED                                         
*             CC=NZ, IF <DSN>  ... SCANNED                                      
*             NULL SCAN AND ERRORS EXIT VIA CVERROR                             
*                                                                               
*                                                                               
*                                                                               
SCNDSN   PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK FOR MISSING DSN                        
         XPUSH R0                                                               
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCANCHK ,                                                              
         SCPOP ,                                                                
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing data set name.'                                         
         END                                                                    
         XPOP  R0                                                               
*                                                                               
         COMMENT                   SCAN 'FROM' DSN  OR 'ACTIVE'                 
         IF    (R0,Z),BEGIN                                                     
         SCAN  SDSNPRT                                                          
         SCANCHK                                                                
         END                                                                    
*                                                                               
         COMMENT                   SCAN, 'FROM' OPTIONAL                        
         ELSE BEGIN                                                             
         SCAN  SDSNXPRT                                                         
         END                                                                    
*                                                                               
SDSNDONE LABEL ,                                                                
         LR    R15,R5              SET RETURN CODE                              
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       DSN SCANNER ROUTINES                                                   
*-                                                                              
SDSNPRT  SCKW  FROM,SDSNFROM,A                                                  
         SCKW  ACTIVE,SDSNACT,A                                                 
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
SDSNXPRT SCKW  FROM,SDSNFROM,A                                                  
         SCKW  ACTIVE,SDSNACT,A                                                 
         SCKW  ,SDSNDSN                                                         
         SPACE 4                                                                
*-                                                                              
*-       ACTIVE                                                                 
*-                                                                              
SDSNACT  PROC  ,                                                                
         CLEAR R5                  MARK 'ACTIVE' SCANNED                        
         PRETURN (R5)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         SPACE 3                                                                
*-                                                                              
*-       DSN                                                                    
*-                                                                              
SDSNDSN  PROC  ,                                                                
         SCPUSH ,                  SAVE OPTIONS                                 
         SCINIT (R1),(R0)          INIT NAME                                    
         VCALL DOFNAME                                                          
         VCALL FILEOPTS                                                         
         SCPOP ,                                                                
         VCALL FILFOPTS                                                         
         VCALL FNAMEFIN                                                         
         VCALL NRESOLVE                                                         
         LA    R5,4                                                             
         PRETURN (R5)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         SPACE 3                                                                
*-                                                                              
*-       FROM DSN                                                               
*-                                                                              
SDSNFROM PROC  ,                                                                
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCANCHK                                                                
         SCPOP ,                                                                
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing data set name.'                                         
         END                                                                    
         VCALL DOFNAME                                                          
         VCALL FILEOPTS                                                         
         VCALL FNAMEFIN                                                         
         VCALL NRESOLVE                                                         
         LA    R5,4                                                             
         PRETURN (R5)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'XCALL/PCALL - Parameter Scanning Routines'                      
*box                                                                            
*                                                                               
*                                                                               
*  PSCNPARM - PRESCAN PARM FIELD                                                
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - SCAN CONTROL BLOCK                                             
*  ON EXIT:                                                                     
*       R0 - PARM LENGTH                                                        
*       R1 - PARM LOCATION                                                      
*      R15 - CC=ZERO                                                            
*            ALL ERRORS EXIT VIA CVERROR                                        
*                                                                               
*                                                                               
PSCNPARM XPROC ,                                                                
*                                                                               
         CLEAR R4                  R4 - START                                   
         CLEAR R5                  R5 - END                                     
*                                                                               
         COMMENT                   SCAN TO END OF PARM                          
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,NZ),BEGIN                                                    
         LR    R4,R1               MARK START                                   
         LR    R5,R1                                                            
         AR    R5,R0                                                            
         IF    (@R1,EQ,'('),BEGIN  IF ... (PARM LIST),                          
         VCALL NOOPTS              CHECK FOR NO FURTHER PARMS                   
         END                                                                    
         ELSE  BEGIN               IF ... FREE FORM PARMS ..,                   
         SCAN  ,                   KEEP SCANNING                                
         SCANCHK                                                                
         WHILE (R0,NZ),BEGIN                                                    
         LR    R5,R1                                                            
         AR    R5,R0                                                            
         SCAN  ,                                                                
         SCANCHK                                                                
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         LR    R1,R4                                                            
         LR    R0,R5                                                            
         SR    R0,R1                                                            
         VCALL LRTRIM                                                           
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNNLIST - EVALUATE PROCEDURE NAME LIST                                      
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF REMAINING EXPRESSION                                     
*       R1 - START OF PARM LIST (POINTS AT "(")                                 
*       R2 - RETURN AREA FOR NAME LIST                                          
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NAME COUNT (SEE NOTE)                                              
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, INVALID NAME LIST, ERR MSG TSEG'D                           
*                                                                               
*  REGISTER USAGE:                                                              
*       R1,R0 - STRING LOCATION,LENGTH                                          
*       R2 - NAME RETURN LIST INDEX                                             
*       R3 - VALUE COUNT                                                        
*                                                                               
*  NOTE: NAME COUNT IS -1 IF FREE FORM PARAMETER.                               
*        NAME COUNT IS 0 IF NONE.                                               
*        NAME COUNT IS 0 TO N FOR NAMES INCLOSED IN PARENTHESIS.                
*                                                                               
*                                                                               
SCNNLIST XPROC ,                                                                
*                                                                               
         LR    R4,R2                                                            
         L     R5,=AL4(L'NLIST)                                                 
         CLEAR R15                                                              
         MVCL  R4,R14              CLEAR VALUE LIST                             
         CLEAR R3                                                               
         VCALL LRTRIM                                                           
*                                                                               
         COMMENT                   CHECK FOR NO NAMES                           
         IF    (R0,NP),BEGIN                                                    
         CLEAR R3                                                               
         CLEAR R15                                                              
         B     SCNNDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF FREE FORMAT LIST, BOMB                    
         IF    (@R1,NE,'('),BEGIN                                               
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GOT '(',  SKIP IT                            
         LA    R1,1(R1)                                                         
         BCTR  R0,0                                                             
*                                                                               
         COMMENT                   CHECK FOR (),   EMPTY NAMES                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,')'),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         CLEAR R3                                                               
         CLEAR R15                                                              
         B     SCNNDONE                                                         
         END                                                                    
*-                                                                              
*-       LOOP SCANNING NAMES !!                                                 
*-                                                                              
         COMMENT                   LOOP EVALUATING NAMES                        
         CLEAR R3                                                               
         WHILE (@R1,NE,')'),BEGIN  LOOP                                         
         LA    R3,1(R3)                                                         
         IF    (R3,GT,CPPLIMIT),BEGIN   ERROR: TOO MANY NAMES                   
         ACALL MANYPARM                                                         
         B     SCNNDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         TPUSH ,                   TPUSH-TPOP - JUNK ANY ERR MSG                
         ACALL SCNPDESC            EVALUATE NAME NAME (DESCRIPTOR)              
         TPOP  JUNK                                                             
         IF    (R15,NZ),SCNNDONE                                                
         COMMENT                   IF ALL OK, NAME RETURNED @R2                 
*                                                                               
         COMMENT                   SKIP TRAILING BLANKS                         
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   ONLY ALLOW SIMPLE NAMES                      
         USING VNENTRY,R2                                                       
         IF    (VNPREFIX,NE,0),BEGIN                                            
         TSEG  'Procedure variable list may contain only '                      
         TSEG  'simple local variables and arrays.',,CR                         
         TSEG  'Prefixed variables are not allowed.  '                          
         ACALL PARMERR                                                          
         B     SCNNDONE                                                         
         END                                                                    
         IF    (VNASUB1,NE,0),BEGIN                                             
         TSEG  'Procedure variable list may contain only '                      
         TSEG  'simple local variables and arrays.',,CR                         
         TSEG  'Array element specified is not a local variable.'               
         ACALL PARMERR                                                          
         B     SCNNDONE                                                         
         END                                                                    
         DROP  R2                                                               
*                                                                               
         IF    (R0,NP),BEGIN            ERROR: PREMATURE END OF NAMES           
         TSEG  'Expecting ")".'                                                 
         ACALL PARMERR                                                          
         B     SCNNDONE                                                         
         END                                                                    
         IF    (@R1,EQ,';'),BEGIN       ERROR: PREMATURE END OF NAMES           
         TSEG  '; -- invalid.  Expecting ")".'                                  
         ACALL PARMERR                                                          
         B     SCNNDONE                                                         
         END                                                                    
         IF    (@R1,NE,','),BEGIN       ERROR: MISSING NAME SEPARATOR           
         IF    (@R1,NE,')'),BEGIN                                               
         VCALL CHECKEND                                                         
         TSEG  'Procedure parameters must be separated by commas.'              
         ACALL PARMERR                                                          
         B     SCNNDONE                                                         
         END                                                                    
         END                                                                    
         IF    (@R1,EQ,','),BEGIN       GOT NAME SEPARATOR, SKIP                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         COMMENT                   GET SET TO EVALUATE NEXT NAME                
         LA    R2,L'VNDESC(R2)     R2 - NEXT PLACE TO PUT VALUE                 
         COMMENT                   R1,R0 - LOC,LEN OF REMAINING NAMES           
         COMMENT                                                                
         END   ,,                  LOOP EVALUATING PARAMETERS                   
*                                                                               
         COMMENT                   LOOP TERMINATED OK,,                         
         LA    R1,1(R1)            SKIP FINAL ')'                               
         DECR  R0                                                               
         CLEAR R15                 SET CC=ZERO                                  
*                                                                               
SCNNDONE LABEL ,                                                                
         LR    R0,R3               RETURN PARM COUNT                            
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNVLIST - EVALUATE PROCEDURE CALL PARAMETERS (INTO VALUES)                  
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF REMAINING EXPRESSION                                     
*       R1 - START OF PARM LIST, (STARTS WITH "(")                              
*       R2 - RETURN AREA FOR VALUE LISTS                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - PARAMETER COUNT (SEE NOTE)                                         
*      R15 - CC=ZERO, ALL OK                                                    
*            ALL ERRORS EXIT VIA CVERROR                                        
*                                                                               
*  REGISTER USAGE:                                                              
*       R1,R0 - STRING LOCATION,LENGTH                                          
*       R2 - VALUE RETURN LIST INDEX                                            
*       R3 - VALUE COUNT                                                        
*                                                                               
*  NOTE: NAME COUNT IS -1 IF FREE FORM PARAMETER.                               
*        NAME COUNT IS 0 IF NONE.                                               
*        NAME COUNT IS 0 TO N FOR NAMES INCLOSED IN PARENTHESIS.                
*                                                                               
*                                                                               
SCNVLIST PROC  ,                                                                
*                                                                               
         LR    R4,R2                                                            
         L     R5,=AL4(L'VLIST)                                                 
         CLEAR R15                                                              
         MVCL  R4,R14              CLEAR VALUE LIST                             
         CLEAR R3                                                               
         VCALL LRTRIM                                                           
*                                                                               
         COMMENT                   CHECK FOR NO PARMS                           
         IF    (R0,NP),BEGIN                                                    
         CLEAR R3                                                               
         B     SCNVDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF FREE FORMAT LIST, BOMB                    
         IF    (@R1,NE,'('),BEGIN                                               
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GOT '(',  SKIP IT                            
         LA    R1,1(R1)                                                         
         BCTR  R0,0                                                             
*                                                                               
         COMMENT                   CHECK FOR (),   EMPTY PARMS                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,')'),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         CLEAR R3                                                               
         B     SCNVDONE                                                         
         END                                                                    
*-                                                                              
*-       LOOP EVALUATING PARAMTERES !!                                          
*-                                                                              
         COMMENT                   LOOP EVALUATING PARAMTERS                    
         CLEAR R3                                                               
         WHILE (@R1,NE,')'),BEGIN  LOOP UNTIL END OF SUBSCRIPTS                 
         LA    R3,1(R3)                                                         
         IF    (R3,GT,CPPLIMIT),BEGIN   ERROR: TOO MANY PARMS                   
         ACALL MANYPARM                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                                                                
         ACALL EVALPARM            EVALUATE PARM EXPRESSION                     
         COMMENT                   IF ALL OK, VALUE RETURNED @R2                
*                                                                               
         COMMENT                                                                
         IF    (R15,POS),BEGIN          ERROR: BAD PARM EXPRESSION              
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (R15,NEG),BEGIN          ERROR: NULL PARAMETER                   
         TSEG  'Missing Parameter.'                                             
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (R0,NP),BEGIN            ERROR: PREMATURE END OF PARMS           
         TSEG  'Expecting ")".'                                                 
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (@R1,EQ,';'),BEGIN       ERROR: PREMATURE END OF PARMS           
         TSEG  '; -- invalid.  Expecting ")".'                                  
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (@R1,NE,','),BEGIN       ERROR: MISSING PARM SEPARATOR           
         IF    (@R1,NE,')'),BEGIN                                               
         VCALL CHECKEND                                                         
         TSEG  'Procedure parameters must be separated by commas.'              
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         END                                                                    
         IF    (@R1,EQ,','),BEGIN       GOT PARM SEPARATOR, SKIP                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         COMMENT                   GET SET TO EVALUATE NEXT PARM                
         LA    R2,L'VPARM(R2)      R2 - NEXT PLACE TO PUT VALUE                 
         COMMENT                   R1,R0 - LOC,LEN OF REMAINING PARMS           
         COMMENT                                                                
         END   ,,                  LOOP EVALUATING PARAMETERS                   
*                                                                               
SCNVDONE LABEL ,                                                                
         LR    R0,R3                                                            
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNPLIST - EVALUATE PROCEDURE CALL PARAMETERS (INTO NAMES)                   
*                                                                               
*  WE SCAN FOR PARMS THAT ARE VALID VARIABLES AS WELL AS                        
*  VALUES. THESE ENTRIES FORM A PARM LIST THAT IS USED IF                       
*  WHEN RETURNING FROM A PROC/XPROC CALL.  WE SCAN BY                           
*  SCANNING EACH PARM AS A VALUE AND AS A DESCRIPTOR. IF                        
*  THE TWO ARE EQUAL WE HAVE A PARM AS A NAME AND PUT IT                        
*  IN THE LIST TO BE USED IF VAIABLES ARE RETURED.  ONE                         
*  EXTRA POINT,  GENERIC ARRAYS (IE. X[]) ARE VALID AND                         
*  ARE HANDLED BY USING SCNPDESC AND EVALPARM,, INSTEAD OF                      
*  SCNVDESC AND EVALEXPR ROUTINES.  THINK ABOUT IT, BUD.                        
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF REMAINING EXPRESSION                                     
*       R1 - START OF PARM LIST, (STARTS WITH "(")                              
*       R2 - RETURN AREA FOR PARAMETER NAME LIST                                
*                                                                               
*  ON EXIT:                                                                     
*       R0 - PARAMETER COUNT (SEE NOTE)                                         
*      R15 - CC=ZERO, ALL OK                                                    
*            ALL ERRORS EXIT VIA CVERROR                                        
*                                                                               
*  REGISTER USAGE:                                                              
*       R1,R0 - STRING LOCATION,LENGTH                                          
*       R3 - VALUE COUNT                                                        
*       R6 - VALUE RETURN LIST INDEX                                            
*                                                                               
*  NOTE: NAME COUNT IS -1 IF FREE FORM PARAMETER.                               
*        NAME COUNT IS 0 IF NONE.                                               
*        NAME COUNT IS 0 TO N FOR NAMES INCLOSED IN PARENTHESIS.                
*                                                                               
*                                                                               
SCNPWA   RECORD BEGIN                                                           
SCNPLOC  DS    F                   SCAN LOCATION, LENGTH                        
SCNPLEN  DS    F                                                                
SCNPVAR  DS    XL(L'VPARM)         TEMP EVAL AREA                               
         END                                                                    
*                                                                               
*                                                                               
SCNPLIST PROC  SCNPWA                                                           
*                                                                               
         LR    R6,R2                                                            
         LR    R4,R2                                                            
         L     R5,=AL4(L'PLIST)                                                 
         CLEAR R15                                                              
         MVCL  R4,R14              CLEAR VALUE LIST                             
         CLEAR R3                                                               
         VCALL LRTRIM                                                           
*                                                                               
         COMMENT                   CHECK FOR NO PARMS                           
         IF    (R0,NP),BEGIN                                                    
         CLEAR R3                                                               
         B     SCNPDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF FREE FORMAT LIST, BOMB                    
         IF    (@R1,NE,'('),BEGIN                                               
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GOT '(',  SKIP IT                            
         LA    R1,1(R1)                                                         
         BCTR  R0,0                                                             
*                                                                               
         COMMENT                   CHECK FOR (),   EMPTY PARMS                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,')'),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         CLEAR R3                                                               
         B     SCNPDONE                                                         
         END                                                                    
*-                                                                              
*-       LOOP EVALUATING PARAMTERES !!                                          
*-                                                                              
         COMMENT                   LOOP EVALUATING PARAMTERS                    
         CLEAR R3                                                               
         WHILE (@R1,NE,')'),BEGIN  LOOP UNTIL END OF SUBSCRIPTS                 
         LA    R3,1(R3)                                                         
         IF    (R3,GT,CPPLIMIT),BEGIN   ERROR: TOO MANY PARMS                   
         ACALL MANYPARM                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   SCAN AS NAME                                 
         ST    R1,SCNPLOC                                                       
         ST    R0,SCNPLEN                                                       
         LR    R2,R6                                                            
         TPUSH ,                   TPUSH-TPOP - JUNKS ANY ERR MSG               
         ACALL SCNPDESC            SCAN                                         
         TPOP  JUNK                                                             
         COMMENT                   IF ALL OK, DESCRIPTOR RETURNED @R2           
         IF    (R15,Z),BEGIN                                                    
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),BEGIN SKIP TRAILING BLANKS           
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R1                                                               
         END                                                                    
         LR    R4,R1               R4 - END OF SCANNED NAME                     
*                                                                               
         COMMENT                   SCAN AGAIN AS VALUE                          
         L     R0,SCNPLEN                                                       
         L     R1,SCNPLOC                                                       
         LA    R2,SCNPVAR                                                       
         ACALL EVALPARM            EVALUATE PARM EXPRESSION                     
         IF    Z,BEGIN             IF EVALUATED OK,,,                           
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),BEGIN SKIP TRAILING BLANKS           
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         COMMENT                   R1 - END OF SCANNED VALUE                    
*                                                                               
         COMMENT                   IF PARM IS NOT NAME, (BUT EXPR)              
         IF    (R1,NE,R4),BEGIN    CLEAR PARM (NAME) ENTRY IN LIST              
         XC    0(L'VNDESC,R6),0(R6)                                             
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    (R15,POS),BEGIN          ERROR: BAD PARM EXPRESSION              
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (R15,NEG),BEGIN          ERROR: NULL PARAMETER                   
         TSEG  'Missing Parameter.'                                             
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (R0,NP),BEGIN            ERROR: PREMATURE END OF PARMS           
         TSEG  'Expecting ")".'                                                 
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (@R1,EQ,';'),BEGIN       ERROR: PREMATURE END OF PARMS           
         TSEG  '; -- invalid.  Expecting ")".'                                  
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         IF    (@R1,NE,','),BEGIN       ERROR: MISSING PARM SEPARATOR           
         IF    (@R1,NE,')'),BEGIN                                               
         VCALL CHECKEND                                                         
         TSEG  'Procedure parameters must be separated by commas.'              
         ACALL PARMERR                                                          
         B     CVERROR                                                          
         END                                                                    
         END                                                                    
         IF    (@R1,EQ,','),BEGIN       GOT PARM SEPARATOR, SKIP                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         COMMENT                   GET SET TO EVALUATE NEXT PARM                
         LA    R6,L'VNDESC(R6)     R2 - NEXT PLACE TO PUT VALUE                 
         COMMENT                   R1,R0 - LOC,LEN OF REMAINING PARMS           
         COMMENT                                                                
         END   ,,                  LOOP EVALUATING PARAMETERS                   
*                                                                               
SCNPDONE LABEL ,                                                                
         LR    R0,R3                                                            
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNPDESC - SCAN PARMATER NAME (DESCRIPTOR)                                   
*                                                                               
*  THIS ROUTINE SCANS VARIABLE NAMES, AND IN THE CASE OF                        
*  AN ARRAY, IT'S SUBSCRIPTS.  SEE VNDESC IN VNENTRY DSECT.                     
*  ONLY NAME SYNTAX IS CHECKED, WHILE SUBSCRIPTS ARE EVALUATED.                 
*  THIS ROUTINE DIFFERS FROM SCNVDESC IN THAT IT ALLOWS                         
*  GENERIC ARRAY ENTRIES (EG. X[], TEST2[])                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF STRING                                                   
*       R1 - LOCATION OF STRING                                                 
*      @R2 - AREA TO RETURN NAME DESCRIPTOR (OF LEN L'VNDESC)                   
*                                                                               
*  ON EXIT:                                                                     
*       R0 - REMAINING STRING LENGTH                                            
*       R1 - REMAINING STRING LOCATION                                          
*      @R2 - VARIABLE DESCRIPTOR INFORMATION                                    
*      R15 - CC=ZERO, VALID DESCRIPTOR                                          
*            CC=NZ, INVALID DESCRIPTOR                                          
*                                                                               
*  NOTE: IF ERROR, NO ERROR MESSAGE IS T'SEGD.                                  
*        IF GENERIC ARRAY, VNASUB1 IS SET TO -1                                 
*                                                                               
SCNPDESC XPROC ,                                                                
*                                                                               
         COMMENT                   SCAN VARIABLE NAME                           
         VCALL SCNVNAME                                                         
         IF    NZ,BEGIN                                                         
         IF    (R15,EQ,-4),BEGIN                                                
         TSEG  'Missing vairable name.'                                         
         LA    R15,4                                                            
         END                                                                    
         ELSEIF (R15,EQ,4),BEGIN                                                
         VCALL SEGVNAME                                                         
         TSEG  ': invalid variable name.'                                       
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   VALID VARIABLE NAME, CHECK FOR               
         COMMENT                   SUBSCRIPTS                                   
         ELSE  BEGIN                                                            
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN   SKIP BLANKS                  
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   IF ARRAY,  CHECK IT OUT                      
         IF    ((@R1,EQ,'['),AND,(R0,POS)),BEGIN   FOUND '['                    
*                                                                               
         COMMENT                   CHECK IF FOUND '[]'                          
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         INCR  R4                  SKIP '['                                     
         DECR  R5                                                               
         WHILE ((@R4,EQ,' '),AND,(R0,POS)),BEGIN   SKIP BLANKS                  
         LA    R4,1(R4)                                                         
         DECR  R5                                                               
         END                                                                    
         IF    ((@R4,EQ,']'),AND,(R3,POS)),BEGIN    FOUND '[]'                  
         USING VNENTRY,R2                                                       
         SET   VNADESC             GOT GENERIC ARRAY DESCRIPTOR                 
         DROP  R2                                                               
         LA    R4,1(R4)            SKIP ']'                                     
         DECR  R5                                                               
         LR    R1,R4               AND UPDATE R0,R1                             
         LR    R0,R5                                                            
         END                                                                    
*                                                                               
         ELSE  BEGIN               ELSE, GOT ARRAY ELEMENT                      
         VCALL GETSUBS             GET SUBSCRIPTS                               
         END   , GET SUBS                                                       
         END   , IF SUBS                                                        
         END   , IF VALID NAME                                                  
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EVALPARM - EVALUATE PARMATER                                                 
*                                                                               
*  THIS ROUTINE DOES THE SAME THING AS EVALEXPR, EXCEPT THAT                    
*  IT ALLOWS GENERIC ARRAY DESCRIPTORS TO BE SPECIFIED. IF                      
*  ONE IS SPECFIED THE ARRAY ENTRY IS THE VALUE RETURNED.                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - STRING LENGTH                                                      
*       R1 - STRING LOCATION                                                    
*      @R2 - VALUE RETURN AREA.  SEE VNENTRY DSECT.                             
*                                                                               
*  ON EXIT:                                                                     
*       R0 - REMAINING STRING LENGTH                                            
*       R1 - REMAINING STRING LOCATION                                          
*      @R2 - VALUE OF PARM RETURNED                                             
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, IF ERROR, ERROR MSG T'SEGD.                                 
*                                                                               
*                                                                               
*                                                                               
EVALPARM PROC  ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R2                                                            
*                                                                               
         COMMENT                   CHECK FOR GENERIC ARRAY                      
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         TPUSH ,                   TPUSH-TPOP - JUNK ANY ERR MSG                
         ACALL SCNPDESC                                                         
         TPOP  JUNK                                                             
         IF    ((R15,EQ,0),AND,VNADESC),BEGIN   GOT DESCRIPTOR                  
         XPUSH R0,R1                                                            
         CLEAR R0                                                               
         LR    R1,R6                                                            
         LR    R2,R6                                                            
         VCALL GETVAR              GET ARRAY DESCRIPTOR                         
         XPOP  R0,R1                                                            
         IF    (R15,NZ),BEGIN      ERROR: NOT DECLARED                          
         VCALL SEGVNAME                                                         
         TSEG  ': undefined.'                                                   
         LA    R15,4                                                            
         B     EVLPDONE                                                         
         END                                                                    
         IF    (R15,Z),BEGIN       ERROR: NOT AN ARRAY                          
         IF    ^VNARRAY,BEGIN                                                   
         VCALL SEGVNAME                                                         
         TSEG  ': not DECLARED as array.  Subscripts invalid.'                  
         LA    R15,4                                                            
         B     EVLPDONE                                                         
         END                                                                    
         END                                                                    
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         CLEAR R15                 GOT VALID ARRAY DESCRIPTOR, EXIT             
         B     EVLPDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF NOT GENRIC ARRAY, TREAT                   
         COMMENT                   AS NORMAL EXPRESSION                         
         LR    R1,R4                                                            
         LR    R0,R5                                                            
         VCALL EVALEXPR                                                         
*                                                                               
EVLPDONE LABEL ,                                                                
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'XCALL/PCALL - Parameter Passing Routines'                       
*box                                                                            
*                                                                               
*                                                                               
*  PASSPARM - PASS PARM_COUNT, PARM_STRING PARAMETERS                           
*                                                                               
*  THESE TWO PARMS ARE ALWAYS PASSED, ADDITIONALLY PARM                         
*  LIST PARAMETERS MAY BE PASSED AS WELL. SEE PASSPRMS ROUTINE.                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - PARM STRING LENGTH                                                 
*       R1 - PARM STRING LOCATION                                               
*       R2 - PARM COUNT.  SEE NOTE.                                             
*                                                                               
*  ON EXIT:                                                                     
*       PARMS PASSED.                                                           
*       R15 - CC=ZERO,                                                          
*             ALL ERRORS EXIT VIA CVERROR                                       
*                                                                               
*  NOTE: NAME COUNT IS -1 IF FREE FORM PARAMETER.                               
*        NAME COUNT IS 0 IF NONE.                                               
*        NAME COUNT IS 0 TO N FOR NAMES INCLOSED IN PARENTHESIS.                
*                                                                               
*                                                                               
PPRMWA   RECORD BEGIN                                                           
PPRMLOC  DS    F                   PARM LOC                                     
PPRMLEN  DS    F                   PARM LEN                                     
PPRMCNT  DS    F                   PARM COUNT                                   
PPRMNSET DS    F                   NEW VARIABLE SET #                           
PPRMCSET DS    F                   CURRENT VARIABLE SET #                       
PPRMVAR  DS    XL(L'VPARM)         VARIABLE AREA                                
         END                                                                    
*                                                                               
*                                                                               
PASSPARM PROC  PPRMWA                                                           
         ST    R0,PPRMLEN                                                       
         ST    R1,PPRMLOC                                                       
         ST    R2,PPRMCNT                                                       
*                                                                               
         COMMENT                   GET VARIABLE SET NUMBERS                     
         VCALL GTNEWSET                                                         
         ST    R0,PPRMNSET         NEW VSET NUMBER                              
         L     R0,CPVSET#                                                       
         ST    R0,PPRMCSET         CURRENT VSET NUMBER                          
*                                                                               
         COMMENT                   PASS PARM_COUNT                              
         LA    R2,PPRMVAR                                                       
         VCALL VCLEAR                                                           
         L     R1,PPRMCNT                                                       
         VCALL VINTEGER                                                         
         SYSQS R1,R0,'PARM_COUNT'                                               
         VCALL VNAME                                                            
         LA    R0,1                ALLOW UNDECLARED SAVE                        
         LA    R1,PPRMVAR                                                       
         LA    R2,PPRMVAR                                                       
         MVC   CPVSET#,PPRMNSET                                                 
         VCALL SAVEVAR             SAVE PARM_COUNT                              
         MVC   CPVSET#,PPRMCSET                                                 
         IF    NZ,BEGIN                                                         
         ERROR 'Unable to pass PARM_COUNT parameter.'                           
         END                                                                    
*                                                                               
         COMMENT                   PASS PARM_STRING                             
         LA    R2,PPRMVAR                                                       
         VCALL VCLEAR                                                           
         L     R0,PPRMLEN                                                       
         L     R1,PPRMLOC                                                       
         VCALL VSTRING                                                          
         SYSQS R1,R0,'PARM_STRING'                                              
         VCALL VNAME                                                            
         LA    R0,1                ALLOW UNDECLARED SAVE                        
         LA    R1,PPRMVAR                                                       
         LA    R2,PPRMVAR                                                       
         MVC   CPVSET#,PPRMNSET                                                 
         VCALL SAVEVAR             SAVE PARM_COUNT                              
         MVC   CPVSET#,PPRMCSET                                                 
         IF    NZ,BEGIN                                                         
         ERROR 'Unable to pass PARM_STRING parameter.'                          
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PASSPRMS - PASS PARAMETERS                                                   
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - PARM COUNT                                                         
*       R1 - VALUE LIST                                                         
*       R2 - NAME LIST                                                          
*       R3 - NEW VARIABLE SET NUMBER                                            
*                                                                               
*  ON EXIT:                                                                     
*       PARMS PASSED                                                            
*       R15 - CC=ZERO,                                                          
*             ALL ERRORS EXIT VIA CVERROR                                       
*                                                                               
*  NOTE: VARIABLE SET NUMBER TO WHICH WE PASS THE PARMS IS NOT                  
*        CLEARED BY THIS ROUTINE,  THIS MUST BE DONE EXTERNALLY                 
*        IF NEEDED.                                                             
*                                                                               
*                                                                               
PPWA     RECORD BEGIN                                                           
PPCNT    DS    F                   PARM COUNT                                   
PPNLIST  DS    F                   VALUE LIST ADDRESS                           
PPVLIST  DS    F                   NAME LIST ADDRESS                            
PPNEWSET DS    F                   NEW VARIABLE SET #                           
PPCURSET DS    F                   CURRENT VARIABLE SET #                       
PPNCNT   DS    F                   VALUE COUNT                                  
PPVCNT   DS    F                   NAME COUNT                                   
         END                                                                    
*                                                                               
*                                                                               
PASSPRMS PROC  PPWA                                                             
         ST    R0,PPCNT                                                         
         ST    R1,PPVLIST                                                       
         ST    R2,PPNLIST                                                       
         ST    R3,PPNEWSET                                                      
*                                                                               
         COMMENT                   IF PARMS TO PASS, PASS THEM                  
         IF    (R0,POS),BEGIN                                                   
         L     R3,CPVSET#                                                       
         ST    R3,PPCURSET         CURRENT VARIABLE SET NUMBER                  
*                                                                               
         COMMENT                   CALCULATE ACTUAL VALUE COUNT                 
         USING VNENTRY,R1                                                       
         L     R1,PPVLIST          R1 - LIST INDEX                              
         L     R2,PPVLIST          R2 - LIST END                                
         A     R2,=AL4(L'VLIST)    R3 - LIST COUNTER                            
         CLEAR R3                                                               
         WHILE ((R1,LT,R2),AND,(VNENTRY,NE,0)),BEGIN                            
         LA    R1,L'VPARM(R1)                                                   
         LA    R3,1(R3)                                                         
         END                                                                    
         ST    R3,PPVCNT                                                        
         DROP  R1                                                               
*                                                                               
         COMMENT                   CALCULATE ACTUAL NAME COUNT                  
         USING VNENTRY,R1                                                       
         L     R1,PPNLIST          R1 - LIST INDEX                              
         L     R2,PPNLIST          R2 - LIST END                                
         A     R2,=AL4(L'NLIST)    R3 - LIST COUNTER                            
         CLEAR R3                                                               
         WHILE ((R1,LT,R2),AND,(VNDESC,NE,0)),BEGIN                             
         LA    R1,L'VNDESC(R1)                                                  
         LA    R3,1(R3)                                                         
         END                                                                    
         ST    R3,PPNCNT                                                        
         DROP  R1                                                               
*                                                                               
         COMMENT                   CHECK VALUE/NAME COUNTS                      
         IF    (PPCNT,NE,PPVCNT),BEGIN                                          
         BOMB                                                                   
         END                                                                    
         IF    (PPVCNT,GT,PPNCNT),BEGIN                                         
         ERROR 'Too many parameters specified on PCALL/XCALL.'                  
         END                                                                    
*-                                                                              
*-       LOOP PASSING PARMS                                                     
*-                                                                              
         COMMENT                   PASS PARMS,,, (IE. SAVE VALUES)              
         COMMENT                                                                
         LA    R0,1                SET UP SAVE ROUTINE PARMS                    
         L     R1,PPVLIST            (R0=1 ALLOWS UNDECLARED SAVE)              
         L     R2,PPNLIST                                                       
         LA    R3,1                SET UP LOOP COUNTERS                         
         L     R4,PPCNT                                                         
         WHILE (R3,LE,R4),BEGIN    LOOP SAVING VALUES                           
*                                                                               
         COMMENT                   CHECK ARRAY/VAR COMPATIBLITY                 
         COMMENT                   ARRAYS MUST BE PASSED ARRAYS                 
         COMMENT                                                                
         CLEAR R5                  ASSUME NOT ARRAY PARMS                       
         USING VNENTRY,R1                                                       
         IF    VNARRAY,' LA R5,1(R5) '                                          
         DROP  R1                                                               
         USING VNENTRY,R2                                                       
         IF    VNADESC,' LA R5,1(R5) '                                          
         DROP  R2                                                               
         IF    (R5,EQ,1),BEGIN     DIG THIS !                                   
         ERROR 'Parameter mismatch. Arrays must be passed arrays.'              
         END                                                                    
*                                                                               
         COMMENT                   SAVE PARAMETER                               
         MVC   CPVSET#,PPNEWSET                                                 
         VCALL SAVEVAR                                                          
         MVC   CPVSET#,PPCURSET                                                 
         IF    NZ,BEGIN                                                         
         ERROR 'Unable to pass PCALL/XCALL parameter.'                          
         END                                                                    
*                                                                               
         COMMENT                   LOOP PASSING PARMS                           
         LA    R1,L'VPARM(R1)                                                   
         LA    R2,L'VNDESC(R2)                                                  
         LA    R3,1(R3)                                                         
         END   ,                   OF LOOP                                      
         END   ,                   OF IF PARMS                                  
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  RETPARMS - RETURN PASSED PARAMETERS                                          
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - PARM COUNT                                                         
*       R1 - RETURN NAME LIST (AS KNOWN TO CALLER)                              
*       R2 - RETURN VALUE LIST                                                  
*       R3 - RETURN NAME LIST (AS KNOWN WITHIN PROCEDURE)                       
*       R4 - RETURN VARIABLES SET NUMBER                                        
*       R5 - RETURN XPROC VARIABLE SET NUMBER                                   
*                                                                               
*  ON EXIT:                                                                     
*       VARIABLES RETURNED ,,,                                                  
*       ALL ERRORS EXIT VIA CVERROR                                             
*                                                                               
*                                                                               
RPWA     RECORD BEGIN                                                           
RPFLAGS  FLAG                                                                   
         FLAG  RPERROR             RETURN PARM ERRORS                           
RPCNT    DS    F                   RETURN PARM COUNT                            
RPVLIST  DS    F                   RETURN VALUE LIST                            
RPRLIST  DS    F                   RETURN NAME LIST                             
RPPRLIST DS    F                   RETURN NAME LIST (AS KNOWN IN PROC)          
RPRTSET  DS    F                   RETURN VARIABLE SET NUMBER                   
RPCRSET  DS    F                   CURRENT VARIABLE SET NUMBER                  
RPRTXSET DS    F                   RETURN XPROC VARIABLE SET                    
RPCRXSET DS    F                   CURRENT XPROC VARIABLE SET                   
         END                                                                    
*                                                                               
*                                                                               
RETPARMS PROC  RPWA                                                             
         ST    R0,RPCNT                                                         
         ST    R1,RPVLIST                                                       
         ST    R2,RPRLIST                                                       
         ST    R3,RPPRLIST                                                      
         ST    R4,RPRTSET                                                       
         L     R4,CPVSET#                                                       
         ST    R4,RPCRSET                                                       
         ST    R5,RPRTXSET                                                      
         L     R5,CPXVSET#                                                      
         ST    R5,RPCRXSET                                                      
         CLEAR RPERROR                                                          
*                                                                               
         COMMENT                   IF PARMS TO RETURN, RETURN THEM              
         IF    (R0,POS),BEGIN                                                   
*-                                                                              
*-       LOOP RETURNING PARMS                                                   
*-                                                                              
         COMMENT                   RETURN PARMS,,, (IE SAVE VALUES)             
         COMMENT                                                                
         USING VNENTRY,R2                                                       
         CLEAR R0                  VARIABLE MUST BE DECLARED                    
         L     R1,RPVLIST          SAVE VALUE                                   
         L     R2,RPRLIST          SAVE NAME (FROM CALLER)                      
         L     R3,RPPRLIST         VAR NAME (USED WITHIN PROC)                  
         LA    R4,1                SET UP LOOP COUNTERS                         
         L     R5,RPCNT                                                         
         WHILE (R4,LE,R5),BEGIN    LOOP SAVING VALUES                           
         IF    (VNDESC,EQ,0),BEGIN                                              
         SET   RPERROR                                                          
         ACALL BADPRETN                                                         
         END                                                                    
         ELSEIF VNADESC,BEGIN                                                   
         SET   RPERROR                                                          
         ACALL BADPRETN                                                         
*$ UNABLE TO RETURN ARRAY ARGUMENT !!  DO WE WANT SEPARATE ERROR                
*$ MESSAGE FOR ARRAYS,,, NEED ALSO TO FIX SEGVAR FOR ARRAY ELEMENTS             
*$ AND ARRAYS, NO?  DO IT TO IT !!!                                             
         END                                                                    
         ELSE  BEGIN ,             SAVE VARIABLE                                
         MVC   CPVSET#,RPRTSET                                                  
         MVC   CPXVSET#,RPRTXSET                                                
         VCALL SAVEVAR                                                          
         MVC   CPVSET#,RPCRSET                                                  
         MVC   CPXVSET#,RPCRXSET                                                
         IF    NZ,BEGIN                                                         
         SET   RPERROR                                                          
         ACALL BADPRETN                                                         
         END                                                                    
         END                                                                    
         LA    R1,L'VPARM(R1)                                                   
         LA    R2,L'VNDESC(R2)                                                  
         LA    R3,L'VNDESC(R3)                                                  
         LA    R4,1(R4)                                                         
         END   ,                   OF LOOP                                      
         DROP  R2                                                               
*                                                                               
         COMMENT                   IF ERRORS,                                   
         IF    RPERROR,BEGIN                                                    
         B     CVERROR             EXIT VIA CVERROR                             
         END                                                                    
         END   ,                   OF IF PARMS                                  
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  RETSYSP - RETURN SYSTEM PROCEDURE PARMS                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - PARM COUNT                                                         
*       R1 - RETURN NAME LIST (AS KNOWN TO CALLER)                              
*       R2 - RETURN VALUE LIST                                                  
*                                                                               
*  ON EXIT:                                                                     
*       VARIABLES RETURNED ,,,                                                  
*       ALL ERRORS EXIT VIA CVERROR                                             
*                                                                               
*                                                                               
RSPWA    RECORD BEGIN                                                           
RSPFLAGS FLAG                                                                   
         FLAG  RSPERROR            RETURN PARM ERRORS                           
RSPCNT   DS    F                   RETURN PARM COUNT                            
RSPVLIST DS    F                   RETURN VALUE LIST                            
RSPRLIST DS    F                   RETURN NAME LIST                             
         END                                                                    
*                                                                               
*                                                                               
RETSYSP  PROC  RSPWA                                                            
         ST    R0,RSPCNT                                                        
         ST    R1,RSPVLIST                                                      
         ST    R2,RSPRLIST                                                      
         CLEAR RSPERROR                                                         
*                                                                               
         COMMENT                   IF PARMS TO RETURN, RETURN THEM              
         IF    (R0,POS),BEGIN                                                   
*-                                                                              
*-       LOOP RETURNING PARMS                                                   
*-                                                                              
         COMMENT                   RETURN PARMS,,, (IE SAVE VALUES)             
         COMMENT                                                                
         CLEAR R0                  VARIABLE MUST BE DECLARED                    
         L     R1,RSPVLIST         SAVE VALUE                                   
         L     R2,RSPRLIST         SAVE NAME (FROM CALLER)                      
         LA    R3,1                PARM NUMBER (USED IF ERROR)                  
         LA    R4,1                SET UP LOOP COUNTERS                         
         L     R5,RSPCNT                                                        
         COMMENT                   LOOP SAVING VALUES                           
         WHILE (R4,LE,R5),BEGIN                                                 
*                                                                               
         COMMENT                   IF VALUE TO RETURN, RETURN IT                
         USING VNENTRY,R1          VNENTRY IS VALUE                             
         IF    (VNTYPE,NE,0),BEGIN                                              
         DROP  R1                                                               
         USING VNENTRY,R2          VNENTRY IS NAME                              
         IF    (VNDESC,EQ,0),BEGIN IF NO NAME, CAN'T RETURN                     
         SET   RSPERROR            (EG. IF CONSTANT, CAN'T RETURN)              
         ACALL BADSYSP                                                          
         END                                                                    
         ELSEIF VNADESC,BEGIN      IF ARRAY, CAN'T RETURN                       
         SET   RSPERROR                                                         
         ACALL BADSYSP                                                          
         END                                                                    
         ELSE  BEGIN ,             SAVE VARIABLE                                
         VCALL SAVEVAR                                                          
         IF    NZ,BEGIN                                                         
         SET   RSPERROR                                                         
         ACALL BADSYSP                                                          
         END                                                                    
         END                                                                    
         DROP  R2                                                               
         END                                                                    
*                                                                               
         LA    R1,L'VPARM(R1)                                                   
         LA    R2,L'VNDESC(R2)                                                  
         LA    R3,1(R3)                                                         
         LA    R4,1(R4)                                                         
         END   ,                   OF LOOP                                      
*                                                                               
         COMMENT                   IF ERRORS,                                   
         IF    RSPERROR,BEGIN                                                   
         B     CVERROR             EXIT VIA CVERROR                             
         END                                                                    
         END   ,                   OF IF PARMS                                  
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'XCALL/PCALL - EXEC/Procedure Initiator Routines'                
*box                                                                            
*                                                                               
*                                                                               
*  START - START EXEC FILE                                                      
*                                                                               
*  THIS ROUTINE IS USED TO START EXEC FILES.  IT IS CALLED                      
*  FROM XCALL/PCALL ROUTINES.  IT IS ALSO CALLED TO RESTART                     
*  EXECS WHEN A PROC RETURN OR AN XPROC XRETURN IS ISSUED.                      
*  THIS ROUTINE DOES NOT RETURN.                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - EXEC STATE INFORMATION DSECT                                       
*                                                                               
*                                                                               
START    PROC  ,                                                                
         USING SNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   COPY STATE INFORMATION TO CP                 
         CLEAR CPFEXEC                                                          
         IF    SNFEXEC,' SET CPFEXEC '                                          
         MVC   CPEXLINE,SNEXLINE                                                
         MVC   CPEXNEXT,SNEXNEXT                                                
         MVC   CPSCOPE,SNSCOPE                                                  
         MVC   CPSCOPD,SNSCOPD                                                  
         MVC   CPXSCOPE,SNXSCOPE                                                
         MVC   CPXSCOPD,SNXSCOPD                                                
         MVC   CP#XSET,SN#XSET                                                  
         MVC   CPRGEXEC,SNRGEXEC                                                
         MVC   CPRGFLOW,SNRGFLOW                                                
         MVC   CPVSET#,SNVSET#                                                  
         MVC   CPXVSET#,SNXVSET#                                                
         MVC   CPESCAPE,SNESCAPE                                                
         MVC   CPSKIP,SNSKIP                                                    
         MVC   CPPROCNM,SNPROCNM                                                
         MVC   CPXPUSH,SNXPUSH                                                  
         CLEAR CPFXTEND                                                         
         CLEAR CPFXOLD                                                          
         CLEAR CPFXAUTHLIB                                                      
         CLEAR CPFXAUTH                                                         
         CLEAR CPFTRY                                                           
         CLEAR CPFQUIET                                                         
         CLEAR CPFXQT                                                           
         CLEAR CPFXDUMP                                                         
         CLEAR CPFVCMD                                                          
         CLEAR CPFTYPED                                                         
         IF    SNFXTEND,' SET CPFXTEND '                                        
         IF    SNFXOLD,'  SET CPFXOLD '                                         
         IF    SNFXAUTHLIB,' SET CPFXAUTHLIB '                                  
         IF    SNFXAUTH,' SET CPFXAUTH '                                        
         IF    SNFTRY,'   SET CPFTRY  '                                         
         IF    SNFQUIET,' SET CPFQUIET '                                        
         IF    SNFXQT,' SET CPFXQT '                                            
         IF    SNFXDUMP,' SET CPFXDUMP '                                        
         IF    SNFVCMD,' SET CPFVCMD '                                          
         IF    SNFTYPED,' SET CPFTYPED '                                        
*                                                                               
         COMMENT                   NEW STATE INFORMATION IN CP                  
         COMMENT                   START UP EXEC BY GETTING NEXT CMD            
         B     CVNEXT                                                           
*                                                                               
         COMMENT                   DOES NOT RETURN !!                           
         BOMB                                                                   
         CLEAR R15                                                              
         PEND  ,          THIS EXIT NEVER TAKEN,                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XRETXCMD -  XRETURN CMD=                                                     
*                                                                               
*  THIS ROUTINE IS A SPECIAL CASE OF THE START ROUTINE FOUND                    
*  ABOVE, THIS SPECIAL CASE ROUTINE IS USED TO RETURN FROM                      
*  XPROC (XRETURN) AND ISSUE A XCOMMAND AT THE COMPLETION                       
*  OF THE RETURN,,, KIND OF A YUCKKY THING FROM A PROGRAMING                    
*  LANGUAGE POINT OF VIEW, DYNAMIC AND STATIC BEHAVIOR, BEING                   
*  TOTALLY AT ODDS !!                                                           
*  ANYWAY WE CONTINUE WITH ORIGINAL COMMENTS:                                   
*                                                                               
*  THIS ROUTINE IS USED TO START EXEC FILES.  IT IS CALLED                      
*  FROM XCALL/PCALL ROUTINES.  IT IS ALSO CALLED TO RESTART                     
*  EXECS WHEN A PROC RETURN OR AN XPROC XRETURN IS ISSUED.                      
*  THIS ROUTINE DOES NOT RETURN.                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - EXEC STATE INFORMATION DSECT                                       
*                                                                               
*                                                                               
XRETXCMD PROC  ,                                                                
         USING SNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   COPY STATE INFORMATION TO CP                 
         CLEAR CPFEXEC                                                          
         IF    SNFEXEC,' SET CPFEXEC '                                          
         MVC   CPEXLINE,SNEXLINE                                                
         MVC   CPEXNEXT,SNEXNEXT                                                
         MVC   CPSCOPE,SNSCOPE                                                  
         MVC   CPSCOPD,SNSCOPD                                                  
         MVC   CPXSCOPE,SNXSCOPE                                                
         MVC   CPXSCOPD,SNXSCOPD                                                
         MVC   CP#XSET,SN#XSET                                                  
         MVC   CPRGEXEC,SNRGEXEC                                                
         MVC   CPRGFLOW,SNRGFLOW                                                
         MVC   CPVSET#,SNVSET#                                                  
         MVC   CPXVSET#,SNXVSET#                                                
         MVC   CPESCAPE,SNESCAPE                                                
         MVC   CPSKIP,SNSKIP                                                    
         MVC   CPPROCNM,SNPROCNM                                                
         MVC   CPXPUSH,SNXPUSH                                                  
         CLEAR CPFXTEND                                                         
         CLEAR CPFXOLD                                                          
         CLEAR CPFXAUTHLIB                                                      
         CLEAR CPFXAUTH                                                         
         CLEAR CPFTRY                                                           
         CLEAR CPFQUIET                                                         
         CLEAR CPFXQT                                                           
         CLEAR CPFXDUMP                                                         
         CLEAR CPFVCMD                                                          
         CLEAR CPFTYPED                                                         
         IF    SNFXTEND,' SET CPFXTEND '                                        
         IF    SNFXOLD,'  SET CPFXOLD '                                         
         IF    SNFXAUTHLIB,' SET CPFXAUTHLIB '                                  
         IF    SNFXAUTH,' SET CPFXAUTH '                                        
         IF    SNFTRY,'   SET CPFTRY  '                                         
         IF    SNFQUIET,' SET CPFQUIET '                                        
         IF    SNFXQT,' SET CPFXQT '                                            
         IF    SNFXDUMP,' SET CPFXDUMP '                                        
         IF    SNFVCMD,' SET CPFVCMD '                                          
         IF    SNFTYPED,' SET CPFTYPED '                                        
*                                                                               
         COMMENT                   ISSUE XCOMMAND COMMAND                       
         CLEAR CPFTYPED                                                         
         SETMSG CPCMD,LH:CPCMDLEN                                               
         VCALL EDTCOM              -->> DO COMMAND, DOES NOT RETURN             
*                                                                               
         COMMENT                   DOES NOT RETURN !!                           
         BOMB                                                                   
         COMMENT                   NOTE: NEW STATE INFORMATION IN CP            
         COMMENT                   AFTER XCOMMAND XRETURN WILL RETURN           
         COMMENT                   BY JUST GETTING NEXT COMMAND                 
*                                                                               
         CLEAR R15                                                              
         PEND  ,          THIS EXIT NEVER TAKEN,                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETSTATE - GET CURRENT EXEC STATE INFO                                       
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC STATE INFO DSECT                                              
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - EXEC STATE INFO AREA RETURNED                                      
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
*                                                                               
GETSTATE XPROC ,                                                                
         USING SNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         CLEAR SNENTRY                                                          
         IF    CPFEXEC,' SET SNFEXEC '                                          
         MVC   SNEXLINE,CPEXLINE                                                
         MVC   SNEXNEXT,CPEXNEXT                                                
         MVC   SNSCOPE,CPSCOPE                                                  
         MVC   SNSCOPD,CPSCOPD                                                  
         MVC   SNXSCOPE,CPXSCOPE                                                
         MVC   SNXSCOPD,CPXSCOPD                                                
         MVC   SN#XSET,CP#XSET                                                  
         MVC   SNRGEXEC,CPRGEXEC                                                
         MVC   SNRGFLOW,CPRGFLOW                                                
         MVC   SNVSET#,CPVSET#                                                  
         MVC   SNXVSET#,CPXVSET#                                                
         MVC   SNESCAPE,CPESCAPE                                                
         MVC   SNSKIP,CPSKIP                                                    
         MVC   SNPROCNM,CPPROCNM                                                
         MVC   SNXPUSH,CPXPUSH                                                  
         IF    CPFXOLD,' SET SNFXOLD '                                          
         IF    CPFXTEND,' SET SNFXTEND '                                        
         IF    CPFXAUTHLIB,' SET SNFXAUTHLIB '                                  
         IF    CPFXAUTH,' SET SNFXAUTH '                                        
         IF    CPFTRY,' SET SNFTRY '                                            
         IF    CPFQUIET,' SET SNFQUIET '                                        
         IF    CPFXQT,' SET SNFXQT '                                            
         IF    CPFXDUMP,' SET SNFXDUMP '                                        
         IF    CPFVCMD,' SET SNFVCMD '                                          
         IF    CPFTYPED,' SET SNFTYPED '                                        
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTXSTART - GET STARTING STATE INFOMATION FOR XCALLED EXEC                    
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - NEW EXEC SET NUMBER                                                
*       R1 - POINTER TO EXEC RECORD GROUP                                       
*       R2 - POINTER TO FLOW RECORD GROUP                                       
*       R3 - POINTER TO EXEC STATE INFO DSECT                                   
*                                                                               
*  ON EXIT:                                                                     
*      @R3 - STARTING STATE INFO FOR EXEC RETURNED                              
*                                                                               
*                                                                               
GTXWA    RECORD BEGIN                                                           
GTX#XSET DS    F                   NEW EXEC SET NUMBER                          
GTXGEXEC DS    F                   POINTER TO NEW EXEC RECD GROUP               
GTXGFLOW DS    F                   POINTER TO NEW FLOW RECD GROUP               
GTXSCOPE DS    F                   START OF NEW EXEC SCOPE                      
GTXSCOPD DS    F                   END OF NEW EXEC SCOPE                        
GTXVSET# DS    F                   NEW VARIABLE STACK SET NUMBER                
GTXFLAG  FLAG  ,                                                                
         FLAG  GTXAUTHLIB          AUTHORIZED EXEC                              
         END                                                                    
*                                                                               
*                                                                               
GTXSTART PROC  GTXWA                                                            
         USING SNENTRY,R6                                                       
         LR    R6,R3                                                            
         ST    R0,GTX#XSET                                                      
         ST    R1,GTXGEXEC                                                      
         ST    R2,GTXGFLOW                                                      
*                                                                               
         COMMENT                   GET EXEC SCOPE (START/END)                   
         L     R0,GTX#XSET                                                      
         L     R1,GTXGFLOW                                                      
         VCALL GTXSCOPE                                                         
         ST    R0,GTXSCOPE                                                      
         ST    R1,GTXSCOPD                                                      
         VCALL GTNEWSET            GET NEW VARIABLE SET #                       
         ST    R0,GTXVSET#                                                      
*                                                                               
         COMMENT                   CHECK FOR AUTHORIZED EXEC                    
         L     R5,CPFINFOP                                                      
         IF    (R5,Z),' BOMB '                                                  
         L     R0,FFRNAMEL-FFINFO(R5)   R1,R0 - FULL NAME LOC,LEN               
         LA    R1,FFRNAME-FFINFO(R5)                                            
         IF    ((R0,GE,11),AND,(@R1,EQ,'WYL.GS.WYL.')),BEGIN                    
         SET   GTXAUTHLIB                                                       
         END                                                                    
*                                                                               
         COMMENT                   SET STARTING VALUES FOR                      
         COMMENT                   NEW EXEC FILE                                
         CLEAR SNENTRY                                                          
         SET   SNFEXEC             START IN EXEC MODE ... ETC.                  
         MVC   SNEXLINE,GTXSCOPE                                                
         MVC   SNEXNEXT,=CL4'NEXT'                                              
         MVC   SNSCOPE,GTXSCOPE                                                 
         MVC   SNSCOPD,GTXSCOPD                                                 
         MVC   SNXSCOPE,GTXSCOPE                                                
         MVC   SNXSCOPD,GTXSCOPD                                                
         MVC   SN#XSET,GTX#XSET                                                 
         MVC   SNRGEXEC,GTXGEXEC                                                
         MVC   SNRGFLOW,GTXGFLOW                                                
         MVC   SNVSET#,GTXVSET#                                                 
         MVC   SNXVSET#,GTXVSET#                                                
         MVI   SNESCAPE,C'&&'                                                   
         MVI   SNSKIP,0                                                         
         MVC   SNPROCNM,=CL(L'SNPROCNM)'XPROC '                                 
         CLEAR SNXPUSH                                                          
         SET   SNFXTEND                                                         
         IF    GTXAUTHLIB,BEGIN    We are coming from an auth lib...            
         SET   SNFXAUTHLIB         We can be authorized                         
         IF    CPFXAUTH,'SET SNFXAUTH'  We retain existing auth                 
         END                                                                    
         IF    CPFQUIET,' SET SNFXQT '                                          
         IF    CPFXQT,' SET SNFXQT '                                            
         IF    CPFXDUMP,' SET SNFXDUMP '                                        
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTPSTART - GET STARTING STATE INFOMATION FOR PCALLED PROC                    
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - POINTER TO PROCEDURE NAME                                          
*       R3 - POINTER TO EXEC STATE INFO DSECT                                   
*                                                                               
*  ON EXIT:                                                                     
*      @R3 - STARTING STATE INFO FOR EXEC RETURNED                              
*                                                                               
*                                                                               
GTPWA    RECORD BEGIN                                                           
GTPSCOPE DS    F                   START OF NEW EXEC SCOPE                      
GTPSCOPD DS    F                   END OF NEW EXEC SCOPE                        
GTPVSET# DS    F                   NEW VARIABLE STACK SET NUMBER                
         END                                                                    
*                                                                               
*                                                                               
GTPSTART PROC  GTPWA                                                            
         USING SNENTRY,R6                                                       
         LR    R6,R3                                                            
         LR    R5,R1               R5 - PROC NAME                               
*                                                                               
         COMMENT                   GET EXEC SCOPE (START/END)                   
         VCALL GTPSCOPE                                                         
         ST    R0,GTPSCOPE                                                      
         ST    R1,GTPSCOPD                                                      
         VCALL GTNEWSET            GET NEW VARIABLE SET #                       
         ST    R0,GTPVSET#                                                      
*                                                                               
         COMMENT                   SET STARTING VALUES FOR                      
         COMMENT                   NEW EXEC FILE                                
         CLEAR SNENTRY                                                          
         SET   SNFEXEC             START IN EXEC MODE ... ETC.                  
         MVC   SNEXLINE,GTPSCOPE                                                
         MVC   SNEXNEXT,=CL4'NEXT'                                              
         MVC   SNSCOPE,GTPSCOPE                                                 
         MVC   SNSCOPD,GTPSCOPD                                                 
         MVC   SNXSCOPE,CPXSCOPE                                                
         MVC   SNXSCOPD,CPXSCOPD                                                
         MVC   SN#XSET,CP#XSET                                                  
         MVC   SNRGEXEC,CPRGEXEC                                                
         MVC   SNRGFLOW,CPRGFLOW                                                
         MVC   SNVSET#,GTPVSET#                                                 
         MVC   SNXVSET#,CPXVSET#                                                
         MVC   SNESCAPE,CPESCAPE                                                
         MVC   SNSKIP,CPSKIP                                                    
         MVC   SNPROCNM,0(R5)                                                   
         MVC   SNXPUSH,CPXPUSH                                                  
         SET   SNFXTEND                                                         
         IF    CPFXAUTHLIB,' SET SNFXAUTHLIB '                                  
         IF    CPFXAUTH,' SET SNFXAUTH '                                        
         IF    CPFQUIET,' SET SNFXQT '                                          
         IF    CPFXQT,' SET SNFXQT '                                            
         IF    CPFXDUMP,' SET SNFXDUMP '                                        
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SETPRET - RESET SOME PCALL RETURN STATE INFO                                 
*                                                                               
*  THIS ROUTINE, RESETS SOME STATE INFO FOR PCALL RETURNS.                      
*  FOR EXAMPLE, ESC AND SKIP ARE SET AND RESTORED ON                            
*  AN XPROC LEVEL.  SO ON RETURN FROM A PCALL WE MUST                           
*  NOT RESET ESC AND SKIP CHARACTERS.  THIS ALLOWS THE                          
*  EXEC PROGRAMMER TO USE A PROC TO INIT STUFF INCLUDING                        
*  SKIP AND ESCAPE.                                                             
*                                                                               
*                                                                               
SETPRET  PROC  ,                                                                
         USING SNENTRY,R1                                                       
         MVC   SNESCAPE,CPESCAPE                                                
         MVC   SNSKIP,CPSKIP                                                    
         CLEAR SNFXDUMP                                                         
         IF    CPFXDUMP,' SET SNFXDUMP '                                        
         CLEAR SNFXQT                                                           
         IF    CPFXQT,' SET SNFXQT '                                            
         PEND  ,                                                                
         DROP  R1                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Error Routines'                                                 
*box                                                                            
*                                                                               
*                                                                               
*  MANYPARM - TOO MANY PARAMETERS                                               
*                                                                               
*                                                                               
*                                                                               
MANYPARM PROC  ,                                                                
         TSEG   'Too many parameters specified.'                                
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PARMERR - INVALID PARAMETER SPECIFIED                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R3 - PARM NUMBER                                                        
*                                                                               
*                                                                               
PARMERR  PROC  ,                                                                
         TCCR                                                                   
         TSEG  'Parameter #'                                                    
         TNUM  (R3)                                                             
         TSEG  ' is invalid.'                       NO RETURN                   
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BADPRETN - UNABLE TO RETURN PARM                                             
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - VALUE                                                             
*       @R3 - NAME OF VARIABLE ATTEMPTED TO RETURN                              
*                                                                               
*                                                                               
BPRWA    RECORD BEGIN                                                           
BPRVAR   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
BADPRETN PROC  BPRWA                                                            
         LA    R0,BPRVAR                                                        
         LR    R4,R1                                                            
         LA    R1,L'VPARM                                                       
         LR    R5,R1                                                            
         MVCL  R0,R4               COPY VALUE                                   
         LA    R0,BPRVAR                                                        
         LA    R1,L'VNDESC                                                      
         LR    R4,R3                                                            
         LR    R5,R1                                                            
         MVCL  R0,R4               COPY NAME                                    
         TCCR                                                                   
         TSEG  'Unable to return parameter:  '                                  
         LA    R2,BPRVAR                                                        
         VCALL SEGVAR                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BADSYSP - UNABLE TO RETURN SYSTEM PARM                                       
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - VALUE                                                             
*       @R3 - NUMBER OF PARMAMETER ATTEMPTED TO RETURN                          
*                                                                               
*                                                                               
BSPWA    RECORD BEGIN                                                           
BSPVAR   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
BADSYSP  PROC  BSPWA                                                            
         LA    R0,BSPVAR                                                        
         LR    R4,R1                                                            
         LA    R1,L'VPARM                                                       
         LR    R5,R1                                                            
         MVCL  R0,R4               COPY VALUE                                   
         TCCR                                                                   
         TSEG  'Unable to return parameter: RETURN_PARM'                        
         TNUM  (R3)                                                             
         TSEG  ' = '                                                            
         LA    R2,BSPVAR                                                        
         VCALL SEGVALUE                                                         
         PEND  ,                                                                
         TITLE 'Common Routines'                                                
*box                                                                            
*                                                                               
*                                                                               
*  SETXNAME - SET XFXNAME                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       FFINFO (CPFINFOP) DSECT CONTAINS FULL NAME IN FFRNAME                   
*       @R1 - XDSNAME DSECT AREA                                                
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*                                                                               
*  THIS ROUTINE EXISTS JUST TO INSURE THAT XDSNAME DSECT IS SET                 
*  CONSISTENTLY,  ALL BYTES ARE BLANKED, NOT ZEROED.  SEE XDSNAME               
*  DSECT FOR DETAILS.                                                           
*                                                                               
*                                                                               
SETXNAME XPROC ,                                                                
*                                                                               
         L     R6,CPFINFOP                                                      
         IF    (R6,Z),' BOMB '                                                  
         USING FFINFO,R6                                                        
         LR    R5,R1                                                            
         USING XDSNAME,R5                                                       
*                                                                               
         COMMENT                   CHECK NAME FOR LENGTH                        
         IF    (FFRNAMEL,GT,L'XNAME),BEGIN                                      
         ERROR 'Exec file name too long. '                                      
         END                                                                    
         MVC   XDSNAME,CVBLANKS                                                 
         L     R3,FFRNAMEL                                                      
         MOVE  R3,XNAME,FFRNAME                                                 
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  MRKNAME - MARK NAME WITH UNIQUE TAG FOR PUBLOAD, RELOAD                      
*                                                                               
*  ON ENTRY:                                                                    
*       @R2 - XFENTRY                                                           
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*                                                                               
*  EXPLANATION:  IF WE RELOAD A PUB EXEC WE NEED TO CHANGE THE                  
*  NAME OF THE OLD EXEC.  THIS WAY IT WILL STILL BE THERE FOR                   
*  PEOPLE WHO HAVE POINTERS TO IT.  AND NEW CALLERS CAN GET THE                 
*  NEW VERSION.  WE MARK THE NAME WITH THE SET # OF THE EXEC,                   
*  THIS NUMBER CREATES A UNIQUE NAME FOR EACH RELOADED EXEC,                    
*  ALSO IT PLACED IN SUCH A LOCATION AS TO NOT BE PRINTED OUT                   
*  BY THE SEGXDSN ROUTINE.                                                      
*                                                                               
*                                                                               
MRKNAME  XPROC ,                                                                
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LR    R6,R2                                                            
         LA    R5,XFNAME                                                        
*                                                                               
         COMMENT                   MARK NAME WITH EXEC SET# IN HEX              
         L     R1,XF#SETNO                                                      
         ST    R1,XMRK                                                          
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  RSETNAME - CLEAR MARK USED FOR PUBLOAD TO MAKE NAME TEMP                     
*                                                                               
*  ON ENTRY:                                                                    
*       @R2 - XFENTRY                                                           
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
RSETNAME XPROC ,                                                                
         USING XFENTRY,R6                                                       
         USING XDSNAME,R5                                                       
         LR    R6,R2                                                            
         LA    R5,XFNAME                                                        
*                                                                               
         COMMENT                   CLEAR MARK WITH BLANKS                       
         MVC   XMRK,CVBLANKS                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         TITLE 'SEG EXEC Info '                                                 
*box                                                                            
*                                                                               
*                                                                               
*  SEGXLINE - OUTPUT EXEC LINE                                                  
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*      @R1 - EXEC RGROUP INFO                                                   
*      @R2 - EXEC LINE NUMBER                                                   
*                                                                               
*                                                                               
SGLNWA   RECORD BEGIN                                                           
SGLNLEN  DS    F                   EXEC LINE LENGTH                             
SGLNLINE DS    XL(&LINESZ)         EXEC LINE TEXT                               
         END                                                                    
*                                                                               
*                                                                               
SEGXLINE XPROC SGLNWA                                                           
*                                                                               
         COMMENT                   PUT CR IF NEEDED                             
         XPUSH R0,R1                                                            
         TCCR                                                                   
         XPOP  R0,R1                                                            
*                                                                               
         COMMENT                   GET EXEC LINE                                
*$       LR    R15,R1              MAYBE SOMEDAY NEED EXEC POINTER              
         COMMENT R0-EXEC SET#                                                   
         LA    R1,SGLNLINE                                                      
         COMMENT @R2-LINENO                                                     
         VCALL EXSPGET                                                          
         BNZ   SGLNDONE            IF LINE DOES NOT EXIST, CC=NZ                
         ST    R0,SGLNLEN                                                       
*                                                                               
         COMMENT                   SEG LINE NUMBER, LINE TEXT                   
         L     R0,0(R2)                                                         
         VCALL CVEXNO                                                           
         TSEG  CPDOUB,9                                                         
         TSEG  ' '                                                              
         L     R0,SGLNLEN                                                       
         LA    R1,SGLNLINE                                                      
         TSEG  (R1),(R0),CR                                                     
         CLEAR R15                                                              
*                                                                               
SGLNDONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGXLNO - OUTPUT EXEC LINE NUMBER AND EXEC FILE                              
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*      @R1 - EXEC RGROUP INFO                                                   
*      @R2 - EXEC LINE NUMBER                                                   
*                                                                               
*  NOTE: WE DO NOT CHECK TO SEE IF LINE EXISTS.                                 
*                                                                               
*                                                                               
SGXLWA   RECORD BEGIN                                                           
SGXLSET  DS    F                   EXEC SET NUMBER                              
SGXLRG   DS    F                   EXEC RGROUP NUMBER                           
SGXLLPTR DS    F                   EXEC LINE NUMBER POINTER                     
SGXLLINE DS    XL(&LINESZ)         EXEC LINE TEXT AREA                          
         END                                                                    
*                                                                               
*                                                                               
SEGXLNO  XPROC SGXLWA                                                           
         ST    R0,SGXLSET                                                       
         ST    R1,SGXLRG                                                        
         ST    R2,SGXLLPTR                                                      
*                                                                               
         COMMENT                   IF VALID LINE NUMBER, SEG IT                 
         L     R0,0(R2)            R0 - LINE NUMBER                             
         IF    ((R0,NL),AND,(R0,LT,CVMAXLNO)),BEGIN                             
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         COMMENT                   IF LINE DOES NOT EXIST, SEG ###              
         ELSE  BEGIN                                                            
         TSEG  '######'                                                         
         END                                                                    
*                                                                               
         COMMENT                   SEG EXEC NAME                                
*                                                                               
         COMMENT                   HANDLE SPECIAL CASES                         
         L     R0,SGXLSET                                                       
         L     R1,SGXLRG                                                        
         LA    R2,CPEXEC                                                        
         IF    ((R0,EQ,CP#XACT),AND,(R1,EQ,R2)),BEGIN    IF ACTIVE,             
         TSEG  ' of EXEC: (ACTIVE file)'                                        
         END                                                                    
         ELSEIF ((R0,EQ,CP#XTEMP),AND,(R1,EQ,R2)),BEGIN  IF TEMP,               
         END                                                                    
*                                                                               
         COMMENT                   OTHERWISE SEG FILE NAME...                   
         ELSE  BEGIN ,                                                          
         TSEG  ' of EXEC: '                                                     
         L     R0,SGXLSET                                                       
         L     R1,SGXLRG                                                        
         ACALL SEGXNAME                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGXNAME - OUTPUT EXEC FILE NAME                                             
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*      @R1 - EXEC RGROUP INFO                                                   
*                                                                               
*                                                                               
SXNWA    RECORD BEGIN                                                           
SXNXF    DS    XL(L'XFENTRY)       EXEC INFO DSECT                              
SXNXNAME DS    XL(L'XDSNAME)       DSN NAME WORK AREA                           
         END                                                                    
*                                                                               
*                                                                               
SEGXNAME XPROC SXNWA                                                            
         USING XFENTRY,R6                                                       
         LA    R6,SXNXF                                                         
*                                                                               
         COMMENT                   MUST DEAL WITH ACTIVE,TEMP,,,ETC.            
         LA    R2,CPEXEC                                                        
*                                                                               
         COMMENT                   IF OLD EXEC                                  
         IF    ((R0,EQ,CP#XOLD),AND,(R1,EQ,R2)),BEGIN                           
         LA    R0,L'SXNXNAME                                                    
         LA    R1,SXNXNAME                                                      
         LA    R2,=CL16'SYS.EXECNAME    '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R1,SXNXNAME                                                      
         IF    (@R1,EQ,'(ACTIVE file)'),BEGIN                                   
         TSEG  '(ACTIVE file)'                                                  
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL SEGXDSN                                                          
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF XCALLED ACTIVE/TEMP                       
         ELSEIF ((R0,EQ,CP#XACT),AND,(R1,EQ,R2)),BEGIN                          
         TSEG  '(ACTIVE file)'                                                  
         END                                                                    
         ELSEIF ((R0,EQ,CP#XTEMP),AND,(R1,EQ,R2)),BEGIN                         
         TSEG  '(....) '           SHOULD NEVER, REPLACE WITH BOMB!             
         END                                                                    
*                                                                               
         COMMENT                   IF XCALLED <FILENAME>                        
         ELSE  BEGIN ,,                                                         
         COMMENT                   HANDLE STADARD DSN NAME,                     
         COMMENT                   GET XINFO (CONTAINS DSN NAME)                
         LA    R2,XFENTRY                                                       
         ACALL GETXINFO                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R1,XFNAME                                                        
         ACALL SEGXDSN                                                          
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGXDSN - SEG DSN NAME                                                       
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - DSN IN XDSNAME DSECT FORMAT                                        
*                                                                               
*                                                                               
*                                                                               
SEGXDSN  XPROC ,                                                                
         USING XDSNAME,R2                                                       
         LR    R2,R1               R2 - DSN NAME                                
*                                                                               
         COMMENT                   FILE NAME                                    
         SETMSG XNAME                                                           
         VCALL RTRIM               R1,R0 - LOC,LEN OF FILE NAME                 
         VCALL RETFNAME                                                         
         IF    ((R0,GE,11),AND,(@R1,EQ,'WYL.'),AND,                    X        
               (@R1+6,EQ,'.'),AND,(@R1+10,EQ,'.'),AND,                 X        
               (CPSGRP,EQ,@R1+4),AND,(CPSUSER,EQ,@R1+7)),BEGIN                  
         LA    R1,@R1+11           Skip past the standard prefix                
         SH    R0,=H'11'                                                        
         END                                                                    
         TSEG  (R1),(R0)                                                        
*                                                                               
         COMMENT                   #MEMBER, IF ANY                              
         SETMSG XNAME                                                           
         VCALL RTRIM                                                            
         VCALL RETFMEM                                                          
         IF    (R0,POS),BEGIN                                                   
         XPUSH R0,R1                                                            
         TSEG  '#'                                                              
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGFDSN - SEG FULLY QUALIFIED DSN NAME                                       
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - DSN IN XDSNAME DSECT FORMAT                                        
*                                                                               
*                                                                               
*                                                                               
SEGFDSN  XPROC ,                                                                
         USING XDSNAME,R2                                                       
         LR    R2,R1               R2 - DSN NAME                                
*                                                                               
         COMMENT                   FILE NAME                                    
         SETMSG XNAME                                                           
         VCALL RTRIM               R1,R0 - LOC,LEN OF FILE NAME                 
         VCALL RETFNAME                                                         
         TSEG  (R1),(R0)                                                        
*                                                                               
         COMMENT                   #MEMBER, IF ANY                              
         SETMSG XNAME                                                           
         VCALL RTRIM                                                            
         VCALL RETFMEM                                                          
         IF    (R0,POS),BEGIN                                                   
         XPUSH R0,R1                                                            
         TSEG  '#'                                                              
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
         QLTORG                                                                 
         VLTORG                                                                 
         END   .                                                                
