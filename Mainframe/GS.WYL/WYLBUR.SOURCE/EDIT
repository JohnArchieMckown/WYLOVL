EDIT     TITLE 'WYLBUR''s Editing Commands'                                     
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLEDIT  CSECT                                                                  
         SPACE 2                                                                
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
         SYSDEFN ,                 Define installation params                   
*                                                                               
EDIT     IDENT 2193                15:11:43 07/12/02  (SLP)                     
*                                                                               
         PUSH  DSECTS                                                           
         EJECT                                                                  
         COPY  RTNCODES                                                         
*                                                                               
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         POP   DSECTS                                                           
         TITLE 'DSECTS'                                                         
AINFO    RECORD BEGIN                                                           
AINA     AINFO                                                                  
         END                                                                    
         TITLE 'SHOW ACTIVE and SHOW ACTIVES Commands'                          
SACTWA   RECORD BEGIN                                                           
SACTINF  DS    XL(L'AINFO)         Working AINFO area                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SHOWACT -- SHOW ACTIVE Command.                                              
*                                                                               
SHOWACT  XPROC SACTWA                                                           
*                                                                               
         CLEAR R5                                                               
         SCAN  SACTPRT             Look for options                             
         SCANCHK                                                                
*                                                                               
         IF    (R5,Z),'L R5,CPACTNO'  Current Active file                       
*                                                                               
         LR    R15,R5                                                           
         SETMSG SACTINF                                                         
         VCALL ACTGINFO            Get AINFO for this Active file               
         IF    Z,BEGIN             Show Active file info...                     
         LR    R15,R5                                                           
         SETMSG SACTINF                                                         
         VCALL ACTDISP             SHOW ACTIVE info                             
         END                                                                    
*                                                                               
         ELSE  BEGIN               Active not found...                          
         TSEG  'Active '                                                        
         TNUM  (R5)                                                             
         TSEG  ' does not exist.'                                               
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
***                                                                             
***  SHOW ACTIVE Options.                                                       
***                                                                             
SACTPRT  SCKW  ,SACTNUM,PI,99999                                                
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*                                                                               
SACTNUM  PROC  ,                                                                
         IF    (R5,NZ),BEGIN       Number already specified                     
         VCALL NOTVALID                                                         
         END                                                                    
         LR    R5,R0                                                            
         PRETURN (R5)                                                           
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
SACTSWA  RECORD BEGIN                                                           
SAFLAG   FLAG  ,                                                                
         FLAG  SAFCLR              - Show cleared actives                       
         FLAG  SAFREV              - Reverse order                              
*                                                                               
SATYPE   DS    CL8                 Show these types of Actives                  
*                                                                               
SAINFO   DS    XL(L'CPAINFO)       Working AINFO area                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SHOWACTS -- SHOW ACTIVES Command.                                            
*                                                                               
SHOWACTS XPROC SACTSWA                                                          
         LA    R6,SACTSWA                                                       
         USING SACTSWA,R6                                                       
         CLEAR SACTSWA                                                          
*                                                                               
         MVC   SATYPE,=CL8'TEXT'   Only show TEXT Active files                  
         SCAN  SACTSPRT                                                         
         SCANCHK                                                                
*-                                                                              
*-       Show Active files from lowest to highest.                              
*-                                                                              
         IF    ^SAFREV,BEGIN       Normal order...                              
         CLEAR R4                  Start at first possible                      
*                                                                               
         LOOP  BEGIN               Go through Actives...                        
SANEXT   CLEAR R1                                                               
         LR    R15,R4                                                           
         VCALL ACTNEXT             Get next Active file number                  
         IF    Z,EXIT              No more, scram                               
*                                                                               
         LR    R4,R15                                                           
         SETMSG SAINFO                                                          
         VCALL ACTGINFO            Get AINFO for this Active file               
         FAIL  NZ,ACTSERR1,'Multiple Active file logic error.'                  
*                                                                               
         LA    R3,SAINFO                                                        
         WITH  (AINFO,R3)                                                       
         IF    (R4,NE,CPACTNO),BEGIN  Should we display it...                   
         IF    (AINATYPE,NE,SATYPE),SANEXT  Only match our type                 
         IF    (^SAFCLR,AND,AINAFCLR),SANEXT  Skip cleared actives              
         END                                                                    
         LR    R15,R4                                                           
         SETMSG AINFO                                                           
         VCALL ACTDISP             Show info from this Active file              
         BNZ   CVNEXT              Attn, scram                                  
         END                                                                    
         END                                                                    
*-                                                                              
*-       Show Active files from highest to lowest.                              
*-                                                                              
         ELSE  BEGIN               Reverse order...                             
         L     R4,CPACTMAX         Start at highest possible                    
         INCR  R4                                                               
*                                                                               
SARNEXT  LOOP  BEGIN               Go through Actives...                        
         CLEAR R1                                                               
         LR    R15,R4                                                           
         VCALL ACTPREV             Get previous Active file number              
         IF    Z,EXIT              No more, scram                               
*                                                                               
         LR    R4,R15                                                           
         SETMSG SAINFO                                                          
         VCALL ACTGINFO            Get AINFO for this Active file               
         FAIL  NZ,ACTSERR2,'Multiple Active file logic error.'                  
*                                                                               
         LA    R3,SAINFO                                                        
         WITH  (AINFO,R3)                                                       
         IF    (R4,NE,CPACTNO),BEGIN  Should we display it...                   
         IF    (AINATYPE,NE,SATYPE),SARNEXT  Only match our type                
         IF    (^SAFCLR,AND,AINAFCLR),SARNEXT  Skip cleared actives             
         END                                                                    
         LR    R15,R4                                                           
         SETMSG AINFO                                                           
         VCALL ACTDISP             Show info from this Active file              
         BNZ   CVNEXT              Attn, scram                                  
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
***                                                                             
***  SHOW ACTIVES Options.                                                      
***                                                                             
SACTSPRT SCKW  REVERSE,SACTSREV,A                                               
         SCKW  ALL,SACTSALL                                                     
         SCKW  TEMPORARY,SACTSTMP,A                                             
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*                                                                               
SACTSALL PROC  ,                                                                
         SET   SAFCLR              Show cleared Actives too                     
         PEND  ,                                                                
*                                                                               
SACTSTMP PROC  ,                                                                
         MVC   SATYPE,=CL8'TEMP'   Show this Active file type                   
         PEND  ,                                                                
*                                                                               
SACTSREV PROC  ,                                                                
         SET   SAFREV              Show in reverse order                        
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         QLTORG                                                                 
         TITLE 'SET ACTIVE Command'                                             
SETACTWA RECORD BEGIN                                                           
SETAFLAG FLAG                                                                   
         FLAG  SETAFOPT            - At least one option given                  
         FLAG  SETAFLOCK           - Locked (in use)                            
         FLAG  SETAFNLOCK          - Unlocked (no longer in use)                
         FLAG  SETAFPRT            - Protected                                  
         FLAG  SETAFNPRT           - Unprotected                                
*                                                                               
SETANUM  DS    F                   Active file number                           
SETATYPE DS    CL8                 Active file type                             
SETANAME DS    CL(L'AINANAME)      New Active file name                         
SETATITL DS    CL(L'AINATITL)      New Active file title                        
*                                                                               
SETAINF  DS    XL(L'AINFO)         Working AINFO area                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SETACT -- SET ACTIVE Command.                                                
*                                                                               
SETACT   XPROC SETACTWA                                                         
         LA    R6,SETACTWA                                                      
         USING SETACTWA,R6                                                      
         CLEAR SETACTWA                                                         
*-                                                                              
*-       Look for options.                                                      
*-                                                                              
         SCAN  SETAPRT                                                          
         SCANCHK                                                                
*                                                                               
         IF    (SETANUM,Z),'MVC SETANUM,CPACTNO'  Set Current Active            
*                                                                               
         IF    ^SETAFOPT,BEGIN     No options specified...                      
         TSEG  'Missing "SET ACTIVE" options.',,CR                              
         TSEG  'Use the PICK command to select a different Active '             
         ERROR 'file.'                                                          
         END                                                                    
*-                                                                              
*-       Update AINFO for specified Active file.                                
*-                                                                              
         L     R15,SETANUM                                                      
         SETMSG SETAINF             Put AINFO here                              
         VCALL ACTGINFO            Get AINFO                                    
         IF    NZ,BEGIN                                                         
         TSEG  'Active '                                                        
         TNUM  L:SETANUM                                                        
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         ELSE  BEGIN               We have the AINFO...                         
         LA    R4,SETAINF                                                       
         WITH  (AINFO,R4)                                                       
         IF    (SETANAME,NZ),'MVC AINANAME,SETANAME'                            
         IF    (SETATITL,NZ),'MVC AINATITL,SETATITL'                            
         IF    SETAFLOCK,'SET AINAFLOCK'     Locked                             
         IF    SETAFNLOCK,'CLEAR AINAFLOCK'  Unlocked                           
         IF    SETAFPRT,'SET AINAFPRT'     Protected                            
         IF    SETAFNPRT,'CLEAR AINAFPRT'  Unprotected                          
*                                                                               
         IF    (SETATYPE,NZ),BEGIN  Switch active file types...                 
         IF    ^AINAFCLR,BEGIN     Keep counts accurate...                      
         IF    (AINATYPE,EQ,'TEMP'),'DECR R15,CPATTEMP'                         
         ELSE  'DECR R15,CPATTEXT'                                              
*                                                                               
         IF    (SETATYPE,EQ,'TEMP'),'INCR R15,CPATTEMP'                         
         ELSE  'INCR R15,CPATTEXT'                                              
         END                                                                    
*                                                                               
         MVC   AINATYPE,SETATYPE   Update Active file type                      
         END                                                                    
*                                                                               
         L     R15,SETANUM                                                      
         SETMSG SETAINF                                                         
         VCALL ACTSINFO            Update AINFO                                 
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         SPACE 2                                                                
***                                                                             
***  SET ACTIVE Options                                                         
***                                                                             
SETAPRT  SCKW  ,SETXNUM,PI,999999                                               
         SCKW  NAME,SETXNAME,(A,P)                                              
         SCKW  TITLE,SETXTITL,(A,P)                                             
         SCKW  TEMPORARY,SETXTEMP,A                                             
         SCKW  NOTEMPORARY,SETXTEXT,A                                           
         SCKW  TEXT,SETXTEXT,A                                                  
         SCKW  RECOVERY,SETXTEXT,A       !!! Remove me !!!                      
         SCKW  NORECOVERY,SETXTEMP,A     !!! Remove me !!!                      
         SCKW  DISPLAY,SETXTEXT,A        !!! Remove me !!!                      
         SCKW  NODISPLAY,SETXTEMP,A      !!! Remove me !!!                      
         SCKW  PROTECTED,SETXPRT,A                                              
         SCKW  UNPROTECTED,SETXNPRT,A                                           
         SCKW  LOCK,SETXLOCK,A                                                  
         SCKW  UNLOCK,SETXNLCK,A                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SETXNUM  PROC  ,                                                                
         ST    R0,SETANUM                                                       
         PEND  ,                                                                
*                                                                               
SETXNAME PROC  ,                                                                
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPCASE SETANAME                                                      
         SET   SETAFOPT                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SETXTEMP PROC  ,                                                                
         MVC   SETATYPE,=CL8'TEMP'                                              
         SET   SETAFOPT                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SETXTEXT PROC  ,                                                                
         MVC   SETATYPE,=CL8'TEXT'                                              
         SET   SETAFOPT                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SETXTYPE PROC  ,                                                                
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         VCALL NOTVALID                                                         
         END                                                                    
         SCUPCASE SETATYPE                                                      
         SET   SETAFOPT                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SETXTITL PROC  ,                                                                
         VCALL CKQUOTES                                                         
         SCDQUOTE SETATITL                                                      
         SET   SETAFOPT                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SETXPRT  PROC  ,                                                                
         SET   SETAFPRT                                                         
         CLEAR SETAFNPRT                                                        
         SET   SETAFOPT                                                         
         PEND  ,                                                                
*                                                                               
SETXNPRT PROC  ,                                                                
         CLEAR SETAFPRT                                                         
         SET   SETAFNPRT                                                        
         SET   SETAFOPT                                                         
         PEND  ,                                                                
*                                                                               
SETXLOCK PROC  ,                                                                
         SET   SETAFLOCK                                                        
         CLEAR SETAFNLOCK                                                       
         SET   SETAFOPT                                                         
         PEND  ,                                                                
*                                                                               
SETXNLCK PROC  ,                                                                
         CLEAR SETAFLOCK                                                        
         SET   SETAFNLOCK                                                       
         SET   SETAFOPT                                                         
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         TITLE 'OPEN Command'                                                   
OPENWA   RECORD BEGIN                                                           
OPENNAME DS    CL(L'CPANAME)       Active file name                             
OPENTYPE DS    CL(L'CPATYPE)       Active file type                             
OPENTITL DS    CL(L'CPATITL)       Active file title                            
*                                                                               
OPENDUPN DS    F                   Duplicate this Active file                   
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  OPEN -- OPEN Command.                                                        
*                                                                               
OPEN     XPROC OPENWA                                                           
         LA    R6,OPENWA                                                        
         USING OPENWA,R6                                                        
         CLEAR OPENWA                                                           
*-                                                                              
*-       First do compatability check for "OPEN front-end" command.             
*-                                                                              
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCANCHK ,                                                              
         IF    (R0,POS),BEGIN                                                   
         IF    (R0,LT,3),EXIT                                                   
         IF    ((@R1,EQ,'11'),OR,(@R1,EQ,'ALF')),BEGIN                          
         SCPOP ,                                                                
         TSEG  'MOPEN assumed.',,WR                                             
         VCALL MOPEN                                                            
         B     CVNEXT                                                           
         END                                                                    
         END                                                                    
         SCPOP ,                                                                
*                                                                               
* SCAN ACTIVE FILE NAME                                                         
*                                                                               
         COMMENT                   SCAN ACTIVE FILE NAME                        
         SCAN                                                                   
         SCANCHK                                                                
*                                                                               
         COMMENT                   IF GOT NAME, SAVE IT                         
         IF    (R0,POS),BEGIN                                                   
         COMMENT                   '*' IS NULL NAME                             
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),EXIT  Place holder                  
         COMMENT                   IF NAME TOO LONG, TRY SPIRES                 
         IF    (R0,GT,L'OPENNAME),BEGIN                                         
         VCALL ORVCPASS            (For "OPEN TRANSACTION GROUP")               
         CEIL  R0,16                                                            
         TSEG  (R1),(R0)                                                        
         ERROR ': Active file name is too long.'                                
         END                                                                    
         COMMENT                   SAVE AND CHECK NAME                          
         SCUPCASE OPENNAME                                                      
         SCUPTOK  (R1)                                                          
         VCALL CHARCHKA            Make sure name is ok                         
         IF    NZ,BEGIN                                                         
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in Active file type.'                      
         END                                                                    
         END                                                                    
*                                                                               
* Look for other OPEN options.                                                  
*                                                                               
         SCAN  OPENPRT             OPEN Command options                         
         SCANCHK                                                                
*-                                                                              
*-       Make the current active file explicitly defined, so we                 
*-         will never return to it automatically when an Active                 
*-         file is cleared.                                                     
*-                                                                              
         SET   CPAFDEF             Active is explicitly defined                 
*                                                                               
         VCALL ACTOPEN             Open a new Active file                       
*                                                                               
         IF    (OPENNAME,NZ),'MVC CPANAME,OPENNAME'                             
         IF    (OPENTYPE,NZ),'MVC CPATYPE,OPENTYPE'                             
         IF    (OPENTITL,NZ),'MVC CPATITL,OPENTITL'                             
*                                                                               
         IF    ('LT R15,OPENDUPN',NZ),'VCALL ACTDUP'                            
*                                                                               
         VCALL ACTVREST            Set current view position                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         SPACE 2                                                                
***                                                                             
***  OPEN Command Options.                                                      
***                                                                             
OPENPRT  SCKW  TITLE,OTITLE,(A,P)                                               
         SCKW  TEMPORARY,OTEMP,A                                                
         SCKW  NOTEMPORARY,OTEXT,A                                              
         SCKW  TEXT,OTEXT,A                                                     
         SCKW  RECOVERY,OTEXT,A    !!! Delete me !!!                            
         SCKW  NORECOVERY,OTEMP,A  !!! Delete me !!!                            
         SCKW  DISPLAY,OTEXT,A     !!! Delete me !!!                            
         SCKW  NODISPLAY,OTEMP,A   !!! Delete me !!!                            
         SCKW  DUPLICATE,ODUP,(A,P,PI),999999                                   
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*                                                                               
OTEXT    PROC                                                                   
         MVC   OPENTYPE,=CL8'TEXT'                                              
         PEND  ,                                                                
*                                                                               
OTEMP    PROC  ,                                                                
         MVC   OPENTYPE,=CL8'TEMP'                                              
         PEND  ,                                                                
*                                                                               
OTITLE   PROC  ,                                                                
         VCALL CKQUOTES                                                         
         SCDQUOTE OPENTITL                                                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
ODUP     PROC  ,                                                                
         ST    R15,OPENDUPN                                                     
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         TITLE 'CLOSE Command'                                                  
*box                                                                            
*                                                                               
*  CLOSE -- CLOSE Command.                                                      
*                                                                               
CLOSE    XPROC ,                                                                
*-                                                                              
*-       First do compatability check for "CLOSE front-end" command.            
*-                                                                              
         SCPUSH                                                                 
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,POS),BEGIN                                                   
         IF    (R0,LT,3),EXIT                                                   
         IF    ((@R1,EQ,'11'),OR,(@R1,EQ,'ALF')),BEGIN                          
         SCPOP                                                                  
         TSEG  'MCLOSE assumed.',,WR                                            
         VCALL MCLOSE                                                           
         B     CVNEXT                                                           
         END                                                                    
         END                                                                    
         SCPOP                                                                  
*                                                                               
         SCAN  CLOSEPRT            CLOSE Command options                        
         SCANCHK                                                                
*                                                                               
*                                                                               
         IF    CPFKEEP,BEGIN                                                    
         ERROR 'NOCLEAR is not valid here.'                                     
         END                                                                    
*                                                                               
         VCALL ACTCLOSE            Close current Active file                    
*                                                                               
         VCALL ACTVREST            Set current view position                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         SPACE 2                                                                
***                                                                             
***  CLOSE Command Options.                                                     
***                                                                             
CLOSEPRT SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  TRANSACTION,CLCHKORV,A                                           
         SCKW  AREA,CLCHKORV,A                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*  IF 'CLOSE AREA' OR 'CLOSE TRANSACTION', PASS TO SPIRES                       
*                                                                               
CLCHKORV PROC  ,                                                                
         VCALL ORVCPASS            IF VALID SPIRES, DOES NOT RETURN             
         VCALL NOTVALID            OTHERWISE INVALID COMMAND                    
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'PICK Command'                                                   
PICKWA   RECORD BEGIN                                                           
PICKFLAG FLAG                                                                   
         FLAG  PICKFPRM            - Parameter specified                        
         FLAG  PICKFPRV            - Previous                                   
         FLAG  PICKFNXT            - Next                                       
         FLAG  PICKFFIR            - First                                      
         FLAG  PICKFLST            - Last                                       
         FLAG  PICKFNOPROMPT       - Don't do prompting                         
         FLAG  PICKFNOVERIFY       - Allow unclearing w/o checking              
*                                                                               
PICKNUM  DS    F                   Explicit Active number                       
*                                                                               
PICKAINF DS    XL(L'AINFO)         Working AINFO area                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  PICK -- PICK Command.                                                        
*                                                                               
PICK     XPROC PICKWA                                                           
         LA    R6,PICKWA                                                        
         USING PICKWA,R6                                                        
         CLEAR PICKWA                                                           
*                                                                               
         SCAN  PICKPRT             Scan options                                 
         SCANCHK                                                                
*-                                                                              
*-       Pick number.                                                           
*-                                                                              
         LT    R15,PICKNUM         Any explicit selection?                      
         IF    NZ,BEGIN            Yes...                                       
         SETMSG PICKAINF            Working AINFO area                          
         VCALL ACTGINFO            Get AINFO for this Active file               
         IF    NZ,BEGIN                                                         
         TSEG  'Active '                                                        
         TNUM  L:PICKNUM                                                        
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LA    R2,PICKAINF                                                      
         WITH  (AINFO,R2),BEGIN                                                 
         IF    AINAFCLR,BEGIN                                                   
         IF    PICKFNOVERIFY,EXIT                                               
         TSEG  'Active '                                                        
         TNUM  L:PICKNUM                                                        
         TSEG  CVBLANKS,1                                                       
         TSEG  AINANAME,,T                                                      
         SETMSG ' has been cleared.'                                            
         IF    PICKFNOPROMPT,CVERRMSG                                           
*                                                                               
         TSEG  (R1),(R0),CR                                                     
         SETMSG 'Do you want to pick it anyway'                                 
         VCALL YESREQ                                                           
         END                                                                    
         END                                                                    
*                                                                               
         L     R15,PICKNUM                                                      
         VCALL ACTPICK                                                          
         B     PICKDONE                                                         
         END                                                                    
*-                                                                              
*-       Pick previous.                                                         
*-                                                                              
         IF    PICKFPRV,BEGIN      Pick previous...                             
         LA    R1,CPATYPE                                                       
         L     R15,CPACTNO                                                      
         VCALL ACTPREV             Find previous Active file                    
         IF    Z,BEGIN                                                          
         ERROR 'No previous Active file to pick.'                               
         END                                                                    
         VCALL ACTPICK             Pick it                                      
         B     PICKDONE                                                         
         END                                                                    
*-                                                                              
*-       Pick next.                                                             
*-                                                                              
         IF    PICKFNXT,BEGIN      Pick next...                                 
         LA    R1,CPATYPE                                                       
         L     R15,CPACTNO                                                      
         VCALL ACTNEXT             Find next Active file                        
         IF    Z,BEGIN                                                          
         ERROR 'No next Active file to pick.'                                   
         END                                                                    
         VCALL ACTPICK             Pick it                                      
         B     PICKDONE                                                         
         END                                                                    
*-                                                                              
*-       Pick first.                                                            
*-                                                                              
         IF    PICKFFIR,BEGIN      Pick first...                                
         CLEAR R15                                                              
         LA    R1,CPATYPE                                                       
         VCALL ACTFIRST            Find first possible Active file              
         IF    NZ,'VCALL ACTPICK'  Pick it                                      
         B     PICKDONE                                                         
         END                                                                    
*-                                                                              
*-       Pick last.                                                             
*-                                                                              
         IF    PICKFLST,BEGIN      Pick last...                                 
         L     R15,=F'-1'                                                       
         LA    R1,CPATYPE                                                       
         VCALL ACTLAST             Find last possible Active file               
         IF    NZ,'VCALL ACTPICK'  Pick it                                      
         B     PICKDONE                                                         
         END                                                                    
*                                                                               
         ERROR 'Invalid pick option.'  (Can't happen???)                        
*-                                                                              
*-       Finish up.                                                             
*-                                                                              
PICKDONE VCALL ACTVREST            Restore view position                        
         B     CVNEXT                                                           
         PEND  ,                                                                
         SPACE 2                                                                
***                                                                             
***  PICK Command Options.                                                      
***                                                                             
PICKPRT  SCKW  ,PCKNULL,NULL                                                    
         SCKW  PREVIOUS,PPREV,A                                                 
         SCKW  NEXT,PNEXT,A                                                     
         SCKW  FIRST,PFIRST,A                                                   
         SCKW  F,PFIRST,A                                                       
         SCKW  LAST,PLAST,A                                                     
         SCKW  L,PLAST,A                                                        
         SCKW  ,PNUM,PI,99999                                                   
         SCKW  NOPROMPT,PNOPRO,A                                                
         SCKW  NOVERIFY,PNOVER,A                                                
         SCKW  ,PERR                                                            
*                                                                               
*                                                                               
PCKNULL  PROC ,                                                                 
         TSEG  'You must specify the Active file you want to '                  
         TSEG  'pick.',,CR                                                      
         TSEG  'The full form is '                                              
         ERROR '"PICK {n|FIRST|LAST|PREVIOUS|NEXT}".'                           
         PEND  ,                                                                
*                                                                               
PPREV    PROC  ,                                                                
         IF    PICKFPRM,'ACALL PERR'                                            
         SET   PICKFPRM+PICKFPRV                                                
         PEND  ,                                                                
*                                                                               
PNEXT    PROC  ,                                                                
         IF    PICKFPRM,'ACALL PERR'                                            
         SET   PICKFPRM+PICKFNXT                                                
         PEND  ,                                                                
*                                                                               
PFIRST   PROC  ,                                                                
         IF    PICKFPRM,'ACALL PERR'                                            
         SET   PICKFPRM+PICKFFIR                                                
         PEND  ,                                                                
*                                                                               
PLAST    PROC  ,                                                                
         IF    PICKFPRM,'ACALL PERR'                                            
         SET   PICKFPRM+PICKFLST                                                
         PEND  ,                                                                
*                                                                               
PNUM     PROC  ,                                                                
         IF    PICKFPRM,'ACALL PERR'                                            
         SET   PICKFPRM                                                         
         ST    R0,PICKNUM                                                       
         PEND  ,                                                                
*                                                                               
PNOPRO   PROC                                                                   
         SET   PICKFNOPROMPT                                                    
         PEND                                                                   
*                                                                               
PNOVER   PROC                                                                   
         SET   PICKFNOVERIFY                                                    
         PEND                                                                   
*                                                                               
PERR     PROC  ,                                                                
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid PICK option.'                                         
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         TITLE 'UNCLEAR Command'                                                
UCLRWA   RECORD BEGIN                                                           
UCAINFO  DS    XL(L'AINFO)         Working AINFO area                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  UNCLEAR -- UNCLEAR Command.                                                  
*                                                                               
UNCLEAR  XPROC UCLRWA                                                           
*                                                                               
         VCALL NOOPTS              No options                                   
*-                                                                              
*-       Find the highest numbered cleared Active file.                         
*-                                                                              
         L     R4,CPACTMAX         Start at highest possible                    
         INCR  R4                                                               
*                                                                               
*                                                                               
UCRNEXT  CLEAR R1                                                               
         LR    R15,R4                                                           
         VCALL ACTPREV             Get previous Active file number              
         IF    Z,BEGIN                                                          
         TSEG  'UNCLEAR not possible; there aren''t any more'                   
         ABORT ' cleared Active files.'                                         
         END                                                                    
*                                                                               
         LR    R4,R15                                                           
         SETMSG UCAINFO                                                         
         VCALL ACTGINFO            Get AINFO for this Active file               
         FAIL  NZ,UCLRERR,'Multiple Active file logic error.'                   
*                                                                               
         LA    R3,UCAINFO                                                       
         WITH  (AINFO,R3),BEGIN                                                 
         IF    ^AINAFCLR,UCRNEXT   We only want cleared actives                 
         IF    (AINATYPE,NE,=CL8'TEXT'),UCRNEXT                                 
         END                                                                    
*-                                                                              
*-      Now pick the highest cleared Active (which will unclear it).            
*-                                                                              
         LR    R15,R4              Highest uncleared Active                     
         VCALL ACTPICK             Pick it                                      
*                                                                               
         VCALL ACTVREST            Set view position too                        
         B     CVNEXT                                                           
         PEND  ,                                                                
         QLTORG                                                                 
         TITLE 'LIST and POINT Commands'                                        
*box                                                                            
*                                                                               
*  LIST   -- LIST command.                                                      
*  POINT  -- POINT command.                                                     
*                                                                               
*                                                                               
LISTWA   RECORD BEGIN                                                           
LISTFLG  FLAG                                                                   
         FLAG  LISFSQU             - Squash                                     
         FLAG  LISFDOUB            - Double                                     
         FLAG  LISFTRIP            - Triple                                     
         FLAG  LISFCC              - CC                                         
         FLAG  LISFMC              - MC                                         
         FLAG  LISFHEX             - Hex                                        
         FLAG  LISFMIX             - Mixed                                      
LISTFLG2 FLAG                                                                   
         FLAG  LISFFULL            - Full                                       
         FLAG  LISFBIN             - Binary (image)                             
LISTFLG3 FLAG                                                                   
LISTMARK DS    C                   Marker character                             
LISTIND  DS    H                   Indentation count                            
LISTALNO DS    F                   Attn line-number                             
         END                                                                    
*                                                                               
*                                                                               
LIST     XPROC ,                                                                
         SET   CPFNCUR             Don't update current line ptr                
         ACALL POINT               Go to common code                            
         PEND  ,                                                                
*                                                                               
*                                                                               
POINT    XPROC LISTWA                                                           
         LA    R6,LISTWA                                                        
         USING LISTWA,R6                                                        
         CLEAR LISTWA                                                           
         MVC   LISTALNO,=F'-1'     No attn lineno                               
         ST    R6,CPWK4            Save LISTWA ptr                              
*                                                                               
         VCALL SCNEXFR             Allow active/exec/from dsname                
         SET   CPLFLG1.CPFALL      Default is ALL                               
         VCALL NDETRNG             Scan range                                   
*                                                                               
LISTSCAN LABEL ,                                                                
         COMMENT                   SCAN LIST OPTIONS                            
         IF    CPFRDEXT,BEGIN      (IF EXTERNAL (FROM DSN..))                   
         VCALL MKFFINFO                                                         
         SCAN  LISTPRTX                                                         
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  LISTPRT                                                          
         SCANCHK                                                                
         END                                                                    
*                                                                               
         IF    LISFCC+LISFMC,BEGIN  Adjust cols to skip CC/MC char              
         LH    R15,CPFCOL                                                       
         IF    (R15,Z),'MVC CPFCOL,=Y(1)'  Set to col 2 (effectively)           
         END                                                                    
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         END                                                                    
*                                                                               
         CLEAR DSNFSTAT            No SAMSON stats ...                          
         VCALL PAGERES             Reset page mode                              
*                                                                               
         COMMENT                   NICE SET OF FLAGS DUDE !!                    
         IF CPFRDEXT,BEGIN         EXTERNAL                                     
         RDRNG LISTRTN,(LXCATRTN+DESRTRN+UNPRST+DESABORT)                       
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN     EXEC                                         
         RDRNG LISTRTN,(LEXATRTN+DESRTRN+UNPRST+DESABORT)                       
         END                                                                    
         ELSE  BEGIN               ACTIVE                                       
         RDRNG LISTRTN,(LOCATRTN+DESRTRN+UNPRST+DESABORT)                       
         END                                                                    
*                                                                               
         IF    LISFCC+LISFMC,'TCR'                                              
         TWRITE                                                                 
         BZ    CVNEXT              Normal completion                            
*                                                                               
         IF    (LISTALNO,EQ,-1),CVABORT  No attn lineno                         
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         MVC   CPEXNEXT,LISTALNO   Set new exec lineno                          
         B     CVNEXT                                                           
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
LISTPRTX SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(LTNPSH),PUSH     MUST COME BEFORE FNAMEPSH                    
         SCKW  ,V(FNAMPSHF),PUSH   (NUMBER IS LIST OPT NOT FILE OPT)            
*                                                                               
LISTPRT  SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  ,V(COLSPSH),PUSH                                                 
         SCKW  BINARY,LBINARY,A                                                 
         SCKW  SQUASHED,LSQUASH,A                                               
         SCKW  DOUBLE,LDOUBLE,A                                                 
         SCKW  TRIPLE,LTRIPLE,A                                                 
         SCKW  CC,LCC                                                           
         SCKW  MC,LMC                                                           
         SCKW  HEX,LHEX                                                         
         SCKW  MIXED,LMIXED,A                                                   
         SCKW  FULL,LFULL,A                                                     
         SCKW  NONL,LNONL          (NON means nonumber)                         
         SCKW  MODEL,LMODEL,A                                                   
         SCKW  NOMODEL,LNOMODEL,A                                               
         SCKW  FORMATTED,LFMT,A                                                 
         SCKW  UNFORMATTED,LUFMT,A                                              
         SCKW  FORCE,LFORCE,A                                                   
         SCKW  NOPAUSE,LNOPAUSE,A                                               
         SCKW  TRANSPARENT,LTRAN,A                                              
         SCKW  CLEAN,LCLEAN,A                                                   
         SCKW  INDENT,LINDENT,(A,P,PI),&LINESZ                                  
         SCKW  MARKER,LMARKER,(A,P)                                             
         SCKW  ATTN,LATTN,A                                                     
         SCKW  OFFLINE,LOFFLINE,A                                               
         SCKW  ACTIVE,V(SCNNOOP),A           (Processed elsewhere)              
         SCKW  EXECUTE,V(SCNNOOP),A          (Processed elsewhere)              
         SCKW  ,V(NOTVALID)                                                     
         SPACE 2                                                                
LSQUASH  PROC  ,                                                                
         SET   LISFSQU                                                          
         PEND  ,                                                                
*                                                                               
LDOUBLE  PROC  ,                                                                
         SET   LISFDOUB,(LISFTRIP,OFF)                                          
         PEND  ,                                                                
*                                                                               
LTRIPLE  PROC  ,                                                                
         SET   LISFTRIP,(LISFDOUB,OFF)                                          
         PEND  ,                                                                
*                                                                               
LCC      PROC  ,                                                                
         SET   LISFCC,(LISFMC,OFF)                                              
         SET   CPFNONL                                                          
         PEND  ,                                                                
*                                                                               
LMC      PROC  ,                                                                
         SET   LISFMC,(LISFCC,OFF)                                              
         SET   CPFNONL                                                          
         PEND  ,                                                                
*                                                                               
LHEX     PROC  ,                                                                
         SET   LISFHEX                                                          
         PEND  ,                                                                
*                                                                               
LMIXED   PROC  ,                                                                
         SET   LISFMIX                                                          
         SET   CPFUFMT                                                          
         PEND  ,                                                                
*                                                                               
LNONL    PROC  ,                                                                
         SET   CPFNONL                                                          
         PEND  ,                                                                
*                                                                               
LFULL    PROC  ,                                                                
         SET   LISFFULL                                                         
         PEND  ,                                                                
*                                                                               
LBINARY  PROC  ,                                                                
         SET   LISFBIN                                                          
         SET   CPFTRAN                                                          
         SET   CPFNONL                                                          
         SET   CPFNMOD                                                          
         SET   CPFNPAU                                                          
         IF    ^CPLFLG5.CPFFOPT,BEGIN                                           
         CLEAR CPLFLG5.CPFNONUM    Clear nonumber                               
         SET   CPLFLG5.CPFUNUM     Set unnumbered                               
         END                                                                    
         PEND  ,                                                                
*                                                                               
LFMT     PROC  ,                                                                
         CLEAR CPFUFMT                                                          
         PEND  ,                                                                
*                                                                               
LUFMT    PROC  ,                                                                
         SET   CPFUFMT                                                          
         PEND  ,                                                                
*                                                                               
LFORCE   PROC  ,                                                                
         SET   CPFFORCE                                                         
         PEND  ,                                                                
*                                                                               
LMODEL   PROC  ,                                                                
         CLEAR CPFNMOD                                                          
         PEND  ,                                                                
*                                                                               
LNOMODEL PROC  ,                                                                
         SET   CPFNMOD                                                          
         PEND  ,                                                                
*                                                                               
LNOPAUSE PROC  ,                                                                
         SET   CPFNPAU                                                          
         PEND  ,                                                                
*                                                                               
LTRAN    PROC  ,                                                                
         SET   CPFTRAN                                                          
         IF    ^CPLFLG5.CPFFOPT,BEGIN                                           
         CLEAR CPLFLG5.CPFNONUM    Clear nonumber                               
         SET   CPLFLG5.CPFUNUM     Set unnumbered                               
         END                                                                    
         PEND  ,                                                                
*                                                                               
LCLEAN   PROC  ,                                                                
         SET   CPFCLEAN                                                         
         PEND  ,                                                                
*                                                                               
LINDENT  PROC  ,                                                                
         STH   R0,LISTIND                                                       
         PEND  ,                                                                
*                                                                               
LMARKER  PROC  ,                                                                
         SCDQUOTE LISTMARK                                                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
LATTN    PROC  ,                                                                
         VCALL ATTNLINE                                                         
         ST    R0,LISTALNO         Save lineno                                  
         VCALL ALLOWATN            Allow attentions                             
         PEND  ,                                                                
*                                                                               
LOFFLINE PROC  ,                                                                
         TSEG  'The OFFLINE option has been removed.  '                         
         TSEG  'Use the PRINT command instead.',,CR                             
         B     CVNOACTN                                                         
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
*                                                                               
         EJECT                                                                  
LRTNWA   RECORD BEGIN                                                           
         XSA   R2,R8                                                            
*                                                                               
LRTNFLAG FLAG                                                                   
         FLAG  (LRTNFMRK,X'40')    - Line is marked                             
LRTNID   DS    CL3                 User "id"                                    
LRTNCLCK DS    XL4                 TOD Clock                                    
LRTNPFX  EQU   LRTNFLAG,*-LRTNFLAG,C'X'                                         
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  LISTRTN -- Co-routine to list lines from the range processor.                
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
LISTRTN  PROC  LRTNWA                                                           
*                                                                               
         L     R6,CPWK4            Listwa ptr                                   
         USING LISTWA,R6                                                        
         IF    CPLFLG5.CPFNLST,LISTEXIT  Nolist                                 
         LR    R5,R1               Save ptr to first char                       
         CEIL  R0,CPLCOL           Don't go past last col                       
         AH    R1,CPFCOL                                                        
         SH    R0,CPFCOL                                                        
         IF    NEG,'CLEAR R0'                                                   
         IF    LISFSQU,'VCALL SQSHRTN'  Squash                                  
         LR    R3,R0                                                            
         LR    R4,R1                                                            
*-                                                                              
*-       Handle MARKER lines.                                                   
*-                                                                              
         IF    ((LISTMARK,NZ),AND,(@R5,EQ,LISTMARK)),BEGIN                      
         IF    (LISTMARK,EQ,' '),BEGIN                                          
         IF    (R3,NZ),NOMARK                                                   
         END                                                                    
*                                                                               
         TREAD CVBLANKS,2          Default marker prompt                        
         BNZ   LISTATTN                                                         
         B     LISTEXIT                                                         
         END                                                                    
*                                                                               
NOMARK   IF    LISFCC,BEGIN        CC...                                        
         SETMSG X'15'               Assume NL                                   
         IF    (@R5,EQ,'+'),'SETMSG X"0D"'                                      
         IF    (@R5,EQ,'0'),'SETMSG X"1515"'                                    
         IF    (@R5,EQ,'-'),'SETMSG X"151515"'                                  
         IF    (@R5,EQ,'1'),'SETMSG X"150C"'                                    
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         IF    (LISFMC,AND,@R5.X'02'),LISTSKIP  MC ctrl has no text             
*                                                                               
         TSEG  CVBLANKS,LH:LISTIND   Indentation                                
*                                                                               
         IF    LISFFULL,BEGIN      Include id info...                           
         IF    (CPFRDEXE,OR,CPFRDEXT),EXIT  Only if active                      
*                                                                               
* There is no line prefix anymore (see the note in the GETLINFO                 
* routine in ACT). For compatibility, we'll accept the LIST                     
* FULL option, but we'll just print the hyphens.                                
*                                                                               
         TSEG  '--------------------'                                           
         END                                                                    
*                                                                               
         SETMSG (R4),(R3)                                                       
         IF    LISFHEX,BEGIN                                                    
         IF    LISFMIX,'LCR R0,R0'  Hex mixed format                            
         ACALL LISTHEX                                                          
         END                                                                    
         ELSEIF LISFMIX,'ACALL LISTMIX'                                         
         ELSEIF LISFBIN,'ACALL LISTBIN'                                         
         ELSE  'VCALL LISTLINE'                                                 
*                                                                               
LISTSKIP IF    LISFMC,BEGIN        Mc...                                        
         CLEAR @R5.X'02'           Zot control bit                              
         SETMSG X'15'               Assume nl                                   
         IF    (@R5,EQ,X'01'),'SETMSG X"0D"'      +                             
         IF    (@R5,EQ,X'11'),'SETMSG X"1515"'    0                             
         IF    (@R5,EQ,X'19'),'SETMSG X"151515"'  -                             
         IF    (@R5,EQ,X'89'),'SETMSG X"150C"'    1                             
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         IF    LISFDOUB+LISFTRIP,'TCR'                                          
         IF    LISFTRIP,'TCR'                                                   
*                                                                               
         TMARK                                                                  
*                                                                               
         IF    ('TSTAT',NZ),BEGIN  Attn...                                      
LISTATTN IF    (LISTALNO,EQ,-1),CVABORT                                         
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         MVC   CPEXNEXT,LISTALNO   Set new exec lineno                          
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
LISTEXIT PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LISTHEX -- Routine to list line in hex.                                      
*                                                                               
*    On entry:                                                                  
*      CPLCNO - lineno                                                          
*      R1,R0 - line loc, len (len complemented for mixed format)                
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      nz- attn                                                                 
*                                                                               
LISTHEX  PROC  ,                                                                
         XPUSH R0,R1                                                            
         LA    R15,9               Indentation if unnumbered                    
         IF    ^CPLFLG5.CPFUNUM,BEGIN                                           
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         TSEG  CPDOUB,9                                                         
         TSEG  CVBLANKS,2                                                       
         LA    R15,11              Indentation to use on cont lines             
         END                                                                    
         IF    CPLFLG5.CPFNONUM,'TSEG CVBLANKS,11; LA R15,11'                   
         AH    R15,LISTIND         Account for "indent" option                  
         XPOP  R0,R1                                                            
         VCALL HEXFMT              Write out line in hex                        
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG ,                                                               
*box                                                                            
*                                                                               
*  LISTBIN -- Routine to list line in image mode                                
*                                                                               
*    On entry:                                                                  
*      CPLCNO - lineno                                                          
*      R1,R0 - line loc, len                                                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      nz- attn                                                                 
*                                                                               
*    This code was stolen from an equivalent routine                            
*    in the NET module. Make it a subroutine and put                            
*    it in UTIL.                                                                
*                                                                               
LISTBIN  PROC  ,                                                                
         LR    R15,R1              R1 = first character on line                 
         AR    R15,R0                                                           
         DECR  R15                 R15 = last character on line                 
         IF    ((R0,LT,2),OR,                                          *        
               ('CLC @R1(1),@R15',NE),OR,                              *        
               ((@R1,NE,'|'),AND,(@R1,NE,'@'))),BEGIN                           
         TSEG  'Line '                                                          
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         SETMSG ' is not valid TRANSPARENT data.'                               
         IF    ((@R4,EQ,'-'),OR,(@R4,EQ,X'2D')),BEGIN                           
         TSEG  ' has Macintosh transparent data which can not '                 
         SETMSG 'be sent over the net.'                                         
         END                                                                    
         TSEG  (R1),(R0),CR                                                     
         ABORT 'LIST command incomplete.'                                       
         END                                                                    
*-                                                                              
*-       If the first character is an "@" then the data is in ASCII,            
*-         and if the first character is a "|" then it is in EBCDIC.            
*-                                                                              
         IF    (@R1,EQ,'|'),BEGIN  ASCII...                                     
         VCALL TOASCII             If EBCDIC, convert to ASCII                  
         INCR  R1                  Don't send the delimiters                    
         DECR  R0                                                               
         DECR  R0                                                               
         TSEG  (R1),(R0)                                                        
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG ,                                                               
*box                                                                            
*                                                                               
*  LISTMIX -- Routine to list line in mixed EBCDIC and HEX                      
*    format.                                                                    
*                                                                               
*    On entry:                                                                  
*      CPCLNO - lineno                                                          
*      R1,R0 - line loc, len                                                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      nz-attn                                                                  
*                                                                               
LISTMIX  PROC  ,                                                                
         XPUSH R0,R1                                                            
         LA    R15,9               Indentation if unnumbered                    
         CLEAR R0                                                               
         IF    ^CPLFLG5.CPFUNUM,BEGIN                                           
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         TSEG  CPDOUB,9                                                         
         SETMSG CVBLANKS,2                                                      
         END                                                                    
         IF    CPLFLG5.CPFNONUM,'SETMSG CVBLANKS,11'                            
         TSEG  (R1),(R0)                                                        
         XPOP  R0,R1                                                            
         VCALL MIXFMT                                                           
         TCR                                                                    
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
*                                                                               
         DROP  R6                                                               
         TITLE 'SQUASH Command'                                                 
*box                                                                            
*                                                                               
*  SQUASH -- SQUASH command.                                                    
*                                                                               
SQUASH   XPROC ,                                                                
         SET   CPLFLG1.CPFALL,CPLFLG5.CPFNLST  all and nolist                   
         VCALL NDETRNG                                                          
         SCAN  SQUPRT                                                           
         SCANCHK                                                                
*                                                                               
         CLEAR CPWK3               No lines processed yet                       
         RDRNG SQURTN,(DESNRTN+LOCATRTN+UNPRST+DESABORT)                        
         BOMB  ,                   No return                                    
         PEND  ,                                                                
*                                                                               
         SPACE 2                                                                
SQUPRT   SCKW  ,V(LISTPSH),PUSH                                                 
         SCKW  ,V(COLSPSH),PUSH                                                 
         SCKW  ,V(NUMPSH),PUSH                                                  
         SCKW  ,V(NOTVALID)                                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SQURTN -- Co-routine to process lines from the range processor.              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
SQRTWA   RECORD BEGIN                                                           
SQRTLINE DS    XL(&PRESSZ)                                                      
         END                                                                    
*                                                                               
*                                                                               
SQURTN   PROC  SQRTWA                                                           
         MVC   SQRTLINE(&LINESZ),@R1                                            
         LA    R1,SQRTLINE                                                      
         LR    R6,R1                                                            
         LR    R15,R0                                                           
         CLEAR R5                                                               
         IF    (R0,GT,CPLCOL),'LR R5,R0; CEIL R0,CPLCOL'                        
         AH    R1,CPFCOL                                                        
         SH    R0,CPFCOL                                                        
         IF    NP,'LR R1,R6; LR R0,R15; B SQUREP'                               
         VCALL SQSHRTN             Get rid of multiple blanks                   
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LH    R15,CPLCOL                                                       
         SH    R15,CPFCOL                                                       
         SR    R15,R0                                                           
         MOVE  R15,@R2,CVBLANKS    Pad with blanks                              
         LR    R1,R6                                                            
         AH    R0,CPFCOL                                                        
         IF    (R5,NZ),'LR R0,R5'                                               
*                                                                               
SQUREP   CLEAR R2                  Attn flag                                    
         XPUSH R0,R1               Save line ptrs                               
         IF    ^CPLFLG5.CPFNLST,BEGIN                                           
         VCALL LISTLINE                                                         
         IF    ('TWRITE',NZ),'LA R2,1'                                          
         END                                                                    
         XPOP  R0,R1               Line len, loc                                
         L     R15,CPLCNO          Lineno                                       
         VCALL PUTLINE             Replace line                                 
*                                                                               
         IF    (CPWK3,Z),BEGIN     First change, count it...                    
         IF    CPFTYPED,'COUNT CPACHCT'  Count if typed                         
         MVC   CPWK3,=F'1'                                                      
         END                                                                    
*                                                                               
         IF    (R2,NZ),CVABORT                                                  
         PEND  ,                                                                
         TITLE 'COMPARE Command'                                                
COMPWA   RECORD BEGIN                                                           
COMPLN1  DS    F                   Line 1                                       
COMPLN1E DS    F                                                                
COMPLN2  DS    F                   Line 2                                       
COMPLN2E DS    F                                                                
COMPSVL  DS    XL(4*4)             Save copy of line1,line2                     
COMPFLAG FLAG                                                                   
         FLAG  FSINGLE             - single option                              
         FLAG  FREVERSE            - line1 & line2 are reversed                 
COMPLINE DS    XL(&PRESSZ)         Saved copy of line                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  COMPARE -- COMPARE command.                                                  
*                                                                               
COMPARE  XPROC COMPWA                                                           
         LA    R6,COMPWA                                                        
         USING COMPWA,R6                                                        
         CLEAR COMPWA                                                           
         VCALL ORVCPASS            (For Spicomp's COMPILE command)              
*                                                                               
         VCALL ACTCHK              Verify that there is an Active               
*                                                                               
         COMMENT                   SET UP FOR COMPARE LNO CALC'S                
         L     R3,CPACLNO                                                       
         L     R4,CPACLNO                                                       
*                                                                               
COMREST  LABEL ,                                                                
*                                                                               
         COMMENT                   SCAN FOR FIRST LINE                          
         LA    R5,COMPLN1          First lineno                                 
         ST    R3,CPACLNO          for * calculations                           
         SCAN  COMPRT                                                           
         SCANCHK                                                                
         IF    (R15,EQ,8),BEGIN                                                 
         ERROR 'Missing line-number pair for COMPARE.'                          
         END                                                                    
*                                                                               
         COMMENT                   'TO' OPTIONAL                                
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R1                                                             
         IF    (@R1,NE,'TO '),BEGIN                                             
         SCBKUP                                                                 
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR SECOND LINE                         
         LA    R5,COMPLN2          Second lineno                                
         ST    R4,CPACLNO          for * calculations                           
         SCAN  COMPRT                                                           
         SCANCHK                                                                
         IF    (R15,EQ,8),BEGIN                                                 
         ERROR 'Missing second line-number for COMPARE.'                        
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR OTHER OPTIONS                       
         SCAN  COMOPRT             Scan for any list options                    
         SCANCHK                                                                
*                                                                               
         COMMENT                   DO COMPARE                                   
         MVC   CPACLNO,=F'-1000'   Current line not yet set                     
         CLEAR COMPSVL             Init save copy of linenos                    
         CLEAR FREVERSE            Assume line1 > line2                         
         IF    (COMPLN1,GT,COMPLN2),BEGIN  flip ranges                          
         FLIP  (COMPLN1,COMPLN2,8)                                              
         SET   FREVERSE            We reversed the ranges                       
         END                                                                    
         L     R2,COMPLN1          Line 1                                       
         L     R3,COMPLN2          Line 2                                       
         IF    (R2,EQ,R3),BEGIN                                                 
         ERROR 'COMPARE line-numbers are the same.',,COMPSAME                   
         END                                                                    
         IF    ('LT R15,COMPLN1E',NEG),BEGIN                                    
         LR    R15,R3              Start of line2 range                         
         DECR  R15                 Back up                                      
         ST    R15,COMPLN1E        Set up line1 range end                       
         END                                                                    
         IF    (R15,GE,R3),'ERROR "Ranges overlap."'                            
         IF    ('LT R15,COMPLN2E',NEG),BEGIN                                    
         L     R1,CPALLNO          Get LAST line-number                         
         ST    R1,COMPLN2E         Save last line-number                        
         END                                                                    
*                                                                               
         LA    R5,L'DONT           Set length for don't message                 
LOOPA    LA    R1,COMPLN1          Point at 1st line no.                        
         VCALL LOCATE              Locate                                       
         IF    CPDRPT.CPFMTCH,HAVLN1  got line                                  
         MVC   COMPLN1,CPLCNO      Set for next locate                          
         IF    CPDRPT.CPFNTYT+CPFHIGH,COMPBACK  off the end                     
HAVLN1   IF    (COMPLN1,GT,COMPLN1E),COMPBACK  all done                         
         IC    R2,@R15+4           Get line count                               
         LA    R2,@R2+5-1          Total len (in ex form)                       
         EX    R2,'MVC COMPLINE(0),@R15'  save copy of line                     
LOOPB    LA    R1,COMPLN2          Point at 2nd line no.                        
         VCALL LOCATE              Locate                                       
         IF    CPDRPT.CPFMTCH,HAVLN2  lineno matches                            
         MVC   COMPLN2,CPLCNO      Set for next locate                          
         IF    (CPDRPT.CPFNTYT+CPFHIGH,Z),HAVLN2                                
COMPBACK IF    (COMPSVL,Z),'ERROR "No lines found.",,NOLINESX'                  
         MVC   COMPLN1(16),COMPSVL Backup to last compared lines                
         B     COMPDONE            All done                                     
*                                                                               
HAVLN2   IF    (COMPLN2,GT,COMPLN2E),COMPBACK                                   
         IC    R2,COMPLINE+4       Get line 1 count for clc                     
         EX    R2,'CLC @R15+4(0),COMPLINE+4'  compare lines                     
         IF    NE,BEGIN            Not the same...                              
         XPUSH ,,&LINESZ,PTR=R2                                                 
         LR    R1,R2               Unpress area #1                              
         VCALL UNPRESS             Unpress second line                          
         XPUSH ,,&LINESZ,PTR=R1                                                 
         LA    R15,COMPLINE                                                     
         VCALL UNPRESS             Unpress first line                           
         XPOP  ,,&LINESZ*2                                                      
         AH    R2,CPFCOL           Adjust for starting col                      
         AH    R1,CPFCOL                                                        
         LH    R15,CPLCOL                                                       
         SH    R15,CPFCOL          Length for comparison                        
         IF    ('DEX R15,"CLC @R1(0),@R2"',NE),COMPDIFF  not the same           
         END                                                                    
         L     R1,CVCURJCB         Get JCB to test attn                         
         WITH  (JCB,R1),'IF JCBFATTN,COMABORT'  Abort on attn                   
         MVC   COMPSVL,COMPLN1     Save current line1,line2                     
COMNEXT  GEN   'L R0,COMPLN1; LA R1,1; VCALL ADDER; ST R0,COMPLN1'              
         GEN   'L R0,COMPLN2; LA R1,1; VCALL ADDER; ST R0,COMPLN2'              
         B     LOOPA               More                                         
         EJECT                                                                  
COMABORT MVC   CPERRID,=CL8'ABORTCMP'                                           
         TSEG  'Compare stopped.',,CR                                           
COMPDONE CLEAR R5                  zero count so 'don''t ' omitted              
COMPDIFF IF    FREVERSE,BEGIN      Need to flip range back again...             
         FLIP  (COMPLN1,COMPLN2,8)                                              
         END                                                                    
         IF    (R5,NZ),'MVC CPACLNO,COMPLN1'  Set current                       
         IF    CPLFLG5.CPFNLST,CVNEXT                                           
         TSEG  'Lines',,B                                                       
         L     R0,COMPLN1          First lineno                                 
         VCALL CVEXNO              Conv                                         
         TSEG  (R1),(R0),B         Into msg                                     
         TSEG  'and',,B                                                         
         L     R0,COMPLN2          Second lineno                                
         VCALL CVEXNO              Conv                                         
         TSEG  (R1),(R0),B         Into msg                                     
         TSEG  DONT,(R5)           add don't or nothing to msg                  
         TSEG  'compare.',,CR                                                   
         IF    (CPLFLG5.CPFNTEX,AND,^CPLFLG5.CPFUNUM),COMCONT                   
         IF    (R5,Z),CVNEXT       don't display lines if match                 
         LA    R1,COMPLN1          Point to 1st line no                         
COMPLST  VCALL LOCATE              Go locate it                                 
         LA    R1,COMPLINE         Point to unpress area                        
         VCALL UNPRESS             Unpress the line                             
         VCALL LISTLINE            Format line for output                       
         IF    (R5,Z),COMCONT      Finsihed listing lines                       
         TWRITE ,                  No, write out first line                     
         BNZ   CVABORT             Stop if attn                                 
         CLEAR R5                  Set switch for 2nd pass                      
         LA    R1,COMPLN2          Point to second line                         
         B     COMPLST             Go do it                                     
*                                                                               
*                                                                               
*  continue ?  stuff                                                            
*                                                                               
*                                                                               
COMCONT  TWRITE                                                                 
         BNZ   CVABORT                                                          
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'IF JCBFATTN,CVABORT'  Abort if attn                   
         IF    FSINGLE,CVNEXT                                                   
         IF  ((COMPLN1,GE,COMPLN1E),OR,(COMPLN2,GE,COMPLN2E)),CVNEXT            
         SETMSG 'Continue'                                                      
*                                                                               
         VCALL YESRTN              Look at reply                                
         BM    CVABORT                                                          
         IF    POS,'SETMSG "Compare stopped."; B CVNXTMSG'                      
*                                                                               
         LA    R5,L'DONT           RESET DO/DON'T MSG TO DON'T                  
*                                                                               
         COMMENT ,                 DO CONTINUE OR RESTART                       
         SCPUSH ,                  CHECK FOR RESTART OR CONTINUE                
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,POS),BEGIN                                                   
         SCPOP ,                                                                
         L     R3,COMPLN1          SET FOR * LINE NO CALCULATIONS               
         L     R4,COMPLN2                                                       
         CLEAR COMPWA                                                           
         B     COMREST             RESTART COMPARE                              
         END                                                                    
         B     COMNEXT             CONTINUE COMPARE                             
         PEND  ,                                                                
*                                                                               
COMPRT   SCKW  ,COMNORNG,NULL                                                   
         SCKW  ,COMRNG,SYMLN                                                    
         SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  S,COMPSING                                                       
         SCKW  SINGLE,COMPSING,A                                                
         SCKW  ,V(COLSPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
COMOPRT  SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  S,COMPSING                                                       
         SCKW  SINGLE,COMPSING,A                                                
         SCKW  ,V(COLSPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
LNO2PRT  SCKW  ,MISSLNO2,NULL                                                   
         SCKW  ,COMLNO2,SYMLN                                                   
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
COMNORNG PROC  ,                                                                
         LA    R15,8                                                            
         PEND  ,                                                                
*                                                                               
COMRNG   PROC  ,                                                                
         ST    R0,@R5              Save lineno                                  
         MVC   @R5+4(4),=F'-1'                                                  
         SCPUSH                                                                 
         SCAN  ,                   Optional "/<end lineno>"                     
         SCANCHK                                                                
         IF    ((R0,EQ,1),AND,(@R1,EQ,'/')),BEGIN                               
         SCAN  LNO2PRT                                                          
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCPOP ,                                                                
         END                                                                    
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
MISSLNO2 PROC  ,                                                                
         ERROR '/: missing operand.  (Number must follow "/".)'                 
         PEND  ,                                                                
*                                                                               
COMLNO2  PROC  ,                                                                
         IF    ('CL R0,@R5',LT),'ERROR "Range is decreasing."'                  
         ST    R0,@R5+4            Save ending lineno                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
COMPSING PROC  ,                                                                
         SET   FSINGLE                                                          
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
DONT     DC    C'don''t '                                                       
*                                                                               
         QLTORG                                                                 
         TITLE 'COUNT Command'                                                  
CNTWA    RECORD BEGIN                                                           
CNTNO    DS    F                   Number of lines                              
TOTALCNT DS    F                   String count                                 
TOTALLEN DS    H                   Length of string parm                        
TOTALFLG FLAG  ,                   Flags                                        
         FLAG  ANYCASE             - Case independance                          
TOTALSTR DS    CL(&PRESSZ)         String                                       
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  COUNT -- COUNT command.                                                      
*                                                                               
COUNT    XPROC CNTWA                                                            
         LA    R6,CNTWA                                                         
         USING CNTWA,R6                                                         
         ST    R6,CPCWA                                                         
         CLEAR CNTWA                                                            
*                                                                               
         VCALL SCNEXFR             Allow active/exec/from dsname                
         SET   CPLFLG1.CPFALL      Set default all ok                           
         SET   CPFNCUR             Don't update line ptr                        
         VCALL NDETRNG             Get range defined                            
*                                                                               
         COMMENT                   SCAN COUNT OPTIONS                           
         IF    CPFRDEXT,BEGIN      IF EXTERNAL (FROM) DSN,,                     
         VCALL MKFFINFO                                                         
         SCAN  CNTPRTX                                                          
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN               IF ACTIVE OR EXEC ,,,                        
         SCAN  CNTPRT                                                           
         SCANCHK                                                                
         END                                                                    
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         END                                                                    
*-                                                                              
*-      Get line count from AINFO if the total line count is wanted.            
*-                                                                              
         COMMENT                   If from the active file                      
         IF    CPFRDACT,BEGIN      From the Active file...                      
         IF    ^CPFEXPRG,EXIT      Not explicit range, scram                    
         IF    (TOTALLEN,NZ),EXIT                                               
*                                                                               
         L     R15,CPALNCT         Get line count for entire file               
         IF    (R15,Z),EXIT        No lines, forget quick version               
         IF    (CPFSLN,NE,CPAFLNO),EXIT  Entire Active file                     
         IF    (CPLSLN,NE,CPALLNO),EXIT                                         
         ST    R15,CPCOUNT         Save total line count                        
         B     CNTDONE             Re-join common code                          
         END                                                                    
*-                                                                              
*-       CALL RDRANGE TO COUNT TOTAL                                            
*-                                                                              
         IF    (TOTALLEN,Z),BEGIN                                               
         IF    CPFRDEXT,BEGIN      EXTERNAL                                     
         RDRNG COUNTRTN,(LXCATRTN+DESRTRN+DESABORT+UNPRST)                      
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN     EXEC                                         
         RDRNG COUNTRTN,(LEXATRTN+DESRTRN+DESABORT+UNPRST)                      
         END                                                                    
         ELSE  BEGIN               ACTIVE                                       
         RDRNG COUNTRTN,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                      
         END                                                                    
         END                                                                    
*                                                                               
*  WE NEED DIFFERENT CO-ROUTINE IF 'STRING' OPTION SPECIFIED                    
*                                                                               
         ELSE  BEGIN               IE. (TOTALLEN,NZ),BEGIN                      
         IF    CPFRDEXT,BEGIN      EXTERNAL                                     
         RDRNG KOWNTTOT,(LXCATRTN+DESRTRN+LOCATRTN+DESABORT+UNPRST)             
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN     EXEC                                         
         RDRNG KOWNTTOT,(LEXATRTN+DESRTRN+LOCATRTN+DESABORT+UNPRST)             
         END                                                                    
         ELSE  BEGIN               ACTIVE                                       
         RDRNG KOWNTTOT,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                      
         END                                                                    
         END                                                                    
*                                                                               
         L     R15,CNTNO           Get lines counted                            
*                                                                               
*                                                                               
CNTDONE  LR    R2,R15                                                           
         IF    ((R2,Z),AND,^CPFDUMP),'TCLEAR'  Zot "no lines" msg               
         IF    CPLFLG5.CPFNLST,CVNEXT                                           
*                                                                               
         TNUM  (R2)                                                             
         SETMSG ' lines in range.'                                              
         IF    (TOTALLEN,NZ),BEGIN "Total" count ...                            
         TSEG  (R1),(R0)           Put out held text                            
         TSEG  CVBLANKS,2          Put out some blanks                          
         BTD   CPDOUB,0,L:TOTALCNT Get string count                             
         TSEG  (R1),(R0),B         Put out "total" count                        
         SETMSG 'string occurrences.' Put out heading                           
         END                                                                    
         B     CVNXTMSG            Go get next cmd                              
         PEND  ,                                                                
*                                                                               
CNTPRTX  SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
CNTPRT   SCKW  EXECUTE,V(SCNNOOP),A                                             
         SCKW  ACTIVE,V(SCNNOOP),A                                              
         SCKW  STRING,V(SCNNOOP),A                                              
         SCKW  X,XTOTAL,P                                                       
         SCKW  C,CTOTAL,P                                                       
         SCKW  ,V(LTNPSH),PUSH     Allow NOLIST                                 
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,STOTAL                                                          
*                                                                               
XTOTAL   PROC  ,                                                                
         VCALL CKQUOTES                                                         
         LA    R15,TOTALSTR        Point to target area                         
         VCALL HEXSTR              Convert to hex                               
         IF    NEG,'TSEG (R1),(R0); ERROR ": Invalid hex",,INVHEX'              
         IF    Z,'VCALL NOTVALID'  Null string invalid                          
         STH   R0,TOTALLEN         Save length                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
CTOTAL   PROC  ,                                                                
         VCALL CKQUOTES                                                         
         LA    R15,TOTALSTR        Point to target area                         
         VCALL DEQSTR              Dequote the string                           
         IF    NP,'VCALL NOTVALID' Invalid ...                                  
         L     R14,CVUPPTBL        Load ptr to translate table                  
         LR    R15,R0              Copy length                                  
         DEX   R15,'TR @R1(0),@R14'   Translate to upper                        
         SET   ANYCASE             Set flag                                     
         STH   R0,TOTALLEN         Save length                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
STOTAL   PROC  ,                                                                
         VCALL CKQUOTES                                                         
         LA    R15,TOTALSTR        Point to target area                         
         VCALL DEQSTR              Dequote string                               
         IF    NP,'VCALL NOTVALID' Null string invalid                          
         STH   R0,TOTALLEN         Save length                                  
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         SPACE 2                                                                
         EJECT                                                                  
*                                                                               
*  COUNT CO-ROUTINES                                                            
*                                                                               
*                                                                               
         COMMENT                   SIMPLE COUNT CO-ROUTINE                      
COUNTRTN PROC  ,                                                                
         L     R6,CPCWA            Get work area pointer                        
         WITH  (CNTWA,R6),'INCR R2,CNTNO'                                       
         PEND  ,                                                                
*                                                                               
*                                                                               
KNTWA    RECORD BEGIN                                                           
KNTLINE  DS    CL(&PRESSZ)                                                      
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   COUNT CO-ROUTINE W/STRING OPT.               
KOWNTTOT PROC  KNTWA               Regs and space                               
         L     R6,CPCWA            Get work area pointer                        
         USING CNTWA,R6            Make addressable                             
         INCR  R2,CNTNO            Count the line                               
         SPACE 1                                                                
         IF    (ANYCASE,AND,(R0,POS)),BEGIN                                     
         LR    R2,R0               Copy length                                  
         DEX   R2,'MVC KNTLINE(0),@R1'  Move copy of line                       
         L     R14,CVUPPTBL            Load pointer to translate table          
         EX    R2,'TR KNTLINE(0),@R14'  Translate to upper                      
         LA    R1,KNTLINE          Point to translated copy                     
         END                                                                    
         SPACE 1                                                                
         LR    R5,R0               Load line length                             
         LH    R4,TOTALLEN         Load compare length                          
         SR    R5,R4               Compute compare count                        
         INCR  R5                  Adjust for "fence-post" effect               
         BCTR  R4,0                Get machine length for execute               
         WHILE (R5,POS),BEGIN      Do compares                                  
         EX    R4,'CLC 0(0,R1),TOTALSTR'                                        
         IF    EQ,'INCR R15,TOTALCNT'                                           
         INCR  R1                  Skip to next character                       
         DECR  R5                  Decrease compare count                       
         END                                                                    
         PEND  ,                                                                
         DROP  R6                                                               
         SPACE 2                                                                
         QLTORG                                                                 
         TITLE 'SHOW SIZE Command'                                              
SIZWA    RECORD BEGIN                                                           
SIZOPTS  FLAG                                                                   
         FLAG  SIZFLFST            - first pass done                            
         FLAG  SIZFLOV             - show size OVER                             
         FLAG  SIZFLUN             - show size UNDER                            
         FLAG  SIZFLEQ             - show size EQUAL                            
SIZWRK   FLAG                                                                   
*                                                                               
SIZUN    DS    F                   Under length                                 
SIZOV    DS    F                   Over length                                  
SIZLNG   DS    F                   Longest line encountered in range            
SIZSHR   DS    F                   Shortest line encountered                    
         END                                                                    
*box                                                                            
*                                                                               
*  SHOW SIZE command                                                            
*                                                                               
SHOSIZ   XPROC SIZWA                                                            
         LA    R6,SIZWA                                                         
         USING SIZWA,R6            work area                                    
         CLEAR SIZWA               Zero work area                               
         ST    R6,CPWK4            Save work area pointer                       
*                                                                               
         MVC   SIZSHR,=AL4(&LINESZ+1)                                           
         SET   CPLFLG1.CPFALL      default range is 'all'                       
         VCALL NDETRNG             Find range if specified                      
*                                                                               
         SCAN  SHOSIZOP            Scan for a specified option                  
         SCANCHK                                                                
*                                                                               
         IF    SIZFLOV*SIZFLUN,BEGIN   only if over and under given             
         L     R3,SIZUN            Get under size                               
         DECR  R3                  Subtract 1                                   
         IF    (R3,LE,SIZOV),BEGIN     whoops -- cant have this                 
         TSEG  'Can''t have lines',,B                                           
         B     SHOSZMSG            Same as over and under                       
         END                                                                    
         END                                                                    
         MVC   CPACLNO,=F'-1000'   Set current line invalid                     
         SET   CPFNCUR             Quash current line pntr updat                
*                                                                               
         RDRNG DOSHOSZ,(UNPRST+DESRTRN+DESABORT)                                
*                                                                               
         BP    CVERROR             End if attention                             
         IF    ^CPFSOME,CVNEXT     If void range, skip more msgs                
         IF    SIZFLFST,CVNEXT     Got at least one line                        
         TSEG  'No lines',,B                                                    
SHOSZMSG IF    SIZFLOV+SIZFLUN,BEGIN   no lines over, under                     
         IF    SIZFLOV,BEGIN                                                    
         SETMSG 'over'                                                          
         L     R2,SIZOV                                                         
         ACALL SIZVAL                                                           
         IF    SIZFLUN,BEGIN           over and under also                      
         SETMSG ' and under'                                                    
         L     R2,SIZUN                                                         
         ACALL SIZVAL                                                           
         END   ELSE,BEGIN              just over, report longest                
         SETMSG ', longest was'                                                 
         L     R2,SIZLNG                                                        
         ACALL SIZVAL                                                           
         END                                                                    
         END   ELSE,BEGIN              under                                    
         SETMSG 'under'                                                         
         L     R2,SIZUN                                                         
         ACALL SIZVAL                                                           
         SETMSG ', shortest was'                                                
         L     R2,SIZSHR                                                        
         ACALL SIZVAL                                                           
         END                                                                    
         END   ELSE,BEGIN                                                       
         SETMSG 'equal to'                                                      
         L     R2,SIZUN                                                         
         ACALL SIZVAL                                                           
         END                                                                    
         B     CVNEXT              Go get the next command                      
         PEND  ,                                                                
*                                                                               
*                                                                               
SIZVAL   PROC   ,                  Local subroutine                             
         TSEG  (R1),(R0),B         Buffer prefix                                
         TNUM  (R2)                                                             
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
* "SHOW SIZE" scan tables                                                       
*                                                                               
SHOSIZOP SCKW  OVER,HAVEOVER,A                                                  
         SCKW  UNDER,HAVUNDER,A                                                 
         SCKW  EQUAL,HAVEQUAL,A                                                 
         SCKW  IN,HAVLINS                                                       
         SCKW  LINES,HAVLINS,A                                                  
         SCKW  LINS,HAVLINS                                                     
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SIZLEN   SCKW  ,MISSSLEN,NULL                                                   
         SCKW  ,GOTNUM,PI,&LINESZ                                               
         SCKW  LENGTH,GOTLEN,A                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
HAVLINS  PROC  ,                                                                
         IF    CPLFLG1.CPFALL,BEGIN range already specified                     
         VCALL NOTVALID                                                         
         END                                                                    
         SET   CPLFLG1.CPFALL      Allow default explicit                       
         VCALL NDETRNG             Go do range                                  
         IF    ^CPLFLG1.CPFALL,BEGIN no range found                             
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HAVEOVER PROC  ,                                                                
         IF    SIZFLOV+SIZFLEQ,BEGIN conflicting options                        
         VCALL NOTVALID                                                         
         END                                                                    
         SET   SIZFLOV             Set over flag                                
         SCAN  SIZLEN              Scan for <int> or length                     
         SCANCHK                                                                
         ST    R3,SIZOV                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE                                                                  
HAVUNDER PROC  ,                                                                
         IF    SIZFLUN+SIZFLEQ,BEGIN conflicting options                        
         VCALL NOTVALID                                                         
         END                                                                    
         SET   SIZFLUN             Set under flag                               
         SCAN  SIZLEN              Scan for <int> or length                     
         SCANCHK                                                                
         ST    R3,SIZUN                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
HAVEQUAL PROC  ,                                                                
         IF    SIZFLUN+SIZFLOV+SIZFLEQ,BEGIN  conflicting                       
         VCALL NOTVALID                                                         
         END                                                                    
         SET   SIZFLEQ             Set equal flag                               
         SCAN  SIZLEN              Scan for <int> or length                     
         SCANCHK                                                                
         ST    R3,SIZUN                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
MISSSLEN PROC  ,                                                                
         B     CVABSENT                                                         
         PEND  ,                                                                
*                                                                               
GOTLEN   PROC  ,                                                                
         LH    R3,CPLENGTH         Use "set length" value                       
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
GOTNUM   PROC  ,                                                                
         LR    R3,R0                                                            
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
***       show size do routine ----                                             
*                                                                               
*        entry  conditions                                                      
*    R1=pointer to text  R0=text count cplcn0=line # intrnl fmt                 
DOSHOSZ  PROC  ,                                                                
         L     R6,CPWK4            Work address                                 
         USING SIZWA,R6                                                         
         LR    R3,R0                                                            
         IF    (SIZFLOV+SIZFLUN+SIZFLEQ,Z),PUTOUT                               
         IF    (R0,GT,SIZLNG),'ST R0,SIZLNG'   set longest                      
         IF    (R0,LT,SIZSHR),'ST R0,SIZSHR'   set shortest                     
         IF    (R0,GT,SIZOV),'SET SIZWRK.SIZFLOV'                               
         IF    (R0,LT,SIZUN),'SET SIZWRK.SIZFLUN'                               
         IF    (R0,EQ,SIZUN),'SET SIZWRK.SIZFLEQ'                               
         NC    SIZWRK,SIZOPTS      Do conditions hold?                          
         XC    SIZWRK,SIZOPTS                                                   
         IF    (SIZWRK.SIZFLOV+SIZFLUN+SIZFLEQ,NZ),NOTQUITE                     
PUTOUT   L     R0,CPLCNO           Line # to arg reg for cvexno                 
         ST    R0,CPACLNO          Update current line pointer                  
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0),B         Buffer it                                    
         TSEG  '-',,B              Make fancy line                              
         BTD   CPDOUB,0,(R3)       Cnvrt binary text count to bcd               
         TSEG  (R1),(R0),B         Buffer it                                    
         IF    ^SIZFLFST,'TSEG "characters"'                                    
         SET   SIZFLFST            Set not first time flag                      
         TWRITE                                                                 
         BP    CVERROR             Break processing if attention                
NOTQUITE CLEAR  SIZWRK                                                          
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG                                                                 
         TITLE 'MOVE and COPY Commands'                                         
*box                                                                            
*                                                                               
*  COPY -- COPY command.                                                        
*  MOVE -- MOVE command.                                                        
*                                                                               
*                                                                               
*                                                                               
MVWA     RECORD BEGIN                                                           
MVWFLAG1 FLAG   ,                                                               
         FLAG  MVFACT                                                           
         FLAG  MVFEXEC                                                          
         FLAG  MVFFROM                                                          
MVWFLAG2 FLAG  ,                                                                
         FLAG  MVFCOMB             COMBINE                                      
         FLAG  MVFDLT              DELTA SCANNED                                
         FLAG  MVFTO               NO "TO XXX' SCANNED                          
         FLAG  MVFBY               BY                                           
         FLAG  MVFMERG             MERGE                                        
         FLAG  MVFREPL             REPLACE                                      
MVWDELTA DS    F                                                                
MVWNEXTL DS    F                                                                
MVWPLUS  DS    F                                                                
MVWREPT  DS    F                                                                
MVWBEGIN DS    F                                                                
MVWEND   DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
COPY     XPROC ,                                                                
         OI    CPLFLG1,CPFCPY+CPFALL  set copy & default all                    
         ACALL MOVE                                                             
         B     CVNEXT                                                           
         PEND  ,                                                                
*                                                                               
MOVE     XPROC MVWA                                                             
         LA    R6,MVWA                                                          
         USING MVWA,R6                                                          
         ST    R6,CPWK4                                                         
         CLEAR MVWA                                                             
*                                                                               
         COMMENT                   SET DEFAULTS                                 
         SET   CPLFLG5.CPFNLST     Set nolist as default                        
         SET   CPLFLG4.CPFSTINS    Set so stow will only insert                 
         VCALL SCNEXFR             Scan to see if exec or from                  
         IF     CPFRDEXT,' SET MVFFROM'                                         
         ELSEIF CPFRDEXE,' SET MVFEXEC'                                         
         ELSE  'SET MVFACT'                                                     
         VCALL NDETRNG             Define range                                 
         VCALL ENDLNO              End line-number and delta                    
         ST    R0,MVWNEXTL         Set for "end" & no "by"                      
         ST    R1,MVWDELTA                                                      
         MVC   MVWREPT,=A(1)       Init replication factor                      
         OI    CPLFLG1,CPFTODFT    indicate default 'to'                        
         CLEAR MVWPLUS             Init plus value to zero                      
*                                                                               
         COMMENT                   SCAN COMMAND                                 
         IF    MVFFROM,BEGIN                                                    
         VCALL MKFFINFO                                                         
         SCAN  TOPRTX                                                           
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  TOPRT                                                            
         SCANCHK                                                                
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR 'TO' OR 'PLUS'                     
         IF    CPLFLG5.CPFFOPT,'CLEAR CPLFLG5.CPFNLST'                          
         TM    CPLFLG1,CPFCPY      Default "to end" okay                        
         BO    READY               yes, it's copy                               
         IF    MVFTO,READY         IF TO, ALL OK, DO IT TO IT                   
         IF    MVFCOMB,READY       IF COMBINE, ALL OK, DO IT TO IT              
         ERROR 'TO or PLUS must be specified on MOVE.',,MOVESPEC                
*                                                                               
         COMMENT                   SET UP TO DO MOVE/COPY                       
READY    LABEL ,                   * FIX FIX FIX ! *                            
         IF    CPFTYPED,'COUNT CPACHCT'  Count if typed                         
         MVC   MVWEND(4),CVHILNO   Set comparand too high                       
         IF    (^MVFREPL,AND,^MVFMERG),BEGIN IF NOT MERGE, REPLACE              
         TM    MVWNEXTL,X'80'      Illegal value?                               
         IF    O,BEGIN             Yes                                          
         ERROR 'Line number is invalid.'                                        
         END                                                                    
         LA    R1,MVWNEXTL         Point at 1st line no.                        
         VCALL LOCATE              Locate it                                    
         TM    CPDRPT,CPFNTYT+CPFHIGH                                           
         IF    Z,BEGIN             IF THERE IS A LINE AFTER DEST RNG            
         TM    CPDRPT,CPFMTCH      Exact match?                                 
         IF    O,'ACALL BADLINEL'  copy-move won't replace                      
         MVC   MVWEND(4),CPLCNO    Set first illegal line no.                   
         END                                                                    
         END                                                                    
         MVC   MVWBEGIN(4),MVWNEXTL Save start line no.                         
         CLEAR R1                  Zero R1 for later   ??? AX ??                
         SET   CPFNCUR             Set not to update current line ptr           
*                                                                               
         IF    MVFFROM,BEGIN       SPECIAL EXTERNAL FILE SET UP                 
         OI    CPLFLG5,CPFNLST     Set nolist option                            
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN             Do open for external range                   
         IF    (R15,NZ),CVNEXT                                                  
         END                                                                    
*                                                                               
         IF    (^CPLFLG5.CPFNLST,AND,^MVFCOMB),BEGIN                            
         LA    R1,MVWNEXTL         Point to insert line number                  
         VCALL LOCLIST             Go list line before                          
         TWRITE ,                  Write out the line                           
         BP    CVABORT             Stop if attention                            
         END                                                                    
*                                                                               
         MVC   CPACLNO,=F'-1000'   Set no current line yet                      
DESLOOP  LABEL ,                                                                
         IF    MVFACT,BEGIN                                                     
         RDRNG WORK,(DESRTRN+LOCATRTN+PREST+DESABORT)                           
         END                                                                    
         IF    MVFEXEC,BEGIN                                                    
         RDRNG WORK,(DESRTRN+LEXATRTN+PREST+DESABORT)                           
         END                                                                    
         IF    MVFFROM,BEGIN                                                    
         RDRNG WORK,(DESRTRN+LXCATRTN+PREST+DESABORT)                           
         END                                                                    
*                                                                               
         COMMENT                   DO REPLICATION "DITTO" IF ANY                
         L     R1,MVWREPT          Load replication factor                      
         DECR  R1                  We did one ...                               
         ST    R1,MVWREPT          Update count                                 
         IF    (R1,NZ),BEGIN                                                    
         IF    CPFRDEXT,BEGIN      External ...                                 
         VCALL EXTCLOSE            Close the dataset                            
         VCALL EXTOPEN             Re-open (inefficient but safe)               
         IF    (R15,NZ),CVNEXT                                                  
         END   ,                                                                
         B     DESLOOP             And away we go (again)                       
         END   ,                                                                
         VCALL LASTLINE            Show last lineno                             
         B     CVNEXT              New command                                  
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
*  MOVE/COPY CO-ROUTINE                                                         
*                                                                               
*  OLD LOGIC NOT CLEANED UP ...     SAME OLD CODE...                            
*                                                                               
*                                                                               
*  PREST option removed 5/91 by CH.  We press line on entry for                 
*  backwards compatibility.                                                     
*                                                                               
*                                                                               
WORKWA   RECORD BEGIN                                                           
WWBUF    DS    XL(&PRESSZ)                                                      
         END                                                                    
*                                                                               
*                                                                               
WORK     PROC  WORKWA                                                           
*                                                                               
         COMMENT                   BE BACKWARDS COMPAT, PRESS LINE              
         L     R15,CPLCNO                                                       
         VCALL PRESS                                                            
         LR    R15,R1                                                           
*                                                                               
         L     R6,CPWK4                                                         
         L     R5,@R15             Save source line-number                      
         LC    R0,@R15+4           Get prest count                              
         LR    R3,R0                                                            
         LA    R3,@R3+4            To move line no. also                        
         EX    R3,'MVC WWBUF(0),@R15'  Move line                                
*                                                                               
TRYDEL   L     R2,MVWNEXTL         Working position                             
         IF    (MVFMERG,OR,MVFCOMB),MRGECHK  if merge (comb)                    
         CL    R2,MVWEND           Legal?                                       
         BNL   FIXUPP              Too big - try to fix                         
WRKMRGE  IF    ^CPLFLG1.CPFCPY,BEGIN  Move...                                   
         LR    R0,R5               Line-number to delete                        
         LR    R1,R5                                                            
         VCALL DELRANGE            Delete the old line (for MOVE)               
         END                                                                    
*                                                                               
         ST    R2,WWBUF            Set new line no.                             
         ST    R2,CPACLNO          Update current line pointer                  
         LA    R1,WWBUF            Pressed line addr                            
         VCALL STOW                Line into page                               
         BM    BLASTER             Error if try to replace                      
         TM    CPLFLG5,CPFNLST     List option in effect                        
         BO    WORKEX              Branch no                                    
         L     R15,WWBUF           Line number                                  
         XPUSH ,,&LINESZ,PTR=R1    Working line area                            
         VCALL GETLINE             Get line text                                
         VCALL LISTLINE            Format line for listing                      
         XPOP  ,,&LINESZ                                                        
         TWRITE ,                  Write out the line                           
         BP    CMSGLAST            Abort if attention                           
WORKEX   LR    R0,R2               Position used into R0                        
         L     R1,MVWDELTA         Plus delta                                   
         VCALL ADDER               Give                                         
         ST    R0,MVWNEXTL         New position                                 
         B     WORKEXIT            Scram                                        
*                                                                               
*  CODE TO CHECK REPLACE/INTERLEAVE                                             
*                                                                               
MRGECHK  LABEL ,                                                                
         IF    (MVFMERG,AND,^MVFCOMB),MRGECHK1 branch if ,,                     
         L     R2,WWBUF            Use old line number for combine              
         A     R2,MVWPLUS          Add in plus value if any                     
         BM    FIXUPP              Br if negative result - bad                  
MRGECHK1 CL    R2,CVHILNO          Line number o.k.                             
         BL    WRKMRGE             Branch yes to continue                       
FIXUPP   IF    MVFMERG,BLASTER     Merge option?, Abort the copy                
* HELP ??L     R6,CPLCNO           Get current range line no.                   
*                                  save for after fixup                         
         C     R2,MVWBEGIN         Is current line >= 1st line                  
         BL    FIXUPP1             Br no, overlap impossible                    
         C     R2,CPSTLNO          Compare to last line                         
         BNH   BLASTER             Abort if overlap case                        
FIXUPP1  L     R1,MVWDELTA         Get current delta                            
         SRA   R1,1                Halve it                                     
         BZ    BLASTER             Abort if zero                                
         VCALL SELDELTA            Convert it to suitable delta                 
         ST    R1,MVWDELTA         Save new delta                               
*                                                                               
         L     R15,CPSTLNO         Last line saved                              
         A     R15,CVONE                                                        
         VCALL GETLNO              Get next existing lineno                     
         L     R15,CPLCNO                                                       
         DECR  R15                 Last lineno before existing                  
*                                                                               
         XPUSH R2,R3                                                            
         L     R0,MVWBEGIN         R0 = Set first line no. to fix               
         LR    R1,R15              R1 = Set last line to renumber               
         L     R2,MVWDELTA         R2 = delta                                   
         L     R3,=F'-1'           R3 = starting lineno                         
         VCALL NUMRANGE            Re-number lines                              
         ST    R2,MVWDELTA         Delta could have changed again               
         XPOP  R2,R3                                                            
*                                                                               
         IF    (R15,NZ),BLASTER    Couldn't renumber, scram                     
         ST    R0,CPSTLNO          Save last line no.                           
         A     R0,MVWDELTA         Calculate next line no.                      
         ST    R0,MVWNEXTL         Save it for next line                        
         IF    ^CPLFLG5.CPFNLST,BEGIN                                           
         TSEG  'Renumbered by '                                                 
         L     R0,MVWDELTA         Get new delta                                
         VCALL CVEXNO              Convert to external format                   
         TSEG  (R1),(R0),WR        Add to message and write                     
         BP    CMSGLAST            Abort command if attn                        
         END                                                                    
         B     TRYDEL              Go to it again                               
         EJECT                                                                  
*                                                                               
* ERROR EXITS                                                                   
*                                                                               
BLASTER  IF    MVFFROM,BEGIN       IF EXTERNAL FILE, ABORT IT                   
         VCALL EXTCLOSE                                                         
         TSEG  'Copy stopped before external line '                             
         L     R0,WWBUF            Next line no.                                
         VCALL CVEXNO              Convert it                                   
         TSEG  (R1),(R0)           Into msg                                     
         TSEG  '.  '                                                            
         B     CMSGLAST            Abort                                        
         END                                                                    
         IF    MVFMERG,BEGIN                                                    
         TSEG  'Aborted by attempt to replace.'                                 
         VCALL LASTLINE            Show last lineno                             
         B     CVABORT                                                          
         END                                                                    
         TSEG  'Aborted by attempt to replace.'                                 
         TSEG  ' or interleave.  '                                              
         VCALL LASTLINE            Show last lineno                             
         B     CVABORT                                                          
*                                                                               
CMSGLAST VCALL LASTLINE            Show last lineno                             
         B     CVABORT                                                          
*                                                                               
* CO-ROUTINE EXIT                                                               
*                                                                               
WORKEXIT PEND  ,                                                                
         QLTORG                                                                 
         EJECT                                                                  
*-                                                                              
*-       Line already exists.                                                   
*-                                                                              
BADLINEL PROC  ,                                                                
         TSEG  'Line '                                                          
         L     R0,CPLCNO           Get bad line number                          
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         ERROR ' already exists.'                                               
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
*                                                                               
*        For MOVE/COPY.                                                         
*                                                                               
TOPRTX   SCKW  ,V(FNAMPSHF),PUSH                                                
TOPRT    SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  TO,HAVTO                                                         
         SCKW  BY,HAVBY                                                         
         SCKW  FROM,COPYFROM,A                                                  
         SCKW  EXECUTE,COPYEXEC,A                                               
         SCKW  MERGE,SCNMRGE,A                                                  
         SCKW  NOMERGE,SCNNMRGE,A                                               
         SCKW  PLUS,HAVPLUS,A                                                   
         SCKW  COMBINE,HAVCOMB,A                                                
         SCKW  REPLACE,HAVREP,A                                                 
         SCKW  DITTO,SCNDITTO,(A,P,PI),32767                                    
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
TOLNOPRT SCKW  ,MISSLNO,NULL                                                    
         SCKW  ,HAVLNENO,SYMLN                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
BYLNOPRT SCKW  ,MISSLNO,NULL                                                    
         SCKW  ,HAVBY1,SYMLN                                                    
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
PLUSPRT  SCKW  ,MISSLNO,NULL                                                    
         SCKW  ,HAVPLUS1,SYMLN                                                  
         SCKW  -,HAVNEG                                                         
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
MINUSPRT SCKW  ,MISSLNO,NULL                                                    
         SCKW  ,HAVNEG1,SYMLN                                                   
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
MISSLNO  PROC  ,                                                                
         ERROR 'Missing operand.'                                               
         PEND                                                                   
*                                                                               
HAVTO    PROC  ,                                                                
         IF    MVFCOMB,BEGIN                                                    
         ERROR 'COMBINE invalid with TO.'                                       
         END                                                                    
         NI    CPLFLG1,255-CPFTODFT  set 'to' was specified                     
         SET   MVFTO               Set to option given                          
         IC    R3,CPFRDSRC         Save active/exec/from flags                  
         CLEAR CPFRDSRC            Clear so active file used                    
         SCAN  TOLNOPRT            Go get to line                               
         SCANCHK                                                                
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HAVLNENO PROC  ,                                                                
         STC   R3,CPFRDSRC         Restore active/exec/from flags               
         ST    R0,MVWNEXTL         Set line no. & default delta                 
         ST    R1,MVWDELTA                                                      
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
COPYFROM PROC  ,                                                                
         IF    ^CPLFLG1.CPFCPY,BEGIN  move...                                   
         ABORT 'MOVE FROM capability is not supported.'                         
         END                                                                    
         IF    MVFEXEC,BEGIN       Execute option given                         
         ERROR 'FROM invalid with EXEC option.'                                 
         END                                                                    
         VCALL DOFNAME             Get them                                     
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
COPYEXEC PROC  ,                                                                
         IF    ^CPLFLG1.CPFCPY,BEGIN  move...                                   
         ABORT 'MOVE EXEC capability is not supported.'                         
         END                                                                    
         IF    MVFFROM,BEGIN       Execute option given                         
         ERROR 'FROM invalid with EXEC option.'                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
HAVBY    PROC  ,                                                                
         IC    R3,CPFRDSRC         Save active/exec/from flags                  
         CLEAR CPFRDSRC            Clear so active file used                    
         SCAN  BYLNOPRT                                                         
         SCANCHK                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
HAVBY1   PROC  ,                                                                
         ST    R0,MVWDELTA                                                      
         SET   MVFBY                                                            
         IF    (R0,NP),'VCALL NOTVALID'    Delta error                          
         STC   R3,CPFRDSRC         Restore active/exec/from flags               
         IF    MVFCOMB,BEGIN                                                    
         ERROR 'COMBINE invalid with BY.'                                       
         END                                                                    
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
HAVCOMB  PROC  ,                                                                
         IF    MVFTO,BEGIN                                                      
         ERROR 'COMBINE invalid with TO.'                                       
         END                                                                    
         IF    MVFBY,BEGIN                                                      
         ERROR 'COMBINE invalid with BY.'                                       
         END                                                                    
         SET   MVFCOMB                                                          
         SET   MVFMERG                                                          
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HAVREP   PROC  ,                                                                
         SET   MVFMERG                                                          
         SET   MVFREPL               set replace and merge                      
         NI    CPLFLG4,255-CPFSTINS  allow stow to replace                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HAVPLUS  PROC  ,                                                                
         IC    R3,CPFRDSRC         Save active/exec/from flags                  
         CLEAR CPFRDSRC            Clear so active file used                    
         SCAN  PLUSPRT             scan for lineno or '-'                       
         SCANCHK                                                                
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HAVPLUS1 PROC  ,                                                                
         ST    R0,MVWPLUS          Save value                                   
         STC   R3,CPFRDSRC         Restore active/exec/from flags               
         ACALL HAVCOMB             do same as for combine                       
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
HAVNEG   PROC  ,                                                                
         SCAN  MINUSPRT                                                         
         SCANCHK                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
HAVNEG1  PROC  ,                                                                
         LCR   R0,R0               Set value negative                           
         ST    R0,MVWPLUS          Save value                                   
         STC   R3,CPFRDSRC         Restore active/exec/from flags               
         ACALL HAVCOMB             do same as for combine                       
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
SCNMRGE  PROC  ,                                                                
         SET   MVFMERG                                                          
         PEND  ,                                                                
*                                                                               
SCNNMRGE PROC  ,                                                                
         CLEAR MVFMERG                                                          
         PEND  ,                                                                
*                                                                               
SCNDITTO PROC  ,                                                                
         ST    R0,MVWREPT          Save replication factor                      
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG                                                                 
         TITLE 'COPYA Command'                                                  
*box                                                                            
*                                                                               
*  COPYA -- COPYA Command.                                                      
*                                                                               
*    Editorial:  Maybe this command is just temporary.  If                      
*      the COPY/MOVE commands were re-written and the range                     
*      processor was extended to support Multiple Active files                  
*      this command would not be needed.               8/86                     
*                                                                               
COPYAWA  RECORD BEGIN                                                           
COPYAFLG FLAG                                                                   
         FLAG  COPYAFAPPEND        - APPEND                                     
*                                                                               
COPYASRC DS    F                   Source Active file number                    
COPYADST DS    F                   Dest Active file number                      
COPYAOLD DS    F                   Old Active file number                       
         END                                                                    
*-                                                                              
COPYA    XPROC COPYAWA                                                          
         LA    R6,COPYAWA                                                       
         USING COPYAWA,R6                                                       
         ST    R6,CPCWA                                                         
         CLEAR COPYAWA                                                          
*-                                                                              
*-       First look for source Active file number.                              
*-                                                                              
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,Z),CVABSENT                                                  
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         DECR  R15                                                              
         IF    (@R15,NE,':'),BEGIN                                              
         TSEG  (R1),(R0)                                                        
         TSEG  ' is an illegal Active file number.',,CR                         
         ERROR 'Expecting "number:" for Active file specification.'             
         END                                                                    
*                                                                               
         DECR  R0                  Throw away colon suffix                      
         DTB   (R1),(R0)                                                        
         IF    NZ,'VCALL NOTVALID'                                              
         IF    (R15,Z),'VCALL NOTVALID'                                         
         ST    R15,COPYASRC        Source Active file number                    
*-                                                                              
*-       Switch into source Active file.                                        
*-                                                                              
         L     R15,COPYASRC                                                     
         VCALL ACTPICK             Switch to source Active file                 
         IF    Z,BEGIN                                                          
         TSEG  'Active '                                                        
         TNUM  L:COPYASRC                                                       
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         ST    R15,COPYAOLD        Save previous Active file number             
*-                                                                              
*-       Scan range.                                                            
*-                                                                              
         SET   CPLFLG1.CPFALL      Default range is ALL                         
         VCALL NDETRNG             Scan range                                   
*-                                                                              
*-       Scan COPYA options.                                                    
*-                                                                              
         SCAN  COPYAPRT            Scan options                                 
         SCANCHK                                                                
         LT    R15,COPYADST        Check for dest active                        
         IF    Z,BEGIN                                                          
         TSEG  'No destination active file number given.',,CR                   
         ERROR 'Expecting "TO dest-actno:".'                                    
         END                                                                    
*-                                                                              
*-       Process the range (see COPYARTN range co-routine).                     
*-                                                                              
         RDRNG COPYARTN,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                      
*-                                                                              
*-       Switch back into the original Active file.                             
*-                                                                              
         LT    R15,COPYAOLD        Check for old active                         
         VCALL ACTPICK             Switch to source Active file                 
         IF    Z,BEGIN                                                          
         TSEG  'COPYA logic problem.  Original active '                         
         TNUM  L:COPYAOLD                                                       
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       COPYA Options.                                                         
*-                                                                              
COPYAPRT SCKW  TO,COPYATO,P                                                     
         SCKW  APPEND,COPYAAPP,A                                                
         SCKW  ,V(NOTVALID)                                                     
*-                                                                              
COPYATO  PROC                                                                   
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         DECR  R15                                                              
         IF    (@R15,NE,':'),BEGIN                                              
         TSEG  (R1),(R0)                                                        
         TSEG  ' is an illegal Active file number.',,CR                         
         ERROR 'Expecting "number:" for Active file specification.'             
         END                                                                    
*                                                                               
         DECR  R0                  Throw away colon suffix                      
         DTB   (R1),(R0)                                                        
         IF    NZ,'VCALL NOTVALID'                                              
         IF    (R15,Z),'VCALL NOTVALID'                                         
         ST    R15,COPYADST        Source Active file number                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
COPYAAPP PROC                                                                   
         SET   COPYAFAPPEND        Append to end of dest Active file            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COPYARTN -- COPYA range processing co-routine.                               
*                                                                               
COPYARTN PROC                                                                   
         L     R6,CPCWA                                                         
*                                                                               
         XPUSH R0,R1                                                            
*-                                                                              
*-       Switch into destination Active file.                                   
*-                                                                              
         L     R15,COPYADST                                                     
         VCALL ACTPICK             Switch to destination Active file            
         IF    Z,BEGIN                                                          
         TSEG  'Destination active '                                            
         TNUM  L:COPYADST                                                       
         ERROR ' does not exist.'                                               
         END                                                                    
*                                                                               
         LR    R5,R15              Save previous Active file number             
*-                                                                              
*-       Put the line in the destination Active file.                           
*-                                                                              
         L     R15,CPLCNO          Line-number (assumes merge)                  
*                                                                               
         IF    COPYAFAPPEND,BEGIN  Append to the end...                         
         L     R0,CPALLNO          Last lineno                                  
         L     R1,CPDELTA          Delta                                        
         VCALL ADDER                                                            
         LR    R15,R0              New last lineno                              
         END                                                                    
*                                                                               
         XPOP  R0,R1               Line text                                    
         VCALL PUTLINE             Add the line to the destination              
*-                                                                              
*-       Switch back to previous Active file.                                   
*-                                                                              
         LR    R15,R5                                                           
         VCALL ACTPICK             Switch back                                  
         PEND                                                                   
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'EVALUATE Command'                                               
*box                                                                            
*                                                                               
*  EVALUATE -- EVALUATE Command.                                                
*                                                                               
EVALWA   RECORD BEGIN                                                           
EVALFLAG FLAG                                                                   
         FLAG  EVAFSPEC            - Specific option specified                  
*                                                                               
EVALEXPL DS    F                   Expression length                            
EVALEXP  DS    CL256               Expression to evaluate                       
*                                                                               
EVALTSTL DS    F                   Test expr length                             
EVALTEST DS    CL256               Expression that must be TRUE                 
*                                                                               
EVALRES  DS    CL256               Result of expression                         
         END                                                                    
*-                                                                              
EVALUATE XPROC EVALWA                                                           
         LA    R6,EVALWA                                                        
         USING EVALWA,R6                                                        
         ST    R6,CPCWA                                                         
         CLEAR EVALWA                                                           
*                                                                               
         VCALL ORVCPASS            (For Spires's EVAL command)                  
*                                                                               
         VCALL SCNEXFR             Allow active/exec/fromt dsname               
         SET   CPLFLG1.CPFALL      Allow default of all                         
         VCALL NDETRNG             Get range specified                          
*                                                                               
         COMMENT                   If FROM ...                                  
         IF    CPFRDEXT,BEGIN                                                   
         VCALL MKFFINFO                                                         
         SCAN  EVALPRTF                                                         
         SCANCHK                                                                
         IF    ^EVAFSPEC,BEGIN     Missing options...                           
         ERROR 'Missing action for EVALUATE command.'                           
         END                                                                    
         VCALL FNAMEFIN                                                         
         VCALL EXTOPEN             Open file                                    
         IF    (R15,NZ),CVNEXT                                                  
         RDRNG EVALRTN,(DESRTRN+LXCATRTN+DESABORT+UNPRST)                       
         VCALL EXTCLOSE            Close file                                   
         END                                                                    
*                                                                               
         COMMENT                   If EXEC ...                                  
         ELSEIF CPFRDEXE,BEGIN                                                  
         SCAN  EVALPRT                                                          
         SCANCHK                                                                
         IF    ^EVAFSPEC,BEGIN     Missing options...                           
         ERROR 'Missing action for EVALUATE command.'                           
         END                                                                    
         RDRNG EVALRTN,(DESRTRN+LEXATRTN+DESABORT+UNPRST)                       
         VCALL DESPOT                                                           
         END                                                                    
*                                                                               
         COMMENT                   If Active Range (usually is) ,,,             
         ELSE  BEGIN                                                            
         SCAN  EVALPRT                                                          
         SCANCHK                                                                
         IF    ^EVAFSPEC,BEGIN     Missing options...                           
         ERROR 'Missing action for EVALUATE command.'                           
         END                                                                    
         RDRNG EVALRTN,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                       
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         SPACE 3                                                                
*                                                                               
*                                                                               
*                                                                               
*                                                                               
EVALPRTF SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
EVALPRT  SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  EXECUTE,V(SCNNOOP),A                                             
         SCKW  EXPRESSION,EXPR,(A,P)                                            
         SCKW  TEST,TEST,(A,P)                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
EXPR     PROC  ,                                                                
         CEIL  R0,L'EVALEXP                                                     
         ST    R0,EVALEXPL                                                      
         LR    R15,R0                                                           
         MOVE  R15,EVALEXP,@R1     Save expression                              
         SET   EVAFSPEC                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
TEST     PROC  ,                                                                
         CEIL  R0,L'EVALTEST                                                    
         ST    R0,EVALTSTL                                                      
         LR    R15,R0                                                           
         MOVE  R15,EVALTEST,@R1    Save expression                              
         SET   EVAFSPEC                                                         
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  EVALRTN -- EVAL command's range processing co-routine.                       
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
*                                                                               
EVALRTN  PROC                                                                   
         L     R6,CPCWA                                                         
*-                                                                              
*-       Save parameter information where expression can get to it.             
*-                                                                              
         MVC   CPEVLNO,CPLCNO      Save current line-number                     
         ST    R1,CPEVLOC          Current line text loc                        
         ST    R0,CPEVLEN          Current line text len                        
*-                                                                              
*-       Evaluate expression.                                                   
*-                                                                              
         TPUSH                                                                  
*-                                                                              
*-       Do TEST expression first.                                              
*-                                                                              
         SETMSG EVALTEST,L:EVALTSTL                                             
         IF    (R0,POS),BEGIN      We have a TEST expression...                 
         LA    R2,EVALRES                                                       
         LA    R3,L'EVALRES                                                     
         VCALL EVALSTR                                                          
         IF    ((R3,NE,4),OR,(@R2,NE,'TRUE')),EVALRSK                           
         END                                                                    
*-                                                                              
*-       Do the expression.                                                     
*-                                                                              
         SETMSG EVALEXP,L:EVALEXPL                                              
         LA    R2,EVALRES                                                       
         LA    R3,L'EVALRES                                                     
         VCALL EVALSTR             Evaluate expression                          
*-                                                                              
*-       Right now we simply throw away the result of the expression            
*-         and any errors that might have occurred.                             
*-                                                                              
EVALRSK  TPOP  JUNK                                                             
*                                                                               
         PEND                                                                   
         TITLE 'Record Manager Commands'                                        
RLDWA    RECORD BEGIN                                                           
RLDFLAG  FLAG                                                                   
         FLAG  RLDFPUB             - Public record group                        
         FLAG  RLDFUPP             - Convert key to upper case                  
         FLAG  RLDFNDUP            - Don't allow duplicate keys                 
         FLAG  RLDFFREE            - Free form                                  
*                                                                               
RLDSEP   DS    C                   Keyword/Value separator char                 
*                                                                               
RLDMIN   DS    A                   If non-zero: key must be GE this             
RLDMAX   DS    A                   If non-zero: key must be LE this             
*                                                                               
RLDDRG   DS    A                   Record group ptr                             
RLDSETNO DS    F                   Record set number                            
RLDCOUNT DS    F                   Record counter                               
*                                                                               
RLDLPTRS DS    2A                  Working line loc, len                        
*                                                                               
RLDKEY   DS    0CL(CPDATANL)       Working key                                  
RLDKPFX  DS    C                   .prefix (always blank)                       
RLDKDATA DS    CL(CPDATANL-1)      .main key                                    
RLDKCNT  EQU   *-9,9,C'C'          .count suffix                                
*                                                                               
RLDLINE  DS    CL(&LINESZ)         Working line image                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  REC_LOAD -- REC_LOAD Command.                                                
*                                                                               
RECLOAD  XPROC RLDWA                                                            
         LA    R6,RLDWA                                                         
         USING RLDWA,R6                                                         
         ST    R6,CPCWA                                                         
         CLEAR RLDWA                                                            
*                                                                               
         MVI   RLDSEP,C'='         Default separator                            
         LA    R15,CPDATA          Default record group                         
         ST    R15,RLDDRG          Save rg ptr                                  
*                                                                               
         VCALL SCNEXFR             Allow active/exec/fromt dsname               
         SET   CPLFLG1.CPFALL      Default is ALL                               
         VCALL NDETRNG             Get range specified                          
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         SCAN  RLDPRTX                                                          
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  RLDPRT                                                           
         SCANCHK                                                                
         END                                                                    
*                                                                               
         IF    (RLDSETNO,Z),BEGIN  Missing set number...                        
         ERROR 'Missing "INTO setno" for REC_LOAD command.'                     
         END                                                                    
*-                                                                              
*-       Make sure the record set is open.                                      
*-                                                                              
         CLEAR RLDKEY              Set binary zero key                          
         LA    R2,RLDKEY           Key ptr                                      
         L     R15,RLDDRG          Data record group                            
         VCALL XLOCATE             Does our zero record exist?                  
         IF    NZ,BEGIN            No, set is not open...                       
         SETMSG 'Record set '                                                   
         IF    RLDFPUB,'SETMSG "Public record set "'                            
         TSEG  (R1),(R0)                                                        
         TNUM  L:RLDSETNO                                                       
         ERROR ' is not open.'                                                  
         END                                                                    
*-                                                                              
*-       Process the range (see RLDRTN range co-routine).                       
*-                                                                              
         IF    CPFRDEXT,BEGIN                                                   
         RDRNG RLDRTN,(DESRTRN+LXCATRTN+DESABORT+UNPRST)                        
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN                                                  
         RDRNG RLDRTN,(DESRTRN+LEXATRTN+DESABORT+UNPRST)                        
         END                                                                    
         ELSE  BEGIN                                                            
         RDRNG RLDRTN,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                        
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
*-                                                                              
RLDPRTX  SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
RLDPRT   SCKW  ,V(LTNPSH),PUSH                                                  
         SCKW  INTO,RLDINTO,(P,PI)                                              
         SCKW  PUBLIC,RLDPUB,A                                                  
         SCKW  XPUBLIC,RLDPUB,A                                                 
         SCKW  UPPER,RLDUPP,A                                                   
         SCKW  NODUPLICATE,RLDNDUP,A                                            
         SCKW  SEPARATOR,RLDXSEP,(A,P)                                          
         SCKW  FREEFORM,RLDXFF,A                                                
         SCKW  MINIMUM,RLDXMIN,(A,P,PI)                                         
         SCKW  MAXIMUM,RLDXMAX,(A,P,PI)                                         
         SCKW  ACTIVE,V(SCNNOOP),A           (Processed elsewhere)              
         SCKW  EXECUTE,V(SCNNOOP),A          (Prcoessed elsewhere)              
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
RLDINTO  PROC  ,                                                                
         ST    R0,RLDSETNO         Save set number                              
         PEND  ,                                                                
*                                                                               
RLDPUB   PROC                                                                   
         IF    (^CPSFSPR,AND,^CPFXAUTH),BEGIN                                   
         ABORT 'Write access for PUBLIC record groups denied.'                  
         END                                                                    
*                                                                               
         SET   RLDFPUB             Public record group                          
         LA    R15,CVDATA                                                       
         ST    R15,RLDDRG          Set RG ptr                                   
         PEND                                                                   
*                                                                               
RLDUPP   PROC                                                                   
         SET   RLDFUPP                                                          
         PEND                                                                   
*                                                                               
RLDNDUP  PROC                                                                   
         SET   RLDFNDUP                                                         
         PEND                                                                   
*                                                                               
RLDXFF   PROC                                                                   
         SET   RLDFFREE                                                         
         CLEAR RLDSEP                                                           
         PEND                                                                   
*                                                                               
RLDXSEP  PROC                                                                   
         CLEAR RLDFFREE                                                         
         VCALL DEQUOTE             De-quote, if necessary                       
         IF    (R0,NE,1),BEGIN     Must be one character                        
         VCALL NOTVALID                                                         
         END                                                                    
         MVC   RLDSEP,@R1                                                       
         PEND                                                                   
*                                                                               
RLDXMIN  PROC                                                                   
         ST    R0,RLDMIN                                                        
         PEND                                                                   
*                                                                               
RLDXMAX  PROC  ,                                                                
         ST    R0,RLDMAX                                                        
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RLDRTN -- REC_LOAD command's range processing co-routine.                    
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
*                                                                               
RLDRTN   PROC                                                                   
         L     R6,CPCWA                                                         
*-                                                                              
*-       Scan for fixed "keyword=value" lines.                                  
*-                                                                              
         IF    (RLDSEP,NZ),BEGIN   Looking for "key=data"...                    
         LR    R4,R1               Start of line                                
*                                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,RLDSEP),BEGIN  We found the sep char...                  
         LR    R15,R1                                                           
         SR    R15,R4              Length of key                                
         IF    POS,BEGIN                                                        
         MVC   RLDKEY,CVBLANKS     Pre-blank key                                
         CEIL  R15,L'RLDKDATA                                                   
         MOVE  R15,RLDKDATA,@R4    Save key                                     
         IF    RLDFUPP,BEGIN       Convert key to caps...                       
         L     R14,CVUPPTBL                                                     
         IF    (R15,NZ),BEGIN                                                   
         EX    R15,'TR RLDKDATA(0),@R14'                                        
         END                                                                    
         END                                                                    
*                                                                               
         XPUSH R0,R1                                                            
         INCR  R15,RLDCOUNT        Kick counter                                 
         IF    ^RLDFNDUP,BEGIN     Make key unique...                           
         MVC   RLDKCNT,CVBLANKS    Blank count field                            
         BTD   RLDKCNT,,(R15)                                                   
         END                                                                    
*                                                                               
         L     R0,RLDSETNO                                                      
         L     R15,RLDDRG                                                       
         VCALL XSETSET             Set our set number                           
*                                                                               
         XPOP  R0,R1                                                            
*                                                                               
         LA    R1,@R1+1            Skip past separator character                
         DECR  R0                                                               
         LA    R2,RLDKEY           Key                                          
         L     R15,RLDDRG          Data record group                            
         VCALL XPUT                Add record                                   
         BNZ   RLDPERR             Trouble, go complain                         
         B     RLDREXIT            All done with this line                      
         END                                                                    
         END                                                                    
*                                                                               
         LA    R1,@R1+1            Next char                                    
         DECR  R0                                                               
         END                                                                    
*                                                                               
         B     RLDREXIT            No separator char, scram                     
         END                                                                    
*-                                                                              
*-       Scan free-format line--every token is a key with it's data             
*-         being the entire line image.                                         
*-                                                                              
         STM   R0,R1,RLDLPTRS      Save working line ptrs                       
         LR    R15,R0                                                           
         CEIL  R15,L'RLDLINE                                                    
         MOVE  R15,RLDLINE,@R1     Working line image (for SCAN)                
         IF    RLDFUPP,BEGIN                                                    
         L     R14,CVUPPTBL                                                     
         IF    (R15,NZ),BEGIN                                                   
         EX    R15,'TR RLDLINE(0),@R14'                                         
         END                                                                    
         END                                                                    
*                                                                               
         SCINIT RLDLINE,(R0)                                                    
         IF    ^RLDFUPP,'SET CPFSUPLO'                                          
*                                                                               
         SCANSTR ,                                                              
         WHILE ((R15,Z),AND,(R0,POS)),BEGIN                                     
         CLEAR R15                 Assume OK to put                             
         IF    ((RLDMIN,NZ),AND,(R0,LT,RLDMIN)),'LA R15,1'  No go               
         IF    ((RLDMAX,NZ),AND,(R0,GT,RLDMAX)),'LA R15,1'  No go               
*                                                                               
         IF    (R15,Z),BEGIN       Put the record...                            
         MVC   RLDKEY,CVBLANKS                                                  
         LR    R15,R0                                                           
         CEIL  R15,L'RLDKDATA                                                   
         MOVE  R15,RLDKDATA,@R1    Save key data                                
         INCR  R15,RLDCOUNT        Kick counter                                 
         IF    ^RLDFNDUP,BEGIN     Make key unique...                           
         BTD   RLDKCNT,,(R15)                                                   
         END                                                                    
*                                                                               
         L     R0,RLDSETNO                                                      
         L     R15,RLDDRG                                                       
         VCALL XSETSET             Set our set number                           
*                                                                               
         LM    R0,R1,RLDLPTRS      Line image                                   
         LA    R2,RLDKEY           Key                                          
         L     R15,RLDDRG          Data record group                            
         VCALL XPUT                Add record                                   
         IF    NZ,BEGIN            Trouble...                                   
RLDPERR  TSEG  'Can''t add new record at line '                                 
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         ERROR (R1),(R0)                                                        
         END                                                                    
         END                                                                    
         SCANSTR ,                 LOOP THRU LINE                               
         END                                                                    
*                                                                               
RLDREXIT PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'NUMBER Command'                                                 
NUMWA    RECORD BEGIN                                                           
NUMFIRST DS    F                   First line-number in range                   
NUMLAST  DS    F                   Last line-number in range                    
*                                                                               
NUMSTART DS    F                   Start at this line-number (or -1)            
NUMBY    DS    F                   Delta                                        
*                                                                               
NUMAVAL  DS    F                   Addition factor                              
NUMMVAL  DS    F                   Multiplication factor                        
*                                                                               
NUMWLNO  DS    F                   Working lineno (for NUMRTN)                  
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  NUMBER -- NUMBER Command.                                                    
*                                                                               
NUMBER   XPROC NUMWA                                                            
         LA    R6,NUMWA                                                         
         USING NUMWA,R6                                                         
         ST    R6,CPCWA                                                         
         CLEAR NUMWA                                                            
*                                                                               
         MVC   NUMSTART,=F'-1'     No explicit lineno yet                       
*                                                                               
         SET   CPLFLG1.CPFALL      Default range is ALL                         
         VCALL NDETRNG             Scan range                                   
*                                                                               
         SCAN  NUMPRT              Scan options                                 
         SCANCHK                                                                
*                                                                               
         IF    CPFSTRRG,BEGIN      String range...                              
         TSEG  'Can''t re-number a string range.',,CR                           
         TSEG  'Expecting "first-lineno/last-lineno" '                          
         ERROR 'to be specified.'                                               
         END                                                                    
*-                                                                              
*-       Assume it's a simple explicit range.                                   
*-                                                                              
         MVC   NUMFIRST,CPFSLN     First line-number of range                   
         MVC   NUMLAST,CPLSLN      Last line-number of range                    
*-                                                                              
*-     If it's a more complicated range we must go through the range            
*-         to get the first/last line-numbers (or complain if it's              
*-         a disjoint range).                                                   
*-                                                                              
         IF  (CPFDSJNT,OR,(CPLSLN.FRNGTYP,NZ)),BEGIN                            
         MVC   NUMFIRST,=F'-1'     First lineno must be calculated              
         RDRNG NUMRTN,(DESRTRN+LOCATRTN+UNPRST+DESABORT)                        
         END                                                                    
*-                                                                              
*-       Number with a single line-number means re-number all lines             
*-         starting at that line-number.                                        
*-                                                                              
         IF    CPFONEL,BEGIN       Single lineno specified...                   
         IF    (NUMSTART,NE,-1),EXIT  Explicit START specified                  
         MVC   NUMSTART,NUMFIRST   Set starting lineno                          
         CLEAR NUMFIRST            Set range=ALL                                
         MVC   NUMLAST,CVMAXLNO                                                 
         END                                                                    
*-                                                                              
*-       Do some final cleanup and validity checking.                           
*-                                                                              
         IF    ((NUMAVAL,NZ),OR,(NUMMVAL,NZ)),BEGIN                             
         IF    ((NUMBY,NZ),OR,(NUMSTART,NE,-1)),BEGIN                           
         TSEG  'PLUS/TIMES option can not be specified '                        
         ABORT 'if BY/START option is also specified.'                          
         END                                                                    
         END                                                                    
*                                                                               
         IF    (NUMSTART,EQ,-1),BEGIN  Set starting line-number...              
         IF    CPLFLG1.CPFALLV,EXIT  Default of ALL, so scram                   
         MVC   NUMSTART,NUMFIRST   Set explicit START                           
         END                                                                    
*                                                                               
         IF    (NUMFIRST,LE,CPAFLNO),'CLEAR NUMFIRST'                           
         IF    (NUMLAST,GE,CPALLNO),'MVC NUMLAST,CVMAXLNO'                      
*                                                                               
         IF    (NUMSTART,NE,-1),BEGIN  Explicit START specified...              
         IF    (NUMSTART,LE,NUMLAST),EXIT  OK, scram                            
         TSEG  'You can not specify a START line-number '                       
         TSEG  'greater than the LAST line-number.',,CR                         
         ERROR 'No action taken.'                                               
         END                                                                    
*-                                                                              
*-       Re-number the range of lines.                                          
*-                                                                              
         L     R0,NUMFIRST                                                      
         L     R1,NUMLAST                                                       
*                                                                               
         L     R2,NUMAVAL                                                       
         L     R3,NUMMVAL                                                       
         IF    ((R2,NZ),OR,(R3,NZ)),BEGIN  Plus/times...                        
         VCALL NUMFACT             Number plus/times                            
         END                                                                    
*                                                                               
         ELSE  BEGIN               Regular number...                            
         LT    R2,NUMBY            Delta                                        
         IF    Z,'L R2,CPDELTA'                                                 
         LT    R3,NUMSTART         Starting new lineno (or -1)                  
         IF    NEG,BEGIN           Nothing specific...                          
         IF    (NUMFIRST,Z),'LR R3,R2'  Start at delta                          
         END                                                                    
*                                                                               
         VCALL NUMRANGE            Number range                                 
         END                                                                    
*-                                                                              
*-       Process return codes from NUMBER subroutine.                           
*-                                                                              
         IF    ((R15,Z),OR,(R15,EQ,16)),BEGIN  Re-numbered...                   
         IF    CPFTYPED,'INCR R15,CPACHCT'  Count change                        
         CLEAR CPACINFO            Reset collect info                           
         IF    CPFTYPED,BEGIN      Give info message...                         
         LR    R2,R0               Save last lineno                             
         TSEG  'Line '                                                          
         LR    R0,R2                                                            
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  ' is the last line.'                                             
         END                                                                    
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         SETMSG 'Can''t re-number'                                              
         IF    (R15,EQ,4),'SETMSG "PLUS/TIMES value is out of bounds"'          
         IF    (R15,EQ,8),'SETMSG "No lines found"'                             
         IF    (R15,EQ,12),'SETMSG "Bad range"'                                 
         LR    R2,R15                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  ' (Range='                                                       
         L     R0,NUMFIRST                                                      
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  '/'                                                              
         L     R0,NUMLAST                                                       
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
*                                                                               
         IF    (R2,NZ),BEGIN       Show rc...                                   
         IF    (R2,EQ,4),EXIT                                                   
         IF    (R2,EQ,8),EXIT                                                   
         IF    (R2,EQ,12),EXIT                                                  
         TSEG  ', return code='                                                 
         TNUM  (R2)                                                             
         END                                                                    
         TSEG  ').',,CR                                                         
*                                                                               
         B     CVNOACTN                                                         
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NUMRTN -- Number range processing routine.  This routine                     
*    simply stores the first line of the range in "NUMFIRST"                    
*    and the last line in "NUMLAST".  It is only used when                      
*    a non-explicit range is used.                                              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line text loc, len                                               
*      CPLCNO - line-number                                                     
*                                                                               
NUMRTN   PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (NUMWA,R6)                                                       
*                                                                               
         L     R15,CPLCNO          Line-number                                  
*                                                                               
         IF    (NUMFIRST,EQ,-1),BEGIN  First time...                            
         ST    R15,NUMFIRST        Save first lineno                            
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         IF    (R15,NE,NUMWLNO),BEGIN  Disjoint range...                        
         TSEG  'Can''t re-number a discontiguous range of lines.',,CR           
         ERROR 'No action taken.'                                               
         END                                                                    
         END                                                                    
*                                                                               
         ST    R15,NUMLAST         Last lineno (so far)                         
*                                                                               
         A     R15,CVONE                                                        
         VCALL GETLNO              Next lineno                                  
         MVC   NUMWLNO,CPLCNO      Save next expected lineno                    
         PEND                                                                   
         EJECT                                                                  
***                                                                             
***  Number options.                                                            
***                                                                             
NUMPRT   SCKW  BY,NUMXBY                                                        
         SCKW  TIMES,NUMXTIME,A                                                 
         SCKW  PLUS,NUMXPLUS,A                                                  
         SCKW  START,NUMXSTRT,A                                                 
         SCKW  FROM,NUMXSTRT,A                                                  
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*                                                                               
NUMXBY   PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (NUMWA,R6)                                                       
*                                                                               
         ACALL NUMXLNO             Scan lineno value                            
         IF    (R3,NEG),BEGIN                                                   
         ERROR 'Negative DELTA value is not allowed.'                           
         END                                                                    
*                                                                               
         ST    R3,NUMBY            Save DELTA value                             
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
NUMXTIME PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (NUMWA,R6)                                                       
*                                                                               
         ACALL NUMXLNO             Scan lineno value                            
         IF    (R3,NEG),BEGIN                                                   
         ERROR 'Negative TIMES value is not allowed.'                           
         END                                                                    
*                                                                               
         ST    R3,NUMMVAL          Save TIMES value                             
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
NUMXPLUS PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (NUMWA,R6)                                                       
*                                                                               
         ACALL NUMXLNO             Scan lineno value                            
*                                                                               
         ST    R3,NUMAVAL          Save (signed) PLUS value                     
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
NUMXSTRT PROC                                                                   
         L     R6,CPCWA                                                         
         WITH  (NUMWA,R6)                                                       
*                                                                               
         ACALL NUMXLNO             Scan lineno value                            
         IF    (R3,NEG),BEGIN                                                   
         ERROR 'Negative START value is not allowed.'                           
         END                                                                    
*                                                                               
         ST    R3,NUMSTART         Save START value                             
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NUMXLNO -- Local routine to scan the next operand as a                       
*    possibly signed line-number value.                                         
*                                                                               
*    On exit:                                                                   
*       R3 - line-number value                                                  
*                                                                               
NUMXLNO  PROC                                                                   
         SCAN  NUMXPRT             Scan for lineno                              
         SCANCHK                                                                
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
NUMXMISS PROC  ,                                                                
         B     CVABSENT                                                         
         PEND  ,                                                                
*                                                                               
NUMXGOT  PROC  ,                                                                
         LR    R3,R0                                                            
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
NUMXMGOT PROC  ,                                                                
         LNR   R3,R0                                                            
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
NUMXNEG  PROC  ,                                                                
         SCAN  NEGXPRT                                                          
         SCANCHK                                                                
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
*                                                                               
*                                                                               
NUMXPRT  SCKW  ,NUMXMISS,NULL                                                   
         SCKW  -,NUMXNEG                                                        
         SCKW  ,NUMXGOT,SYMLN                                                   
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
NEGXPRT  SCKW  ,NUMXMISS,NULL                                                   
         SCKW  ,NUMXMGOT,SYMLN                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
