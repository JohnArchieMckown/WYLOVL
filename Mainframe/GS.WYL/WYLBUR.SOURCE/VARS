VARS     TITLE 'EXEC Variable Commands, Routines'                               
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         EJECT                                                                  
WYLVARS  CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
VARS     IDENT 9014                19:46:20 01/14/99  (MPD)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         POP   DSECTS                                                           
WYLVARS  CSECT                                                                  
         DC    C'It do it too!'                                                 
         EJECT                                                                  
**                                                                              
**                                                                              
**   RESERVED FOR COMMENTS ON VARIABLE ORGANIZATION                             
**                                                                              
**                                                                              
         SPACE 2                                                                
*-                                                                              
*-       VARIABLE SET NUMBERS                                                   
*-                                                                              
VARINFO  EQU   1                   VARIABLE INFO (NOT USED)                     
VBASE    EQU   2                   BASE VARIABLES                               
VGLOBAL  EQU   3                   GLOABAL VARAIBLES                            
VSTACK   EQU   16                  BEGINING OF VAIABLE STACK                    
MAXSET   EQU   32000               MAX STACK SET NUMBER                         
*$                                                                              
         EJECT                                                                  
         COPY  VNENTRY                                                          
         SPACE 5                                                                
*                                                                               
* ARRAY ELEMENTS NAME DEFINITION                                                
*                                                                               
* (EG. X(2,3) ELEMENT NAME IS: 'NAME<0024>*A<02><03><00>'                       
*  WHERE NAME IS THE FIRST 4 LETTERS OR THE ARRQY NAME, <0024> IS               
*  THE ARRAY NUMBER (A UNIQUE FULLWORD), '*A' A CONSTANT, AND THE               
*  REMAINDER ARE THE SUBSCRIPTS AS HEX HALFWORDS !)                             
*                                                                               
ANAME    EQU   0,4                 ARRAY NAME                                   
AIDENT   EQU   4,4                 ARRAY ID NUMBER                              
ACONST   EQU   8,2                 ARRAY CONSTANT = '*A'                        
ASUB1    EQU   10,2                ARRAY SUBSCRIPT 1                            
ASUB2    EQU   12,2                ARRAY SUBSCRIPT 2                            
ASUB3    EQU   14,2                ARRAY SUBSCRIPT 3                            
         DS    0S(L'VNNAME-16)                                                  
         SPACE 5                                                                
         COPY  VPARM                                                            
WYLVARS  CSECT                                                                  
         TITLE 'INITIALIZE/FREE VARIABLE RECORD GROUP'                          
*box                                                                            
*                                                                               
*                                                                               
*  INITVARS - INITIALIZE OLD EXEC VARIABLES                                     
*                                                                               
*                                                                               
*  CALLED AT USER LOGON.   DESTROYS ANY EXISTING VARAIABLES!                    
*  SETS BASE VARIABLE SET.                                                      
*                                                                               
*                                                                               
INITVARS XPROC ,                                                                
         ACALL INITBASE            INITIALIZE N0-N9, S0-S9, W0-W9               
         LA    R0,VBASE                                                         
         ST    R0,CPVSET#          SET BASE VARIABLE SET                        
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'DECLARE Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  DECLARE - DECLARE VARIABLES                                                  
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - COMMAND W/DECLARE PART SCANNED                                 
*                                                                               
*  ON EXIT:                                                                     
*       WE EXIT TO NEXT COMMAND, (OR ERROR)                                     
*                                                                               
*                                                                               
DCLWA    RECORD BEGIN                                                           
DCLLIMIT EQU   10                  MAXIMUM VARS PER DECLARE                     
DCLTYPE  FLAG  ,                                                                
         FLAG  DCLBOOL             DECLARE BOOLEAN                              
         FLAG  DCLNUMB             DECLARE NUMBER                               
         FLAG  DCLSTR              DECLARE STRING                               
         DS    0F                                                               
DCLLIST  DS    XL((L'VNDESC)*(DCLLIMIT+1))  DECLARE LIST                        
DCLVDESC DS    XL(L'VNDESC)        SAVE AREA                                    
DCLVAR   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
DECLARE  XPROC DCLWA                                                            
         LA    R5,DCLWA                                                         
         USING DCLWA,R5                                                         
         USING VNENTRY,R6                                                       
         LA    R6,DCLVAR                                                        
         CLEAR DCLLIST                                                          
*                                                                               
         COMMENT                   SCAN DECLARE OPTIONS                         
         CLEAR DCLTYPE                                                          
         SCAN DECPRT                  IE. BOOLEAN/NUMBER/STRING                 
         SCANCHK                                                                
         CLEAR R15                                                              
*                                                                               
         IF    (DCLTYPE,EQ,0),BEGIN                                             
         ERROR 'Missing DECLARE type specification.'                            
         END                                                                    
*                                                                               
         COMMENT                   GET LIST OF VARIABLE NAMES                   
         SCINFO ,                  R1,R0 - LOC, LEN OF VAR LIST                 
         IF    (R0,NP),BEGIN                                                    
         ERROR 'Missing variable list for DECLARE.'                             
         END                                                                    
         VCALL LRTRIM                                                           
*                                                                               
         COMMENT                   SCAN THRU VARIABLE LIST                      
         COMMENT                                                                
         LA    R2,DCLLIST          R2 - VAR LIST                                
         CLEAR R3                  R3 - VAR COUNT                               
         WHILE (R0,POS),BEGIN                                                   
         ACALL SCNVDESC                                                         
         BNZ   CVERROR                                                          
         LA    R2,L'VNDESC(R2)                                                  
         LA    R3,1(R3)                                                         
         IF    (R3,GT,DCLLIMIT),BEGIN                                           
         TSEG  'Too many (>10) variables in DECLARE list.  '                    
         ERROR 'Command invalid.'                                               
         END                                                                    
         COMMENT                   SKIP ANY INTERMEDIATE COMMAS                 
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,','),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,';'),AND,(R0,POS)),BEGIN                                
         CLEAR R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK VARIABLE LIST                          
         LA    R2,DCLLIST                                                       
         WHILE (@R2,NE,0),BEGIN                                                 
         LR    R6,R2                                                            
         IF    VNPFSYS,BEGIN       NO SYSTEM VARIABLES                          
         ACALL SEGVNAME                                                         
         ERROR ': invalid.  System variables may not be declared.'              
         END                                                                    
*                                                                               
         IF    VNPFX,BEGIN        CHECK XPROC 'X.' VARIABLES,                   
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN   XPROC MUST BE ACTIVE            
         ACALL SEGVNAME                                                         
         ERROR ': invalid.  No XPROC active.'                                   
         END                                                                    
         END                                                                    
*                                                                               
         IF    VNPFPUB,BEGIN       CHECK PERMITS FOR PUB VARS                   
         IF    ^CPSFSPR,BEGIN                                                   
         ERROR 'Write access for PUBLIC variables denied.'                      
         END                                                                    
         END                                                                    
*                                                                               
         ACALL CHKRESVD            NO RESERVED WORDS                            
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         ERROR ': reserved word.  Invalid variable name.'                       
         END                                                                    
*                                                                               
         MVC   DCLVDESC,VNDESC     NO PREVIOUSLY DEFINED VARIABLES              
         CLEAR VNASUB1               (WE MUST CLEAR SUBSCRIPTS                  
         CLEAR VNASUB2                TO CHECK ARRAY DESCRIPTOR)                
         CLEAR VNASUB3                                                          
         LA    R1,DCLVAR                                                        
         COMMENT @R2 - VAR NAME                                                 
         ACALL GETVAR                                                           
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         ERROR ': invalid.  Variable previously declared.'                      
         END                                                                    
         MVC   VNDESC,DCLVDESC       (SUBSCRIPTS RESTORED)                      
*                                                                               
         LA    R3,DCLLIST          NO DECLARING VARIABLES TWICE                 
         WHILE (R3,LT,R2),BEGIN    CHECK IF VAR NAME APPEARS TWICE              
         CLC   VNNAME,0(R3)                                                     
         IF    E,BEGIN                                                          
         CLC   VNPREFIX,VNPREFIX-VNENTRY(R3)                                    
         IF    E,BEGIN                                                          
         ACALL SEGVNAME                                                         
         ERROR ': invalid.  Variable may not be DECLARED twice.'                
         END                                                                    
         END                                                                    
         LA    R3,L'VNDESC(R3)                                                  
         END                                                                    
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
*                                                                               
         COMMENT                   DECLARE STRING                               
         IF    DCLSTR,BEGIN                                                     
         LA    R6,DCLVAR                                                        
         CLEAR VNSDATA                                                          
         SET   VNSTRING                                                         
         CLEAR VNSTRLEN            SET STRING DEFAULT, ''                       
         LA    R0,4                ALLOW NON-DELCARED SAVE                      
         LA    R1,VNENTRY          R1 - DEFAULT VALUE                           
         LA    R2,DCLLIST          R2 - VARIABLE DESCRIPTOR                     
         WHILE (@R2,NE,0),BEGIN    LOOP DECLARING VARS                          
         LR    R6,R2                                                            
         IF    (VNASUB1,EQ,0),BEGIN  SIMPLE DECLARE                             
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
         END                                                                    
         IF    (VNASUB1,NE,0),BEGIN  ARRAY DECLARE                              
         ACALL DCLARRAY                                                         
         BNZ   CVERROR                                                          
         END                                                                    
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DECLARE NUMBER                               
         ELSEIF DCLNUMB,BEGIN                                                   
         LA    R6,DCLVAR                                                        
         CLEAR VNDATA                                                           
         SET   VNNUMB                                                           
         LA    R3,CVPKZERO                                                      
         MVC   VNVALUE(NL),0(R3)   SET NUMBER DEFAULT, 0 (ZERO)                 
         LA    R0,4                ALLOW NON-DELCARED SAVE                      
         LA    R1,VNENTRY          R1 - DEFAULT VALUE                           
         LA    R2,DCLLIST          R2 - VARIABLE DESCRIPTOR                     
         WHILE (@R2,NE,0),BEGIN    LOOP DECLARING VARS                          
         LR    R6,R2                                                            
         IF    (VNASUB1,EQ,0),BEGIN  SIMPLE DECLARE                             
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
         END                                                                    
         IF    (VNASUB1,NE,0),BEGIN  ARRAY DECLARE                              
         ACALL DCLARRAY                                                         
         BNZ   CVERROR                                                          
         END                                                                    
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DECLARE BOOLEAN                              
         ELSEIF DCLBOOL,BEGIN                                                   
         LA    R6,DCLVAR                                                        
         CLEAR VNDATA                                                           
         SET   VNBOOL                                                           
         LA    R3,CVPKZERO                                                      
         MVC   VNVALUE(NL),0(R3)   SET BOOLEAN DEFAULT, FALSE                   
         LA    R0,4                ALLOW NON-DELCARED SAVE                      
         LA    R1,VNENTRY          R1 - DEFAULT VALUE                           
         LA    R2,DCLLIST          R2 - VARIABLE DESCRIPTOR                     
         WHILE (@R2,NE,0),BEGIN    LOOP DECLARING VARS                          
         LR    R6,R2                                                            
         IF    (VNASUB1,EQ,0),BEGIN  SIMPLE DECLARE                             
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
         END                                                                    
         IF    (VNASUB1,NE,0),BEGIN  ARRAY DECLARE                              
         ACALL DCLARRAY                                                         
         BNZ   CVERROR                                                          
         END                                                                    
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DECLARE STRING                               
         ELSEIF DCLSTR,BEGIN                                                    
         LA    R6,DCLVAR                                                        
         CLEAR VNSDATA                                                          
         SET   VNSTRING                                                         
         CLEAR VNSTRLEN            SET STRING DEFAULT, ''                       
         LA    R1,VNENTRY          R1 - DEFAULT VALUE                           
         LA    R2,DCLLIST          R2 - VARIABLE DESCRIPTOR                     
         WHILE (@R2,NE,0),BEGIN    LOOP DECLARING VARS                          
         LR    R6,R2                                                            
         IF    (VNASUB1,EQ,0),BEGIN  SIMPLE DECLARE                             
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
         END                                                                    
         IF    (VNASUB1,NE,0),BEGIN  ARRAY DECLARE                              
         ACALL DCLARRAY                                                         
         BNZ   CVERROR                                                          
         END                                                                    
         LA    R2,L'VNDESC(R2)                                                  
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF NOT STRING, NUMB, OR BOOLEAN              
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         B     CVNEXT              GET OUT OF HERE BOZO                         
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 5                                                                
DECPRT   SCKW  STRING,DECSTR       DECLARE TYPES                                
         SCKW  STR,DECSTR                                                       
         SCKW  NUMBER,DECNUMB                                                   
         SCKW  NUMB,DECNUMB                                                     
         SCKW  NUM,DECNUMB                                                      
         SCKW  BOOLEAN,DECBOOL                                                  
         SCKW  BOOL,DECBOOL                                                     
         SCKW  ,NOTYPE                                                          
         SPACE 2                                                                
*                                                                               
DECSTR   PROC  ,                   DECLARE STRING                               
         SET   DCLSTR                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
DECNUMB  PROC  ,                   DECLARE NUMBER                               
         SET   DCLNUMB                                                          
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
DECBOOL  PROC  ,                   DECLARE BOOLEAN                              
         SET   DCLBOOL                                                          
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
NOTYPE   PROC  ,                                                                
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid DECLARE type specification.'                          
         PEND  ,                                                                
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DCLARRAY - DECLARE (INITIALIZE) ARRAY                                        
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - DEFAULT VARIABLE VALUE                                             
*      @R2 - VARIABLE NAME DESCRIPTOR (CONTAINS NAME/SUBSCRIPTS)                
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*                                                                               
*      ARRAY DESCRIPTOR SAVED, AND ARRAY INITIALIZED !                          
*                                                                               
*                                                                               
DXWA     RECORD BEGIN                                                           
DXCUR1   DS    H                   CURRENT ARRAY ELEMENT                        
DXCUR2   DS    H                                                                
DXCUR3   DS    H                                                                
DXDIM1   DS    H                   MAXIMUM ARRAY DIMENSIONS                     
DXDIM2   DS    H                                                                
DXDIM3   DS    H                                                                
DXNAME   DS    XL(L'VPARM)         ARRAY DEFINITION ENTRY                       
DXELEM   DS    XL(L'VPARM)         ARRAY ELEMENT ENTRIES                        
DXVAL    DS    XL(L'VPARM)         ARRAY ELEMENT VALUE                          
         END                                                                    
*                                                                               
*                                                                               
DCLARRAY PROC  DXWA                                                             
         USING VNENTRY,R6                                                       
*                                                                               
         COMMENT                   INITIALIZE WORK AREA                         
         CLEAR DXCUR1                                                           
         CLEAR DXCUR2                                                           
         CLEAR DXCUR3                                                           
         LR    R6,R2               SET ARRAY DIMENSIONS                         
         MVC   DXDIM1,VNASUB1                                                   
         MVC   DXDIM2,VNASUB2                                                   
         MVC   DXDIM3,VNASUB3                                                   
         LA    R6,DXNAME           SET ARRAY NAME                               
         MVC   VNDESC(L'VNDESC),0(R2)                                           
         CLEAR VNASUB1                                                          
         CLEAR VNASUB2                                                          
         CLEAR VNASUB3                                                          
         LA    R2,DXVAL            SET ELEMENT VALUE                            
         LA    R3,L'DXVAL                                                       
         LR    R4,R1                                                            
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
*                                                                               
         COMMENT                   CREATE ARRAY DESCRIPTOR                      
         LA    R6,DXNAME                                                        
         CLEAR VNADATA                                                          
         SET   VNARRAY             SET ARRAY TYPE, NAME, DIMESIONS              
         MVC   VNANAME,VNNAME                                                   
         MVC   VNADIM1,DXDIM1                                                   
         MVC   VNADIM2,DXDIM2                                                   
         MVC   VNADIM3,DXDIM3                                                   
         LA    R2,VNENTRY          SET ARRAY RECORD GROUP, SET #                
         ACALL GETVRG                  AND ARRAY ID NUMBER                      
         ST    R0,VNARG                                                         
         LA    R2,VNENTRY                                                       
         ACALL GETVSET                                                          
         ST    R0,VNASET                                                        
         L     R1,CPARRAY#                                                      
         LA    R1,1(R1)                                                         
         ST    R1,CPARRAY#                                                      
         ST    R1,VNAIDENT                                                      
         FAIL  (R1,NP),ARRAY1,'Too many arrays declared. '                      
         LA    R6,DXVAL            SET ARRAY TYPE                               
         IF    VNSTRING,BEGIN                                                   
         LA    R6,DXNAME                                                        
         SET   VNASTR                                                           
         END                                                                    
         ELSEIF VNNUMB,BEGIN                                                    
         LA    R6,DXNAME                                                        
         SET   VNANUMB                                                          
         END                                                                    
         ELSEIF VNBOOL,BEGIN                                                    
         LA    R6,DXNAME                                                        
         SET   VNABOOL                                                          
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE ARRAY DESCRIPTOR                        
         LA    R6,DXNAME           R6 - ARRAY DESCRIPTOR ENTRY                  
         COMMENT                                                                
         LA    R0,4                                                             
         LA    R1,VNENTRY                                                       
         LA    R2,VNENTRY                                                       
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
*                                                                               
         COMMENT                   SET ELEMENT ROOT NAME                        
         LA    R6,DXELEM                                                        
         CLEAR VNENTRY                                                          
         MVC   VNNAME(4),VNNAME-VNENTRY+DXNAME SET ELEMENT NAME                 
         MVC   VNNAME+AIDENT(4),(VNAIDENT-VNENTRY)+DXNAME                       
         MVC   VNNAME+ACONST(L'ACONST),=CL4'*A'                                 
         MVC   VNPREFIX,VNPREFIX-VNENTRY+DXNAME                                 
*                                                                               
         COMMENT                   INITIALIZE 1 DIMENSIONAL ARRAY               
         IF    (DXDIM2,EQ,0),BEGIN                                              
         MVC   DXCUR1(2),=H'1'                                                  
         WHILE (DXCUR1,LE,DXDIM1),BEGIN                                         
         MVC   VNNAME+ASUB1(2),DXCUR1                                           
         LA    R0,4                                                             
         LA    R1,DXVAL                                                         
         LA    R2,VNENTRY                                                       
         ACALL SAVEVAR             SAVE ELEMENTS                                
         BNZ   CVERROR                                                          
         LH    R3,DXCUR1                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR1                                                        
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   INITIALIZE 2 DIMENSIONAL ARRAY               
         ELSEIF (DXDIM3,EQ,0),BEGIN                                             
         MVC   DXCUR2(2),=H'1'                                                  
         WHILE (DXCUR2,LE,DXDIM2),BEGIN                                         
         MVC   DXCUR1(2),=H'1'                                                  
         WHILE (DXCUR1,LE,DXDIM1),BEGIN                                         
         MVC   VNNAME+ASUB1(2),DXCUR1                                           
         MVC   VNNAME+ASUB2(2),DXCUR2                                           
         LA    R0,4                                                             
         LA    R1,DXVAL                                                         
         LA    R2,VNENTRY                                                       
         ACALL SAVEVAR             SAVE ELEMENTS                                
         BNZ   CVERROR                                                          
         LH    R3,DXCUR1                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR1                                                        
         END                                                                    
         LH    R3,DXCUR2                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR2                                                        
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   INITIALIZE 3 DIMENSIONAL ARRAY               
         ELSEIF (DXDIM3,NE,0),BEGIN                                             
         MVC   DXCUR3(2),=H'1'                                                  
         WHILE (DXCUR3,LE,DXDIM3),BEGIN                                         
         MVC   DXCUR2(2),=H'1'                                                  
         WHILE (DXCUR2,LE,DXDIM2),BEGIN                                         
         MVC   DXCUR1(2),=H'1'                                                  
         WHILE (DXCUR1,LE,DXDIM1),BEGIN                                         
         MVC   VNNAME+ASUB1(2),DXCUR1                                           
         MVC   VNNAME+ASUB2(2),DXCUR2                                           
         MVC   VNNAME+ASUB3(2),DXCUR3                                           
         LA    R0,4                                                             
         LA    R1,DXVAL                                                         
         LA    R2,VNENTRY                                                       
         ACALL SAVEVAR             SAVE ELEMENTS                                
         BNZ   CVERROR                                                          
         LH    R3,DXCUR1                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR1                                                        
         END                                                                    
         LH    R3,DXCUR2                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR2                                                        
         END                                                                    
         LH    R3,DXCUR3                                                        
         LA    R3,1(R3)                                                         
         STH   R3,DXCUR3                                                        
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'SET VALUE Command (assignment statement)'                       
*box                                                                            
*                                                                               
*                                                                               
*  SETVAR - SET VARIABLE                                                        
*                                                                               
*  PROCESS VARIABLE ASSIGNMENT STATEMENT                                        
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB POINTS TO ASSIGNMENT STATEMENT,                                  
*       SCTELL RETURNS R1,R0 - LOCATION AND LENGTH OF:                          
*           <VAR NAME> = <EXPRESSION>                                           
*           <ARRAY NAME>(SUBSCRIPTS) = <EXPRESSION>                             
*                                                                               
*  ON EXIT:                                                                     
*       VALUE ASSIGNED TO VARIABLE !!                                           
*       EXIT IS VIA CVNEXT !!!                                                  
*                                                                               
*                                                                               
STVWA    RECORD BEGIN                                                           
STVNAME  DS    XL(L'VPARM)                                                      
STVVAL   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
SETVAL   XPROC STVWA                                                            
         CLEAR STVNAME             STVNAME - PARM AREA FOR NAME                 
         CLEAR STVVAL              STVVAL  - PARM AREA FOR LENGTH               
         COMMENT                                                                
         SCINFO ,                  R1,R0 - LOC LEN OF ASSIGNMENT                
*                                                                               
         COMMENT                   SCAN VAR NAME                                
         LA    R2,STVNAME                                                       
         VCALL SCNVDESC                                                         
         BNZ   CVERROR                                                          
*                                                                               
         COMMENT                   SKIP BLANKS                                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   FIND, SKIP '='                               
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,NE,'='),AND,(R0,POS)),BEGIN                                
         ERROR 'Missing "=" in assignment statement.'                           
         END                                                                    
         IF    ((@R1,EQ,'='),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE EXPRESSION                          
         COMMENT                                                                
         COMMENT                   R1,R0 - EXPR LOC,LEN                         
         LA    R2,STVVAL           R2 - EXPR RETURN AREA                        
         VCALL EVALEXPR                                                         
         BH    CVERROR                                                          
         IF    NEG,BEGIN                                                        
         ERROR 'Missing expression.'                                            
         END                                                                    
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK ,                                                              
         IF    (R0,NZ),BEGIN                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': unrecognized option following expression.'                    
         END                                                                    
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         LA    R0,4                                                             
         LA    R1,STVVAL                                                        
         LA    R2,STVNAME                                                       
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
*                                                                               
         B     CVNEXT              GET OUT OF HERE BUDDY !                      
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
**                                                                              
*$  *** OLD EXEC ***                                                            
**                                                                              
**  OLD SET VALUE COMMAND                                                       
**                                                                              
**  THE 'SET VALUE' COMMAND IS THE OLD WAY TO ASSIGN WYLBUR                     
**  EXEC VARIABLES.   NOW VARIABLES ARE ASSIGNED VALUES WITH                    
**  AN ASSIGNMENT STATEMENT:                                                    
**       <VAR NAME> = <EXPRESSION>                                              
**  THIS ENTRY POINT IS USED TO MAINTAIN COMPATIBILITY.                         
**  (THOUGH AS THE ENTRY CONVENTIONS ARE IDENTICAL THIS                         
**  ENTRY POINT MAY BE AXED AT A LATER DATE.                                    
**                                                                              
**  ON ENTRY:                                                                   
**  ON EXIT:                                                                    
**     ENTRY EXIT SAME AS FOR 'SETVAL' ROUTINE. SEE ABOVE.                      
**                                                                              
**                                                                              
**  NOTES !!!                                                                   
**  THIS CODE IS THE SAME AS 'SETVAL' ROUTINE. EXCEPT THAT:                     
**       1. ONLY N0-N9,S0-S9,W0-W9 ARE ALLOWED                                  
**       2. '=' IS OPTIONAL.                                                    
*                                                                               
OLDSETV  XPROC STVWA                                                            
         CLEAR STVNAME             STVNAME - PARM AREA FOR NAME                 
         CLEAR STVVAL              STVVAL  - PARM AREA FOR LENGTH               
         COMMENT                                                                
         SCINFO ,                  R1,R0 - LOC LEN OF ASSIGNMENT                
*                                                                               
         COMMENT                   CHECK FOR N0-N9,S0-S9,W0-W9                  
         ACALL SCNNSW              SCAN FOR N0-N9,S0-S9,W0-W9                   
         IF    NZ,BEGIN                                                         
         TSEG  'Please use "variable=value" instead of '                        
         ERROR '"SET VAL variable=value".'                                      
         END                                                                    
         COMMENT                   SCAN VAR NAME                                
         LA    R2,STVNAME                                                       
         VCALL SCNVDESC                                                         
         BNZ   CVERROR                                                          
*                                                                               
         COMMENT                   SKIP BLANKS                                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   SKIP ANY '='                                 
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,'='),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE EXPRESSION                          
         COMMENT                                                                
         COMMENT                   R1,R0 - EXPR LOC,LEN                         
         LA    R2,STVVAL           R2 - EXPR RETURN AREA                        
         VCALL EVALEXPR                                                         
         BH    CVERROR                                                          
         IF    NEG,BEGIN                                                        
         ERROR 'Missing expression.'                                            
         END                                                                    
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK ,                                                              
         IF    (R0,NZ),BEGIN                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': unrecognized option following expression.'                    
         END                                                                    
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         LA    R0,4                                                             
         LA    R1,STVVAL                                                        
         LA    R2,STVNAME                                                       
         ACALL SAVEVAR                                                          
         BNZ   CVERROR                                                          
*                                                                               
         B     CVNEXT              GET OUT OF HERE BUDDY !                      
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'SHOW VALUE/VARIABLES/ARRAY Commands'                            
*box                                                                            
*                                                                               
*                                                                               
*  SHOWVAL - SHOW VARIABLE CODE                                                 
*                                                                               
*  NOTE, THERE MAY BE MULTIPLE EXPRESSIONS SEPARATED BY COMMAS.                 
*                                                                               
*                                                                               
*                                                                               
SHVWA    RECORD BEGIN                                                           
SHVVAL   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
SHOWVAL  XPROC SHVWA                                                            
         USING VNENTRY,R6                                                       
         LA    R6,SHVVAL                                                        
         SCINFO ,                  RETURNS R1,R0 - EXPR LOC, LEN                
*-                                                                              
*-       LOOP THRU EXPRESSIONS                                                  
*-                                                                              
         COMMENT                   LOOP EVALUATING/TSEG'NG EXPRNS.              
         CLEAR R4                                                               
         LA    R2,VNENTRY                                                       
         VCALL EVALEXPR            EVALUATE 1ST EXPRESSION                      
         COMMENT                                                                
         WHILE (R15,Z),BEGIN       LOOP LOOP LOOP LOOP                          
         LA    R4,1(R4)                                                         
         ACALL SEGVALUE            TSEG EXPRESSION VALUE                        
         IF    ((R0,POS),AND,(@R1,EQ,',')),BEGIN                                
         LA    R1,1(R1)            SKIP COMMA FOLLOWING EXPRN, IF ANY           
         DECR  R0                                                               
         END                                                                    
         VCALL EVALEXPR            EVALUATE REMAINING EXPRESSIONS               
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   CHECK END OF LOOP CONDITIONS                 
         IF    (R15,POS),BEGIN     ERROR IN EXPRESSION                          
         B     CVERROR                                                          
         END                                                                    
         IF    ((R0,POS),AND,(@R1,EQ,')')),BEGIN     UNMATCHED ')'              
         XPUSH R0,R1                                                            
         TSEG  'Unmatched parenthesis in expression.  '                         
         XPOP  R0,R1                                                            
         VCALL CHECKEND                                                         
         B     CVERROR                                                          
         END                                                                    
         IF    (R4,EQ,0),BEGIN     MISSING 1ST EXPRESSION                       
         ERROR 'Missing expression(s).'                                         
         END                                                                    
         IF    (R4,GE,1),BEGIN     MISSING 2/3/4TH ... EXPRESSION ??            
         IF    ((R0,POS),AND,(@R1,EQ,',')),BEGIN                                
         ERROR 'Missing expression(s).'                                         
         END                                                                    
         END                                                                    
         COMMENT                   IF ALL OK,,, EXIT                            
         B     CVNEXT              GET OUT OF HERE BOZO !                       
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOW VARIABLES                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
SVRWA    RECORD BEGIN ,                                                         
SVRRG    DS    F                   VARIABLE RECORD GROUP INFO PTR               
SVRSET   DS    F                   VARIABLE RECORD GROUP SET NUMBER             
SVRFIRST DS    XL(L'VNNAME)        FIRST VAR TO SHOW                            
SVRLAST  DS    XL(L'VNNAME)        LAST VAR TO SHOW                             
SVRTEMP  DS    XL(L'VPARM)         CURRENT VARIABLE TO SHOW                     
SVRTOK   DS    CL32                SCAN UPPER CASE WORK AREA                    
SVRTRT   DS    XL256               TRT TABLE                                    
         END                                                                    
*                                                                               
*                                                                               
SHOWVARS XPROC SVRWA                                                            
         LA    R5,SVRWA                                                         
         USING SVRWA,R5                                                         
         USING VNENTRY,R6                                                       
         LA    R6,SVRTEMP                                                       
*                                                                               
         COMMENT                   INITIALIZE/SET DEFAULTS                      
         LA    R1,CPVARS           SET LOCAL VARS AS DEFAULT                    
         ST    R1,SVRRG                                                         
         L     R1,CPVSET#                                                       
         ST    R1,SVRSET                                                        
         CLEAR SVRFIRST            ZERO FIRST NAME AS DEFAULT                   
         LA    R2,SVRLAST          X'FF' LAST NAME AS DEFAULT                   
         LA    R3,L'VNNAME                                                      
         L     R15,=X'FF000000'                                                 
         MVCL  R2,R14                                                           
         CLEAR SVRTEMP                                                          
         CLEAR SVRTRT              SET TRT TABLE                                
         MVI   SVRTRT+C'.',4       DON'T PRINT VARS NAMES WITH . OR *           
         MVI   SVRTRT+C'*',4                                                    
*                                                                               
         COMMENT                   SCAN COMMAND                                 
         SCAN  SVRPRT                                                           
         SCANCHK                                                                
*                                                                               
         COMMENT                   SET CORRECT VARIABLE SET NUMBER              
         L     R15,SVRRG                                                        
         L     R0,SVRSET                                                        
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOOP PRINTING VARIABLES                      
         MVC   VNNAME,SVRFIRST                                                  
         L     R15,SVRRG                                                        
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         LA    R2,VNNAME                                                        
         VCALL XGETFRST            GET FIRST VARIABLE IN SET                    
         WHILE (Z,AND,(VNNAME,LE,SVRLAST)),BEGIN  LOOP THRU ALL VARS            
         TRT   VNNAME,SVRTRT       SKIP VARS NAMES WITH . OR *                  
         IF    NZ,BEGIN                                                         
         CLC   VNNAME+ACONST(L'ACONST),=CL4'*A'   IF ARRAY ELEMENT,             
         IF    E,BEGIN                               SKIP ENTIRE ARRAY          
         MVC   VNNAME+ASUB1(2),=XL2'FFFF'                                       
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R2,VNENTRY                                                       
         ACALL SEGVAR              PRINT VARIABLE                               
         TCR                                                                    
         BNZ   CVNEXT              IF ATTENTION HIT, QUICK EXIT !!              
         END                                                                    
         L     R15,SVRRG                                                        
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         LA    R2,VNNAME                                                        
         VCALL XGETNEXT                                                         
         END                                                                    
*                                                                               
         B     CVNEXT              OUT OF HERE BOZO !!                          
*                                                                               
         PEND  ,                                                                
         SPACE 3                                                                
*-                                                                              
*-       SHOW VARS OPTIONS  (SCANNED)                                           
*-                                                                              
SVRPRT   SCKW  TYPE,SVRTYPE,(A,P)                                               
         SCKW  LIKE,SVRLIKE,(A,P)                                               
         SCKW  FROM,SVRFROM,(A,P)                                               
         SCKW  TO,SVRTO,P                                                       
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE 5                                                                
*-                                                                              
*-       TYPE                                                                   
*-                                                                              
SVRTYPE  PROC  ,                                                                
         SCUPCASE SVRTOK,L'SVRTOK  R1,R0 - TYPE LOC, LEN                        
         ACALL SETVTYPE            SET TYPE                                     
         CLEAR R15                                                              
         PEND  ,                                                                
*-                                                                              
*-       LIKE                                                                   
*-                                                                              
SVRLIKE  PROC  ,                                                                
         SCUPCASE SVRTOK,L'SVRTOK  R1,R0 - TYPE LOC, LEN                        
         ACALL SVRPREFX            PROCESS ANY PREFIX                           
         LR    R3,R0                                                            
         MOVE  R3,SVRFIRST,@R1                                                  
         LR    R3,R0                                                            
         MOVE  R3,SVRLAST,@R1                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*-                                                                              
*-       FROM                                                                   
*-                                                                              
SVRFROM  PROC  ,                                                                
         SCUPCASE SVRTOK,L'SVRTOK  R1,R0 - TYPE LOC, LEN                        
         ACALL SVRPREFX            PROCESS ANY PREFIX                           
         LR    R3,R0                                                            
         MOVE  R3,SVRFIRST,@R1                                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*-                                                                              
*-       TO                                                                     
*-                                                                              
SVRTO    PROC  ,                                                                
         SCUPCASE SVRTOK,L'SVRTOK  R1,R0 - TYPE LOC, LEN                        
         ACALL SVRPREFX            PROCESS ANY PREFIX                           
         LR    R3,R0                                                            
         MOVE  R3,SVRLAST,@R1                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-       GET VARIABLE TYPE, SET VARIABLE SET, GROUP INFO                        
*-                                                                              
*-       R1,R0 - KEYWORD LOC, LEN ON ENTRY                                      
*-                                                                              
*-                                                                              
SETVTYPE PROC  ,                                                                
*                                                                               
         COMMENT                   POINT R1 TO UPPER CASED TOKEN                
         SCUPCASE SVRTOK,L'SVRTOK  R1,R0 - TYPE LOC, LEN                        
*                                                                               
         COMMENT                   X. VARS                                      
         IF    (@R1,EQ,'X '),BEGIN                                              
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN                                   
         ERROR 'X.: invalid.  No XPROC active.'                                 
         END                                                                    
         SET   VNPFX                                                            
         ACALL GETXVSET                                                         
         ST    R0,SVRSET                                                        
         END                                                                    
*                                                                               
         COMMENT                   GLOBAL VARIABLES                             
         ELSEIF (@R1,EQ,'GBL '),BEGIN                                           
         SET   VNPFGBL                                                          
         LA    R0,VGLOBAL                                                       
         ST    R0,SVRSET                                                        
         END                                                                    
*                                                                               
         COMMENT                   SYSTEM VARS                                  
         ELSEIF (@R1,EQ,'SYS '),BEGIN                                           
         ERROR 'Unable to display multiple System variables.'                   
         END                                                                    
*                                                                               
         COMMENT                   SESSION VARS                                 
         ELSEIF (@R1,EQ,'SES '),BEGIN                                           
         SET   VNPFSES                                                          
         LA    R0,CPRG16                                                        
         ST    R0,SVRRG                                                         
         LA    R0,CP#SESV                                                       
         ST    R0,SVRSET                                                        
         END                                                                    
*                                                                               
         COMMENT                   PUBLIC VARS                                  
         ELSEIF (@R1,EQ,'PUB '),BEGIN                                           
         SET   VNPFPUB                                                          
         LA    R0,CVRG16                                                        
         ST    R0,SVRRG                                                         
         LA    R0,CV#PUBV                                                       
         ST    R0,SVRSET                                                        
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   ERROR VARS                                   
         ELSEIF ((@R1,EQ,'ERR '),OR,(@R1,EQ,'ERROR')),BEGIN                     
         IF    ^CPFXXERR,BEGIN     IF NO ERROR, NO ERROR VARS                   
         B     CVNEXT              COMMAND DONE                                 
         END                                                                    
         LA    R0,4                                                             
         LA    R1,SVRSET                                                        
         LA    R2,=CL16'SYS.ERRSETNO    '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN            ERR SET # MUST EXIST AND BE NZ               
         BOMB                                                                   
         END                                                                    
         IF ((SVRSET,LE,=A(CP#XOLD)),OR,(SVRSET,GT,CPVSET#)),BEGIN              
         ERROR 'Error did not occur in active PROC.'                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-       HANDLE PREFIXES                                                        
*-                                                                              
*-       R1,R0 - KEYWORD LOC, LEN ON ENTRY                                      
*-       R1,R0 - KEYWORD LOC, LEN WITHOUT PREFIX ON EXIT                        
*-                                                                              
*-                                                                              
SVRPREFX PROC  ,                                                                
         LR    R3,R0               R3 - LENGTH OF KEYWORD                       
*                                                                               
         COMMENT                   X. PREFIX                                    
         IF    ((R3,GE,2),AND,(@R1,EQ,'X.')),BEGIN                              
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN                                   
         ERROR 'X.: invalid.  No XPROC active.'                                 
         END                                                                    
         SET   VNPFX                                                            
         ACALL GETXVSET                                                         
         ST    R0,SVRSET                                                        
         S     R3,=F'2'                                                         
         LA    R1,2(R1)                                                         
         END                                                                    
*                                                                               
         COMMENT                   GBL. PREFIX                                  
         ELSEIF ((R3,GE,4),AND,(@R1,EQ,'GBL.')),BEGIN                           
         SET   VNPFGBL                                                          
         LA    R0,VGLOBAL                                                       
         ST    R0,SVRSET                                                        
         S     R3,=F'4'                                                         
         LA    R1,4(R1)                                                         
         END                                                                    
*                                                                               
         COMMENT                   SYS. PREFIX                                  
         ELSEIF ((R3,GE,4),AND,(@R1,EQ,'SYS.')),BEGIN                           
         ERROR 'Unable to display multiple System variables.'                   
         END                                                                    
*                                                                               
         COMMENT                   SES. PREFIX                                  
         ELSEIF ((R3,GE,4),AND,(@R1,EQ,'SES.')),BEGIN                           
         SET   VNPFSES                                                          
         LA    R0,CPRG16                                                        
         ST    R0,SVRRG                                                         
         LA    R0,CP#SESV                                                       
         ST    R0,SVRSET                                                        
         S     R3,=F'4'                                                         
         LA    R1,4(R1)                                                         
         END                                                                    
*                                                                               
         COMMENT                   PUB. PREFIX                                  
         ELSEIF ((R3,GE,4),AND,(@R1,EQ,'PUB.')),BEGIN                           
         SET   VNPFPUB                                                          
         LA    R0,CVRG16                                                        
         ST    R0,SVRRG                                                         
         LA    R0,CV#PUBV                                                       
         ST    R0,SVRSET                                                        
         S     R3,=F'4'                                                         
         LA    R1,4(R1)                                                         
         END                                                                    
*                                                                               
         LR    R0,R3               R0 - RESTORE UPDATED LENGTH                  
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOWARRY - SHOW ARRAY ELEMENTS                                               
*                                                                               
*                                                                               
*                                                                               
SHYWA    RECORD BEGIN                                                           
SHYCUR1  DS    H                   CURRENT ELEMENT TO DISPLAY                   
SHYCUR2  DS    H                                                                
SHYCUR3  DS    H                                                                
SHYFRST1 DS    H                   FIRST ELEMENT TO DISPLAY                     
SHYFRST2 DS    H                                                                
SHYFRST3 DS    H                                                                
SHYLAST1 DS    H                   LAST ELEMENT TO DISPLAY                      
SHYLAST2 DS    H                                                                
SHYLAST3 DS    H                                                                
SHYDIM1  DS    H                   ARRAY DIMENSIONS                             
SHYDIM2  DS    H                                                                
SHYDIM3  DS    H                                                                
SHYSCNCT DS    F                   SCANNED DIMENSION COUNT                      
SHYARRAY DS    XL(L'VPARM)         ARRAY DESCRIPTOR                             
SHYELEM  DS    XL(L'VPARM)         ELEMENT NAME                                 
         END                                                                    
*                                                                               
*                                                                               
SHOWARRY XPROC SHYWA                                                            
         LA    R5,SHYWA                                                         
         USING SHYWA,R5                                                         
         USING SHYWA,R8                                                         
         USING VNENTRY,R6                                                       
*                                                                               
         COMMENT                   SCAN ARRAY NAME                              
         LA    R6,SHYARRAY                                                      
         SCINFO ,                                                               
         LA    R2,SHYARRAY                                                      
         VCALL SCNVNAME                                                         
         IF    M,BEGIN                                                          
         ERROR 'Missing array name.'                                            
         END                                                                    
         IF    NZ,BEGIN                                                         
         TSEG  VNNAME,L'VNNAME,T                                                
         ERROR ': invalid array name.'                                          
         END                                                                    
         LA    R6,SHYELEM                                                       
         LA    R2,SHYARRAY                                                      
         MVC   VNDESC,VNDESC-VNENTRY(R2)                                        
         SCINIT (R1),(R0)                                                       
*                                                                               
         COMMENT                   GET ARRAY DESCRIPTOR                         
         LA    R6,SHYARRAY                                                      
         LA    R1,SHYARRAY                                                      
         LA    R2,SHYARRAY                                                      
         VCALL GETVAR                                                           
         IF    NZ,BEGIN                                                         
         ACALL SEGVNAME                                                         
         ERROR ': undeclared array.'                                            
         END                                                                    
         IF    ^VNARRAY,BEGIN                                                   
         ACALL SEGVNAME                                                         
         ERROR ': is not an array.'                                             
         END                                                                    
         VCALL SEGVAR                                                           
         TCR                                                                    
*                                                                               
         COMMENT                   SET <FROM> ... <TO> DEFAULTS                 
         LA    R6,SHYARRAY                                                      
         MVC   SHYFRST1,=H'1'                                                   
         MVC   SHYFRST2,=H'1'                                                   
         MVC   SHYFRST3,=H'1'                                                   
         MVC   SHYLAST1,VNADIM1                                                 
         MVC   SHYLAST2,VNADIM2                                                 
         MVC   SHYLAST3,VNADIM3                                                 
         MVC   SHYDIM1,VNADIM1                                                  
         MVC   SHYDIM2,VNADIM2                                                  
         MVC   SHYDIM3,VNADIM3                                                  
*                                                                               
         COMMENT                   SCAN OPTIONS                                 
         MVC   SHYSCNCT,=F'1'                                                   
         SCAN SHYPRT                                                            
         SCANCHK                                                                
*                                                                               
         COMMENT                   SET LOOP LIMITS                              
         IF    (SHYLAST1,GT,SHYDIM1),BEGIN                                      
         MVC   SHYLAST1,SHYDIM1                                                 
         END                                                                    
         IF    (SHYLAST2,GT,SHYDIM2),BEGIN                                      
         MVC   SHYLAST2,SHYDIM2                                                 
         END                                                                    
         IF    (SHYLAST3,GT,SHYDIM3),BEGIN                                      
         MVC   SHYLAST3,SHYDIM3                                                 
         END                                                                    
         MVC   SHYCUR1,SHYFRST1                                                 
         MVC   SHYCUR2,SHYFRST2                                                 
         MVC   SHYCUR3,SHYFRST3                                                 
*                                                                               
         COMMENT                   IF 1 DIMENSION, DO IT TO IT                  
         IF    (SHYDIM2,EQ,0),BEGIN                                             
         LA    R6,SHYELEM                                                       
         WHILE (SHYCUR1,LE,SHYLAST1),BEGIN                                      
         MVC   VNDESC,SHYARRAY                                                  
         MVC   VNASUB1,SHYCUR1                                                  
         COMMENT                   GET ARRAY ELEMENT                            
         LA    R0,SHYARRAY                                                      
         LA    R1,VNENTRY                                                       
         LA    R2,VNENTRY                                                       
         ACALL GETARRAY                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         COMMENT                   PRINT ARRAY ELEMENT                          
         LA    R0,SHYARRAY                                                      
         LA    R2,SHYELEM                                                       
         ACALL SEGELEM                                                          
         TCR                                                                    
         BNZ   SHYDONE                                                          
         LH    R3,SHYCUR1                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR1                                                       
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF 2 DIMENSION, IT DO IT TOO                 
         ELSEIF (SHYDIM3,EQ,0),BEGIN                                            
         LA    R6,SHYELEM                                                       
         MVC   SHYCUR1,SHYFRST1                                                 
         WHILE (SHYCUR1,LE,SHYLAST1),BEGIN                                      
         MVC   SHYCUR2,SHYFRST2                                                 
         WHILE (SHYCUR2,LE,SHYLAST2),BEGIN                                      
         MVC   VNDESC,SHYARRAY                                                  
         MVC   VNASUB1,SHYCUR1                                                  
         MVC   VNASUB2,SHYCUR2                                                  
         COMMENT                   GET ARRAY ELEMENT                            
         LA    R0,SHYARRAY                                                      
         LA    R1,VNENTRY                                                       
         LA    R2,VNENTRY                                                       
         ACALL GETARRAY                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         COMMENT                   PRINT ARRAY ELEMENT                          
         LA    R0,SHYARRAY                                                      
         LA    R2,SHYELEM                                                       
         ACALL SEGELEM                                                          
         TCR                                                                    
         BNZ   SHYDONE                                                          
         LH    R3,SHYCUR2                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR2                                                       
         END                                                                    
         LH    R3,SHYCUR1                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR1                                                       
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF 3 DIMENSION, DO IT TO THE MAX             
         ELSEIF (SHYDIM3,NE,0),BEGIN                                            
         LA    R6,SHYELEM                                                       
         MVC   SHYCUR1,SHYFRST1                                                 
         WHILE (SHYCUR1,LE,SHYLAST1),BEGIN                                      
         MVC   SHYCUR2,SHYFRST2                                                 
         WHILE (SHYCUR2,LE,SHYLAST2),BEGIN                                      
         MVC   SHYCUR3,SHYFRST3                                                 
         WHILE (SHYCUR3,LE,SHYLAST3),BEGIN                                      
         MVC   VNDESC,SHYARRAY                                                  
         MVC   VNASUB1,SHYCUR1                                                  
         MVC   VNASUB2,SHYCUR2                                                  
         MVC   VNASUB3,SHYCUR3                                                  
         COMMENT                   GET ARRAY ELEMENT                            
         LA    R0,SHYARRAY                                                      
         LA    R1,VNENTRY                                                       
         LA    R2,VNENTRY                                                       
         ACALL GETARRAY                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         COMMENT                   PRINT ARRAY ELEMENT                          
         LA    R0,SHYARRAY                                                      
         LA    R2,SHYELEM                                                       
         ACALL SEGELEM                                                          
         TCR                                                                    
         BNZ   SHYDONE                                                          
         LH    R3,SHYCUR3                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR3                                                       
         END                                                                    
         LH    R3,SHYCUR2                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR2                                                       
         END                                                                    
         LH    R3,SHYCUR1                                                       
         LA    R3,1(R3)                                                         
         STH   R3,SHYCUR1                                                       
         END                                                                    
         END                                                                    
*                                                                               
SHYDONE  LABEL ,                                                                
         COMMENT                   BUG OUT OF HERE BUD !!                       
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       SHOW ARRAY OPTIONS  (SCANNED)                                          
*-                                                                              
SHYPRT   SCKW  FROM,SHYFROM,(A,P,PI)                                            
         SCKW  TO,SHYTO,(A,P,PI)                                                
         SCKW  LIKE,SHYLIKE,(A,P,PI)                                            
         SCKW  AND,SHYAND                                                       
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE 5                                                                
*-                                                                              
*-       FROM                                                                   
*-                                                                              
SHYFROM  PROC  ,                                                                
         IF    (SHYSCNCT,EQ,1),BEGIN                                            
         STH   R0,SHYFRST1                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,2),BEGIN                                           
         STH   R0,SHYFRST2                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,3),BEGIN                                           
         STH   R0,SHYFRST3                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       TO                                                                     
*-                                                                              
SHYTO    PROC  ,                                                                
         IF    (SHYSCNCT,EQ,1),BEGIN                                            
         STH   R0,SHYLAST1                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,2),BEGIN                                           
         STH   R0,SHYLAST2                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,3),BEGIN                                           
         STH   R0,SHYLAST3                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       LIKE                                                                   
*-                                                                              
SHYLIKE  PROC  ,                                                                
         IF    (SHYSCNCT,EQ,1),BEGIN                                            
         STH   R0,SHYFRST1                                                      
         STH   R0,SHYLAST1                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,2),BEGIN                                           
         STH   R0,SHYFRST2                                                      
         STH   R0,SHYLAST2                                                      
         END                                                                    
         ELSEIF (SHYSCNCT,EQ,3),BEGIN                                           
         STH   R0,SHYFRST3                                                      
         STH   R0,SHYLAST3                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       AND                                                                    
*-                                                                              
SHYAND   PROC  ,                                                                
         IF    ((SHYSCNCT,EQ,1),AND,(SHYDIM2,NE,0)),BEGIN                       
         L     R3,SHYSCNCT                                                      
         LA    R3,1(R3)                                                         
         ST    R3,SHYSCNCT                                                      
         END                                                                    
         ELSEIF ((SHYSCNCT,EQ,2),AND,(SHYDIM3,NE,0)),BEGIN                      
         L     R3,SHYSCNCT                                                      
         LA    R3,1(R3)                                                         
         ST    R3,SHYSCNCT                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  'Command invalid.  '                                             
         ERROR 'Too many dimension qualifiers specified.'                       
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R5                                                               
         DROP  R6                                                               
         TITLE 'CLEAR VARIABLES Command'                                        
*box                                                                            
*                                                                               
*                                                                               
*  CLRVARS - CLEAR VALUES                                                       
*                                                                               
*  CLEAR ALL EXEC BASE VARIABLES,  VALID ONLY FROM SIMPLE                       
*  EXEC FILE.                                                                   
*                                                                               
*                                                                               
*                                                                               
CLRVARS  XPROC ,                                                                
*$                                                                              
*$  HEY BUD !!!                                                                 
*$  USER MAY NOT CLEAR VARIABLES FROM WITHIN XCALL !! ONLY BASE                 
*$  LEVEL VARIABLES AND GLOBALS CAN BE CLEARED.  THIS ROUTINE                   
*$  CLEARS BASE LEVEL VARIABLES (IE. OLD EXEC AND TERMINAL VARS,                
*$  INCLUDING  N0,S0,W0 ETC.)  IT MAY ONLY BE CALLED FROM OLD                   
*$  EXEC OR A TERMINAL SESSION.  USER MAY CLOBBER GLOBALS AT                    
*$  WILL.  THAT SHOULD MAKE USER HAPPY.                                         
*$                                                                              
         IF    (CPFXOLD,OR,^CPFEXEC),BEGIN                                      
         ACALL INITBASE            CLEAR AND INITIALIZE BASE VARS               
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'Command invalid from within XCALLED procedures.'                
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'GET/SAVE VARIABLES'                                             
*box                                                                            
*                                                                               
*                                                                               
*  GETVAR - GET VARIABLE                                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*       R2 - VARIABLE NAME, DESCRIPTOR.  SEE VNENTRY DSECT.                     
*            (CONTAINS PREFIX FLAGS AND SUBSCRIPTS, IF ANY)                     
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, VARIABLE FOUND                                            
*            CC=NZ, VARIABLE NOT FOUND, SEE CAUTION BELOW !                     
*                                                                               
*  NOTE: R1 AND R2 MAY POINT TO SAME VARIABLE PARM AREA IF                      
*        DESIRED.                                                               
*                                                                               
*  CAUTION: !!!!!!!!!!!!!!!!                                                    
*       THIS ROUTINE IS NOT WILL NOT WORK WITH ARRAY ELEMENTS!                  
*       THIS IS BECAUSE ARRAY ELEMENT MAY FAIL BECAUSE OF                       
*       INVALID SUBSCRIPTS.  USE GET ARRAY INSTEAD IF NEEDED.                   
*       THIS SHOULD NOT BE A PROBLEM AS ONLY CURRENT CALLER IS                  
*       IN EVAL MODULE. ALL OTHERS CALL EVAL TO GET VAR VALUE.                  
*       IT IS BEST TO KEEP IT THAT WAY,, DO NOT BE A BOZO !!                    
*                                                                               
*  INPLEMENTATION NOTE: GETVAR SHUTTLES OFF TO HANDLE EACH PREFIX               
*       VARIABLE TYPE (IE. SES./PUB./GBL./X./ETC ..) WITH THE                   
*       EXCEPTION OF SYS. ALL COULD BE HANDLED USING GETVRG FOR                 
*       RECORD GROUP POINTER AND GETVSET FOR SET NUMBER!  THIS                  
*       COLLAPSING FOR COMMON ROUTINES WAS NOT DONE AS FOR SAVEXVAR             
*       ROUTINES IT STILL APPEARED TO BE SOMEWHAT PAIN IN BUTT.                 
*                                                                               
GETVAR   XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R2                                                            
*                                                                               
         COMMENT                   ARRAY ELEMENTS NOT ALLOWED !                 
         IF    (VNASUB1,NE,0),BEGIN                                             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   LOCAL VARIABLE                               
         IF    (VNPREFIX,EQ,0),BEGIN                                            
         ACALL GETLVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   XPROC 'X.' VARIABLE                          
         ELSEIF VNPFX,BEGIN                                                     
         ACALL GETXVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   GLOBAL VARIABLE                              
         ELSEIF VNPFGBL,BEGIN                                                   
         ACALL GETGVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   SYSTEM VARIABLE                              
         ELSEIF VNPFSYS,BEGIN                                                   
         ACALL GETSVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   SESSION VARIABLE                             
         ELSEIF VNPFSES,BEGIN                                                   
         ACALL GETNVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   PUBLIC VARIABLE                              
         ELSEIF VNPFPUB,BEGIN                                                   
         ACALL GETPVAR                                                          
         END                                                                    
*                                                                               
         COMMENT                   NONE OF THE ABOVE                            
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETLVAR - GET VARIABLE FROM LOCAL (CURRENT) VAR SET                          
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST                                       
*                                                                               
*                                                                               
*                                                                               
GETLVAR  PROC  ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   SET LOCAL VARIABLE SET                       
         LA    R15,CPVARS                                                       
         L     R0,CPVSET#                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET VARIABLE                                 
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         COMMENT , @R2=NAME                                                     
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETXVAR - GET XPROC 'X.' VARIABLE                                            
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST, (OR NOT IN XPROC)                    
*                                                                               
*                                                                               
GETXVAR  XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   IF IN XPROC ACTIVE, GET VAR                  
         IF    (CP#XSET,NE,=A(CP#XOLD)),BEGIN                                   
*                                                                               
         COMMENT                   SET 'X.' VARIABLE SET                        
         LA    R15,CPVARS                                                       
         ACALL GETXVSET                                                         
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET XPROC 'X.' VARIABLE                      
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         COMMENT , @R2=NAME                                                     
         VCALL XGET                                                             
*                                                                               
         END   , OF XPROC ACTIVE                                                
*                                                                               
         COMMENT                   IF XPROC NOT ACTIVE,                         
         ELSE  BEGIN                  TREAT AS VARIABLE NOT FOUND               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETGVAR - GET GLOBAL VARIABLE                                                
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST                                       
*                                                                               
*                                                                               
GETGVAR  XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   SET GLOBAL VARIABLE SET                      
         LA    R15,CPVARS                                                       
         LA    R0,VGLOBAL                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET GLOBAL VARIABLE                          
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         COMMENT , @R2=NAME                                                     
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETSVAR - GET SYSTEM VARIABLE                                                
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST                                       
*                                                                               
*                                                                               
GSVWA    RECORD BEGIN                                                           
GSVARRTN DEFRTNP ,                 SYSTEM ROUTINE TO OBTAIN VALUE               
GSVVALUE DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
GETSVAR  XPROC GSVWA                                                            
         USING VNENTRY,R6                                                       
         LR    R3,R1               R3 - VARIABLE RETURN AREA                    
*                                                                               
         COMMENT                   SEARCH FOR SYSTEM VAR ROUTINE                
         LA    R0,GSVARRTN                                                      
         LR    R1,R2                                                            
         VCALL GTVARRTN                                                         
*                                                                               
         COMMENT                   IF SYSTEM VARIABLE ROUTINE,                  
         IF    (R15,Z),BEGIN                                                    
         CLEAR R0                                                               
         CLEAR R1                                                               
         LA    R6,GSVVALUE         @R6 - WORK AREA FOR SYS VAR                  
         MVC   VNDESC,VNDESC-VNENTRY(R2)                                        
         LA    R14,GSVARRTN        "ACALL' SYSTEM-VAR ROUTINE                   
         CALLRTNP @R14                                                          
*                                                                               
         COMMENT                   SYSTEM VARIABLE RETURNED                     
         IF    (R15,Z),BEGIN                                                    
         MVC   VNDESC,VNDESC-VNENTRY(R2)                                        
         LR    R0,R3                                                            
         LA    R1,L'VPARM                                                       
         LA    R4,VNENTRY                                                       
         LR    R5,R1                                                            
         MVCL  R0,R4               MOVE WORK COPY TO RETURN VAR                 
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETNVAR - GET SESSION (SES.) VARIABLE                                        
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST                                       
*                                                                               
*                                                                               
GETNVAR  XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   SET SESSION VARIABLE SET                     
         LA    R15,CPRG16                                                       
         LA    R0,CP#SESV                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET GLOBAL VARIABLE                          
         LA    R15,CPRG16                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         COMMENT , @R2=NAME                                                     
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETPVAR - GET PUBLIC (PUB.) VARIABLE.                                        
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT                             
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - VARIABLE PARM AREA RETURNED                                        
*      R15 - CC=ZERO, RECORD FOUND                                              
*            CC=NZ, RECORD DOES NOT EXIST                                       
*                                                                               
*                                                                               
GETPVAR  XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   SET GLOBAL VARIABLE SET                      
         LA    R15,CVRG16                                                       
         LA    R0,CV#PUBV                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET GLOBAL VARIABLE                          
         LA    R15,CVRG16                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         COMMENT , @R2=NAME                                                     
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEVAR - SAVE VARIABLE OR ARRAY ELEMENT                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0                                  
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME, DESCRIPTOR.  SEE VNENTRY DSECT.                     
*            (CONTAINS PREFIX FLAGS AND SUBSCRIPTS, IF ANY)                     
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  NOTE: R1 AND R2 MAY POINT TO SAME VNENTRY, IF DESIRED.                       
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ZZZZZ: SYSTEM VARIABLES MAY NOT BE SAVED.                             
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SAVEVAR  XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R2                                                            
*                                                                               
         COMMENT                   LOCAL VARIABLE                               
         IF    ((VNPREFIX,EQ,0),AND,(VNASUB1,EQ,0)),BEGIN                       
         ACALL SAVELVAR                                                         
         END                                                                    
*                                                                               
         COMMENT                   XPROC 'X.' VARIABLE                          
         ELSEIF (VNPFX,AND,(VNASUB1,EQ,0)),BEGIN                                
         ACALL SAVEXVAR                                                         
         END                                                                    
*                                                                               
         COMMENT                   GLOBAL VARIABLE                              
         ELSEIF (VNPFGBL,AND,(VNASUB1,EQ,0)),BEGIN                              
         ACALL SAVEGVAR                                                         
         END                                                                    
*                                                                               
         COMMENT                   SYSTEM VARIABLE                              
         ELSEIF VNPFSYS,BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': system variables may not be saved.'                           
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   SESSION VARIABLE                             
         ELSEIF (VNPFSES,AND,(VNASUB1,EQ,0)),BEGIN                              
         ACALL SAVENVAR                                                         
         END                                                                    
*                                                                               
         COMMENT                   PUBLIC VARIABLE                              
         ELSEIF (VNPFPUB,AND,(VNASUB1,EQ,0)),BEGIN                              
         ACALL SAVEPVAR                                                         
         END                                                                    
*                                                                               
         COMMENT                   ARRAY ELEMENT                                
         ELSEIF (VNASUB1,NE,0),BEGIN                                            
         ACALL SAVARRAY                                                         
         END                                                                    
*                                                                               
         COMMENT                   NONE OF THE ABOVE                            
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVELVAR - SAVE VARIABLE INTO LOCAL (CURRENT) VAR SET                        
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0.                                 
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SVWA     RECORD BEGIN                                                           
SVVDCL   DS    A                   SAVE VAR MUST BE DECLARED IF =0              
SVVAR    DS    A                   SAVE VARIABLE AREA                           
SVNAME   DS    A                   SAVE VARIABLE NAME                           
SVTEMP   DS    XL(L'VPARM)         TEMP VARIABLE AREA                           
         END                                                                    
*                                                                               
*                                                                               
SAVELVAR PROC  SVWA                                                             
         USING VNENTRY,R6                                                       
         ST    R0,SVVDCL                                                        
         ST    R1,SVVAR                                                         
         ST    R2,SVNAME                                                        
         LA    R6,SVTEMP           R6 - PREVIOUS COPY OF VAR, IF ANY            
*                                                                               
         COMMENT                   SET LOCAL VARIABLE SET                       
         LA    R15,CPVARS                                                       
         L     R0,CPVSET#                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET OLD COPY OF VARIABLE                     
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         VCALL XGET                                                             
*                                                                               
         COMMENT                   CHECK IF VARIABLE DECLARED,                  
         IF    (SVVDCL,EQ,0),BEGIN                                              
         IF    (R15,NZ),BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': undeclared. '                                                 
         LA    R15,4                                                            
         B     SAVLDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR RESERVED WORD                      
         IF    (R15,NZ),BEGIN                                                   
         L     R2,SVNAME                                                        
         ACALL CHKRESVD                                                         
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         TSEG  ': reserved word.  Invalid variable name. '                      
         LA    R15,4                                                            
         B     SAVLDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ARBITRATE TYPE                               
         IF    (R15,Z),BEGIN       IF VARIABLE EXISTS,                          
         L     R2,SVVAR                                                         
         IF    VNARRAY,BEGIN       ARRAY TYPE DOES NOT CHANGE                   
         L     R2,SVNAME           ARRAY SAVED ONLY WHEN DECLARED               
         ACALL SEGVNAME                                                         
         TSEG  ': invalid reference to array descriptor. '                      
         LA    R15,4                                                            
         B     SAVLDONE                                                         
         END                                                                    
         ELSEIF VNNUMB,BEGIN       NUMBER                                       
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNSTRING,BEGIN     STRING                                       
         VCALL  MAKESTR                                                         
         END                                                                    
         ELSEIF VNBOOL,BEGIN       BOOLEAN                                      
         VCALL  MAKEBOOL                                                        
         END                                                                    
         ELSE   BEGIN ,            IF NONE OF THE ABOVE,                        
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      IF UNABLE TO CONVERT,,                       
         B     SAVLDONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         END   , OF VARIABLE EXISTS                                             
*                                                                               
         COMMENT                   CHECK AND ZAP N0-N9 !!!                      
         COMMENT                                                                
         COMMENT                   YUCK CITY,, YUCK CITY !!                     
         COMMENT                   ZAP..ZAP..ZAP..ZAP..ZAP..ZAP..               
         VCALL ZAPOFFR6                                                         
         L     R2,SVNAME                                                        
         IF    ((CPVSET#,EQ,VBASE),AND,(@R2,EQ,'N'),                   *        
               AND,(@R2+1,GE,'0'),AND,(@R2+1,LE,'9'),                  X        
               AND,(@R2+2,EQ,' ')),BEGIN                                        
         L     R6,SVVAR                                                         
         L     R0,VNVALUE+NL-8                                                  
         N     R0,=X'000000FF'                                                  
         L     R1,VNVALUE+NL-4                                                  
         N     R1,=X'FFFFFFF0'                                                  
         IF    ((R0,NZ),OR,(R1,NZ)),BEGIN                                       
         NC    VNVALUE+NL-5(5),=X'000000000F'                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         L     R6,SVVAR            R6 - VARIABLE TO SAVE                        
         COMMENT                                                                
         LA    R15,CPVARS          R15 - VAR GROUP INFO                         
         LA    R0,L'VNDATA         R0 - LENGTH OF VAR DATA                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         IF    VNARRAY,BEGIN                                                    
         LA    R0,L'VNADATA                                                     
         END                                                                    
         VCALL XPUT                SAV VARIABLE       !!!!                      
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>30000) defined. '                          
         LA    R15,4                                                            
         B     SAVLDONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVLDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEXVAR - SAVE XPROC 'X.' VARIABLE                                          
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0.                                 
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * XPROC NOT ACTIVE                                                      
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SAVEXVAR PROC  SVWA                                                             
         USING VNENTRY,R6                                                       
         ST    R0,SVVDCL                                                        
         ST    R1,SVVAR                                                         
         ST    R2,SVNAME                                                        
         LA    R6,SVTEMP           R6 - PREVIOUS COPY OF VAR, IF ANY            
*                                                                               
         COMMENT                   CHECK IF XPROC ACTIVE                        
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN                                   
         ACALL SEGVNAME                                                         
         TSEG  ': invalid.  No XPROC active. '                                  
         LA    R15,4                                                            
         B     SAVXDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SET 'X.' VARIABLES                           
         LA    R15,CPVARS                                                       
         ACALL GETXVSET                                                         
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET OLD COPY OF VARIABLE                     
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         VCALL XGET                GET VARIABLE                                 
*                                                                               
         COMMENT                   CHECK IF DECLARED                            
         IF    (SVVDCL,EQ,0),BEGIN                                              
         IF    (R15,NZ),BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': undeclared.'                                                  
         LA    R15,4                                                            
         B     SAVXDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR RESERVED WORD                      
         IF    (R15,NZ),BEGIN                                                   
         L     R2,SVNAME                                                        
         ACALL CHKRESVD                                                         
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         TSEG  ': reserved word.  Invalid variable name. '                      
         LA    R15,4                                                            
         B     SAVXDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ARBITRATE TYPE                               
         IF    (R15,Z),BEGIN       IF VARIABLE EXISTS,                          
         L     R2,SVVAR                                                         
         COMMENT                                                                
         IF    VNARRAY,BEGIN       ARRAY TYPE DOES NOT CHANGE                   
         L     R2,SVNAME           ARRAY SAVED ONLY WHEN DECLARED               
         ACALL SEGVNAME                                                         
         TSEG  ': invalid reference to array descriptor. '                      
         LA    R15,4                                                            
         B     SAVXDONE                                                         
         END                                                                    
         ELSEIF VNNUMB,BEGIN       NUMBER                                       
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNSTRING,BEGIN     STRING                                       
         VCALL  MAKESTR                                                         
         END                                                                    
         ELSEIF VNBOOL,BEGIN       BOOLEAN                                      
         VCALL  MAKEBOOL                                                        
         END                                                                    
         ELSE   BEGIN ,            IF NONE OF THE ABOVE,                        
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      IF UNABLE TO CONVERT,,                       
         B     SAVXDONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         END   , OF VARIABLE EXISTS                                             
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         COMMENT                                                                
         L     R6,SVVAR            R6 - VARIABLE TO SAVE                        
         COMMENT                                                                
         LA    R15,CPVARS          R15 - VAR GROUP INFO                         
         LA    R0,L'VNDATA         R0 - LENGTH OF VAR DATA                      
         LA    R1,VNDATA           R1 - VALUE                                   
         L     R2,SVNAME           R2 - NAME                                    
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         IF    VNARRAY,BEGIN                                                    
         LA    R0,L'VNADATA                                                     
         END                                                                    
         VCALL XPUT                SAV VARIABLE       !!!!                      
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>100000) defined.'                          
         LA    R15,4                                                            
         B     SAVXDONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVXDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEGVAR - SAVE GLOBAL VARIABLE                                              
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0.                                 
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SAVEGVAR PROC  SVWA                                                             
         USING VNENTRY,R6                                                       
         ST    R0,SVVDCL                                                        
         ST    R1,SVVAR                                                         
         ST    R2,SVNAME                                                        
         LA    R6,SVTEMP           R6 - PREVIOUS COPY OF VAR, IF ANY            
*                                                                               
         COMMENT                   SET GLOBAL VARIABLES                         
         LA    R15,CPVARS                                                       
         LA    R0,VGLOBAL                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET OLD COPY OF VARIABLE                     
         LA    R15,CPVARS                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         VCALL XGET                GET VARIABLE                                 
*                                                                               
         COMMENT                   CHECK IF DECLARED                            
         IF    (SVVDCL,EQ,0),BEGIN                                              
         IF    (R15,NZ),BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': undeclared.'                                                  
         LA    R15,4                                                            
         B     SAVGDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR RESERVED WORD                      
         IF    (R15,NZ),BEGIN                                                   
         L     R2,SVNAME                                                        
         ACALL CHKRESVD                                                         
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         TSEG  ': reserved word.  Invalid variable name. '                      
         LA    R15,4                                                            
         B     SAVGDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ARBITRATE TYPE                               
         IF    (R15,Z),BEGIN       IF VARIABLE EXISTS,                          
         L     R2,SVVAR                                                         
         COMMENT                                                                
         IF    VNARRAY,BEGIN       ARRAY TYPE DOES NOT CHANGE                   
         L     R2,SVNAME           ARRAY SAVED ONLY WHEN DECLARED               
         ACALL SEGVNAME                                                         
         TSEG  ': invalid reference to array descriptor. '                      
         LA    R15,4                                                            
         B     SAVGDONE                                                         
         END                                                                    
         ELSEIF VNNUMB,BEGIN       NUMBER                                       
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNSTRING,BEGIN     STRING                                       
         VCALL  MAKESTR                                                         
         END                                                                    
         ELSEIF VNBOOL,BEGIN       BOOLEAN                                      
         VCALL  MAKEBOOL                                                        
         END                                                                    
         ELSE   BEGIN ,            IF NONE OF THE ABOVE,                        
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      IF UNABLE TO CONVERT,,                       
         B     SAVGDONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         END   , OF VARIABLE EXISTS                                             
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         COMMENT                                                                
         L     R6,SVVAR            R6 - VARIABLE TO SAVE                        
         COMMENT                                                                
         LA    R15,CPVARS          R15 - VAR GROUP INFO                         
         LA    R0,L'VNDATA         R0 - LENGTH OF VAR DATA                      
         LA    R1,VNDATA           R1 - VALUE                                   
         L     R2,SVNAME           R2 - NAME                                    
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         IF    VNARRAY,BEGIN                                                    
         LA    R0,L'VNADATA                                                     
         END                                                                    
         VCALL XPUT                SAV VARIABLE       !!!!                      
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>100000) defined.'                          
         LA    R15,4                                                            
         B     SAVGDONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVGDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVENVAR - SAVE SESSION (SES.) VARIABLES                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0.                                 
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ETC,,,                                                                
*                                                                               
*                                                                               
         DS    0S(CPRG16NL-L'VNNAME)  NAME RG16 LEN MUST BE SAME                
         DS    0S(L'VNNAME-CPRG16NL)                                            
*                                                                               
*                                                                               
SAVENVAR PROC  SVWA                                                             
         USING VNENTRY,R6                                                       
         ST    R0,SVVDCL                                                        
         ST    R1,SVVAR                                                         
         ST    R2,SVNAME                                                        
         LA    R6,SVTEMP           R6 - PREVIOUS COPY OF VAR, IF ANY            
*                                                                               
         COMMENT                   SET SESSION VARIABLES                        
         LA    R15,CPRG16                                                       
         LA    R0,CP#SESV                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET OLD COPY OF VARIABLE                     
         LA    R15,CPRG16                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         VCALL XGET                GET VARIABLE                                 
*                                                                               
         COMMENT                   CHECK IF DECLARED                            
         IF    (SVVDCL,EQ,0),BEGIN                                              
         IF    (R15,NZ),BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': undeclared.'                                                  
         LA    R15,4                                                            
         B     SAVNDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR RESERVED WORD                      
         IF    (R15,NZ),BEGIN                                                   
         L     R2,SVNAME                                                        
         ACALL CHKRESVD                                                         
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         TSEG  ': reserved word.  Invalid variable name. '                      
         LA    R15,4                                                            
         B     SAVNDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ARBITRATE TYPE                               
         IF    (R15,Z),BEGIN       IF VARIABLE EXISTS,                          
         L     R2,SVVAR                                                         
         COMMENT                                                                
         IF    VNARRAY,BEGIN       ARRAY TYPE DOES NOT CHANGE                   
         L     R2,SVNAME           ARRAY SAVED ONLY WHEN DECLARED               
         ACALL SEGVNAME                                                         
         TSEG  ': invalid reference to array descriptor. '                      
         LA    R15,4                                                            
         B     SAVNDONE                                                         
         END                                                                    
         ELSEIF VNNUMB,BEGIN       NUMBER                                       
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNSTRING,BEGIN     STRING                                       
         VCALL  MAKESTR                                                         
         END                                                                    
         ELSEIF VNBOOL,BEGIN       BOOLEAN                                      
         VCALL  MAKEBOOL                                                        
         END                                                                    
         ELSE   BEGIN ,            IF NONE OF THE ABOVE,                        
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      IF UNABLE TO CONVERT,,                       
         B     SAVNDONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         END   , OF VARIABLE EXISTS                                             
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         COMMENT                                                                
         L     R6,SVVAR            R6 - VARIABLE TO SAVE                        
         COMMENT                                                                
         LA    R15,CPRG16          R15 - VAR GROUP INFO                         
         LA    R0,L'VNDATA         R0 - LENGTH OF VAR DATA                      
         LA    R1,VNDATA           R1 - VALUE                                   
         L     R2,SVNAME           R2 - NAME                                    
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         IF    VNARRAY,BEGIN                                                    
         LA    R0,L'VNADATA                                                     
         END                                                                    
         VCALL XPUT                SAV VARIABLE       !!!!                      
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>100000) defined.'                          
         LA    R15,4                                                            
         B     SAVNDONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVNDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEPVAR - SAVE PUBLIC (PUB.) VARIABLES                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE MUST BE DECLARED IF R0=0.                                 
*      @R1 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*      @R2 - VARIABLE NAME                                                      
*                                                                               
*      R15 - CC=ZERO, RECORD SAVED                                              
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARAIBLES SAVED (>30000)                                     
*       * XXXXX: RESERVED WORD.  INVALID VARIABLE NAME.                         
*       * YYYYY: UNDECLARED.                                                    
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SAVEPVAR PROC  SVWA                                                             
         USING VNENTRY,R6                                                       
         ST    R0,SVVDCL                                                        
         ST    R1,SVVAR                                                         
         ST    R2,SVNAME                                                        
         LA    R6,SVTEMP           R6 - PREVIOUS COPY OF VAR, IF ANY            
*                                                                               
         COMMENT                   CHECK FOR SYSTEM PRIVLEGES                   
         IF    (CPSFSPR,OR,CPFXAUTH),BEGIN                                      
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  'Write access for PUBLIC variables denied. '                     
         LA    R15,4                                                            
         B     SAVPDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SET PUBLIC VARIABLES                         
         LA    R15,CVRG16                                                       
         LA    R0,CV#PUBV                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET OLD COPY OF VARIABLE                     
         LA    R15,CVRG16                                                       
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         L     R2,SVNAME                                                        
         VCALL XGET                GET VARIABLE                                 
*                                                                               
         COMMENT                   CHECK IF DECLARED                            
         IF    (SVVDCL,EQ,0),BEGIN                                              
         IF    (R15,NZ),BEGIN                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': undeclared.'                                                  
         LA    R15,4                                                            
         B     SAVPDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR RESERVED WORD                      
         IF    (R15,NZ),BEGIN                                                   
         L     R2,SVNAME                                                        
         ACALL CHKRESVD                                                         
         IF    Z,BEGIN                                                          
         ACALL SEGVNAME                                                         
         TSEG  ': reserved word.  Invalid variable name. '                      
         LA    R15,4                                                            
         B     SAVPDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ARBITRATE TYPE                               
         IF    (R15,Z),BEGIN       IF VARIABLE EXISTS,                          
         L     R2,SVVAR                                                         
         COMMENT                                                                
         IF    VNARRAY,BEGIN       ARRAY TYPE DOES NOT CHANGE                   
         L     R2,SVNAME           ARRAY SAVED ONLY WHEN DECLARED               
         ACALL SEGVNAME                                                         
         TSEG  ': invalid reference to array descriptor. '                      
         LA    R15,4                                                            
         B     SAVPDONE                                                         
         END                                                                    
         ELSEIF VNNUMB,BEGIN       NUMBER                                       
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNSTRING,BEGIN     STRING                                       
         VCALL  MAKESTR                                                         
         END                                                                    
         ELSEIF VNBOOL,BEGIN       BOOLEAN                                      
         VCALL  MAKEBOOL                                                        
         END                                                                    
         ELSE   BEGIN ,            IF NONE OF THE ABOVE,                        
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      IF UNABLE TO CONVERT,,                       
         B     SAVPDONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         END   , OF VARIABLE EXISTS                                             
*                                                                               
         COMMENT                   SAVE VARIABLE                                
         COMMENT                                                                
         L     R6,SVVAR            R6 - VARIABLE TO SAVE                        
         COMMENT                                                                
         LA    R15,CVRG16          R15 - VAR GROUP INFO                         
         LA    R0,L'VNDATA         R0 - LENGTH OF VAR DATA                      
         LA    R1,VNDATA           R1 - VALUE                                   
         L     R2,SVNAME           R2 - NAME                                    
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         IF    VNARRAY,BEGIN                                                    
         LA    R0,L'VNADATA                                                     
         END                                                                    
         VCALL XPUT                SAV VARIABLE       !!!!                      
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>100000) defined.'                          
         LA    R15,4                                                            
         B     SAVPDONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVPDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKRESVD - CHECK RESERVED WORD LIST                                          
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE NAME DESCRIPTOR                                           
*            (IE.  NAME IN 16 BYTES LEFT JUSTIFIED BLANK PADDED)                
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, RESERVED WORD                                            
*             CC=NZ, NOT RESERVED WORD                                          
*                                                                               
*  NOTE: THIS LIST IS NOT CRITICAL,  THE IDEA IS TO AVOID                       
*        CONFUSION BY NOT ALLOWING THE USER TO USE CONTROL                      
*        COMMANDS AS VARIABLE NAMES.  LIST IS ARBITRARY.                        
*        DO NOT NEED TO MAKE LIST EXHAUSTIVE. STAY COOL.                        
*                                                                               
*                                                                               
         MACRO                                                                  
         RCHECK &WORD                                                           
         CLC   VNNAME(8),=CL8'&WORD'       SIMPLE SIMON                         
         BE    RESVD                                                            
         MEND                                                                   
*                                                                               
*                                                                               
CHKRESVD XPROC ,                                                                
         USING VNENTRY,R2                                                       
         LA    R15,4               ASSUME NOT RESERVED                          
*                                                                               
         COMMENT                   LINEAR SEARCH LIST                           
         RCHECK WHILE                                                           
         RCHECK IF                                                              
         RCHECK ELSE                                                            
         RCHECK CASE                                                            
         RCHECK DO                                                              
         RCHECK LOOP                                                            
         RCHECK UNTIL                                                           
         RCHECK BEGIN                                                           
         RCHECK END                                                             
         RCHECK LAST                                                            
         RCHECK FIRST                                                           
         RCHECK COMM                                                            
         RCHECK COMMENT                                                         
         RCHECK XCALL                                                           
         RCHECK PCALL                                                           
         RCHECK EXEC                                                            
         RCHECK EXE                                                             
         RCHECK GOTO                                                            
         RCHECK SET                                                             
         RCHECK SHOW                                                            
         RCHECK SHO                                                             
         RCHECK CLEAR                                                           
         RCHECK CLR                                                             
         RCHECK READ                                                            
         RCHECK WRITE                                                           
         RCHECK PASS                                                            
         RCHECK DCL                                                             
         RCHECK DECLARE                                                         
         RCHECK NUM                                                             
         RCHECK NUMB                                                            
         RCHECK NUMBER                                                          
         RCHECK BOOL                                                            
         RCHECK BOOLEAN                                                         
         RCHECK STR                                                             
         RCHECK STRING                                                          
         RCHECK EQ                                                              
         RCHECK NE                                                              
         RCHECK LT                                                              
         RCHECK LE                                                              
         RCHECK GT                                                              
         RCHECK GE                                                              
         RCHECK NOT                                                             
         RCHECK AND                                                             
         RCHECK OR                                                              
         RCHECK EXP                                                             
         RCHECK MOD                                                             
         B     NOTRESVD                                                         
*                                                                               
RESVD    LABEL ,                                                                
         CLEAR R15                                                              
*                                                                               
NOTRESVD LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'GET/SAVE SESSION DATA'                                          
*box                                                                            
*                                                                               
*                                                                               
*  GETDATA - GET SESSION DATA                                                   
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF AREA                                                     
*       R1 - AREA FOR DATA                                                      
*      @R2 - 16 CHAR DATA NAME                                                  
*                                                                               
*  ON EXIT:                                                                     
*       R0 - DATA LENGTH                                                        
*       R1 - AREA                                                               
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, VARIABLE NOT FOUND                                          
*                                                                               
*                                                                               
GETDATA  XPROC ,                                                                
*                                                                               
         COMMENT                   GET DATA                                     
         LR    R3,R0                                                            
         LA    R15,CPRG16                                                       
         LA    R0,CP#SYSV                                                       
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LR    R0,R3                                                            
         COMMENT @R1 - DATA AREA                                                
         COMMENT @R2 - DATA NAME                                                
         VCALL XGET                                                             
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEDATA - SAVE SESSION DATA                                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - DATA LENGTH                                                        
*       R1 - DATA LOCATION                                                      
*      @R2 - 16 CHARACTER DATA NAME                                             
*                                                                               
*  ON EXIT                                                                      
*       R15 - CC=ZERO                                                           
*                                                                               
SAVEDATA XPROC ,                                                                
*                                                                               
         COMMENT                   SAVE SESSION DATA                            
         LR    R3,R0                                                            
         LA    R15,CPRG16                                                       
         LA    R0,CP#SYSV                                                       
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LR    R0,R3   VARIABLE LENGTH                                          
         COMMENT @R1 - VARIABLE VALUE                                           
         COMMENT @R2 - VARIABLE NAME                                            
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT DATAFULL,'Session Data Record Group Overflow.'                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'GET/SAVE INFO ON VARIABLE STACK'                                
*box                                                                            
*                                                                               
*                                                                               
*  GETVSTK - GET SYSTEM INFO FROM VARIABLE STACK                                
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF RETURN AREA                                              
*       R1 - LOCATION OF RETURN AREA                                            
*      @R2 - 16 CHARACTER INFO NAME (EG. 'SYS*CALLER_PLIST')                    
*       R3 - VARIABLE SET NUMBER                                                
*                                                                               
*  ON EXIT:                                                                     
*       R0 - INFO LENGTH                                                        
*      @R1 - INFO AREA                                                          
*      R15 - CC=ZERO                                                            
*            CC=NZ, INFO NOT FOUND                                              
*                                                                               
*                                                                               
*                                                                               
GETVSTK  XPROC ,                                                                
         LR    R4,R0                                                            
*                                                                               
         COMMENT                   SET VARIABLE SET NUMBER                      
         LA    R15,CPVARS                                                       
         LR    R0,R3                                                            
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   SAVE INFORMATION                             
         LA    R15,CPVARS                                                       
         LR    R0,R4               R0 - INFO LENGTH                             
         COMMENT                   @R1 - INFO LOC                               
         COMMENT                   @R2 - NAME                                   
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEVSTK - SAVE SYSTEM INFO IN VARIABLE STACK                                
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF INFO                                                     
*       R1 - LOCATION OF INFO                                                   
*      @R2 - 16 CHARACTER INFO NAME (EG. 'SYS*CALLER_PLIST')                    
*       R3 - VARIABLE SET NUMBER                                                
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ALL ERRORS EXIT VIA CVERROR                                        
*                                                                               
*                                                                               
*                                                                               
SAVEVSTK XPROC ,                                                                
         LR    R4,R0                                                            
*                                                                               
         COMMENT                   SET VARIABLE SET NUMBER                      
         LA    R15,CPVARS                                                       
         LR    R0,R3                                                            
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   SAVE INFORMATION                             
         LA    R15,CPVARS                                                       
         LR    R0,R4               R0 - INFO LENGTH                             
         COMMENT                   @R1 - INFO LOC                               
         COMMENT                   @R2 - NAME                                   
         VCALL XPUT                                                             
         IF    NEG,BEGIN                                                        
         ERROR 'Too many variables (>100000) defined.'                          
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'GET/SAVE ARRAY ELEMENTS'                                        
*box                                                                            
*                                                                               
*                                                                               
*  GETARRAY - GET ARRAY ELEMENT                                                 
*                                                                               
*  ON ENTRY:                                                                    
*      @R0 - ARRAY DESCRIPTOR OR ZERO.  SEE VNENTRY DSECT.                      
*      @R1 - VARIABLE VALUE AREA.  SEE VNENTRY DSECT.                           
*      @R2 - VARIABLE NAME DESCRIPTOR, INCLUDES SUBSCRIPT VALUES                
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, FOUND ELEMENT                                             
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * XXXXX: NOT DECLARED AS ARRAY                                          
*       * INVALID SUBSCRIPT VALUE                                               
*                                                                               
*                                                                               
*                                                                               
GYWA     RECORD BEGIN                                                           
GYVALUE  DS    F                   ADDRESS OF ELEMENT VALUE                     
GYNAME   DS    F                   ADDRESS OF ELEMENT NAME                      
GYARRAY  DS    XL(L'VPARM)         ARRAY DESCRIPTOR ENTRY                       
GYELEM   DS    XL(L'VPARM)         VARIABLE PARM AREA FOR ELEMENT               
         END                                                                    
*                                                                               
*                                                                               
GETARRAY XPROC GYWA                                                             
         USING VNENTRY,R6                                                       
         ST    R1,GYVALUE                                                       
         ST    R2,GYNAME                                                        
*                                                                               
         COMMENT                   ENTRANCE EXAM                                
         L     R6,GYNAME                                                        
         IF    (VNASUB1,EQ,0),BEGIN                                             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF ARRAY DESCRIPTOR, CHECK IT                
         IF    (R0,NZ),BEGIN                                                    
         LR    R6,R0                                                            
         IF    ^VNARRAY,BEGIN                                                   
         BOMB                                                                   
         END                                                                    
         LA    R4,GYARRAY          MOVE DESCRIPTOR                              
         LA    R5,L'GYARRAY                                                     
         LR    R1,R5                                                            
         MVCL  R4,R0                                                            
         END                                                                    
*                                                                               
         COMMENT                   GET ARRAY DESCRIPTOR,                        
         ELSE  BEGIN                                                            
         LA    R0,GYARRAY                                                       
         L     R2,GYNAME                                                        
         ACALL GETADESC                                                         
         BNZ   GETADONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   CHECK SUBSCRIPTS                             
         LA    R0,GYARRAY                                                       
         L     R2,GYNAME                                                        
         ACALL CHKSUBS                                                          
         BNZ   GETADONE                                                         
*                                                                               
         COMMENT                   CREATE ARRAY NAME                            
         LA    R6,GYELEM                                                        
         LA    R1,GYARRAY                                                       
         L     R2,GYNAME                                                        
         CLEAR VNDESC                                                           
         MVC   VNNAME(L'ANAME),VNANAME-VNENTRY(R1)                              
         MVC   VNNAME+AIDENT(L'AIDENT),VNAIDENT-VNENTRY(R1)                     
         MVC   VNNAME+ACONST(L'ACONST),=CL4'*A'                                 
         MVC   VNNAME+ASUB1(L'ASUB1),VNASUB1-VNENTRY(R2)                        
         MVC   VNNAME+ASUB2(L'ASUB2),VNASUB2-VNENTRY(R2)                        
         MVC   VNNAME+ASUB3(L'ASUB3),VNASUB3-VNENTRY(R2)                        
         MVC   VNPREFIX(L'VNPREFIX),VNPREFIX-VNENTRY(R2)                        
*                                                                               
         COMMENT                   SET ARRAY SET NUMBER                         
         LA    R6,GYARRAY          R6 - ARRAY DESC INFO                         
         L     R15,VNARG                                                        
         L     R0,VNASET                                                        
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET ARRAY VARIABLE                           
         L     R15,VNARG                                                        
         L     R6,GYVALUE          R6 - VARIABLE VALUE RETURN AREA              
         LA    R0,L'VPARM-(VNDATA-VNENTRY)                                      
         LA    R1,VNDATA                                                        
         LA    R2,GYELEM                                                        
         VCALL XGET                                                             
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
GETADONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVARRAY - SAVE ARRAY ELEMENT                                                
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - VARIABLE VALUE AREA.  SEE VNENTRY DSECT.                           
*      @R2 - VARIABLE NAME DESCRIPTOR, INCLUDES SUBSCRIPT VALUES                
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, ELEMENT SAVED                                             
*            CC=NZ, IF ERROR, ERROR MSG TSEG'D                                  
*                                                                               
*  THE FOLLOWING ERROR CONDITIONS ARE DETECTED:                                 
*       * TOO MANY VARIABLES SAVED (>30000)                                     
*       * XXXXX: UNDECLARED ARRAY.                                              
*       * INVALID SUBSCRIPT VALUE                                               
*       * VARIABLE TYPE DOES NOT CONVERT                                        
*       * ETC,,,                                                                
*                                                                               
*                                                                               
SYWA     RECORD BEGIN                                                           
SYVALUE  DS    F                   ADDRESS OF ELEMENT VALUE                     
SYNAME   DS    F                   ADDRESS OF ELEMENT NAME                      
SYARRAY  DS    XL(L'VPARM)         ARRAY DESCRIPTOR ENTRY                       
SYELEM   DS    XL(L'VPARM)         VARIABLE PARM AREA FOR ELEMENT               
         END                                                                    
*                                                                               
*                                                                               
SAVARRAY PROC  SYWA                                                             
         USING VNENTRY,R6                                                       
         ST    R1,SYVALUE                                                       
         ST    R2,SYNAME                                                        
*                                                                               
         COMMENT                   ENTRANCE EXAM                                
         L     R6,SYNAME                                                        
         IF    (VNASUB1,EQ,0),BEGIN                                             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET ARRAY DESCRIPTOR,                        
         LA    R0,SYARRAY                                                       
         L     R2,SYNAME                                                        
         ACALL GETADESC                                                         
         BNZ   SAVADONE                                                         
*                                                                               
         COMMENT                   GET/CHECK SUBSCRIPTS                         
         LA    R0,SYARRAY                                                       
         L     R2,SYNAME                                                        
         ACALL CHKSUBS                                                          
         BNZ   SAVADONE                                                         
*                                                                               
         COMMENT                   CONVERT ELEMENT TYPE, IF NEEDED              
         LA    R6,SYARRAY                                                       
         L     R2,SYVALUE                                                       
         IF    VNANUMB,BEGIN                                                    
         VCALL MAKENUMB                                                         
         END                                                                    
         ELSEIF VNASTR,BEGIN                                                    
         VCALL MAKESTR                                                          
         END                                                                    
         ELSEIF VNABOOL,BEGIN                                                   
         VCALL MAKEBOOL                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
         IF    (R15,NZ),BEGIN      CONVERISON ERROR                             
         B     SAVADONE            ERROR MSG TSEG'D BY CONVRT RTNS              
         END                                                                    
*                                                                               
         COMMENT                   CREATE ARRAY NAME                            
         LA    R6,SYELEM                                                        
         LA    R1,SYARRAY                                                       
         L     R2,SYNAME                                                        
         CLEAR VNDESC                                                           
         MVC   VNNAME(L'ANAME),VNANAME-VNENTRY(R1)                              
         MVC   VNNAME+AIDENT(L'AIDENT),VNAIDENT-VNENTRY(R1)                     
         MVC   VNNAME+ACONST(L'ACONST),=CL4'*A'                                 
         MVC   VNNAME+ASUB1(L'ASUB1),VNASUB1-VNENTRY(R2)                        
         MVC   VNNAME+ASUB2(L'ASUB2),VNASUB2-VNENTRY(R2)                        
         MVC   VNNAME+ASUB3(L'ASUB3),VNASUB3-VNENTRY(R2)                        
         MVC   VNPREFIX(L'VNPREFIX),VNPREFIX-VNENTRY(R2)                        
*                                                                               
         COMMENT                   SET ARRAY SET NUMBER                         
         LA    R6,SYARRAY          R6 - ARRAY DESC INFO                         
         L     R15,VNARG                                                        
         L     R0,VNASET                                                        
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   SAVE ARRAY ELEMENT                           
         L     R15,VNARG                                                        
         L     R6,SYVALUE          R6 - VALUE TO SAVE                           
         LA    R0,L'VNDATA                                                      
         LA    R1,VNDATA                                                        
         LA    R2,SYELEM                                                        
         IF    VNSTRING,BEGIN                                                   
         LA    R0,L'VNSDATA                                                     
         AH    R0,VNSTRLEN                                                      
         END                                                                    
         VCALL XPUT                SAVE VARIABLE                                
         IF    (R15,NEG),BEGIN                                                  
         TSEG  'Too many variables (>30000) defined. '                          
         LA    R15,4                                                            
         B     SAVADONE                                                         
         END                                                                    
         IF    (R15,POS),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE COMPLETED OK                            
         CLEAR R15                                                              
*                                                                               
SAVADONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETSUBS - GET SUBSCRIPTS                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF STRING                                                   
*       R1 - LOCATION OF STRING (POINTS TO BLANK OR '[')                        
*      @R2 - VARIABLE NAME DESCRIPTOR,  SEE VNENTRY DSECT.                      
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LENGTH OF REMAINING STRING                                         
*       R1 - LOCATION OF REMANING STRING                                        
*      @R2 - VARIABLE NAME DESCRIPTOR W/SUBSCRIPT FIELDS SET                    
*      R15 - CC=ZERO, IF ALL OK                                                 
*            CC=NZ, IF INVALID SUBSCRIPT, ERR MSG IS TSEG'D                     
*                                                                               
*  NOTE: 3 SUBSCRIPTS ARE SET.  NO OTHER FIELDS IN THE VARIABLE                 
*        AREA (SEE VNENTRY DSECT) ARE MODIFIED.   IF A SUBSCRIPT                
*        IS NOT SPECIFIED, IT IS SET TO ZERO.                                   
*                                                                               
*                                                                               
GSWA     RECORD BEGIN                                                           
GSSUB1   DS    H                   ARRAY SUBSCRIPTS                             
GSSUB2   DS    H                                                                
GSSUB3   DS    H                                                                
         END                                                                    
SUBLIMIT EQU   1                   1 DIMESION FOR STARTERS                      
         DS    0S(3-CPSLIMIT)      3 DIMENSIONS MAX !!                          
*                                                                               
*                                                                               
GETSUBS  XPROC GSWA                                                             
         USING VNENTRY,R6                                                       
         LR    R6,R2                                                            
         CLEAR GSSUB1                                                           
         CLEAR GSSUB2                                                           
         CLEAR GSSUB3                                                           
*                                                                               
         COMMENT                   SKIP BLANKS, SKIP LEADING '['                
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,NE,'['),OR,(R0,NP)),BEGIN                                  
         LA    R2,VNENTRY                                                       
         ACALL SEGVNAME                                                         
         TSEG  ': missing subscripts. '                                         
         LA    R15,4                                                            
         B     SUBSDONE                                                         
         END                                                                    
         LA    R1,1(R1)            SKIP '['                                     
         DECR  R0                                                               
*                                                                               
         COMMENT                   CHECK FOR NULL SUBS (IE. [])                 
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,']'),AND,(R0,POS)),BEGIN                                
         ACALL SEGVNAME                                                         
         TSEG  ': missing subscripts. '                                         
         LA    R15,4                                                            
         B     SUBSDONE                                                         
         END                                                                    
*-                                                                              
*-       LOOP EVALUATING SUBSCRIPTS !                                           
*-                                                                              
         COMMENT                   LOOP EVALUATING SUBSCRIPTS                   
         CLEAR R3                                                               
         LA    R4,GSSUB1           R3,R4 - SUBSCRIPT #, INDEX                   
         WHILE (@R1,NE,']'),BEGIN  LOOP UNTIL END OF SUBSCRIPTS                 
         LA    R3,1(R3)                                                         
*                                                                               
         COMMENT                   SPECIAL PROCESSING FOR ']'                   
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,']'),AND,(R0,POS)),BEGIN                                
         L     R15,=F'-1'          IF ']', TREAT LIKE NULL EXPR                 
         END                                                                    
*                                                                               
*                                                                               
         IF    ((@R1,NE,']'),OR,(R0,NP)),BEGIN     SPECIAL ']' CHECK            
         COMMENT                                                                
         COMMENT                   EVALUATE INTEGER EXPRESSION                  
         COMMENT                   EVALUATE INTEGER EXPRESSION                  
         COMMENT                                                                
         VCALL EVALINT             *******  !!!!                                
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   IF GOOD EXPRESSION,,                         
         IF    (R15,Z),BEGIN                                                    
         STH   R2,0(R4)            SAVE SUBSCRIPT                               
         LA    R4,L'GSSUB1(R4)                                                  
         IF    (R2,NP),BEGIN                                                    
         LA    R2,VNENTRY                                                       
         ACALL SMALLSUB                                                         
         B     SUBSDONE                                                         
         END                                                                    
         IF    (R2,GT,32000),BEGIN                                              
         LA    R2,VNENTRY                                                       
         ACALL BIGSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF NULL EXPRESSION                           
         ELSEIF (R15,NEG),BEGIN                                                 
         LA    R2,VNENTRY                                                       
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF ERROR IN EXPRESSION                       
         ELSEIF (R15,POS),BEGIN                                                 
         LA    R2,VNENTRY                                                       
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET SET TO EVALUATE                          
         COMMENT                   NEXT SUBSCRIPT,  R1,R0=LOC,LEN               
         COMMENT                                                                
         IF    (R0,NP),BEGIN           IF NO NEXT,,                             
         LA    R2,VNENTRY                                                       
         TSEG  'Expecting "]". '                                                
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         IF    (@R1,EQ,';'),BEGIN      IF ';' INVALID                           
         LA    R2,VNENTRY                                                       
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         IF    (@R1,EQ,')'),BEGIN      IF ')' INVALID                           
         LA    R2,VNENTRY                                                       
         TSEG  ')   -- invalid. '                                               
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         IF    ((@R1,NE,','),AND,(@R1,NE,']')),BEGIN                            
         VCALL CHECKEND                                                         
         TSEG  'Subscripts must be separated by commas.  '                      
         LA    R2,VNENTRY                                                       
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         IF    (@R1,EQ,','),BEGIN      IF ',' SKIP                              
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         IF    ((@R1,EQ,']'),AND,(R0,POS)),BEGIN  IF '... ,]' ERROR             
         LA    R3,1(R3)                                                         
         LA    R2,VNENTRY                                                       
         ACALL BADSUB                                                           
         B     SUBSDONE                                                         
         END                                                                    
         END                                                                    
         IF    ((R3,GE,SUBLIMIT),AND,(@R1,NE,']')),BEGIN                        
         LA    R2,VNENTRY                                                       
         ACALL SEGVNAME                                                         
         TSEG  ': too many subscripts specified for array. '                    
         LA    R15,4                                                            
         B     SUBSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   LOOP EVALUATING SUBSCRIPTS,,                 
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   GOT SUBSCRIPTS                               
         INCR  R1                  SKIP FINAL ']'                               
         DECR  R0                                                               
         MVC   VNASUB1,GSSUB1                                                   
         MVC   VNASUB2,GSSUB2                                                   
         MVC   VNASUB3,GSSUB3                                                   
         CLEAR R15                                                              
*                                                                               
SUBSDONE LABEL ,                                                                
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKSUBS - CHECK ARRAY SUBSCRIPTS                                             
*                                                                               
*  ON ENTRY:                                                                    
*       @R0 - ARRAY DESCRIPTOR.  SEE VNDESECT.                                  
*       @R2 - VARIABLE NAME DESCRIPTOR, INCLUDES SUBSCRIPT VALUES.              
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, ALL OK                                                   
*             CC=NZ, INVALID SUBSCRIPTS, ERR MSG IS TSEG'D                      
*                                                                               
*                                                                               
*                                                                               
CKSWA    RECORD BEGIN                                                           
CKSARRAY DS    F                   ADDRESS OF ARRAY DESCRIPTOR                  
CKSNAME  DS    F                   ADDRESS OF ELEMENT NAME                      
CKSSUB1  DS    H                   SUBSCRIPT 1                                  
CKSSUB2  DS    H                   SUBSCRIPT 2                                  
CKSSUB3  DS    H                   SUBSCRIPT 3                                  
CKSDIM1  DS    H                   MAX SUBSCRIPT 1                              
CKSDIM2  DS    H                   MAX SUBSCRIPT 2                              
CKSDIM3  DS    H                   MAX SUBSCRIPT 3                              
         END                                                                    
*                                                                               
*                                                                               
CHKSUBS  PROC  CKSWA                                                            
         USING VNENTRY,R6                                                       
         LR    R6,R0                                                            
         ST    R0,CKSARRAY                                                      
         ST    R2,CKSNAME                                                       
*                                                                               
         COMMENT                   CHECK ARRAY DESCRIPTOR                       
         L     R6,CKSARRAY                                                      
         IF    ^VNARRAY,BEGIN                                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET DIMENSIONS/SUBSCRIPTS                    
         L     R6,CKSARRAY                                                      
         MVC   CKSDIM1,VNADIM1                                                  
         MVC   CKSDIM2,VNADIM2                                                  
         MVC   CKSDIM3,VNADIM3                                                  
         L     R6,CKSNAME                                                       
         MVC   CKSSUB1,VNASUB1                                                  
         MVC   CKSSUB2,VNASUB2                                                  
         MVC   CKSSUB3,VNASUB3                                                  
         LH    R0,CKSDIM1          ARRAY MUST HAVE 1 DIM OR MORE                
         IF    (R0,LE,0),BEGIN                                                  
         BOMB                                                                   
         END                                                                    
         LH    R0,CKSSUB1                                                       
         IF    (R0,LE,0),BEGIN     FIRST SUBSCRIPT MUST BE POSITIVE             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK SUBSCRIPTS                             
         IF    (CKSDIM1,EQ,0),BEGIN                                             
         BOMB                                                                   
         END                                                                    
         IF    ((CKSDIM2,EQ,0),AND,(CKSSUB2,NE,0)),BEGIN                        
         ACALL BADDIM                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    ((CKSDIM3,EQ,0),AND,(CKSSUB3,NE,0)),BEGIN                        
         ACALL BADDIM                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    (CKSSUB1,EQ,0),BEGIN                                             
         ACALL BADDIM                                                           
         END                                                                    
         IF    ((CKSDIM2,NE,0),AND,(CKSSUB2,EQ,0)),BEGIN                        
         ACALL BADDIM                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    ((CKSDIM3,NE,0),AND,(CKSSUB3,EQ,0)),BEGIN                        
         ACALL BADDIM                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    (CKSSUB1,GT,CKSDIM1),BEGIN                                       
         LA    R3,1                                                             
         ACALL BIGSUB                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    (CKSSUB2,GT,CKSDIM2),BEGIN                                       
         LA    R3,2                                                             
         ACALL BIGSUB                                                           
         B     CKSDONE                                                          
         END                                                                    
         IF    (CKSSUB3,GT,CKSDIM3),BEGIN                                       
         LA    R3,3                                                             
         ACALL BIGSUB                                                           
         B     CKSDONE                                                          
         END                                                                    
         CLEAR R15                                                              
*                                                                               
CKSDONE  LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETADESC - GET ARRAY DESCRIPTOR                                              
*                                                                               
*  ON ENTRY:                                                                    
*      @R0 - ARRAY PARM AREA.  SEE VNENTRY                                      
*      @R2 - VARIABLE NAME DESCRIPTOR.  SEE VNENTRY                             
*                                                                               
*  ON EXIT:                                                                     
*      ARRAY DESCRIPTOR RETURNED                                                
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, INVALID ARRAY NAME, ERR MSG TSEG'D                          
*                                                                               
*                                                                               
*                                                                               
GETADESC XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R0                                                            
*                                                                               
         COMMENT                   GET ARRAY DESCRIPTOR NAME                    
         MVC   VNDESC,VNDESC-VNENTRY(R2)                                        
         CLEAR VNASUB1                                                          
         CLEAR VNASUB2                                                          
         CLEAR VNASUB3                                                          
*                                                                               
         COMMENT                   GET ARRAY DESCRIPTOR                         
         LA    R1,VNENTRY                                                       
         LA    R2,VNENTRY                                                       
         ACALL GETVAR                                                           
         IF    Z,BEGIN                                                          
         IF    VNARRAY,BEGIN       IF ARRAY,,,, ALL OK !                        
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN               IF NOT ARRAY,,,                              
         ACALL SEGVNAME                                                         
         TSEG  ': not DECLARED as an array.  Subscripts invalid. '              
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT A VAR, SAME DIFFERENCE                
         ACALL SEGVNAME                                                         
         TSEG  ': not DECLARED as an array.  Subscripts invalid. '              
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'VARIABLE ENTRY FORMATTING ROUTINES'                             
*box                                                                            
*                                                                               
*                                                                               
*  VCLEAR - CLEAR VARIABLE AREA                                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE AREA.  SEE VNENTRY DSECT.                                 
*                                                                               
*                                                                               
VCLEAR   XPROC ,                                                                
         USING VNENTRY,R2                                                       
         CLEAR VNENTRY                                                          
         DROP  R2                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  VNAME - SET NAME IN VARIABLE AREA                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - NAME LENGTH (1 TO 16, SHOULD BE UPPER CASE!)                       
*       R1 - NAME LOCATION                                                      
*       R2 - VARIABLE AREA.  SEE VNENTRY DSECT.                                 
*                                                                               
*                                                                               
VNAME    XPROC ,                                                                
         USING VNENTRY,R2                                                       
         MVC   VNNAME,CVBLANKS     BLANK NAME                                   
         LR    R3,R0                                                            
         MOVE  R3,VNNAME,@R1                                                    
         DROP  R2                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  VSTRING - SET STRING VALUE IN AREA                                           
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - STRING LENGTH                                                      
*       R1 - STRING LOCATION                                                    
*       R2 - VARIABLE AREA.  SEE VNENTRY DSECT.                                 
*                                                                               
*                                                                               
VSTRING  XPROC ,                                                                
         USING VNENTRY,R2                                                       
         COMMENT                   CLEAR VALUE PART OF VARIABLE AREA            
         XC    VNENTRY+L'VNDESC(L'VNENTRY-L'VNDESC),VNENTRY+L'VNDESC            
         SET   VNSTRING                                                         
         STH   R0,VNSTRLEN                                                      
         LR    R3,R0                                                            
         MOVE  R3,VNSTRLOC,@R1                                                  
         DROP  R2                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  VINTEGER - SET INTEGER VALUE IN AREA                                         
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - INTEGER VALUE                                                      
*       R2 - VARIABLE AREA.  SEE VNENTRY DSECT.                                 
*                                                                               
*                                                                               
VINTEGER XPROC ,                                                                
         USING VNENTRY,R2                                                       
         COMMENT                   CLEAR VALUE PART OF VARIABLE AREA            
         XC    VNENTRY+L'VNDESC(L'VNENTRY-L'VNDESC),VNENTRY+L'VNDESC            
         SET   VNNUMB                                                           
         ST    R1,VNVALUE                                                       
         VCALL MAKEITOD                                                         
         DROP  R2                                                               
         PEND  ,                                                                
         EJECT                                                                  
         TITLE 'VARIABLE SET LEVEL ROUTINES'                                    
*box                                                                            
*                                                                               
*                                                                               
*  GTNEWSET - GET NEXT SET NUMBER IN STACK                                      
*                                                                               
*  THIS ROUTINE FINDS THE NEXT VARIABLE SET NUMBER IN THE                       
*  VARIABLE STACK...  EACH TIME AN XPROC IS CALLED TWO RECORD                   
*  GROUP SET ENTRIES ARE CREATED ON THE STACK.  THE FIRST IS                    
*  THE STANDARD ONE, THE SECOND IS FOR 'X.' VARIABLES.  EACH                    
*  PROC HAS ONLY ONE RECORD GROUP SET ENTRY ON THE VARIABLE                     
*  RECORD STACK...  DIG IT !!                                                   
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NEXT (NEW) VARIABLE STACK SET NUMBER                               
*                                                                               
*                                                                               
GTNEWSET XPROC  *                                                               
*                                                                               
         COMMENT                   SET R0 TO CURRENT VAR SET#                   
         L     R0,CPVSET#                                                       
*                                                                               
         COMMENT                   IF BASE, SET TO STACK                        
         IF    (R0,EQ,VBASE),BEGIN       IF BASE, SET TO STACK                  
         LA    R0,VSTACK                                                        
         END                                                                    
*                                                                               
         COMMENT                   IF STACK, INCREMENT                          
         ELSEIF (R0,GE,VSTACK),BEGIN                                            
         L     R0,CPVSET#                                                       
         A     R0,=F'1'                                                         
         COMMENT                   IF XPROC, WE INCREMENT TWICE                 
         COMMENT                   2ND TIME IS FOR 'X.' VARIABLES               
         IF    (CP#XSET,NE,=A(CP#XOLD)),BEGIN                                   
         IF    (CPXVSET#,EQ,CPVSET#),BEGIN                                      
         A     R0,=F'1'                                                         
         END                                                                    
         END                                                                    
         END                                                                    
         ELSE  BEGIN                     IF NEITHER, BOMB                       
         BOMB                                                                   
         END                                                                    
         IF    (R0,GT,MAXSET),BEGIN      IF TOO BIG, BOMB                       
         KAPUT PUSHV,'Variable set stack overflow. '                            
         END                                                                    
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETVRG - GET VARIABLE RECORD GROUP INFO POINTER                              
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE NAME DESCRIPTOR                                           
*            (CHECKS PREFIX FIELD FOR LOCAL/X/GBL/SES/PUB/SYS)                  
*                                                                               
*  ON EXIT:                                                                     
*       R0 - VARIABLE RECORD GROUP INFO POINTER                                 
*      R15 - CC = ZERO                                                          
*                                                                               
*                                                                               
*                                                                               
GETVRG   PROC  ,                                                                
         USING VNENTRY,R2                                                       
*                                                                               
         COMMENT                   IF LOCAL VARIABLE                            
         IF    (VNPREFIX,EQ,0),BEGIN                                            
         LA    R0,CPVARS                                                        
         END                                                                    
*                                                                               
         COMMENT                   IF X. VAR PREFIX                             
         ELSEIF VNPFX,BEGIN                                                     
         LA    R0,CPVARS                                                        
         END                                                                    
         COMMENT                   IF GBL. VAR PREFIX                           
         ELSEIF VNPFGBL,BEGIN                                                   
         LA    R0,CPVARS                                                        
         END                                                                    
         COMMENT                   IF SYS. VAR PREFIX                           
         ELSEIF VNPFSYS,BEGIN                                                   
         KAPUT GETVRG,'System variables record group does not exist.'           
         END                                                                    
         COMMENT                   IF SES. VAR PREFIX                           
         ELSEIF VNPFSES,BEGIN                                                   
         LA    R0,CPRG16                                                        
         END                                                                    
         COMMENT                   IF PUB. VAR PREFIX                           
         ELSEIF VNPFPUB,BEGIN                                                   
         LA    R0,CVRG16                                                        
         END                                                                    
         COMMENT                   ANY OTHER CASES, INVALID                     
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETVSET - GET VARIABLE SET NUMBER                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE NAME DESCRIPTOR                                           
*            (CHECKS PREFIX FIELD FOR LOCAL/X/GBL/SYS)                          
*                                                                               
*  ON EXIT:                                                                     
*       R0 - CURRENT SET NUMBER                                                 
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
*  CAUTION: IF NO XPROC ACTIVE, 'X.' PREFIX WILL BOMB ROUTINE.                  
*       CHECK FOR THIS CONDITION EXTERNALLY.                                    
*                                                                               
GETVSET  PROC  ,                                                                
         USING VNENTRY,R2                                                       
*                                                                               
         IF    (VNPREFIX,EQ,0),BEGIN   LOCAL                                    
         L     R0,CPVSET#                                                       
         END                                                                    
*                                                                               
         ELSEIF VNPFX,BEGIN        XPROC 'X.' VARIABLE                          
         ACALL GETXVSET                                                         
         END                                                                    
*                                                                               
         ELSEIF VNPFGBL,BEGIN      GLOBAL                                       
         LA    R0,VGLOBAL                                                       
         END                                                                    
*                                                                               
         ELSEIF VNPFSYS,BEGIN      SYSTEM                                       
         KAPUT GETVSET,'System variable set number does not exist.'             
         END                                                                    
*                                                                               
         ELSEIF VNPFSES,BEGIN      SESSION                                      
         LA    R0,CP#SESV                                                       
         END                                                                    
*                                                                               
         ELSEIF VNPFPUB,BEGIN      PUBLIC                                       
         LA    R0,CV#PUBV                                                       
         END                                                                    
*                                                                               
         ELSE  BEGIN               NONE OF THE ABOVE                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETXVSET - GET SET NUMBER OF 'X.' VARIABLES                                  
*                                                                               
*  ON EXIT:                                                                     
*       R0 - 'X.' VARIABLES SET NUMBER                                          
*       R15 - UNCHANGED                                                         
*                                                                               
*  HEY GUYS, EACH TIME AN XPROC IS CALLED TWO RECORD GROUP SET                  
*  ENTRIES ARE CREATED ON THE STACK.  THE FIRST IS THE STANDARD                 
*  ONE, THE SECOND IS FOR 'X.' VARIABLES.  EACH PROC HAS ONLY                   
*  ONE RECORD GROUP SET ENTRY ON THE VARIABLE RECORD STACK...                   
*  DIG IT !!                                                                    
*                                                                               
*  CAUTION:  IF NO XPROC ACTIVE, ROUTINE WILL BOMB !                            
*                                                                               
*                                                                               
GETXVSET PROC  ,                                                                
*                                                                               
         COMMENT                   IF NO ACTIVE XPROC, BOMB                     
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         L     R0,CPXVSET#                                                      
         A     R0,=F'1'                                                         
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  **** NO KNOWN CALLER, DO NOT DELETE, GENERIC ROUTINE                         
*                                                                               
*  CLRVSET - CLEAR SET OF VARIABLES                                             
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE SET # TO CLEAR                                            
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
CLRVSET  PROC  ,                                                                
*                                                                               
         COMMENT                   CLEAR A SET OF VARIABLES                     
         LA    R15,CPVARS                                                       
         VCALL XDELSET                                                          
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLRVSETS - CLEAR ALL VARIABLE SET ON STACK FROM SET SPECIFIED.               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - FIRST VARIABLE SET # TO CLEAR                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
*  CAUTION: R0 MUST BE VALID VARIABLE STACK SET NUMBER                          
*                                                                               
*                                                                               
CLRVSETS XPROC ,                                                                
*                                                                               
         COMMENT                   VALIDITY CHECK R0                            
         IF    (R0,LT,VSTACK),BEGIN                                             
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CLEAR REQUESTED SETS                         
         LA    R15,CPVARS                                                       
         COMMENT R0-START                                                       
         L     R1,=X'FFFFFFFF'                                                  
         VCALL XDELSETS                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLRVSTK - CLEAR VARIABLE STACK, RESET BASE VARIABLE SET                      
*                                                                               
*                                                                               
CLRVSTK  XPROC ,                                                                
*                                                                               
         COMMENT                   CLEAR VARIABLE STACK                         
         LA    R15,CPVARS                                                       
         LA    R0,VSTACK                                                        
         L     R1,=X'FFFFFFF'                                                   
         VCALL XDELSETS                                                         
*                                                                               
         COMMENT                   SET BASE VARIABLE SET                        
         LA    R0,VBASE                                                         
         ST    R0,CPVSET#                                                       
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  INITBASE - INITIALIZE BASE VARIABLE SET.   INIT N0-N9, S0-S9,                
*             AND W0-W9.  ALL OTHER VARIABLES IN SET ARE CLEARED !              
*                                                                               
*                                                                               
*                                                                               
VARWA    RECORD BEGIN                                                           
VARAREA  DS    XL(L'VPARM)         TEMP VARIABLE WORK AREA                      
         END                                                                    
*                                                                               
*                                                                               
INITBASE XPROC VARWA                                                            
         USING VNENTRY,R6                                                       
         LA    R6,VARAREA                                                       
*                                                                               
         COMMENT                   SET BASE SET OF VARIABLES                    
         LA    R15,CPVARS                                                       
         LA    R0,VBASE                                                         
         VCALL XSETSET             SET BASE SET VARIABLES                       
         XPUSH R15                 PUSH LAST SET NUMBER                         
*                                                                               
         COMMENT                   CLEAR BASE SET OF VARIABLES                  
         LA    R15,CPVARS                                                       
         LA    R0,VBASE                                                         
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   INITIALIZE N0-N9                             
         CLEAR VNENTRY             CLEAR VAR WORK AREA                          
         LA    R2,VNNAME                                                        
         LA    R3,L'VNNAME                                                      
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4               BLANK NAME                                   
         MVI   VNNAME,C'N'         SET NAME                                     
         SET   VNNUMB              SET TYPE                                     
         COMMENT                   SET VALUE                                    
         MVI   VNVALUE+L'VNVALUE-1,X'0C'                                        
         CLEAR R4                                                               
         WHILE (R4,LT,10),BEGIN    LOOP THRU N0-N9                              
         STC   R4,VNNAME+1         GET NAME RIGHT                               
         OI    VNNAME+1,C'0'                                                    
         LA    R15,CPVARS          SAVE VARIABLE                                
         LA    R0,L'VNDATA                                                      
         LA    R1,VNDATA                                                        
         LA    R2,VNNAME                                                        
         VCALL XPUT                SAVE VARIABLE                                
         LA    R4,1(R4)                                                         
         END                                                                    
*                                                                               
         COMMENT                   INITIALIZE S0-S9                             
         CLEAR VNENTRY             CLEAR VAR WORK AREA                          
         LA    R2,VNNAME                                                        
         LA    R3,L'VNNAME                                                      
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4               BLANK NAME                                   
         MVI   VNNAME,C'S'         SET NAME                                     
         SET   VNSTRING            SET TYPE                                     
         CLEAR VNSTRLEN            SET VALUE                                    
         CLEAR R4                                                               
         WHILE (R4,LT,10),BEGIN    LOOP THRU N0-N9                              
         STC   R4,VNNAME+1         GET NAME RIGHT                               
         OI    VNNAME+1,C'0'                                                    
         LA    R15,CPVARS          SAVE VARIABLE                                
         LA    R0,L'VNDATA                                                      
         LA    R1,VNDATA                                                        
         LA    R2,VNNAME                                                        
         VCALL XPUT                SAVE VARIABLE                                
         LA    R4,1(R4)                                                         
         END                                                                    
*                                                                               
         COMMENT                   INITIALIZE W0-W9                             
         CLEAR VNENTRY             CLEAR VAR WORK AREA                          
         LA    R2,VNNAME                                                        
         LA    R3,L'VNNAME                                                      
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4               BLANK NAME                                   
         MVI   VNNAME,C'W'         SET NAME                                     
         SET   VNNUMB              SET TYPE                                     
         COMMENT                   SET VALUE                                    
         MVI   VNVALUE+L'VNVALUE-1,X'0C'                                        
         CLEAR R4                                                               
         WHILE (R4,LT,10),BEGIN    LOOP THRU N0-N9                              
         STC   R4,VNNAME+1         GET NAME RIGHT                               
         OI    VNNAME+1,C'0'                                                    
         LA    R15,CPVARS          SAVE VARIABLE                                
         LA    R0,L'VNDATA                                                      
         LA    R1,VNDATA                                                        
         LA    R2,VNNAME                                                        
         VCALL XPUT                SAVE VARIABLE                                
         LA    R4,1(R4)                                                         
         END                                                                    
*                                                                               
         COMMENT                   POP LAST VARIABLE SET                        
         XPOP  R15                                                              
         LR    R0,R15                                                           
         LA    R15,CPVARS                                                       
         VCALL XSETSET             SET VARIABLE SET NUMBER                      
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         TITLE 'DEBUG MESSAGES'                                                 
*box                                                                            
*                                                                               
*  **** NO KNOWN CALLERS, DO NOT DELETE,,,,,                                    
*  ****                                                                         
*  **** THIS ROUTINE MAY BE EXAMPLE FOR SIMILAR DEBUG MODULE                    
*  **** ROUTINES,,,, MAY DELETE WHEN DEBUG IMPLEMENTED.                         
*                                                                               
*  ON ENTRY:                                                                    
*      @R0 - ZERO OR ARRAY DESCRIPTOR.  SEE VNENTRY                             
*      @R1 - VARIABLE VALUE.  SEE VNENTRY.                                      
*      @R2 - VARIABLE DESCRIPTOR.  SEE VNENTRY.                                 
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
DBUGGET  PROC  ,                                                                
*$  MUST CHECK IF DEBUG ACTIVE,                                                 
*$  MUST CHECK IF ARRAY, DITTO, DITTO !!                                        
         PEND  ,                                                                
         SPACE 5                                                                
DBUGSAVE PROC  ,                                                                
*$  MUST CHECK IF DEBUG ACTIVE,                                                 
*$  MUST CHECK IF ARRAY, DITTO, DITTO !!                                        
         PEND  ,                                                                
         TITLE 'SCAN VARIABLE DESCRIPTOR'                                       
*box                                                                            
*                                                                               
*                                                                               
*  SCNVDESC - SCAN VARIABLE NAME (DESCRIPTOR)                                   
*                                                                               
*  THIS ROUTINE SCANS VARIABLE NAMES, AND IN THE CASE OF                        
*  AN ARRAY, IT'S SUBSCRIPTS.  SEE VNDESC IN VNENTRY DSECT.                     
*  ONLY NAME SYNTAX IS CHECKED, WHILE SUBSCRIPTS ARE EVALUATED.                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF STRING                                                   
*       R1 - LOCATION OF STRING                                                 
*      @R2 - AREA TO RETURN NAME DESCRIPTOR (OF LEN L'VNDESC)                   
*                                                                               
*  ON EXIT:                                                                     
*       R0 - REMAINING STRING LENGTH                                            
*       R1 - REMAINING STRING LOCATION                                          
*      @R2 - VARIABLE DESCRIPTOR INFORMATION                                    
*      R15 - CC=ZERO, VALID DESCRIPTOR                                          
*            CC=NZ, INVALID DESCRIPTOR, ERR MSG IS T'SEGD                       
*                                                                               
*                                                                               
*                                                                               
SCNVDESC XPROC ,                                                                
*                                                                               
         COMMENT                   SCAN VARIABLE NAME                           
         VCALL SCNVNAME                                                         
         IF    NZ,BEGIN                                                         
         IF    (R15,EQ,-4),BEGIN                                                
         TSEG  'Missing variable name. '                                        
         LA    R15,4                                                            
         END                                                                    
         ELSEIF (R15,EQ,4),BEGIN                                                
         ACALL SEGVNAME                                                         
         TSEG  ': invalid variable name. '                                      
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   VALID VARIABLE NAME, CHECK FOR               
         COMMENT                   SUBSCRIPTS                                   
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN   SKIP BLANKS                  
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,'['),AND,(R0,POS)),BEGIN   FOUND '['                    
         ACALL GETSUBS                             GET SUBSCRIPTS               
         END                                                                    
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNNSW - SCAN FOR N0-N9, W0-W9, OR S0-S9                                     
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - STRING LENGTH                                                      
*       R1 - STRING LOCATION                                                    
*                                                                               
*  ON EXIT:                                                                     
*       R0,R1 - UNCHANGED                                                       
*       R15 - CC=ZERO, IF N0-N9, W0-W9, 0R S0-S9 FOUND,,                        
*             CC=NZ, NSW VARIABLE NOT FOUND,,                                   
*                                                                               
*                                                                               
SCNNSW   PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR VARIABLE NAME                       
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCUPTOK R1                                                             
*                                                                               
         IF    (R0,EQ,2),BEGIN                                                  
         LC    R2,@R1                                                           
         LA    R3,C'N'                                                          
         LA    R4,C'S'                                                          
         LA    R5,C'W'                                                          
         IF    ((R2,EQ,R3),OR,(R2,EQ,R4),OR,(R2,EQ,R5)),BEGIN                   
         IF    ((@R1+1,GE,'0'),AND,(@R1+1,LE,'9')),BEGIN                        
         CLEAR R15                                                              
         END                                                                    
         END                                                                    
         END                                                                    
         PEND  ,                                                                
         TITLE 'VARIABLE SEG (OUTPUT) ROUTINES'                                 
*box                                                                            
*                                                                               
*                                                                               
*  SEGVAR - OUTPUT VARIABLE = VALUE                                             
*                                                                               
*  ON INPUT:                                                                    
*       R2 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, IF OK                                                     
*            CC=NZ, IF ATTN HIT                                                 
*                                                                               
*  OUTPUT EXAMPLES:                                                             
*       N0 = 0                                                                  
*       GBL.TEST = 'THIS IS A STRING'                                           
*                                                                               
*  NOTE:                                                                        
*     THIS ROUTINE WILL NOT HANDLE ARRAY ELEMENTS.                              
*                                                                               
*                                                                               
SEGVAR   XPROC ,                                                                
         ACALL SEGVNAME            NAME                                         
         TSEG  ' = '               =                                            
         ACALL SEGVALUE            VALUE                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGVNAME - TSEG VARIABLE NAME                                                
*                                                                               
*  ON INPUT:                                                                    
*       R2 - VARIABLE PARM AREA.  SEE VNENTRY DSECT.                            
*            (INCLUDES VARIABLE NAME DESCRIPTOR, W/VAR PREFIX)                  
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            CC=NZ, IF ATTN HIT                                                 
*                                                                               
*                                                                               
SEGVNAME XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R2                                                            
*                                                                               
         COMMENT                   PROCESS PREFIX, IF ANY                       
         IF    (VNPREFIX,NE,0),BEGIN                                            
         IF    VNPFX,BEGIN                                                      
         TSEG  'X.'                                                             
         END                                                                    
         ELSEIF VNPFGBL,BEGIN                                                   
         TSEG  'GBL.'                                                           
         END                                                                    
         ELSEIF VNPFSYS,BEGIN                                                   
         TSEG  'SYS.'                                                           
         END                                                                    
         ELSEIF VNPFSES,BEGIN                                                   
         TSEG  'SES.'                                                           
         END                                                                    
         ELSEIF VNPFPUB,BEGIN                                                   
         TSEG  'PUB.'                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   TSEG NAME                                    
         TSEG  VNNAME,L'VNNAME,T                                                
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGVALUE - TSEG VALUE OF A VARIABLE                                          
*                                                                               
*       EXPRESSION IS TSEG'D, FOLLOWED BY A BLANK.                              
*       NOTE THAT STRINGS ARE ENCLOSED IN QUOTES.                               
*       Note that array is simply listed as "*** Array ***".                    
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE PARM ENTRY.  SEE VNENTRY DSECT.                           
*                                                                               
*  ON EXIT:                                                                     
*       R15: - CC=ZERO, IF OK                                                   
*              CC=NZ, IF ATTN HIT                                               
*                                                                               
*  NOTE: ONLY TYPE/VALUE FIELDS OF VNENTRY MUST BE SET. NAME                    
*        FIELDS NOT USED.  MAY BE CALLED WITH XSENTRY @R2.                      
*                                                                               
*                                                                               
SGVWA    RECORD BEGIN                                                           
SGVVAR   DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
SEGVALUE XPROC SGVWA                                                            
         USING VNENTRY,R6                                                       
         LA    R6,SGVVAR                                                        
*                                                                               
         LA    R0,VNENTRY          COPY INPUT VARIABLE                          
         LA    R1,L'VPARM                                                       
         LR    R3,R1                                                            
         MVCL  R0,R2                                                            
*                                                                               
         IF    VNARRAY,BEGIN       IF ARRAY,,                                   
         TSEG  '*** Array ***'                                                  
         END                                                                    
         ELSEIF VNSTRING,BEGIN     IF STRING, ENCLOSE IN QUOTES                 
         TSEG  ''''                                                             
         LA    R1,VNSTRLOC                                                      
         LH    R0,VNSTRLEN                                                      
         TSEG  (R1),(R0)                                                        
         TSEG  ''''                                                             
         END                                                                    
         ELSE BEGIN                                                             
         LA    R2,VNENTRY                                                       
         VCALL MAKESTR                                                          
         LA    R1,VNSTRLOC                                                      
         LH    R0,VNSTRLEN                                                      
         TSEG  (R1),(R0)                                                        
         END                                                                    
         TSEG  ' '                                                              
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SEGELEM - SEG ELEMENT NAME, VALUE                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ARRAY ENTRY                                                        
*            (CONTAINS ARRAY DIMENTSIONS, ETC)                                  
*       R2 - ELEMENT ENTRY                                                      
*            (CONTAINS NAME, SUBSCRIPTS, TYPE AND VALUE)                        
*            (NAME NOT IN INTERNAL FORMAT (*-FORMAT),                           
*             SUBSCRIPTS IN VNDESC(RIPTOR))                                     
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, IF OK                                                     
*            CC=NZ, IF ATTN HIT                                                 
*                                                                               
*                                                                               
SEGELEM  PROC  ,                                                                
         USING VNENTRY,R6                                                       
         LR    R6,R0               VNENTRY - ARRAY ENTRY                        
         LR    R5,R2               R5 - ELEMENT ENTRY                           
*                                                                               
         COMMENT                   SEG ARRAY NAME                               
         LR    R2,R5                                                            
         ACALL SEGVNAME                                                         
*                                                                               
         COMMENT                   SEG ELEMENT SUBSCRIPTS                       
         TSEG  '['                                                              
         LH    R3,VNASUB1-VNENTRY(R5)                                           
         TNUM  (R3)                                                             
         IF    (VNADIM2,NE,0),BEGIN                                             
         TSEG  ','                                                              
         LH    R3,VNASUB2-VNENTRY(R5)                                           
         TNUM  (R3)                                                             
         END                                                                    
         IF    (VNADIM3,NE,0),BEGIN                                             
         TSEG  ','                                                              
         LH    R3,VNASUB3-VNENTRY(R5)                                           
         TNUM  (R3)                                                             
         END                                                                    
         TSEG  ']'                                                              
*                                                                               
         COMMENT                   SEG ELEMENT VALUE                            
         TSEG  ' = '                                                            
         LR    R2,R5                                                            
         ACALL SEGVALUE                                                         
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         TITLE 'ERROR ROUTINES'                                                 
*box                                                                            
*                                                                               
*                                                                               
*  BADSUB - TSEG (OUTPUT) BAD SUBSCRIPT MESSAGE                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE (ARRAY) NAME.  SEE VNENTRY DSECT.                         
*       R3 - SUBSCRIPT NUMBER (L TO R)                                          
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=NZ                                                             
*       ERROR MESSAGE TSEG'D                                                    
*                                                                               
*                                                                               
*                                                                               
BADSUB   PROC  ,                                                                
         TCCR                                                                   
         TSEG  'Subscript #'                                                    
         TNUM  (R3)                                                             
         TSEG  ' of '                                                           
         ACALL SEGVNAME                                                         
         TSEG  ' is invalid.'                                                   
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SMALLSUB - TSEG (OUTPUT) ERROR MESSAGE                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE (ARRAY) NAME.  SEE VNENTRY DSECT.                         
*       R3 - SUBSCRIPT NUMBER (L TO R)                                          
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=NZ                                                             
*       ERROR MSG TSEG'D                                                        
*                                                                               
*                                                                               
SMALLSUB PROC  ,                                                                
         TSEG  'Array subscripts must be > 0. '                                 
         ACALL BADSUB                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BIGSUB - TSEG (OUTPUT) ERROR MESSAGE                                         
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE (ARRAY) NAME.  SEE VNENTRY DSECT.                         
*       R3 - SUBSCRIPT NUMBER (L TO R)                                          
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=NZ                                                             
*       ERROR MSG TSEG'D                                                        
*                                                                               
*                                                                               
BIGSUB   PROC  ,                                                                
         TSEG  'Subscript too big. '                                            
         ACALL BADSUB                                                           
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BADDIM - TSEG (OUTPUT) ERROR MESSAGE                                         
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE (ARRAY) NAME.  SEE VNENTRY DSECT.                         
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=NZ                                                             
*       ERROR MSG TSEG'D                                                        
*                                                                               
*                                                                               
BADDIM   PROC  ,                                                                
         TCCR                                                                   
         ACALL SEGVNAME                                                         
         TSEG  ': array dimensions do not match DECLARED dimensions.  '         
         LA    R15,4                                                            
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         SPACE 3                                                                
         VLTORG                                                                 
         END   .                                                                
