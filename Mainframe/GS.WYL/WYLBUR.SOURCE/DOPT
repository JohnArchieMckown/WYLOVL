DOPT     TITLE 'WYLBUR''s Common Scan Tables'                                   
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
         GBLC  &PUBUSER,&PUBGRP                                                 
*                                                                               
WYLDOPT  CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
DOPT     IDENT 2025                11:49:22 01/25/02  (SLP)                     
*                                                                               
         PUSH  DSECTS                                                           
         EJECT                                                                  
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         COPY  CONTROL             Copy cv/cp                                   
*                                                                               
         POP   DSECTS                                                           
         TITLE 'Account Scan Table'                                             
         ENTRY ACCTPRTL,ACCTPRTN,ACCTPRTC,ACCTPRT                               
         SPACE 2                                                                
ACCTPRTL OSCKW ,SCNLCHK            Gg.uuu[.project]                             
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
         SPACE 2                                                                
ACCTPRTN OSCKW ,SCNACHK            Gg.uuu or uuu$gg                             
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
         SPACE 2                                                                
ACCTPRTC OSCKW ,SCNACHK            Gg.uuu or uuu$gg                             
ACCTPRT  OSCKW ACCOUNT,SCNACCT,(A,P)                                            
         OSCKW ACCT,SCNACCT,P                                                   
         OSCKW USER,SCNUSER,(A,P)                                               
         OSCKW USR,SCNUSER,P                                                    
         OSCKW GROUP,SCNGRP,(A,P)                                               
         OSCKW GRP,SCNGRP,P                                                     
         OSCKW USERID,SCNUID,P                                                  
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
         SPACE 2                                                                
SCNLCHK  XENTER R2,R8,,LOCAL                                                    
         IF    (R0,GE,7),BEGIN                                                  
         IF    ((@R1+2,NE,'.'),OR,(@R1+2,NE,'$')),EXIT                          
         IF    (@R1+6,NE,'.'),EXIT                                              
         LR    R2,R1                                                            
         LA    R1,@R1+7                                                         
         SH    R0,=H'7'                                                         
         IF    (R0,GT,L'CPSPROJ),BEGIN                                          
         TSEG  (R1),(R0)                                                        
         ERROR ': project-id is too long (max is 8 characters).'                
         END                                                                    
         MVC   CPSPROJ,CVBLANKS                                                 
         LR    R15,R0                                                           
         MOVE  R15,CPSPROJ,@R1     Set project                                  
         SETMSG (R2),6              Account                                     
         B     SCNACCTB            Common                                       
         END                                                                    
*                                                                               
         B     SCNACOM                                                          
         DROP  BR                                                               
         SPACE 2                                                                
SCNACHK  XENTER R2,R8,,LOCAL                                                    
SCNACOM  BASE                                                                   
         IF    (R0,EQ,6),BEGIN                                                  
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),SCNACCTB                      
         IF    (@R1+3,EQ,'$'),SCNACCTB                                          
         IF    (@R1+3,EQ,'.'),BEGIN                                             
         SETMSG 'Account should be specified in "gg.uuu" format.'               
         ERROR (R1),(R0),BADACCT                                                
         END                                                                    
         END                                                                    
         MVC   @R8(4),=V(SCNRES)   Continue scanning                            
         XEXIT                                                                  
         DROP  BR                                                               
*                                                                               
SCNACCT  XENTER R2,R8,,LOCAL                                                    
SCNACCTB BASE                                                                   
         IF    (R0,NE,6),INVACCT                                                
         LR    R6,R1                                                            
*-                                                                              
*-       gg.uuu or gg$uuu.                                                      
*-                                                                              
         IF    ((@R6+2,EQ,'.'),OR,(@R6+2,EQ,'$')),BEGIN  gg.uuu...              
         SETMSG @R6,2               Gg                                          
         VCALL CHARCHKA                                                         
         BNZ   INVACCTC                                                         
         MVC   CPCGRP,@R6                                                       
         SETMSG @R6+3,3             Uuu                                         
         VCALL CHARCHKA                                                         
         BNZ   INVACCTC                                                         
         MVC   CPCUSER,@R6+3                                                    
         B     SCNACCTX                                                         
         END                                                                    
*-                                                                              
*-       Allow "uuu$gg" for compatibility.                                      
*-                                                                              
         IF    (@R6+3,EQ,'$'),BEGIN  uuu$gg...                                  
         SETMSG @R6,3               Uuu                                         
         VCALL CHARCHKA                                                         
         BNZ   INVACCTC                                                         
         MVC   CPCUSER,@R6                                                      
         SETMSG @R6+4,2             Gg                                          
         VCALL CHARCHKA                                                         
         BNZ   INVACCTC                                                         
         MVC   CPCGRP,@R6+4                                                     
         B     SCNACCTX                                                         
         END                                                                    
*                                                                               
INVACCT  TSEG  (R1),(R0)                                                        
         ERROR ': invalid account.',,INVACCT                                    
*                                                                               
INVACCTC TSEG  (R6),6                                                           
         ERROR ': invalid characters in account.',,INVCHARS                     
*                                                                               
SCNACCTX XEXIT                                                                  
         DROP  BR                                                               
*                                                                               
SCNUSER  XENTER R2,R8,,LOCAL                                                    
         IF    ((R0,EQ,1),AND,(@R1,EQ,'&&')),'SETMSG CPSUSER'                   
         IF    (R0,NE,3),INVUSER                                                
         VCALL CHARCHKA                                                         
         BNZ   INVUSERC                                                         
         MVC   CPCUSER,@R1                                                      
         XEXIT                                                                  
*                                                                               
INVUSER  TSEG  (R1),(R0)                                                        
         ERROR ': invalid user.',,INVUSER                                       
*                                                                               
INVUSERC TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in user-id.',,INVCHARS                     
         DROP  BR                                                               
*                                                                               
*        userid                                                                 
*                                                                               
SCNUID   XENTER R2,R8,,LOCAL                                                    
         LA    R15,CPCACCT                                                      
         VCALL GETACCT             Convert userid to account                    
         IF    NZ,BEGIN            Unknown userid...                            
         TSEG  (R1),(R0)                                                        
         ERROR ' is an unknown user-ID.'                                        
         END                                                                    
         CLEAR R15                 A-ok                                         
         XEXIT                                                                  
         DROP  BR                                                               
*                                                                               
SCNGRP   XENTER R2,R8,,LOCAL                                                    
         IF    ((R0,EQ,1),AND,(@R1,EQ,'@')),'SETMSG CPSGRP'                     
         IF    (R0,NE,2),INVGRP                                                 
         VCALL CHARCHKA                                                         
         BNZ   INVGRPC                                                          
         MVC   CPCGRP,@R1                                                       
         XEXIT                                                                  
*                                                                               
INVGRP   TSEG  (R1),(R0)                                                        
         ERROR ': invalid group.',,INVGRP                                       
*                                                                               
INVGRPC  TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in group.',,INVCHARS                       
         DROP  BR                                                               
         TITLE 'Editing Command Scan Tables'                                    
         ENTRY LTNPRT,LISTPRT,TEXTPRT,RNUMPRT,NUMPRT                            
         ENTRY CASEPRT,LENPRT,VERPRT                                            
         SPACE 2                                                                
*-                                                                              
*-       list options prt                                                       
*-                                                                              
LTNPRT   OSCKW ,LISTPRT,PUSH                                                    
         OSCKW ,TEXTPRT,PUSH                                                    
         OSCKW ,NUMPRT,PUSH                                                     
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       list/nolist prt                                                        
*-                                                                              
LISTPRT  OSCKW LIST,SCNLIST,A                                                   
         OSCKW NOLIST,SCNNLST,A                                                 
         OSCKW N,SCNNLST                                                        
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       notext prt                                                             
*-                                                                              
TEXTPRT  OSCKW NOTEXT,SCNNTEX,A                                                 
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       numbered prt                                                           
*-                                                                              
RNUMPRT  OSCKW RNUMBERED,SCNRNUM,A                                              
NUMPRT   OSCKW NUMBERED,SCNNUM,A                                                
         OSCKW UNNUMBERED,SCNUNUM,A                                             
         OSCKW NONUMBER,SCNONUM,A                                               
         OSCKW INTEGER,SCNINT,A                                                 
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       case (upper/uplow) prt                                                 
*-                                                                              
CASEPRT  OSCKW UPPER,SCNUPP,A                                                   
         OSCKW UPLOW,SCNUPL,A                                                   
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       length prt                                                             
*-                                                                              
LENPRT   OSCKW NOLENGTH,SCNNOLEN,A                                              
         OSCKW LENGTH,SCNLEN,(A,P,PI),&LINESZ                                   
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*-                                                                              
*-       verify prt                                                             
*-                                                                              
VERPRT   OSCKW VERIFY,SCNVER,A                                                  
         OSCKW NOVERIFY,SCNNVER,A                                               
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
         EJECT                                                                  
SCNLIST  CLEAR CPLFLG5.CPFNLST                                                  
         BR    RAR                                                              
*                                                                               
SCNNLST  SET   CPLFLG5.CPFNLST                                                  
         BR    RAR                                                              
*                                                                               
SCNNTEX  NI    CPLFLG5,255-CPFNONUM-CPFNLST  reset options & list               
         OI    CPLFLG5,CPFNTEX+CPFFOPT  turn on notext flag                     
         BR    RAR                                                              
*                                                                               
SCNONUM  SET   CPLFLG5.CPFNONUM*CPFFOPT                                         
         BR    RAR                                                              
*                                                                               
SCNUNUM  CLEAR CPLFLG5.CPFNONUM                                                 
         SET   CPLFLG5.CPFUNUM+CPFFOPT                                          
         BR    RAR                                                              
*                                                                               
SCNRNUM  CLEAR CPLFLG5.CPFNONUM                                                 
         SET   CPNFRNUM,CPLFLG5.CPFFOPT                                         
         BR    RAR                                                              
*                                                                               
SCNNUM   CLEAR CPLFLG5.CPFNONUM,CPNFRNUM                                        
         SET   CPLFLG5.CPFFOPT                                                  
         BR    RAR                                                              
*                                                                               
SCNINT   SET   CPLFLG5.CPNFINT                                                  
         TM    CPLFLG5,CPFFOPT     Has an option been given                     
         BOR   RAR                 Yes, scan on                                 
         NI    CPLFLG5,255-CPFNONUM  no, turn on numbered                       
         OI    CPLFLG5,CPFFOPT     Set option given                             
         BR    RAR                                                              
*                                                                               
SCNUPP   SET   CPFCASE+CPFUPPER                                                 
         BR    RAR                                                              
*                                                                               
SCNUPL   SET   CPFCASE,(CPFUPPER,OFF)                                           
         BR    RAR                                                              
*                                                                               
SCNNOLEN LA    R15,&LINESZ                                                      
SCNLEN   STH   R15,CPLLEN                                                       
         BR    RAR                                                              
*                                                                               
SCNVER   SET   CPFVER                                                           
         BR    RAR                                                              
*                                                                               
SCNNVER  CLEAR CPFVER                                                           
         BR    RAR                                                              
         EJECT                                                                  
         ENTRY COLSPRT                                                          
         SPACE 2                                                                
*                                                                               
*  columns=x(/y)                                                                
COLSPRT  OSCKW COLUMNS,SCNCOLS,(A,P,PI),&LINESZ                                 
         OSCKW COLS,SCNCOLS,(A,P,PI),&LINESZ                                    
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
         SPACE 2                                                                
SCNCOLS  XENTER R2,R8,*+L'CPOSCAN,LOCAL                                         
         DECR  R15                                                              
         STH   R15,CPFCOL          Save starting col-1                          
         MVC   CPLCOL,=Y(&LINESZ)  Default ending col                           
         MVC   @R8+32(L'CPOSCAN),CPOSCAN  scansave                              
         OSCAN COLSPRT2            Look for "/ending col"                       
SCNCOLSR MVC   CPOSCAN,@R8+32       Scanrstr                                    
SCNCOLSE XEXIT                                                                  
*                                                                               
COLSPRT2 OSCKW /,SCNCOLS2,(P,PI),&LINESZ                                        
         OSCKW ,SCNCOLSR                                                        
*                                                                               
SCNCOLS2 STH   R15,CPLCOL          Save ending col                              
         IF    ('SH R15,CPFCOL',NP),BEGIN                                       
         TSEG  (R1),(R0)                                                        
         ERROR ': bad ending column.',,BADCOL                                   
         END                                                                    
         MVC   CPOSCAN+6(L'CPOSCAN-6),@R8+32+6  restore scan opts               
         B     SCNCOLSE                                                         
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
         ENTRY QUIETPRT,COLLPRT,INTPRT,NOOPTPRT                                 
         SPACE 2                                                                
NOOPTPRT LABEL ,                                                                
         OSCKW ,V(INVALID)                                                      
*                                                                               
INTPRT   OSCKW INTERNAL,SCNINTRL,A                                              
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
SCNINTRL SET   CPFINT                                                           
         BR    RAR                                                              
         SPACE 2                                                                
QUIETPRT OSCKW QUIET,SCNQUIET,A                                                 
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
SCNQUIET SET   CPFQUIET            don't write msgs                             
         BR    RAR                                                              
         SPACE 2                                                                
COLLPRT  OSCKW COLLECT,SCNCOLL,A                                                
         OSCKW ,SCNCHK                                                          
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
SCNCOLL  XENTER R2,R8,,LOCAL                                                    
COLBASE  LABEL                                                                  
         IF    (CPSACCT,Z),BEGIN                                                
         ABORT 'Collect option not allowed before logon.'                       
         END                                                                    
*                                                                               
         IF    CPOFNCHG,BEGIN      ACTIVE file mods blocked...                  
         ABORT 'ACTIVE file changes blocked by ORVYL program.'                  
         END                                                                    
         SET   CPFDUMP             Route output to active file                  
         VCALL BLNKTOK             Blank token if wanted                        
         XPUSH ,,L'CPOSCAN,PTR=R5                                               
         MVC   @R5(L'CPOSCAN),CPOSCAN  save scanner state                       
         SET   CPSCNFLG.CPFRTNER                                                
         MVC   CPSCNERT,=A(COLONLY)                                             
         LA    R6,COLLINE                                                       
         OSCAN COLLN                                                            
COLONLY  MVI   CPDMPLST,X'80'      No lineno yet                                
         MVC   CPACLNO,=F'-1000'   No current lineno                            
         MVC   CPOSCAN(6),@R5       Backup                                      
COLRSTR  MVC   CPOSCAN+6(L'CPOSCAN-6),@R5+6  scanner                            
         CLEAR CPSCNFLG.CPFSBLK    don't blank kw                               
         XEXIT                                                                  
*                                                                               
COLLINE  SET   CPLCOLXP            Lineno is explicit                           
         STM   R0,R1,CPDMPLST      Save lineno & implied delta                  
         VCALL BLNKTOK             Blank token if wanted                        
         B     COLRSTR                                                          
         SPACE 2                                                                
SCNCHK   XENTER R2,R8,,LOCAL                                                    
         BASE  COLBASE                                                          
         XPUSH ,,L'CPOSCAN,PTR=R5                                               
         MVC   @R5(L'CPOSCAN),CPOSCAN                                           
         SET   CPSCNFLG.CPFRTNER                                                
         IF    CPFDUMP,BEGIN                                                    
         OSCBACK ,                 Backup scanner                               
         CLEAR R4                                                               
         IF    CPFCLEAR+CPFKEEP,'LA R4,1'                                       
COLOSCN  SET   CPSCNFLG.CPFRTNER                                                
         MVC   CPSCNERT,=A(COLRES)                                              
         OSCAN COLOPTS                                                          
COLRES   IF    ((R4,Z),AND,CPFCLEAR+CPFKEEP),BEGIN                              
         MVC   @R13+8(L'CPOSCAN),CPOSCAN  scansave                              
         XPUSH R0,R1,8+L'CPOSCAN                                                
         VCALL CLRTEXT                                                          
         XPOP  R0,R1,8+L'CPOSCAN                                                
         MVC   CPOSCAN,@R13+8       Scanrstr                                    
         END                                                                    
         IF    ('CLC CPSCNLOC(6),@R5',NE),BEGIN                                 
         IF    (R1,Z),COLRSTR      Scan is complete                             
         OSCBACK ,                Backup scanner                                
         B     COLRSTR             Start over at top of prt                     
         END                                                                    
         END                                                                    
         L     RAR,=V(SCNRES)                                                   
         ST    RAR,@R8                                                          
         B     COLRSTR                                                          
*                                                                               
COLBY    IF    (R15,Z),CVNVALID                                                 
         ST    R15,CPDMPDLT        Collect delta                                
         LR    R2,RAR                                                           
         IF    CPDMPLST.X'80',BEGIN  No collect lineno yet...                   
         VCALL ENDLNO              Get END line-number                          
         ST    R0,CPDMPLST         Set starting lineno                          
         END                                                                    
         BR    R2                                                               
*                                                                               
COLLN    OSCKW ,LNENOPRT,PUSH                                                   
         OSCKW ,COLONLY                                                         
*                                                                               
COLOPTS  OSCKW BY,COLBY,(P,LN)                                                  
         OSCKW ,CLRPRT,PUSH                                                     
         OSCKW ,COLRES                                                          
         TITLE 'SCRATCH/CLEAR PRTs'                                             
* scratch prt                                                                   
         ENTRY SCRPRT                                                           
SCRPRT   OSCKW SCRATCH,SCNSCR,A                                                 
         OSCKW REPLACE,SCNSCR,A                                                 
         OSCKW ,,POP                                                            
*                                                                               
* clear/noclear and scratch/noscratch                                           
         ENTRY CLSCRPRT                                                         
CLSCRPRT OSCKW ,SCRPRT,PUSH                                                     
         ENTRY CLRPRT                                                           
CLRPRT   OSCKW CLEAR,SCNCLR,A                                                   
         OSCKW CLR,SCNCLR,A                                                     
         OSCKW KEEP,SCNKEEP,A                                                   
         OSCKW NOCLEAR,SCNKEEP,A                                                
         OSCKW NOCLR,SCNKEEP,A                                                  
         OSCKW WARN,SCNWARN,A                                                   
         OSCKW NOWARN,SCNOWRN,A                                                 
         OSCKW ,,POP                                                            
*                                                                               
SCNSCR   SET   CPLFLG2.CPFSCRTC                                                 
         BR    RAR                                                              
*                                                                               
SCNCLR   SET   CPFCLEAR                                                         
         CLEAR CPFKEEP                                                          
         BR    RAR                                                              
*                                                                               
SCNKEEP  SET   CPFKEEP                                                          
         CLEAR CPFCLEAR                                                         
         BR    RAR                                                              
*                                                                               
SCNOWRN  SET   CPFNWARN                                                         
         BR    RAR                                                              
*                                                                               
SCNWARN  CLEAR CPFNWARN                                                         
         BR    RAR                                                              
*                                                                               
         QLTORG                                                                 
         TITLE 'Line Number Recognition Routine'                                
* to use this prt, register r6 must be set to the desired                       
* return address if a line number is recognized                                 
*                                                                               
* R0 contains the specified line number on exit                                 
* R1 has a delta that can be used by collect, move and copy                     
* R15 has return codes to show which way the line no was                        
* specified.                                                                    
* R15 -  0 regular line number specified                                        
*        4 first specified                                                      
*        8 current specified                                                    
*       12 last specified                                                       
*       16 end specified                                                        
*       20 next specified                                                       
*       24 previous specified                                                   
*       28 return specified                                                     
*       32 ATTN specified                                                       
*       36 ABORT specified                                                      
*                                                                               
* after the line number specified has been processed,                           
* an increment is scanned for and added in if found                             
* unless cpfnoinc bit is on in cplflg4.                                         
*                                                                               
         ENTRY LNENOPRT,LNENOPRN                                                
LNENOPRT OSCKW F,SCNFRST                                                        
         OSCKW L,SCNLAST                                                        
         OSCKW RETURN,SCNRETN,A                                                 
LNENOPRN OSCKW ,SCNLNENO,LN                                                     
         OSCKW FIRST,SCNFRST,A                                                  
         OSCKW CURRENT,SCNCURR,A                                                
         OSCKW LAST,SCNLAST,A                                                   
         OSCKW END,SCNEND                                                       
         OSCKW NEXT,SCNNEXT,A                                                   
         OSCKW PREVIOUS,SCNPREV,A                                               
         OSCKW FBLOCK,SCNFBLK,A                                                 
         OSCKW LBLOCK,SCNLBLK,A                                                 
         OSCKW *,SCNCURR                                                        
*IOWA*   OSCKW ATTN,SCNATTN,A                                                   
*IOWA*   OSCKW ATTENTION,SCNATTN,A                                              
*IOWA*   OSCKW ABORT,SCNABORT,A                                                 
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
SCNFRST  XENTER R2,R8,,LOCAL                                                    
         L     R0,CPAFLNO          R0 = FIRST line-number                       
         LA    R15,4               Set code for FIRST                           
         IF    (^CPFRDEXT,AND,^CPFRDEXE),INCRSCN1  IF ACTIVE                    
*                                                                               
         CLEAR R0                  Set first possible for external              
         IF    CPFRDEXT,INCRSCN1   External                                     
*                                                                               
         VCALL EXFRSTLN            Assume select from exec                      
         B     INCRSCN1                                                         
         DROP  BR                                                               
*                                                                               
*                                                                               
SCNLAST  XENTER R2,R8,,LOCAL                                                    
         L     R0,CPALLNO          R0 = LAST line-number                        
         LA    R15,12              Set code for LAST                            
         IF    (^CPFRDEXT,AND,^CPFRDEXE),INCRSCN1  IF ACTIVE                    
*                                                                               
         L     R0,CVMAXLNO         Set last possible for external               
         IF    CPFRDEXT,INCRSCN1   External                                     
*                                                                               
         VCALL EXLASTLN            Assume select from exec                      
         B     INCRSCN1                                                         
         DROP  BR                                                               
*                                                                               
*                                                                               
SCNCURR  XENTER R2,R8,,LOCAL       Current                                      
         LA    R15,8               Set code for current                         
         L     R0,CPACLNO          Get current in acitve file                   
         IF    CPFRDEXT,CVNVALID   Test if correct selection                    
         IF    ^CPFRDEXE,CURRTST   If active                                    
         L     R0,CPEXLINE         Assume select from exec                      
CURRTST  LTR   R0,R0               See if valid line number                     
         BNM   INCRSCN1            Branch if okay                               
         TM    CPLFLG4,CPFNOCUR    Is it ok not to have current                 
         BO    CURRMIN             Br yes to return -1                          
         SETMSG 'Current line is not set.'                                      
         B     SCNABRT                                                          
*                                                                               
CURRMIN  LH    R0,=H'-1000'        Set R0 to -1.000                             
         B     INCRSCN1            Now go look for any increment                
         DROP  RAR                                                              
*                                                                               
SCNEND   XENTER R2,R8              End                                          
         IF    CPFRDEXT,CVNVALID   Only select from active                      
         IF    CPFRDEXE,CVNVALID   Branch not from active                       
         VCALL ENDLNO                                                           
         BM    CVNVALID            Error                                        
         LA    R15,16              Set code for end                             
         B     INCRSCN1            Go get incr if given                         
         DROP  BR                                                               
*                                                                               
SCNLNENO BASE  RAR                 Temp base                                    
         LR    R0,R15              Put normal line no in R0                     
         CLEAR R15                 Set code for normal line no                  
         B     INCRSCN             Go scan for increment                        
*                                                                               
SCNFBLK  BASE  RAR                                                              
         L     R0,CPFBKLNO         FBLOCK line-number                           
         IF    ((CPBLKACT,Z),OR,CPVFHIDE),'L R0,=F"-1000"'                      
         CLEAR R15                 Normal line-number                           
         B     INCRSCN                                                          
*                                                                               
SCNLBLK  BASE  RAR                                                              
         L     R0,CPLBKLNO         LBLOCK line-number                           
         IF    ((CPBLKACT,Z),OR,CPVFHIDE),'L R0,=F"-1000"'                      
         CLEAR R15                 Normal line-number                           
         B     INCRSCN                                                          
*                                                                               
SCNRETN  BASE  RAR                 Temp base                                    
         L     R0,CPRETURN         get value of 'return'                        
         LA    R15,28              Set code for return                          
         B     INCRSCN             Go scan for increment                        
*                                                                               
SCNATTN  XENTER R2,R8,,LOCAL       ATTN was specified                           
         LA    R15,32              Set code for ATTN                            
         L     R0,CPEXATTN         Load ATTN line number                        
         IF    (R0,Z),BEGIN                                                     
         SETMSG 'ATTN line number not set'                                      
         B     SCNABRT                                                          
         END                                                                    
         B     INCRSCN1            Continue ...                                 
         DROP  BR                                                               
*                                                                               
SCNABORT XENTER R2,R8,,LOCAL       ABORT was specified                          
         LA    R15,36              Set code for ABRT                            
         L     R0,CPEXABRT         Load ABRT line number                        
         IF    (R0,Z),BEGIN                                                     
         SETMSG 'ABORT line number not set'                                     
         B     SCNABRT                                                          
         END                                                                    
         B     INCRSCN1            Continue ...                                 
         DROP  BR                                                               
*                                                                               
SCNPREV  BASE  RAR                 Temp base                                    
         LH    R1,=H'-1'           Set negative increment                       
         B     SCNNEXTC            Go join next code                            
         DROP  RAR                                                              
*                                                                               
SCNNEXT  LA    R1,1                Set positive increment                       
SCNNEXTC XENTER R2,R8,,LOCAL                                                    
         IF    CPFRDEXT,CVNVALID   Invalid if external file                     
         IF    CPFRDEXE,SCNNEXT1   Branch if from exec file                     
         L     R0,CPACLNO          Get current in active file                   
         L     R5,=V(LOCATE)       Use locate routine                           
         B     SCNNEXT2                                                         
*                                                                               
SCNNEXT1 L     R0,CPEXLINE         Use current in exec file                     
         L     R5,=V(EXLOCATE)     Use exec locate routine                      
SCNNEXT2 LTR   R0,R0               Is line number valid                         
         BM    NONEXTST            Br if not                                    
         VCALL ADDER               Add line number & increment                  
         BM    NONEXTST            Branch if invalid line                       
         ST    R0,CPLCNO           Store line no. to locate                     
         LTR   R1,R1               Set cc for prev or next                      
         LA    R1,CPLCNO           Point to locate line                         
         BM    SCNPREV1            Branch if previous line                      
         BASR  RAR,R5              Go locate next line                          
         TM    CPDRPT,CPFNTYT+CPFHIGH  does it exist                            
         BNZ   NONEXT              Branch no                                    
         L     R0,CPLCNO           Load line no. of next                        
         LA    R15,20              Set return code                              
         B     INCRSCN1            Go scan for increment                        
*                                                                               
NONEXTST LTR   R1,R1               Is next or prev bad                          
         BM    NOPREV              Branch if previous                           
NONEXT   SETMSG 'NEXT'                                                          
NONEXT1  TM    CPLFLG4,CPFNOCUR    Is it ok to not exist                        
         BE    NONEXT2             Br no to do error                            
         LH    R0,=H'-1000'        Set R0 to -1.000                             
         L     R1,CPDELTA          Set R1 to global delta                       
         B     INCRXIT             Exit to line number routine                  
*                                                                               
NONEXT2  TSEG  (R1),(R0),B                                                      
         SETMSG 'line does not exist.'                                          
         B     SCNABRT                                                          
*                                                                               
SCNPREV1 INCR  R15,CPLCNO          Current lineno                               
         IF    (^CPFRDEXE,AND,^CPFRDEXT),BEGIN  Active lineno...                
         VCALL PREVLNO             Get previous lineno                          
         BM    NOPREV              Sorry                                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               Previous Exec lineno...                      
         XPUSH ,,&LINESZ,PTR=R1                                                 
         SETMSG (R1),&LINESZ        Put line text here                          
         LA    R2,CPLCNO                                                        
         VCALL EXPREV              Get previous exec lineno                     
         XPOP  ,,&LINESZ                                                        
         IF    (R15,NZ),NOPREV     No previous lineno                           
         END                                                                    
*                                                                               
         L     R0,CPLCNO           Previous lineno                              
         LA    R15,24              Set return code                              
         BZ    INCRSCN1            Branch yes to increment                      
*                                                                               
NOPREV   SETMSG 'PREVIOUS'                                                      
         B     NONEXT1             Go finish message                            
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ATTNLINE - GET ATTENTION LINE NUMBER                                         
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - POINTS JUST PAST 'ATTN' KEYWORD                                
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UPDATED                                                        
*       R0 - ATTENTION LINE NUMBER                                              
*                                                                               
*                                                                               
*  WE TREAT TWO CASES: 1. IN OLD EXECS A LINE NUMBER                            
*     MUST BE SPECIFIED.  2. IN EXTENDED EXECS A LABEL                          
*     MUST BE SPECIFIED.                                                        
*                                                                               
*                                                                               
ZTTNLINE XENTER R1,R8                                                           
*                                                                               
         IF    ^CPFEXEC,BEGIN                                                   
         TSEG  'ATTN: invalid. '                                                
         ERROR 'ATTN option may only be used in EXEC files. '                   
         END                                                                    
*                                                                               
         ELSEIF  CPFXOLD,BEGIN     IF OLD,                                      
         IC    R2,CPFRDSRC         GET LINENO IS STANDARD (OLD)                 
         CLEAR CPFRDSRC                FASION                                   
         SET   CPFRDEXE                                                         
         LA    R6,GOTALINE                                                      
         OSCAN L:=V(LNENOPRT)                                                   
         B     CVABSENT                                                         
GOTALINE LABEL ,                   RETURNS R0=LINENO                            
         STC   R2,CPFRDSRC                                                      
         END                                                                    
*                                                                               
         COMMENT                   IF EXTENDED MODE, LABELS ONLY                
         COMMENT                                                                
         ELSEIF CPFXTEND,BEGIN     PROCSSES ATTN = LABEL                        
         OSCINFO                                                                
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN    SKIP BLANKS               
         LA    R1,1(R1)                                                         
         END                                                                    
         IF    ((@R1,EQ,'='),AND,(R1,LT,R0)),BEGIN    SKIP '='                  
         LA    R1,1(R1)                                                         
         END                                                                    
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN    SKIP BLANKS               
         LA    R1,1(R1)                                                         
         END                                                                    
         SR    R0,R1                                                            
         OSCSET (R1),(R0)                                                       
         VCALL LABELLNO            PROCESS LABEL                                
         COMMENT                   RETURNS R0=LABEL LINENO                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               MUST HAVE BEEN ONE OF ABOVE                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         XEXIT R1,R8,LTR                                                        
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Scan for Line Number Increment'                                 
* try to find a line number increment                                           
* R0 - line number + increment (if found)                                       
* R15 - restored to value on entry from lnenoprt                                
*                                                                               
INCRSCN  XENTER R2,R8,,LOCAL       Define base & save area                      
INCRSCN1 BASE                                                                   
         LR    R5,R0               Save first line number                       
         LR    R4,R15              Save R15 return code                         
         OSCTELL ,                 Check to see if last token                   
         LTR   R0,R0               Is it the end?                               
         LR    R0,R5               Restore before we branch                     
         BNP   DELTACAL            Yes - skip the incr check                    
         L     R1,CPSAVE+4         Point to                                     
         LR    R3,R1               Save pointer                                 
         AL    R1,CPSAVE           End of last toke                             
         CLI   @R1,C'+'            Plus increment                               
         BE    INCRSCND            Yes go scan for it                           
         CLI   @R1,C'-'            Minus increment                              
         BE    INCRSCND            Yes go scan for it                           
DELTACAL LTR   R15,R15             Regular line no given                        
         BNZ   DELTAUSE            No use global delta                          
         LM    R5,R6,CPSAVE        Yes calc delta from it                       
DELTALP  CLI   @R6,C'.'            Find period                                  
         BE    DELTAPER            Yes                                          
         LA    R6,@R6+1            No, step to next                             
         BCT   R5,DELTALP          Reduce count & loop                          
DELTAUSE L     R1,CPDELTA          Set R1 to global delta                       
         VCALL BLNKTOK                                                          
INCRXIT  LR    R15,R4                                                           
         LR    R13,R8              Restore R13 for exit                         
         LM    R2,R8,@R13+4        Restore return registers                     
         BR    R6                  Return                                       
*                                                                               
DELTAPER IC    R5,DELTATBL-1(R5)   Get delta from table                         
         LTR   R1,R5               Set R1 and test value                        
         BNZ   INCRXIT             O.k. it not zero                             
         B     DELTAUSE            Use cpgldl if zero                           
*                                                                               
DELTATBL DC    FL1'0,100,10,1'     Table of deltas                              
*                                                                               
INCRSCND TM    CPLFLG4,CPFNOINC    Is the no incr flag set                      
         BO    DELTACAL            Br yes to skip incr                          
         VCALL BLNKTOK                                                          
         OSCAN INCRPRT             Scan for increment                           
         LR    R0,R5               Nothing restore R0                           
         B     DELTAUSE            Use delta increment                          
*                                                                               
MINUS    LA    R6,INCOMPL          Set complement return                        
         B     INCRSCN2            Go get line number                           
*                                                                               
PLUS     LA    R6,INCROK           Set incr ok return                           
INCRSCN2 VCALL BLNKTOK             Blank token if needed                        
         OSCAN INCRPRT2            Scan for line number                         
         B     CVABSENT            Missing                                      
*                                                                               
INCOMPL  LCR   R0,R0               Complement result                            
INCROK   LR    R1,R5               Set entry value for adder                    
         VCALL ADDER               Add lineno and incr                          
         BNM   DELTACAL            Return if value okay                         
         LM    R0,R1,CPSAVE        Get end of last token                        
         AR    R0,R1               Into R0                                      
         LR    R1,R3               Get start of this line number                
         SR    R0,R1               Calculate length of number                   
         TSEG  (R1),(R0)           Put part of message                          
         SETMSG ': invalid value.'                                              
         B     SCNERR                                                           
*                                                                               
LNENOINC LR    R0,R15              Normal line number found                     
         CLEAR R15                 Zero R15 for deltacal                        
         B     LNENORTN            Go finish up                                 
*                                                                               
DELTAINC L     R0,CPDELTA          Delta specified                              
LNENORTN XPUSH R15,R0                                                           
         VCALL BLNKTOK             Blank token if needed                        
         XPOP  R15,R0                                                           
         BR    R6                  Go finish up                                 
*                                                                               
INCRPRT  OSCKW +,PLUS                                                           
         OSCKW -,MINUS                                                          
         OSCKW ,V(INVALID)                                                      
*                                                                               
INCRPRT2 OSCKW ,LNENOINC,LN                                                     
         OSCKW DELTA,DELTAINC,A                                                 
         OSCKW ,V(INVALID)                                                      
         DROP  BR                                                               
*                                                                               
SCNERR   SET   CPFERROR                                                         
SCNABRT  IF    ^CPSCNFLG.CPFRTNER,CVABTMSG                                      
         BASE                                                                   
         TSEG  (R1),(R0),CR                                                     
         L     RAR,CPSCNERT        Error rtn addr                               
         CLEAR R0,CPSCNFLG.CPFRTNER                                             
         LR    R13,R8              Unstack                                      
         LM    R2,R8,@R13+4        Restore regs                                 
         BR    RAR                                                              
*                                                                               
         DROP  BR                                                               
         TITLE 'Scan Line for EXECUTE and/or FROM'                              
*                                                                               
* NO LONGER USED   -CH 2/91                                                     
*                                                                               
         AGO   .SKIPSCN                                                         
* scan line to see if select from active, execute or external                   
*        ENTRY SCNEX,SCNEXFR       External entry points                        
*                                                                               
SCNEX    BASE  R15                                                              
         LA    R1,EXPRT            Allow execute or active                      
         B     SCNCOM              Branch to common code                        
*                                                                               
SCNEXFR  BASE  R15                                                              
         LA    R1,EXFRPRT          Allow execute/active/from                    
         DROP  R15                                                              
*                                                                               
SCNCOM   XENTER R7,R8,*+12,LOCAL                                                
         OSCSAVE @R8+8+4           Save scan pointers for restore               
         OSCAN (R1)                Scan for from or execute                     
SCNEXFR1 OSCRSTR @R8+8+4           Restore scanner                              
         XEXIT R7,R8               Exit                                         
*                                                                               
GOTFROM  OI    CPLFLG5,CPFSELOC    Set to use external                          
         MVC   CPSEQFLD(4),=H'-1,8'  initialize seqflds                         
         MVI   CPLRCL,X'80'        Set file type to undefined                   
         CLEAR (CPDSNWA,DSSIZ)     Clear dsname work area                       
         MVI   DSNWADSN,C' '       Now blank first part                         
         MVC   DSNWADSN+1(DSNWACLR-1),DSNWADSN  of the area                     
         CLEAR DSNON                                                            
         OI    CPLFLG1,CPFDSN1     Set flag for dodsnm init.                    
         B     SCNEXFR1            Go exit                                      
*                                                                               
GOTACT   NI    CPLFLG5,255-CPFSELOC  set to use active                          
         B     SCNEXFR1            Go exit                                      
*                                                                               
GOTEXEC  NI    CPLFLG5,255-CPFSELOC  clear locate flags                         
         OI    CPLFLG5,CPFSELEX    Set to use exec                              
         B     SCNEXFR1            Go exit                                      
*                                                                               
EXFRPRT  OSCKW FROM,GOTFROM,A                                                   
EXPRT    OSCKW EXECUTE,GOTEXEC,A                                                
         OSCKW ACTIVE,GOTACT,A                                                  
         OSCKW                                                                  
.SKIPSCN ANOP                                                                   
         QLTORG                                                                 
         TITLE 'ZSNSCAN/ZODSNM Routines'                                        
*box                                                                            
*                                                                               
*  ZSNSCAN -- Entered as SCKW routine to process dsname.                        
*                                                                               
ZSNSCAN  XENTER R7,R8,*+L'CPOSCAN                                               
         MVC   @R8+12(L'CPOSCAN),CPOSCAN                                        
         LCALL  ZODSNM             Process dsname                               
         MVC   CPOSCAN+6(L'CPOSCAN-6),@R8+12+6  restore scan stuff              
         XEXIT R7,R8                                                            
         DROP  BR                                                               
         SPACE 2                                                                
* dodsnm takes 1st token found as dsname and handles it.                        
*                                                                               
* errors are resolved internally                                                
ZODSNM   XENTER R2,R8,*+8                                                       
         IF    CPLFLG1.CPFDSN1,BEGIN  This is a re-entry...                     
         MVI   DSNWADSN,X'40'      Blank part of work area                      
         MVC   DSNWADSN+1(DSNWACL2-1),DSNWADSN                                  
         CLEAR DSNWLEN,DSNWFILE                                                 
         MVI   DSNWAF1,0           Reset attribute types                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               First time initialization...                 
         SET   CPLFLG1.CPFDSN1     Set first entry done                         
         MVC   CPSEQFLD(4),=H'-1,8'  Initialize seqflds                         
         MVI   CPLRCL,X'80'        Set file type to undefined                   
         CLEAR (CPDSNWA,DSSIZ)     Clear dsname work area                       
         MVI   DSNWADSN,X'40'                                                   
         MVC   DSNWADSN+1(DSNWACLR-1),DSNWADSN                                  
         CLEAR DSNON                                                            
         END                                                                    
*                                                                               
         SR    R6,R6               Zero length counter                          
         LA    R5,DSNWADSN         Addr of dsname work area                     
*-                                                                              
*-      Scan next operand for a dsname.  We use "SCANSTR" instead of            
*-         the original scanner because we want to allow special                
*-         characters in the dsname that would ordinarily stop the              
*-         scan (such as / and -).  Network file names have these               
*-         characters in them.                                                  
*-                                                                              
         XPUSH R2,R5               Save regs used by scan                       
         OSCTELL ,                 R1,R0 - scan loc, len                        
         VCALL LRTRIM                                                           
*                                                                               
         IF    (R0,POS),BEGIN      Do quoted string specially...                
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         OSCAN                                                                  
         B     QDSNCONT            Continue                                     
         END                                                                    
         END                                                                    
*                                                                               
         LS    R2,R3,' ,='         Scan skip string                             
         LS    R4,R5,' ,;('        Scan stop string                             
         VCALL SCANSTR             Isolate dsname                               
         IF    (R0,Z),BEGIN        No dsname...                                 
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'MISSDSN'                                            
         B     DOERREX                                                          
         END                                                                    
         ERROR 'Missing data set name.',,MISSDSN                                
         END                                                                    
         IF    (@R1,EQ,'('),BEGIN                                               
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'BADDSN'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR '(: invalid data set name.'                                      
         END                                                                    
         STM   R0,R1,CPSAVE        Save scan token ptrs (for later)             
         XPUSH R0,R1               Token (dsname) loc, len                      
         OSCSET (R2),(R3)          Update scanner ptrs                          
         XPOP  R0,R1               Restore token ptrs                           
QDSNCONT XPOP  R2,R5               Restore regs used by scan                    
*                                                                               
         VCALL DEQUOTE             Dequote if quoted                            
         IF    NM,'SET DSNWAF1.DSNFQDSN'  Quoted dsname                         
*-                                                                              
*-       Save the file name as the user typed it (for network                   
*-         files).  This entire data set name scanning code needs               
*-         to be re-coded.                                                      
*-                                                                              
         XPUSH R2                                                               
         LR    R2,R0                                                            
         CEIL  R2,L'DSNWFILE                                                    
         STH   R2,DSNWLEN          Save file name length                        
         MVC   DSNWFILE,CVBLANKS   Pre-blank                                    
         MOVE  R2,DSNWFILE,@R1     Save file name (in uplow)                    
         XPOP  R2                                                               
*                                                                               
         IF    ^DSNWAF1.DSNFQDSN,BEGIN  Not-quoted dsname...                    
         LR    R15,R0                                                           
         L     R14,CVUPPTBL                                                     
         DTEX  R15,'TR @R1(0),@R14'   Convert to upper case                     
         END                                                                    
*                                                                               
         LR    R4,R1                                                            
         LR    R2,R0                                                            
*-                                                                              
*-       If not quoted, check for "<dev>" and "volume:" prefixes.               
*-                                                                              
         IF    ^DSNWAF1.DSNFQDSN,BEGIN  Not quoted dsname...                    
*-                                                                              
*-       Check for <device-type> prefix.                                        
*-                                                                              
         IF    ((R2,POS),AND,                                          *        
               (@R4,EQ,'<')),BEGIN  We have <>...                               
         LA    R1,@R1+1            Skip past "<"                                
         DECR  R0                                                               
*                                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,'>'),EXIT   End quote, scram                             
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         IF    (R0,Z),BEGIN                                                     
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  'Missing ">" in '                                                
         ABORT (R4),(R2)                                                        
         END                                                                    
*                                                                               
         XPUSH ,,L'CPOSCAN,PTR=R15                                              
         MVC   @R15(L'CPOSCAN),CPOSCAN  OSCSAVE                                 
*                                                                               
         XPUSH R0,R1                                                            
*                                                                               
         LR    R0,R1                                                            
         SR    R0,R4                                                            
         DECR  R0                  Length of text inside brackets               
         OSCINIT @R4+1,(R0)                                                     
*                                                                               
         XPOP  R0,R1                                                            
         LA    R4,@R1+1            Dsname ptr past [dev]                        
         LR    R2,R0               Dsname length                                
         DECR  R2                                                               
*                                                                               
         OSCAN DEVPRT                                                           
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         ABORT 'Missing device-type name.'                                      
*                                                                               
DEVCONT  XPOP  ,,L'CPOSCAN                                                      
         MVC   CPOSCAN,@R13         OSCRSTR **SCAN**                            
         END                                                                    
*-                                                                              
*-       Check for volume:dsname notation.                                      
*-                                                                              
         LR    R1,R4                                                            
         LR    R0,R2                                                            
*                                                                               
         WHILE (R0,POS),BEGIN      Scan for a :...                              
         IF    ((@R1,EQ,'#'),OR,(@R1,EQ,'(')),EXIT  Scram if member             
*                                                                               
         IF    (@R1,EQ,':'),BEGIN                                               
         LR    R15,R1                                                           
         SR    R15,R4                                                           
         IF    POS,BEGIN                                                        
         IF    (@R4,EQ,'CAT'),'CLEAR R15'  Allow "CAT:"                         
         IF    (@R4,EQ,'TEM'),BEGIN  Allow "TEMP:"...                           
         XPUSH R0,R1               Exit will wipe R0,R1                         
         VCALL EXIT2               Get scratch volser                           
         LR    R4,R1               Copy volser ptr                              
         LR    R15,R0              Copy volser length                           
         XPOP  R0,R1               Restore R0,R1                                
         END                                                                    
         CEIL  R15,L'DSNON         Not too long now                             
*                                                                               
         MVC   DSNON,CVBLANKS                                                   
         MOVE  R15,DSNON,@R4       Set volume                                   
         IF    (DSNON,EQ,CVBLANKS),'CLEAR DSNON'                                
         END                                                                    
*                                                                               
         LA    R4,@R1+1            Update dsname...                             
         LR    R2,R0               ...ptrs                                      
         DECR  R2                                                               
*                                                                               
         IF    (R2,NP),BEGIN       Null dsname...                               
         CLEAR DSNWANL             No dsname                                    
         MVC   DSNWADSN,CVBLANKS   Nada                                         
         CLEAR DSNWAMBR            No member either                             
         CLEAR DSNWAF1.DSNFSTD     Not a standard file name                     
         B     DSCNEXIT            Scam                                         
         END                                                                    
*                                                                               
         B     DMORE                                                            
         END                                                                    
*                                                                               
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
DMORE    LR    R3,R4                                                            
         AR    R3,R2               End of operand +1                            
*  If 'WYL.' specified treat as full dsname.                                    
         CLC   =C'WYL.',0(R4)                                                   
         BE    MAINDSN                                                          
*  Check for "SYSn." and treat as full dsname.                                  
         IF    (@R4,EQ,'SYS'),BEGIN                                             
         IF    (@R4+4,NE,'.'),EXIT                                              
         IF    ((@R4+3,LT,'0'),OR,(@R4+3,GT,'9')),EXIT                          
         B     MAINDSN             Ok, treat as full dsname                     
         END                                                                    
*-                                                                              
*-       For network file names:  If file name begins with % then               
*-         it is a network file.                                                
*-                                                                              
         IF    (@R4,EQ,'%'),BEGIN  Also a network file name...                  
         IF    (DSNON,Z),BEGIN     No volume...                                 
         IF    (CPHOST,Z),BEGIN    No default host...                           
         TSEG  'Can''t access remote file, no default host set.',,CR            
         ABORT 'Type HELP SET HOST for more information.',,REMFILE              
         END                                                                    
*                                                                               
         MVC   DSNON,CVBLANKS                                                   
         MVC   DSNON(L'CPHOST),CPHOST  Set default host                         
*                                                                               
         IF    (DSNWFILE,EQ,'%'),BEGIN  Tidy up other dsname...                 
         MVC   DSNWFILE(L'DSNWFILE-1),DSNWFILE+1  Remove %                      
         MVI   DSNWFILE+L'DSNWFILE-1,C' '                                       
         DECR  R15,DSNWLEN         Adjust file name length                      
         IF    (R15,NEG),'CLEAR DSNWLEN'                                        
         END                                                                    
         END                                                                    
*                                                                               
         LA    R4,@R4+1            Skip over %                                  
         B     MAINDSN                                                          
         END                                                                    
*-                                                                              
*-       If '$' specified its full dsname.                                      
*-                                                                              
         CLI   0(R4),C'$'                                                       
         BE    HAVDOL                                                           
*  full dsn not spec - construct first part of dsn as default                   
         MVC   0(4,R5),=C'WYL.'                                                 
         MVC   DSNXG(DSNXGL-1,R5),CPGRPSV                                       
         MVI   DSNXG+DSNXGL-1(R5),C'.'                                          
         MVC   DSNXU(DSNXUL-1,R5),CPUSERSV                                      
         MVI   DSNXU+DSNXUL-1(R5),C'.'                                          
*                                                                               
         IF    (CPPACCT,NZ),BEGIN  PACCOUNT is set...                           
         IF    ^CPFEXEC,EXIT       Only do if in exec mode                      
         MVC   DSNXG(DSNXGL-1,R5),CPPGRP                                        
         MVC   DSNXU(DSNXUL-1,R5),CPPUSER                                       
         END                                                                    
         LA    R6,DSNXN                                                         
WYLDSNL  EQU   *-2                 Length of wyl dsn std stuff (hcon)           
         SPACE 2                                                                
*  check operand for overrides                                                  
DODSNOR  CLR   R4,R3               Check if operand exhausted                   
         BNL   MAINDSN             Br if yes                                    
         LA    R15,OVERGRP                                                      
         IF    (@R4,EQ,'@'),OVERRIDE  Group override                            
         LA    R15,OVERUSER                                                     
         IF    (@R4,EQ,'&&'),OVERRIDE  User override                            
         B     MAINDSN                                                          
         SPACE 2                                                                
**  common routine to override specified operand                                
*     R15=address of default field                                              
*                                                                               
OVERRIDE LABEL                                                                  
*  find end of override                                                         
         LR    R1,R4               Set to start of field                        
         LR    R0,R3                                                            
         SR    R0,R1               Length remaining in operand                  
         LR    R2,R1                                                            
         B     ORSRCHST            Go enter search loop                         
*                                                                               
ORSRCH   CLI   @R2,C'.'            Is it delimiter?                             
         BE    ORDELIM             Br if yes                                    
         CLI   @R2,C'#'            Is it member specification                   
         BE    ORDELIM             Br if yes                                    
ORSRCHST LA    R2,@R2+1                                                         
         BCT   R0,ORSRCH                                                        
*  found a delim for override or end of operand                                 
ORDELIM  SR    R2,R1               Length of override                           
**  process override value                                                      
*     R1=addr of override operand                                               
*     R2=length of override                                                     
*                                                                               
OREADY   LR    R0,R2                                                            
         LA    R4,0(R2,R4)                                                      
         BASR  RAR,R15             Go do override                               
ORDONE   CLI   0(R4),C'.'          Normal delim?                                
         BNE   MAINDSN             Br if no - no more overrides                 
         LA    R4,1(R4)            Past delim char                              
         B     DODSNOR             Go test for others                           
         SPACE 2                                                                
*  override value is null - use logon value                                     
PLUGORIG LR    R1,R15              Address of source                            
         SR    R2,R2               Indicate null override                       
         B     OREADY                                                           
         SPACE 2                                                                
OVERUSER IF    (R0,EQ,1),'MVC CPCUSER,CPSUSER; BR RAR'                          
         IF    (R0,EQ,4),'MVC CPCUSER,@R1+1; BR RAR'                            
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'INVUSER'                                            
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid user.',,INVUSER                                       
         SPACE 2                                                                
OVERGRP  IF    (R0,EQ,1),'MVC CPCGRP,CPSGRP; BR RAR'                            
         IF    (R0,EQ,3),'MVC CPCGRP,@R1+1; BR RAR'                             
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'INVGRP'                                             
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid group.',,INVGRP                                       
         EJECT                                                                  
**  full dsn specified with '$'                                                 
HAVDOL   LA    R4,1(R4)            Bump past $                                  
         MVI   DSNWANL,C'$'        Flag $ dsname for later                      
         SPACE 2                                                                
**  process dsn after overrides or entire dsname                                
MAINDSN  AR    R5,R6               Update dsn pointer                           
         LR    R2,R3               End of operand                               
         SR    R2,R4               Length remaining                             
         BNP   ENDSNOP             Br if nothing left                           
*  check whether use of prefix was specified                                    
         CLI   0(R4),C'*'          Prefix char?                                 
         BNE   NOPFX                                                            
*-                                                                              
*-       Don't allow "*XYZ" as a dsname; it's "*" or nothing.                   
*-       (But all the code to handle "*xxx" still remains below).               
*-                                                                              
         IF    (R2,NE,1),BEGIN     Only allow "*" as dsname...                  
         IF    ((@R4+1,EQ,'#'),OR,(@R4+1,EQ,'(')),EXIT  "*#member"              
         IF    CPFDSNNE,BEGIN      Error suppression...                         
         MVC   CPERRID,=CL8'STARBAD'                                            
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R4),(R2)                                                        
         ERROR ': invalid characters in dsname.',,STARBAD                       
         END                                                                    
**  prefix to be used                                                           
         SET   DSNWAF1.DSNFQDSN    Avoid OS dsname check                        
         IF    (CPAFNAMEL,EQ,0),BEGIN                                           
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'NOSTAR'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR 'The current dsname is not set.',,NOSTAR                         
         END                                                                    
         IF    (DSNON,Z),BEGIN     DO HOST                                      
         L     R2,CPAHOSTL                                                      
         IF    (R2,POS),BEGIN                                                   
         CEIL  R2,L'DSNON                                                       
         MVC   DSNON,CVBLANKS                                                   
         MOVE  R2,DSNON,CPAHOST                                                 
         END                                                                    
         END                                                                    
         XPUSH R14,R1              DO MEMBER IF ANY                             
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         IF    (R0,POS),BEGIN                                                   
         MVC   DSNWAMBR,CVBLANKS                                                
         LR    R2,R0                                                            
         MOVE  R2,DSNWAMBR,@R1                                                  
         END                                                                    
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         LR    R2,R0               R2 prefix length                             
         XPOP  R14,R1                                                           
DOPFX    LA    R4,1(R4)            Past *                                       
**  insert prefix                                                               
         LA    R15,CPAFNAME                                                     
         IF    (@R15,EQ,'WYL.'),BEGIN  Wylbur dsn...                            
         IF    (R2,LE,11),EXIT  Nope                                            
         IF    ((@R15+3,NE,'.'),OR,(@R15+6,NE,'.')),EXIT                        
         IF    (@R15+10,NE,'.'),EXIT                                            
         SET   DSNWAF1.DSNFSTD     Standard dsn                                 
         B     PFXCOMA                                                          
         END                                                                    
*                                                                               
         CLI   @R15,C'$'           Is prefix entire dsn spec                    
*        BNE   INSPFX              Br if no                                     
         BNE   PFXCOMA             Br if no   CH - 05/91                        
         LA    R15,@R15+1          Past $                                       
         DECR  R2                  Decr len                                     
PFXCOMA  SR    R5,R6               Start over again                             
         LTR   R6,R6               Test if nothing generated yet                
         BZ    INSPFX              Bypass re-clear                              
         LA    R0,X'40'            Set up blank                                 
PFXLOOP  STC   R0,0(R6,R5)         Blank orig dsname                            
         BCT   R6,PFXLOOP          doesn't clear first char - tough             
         IF    (R2,NP),NOPFX       Br if $ was alone in pfx                     
INSPFX   AR    R6,R2               Compute total dsn length                     
         CH    R6,=H'44'           Is there room for pfx                        
         BH    DSNLONG             No                                           
         DEX   R2,PFXMVC           Add prefix to dsn                            
         LA    R5,1(R2,R5)         Update dsn pointer                           
         SPACE 2                                                                
** continue processing operand                                                  
NOPFX    CLR   R4,R3               Is scan at end of operand                    
         BNL   ENDSNOP             Br if yes                                    
         CLI   0(R4),C'('          Test paren specified alone                   
         BE    ENDSNOP             Br if yes                                    
*  scan for member spec (#) - scanner takes care of parens                      
         LR    R0,R3                                                            
         SR    R0,R4               Length remaining                             
         LR    R2,R4               Current pointer                              
MEMSRCH  CLI   @R2,C'#'                                                         
         BE    HVDSNSTR            Found end of string                          
         LA    R2,@R2+1                                                         
         BCT   R0,MEMSRCH                                                       
**  have end of dsname string                                                   
HVDSNSTR SR    R2,R4               Length of string                             
         BZ    ENDSNOP             Br if null string                            
HVDSTR2  AR    R6,R2               Compute total length                         
         CH    R6,=H'44'           Is it too long?                              
         BH    DSNLONG             Br if yes                                    
         LR    R15,R4              For ex mvc                                   
         DEX   R2,PFXMVC           Move string                                  
         LA    R2,@R2+1            Restore length                               
         AR    R4,R2               Update scan pointer                          
         AR    R5,R2               Update dsn pointer                           
         SPACE 2                                                                
***  end of dsn operand except member spec                                      
ENDSNOP  LR    R15,R5              Current point                                
         SR    R5,R6               Start of name                                
         SPACE 2                                                                
**  now process member name present                                             
         CLR   R4,R3               Is operand at the end?                       
         BNL   PARENSCN            Br if yes                                    
         CLI   0(R4),C'#'          Is it #?                                     
         BE    HAVEMEM                                                          
*  only other possibility is '('                                                
         B     PARENMEM                                                         
         SPACE 2                                                                
**  end of scanned stuff - check member in parens or # after ''                 
PARENSCN LM    R0,R1,CPSAVE        Get pointers to dsname                       
         AR    R1,R0               Point to character after                     
         CLI   @R1,C'#'            Member after quoted dsn?                     
         BE    MBRSCN              Br if yes                                    
         CLI   @R1,C'('                                                         
         BNE   DSNDONE             No member specified                          
MBRSCN   OSCTELL ,                 Find out if any left to scan                 
         LTR   R0,R0               Was there anything left?                     
         BNP   DSNDONE             Br if finished                               
         OSCAN L:=A(DSPRTB)        Go scan for member name                      
         B     DSNDONE             Done if nothing there                        
         SPACE 2                                                                
**  have member in parens or # following quoted dsname                          
BGOOD    LR    R4,R1               Loc of lparen                                
         LR    R3,R1                                                            
         AR    R3,R0               Loc of rparen+1                              
         CLI   0(R4),C'#'                                                       
         BE    HAVEMEM                                                          
PARENMEM DECR  R3                  Loc of rparen                                
*  process member name in parens or by #                                        
HAVEMEM  LA    R4,1(R4)            Past # or (                                  
         LR    R2,R3                                                            
         SR    R2,R4               Length of member name                        
         BZ    NULLMBR             Br if null override                          
         SPACE 1                                                                
         IF    ((@R4,EQ,'0)'),OR,(@R4,EQ,'-'),OR,(@R4,EQ,'+')),BEGIN            
         LR    R15,R5              R15 -> Start of dsn                          
         AR    R15,R6              R15 -> End of dsn + 1                        
         BCTR  R4,0                Fall back to start of gen#                   
         LA    R2,2(R2)            Compute the gen# length                      
         AR    R6,R2               Add into total dsn length                    
         DEX   R2,'MVC @R15(0),@R4' Move in generation number                   
         SET   CPFGEN              Flag as generation data set                  
         B     DSNDONE             Finish up                                    
         END                                                                    
         SPACE 1                                                                
         CH    R2,=H'8'            Check length                                 
         BNH   REPLMEM             Br if ok                                     
*  member name is too long                                                      
MBRLONG  IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  (R4),(R2)                                                        
         ERROR ': member name too long.'                                        
*  dsname is too long                                                           
DSNLONG  LR    R0,R6                                                            
         SR    R6,R2               Back up to prev length                       
         SR    R5,R6               Find start of dsname                         
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  (R5),(R0)                                                        
         ERROR ': dsname too long.'                                             
         SPACE 2                                                                
**  insert specified member name                                                
REPLMEM  MVC   DSNWAMBR,CVBLANKS                                                
         DEX   R2,MEMMVC           Move member name                             
         IF    (DSNWAMBR,EQ,=CL8'*'),BEGIN                                      
         XPUSH R14,R2                                                           
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         LR    R2,R0                                                            
         MVC   DSNWAMBR,CVBLANKS                                                
         MOVE  R2,DSNWAMBR,@R1                                                  
         XPOP  R14,R2                                                           
         IF    (DSNWAMBR,NE,' '),EXIT                                           
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'NOSTAR'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR 'The current member is not set.',,NOSTAR                         
         END                                                                    
         OC    DSNWAMBR,CVBLANKS   Convert to upper case                        
         B     DSNDONE                                                          
         EJECT                                                                  
DEVPRT   OSCKW DISK,DEVDISK                                                     
         OSCKW MICRO,DEVMICRO                                                   
         OSCKW SAMSON,DEVMICRO     (Compat--Remove me after 8/85)               
         OSCKW NETWORK,DEVNET,A                                                 
         OSCKW ,INVDEV                                                          
*                                                                               
DEVDISK  MVI   DSNDTYPE,DSNDDISK                                                
         B     DEVCONT                                                          
*                                                                               
DEVMICRO MVI   DSNDTYPE,DSNDSAM                                                 
         B     DEVCONT                                                          
*                                                                               
DEVNET   MVI   DSNDTYPE,DSNDNET                                                 
         B     DEVCONT                                                          
*                                                                               
INVDEV   IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  (R1),(R0)                                                        
         ABORT ' is an unkown device type.'                                     
         EJECT                                                                  
PFXMVC   MVC   0(0,R5),@R15                                                     
MEMMVC   MVC   DSNWAMBR(0),@R4                                                  
         SPACE 2                                                                
***  done with dsname                                                           
NULLMBR  CLEAR DSNWAMBR            Flag explicit null member                    
DSNDONE  TM    CPLFLG2,CPFDSNMS    Test null ok                                 
         BO    DOLGO               Br if so                                     
DSNDONE2 LTR   R6,R6                                                            
         BP    DOLGO                                                            
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         ERROR 'Dsname is null.'                                                
DOERREX  SET   CPFDSNBD            Set error                                    
         B     DSCNEXIT                                                         
DOLGO    STH   R6,DSNWANL          Save length for dsnstddo                     
         LCALL ZSNSTDDO            Go set dsname flags                          
**  if std wyl dsname check null after userid                                   
         TM    DSNWAF1,DSNFSTD     Is it std wyl dsn?                           
         BZ    DOLGO2              Br if no                                     
         CH    R6,WYLDSNL          Is wyl name just std length?                 
         BNE   DOLGO2              Br if no                                     
**  default dsname = 'lib'                                                      
         IF    (CPCMNM,EQ,'PFX'),PFXLIB  IT'S ok, mom                           
         TM    CPLFLG2,CPFDSNMS    Is default permitted?                        
         BO    DOLGO2              Br if no                                     
PFXLIB   MVC   DSNWADSN+DSNXN(L'CPLIB),CPLIB  move in def lib                   
         OI    DSNWAF1,DSNFWLIB    Flag wylbur library                          
         LA    R15,DSNWADSN+DSNXN                                               
BLKLOOP  CLI   @R15,C' '           At end of dsn                                
         BE    DOLGO2              Yes, have correct length                     
         LA    R6,1(,R6)           No, increment length                         
         LA    R15,@R15+1          Pt to next slot                              
         B     BLKLOOP             And look for end                             
DOLGO2   LR    R0,R6                                                            
         STH   R0,DSNWANL          Save dsname length                           
DSCNEXIT XEXIT                                                                  
         DROP  BR                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'Dsname Finish Routine -- Do Overrides'                          
* this routine processes any group or user overrides found.                     
* it must be called after scanning has been completed.                          
*                                                                               
ZSNFIN   XPROC                                                                  
*-                                                                              
*-       Validity checking.                                                     
*-                                                                              
         IF    DSNFASCII+DSNFEBCDIC,BEGIN  ASCII/EBCDIC explicit...             
         IF    DSNFTRAN,EXIT       It's OK, scram                               
         TSEG  'TRANSPARENT must be specified '                                 
         ERROR 'if ASCII/EBCDIC is specified.'                                  
         END                                                                    
*                                                                               
         IF    (DSNDTYPE,Z),BEGIN  Default device type...                       
*-                                                                              
*-       Check the device type based on the volume.                             
*-                                                                              
         MVI   DSNDTYPE,DSNDDISK   Assume disk                                  
*                                                                               
         IF    (DSNON,NZ),BEGIN    Have a volume...                             
*-                                                                              
*-       Check for Samson looking devices.                                      
*-                                                                              
         IF    CPSFSAM,BEGIN       Samson terminal...                           
         IF    ((DSNON,GE,'A'),AND,(DSNON,LE,'Z')),BEGIN                        
         IF    (DSNON+1,NE,' '),EXIT  Looking for one char device               
         MVI   DSNDTYPE,DSNDSAM    It's a samson device                         
         B     DFINEXT                                                          
         END                                                                    
*                                                                               
         IF    ((DSNON,EQ,'CLIP '),OR,(DSNON,EQ,'CLIPBOARD ')),BEGIN            
         MVI   DSNDTYPE,DSNDSAM    It's a samson device                         
         B     DFINEXT                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Check for volume name which is a host name.                            
*-                                                                              
         ACALL HOSTCHK             Check DSNON for host name                    
         IF    Z,'MVI DSNDTYPE,DSNDNET'  It's a network file                    
*-                                                                              
*-       Check for user defined device names.                                   
*-                                                                              
DFINEXT  SETMSG DSNON                                                           
*-                                                                              
*-       This is for an explicit device type set by the                         
*-         SET DEVICE command (it's not really used).                           
*-                                                                              
         VCALL FINDDEV             Is it an external device?                    
         IF    Z,BEGIN             Yes...                                       
         WITH  (DEV,R1)                                                         
         IF    DEVTSAM,'MVI DSNDTYPE,DSNDSAM'                                   
         IF    DEVTNET,'MVI DSNDTYPE,DSNDNET'                                   
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Process a disk device the same as before.                              
*-                                                                              
         IF    (DSNDTYPE,EQ,DSNDDISK),BEGIN  Disk...                            
         IF    (DSNON,NZ),BEGIN    Check volume name...                         
         CLC   DSNON+6(L'DSNON-6),CVBLANKS  Extra chars in vol name?            
         IF    EQ,EXIT             Yes, scram                                   
         TSEG  DSNON,,T                                                         
         ERROR ': volume/host not found.'                                       
         END                                                                    
         END                                                                    
*                                                                               
         IF    DSNWAF1.DSNFSTD,BEGIN  Process user/group override...            
         IF    (CPCGRP,NZ),'MVC DSNWADSN+DSNXG(DSNXGL-1),CPCGRP'                
         IF    (CPCUSER,NZ),'MVC DSNWADSN+DSNXU(DSNXUL-1),CPCUSER'              
         END                                                                    
*                                                                               
         LCALL ZSNUDSDO            Go set user data set flags                   
*                                                                               
         IF    (DSNDTYPE,EQ,DSNDDISK),BEGIN  Check dsname...                    
         IF    (DSNWADSN,EQ,'NULLFILE '),XIBADNM  Never, never                  
*                                                                               
         IF    (DSNFBADN,AND,^DSNWAF1.DSNFQDSN),BEGIN  Bad name...              
         IF    CPFGEN,EXIT         Allow funny chars if GDG dsname              
         LCALL DSNCHK              Is dsn ok?                                   
         IF    Z,EXIT              (Shouldn't happen)                           
         LR    R2,R15                                                           
XIBADNM  IF    CPFDSNNE,BEGIN      Error suppression request?                   
         MVC   CPERRID,=CL8'INVDSN'                                             
         SET   CPFDSNBD            Set error                                    
         EXIT  ZSNFIN                                                           
         END                                                                    
         TSEG  DSNWADSN,LH:DSNWANL                                              
         SETMSG ': invalid dsname.'                                             
         IF    (R2,EQ,8),'SETMSG ": invalid characters in dsname."'             
         ABORT (R1),(R0),INVDSN                                                 
         END                                                                    
         END                                                                    
*-                                                                              
*-       Check the exact copy of the file name the user specifed                
*-         for "*".  If it is an asterisk, then use the saved                   
*-         dsname.                                                              
*-                                                                              
         IF    ((DSNWLEN,EQ,1),AND,(DSNWFILE,EQ,'*')),BEGIN                     
         SETMSG DSNWADSN,LH:DSNWANL  Complete file name                         
         IF    (DSNDTYPE,NE,DSNDDISK),BEGIN  Non-disk...                        
         IF    DSNWAF1.DSNFSTD,'LA R1,@R1+11; SH R0,=H"11"'  No pfx             
         END                                                                    
*                                                                               
         CEIL  R0,L'DSNWFILE       (Safety)                                     
         IF    (R15,NP),'CLEAR R0'                                              
         STH   R0,DSNWLEN                                                       
         LR    R15,R0              File length                                  
         MVC   DSNWFILE,CVBLANKS   Blank out file name                          
         MOVE  R15,DSNWFILE,@R1    Save file name                               
         END                                                                    
*                                                                               
         PEND                                                                   
         TITLE 'Dsname Attribute and Possession Routines'                       
***  routine to set dsname attribute flags                                      
*                                                                               
ZSNSTDDO XENTER R15,R8                                                          
         CLEAR DSNWAF1.DSNFSTD+DSNFWLIB  reset flags                            
         CLC   =C'WYL.',DSNWADSN   First level ok?                              
         BNE   DODSNNS             Br if not std                                
         CLI   DSNWADSN+DSNXG+DSNXGL-1,C'.'  Group delim?                       
         BNE   DODSNNS             Br if not                                    
         CLI   DSNWADSN+DSNXU+DSNXUL-1,C'.'  User delim?                        
         BNE   DODSNNS             Br if not                                    
         LH    R0,DSNWANL          Get dsname length                            
         CH    R0,=Y(DSNXN)        Is it long enough?                           
         BL    DODSNNS             Br if not                                    
         OI    DSNWAF1,DSNFSTD     Set std wyl dsn flag                         
         LA    R2,DSNXN+8          Length of std pfx +8                         
         CR    R0,R2               Is that the length?                          
         BH    DODSNNS             Br if not                                    
         CLC   CPLIB,DSNWADSN+DSNXN  Is a wylbur lib?                           
         BNE   DODSNNS             Br if not                                    
         OI    DSNWAF1,DSNFWLIB    Say so                                       
DODSNNS  XEXIT R15,R8                                                           
         DROP  BR                                                               
         EJECT                                                                  
***  routine to set dsname possession flags                                     
*                                                                               
ZSNUDSDO XENTER R15,R8                                                          
         CLEAR DSNFBADN            Assume a-ok dsname                           
         IF    ^CPFGEN,BEGIN       Not a GDG...                                 
         LCALL DSNCHK              Is it a valid os dsname?                     
         IF    NZ,'SET DSNFBADN'   Flag as invalid                              
         END                                                                    
*                                                                               
         NI    DSNWAF1,255-DSNFMYDS  reset                                      
         TM    DSNWAF1,DSNFSTD     Is it std data set?                          
         BZ    DODSNSX             Br if not                                    
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPSGRP  his group?                      
         BNE   DODSNGSV            Br no to test saved group                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPSUSER  his id?                        
         BE    DODSNSMY            Br if yes                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BNE   DODSNGSV            Br no to test saved group                    
DODSNSMY OI    DSNWAF1,DSNFMYDS    Set my data set flag                         
DODSNSX  XEXIT R15,R8                                                           
*                                                                               
DODSNGSV TM    CPGFLG3,CPFKWENT    Did user give kw for save grp                
         BZ    DODSNSA             no, don't test saved group                   
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPGRPSV  saved group?                   
         BNE   DODSNSA             Br if not                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPUSERSV  saved user?                   
         BE    DODSNSMY            Br yes to set my data set                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BE    DODSNSMY            Br yes to set my data set                    
         B     DODSNSX             Br to set not mine                           
*                                                                               
DODSNSA  IF    (CPPACCT,NZ),BEGIN  Check PACCOUNT...                            
         IF    ^CPFEXEC,EXIT       Only in exec mode                            
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPPGRP  PACCT group?                    
         IF    NE,EXIT             No                                           
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPPUSER  PACCT user?                    
         BE    DODSNSMY            Br if yes                                    
         END                                                                    
*                                                                               
         TM    CPGFLG4,CPFAUSET    Authority field set?                         
         BZ    DODSNSX             no, don't test auth group                    
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPAUTHGP  auth group?                   
         BNE   DODSNSX             Br if not                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPAUTHUS  auth user?                    
         BE    DODSNSMY            Br yes to set my data set                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BE    DODSNSMY            Br yes to set my data set                    
         B     DODSNSX             Exit                                         
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DSNCHK -- Routine to check that dsname conforms to OS                        
*    data set name conventions.                                                 
*                                                                               
*      On entry:                                                                
*        DSNWA - contains dsname, etc.                                          
*                                                                               
*      On exit, R15 (and CC):                                                   
*        0 - ok                                                                 
*        4 - bad os dsname                                                      
*        8 - bad chars in dsname                                                
*                                                                               
DSNCHK   XENTER R2,R8,,LOCAL                                                    
         CLEAR R15                 Assume ok                                    
*                                                                               
         LA    R6,DSNWADSN                                                      
         LTH   R5,DSNWANL                                                       
         BNP   DSNRC4              Null dsname is n.g.                          
*                                                                               
         LA    R4,8                Max no. of chars per level                   
         LA    R3,2                Allow dot, alpha, or national                
         WHILE (R5,POS),BEGIN                                                   
         LC    R2,@R6              Get char                                     
         LC    R0,DSNCHR(R2)       Get char type                                
         IF    (R0,Z),BEGIN        It's a dot...                                
         IF    (R4,GE,8),DSNRC4    Dot can't be first char                      
         LA    R4,8                Allow eight more chars                       
         LA    R3,2                Allow dot, alpha, or national                
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (R0,GT,R3),'LA R15,8; B DSNCHKX'  bad chars                      
         DECR  R4                                                               
         IF    (R4,NEG),DSNRC4     Over 8 chars w/o a dot                       
         LA    R3,3                Allow numbers too now                        
         END                                                                    
         LA    R6,@R6+1                                                         
         DECR  R5                                                               
         END                                                                    
         B     DSNCHKX             All done, aok -- scram                       
*                                                                               
DSNRC4   LA    R15,4                                                            
DSNCHKX  XEXIT ,,LTR                                                            
         DROP  BR                                                               
         SPACE 2                                                                
*-                                                                              
*-        Dsname character type table.                                          
*-                                                                              
*-       0 - Dot (.)                                                            
*-       1 - Alphabetic                                                         
*-       2 - National ($,@,#)                                                   
*-       3 - Numeric                                                            
*-           255 - Everything else                                              
*-                                                                              
DSNCHR   DC    256X'FF'                                                         
         ORG   DSNCHR+C'.'                                                      
         DC    X'00'                                                            
         ORG   DSNCHR+C'A'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'J'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'S'                                                      
         DC    X'0101010101010101'                                              
         ORG   DSNCHR+C'$'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'@'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'#'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'0'                                                      
         DC    X'03030303030303030303'                                          
         ORG                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  HOSTCHK -- Routine to check to see if name passed is                         
*    the name of another host computer on the network.                          
*                                                                               
*    On entry:                                                                  
*      DSNON = possible host name                                               
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - yes, this is a host name                                             
*      4 - no, it is not a host name                                            
*                                                                               
HOSTCHK  PROC                                                                   
         SETMSG DSNON               Possible host name                          
         VCALL RTRIM               Get length                                   
         IF    (R0,LT,2),HOSTNO    Too short, can't be host name                
         IF    (R0,GT,6),HOSTYES   Too long for OS volume                       
*-                                                                              
*-       Check for "aaannn" (a=alpha,n=numeric) style name.                     
*-         If we find a name like this then assume it is a                      
*-         disk volume name.                                                    
*-                                                                              
         IF    (R0,EQ,6),BEGIN     Exactly 6 characters...                      
         IF    ((DSNON,GE,'A'),AND,(DSNON,LE,'Z'),AND,                 *        
               (DSNON+1,GE,'A'),AND,(DSNON+1,LE,'Z'),AND,              *        
               (DSNON+2,GE,'A'),AND,(DSNON+2,LE,'Z'),AND,              *        
               (DSNON+3,GE,'0'),AND,(DSNON+3,LE,'9'),AND,              *        
               (DSNON+4,GE,'0'),AND,(DSNON+4,LE,'9'),AND,              *        
               (DSNON+5,GE,'0'),AND,(DSNON+5,LE,'9')),HOSTNO                    
         END                                                                    
*-                                                                              
*-       Check the volume table to see if this is a disk volume.                
*-                                                                              
         LA    R1,DSNON                                                         
         VCALL VOLCHK              Is this a disk volume?                       
         BNZ   HOSTYES             No, then maybe it's a host                   
*                                                                               
HOSTNO   LA    R15,4               It's not a host name                         
         B     HOSTEXIT                                                         
*                                                                               
HOSTYES  CLEAR R15                 It is a host name                            
HOSTEXIT PEND                                                                   
         TITLE 'Dsname Parameter Scan Routines'                                 
* the dsname parameter scan routines are used by including                      
* a push pointer to dsnprt in the scan table for a command.                     
* these routines recognize group and user overrides. at the                     
* end of the scan of a line, the routine dsnfin completes                       
* processing of these parameters and must be called.                            
*                                                                               
HAVTEMP  PROC                                                                   
         VCALL EXIT2               Get scratch volser                           
         CEIL  R0,L'DSNON                                                       
         MVC   DSNON,CVBLANKS                                                   
         LR    R15,R0                                                           
         MOVE  R15,DSNON,@R1       Set volume name                              
         PEND                                                                   
*                                                                               
HAVOLM   PROC                                                                   
         VCALL SCANWTOK            Pick off next token                          
         IF    NZ,BEGIN            Nothing more...                              
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         SET   CPFDSNBD                                                         
         EXIT  HAVOLM                                                           
         END                                                                    
         ERROR 'Missing disk/host name after "ON".'                             
         END                                                                    
*                                                                               
         CEIL  R0,L'DSNON                                                       
         MVC   DSNON,CVBLANKS      Pre-blank                                    
         LR    R15,R0                                                           
         MOVE  R15,DSNON,@R1       Save volume/host name                        
         L     R15,CVUPPTBL                                                     
         TR    DSNON,@R15          Convert to upper case                        
*                                                                               
         IF    ((DSNON,EQ,'CATALOG '),OR,(DSNON,EQ,'CATLG '),          *        
               OR,(DSNON,EQ,'CAT ')),'CLEAR DSNON'  Reset volume                
         PEND                                                                   
*                                                                               
SETSET   OI    CPLFLG3,CPFSET      Turn on set operand                          
         BR    RAR                                                              
*                                                                               
SETNOSET NI    CPLFLG3,255-CPFSET  Turn off set flag                            
         BR    RAR                                                              
*                                                                               
HAVGUEST BASE  R15                                                              
         MVC   DSNNUSER,=CL(L'DSNNUSER)'guest'                                  
         MVC   DSNNPASS,CVBLANKS                                                
         BR    RAR                                                              
         DROP  R15                                                              
*                                                                               
HAVPUB   BASE  R15                                                              
         MVC   CPCACCT,=C'&PUBUSER&PUBGRP'  Public account                      
         BR    RAR                                                              
         DROP  R15                                                              
*                                                                               
ZNVCODE  XENTER R7,R8,*                                                         
         LR    R15,R0              Load length of code string                   
         IF    ((R15,LT,8),OR,(R15,GT,16)),BEGIN                                
         ERROR 'CODE must be 8-16 characters in length.'                        
         END                                                                    
         CLEAR (@R13,16)           Clear work area                              
         SET   DSNFCODE            Set encode/decode flag                       
         DEX   R15,'MVC @R13(0),@R1'  Move code str to work area                
         L     R15,@R13            Load 1st four bytes                          
         AL    R15,@R13+4          Add 2nd four bytes                           
         AL    R15,@R13+8          Add 3rd four bytes                           
         AL    R15,@R13+12         Add 4th four bytes                           
         LPR   R15,R15             Make code value positive                     
         ST    R15,DSNCODE         Store code value                             
         XEXIT R7,R8                                                            
*                                                                               
LRECLA   ST    R15,CPLRCL          Set LRECL                                    
         BR    RAR                                                              
*                                                                               
NEWEDIT  SET   CPFNEWED            NEWEDIT                                      
         CLEAR CPLRCL              Set Edit format                              
         BR    RAR                                                              
*                                                                               
OLDEDIT  CLEAR CPFNEWED            OLDEDIT                                      
         CLEAR CPLRCL              Set Edit format                              
         BR    RAR                                                              
*                                                                               
EDITA    CLEAR CPLRCL              Set Edit format                              
         BR    RAR                                                              
*                                                                               
CARDA    LA    R15,80                                                           
         ST    R15,CPLRCL          Set CARD format                              
         BR    RAR                                                              
*                                                                               
PRNTA    LA    R15,133                                                          
         ST    R15,CPLRCL          Set PRINT format                             
         BR    RAR                                                              
*                                                                               
TABS     SET   DSNFTABS            Keep TAB characters                          
         BR    RAR                                                              
*                                                                               
NOTABS   CLEAR DSNFTABS            Expand TAB characters in data                
         BR    RAR                                                              
*                                                                               
FMT      CLEAR DSNFUFMT+DSNFTABS   FORMATTED                                    
         BR    RAR                                                              
*                                                                               
UNFMT    SET   DSNFUFMT+DSNFTABS   UNFORMATTED                                  
         BR    RAR                                                              
*                                                                               
TEXTONLY SET   DSNFTEXTONLY        TEXTONLY                                     
         BR    RAR                                                              
*                                                                               
TRANS    SET   DSNFTRAN            Set TRANSPARENT format                       
         BR    RAR                                                              
*                                                                               
DTRANS   SET   DSNFDTRAN+DSNFTRAN  Set DTRANSPARENT format                      
         BR    RAR                                                              
*                                                                               
ASCII    SET   DSNFASCII           ASCII                                        
         CLEAR DSNFEBCDIC          Not EBCDIC                                   
         BR    RAR                                                              
*                                                                               
EBCDIC   SET   DSNFEBCDIC          EBCDIC                                       
         CLEAR DSNFASCII           Not ASCII                                    
         BR    RAR                                                              
*                                                                               
WRAP     IF    (CPCMNM,EQ,'SAV'),CVNVALID  WRAP invalid on SAVE                 
         CLEAR DSNFSPLT            WRAP, not SPLIT                              
         STH   R15,DSNWRAPL        Save WRAP length                             
         BR    RAR                                                              
*                                                                               
SPLIT    IF    (CPCMNM,EQ,'SAV'),CVNVALID  SPLIT invalid on SAVE                
         SET   DSNFSPLT            SPLIT, not WRAP                              
         STH   R15,DSNWRAPL        Save WRAP length                             
         BR    RAR                                                              
*                                                                               
NOENQ    PROC  ,                                                                
         IF    ^CPSFSPR,CVNVALID   Only the strong survive ...                  
         SET   DSNFNONQ            No enqueue on dataset                        
         PEND  ,                                                                
*                                                                               
SETSEQ   XENTER R7,R8,12+L'CPOSCAN,LOCAL  Regsave+scan save area                
         NI    CPLFLG5,255-CPFNONUM  Set numbered default                       
         OI    CPLFLG5,CPFFOPT     Set number option given                      
         MVC   @R8+12(L'CPOSCAN),CPOSCAN  scan pointer save                     
         LM    R0,R1,CPSAVE        Get seq fld pointers                         
         CLI   @R1,C'('            Need left paren                              
         BNE   SETSEQ1             No, only position given                      
         SH    R0,=Y(2)            Strip paren count                            
         BM    CVNVALID                                                         
         LA    R1,@R1+1            Kick pointer past "("                        
SETSEQ1  OSCINIT (R1),(R0)         Initialize scanner                           
         OSCAN SEQPRT1             Scan for seqfld                              
         B     SETSEQ3             Nothing found                                
*                                                                               
SSEQEND  OI    CPSEQFLD,X'80'      Set seq fld at end                           
         B     SETSEQ2             Go to get length                             
*                                                                               
SSEQFLD  LTR   R15,R15             Test length                                  
         BNP   CVNVALID            Must be positive                             
         STH   R15,CPSEQFLD        Save seq fld position                        
SETSEQ2  OSCAN SEQPRT2             Scan for length                              
         B     SETSEQ3             Done, leave length alone                     
*                                                                               
SSEQLN   LTR   R15,R15                                                          
         BNP   CVNVALID            Length must be positive                      
         STH   R15,CPSEQLN         Save length                                  
         OSCAN ,                   Check rest of parm blank                     
         BNZ   CVNVALID            Error if not blank                           
SETSEQ3  MVC   CPOSCAN,@R8+12       Restore scan values                         
         XEXIT R7,R8                                                            
         DROP  BR                                                               
*                                                                               
RTNAD    EQU   32                                                               
PRESTPT  EQU   32+4                                                             
LINAREA  EQU   32+8                                                             
WRKAREA  EQU   32+16                                                            
         TITLE 'Literals'                                                       
         QLTORG                                                                 
         EJECT                                                                  
* for dodsnm routine                                                            
*                                                                               
DSPRTB   OSCKW ,BGOOD                                                           
*                                                                               
* dsnprtf is the scanner prt used by use, save, exec from,                      
* and copy from to recognize format dsname keywords as well                     
* as the normal dsname keywords.                                                
*                                                                               
         ENTRY DSNPRT,DSNPRTF,DSNPRTON                                          
*                                                                               
DSNPRTF  OSCKW EDIT,EDITA,A                                                     
         OSCKW NEWEDIT,NEWEDIT                                                  
         OSCKW OLDEDIT,OLDEDIT                                                  
         OSCKW CARD,CARDA,A                                                     
         OSCKW PRINT,PRNTA,A                                                    
         OSCKW TABS,TABS,A                                                      
         OSCKW NOTABS,NOTABS,A                                                  
         OSCKW UNFORMATTED,UNFMT,A                                              
         OSCKW FORMATTED,FMT,A                                                  
         OSCKW TEXTONLY,TEXTONLY,A                                              
         OSCKW TRANSPARENT,TRANS,A                                              
         OSCKW DTRANSPARENT,DTRANS,A                                            
         OSCKW ASCII,ASCII,A                                                    
         OSCKW EBCDIC,EBCDIC,A                                                  
         OSCKW WRAP,WRAP,(A,P,PI),&LINESZ                                       
         OSCKW SPLIT,SPLIT,(A,P,PI),&LINESZ                                     
         OSCKW LRECL,LRECLA,(P,PI,A),256                                        
         OSCKW CODE,ZNVCODE,(P,A)                                               
         OSCKW ,NUMPRT,PUSH                                                     
         OSCKW SEQFLD,SETSEQ,(A,P)                                              
*                                                                               
* dsnprt is the scanner prt used by all of wylbur to                            
* recognize dsname keywords.                                                    
*                                                                               
DSNPRT   OSCKW ON,HAVOLM                                                        
         OSCKW VOLUME,HAVOLM,A                                                  
         OSCKW WINGS,V(SCNNOOP)    ALLOW, IGNORE TYPES IN                       
         OSCKW UNIX,V(SCNNOOP)     OLD FILE OPTIONS SCANNING                    
         OSCKW SAMSON,V(SCNNOOP),A                                              
         OSCKW NET,V(SCNNOOP)                                                   
         OSCKW OLDIBM,V(SCNNOOP),A                                              
         OSCKW TEMPORARY,HAVTEMP,A                                              
         OSCKW SET,SETSET                                                       
         OSCKW NOSET,SETNOSET,A                                                 
         OSCKW PUBLIC,HAVPUB,A                                                  
         OSCKW GUEST,HAVGUEST,A                                                 
         OSCKW NOENQUEUE,NOENQ,A                                                
         OSCKW ,ACCTPRT,PUSH                                                    
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
DSNPRTON OSCKW ON,HAVOLM                                                        
         OSCKW VOLUME,HAVOLM,A                                                  
         OSCKW ,,POP                                                            
         OSCKW ,V(INVALID)                                                      
*                                                                               
SEQPRT1  OSCKW END,SSEQEND                                                      
         OSCKW ,SSEQFLD,PI,&LINESZ                                              
         OSCKW ,V(INVALID)                                                      
*                                                                               
SEQPRT2  OSCKW ,SSEQLN,PI,9                                                     
         OSCKW ,V(INVALID)                                                      
*                                                                               
         VLTORG                                                                 
         END   .                                                                
