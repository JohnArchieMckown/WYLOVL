COMM     TITLE 'WYLBUR''s Command Processor'                                    
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN ,                 Define installation params                   
         GBLC  &GREET,&INSTALN                                                  
*                                                                               
WYLCOMM  CSECT                                                                  
COMM     IDENT 4072                10:30:05 03/12/04  (SLP)                     
         SPACE 2                                                                
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'Constants, etc.'                                                
         COPY  CONTROL             Copy CV/CP                                   
*                                                                               
         RECORD 'PDB'                                                           
         EJECT                                                                  
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         EJECT                                                                  
*                                                                               
         POP   DSECTS                                                           
         TITLE 'Command Processor'                                              
*box                                                                            
*                                                                               
*                                                                               
*                                                                               
*  COMMAND - main command loop                                                  
*                                                                               
*                                                                               
*  This routine is called at the completion of each command.                    
*  The routine cleans up after the previous command, and                        
*  re-initializes local cells, and then branches to get the                     
*  the next command.                                                            
*        (The next command is then scanned, the scanner                         
*  branching to the correct routine to execute the given                        
*  command.  Upon command completion the command returns                        
*  here. And on and on !)                                                       
*                                                                               
*  NOTE: the cleanup routine has no particular logic, except                    
*  to do the things that are required at command termination                    
*  It handles normal and abnormal terminations.  It is a hodge-                 
*  podge of stuff, some order dependent, some not.  It not                      
*  a pretty sight, but there are worse routines.                                
*                                                                               
*                                                                               
*                                                                               
COMMSEG  XPROC                                                                  
         TSEG  (R1),(R0)                                                        
         ACALL COMMAND                                                          
         PEND                                                                   
*                                                                               
COMMAND  XPROC                                                                  
*                                                                               
*                                                                               
         ACALL CLEANUP             Clean up after last command                  
*                                                                               
         IF    CPFMON,BEGIN        If exec monitor on,,,                        
         VCALL MONCMD              Monitor command                              
         END                                                                    
*                                                                               
         ACALL INITCMD             Re-initialize                                
*                                                                               
         XTRACE COMMAND,REGS=                                                   
         SPACE 3                                                                
*-                                                                              
*-       If EXEC mode,                                                          
*-          get next command from EXEC file.                                    
*-                                                                              
         IF    CPFEXEC,BEGIN                                                    
         VCALL GETEXCMD            DOES NOT RETURN                              
         END                                                                    
         SPACE 2                                                                
*-                                                                              
*-       If XCOMMAND is waiting,                                                
*-          set up that command and execute it.                                 
*-                                                                              
         IF    (CPXCMDP,NZ),BEGIN                                               
         ACALL GETXCCMD            DOES NOT RETURN                              
         END                                                                    
*-                                                                              
*-       Otherwise,                                                             
*-          get next command from terminal.                                     
*-                                                                              
         ACALL GETTCMD             DOES NOT RETURN                              
         PEND                                                                   
         TITLE 'Execute XCOMMAND saved command'                                 
*-                                                                              
*-       Set up saved XCOMMAND and then execute it.                             
*-                                                                              
*-                                                                              
*-   NOTE:  This routine does not return.  Get gets the next command            
*-         and calls EDTCOM to begin command execution.                         
*-                                                                              
GETXCCMD PROC                                                                   
*-                                                                              
*-       Move XCOMMAND from saved area to CPCMD.                                
*-                                                                              
         L     R4,CPXCMDP                                                       
         LH    R15,@R4             Command length                               
         CEIL  R15,L'CPCMD                                                      
         STH   R15,CPCMDLEN                                                     
         MOVE  R15,CPCMD,@R4+2     Move in command text                         
*-                                                                              
*-       Free the XCOMMAND save area.                                           
*-                                                                              
         CLEAR CPXCMDP             Reset XCOMMAND ptr                           
         LR    R1,R4                                                            
         VCALL FREECORE            Free up saved XCOMMAND text                  
*-                                                                              
*-       Execute the command.                                                   
*-                                                                              
         CLEAR CPFTYPED            Not a typed command                          
         SETMSG CPCMD,LH:CPCMDLEN                                               
         ACALL EDTCOM              --- EXIT --->>>                              
         BOMB  ,                   (No return)                                  
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Get command from the terminal'                                  
*-                                                                              
*-       Get, process command from terminal.                                    
*-                                                                              
*-                                                                              
*-   NOTE:  This routine does not return.  Get gets the next command            
*-         and calls EDTCOM to begin command execution.                         
*-                                                                              
GETTCMD  PROC                                                                   
         CLC   CPWCHAR,=CL(L'CPWCHAR)'GENERAL'                                  
         IF    NE,BEGIN            Need to change back to GENERAL...            
         MVC   CPWCHAR,=CL(L'CPWCHAR)'GENERAL'  Force switch back               
         VCALL BILLCHK             Update charging component                    
         END                                                                    
*                                                                               
         IF    CPFSEC,BEGIN        Secure mode...                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Set logoff pending                     
         VCALL SIGNOFF             Go terminate session                         
         END                                                                    
*                                                                               
         IF    CPGFNCMD,BEGIN      New command is already waiting...            
         CLEAR CPGFNCMD+CPGFWR     Reset stuff                                  
*                                                                               
         SETMSG CPCMD,LH:CPCMDLEN   Saved command text                          
         IF    (R0,NP),CVNEXT                                                   
         CLEAR R15                 Dummy tread return code                      
         B     CMDDO                                                            
         END                                                                    
*                                                                               
*        XWTO  'GETTCMD call INITPCMD' *** slp debug ***                        
         IF    CVFPUSR,'VCALL INITPCMD'  Pseudo-user is special                 
*        XWTO  'GETTCMD after INITPCMD' *** slp debug ***                       
*                                                                               
         IF    CPVFPEND,BEGIN      Go back to page mode...                      
         SET   CPVFPAGE,CPVFCLR                                                 
         CLEAR CPVFPEND                                                         
         END                                                                    
*                                                                               
         IF    CPVFPAGE,'VCALL PAGECMD'  Get next page mode cmd                 
*                                                                               
         TSEG  CPCPRMT,LH:CPCPLEN  User's prompt text                           
*                                                                               
         IF    (CPORVCHR,NZ),'TSEG CPORVCHR'  ORVYL prompt char                 
         SETMSG '? '                                                            
         IF    CPSFUPL,'SETMSG "> "'                                            
*                                                                               
         IF    CPSTCONS,BEGIN      No prompt if console...                      
         TCLEAR                                                                 
         IF    ^CPGFWR,'TSEG "@OK",,WR'  Indicate command accepted              
         CLEAR R0                  No prompt text                               
         END                                                                    
*                                                                               
         SET   CPFTYPED            Typed command                                
         CLEAR CPGFWR                                                           
*-                                                                              
*-       Main Command Prompt.                                                   
*-                                                                              
*        XWTO  'GETTCMD TREAD command' *** slp debug ***                        
         TREAD (R1),(R0)           Get a command                                
*        XWTO  'GETTCMD TREAD done'    *** slp debug ***                        
*                                                                               
CMDDO    L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),BEGIN                                                   
         CLEAR JCBFATTN            Reset attn (race condition)                  
         CLEAR JCBSFMIC            No longer in Micro mode                      
         END                                                                    
*                                                                               
         IF    ((R15,Z),AND,(R0,Z),AND,CPGFLG3.CPFCMDFX),'LA R15,3'             
*-                                                                              
*-       Count number of commands typed.                                        
*-                                                                              
         IF    ((R15,Z),AND,(R0,POS)),BEGIN  Command was typed...               
         XPUSH R15,R1                                                           
         L     R5,CVCURJCB                                                      
         LA    R2,=CL16'TYPED_COMMANDS'                                         
         VCALL WCTRINCR            Count number of commands                     
         XPOP  R15,R1                                                           
         END                                                                    
*-                                                                              
*-       Process special command read completion.                               
*-                                                                              
         LTR   R15,R15             Tread return code                            
         IF    NEG,'VCALL SIGNOFF'  Logging off                                 
         IF    POS,BEGIN           Ended with PF...                             
         IF    (R15,EQ,1),BEGIN    Break...                                     
         IF    ^CPGFNATN,BEGIN                                                  
         SETMSG 'COLLECT'                                                       
         ACALL EDTCOMMV                                                         
         END                                                                    
         END                                                                    
*                                                                               
         IF    (R15,EQ,2),BEGIN    ?break...                                    
         IF    (R0,Z),'VCALL ORVCABRT'  Abort any ORVYL read                    
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,3),BEGIN    @break...                                    
         LR    R2,R0                                                            
         VCALL RTRIM                                                            
         IF    (R0,Z),'ACALL RETRY'  Retry previous command                     
         CEIL  R2,L'CPCMD                                                       
         STH   R2,CPCMDLEN                                                      
         MOVE  R2,CPCMD,@R1        Save command text                            
         ACALL CMDRETRY            Go do command retry                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Set up command for execution.                                          
*-                                                                              
         LR    R2,R0                                                            
         VCALL RTRIM                                                            
         IF    (R0,Z),CVNEXT       don't update command, just scram             
*                                                                               
         LR    R0,R2                                                            
         IF    (R0,GT,L'CPCMD),BEGIN                                            
         MVC   CPCMD,@R1           Save command text                            
         TSEG  'Command truncated to &LINESZ characters.',,WR                   
         SETMSG CPCMD                                                           
         END                                                                    
         ACALL EDTCOMMV            --- EXIT --->>>                              
*                                                                               
         PEND  ,              WE EXIT ABOVE,                                    
*                                                                               
         QLTORG                                                                 
         TITLE 'Command clean up'                                               
*box                                                                            
*                                                                               
*                                                                               
*  CLEANUP - clean up after command execution                                   
*                                                                               
*                                                                               
*  NOTE: the cleanup routine has no particular logic, except                    
*  to do the things that are required at command termination                    
*  It handles normal and abnormal terminations.  It is a hodge-                 
*  podge of stuff, some order dependent, some not.  It not                      
*  a pretty sight, but there are worse routines.                                
*                                                                               
*                                                                               
*                                                                               
CLEANUP  PROC                                                                   
         L     R5,CVCURJCB                                                      
         USING JCB,R5                                                           
*-                                                                              
*-       Free owned resources, if any                                           
*-                                                                              
         IF    (CPRFLG,NZ),BEGIN                                                
         IF    CPRFEXT,'VCALL EXTCLOSE'                                         
         IF    CPRFSUB,'VCALL JESOCLS'                                          
         IF    CPRFJES,'VCALL JESICLS'                                          
         IF    CPRFKWR,'VCALL KWRFREE'                                          
         IF    CPRFKEYS,'VCALL KRSCFREE'                                        
         IF    CPRFDICT,'VCALL DICTCLS'                                         
         END                                                                    
*                                                                               
         LA    R15,1               Free any temporary dsn enq's                 
         VCALL DENQFREE            Do any DEQ's needed                          
*                                                                               
         IF    (CPIOWAP,NZ),'VCALL DCLOSE'  Release IOWA                        
*-                                                                              
*-       If error, save error message                                           
*-                                                                              
*-      (we must do this here before the seg buffer is cleared.  the            
*-      error message may or may not be used, but at least it is set            
*-        correctly)                                                            
*-                                                                              
         IF    (CPFERROR,OR,CPFABORT),BEGIN                                     
         VCALL SETEMSG                                                          
         END                                                                    
*-                                                                              
*-       Do miscellaneous terminal/page mode stuff                              
*-                                                                              
         COMMENT                   Miscellaneous stuff follows                  
*                                                                               
         COMMENT                   If error, suppress DUMP/QUIET                
         IF    (CPFERROR,OR,CPFABORT),BEGIN                                     
         IF    ^CPFTRY,BEGIN       unless we have TRY                           
         CLEAR CPFDUMP             Clear DUMP/QUIET flags                       
         CLEAR CPFQUIET                                                         
         CLEAR CPFXDUMP                                                         
*        CLEAR CPFXQT DO NOT EXEC QUIET, UP TO EXEC TO CLEAR                    
         END                                                                    
         END                                                                    
*                                                                               
         IF    (CPLENWRN,NZ),'VCALL LMSGCHK'  Write length warnings             
*                                                                               
         MVC   CPVMSG,CVBLANKS     Reset any page mode info msg                 
*                                                                               
         IF    CPVFPAGE,BEGIN      Page mode...                                 
         VCALL PAGEMSG             Save any message text                        
*                                                                               
         IF    CPVFSPEC,BEGIN      Write final page...                          
         VCALL PAGEMOD             Display the current page                     
         IF    NZ,'VCALL PAGERES'  Reset page mode                              
         B     CVNEXT                                                           
         END                                                                    
         END                                                                    
*                                                                               
         TWRITE                                                                 
*                                                                               
         CLEAR CPFDUMP+CPFQUIET    Reset local flags                            
*                                                                               
         IF    CPLSTOWD,BEGIN      Lines have been stowed...                    
         SET   (CPLFLG5.CPFNLST,EQ)                                             
         CLEAR CPLFLG1.CPFTODFT    Assume TO not defaulted                      
         IF    ^CPLCOLXP,'SET CPLFLG1.CPFTODFT'                                 
         L     R15,CPDMPLST        Next line                                    
         SL    R15,CPDMPDLT        Minus delta for last                         
         CLEAR CPLSTOWD                                                         
         VCALL LASTLINE            Do last line msg                             
         TWRITE                                                                 
         END                                                                    
*-                                                                              
*-       Free up miscelaneous buffers,  Check for auto logoff.                  
*-                                                                              
         PFREE PAR                                                              
         PFREE PBR                                                              
         VCALL PJNKTMP             Junk any temporary pages                     
         ACALL JUNKHEAP            Release heap space                           
*                                                                               
         VCALL PAGEMON             Check global page buffer usage               
*-                                                                              
*-       Check to see if it is necessary to switch charging                     
*-         components.                                                          
*-                                                                              
         CLEAR CPLWCHAR            Reset local charging override                
         VCALL BILLCHK             Keep charging info current                   
*                                                                               
         IF    ('TSTAT',NEG),'VCALL SIGNOFF'  Do auto logoff                    
         CLEAR CPFRCVY             Reset rcvy flags                             
*-                                                                              
*-       Return to logon prompt if not logged on (and in cmd mode).             
*-                                                                              
         IF    (JCBACCT,Z),'CLEAR CPSACCT'  No account yet                      
*                                                                               
         IF    ((CPSACCT,Z),AND,^CPFEXEC),'VCALL SIGNRES'                       
         AIF   (&$PCODE).SKIPSIB                                                
         IF    JCBBFSIB,BEGIN      Get new SIB...                               
         TCNTL CTLSNSI             Sense SIB                                    
         FAIL  NZ,MILERR                                                        
         CEIL  R0,L'CPSIB                                                       
         LR    R15,R0                                                           
         MOVE  R15,CPSIB,@R1                                                    
         CLEAR JCBBFSIB                                                         
         MVC   JCBSUC,CPSUC                                                     
         END                                                                    
.SKIPSIB ANOP                                                                   
*-                                                                              
*-       Handle ERROR/ATTN processing below.                                    
*-                                                                              
*-       Note:  ATTN/ERROR stuff must be done late (ie here),                   
*-              because attns are often posted late.                            
*-                                                                              
         COMMENT                                                                
*-                                                                              
*-       Allow any "pending attention" to be set now.                           
*-                                                                              
         COMMENT                   Allow attns, set attn if                     
         VCALL ALLOWATN              attn is "pending"                          
*-                                                                              
*-       If ATTN exit disabled, throw it out !!                                 
*-                                                                              
         IF    (JCBFATTN,AND,CPFDDATN),BEGIN  If attns disabled,                
         VCALL TRASHATN            and if attn, junk attn                       
         END                                                                    
         SPACE 2                                                                
*-                                                                              
*-       If TRY,  1.)   Set TRY error/attn variables,                           
*-            2.)   Turn off Error/Attn flags,  (for no exec break)             
*-                                                                              
         IF    (CPFTRY,OR,CPFXXTRY),BEGIN                                       
         ACALL SETTRY              Set TRY variables                            
         CLEAR JCBFATTN            Clear attn/error flags                       
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         CLEAR CPFTRY              Clear TRY flags                              
         CLEAR CPFXXTRY                                                         
         END                                                                    
*-                                                                              
*-       If ATTN/ERROR, set users error variables                               
*-                                                                              
         COMMENT                   If attn, set users attn vars                 
         IF    JCBFATTN,BEGIN                                                   
         VCALL SETATTN                                                          
         COMMENT                   Attn overrides any error                     
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         END                                                                    
*                                                                               
         COMMENT                   If error, set users error vars               
         IF    (CPFERROR,OR,CPFABORT),BEGIN                                     
         VCALL SETERROR                                                         
         END                                                                    
*-                                                                              
*-       If command was passed to WYLBUR from ORVYL,  return control            
*-         to ORVYL.                                                            
*-                                                                              
*                                                                               
* do not move.  put after try processing but before exec abort exit.            
         IF    CPFVCMD,BEGIN       If command passed from ORVYL,                
         VCALL ORVRETN             Return control to ORVYL                      
         COMMENT ,                 (routine does not return)                    
         END                                                                    
*-                                                                              
*-       Abort EXEC if ERROR/ATTN                                               
*-                                                                              
         COMMENT                   Check for EXEC abort                         
         IF    CPFEXEC,BEGIN                                                    
         IF    (CPFERROR,OR,CPFABORT,OR,JCBFATTN),BEGIN                         
         VCALL EXECABRT            Abort EXEC                                   
         END                                                                    
         END                                                                    
*-                                                                              
*-       Check to be sure we are still in sync with ORVYL                       
*-                                                                              
         COMMENT                   If ORVYL in charge, does not                 
         VCALL ORVCHK                 return                                    
*-                                                                              
*-       Retry Command, if appropriate                                          
*-                                                                              
         IF    (CPFERROR,AND,CPGFLG3.CPFCMDFX),BEGIN  Retry...                  
         IF    ^JCBFATTN,'SET CPFRETRY'                                         
         END                                                                    
*                                                                               
         IF    CPFRETRY,BEGIN      Command retry...                             
         TWRITE                                                                 
         CLEAR R0,R1                                                            
         ACALL RETRY               Do command retry (no return)                 
         END                                                                    
*                                                                               
         CLEAR CPFCASE             Reset any case override                      
         AIF   (&$PCODE).SKIPBR                                                 
         IF    ('LT RAR,CPCMDRTN',NZ),'CLEAR CPCMDRTN; BR RAR'                  
.SKIPBR  ANOP  ,                                                                
*                                                                               
         PEND                                                                   
         DROP  R5                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'Initialize for Command Execution.'                              
*box                                                                            
*                                                                               
*  INITCMD -- Routine to initialize local command work areas.                   
*    This includes re-initializing the stack and heap.                          
*                                                                               
INITCMD  NULXPROC                                                               
*                                                                               
         COMMENT                   POINT TO START OF STACK                      
         COMMENT                   THEN PUSH REGISTERS                          
         L     R8,CVSTACK                                                       
         LA    R8,@R8+8                                                         
         LR    R13,R8                                                           
         ST    R14,0(R13)          PUT R14, R0-R8 ON STACK                      
         STM   R0,R8,4(R13)                                                     
         LA    R13,40(R13)                                                      
         BASE  BR                                                               
*                                                                               
*        XWTO  'INITCMD entered'   *** slp debug ***                            
         CLEAR CPZERO                                                           
         MVC   CPLCOL,=H'&LINESZ'  Default ending column                        
         IF    ^CPSFUPL,'SET CPFUPPER'                                          
         MVC   CPSTLNO,=F'-1'      No last stow lineno                          
*                                                                               
         LTH   R0,CPSOUMX          Use maximum output length                    
         IF    Z,'LH R0,CPSEGBSZ'  Buffer maximum                               
         CEIL  R0,3600             *** TEMP ***                                 
         IF    (CPSIDLE,NZ),'CEIL R0,500'  *** TEMP ***                         
         ST    R0,CPSEGMAXF        Set maximum seg write length                 
*                                                                               
         L     R2,CVSTACK          MARK START OF STACK                          
         MVC   @R2(8),=CL8'CVSTACK:'                                            
*                                                                               
         CLEAR CPHEAPCR            Initialize heap (see JUNKHEAP)               
         DROP  R15                                                              
*                                                                               
*-       Initialize SCANCB.                                                     
*-                                                                              
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*-                                                                              
*-       Initialize SEGCB.                                                      
*-                                                                              
         SEGINIT L:CPSEGBP,(R0),CPSEG,RTN=V(TERMSEG) Init seg buf               
*                                                                               
         COMMENT                   We do not allow TRY for                      
         COMMENT                   multiple command commands                    
         CLEAR CPFTRY              (eg. VIEW, READ..)                           
*                                                                               
         LR    R13,R8              GET R14, R0-R8 FROM STACK                    
         LM    R0,R8,4(R13)                                                     
         L     R14,0(R13)                                                       
         CLEAR R15                                                              
         NULPEND                                                                
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INITSTK -- Routine to initialize the CP stack.                               
*                                                                               
INITSTK  NULXPROC                                                               
*                                                                               
         COMMENT                   POINT TO START OF STACK                      
         COMMENT                   THEN PUSH REGISTERS                          
         L     R8,CVSTACK                                                       
         LA    R8,@R8+8                                                         
         LR    R13,R8                                                           
         ST    R14,0(R13)          PUT R14, R0-R8 ON STACK                      
         STM   R0,R8,4(R13)                                                     
         LA    R13,40(R13)                                                      
         BASE  BR                                                               
*                                                                               
         L     R2,CVSTACK          MARK START OF STACK                          
         MVC   @R2(8),=CL8'CVSTACK:'                                            
*                                                                               
         LR    R13,R8              GET R14, R0-R8 FROM STACK                    
         LM    R0,R8,4(R13)                                                     
         L     R14,0(R13)                                                       
         CLEAR R15                                                              
         NULPEND                                                                
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GETHEAP -- Routine to allocate user memory in the heap.  Heap                
*    space is usually not released until the end of the command.                
*                                                                               
*    On entry:                                                                  
*      R0 - length of memory to obtain                                          
*                                                                               
*    On exit:                                                                   
*      R1,R0 - address, length of memory obtained                               
*                                                                               
GETHEAP  XPROC                                                                  
         FAIL  (R0,NP),HEAPERR,'GETHEAP parameter error.'                       
*                                                                               
         AH    R0,=H'7'            Round to...                                  
         N     R0,=F'-8'           ...nearest doubleword                        
*                                                                               
         LT    R1,CPHEAPCR                                                      
         IF    Z,'L R1,CVHEAP'                                                  
         LR    R2,R1                                                            
         AR    R2,R0               New heap ptr                                 
         IF    (R2,GT,CVHEAPE),'ACALL STKOVER'  Heap overflow                   
         ST    R2,CPHEAPCR         Current ptr                                  
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MARKHEAP -- Routine to return the current heap pointer.                      
*                                                                               
*    On exit:                                                                   
*      R15 - current heap ptr (can be zero)                                     
*                                                                               
MARKHEAP XPROC                                                                  
         L     R15,CPHEAPCR        Current heap ptr                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RELHEAP -- Routine to release heap space.                                    
*                                                                               
*    On entry:                                                                  
*      R15 - heap ptr obtained from a previous MARKHEAP                         
*                                                                               
RELHEAP  XPROC                                                                  
         ST    R15,CPHEAPCR        Save new heap ptr                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  JUNKHEAP -- Rotine to free all the user's heap memory.                       
*                                                                               
JUNKHEAP PROC                                                                   
         CLEAR CPHEAPCR            Reset heap ptr                               
         PEND                                                                   
         TITLE 'SIB Update Routine'                                             
*box                                                                            
*                                                                               
*  UPDCPSIB -- Routine to update SIB in CP.                                     
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - SIB updated                                                          
*      4 - SIB not updated, make update again                                   
*                                                                               
UPDCPSIB XPROC                                                                  
         CLEAR R15                                                              
         INCR  R15,CPSUC                                                        
         TSEG  CPSOPT                                                           
         TCNTL CTLSETI             Set SIB options                              
         BM    CVMILERR                                                         
         IF    POS,'MVC CPS,@R1'   Caller must re-update                        
         PEND                                                                   
*                                                                               
         TITLE 'MILTEN Interface Error Message Routine'                         
*box                                                                            
*                                                                               
*  MILERR -- Routine to write "MILTEN INTERFACE ERROR" message.                 
*    (Note: See CVMILERR brancher routine.)                                     
*                                                                               
*    On entry:                                                                  
*      R15 - code (to be formatted in message, if non-zero)                     
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - ok                                                                   
*      nz- attn on twrite                                                       
*                                                                               
MILERR   XPROC                                                                  
         LR    R2,R15                                                           
         TSEG  'MILTEN interface error.  Command aborted'                       
         IF    (R2,NZ),BEGIN                                                    
         TSEG  ' (RC='                                                          
         BTD   CPDOUB,0,(R2)                                                    
         TSEG  (R1),(R0)                                                        
         TSEG  ')'                                                              
         END                                                                    
         MVC   CPERRID,=CL8'MILERR'                                             
*                                                                               
         TWRITE                                                                 
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  STKCHK -- Routine to check that we haven't overflowed the                    
*    stack.  Note that usually the stack check in the dispatcher                
*    catches stack overflows.  Routines that are sensitive to                   
*    stack overflows will call this routine to make sure that                   
*    a stack overflow isn't about to happen.                                    
*                                                                               
STKCHK   XPROC                                                                  
         LA    R2,@R13+800         Generous fudge factor                        
         IF    (R2,GE,CVSTACKE),BEGIN                                           
         ACALL INITSTK                                                          
         TCLEAR                                                                 
         ERROR 'Stack overflow.  Command aborted.',,STKOVFL                     
         END                                                                    
         PEND                                                                   
         SPACE 2                                                                
***                                                                             
***  Stack overflow recovery routine.                                           
***                                                                             
STKOVER  NULXPROC                                                               
         LR    R15,BR              Save previous base reg for trace             
         BASE                                                                   
         XTRACE STKOVER            For the record                               
         CLEAR CPCMDRTN            Nothing fancy                                
         ACALL INITSTK             Initialize stack                             
         TCLEAR                                                                 
         ERROR 'Stack overflow.  Command aborted.',,STKOVFL                     
         NULPEND                                                                
         DROP  BR                                                               
*-                                                                              
*-       out of pages                                                           
*-                                                                              
NOMORPG  XPROC                                                                  
         TSEG  CVWYLSSN,,T                                                      
         ERROR ' out of pages.  Command aborted.',,OUTPGS                       
         PEND                                                                   
*-                                                                              
*-       No action taken.                                                       
*-                                                                              
NOACTION XPROC                                                                  
         IF    ^CPFTYPED,CVABORT                                                
         IF    (CPERRID,Z),'MVC CPERRID,=CL8"NOACTION"'                         
         ABORT 'No action taken.'                                               
         PEND                                                                   
*-                                                                              
*-       Invalid option.                                                        
*-                                                                              
INVALID  XPROC                                                                  
         VCALL ORVCPASS            Return cmd if syntax error                   
         LM    R0,R1,CPSAVE        Pick up ptrs                                 
         LTR   R0,R0               Pos count?                                   
         BZ    CVABSENT            Missing                                      
         BNM   NOWSEG              Yes                                          
         LCR   R0,R0               Complement count                             
NOWSEG   CEIL  R0,15               Use no more than 15 chars                    
         TSEG  (R1),(R0)                                                        
         IF    (CPERRID,Z),'MVC CPERRID,=CL8"INVALID"'                          
         ERROR ': invalid.'                                                     
         PEND                                                                   
*-                                                                              
*-       Missing item routines.                                                 
*-                                                                              
ABSCENT  XPROC                                                                  
         VCALL ORVCPASS            Send cmd back to ORVYL if wanted             
         IF    (CPERRID,Z),'MVC CPERRID,=CL8"OPMISSNG"'                         
         ERROR 'Missing operand.'                                               
         PEND                                                                   
*                                                                               
AMMISS   XPROC                                                                  
         ERROR ' missing.'                                                      
         PEND                                                                   
*                                                                               
         TITLE 'Command Retry Routine'                                          
*box                                                                            
*                                                                               
*  RETRY -- Process @ command retry prefix.                                     
*                                                                               
RETRY    PROC                                                                   
         CLEAR CPFRETRY            Avoid recursion                              
         ACALL GETHCMD             Match a previous command                     
         IF    NZ,BEGIN            Sorry...                                     
         CLEAR CPFERROR+CPFABORT   (Reset; "ABORT" below sets again)            
         SETMSG 'No previous command matches.'                                  
         IF    (CPHCMDCR,Z),'SETMSG "No previous command."'                     
         ABORT (R1),(R0)                                                        
         END                                                                    
*                                                                               
         STH   R0,CPCMDLEN         Set command length                           
         LR    R15,R0                                                           
         MOVE  R15,CPCMD,@R1       Move in command text                         
         ACALL CMDRETRY            Go do command retry                          
         PEND                                                                   
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CMDRETRY -- Routine to allow the user to modify the current                  
*    command in CPCMD so that it can be re-executed.                            
*                                                                               
*    In line mode MODIFY/EDIT is used, and in page mode VIEW is                 
*    used.                                                                      
*                                                                               
CMDRETRY XPROC                                                                  
         TWRITE                                                                 
         ACALL INITCMD             Re-initialize                                
*                                                                               
         L     R6,CVCURJCB                                                      
         WITH  (JCB,R6),'CLEAR JCBFATTN'  Reset attn                            
*                                                                               
         IF    CPVFPAGE,BEGIN      Do page mode retry...                        
         SET   CPVFRTRY            Do a command retry                           
         VCALL PAGEMOD             Scram                                        
         BNZ   CVABORT             Prevent another retry                        
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         SET   CPFNUMXP+CPFLISXP+CPFCMDMD  Set modify flags                     
         SET   CPLFLG5.CPFNLST*CPFNONUM*CPFSING  Nolist,nonum,single            
*                                                                               
         SET   CPFUFMT             Display unformatted line                     
         SETMSG CPCMD,LH:CPCMDLEN   Command                                     
         VCALL LINEMOD             Go do modify for line                        
         BNZ   CVABORT             Prevent another retry                        
         CLEAR CPFUFMT             Reset flag                                   
         ACALL EDTCOM              Go do command retry                          
*                                                                               
         PEND                                                                   
         EJECT                                                                  
GBLWA    RECORD BEGIN                                                           
GBLREG01 DS    2F                  Save area for R0,R1                          
GBLSG    SEGCB                                                                  
GBLBUF   DS    XL(L'CPCMD)         Working seg buffer                           
GBLRES   DS    XL(L'CPCMD)         Put variable result here                     
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  GBLCMD -- Routine to execute the command in the global variable              
*    passed to this routine.  In addition, the remaining SCAN                   
*    text is appended to the command.                                           
*                                                                               
*    Note:  This routine will ONLY return to the caller if the                  
*           variable is undefined.                                              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - variable loc, len                                                
*                                                                               
*    On exit:                                                                   
*      Returns to caller only if the variable isn't defined                     
*                                                                               
GBLCMD   XPROC GBLWA                                                            
         STM   R0,R1,GBLREG01      Save param regs                              
*                                                                               
         SEGINIT GBLBUF,,GBLSG                                                  
         TPUSH                                                                  
*                                                                               
         LM    R0,R1,GBLREG01      Variable len, len                            
         LA    R2,GBLRES                                                        
         LA    R3,L'GBLRES                                                      
         VCALL EVALSTR             Get contents of var in (R1),(R0)             
         IF    NP,BEGIN            Variable is defined...                       
         IF    (R3,NP),EXIT        Null string is the same as undef             
*-                                                                              
*-       Build the command text, appending remaining scan text.                 
*-                                                                              
         IF    CPFTRY,'SEG "TRY "'  Preserve TRY prefix                         
         SEGT  (R2),(R3)           Command text (value of variable)             
         SCINFO                                                                 
         IF    (R0,POS),BEGIN      Add in any remaining text...                 
         SEG   (R1),(R0)                                                        
         END                                                                    
*                                                                               
         TPOP  JUNK                                                             
*                                                                               
         L     R15,GBLSGLENF                                                    
         STH   R15,CPCMDLEN        Save command length                          
         MOVE  R15,CPCMD,GBLBUF    Move in command text                         
         SETMSG CPCMD,LH:CPCMDLEN   Command text                                
         ACALL EDTCOMNH            Go execute command                           
         BOMB  ,                   (No return)                                  
         END                                                                    
*                                                                               
         TPOP  JUNK                                                             
         LA    R15,4               Variable not defined                         
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'New Command Setup and Scan'                                     
*box                                                                            
*                                                                               
*  On entry:                                                                    
*    R1,R0 - command location length                                            
*                                                                               
*  All commands must be at CPCMD at start of command scan.  The                 
*  Length of the command is in CPCMDLEN.  If CPFTYPED is on,                    
*  then the command has already been typed or logged at the                     
*  terminal.  This flag affects whether or not the command is                   
*  saved in the history list and also the error logging.                        
*                                                                               
EDTCOMNH XPROC                                                                  
         LR    R5,R1               Save command ptrs                            
         LR    R4,R0                                                            
         LA    R3,1                Flag: don't save in hist list                
         ACALL EDTBASE                                                          
         PEND                                                                   
*                                                                               
EDTCOMMV XPROC                                                                  
         LR    R2,R0                                                            
         TDEX  R2,'MVC CPCMD(0),@R1'  Move in command                           
         LA    R1,CPCMD            Point to command                             
         LR    R5,R1               Save command ptrs                            
         LR    R4,R0                                                            
         CLEAR R3                  Flag: ok to save in hist list                
         ACALL EDTBASE                                                          
         PEND                                                                   
*                                                                               
EDTCOM   XPROC                                                                  
         LR    R5,R1               Save command ptrs                            
         LR    R4,R0                                                            
         CLEAR R3                  Flag: ok to save in hist list                
         ACALL EDTBASE                                                          
         PEND                                                                   
*                                                                               
EDTBASE  PROC                                                                   
*                                                                               
         PFREE PAR                 Free if necessary                            
         PFREE PBR                 Free if necessary                            
         VCALL PJNKTMP             Junk any temporary pages                     
*                                                                               
*        XWTO  'EDTBASE entered' *** slp debug ***                              
         INCR  R15,CPNCOM          Count total no. of commands                  
         INCR  R15,CVNCOM          Ditto for grand total                        
         IF    CPFEXEC,BEGIN       Exec mode...                                 
         INCR  R15,CPNCOME         Count commands in exec mode                  
         INCR  R15,CVNCOME         Ditto for grand total                        
         END                                                                    
*                                                                               
         ACALL INITCMD             Re-initialize                                
         STH   R4,CPCMDLEN         Save command count                           
*        XWTO  'EDTBASE after INITCMD' *** slp debug ***                        
*                                                                               
         IF    CPFTYPED,BEGIN      Typed command...                             
         IF    (R3,NZ),EXIT        Caller said don't save cmd                   
         SETMSG (R5),(R4)           Command text                                
         ACALL ADDHCMD             Save command text                            
         END                                                                    
*                                                                               
         COMMENT                   SET EXEC TYPE FLAGS                          
         CLEAR CPFXOLD             DEFAULT IS OFF, (NOT IN EXEC)                
         CLEAR CPFXTEND                                                         
         IF    CPFEXEC,BEGIN                                                    
         IF    (CP#XSET,EQ,=A(CP#XOLD)),BEGIN                                   
         SET   CPFXOLD             OLD EXEC  (EXEC'D)                           
         END                                                                    
         ELSE  BEGIN                                                            
         SET   CPFXTEND            EXTENDED EXEC  (XCALLED)                     
         END                                                                    
         END                                                                    
*                                                                               
         MVC   CPOERRID,CPERRID    Save prev error id                           
         CLEAR CPERRID             Init current error id                        
*                                                                               
*                                                                               
         COMMENT                   If ON ATTN exit is disabled, or if           
         COMMENT                   ON ATTN NOINTERRUPT, do not allow            
         COMMENT                   attentions to stop commands.                 
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),BEGIN                                                  
         IF    ((CPFDDATN,OR,(CPFONATN,AND,^CPFINATN)),AND,            X        
               CPFEXEC),BEGIN                                                   
         VCALL HOLDATN             Don't allow ATTN's to stop cmds              
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL ALLOWATN                                                         
         END                                                                    
         END                                                                    
*-                                                                              
*-       Pre-process the command.                                               
*-                                                                              
         SETMSG (R5),(R4)           Command                                     
         IF    (CPESCAPE,NZ),BEGIN  No esc char, no pre-processing              
         IF    ^CPGFLG4.CPFNPREP,BEGIN  Preprocess this cmd...                  
         VCALL PREPROC  ,          Preprocess the command                       
         STH   R0,CPCMDLEN         Update the length                            
         END                                                                    
         END                                                                    
         CLEAR CPGFLG4.CPFNPREP    Reset flag                                   
*-                                                                              
*-       Check for %-style network command.                                     
*-         We must do this before the scanner is ever called on                 
*-         the command line to preserve the upper and lower case                
*-         of the first token (damn scanner).                                   
*-                                                                              
         IF    ((CPCMD,EQ,' '),OR,(CPCMD,EQ,'%')),BEGIN  Possibly...            
         SETMSG CPCMD,LH:CPCMDLEN   Command text                                
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    (R0,NP),EXIT        Nothing, forget it                           
         IF    (@R1,EQ,'%'),'ACALL NETCMD'  Do % command                        
         END                                                                    
*-                                                                              
*-       Set up new scancb and do initial scan.                                 
*-                                                                              
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*-                                                                              
*-       Scan for assignment statements.                                        
*-                                                                              
         COMMENT                   Scan for assignment statments                
         SCINIT CPCMD,LH:CPCMDLEN                                               
         VCALL XCMDSCAN            Pre-scan for exec cmds (eg. x=1 )            
         COMMENT                   If assignment, does not return               
*                                                                               
         COMMENT ,                 Don't do defines if extended                 
         COMMENT ,                   exec,,,                                    
         IF    (CPFNDEF,OR,CPFXTEND),BEGIN                                      
         END   ,                                                                
         ELSE  BEGIN               Otherwise, we do them                        
         ACALL DEFCMD                                                           
         IF    Z,BEGIN             Command substituted...                       
         SCINIT CPCMD,LH:CPCMDLEN  (allow x=1 to be defined command)            
         VCALL XCMDSCAN            Pre-scan for exec cmds (ie. x=1 )            
         COMMENT                   If assignment, does not return)              
         END                                                                    
         ELSE  'SCINIT CPCMD,LH:CPCMDLEN'                                       
         END                                                                    
         CLEAR CPFNDEF             One shot no defines flag                     
*-                                                                              
*-       The following is for dual-scanner compatibility only                   
*-                                                                              
         SCPUSH ,                                                               
         SCAN ,                                                                 
         SCINFO ,                                                               
         OSCINIT (R1),(R0)         OLD SCANNER COMPATIBILTY                     
         SCPOP ,                                                                
*                                                                               
*-                                                                              
*-       Scan the command                                                       
*-                                                                              
*        XWTO  'About to scan command' *** slp debug ***                        
*        XWTO   CPCMD,LH:CPCMDLEN  *** slp debug ***                            
         SCCLR                     Clear control block                          
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  PRT                 Scan                                         
         SCANCHK                   Scan error, exit                             
*        XWTO  'Command not recognized' *** slp debug ***                       
         B     CVNEXT              Nothing, go get next                         
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CMDFIN -- Routine to do the final processing of unrecognized                 
*    commands.                                                                  
*                                                                               
CFINWA   RECORD BEGIN                                                           
CFINPFX  DS    CL4                 Variable prefix (e.g., "SES.")               
CFINVAR  DS    CL16                Variable name                                
         END                                                                    
*-                                                                              
CMDFIN   XPROC CFINWA                                                           
         LA    R6,CFINWA                                                        
         USING CFINWA,R6                                                        
*-                                                                              
*-       Command beginning with @ means command retry.                          
*-                                                                              
         IF    (@R1,EQ,'@'),BEGIN  @-type retry prefix...                       
         LA    R2,@R1+1            Start of text we want                        
         SCBKUP                                                                 
         SCINFO                                                                 
         SR    R2,R1                                                            
         AR    R1,R2               R1 = text after "@"                          
         SR    R0,R2               R0 = remaining command length                
*                                                                               
         XPUSH R0,R1                                                            
         LT    R1,CPHCMDQT         Last command is @ command                    
         IF    NZ,BEGIN                                                         
         ACALL FREEHCMD            Delete last command                          
         DECR  R15,CPHCMDNO        Keep command numbering accurate              
         END                                                                    
         XPOP  R0,R1                                                            
*                                                                               
         ACALL RETRY               Go do command retry                          
         END                                                                    
*                                                                               
         COMMENT    BOUNCE USER HERE,  IF NOT LOGGED ON ,,,                     
         IF    (CPSACCT,Z),'VCALL NOTVALID'                                     
*-                                                                              
*-       Pass any command beginning with $ to JES.                              
*-                                                                              
         IF    (@R1,EQ,'$'),BEGIN  $-type unscanned jes command                 
         LR    R2,R1               Start of text we want                        
         SCBKUP                                                                 
         SCINFO                                                                 
         SR    R2,R1                                                            
         AR    R1,R2               R1 = text starting at "$"                    
         SR    R0,R2               R0 = remaining command length                
*                                                                               
         CLEAR R15                 Flag to indicate no scanning                 
         VCALL JESSND              Do jes command                               
         END                                                                    
*-                                                                              
*-       Check the following variables to see if the command is                 
*-         defined:                                                             
*-                                                                              
*-         1.  "SYSCMD_cmdname"                                                 
*-         2.  "X.SYSCMD_cmdname"                                               
*-         3.  "SES.SYSCMD_cmdname"                                             
*-         4.  "PUB.SYSCMD_cmdname"                                             
*-                                                                              
*-       If the command is an unrecognized SHOW command then the                
*-       prefix is SYSSHO_, similarly SET commands are SYSSET_ and              
*-       CLEAR commands are SYSCLR_.                                            
*-                                                                              
         MVC   CFINPFX,CVBLANKS                                                 
         MVC   CFINVAR,=CL(L'CFINVAR)'SYSCMD_'                                  
*                                                                               
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN  CDEFPRT             Pick out command verb                        
         SCANCHK                                                                
         CLEAR R15                                                              
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R1                                                             
*                                                                               
         IF    (R0,POS),BEGIN      Check for user definitions...                
         IF    (R0,GT,9),EXIT      Too long, forget it                          
*                                                                               
         LR    R15,R0                                                           
         CEIL  R15,9                                                            
         MOVE  R15,CFINVAR+7,@R1   Build variable name                          
*-                                                                              
*-    If the variable is defined, then get it's contents and execute            
*-         that as the command.                                                 
*-                                                                              
         COMMENT                   Check for "SYSCMD_cmdname"                   
         SETMSG CFINPFX,L'CFINPFX+L'CFINVAR                                     
         ACALL GBLCMD              Check for "SYSCMD_cmdname"                   
         COMMENT                   Does not return if cmd defined               
         COMMENT                                                                
         COMMENT                   Check for "X.SYSCMD_cmdname"                 
         MVC   CFINPFX,=C'  X.'                                                 
         SETMSG CFINPFX,L'CFINPFX+L'CFINVAR                                     
         ACALL GBLCMD              Check for "X.SYSCMD_cmdname"                 
         COMMENT                   Does not return if cmd defined               
         COMMENT                                                                
         COMMENT                   Check for "SES.SYSCMD_cmdname"               
         MVC   CFINPFX,=C'SES.'    Session variable                             
         SETMSG CFINPFX,L'CFINPFX+L'CFINVAR                                     
         ACALL GBLCMD              Check for "SES.SYSCMD_cmdname"               
         COMMENT                   Does not return if cmd defined               
         COMMENT                                                                
         COMMENT                   Check for "PUB.SYSCMD_cmdname"               
         MVC   CFINPFX,=C'PUB.'    Public variable                              
         SETMSG CFINPFX,L'CFINPFX+L'CFINVAR                                     
         ACALL GBLCMD              Check for "PUB.SYSCMD_cmdname"               
         COMMENT                   Does not return if cmd defined               
         END                                                                    
*                                                                               
*  OLD SUGGEST/ADMIN JUNK                                                       
*                                                                               
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN  ,                   Command verb ptr                             
         IF    (R15,Z),BEGIN                                                    
         SCUPTOK R1                                                             
         IF    ((R0,EQ,5),AND,(@R1,EQ,'ADMIN')),'ACALL MACOLD'                  
         IF    ((R0,EQ,3),AND,(@R1,EQ,'SUG')),'SETMSG "SUGGEST"'                
         IF    ((R0,EQ,7),AND,(@R1,EQ,'SUGGEST')),'ACALL MACOLD'                
         END                                                                    
*                                                                               
*  WE GIVE UP, PASS COMMAND TO ORVYL                                            
*                                                                               
         COMMENT                   WE UP CASE CMD FOR ORVYL (WAT5)              
         COMMENT                   YUCKK CITY                                   
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN  ,                   Command verb ptr                             
         SCUPCASE (R1),(R0)                                                     
         COMMENT                                                                
         VCALL ORVCMD              Send command to ORVYL                        
         ACALL CMDUNREC            Complain                                     
         PEND                                                                   
*                                                                               
*                                                                               
CDEFPRT  SCKW  SET,CDEFSET,A                                                    
         SCKW  SHOW,CDEFSHOW,A                                                  
         SCKW  DUMP,CDEFDUMP,A                                                  
         SCKW  CLEAR,CDEFCLR,A                                                  
         SCKW  CLR,CDEFCLR,A                                                    
         SCKW  ,CDEFSTOP                                                        
*                                                                               
CDEFSET  PROC                                                                   
         MVC   CFINVAR(6),=C'SYSSET'                                            
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CDEFSHOW PROC                                                                   
         MVC   CFINVAR(6),=C'SYSSHO'                                            
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CDEFDUMP PROC                                                                   
         MVC   CFINVAR(6),=C'SYSDMP'                                            
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CDEFCLR  PROC                                                                   
         MVC   CFINVAR(6),=C'SYSCLR'                                            
         LA    R15,4                                                            
         PEND                                                                   
CDEFSTOP PROC                                                                   
         SCBKUP                                                                 
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETCMD -- Routine to execute a %-style network command.                      
*                                                                               
*    Control is only returned to the caller if no network command               
*    processor is defined.                                                      
*                                                                               
NETCMD   PROC                                                                   
*-                                                                              
*-       Pass any command beginning with % to the network handler.              
*-                                                                              
         IF    (@R1,EQ,'%'),BEGIN  %-type network command                       
         LA    R1,@R1+1            Skip past "%"                                
         DECR  R0                  Keep length accurate                         
*                                                                               
         SCINIT (R1),(R0)          Text beyond the % (for GBLCMD)               
*                                                                               
         SETMSG 'PUB.SYSNET'                                                    
         ACALL GBLCMD              Check for netcmd exec                        
*  (Control only returns if the command wasn't defined.)                        
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CMDUNREC -- Routine to write out unrecognized command message.               
*                                                                               
CMDUNREC XPROC                                                                  
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN  SOPPRT                                                           
         SCANCHK                                                                
*                                                                               
         IF    (R0,NZ),'TSEG (R1),(R0); TSEG ": "'                              
         TSEG  'unrecognized '                                                  
         SETMSG 'command.'                                                      
         IF    (R3,NZ),'TSEG (R3),8,TB; SETMSG "option."'                       
         ERROR (R1),(R0)                                                        
         PEND                                                                   
*                                                                               
         SPACE 2                                                                
SOPPRT   SCKW  SET,CFINSET,(A,P)                                                
         SCKW  SHOW,CFINSHOW,(A,P)                                              
         SCKW  CLEAR,CFINCLR,(A,P)                                              
         SCKW  CLR,CFINCLR,(A,P)                                                
         SCKW  ,CFINCMD                                                         
*                                                                               
CFINCMD  PROC                                                                   
         CLEAR R3                                                               
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CFINSET  PROC                                                                   
         LA    R3,=CL8'SET'                                                     
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CFINSHOW PROC                                                                   
         LA    R3,=CL8'SHOW'                                                    
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CFINCLR  PROC                                                                   
         LA    R3,=CL8'CLEAR'                                                   
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  UTIL -- Routine to execute a public exec file.                               
*                                                                               
*    On entry:                                                                  
*      R15 - Exec file member name (8 bytes)                                    
*      SCAN ptrs - Exec file parm loc, len                                      
*                                                                               
UTILWA   RECORD BEGIN                                                           
UTILNAME DS    CL8                                                              
         END                                                                    
*                                                                               
UTIL     XPROC UTILWA                                                           
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing Public EXEC file name.',,MISSPUB                        
         END                                                                    
*                                                                               
         SCUPCASE UTILNAME,L'UTILNAME                                           
         SCINFO                                                                 
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         VCALL SETPARM             Set exec parm                                
         MVC   CPCMD(L'PUBCMD),PUBCMD  Set exec command                         
         MVC   CPCMD+PUBMEM(8),UTILNAME  Plant member name                      
         MVC   CPCMDLEN,=AL2(L'PUBCMD)                                          
         SETMSG CPCMD,LH:CPCMDLEN                                               
         CLEAR CPFTYPED            Not a typed command now                      
         ACALL EDTCOM              Go do new command                            
*                                                                               
         PEND                                                                   
*                                                                               
PUBCMD   DC    C'EXEC FROM LIB#12345678 PUBLIC CLEAR NOPARM'                    
PUBMEM   EQU   14                  Offset to "member" field in above            
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MACOLD -- Routine to execute an old exec file macro.                         
*                                                                               
*    On entry:                                                                  
*      R15 - Exec file member name (8 bytes)                                    
*      SCAN ptrs - Exec file parm loc, len                                      
*                                                                               
MACOWA   RECORD BEGIN                                                           
MACONAME DS    CL8                                                              
         END                                                                    
*                                                                               
MACOLD   PROC  MACOWA                                                           
         SCUPCASE MACONAME,L'MACONAME                                           
         SCINFO                                                                 
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         VCALL SETPARM             Set exec parm                                
         MVC   CPCMD(L'MACROCMD),MACROCMD  Set exec command                     
         MVC   CPCMD+MACROMEM(8),MACONAME  Plant member name                    
         MVC   CPCMDLEN,=AL2(L'MACROCMD)                                        
         SETMSG CPCMD,LH:CPCMDLEN                                               
         CLEAR CPFTYPED            Not a typed command now                      
         ACALL EDTCOM              Go do new command                            
         BOMB                                                                   
         PEND                                                                   
*                                                                               
MACROCMD DC    C'EXEC FROM COMMANDS#12345678 PUBLIC CLEAR NOPARM'               
MACROMEM EQU   19                  Offset to "member" field in above            
         EJECT                                                                  
MACWA    RECORD BEGIN                                                           
MACNAME  DS    CL8                 Member name                                  
MACCB    SEGCB                                                                  
*                                                                               
MACBUF   DS    CL(L'CPCMD)         Working command buffer                       
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  MACRO -- Routine to execute a macro.                                         
*                                                                               
*    On entry:                                                                  
*      R15 - Exec file member name (8 bytes)                                    
*      SCAN ptrs - Exec file parm loc, len                                      
*                                                                               
MACRO    XPROC MACWA                                                            
*                                                                               
         SCUPCASE MACNAME,L'MACNAME                                             
         SEGINIT MACBUF                                                         
         SEG   'XCALL COMMANDS#'                                                
         SEGTB MACNAME                                                          
         SEG   ' PUBLIC'                                                        
*                                                                               
         SCINFO                                                                 
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    POS,BEGIN                                                        
         XPUSH R0,R1                                                            
         SEG   '/'                                                              
         XPOP  R0,R1                                                            
         SEG   (R1),(R0)                                                        
         END                                                                    
*                                                                               
         L     R15,MACCBLENF                                                    
         STH   R15,CPCMDLEN                                                     
         MOVE  R15,CPCMD,MACBUF    Move in command                              
         SETMSG CPCMD,L:MACCBLENF                                               
         CLEAR CPFTYPED            Not a typed command now                      
         ACALL EDTCOM              Go do new command                            
         BOMB  ,                   (No return)                                  
         PEND                                                                   
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NULLCMD - noop                                                               
*                                                                               
*                                                                               
NULLCMD  PROC                                                                   
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  KERMIT -- KERMIT Command.                                                    
*                                                                               
KERMIT   XPROC                                                                  
         MVC   CPCMD(L'KERCMD),KERCMD  Set command text                         
         MVC   CPCMDLEN,=AL2(L'KERCMD)                                          
*                                                                               
         SETMSG CPCMD,LH:CPCMDLEN                                               
         ACALL EDTCOM              Go do new command                            
         BOMB  ,                   (No return)                                  
         PEND                                                                   
*                                                                               
KERCMD   DC    C'CALL KERMIT ACC GG.JDN CLEAR'  *** Temporary? ***              
*                                                                               
         QLTORG                                                                 
         AIF   (&$PCODE).SKIPOS                                                 
         TITLE 'OS Command'                                                     
*box                                                                            
*                                                                               
*  OS -- OS command (priv'd).                                                   
*                                                                               
OS       XPROC                                                                  
         IF    ^CPFXAUTH,BEGIN     Check if not authorized...                   
         IF    (CPSFSPR+CPSFOPR,Z),'VCALL NOTVALID'  Not priv'd                 
         END                                                                    
*                                                                               
         SCINFO                                                                 
         VCALL LRTRIM                                                           
         IF    (R0,NP),CVABSENT    Missing operand                              
         IF    (R0,GT,115),BEGIN   Too long...                                  
         S     R0,=F'115'                                                       
         LR    R2,R0                                                            
         TSEG  'OS command is '                                                 
         TNUM  (R2)                                                             
         ERROR ' characters too long.'                                          
         END                                                                    
*-                                                                              
*-       Log who is about to do the OS command.                                 
*-                                                                              
         XPUSH R0,R1                                                            
         SET   CVWTOFUPFX+CVWTOFWTL  WTO logging flags                          
         SEG   CPCMD,LH:CPCMDLEN,L:CVWTOSGP  OS command text                    
         SEGWR ,                   Log it                                       
         CLEAR CVWTOFUPFX+CVWTOFWTL  Reset                                      
         XPOP  R0,R1                                                            
*-                                                                              
*-       Do the OS command.                                                     
*-                                                                              
         XPUSH ,,250,PTR=R6        (SVC34 area & slop)                          
         LR    R15,R0                                                           
         AH    R0,=AL2(4+1)        Len includes mcs header & blank              
         STH   R0,@R6                                                           
         CLEAR (@R6+2,2)           Mcs flags                                    
         LA    R2,@R6+4(R15)       Last byte                                    
         MVI   @R2,C' '            Make it a blank                              
         MOVE  R15,@R6+4,@R1       Text                                         
         MODESET KEY=ZERO          ********                                     
         LR    R1,R6               Parm ptr                                     
         CLEAR R0                  Ucmid=0  (master console)                    
         SVC   34                  Send command to OS                           
         MODESET KEY=NZERO         ********                                     
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
.SKIPOS  ANOP  ,                                                                
         TITLE 'DEFINE Record Dsect'                                            
*box                                                                            
*                                                                               
*                                                                               
*  DEFINE RECORD DESECT                                                         
*                                                                               
*  NOTE: THE DEFINE RECORD IS SAVED AS A VARIABLE LENGTH RECORD.                
*  THE DEFINE RECORD REPLACEMENT TEXT AND APPEND TEXT ARE                       
*  CONCATENATED TOGETHER AND THE RECORD IS SHORTENED BY WRITTING                
*  OUT A VARIABLE LENGTH DEFINE RECORD.                                         
*                                                                               
*                                                                               
DEFRECD  RECORD BEGIN                                                           
DEFNAME  DS    CL16                DEFINE NAME                                  
DEFDATA  DS    XL(16+256+256)                                                   
         ORG   DEFDATA                                                          
DEFFLAGS FLAG  ,                                                                
         FLAG  DEFNOTRM                                                         
         DS    XL3                                                              
DEFRESV  DS    F                   NOT USED                                     
DEFTEXTL DS    F                   LENGTH OF REPLACEMENT TEXT                   
DEFAPPDL DS    F                   LENGTH OF APPEND TEXT                        
DEFTEXT  DS    2CL256              REPLACEMENT TEXT, APPEND TEXT                
         END                                                                    
         SPACE 4                                                                
*box                                                                            
*                                                                               
*                                                                               
*  ABBREVIATION RECORD                                                          
*                                                                               
*  NOTE: ABBREVIATION RECORD IS VARIABLE LENGTH, DEPENDING                      
*  ON HOW MANY ENTRIES THERE ARE. 32 ENTRIES IS MAX.                            
*                                                                               
*                                                                               
ABBRRECD RECORD BEGIN                                                           
ABBRNAME DS    CL16                ABBREVIATION NAME (3 CHARS)                  
ABBRDATA DS    XL512                                                            
         ORG   ABBRDATA                                                         
ABBRNTRY DS    32CL16              VARIABLE NUMBER OF ABBR ENTRIES              
         END                                                                    
         TITLE 'DEFINE Command/Routines'                                        
*box                                                                            
*                                                                               
*                                                                               
*  DEFINE - WYLBUR Define Command.                                              
*                                                                               
*                                                                               
*  DEFINE RECORD format is described by DEFRECD.                                
*                                                                               
*  ABBREVIATION RECORD format is described by ABBRRECD.                         
*                                                                               
*                                                                               
DEFWA    RECORD BEGIN                                                           
DEFFLAG  FLAG                                                                   
         FLAG  DEFFSET             'AS' SPECIFIED                               
         FLAG  DEFFABBR            ABBREVIATE                                   
         DS    0F                                                               
DEFTEXTA DS    A                   LOC, LEN OF COMMAND TEXT                     
DEFAPPDA DS    A                   LOC, LEN OF APPEND TEXT                      
DEFINFO  DS    XL(L'DEFRECD)       DEFINE RECORD: NAME, TEXT..                  
         END                                                                    
*                                                                               
*                                                                               
DEFINE   XPROC DEFWA                                                            
         LA    R6,DEFWA                                                         
         USING DEFWA,R6                                                         
         LA    R5,DEFINFO                                                       
         USING DEFRECD,R5                                                       
*                                                                               
         COMMENT                   INIT WORK AREA                               
         CLEAR DEFFLAGS                                                         
         CLEAR DEFRECD                                                          
         MVC   DEFNAME,CVBLANKS                                                 
         CLEAR DEFTEXTA                                                         
         CLEAR DEFAPPDA                                                         
*                                                                               
         COMMENT                   SCAN COMMAND                                 
         SCAN  ,                                                                
         SCANCHK ,                                                              
         IF    (@R1,EQ,''''),'VCALL NOTVALID'                                   
         IF    (@R1,EQ,'"'),'VCALL NOTVALID'                                    
         IF    (@R1,EQ,'('),'VCALL NOTVALID'                                    
         IF    (R0,GT,16),'VCALL NOTVALID'                                      
         SCUPCASE DEFNAME,L'DEFNAME                                             
*                                                                               
         IF    (DEFNAME,EQ,'SYSCMD '),BEGIN                                     
         ABORT 'SYSCMD command can not be re-defined.'                          
         END                                                                    
*                                                                               
         SCAN  DEFASPRT            SCAN DEFINE OPTIONS                          
         SCANCHK                                                                
*                                                                               
         COMMENT                   CHECK A COUPLE OF THINGS                     
*                                                                               
* Note: we no longer care if command is replaced or command is                  
*    system command, or if notrim of blanks, let the user beware!               
*                                                                               
         IF    ^DEFFSET,BEGIN                                                   
         VCALL ORVCPASS            GIVE SPIRES A CHANCE                         
         ERROR 'Missing "AS ''new command''".  Define not done.'                
         END                                                                    
         IF    (DEFFABBR,AND,(DEFNAME+3,EQ,' ')),BEGIN                          
         CLEAR DEFFABBR                                                         
         END                                                                    
*                                                                               
         COMMENT                   MOVE TEXT TO DEFINE RECORD                   
         L     R2,DEFTEXTA                                                      
         L     R3,DEFTEXTL                                                      
         MOVE  R3,DEFTEXT,@R2      MOVE REPLACE TEXT                            
         LA    R1,DEFTEXT                                                       
         A     R1,DEFTEXTL                                                      
         L     R2,DEFAPPDA                                                      
         L     R3,DEFAPPDL                                                      
         MOVE  R3,@R1,@R2          MOVE APPEND TEXT                             
*                                                                               
         COMMENT                   DEFINE COMMAND                               
         LA    R1,DEFRECD                                                       
         ACALL ADDDEF                                                           
*                                                                               
         COMMENT                   IF ABBREVIATE, DO IT TOO.                    
         IF    DEFFABBR,BEGIN                                                   
         LA    R1,DEFNAME                                                       
         ACALL ADDABBR                                                          
         END                                                                    
*                                                                               
         B     CVNEXT              ALL DONE !                                   
*                                                                               
         CLEAR R15                                                              
         PEND  ,          DUMMY,, WE EXIT ABOVE !                               
         SPACE 4                                                                
*-                                                                              
*-       DEFINE OPTIONS,,,                                                      
*-                                                                              
DEFASPRT SCKW  ,SDEFNOAS,NULL                                                   
         SCKW  AS,SDEFAS,P                                                      
         SCKW  APPEND,SDEFAPP,(A,P)                                             
         SCKW  ABBREVIATE,SDEFABBR,A                                            
         SCKW  REPLACE,SDEFREP,A                                                
         SCKW  SYSTEM,SDEFSYS,A                                                 
         SCKW  NOTRIM,SDEFNOT,A                                                 
         SCKW  ,SDEFERR                                                         
*                                                                               
SDEFNOAS PROC                                                                   
         VCALL ORVCPASS            GIVE SPIRES A CHANCE                         
         ERROR 'Missing "AS ''new command''".  Define not done.'                
         PEND                                                                   
*                                                                               
SDEFERR  PROC                                                                   
         VCALL ORVCPASS            GIVE SPIRES A CHANCE                         
         VCALL NOTVALID            DOES NOT RETURN                              
         PEND                                                                   
*                                                                               
SDEFAS   PROC                                                                   
         IF    (@R1,EQ,'('),'VCALL NOTVALID'                                    
         VCALL DEQUOTE             DEQUOTE, IF STRING                           
         IF    NEG,BEGIN           ADD TRAIL BLANK IF NOT QUOTED                
         LR    R3,R0                                                            
         AR    R3,R1                                                            
         MVI   0(R3),C' '                                                       
         A     R0,CVONE                                                         
         END                                                                    
         ST    R1,DEFTEXTA         SAVE STRING PTRS                             
         ST    R0,DEFTEXTL                                                      
         SET   DEFFSET                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SDEFAPP  PROC                                                                   
         IF    (@R1,EQ,'('),'VCALL NOTVALID'                                    
         VCALL DEQUOTE             DEQUOTE, IF STRING                           
         IF    NEG,BEGIN           ADD LEAD BLANK IF NOT QUOTED                 
         DECR  R1                                                               
         INCR  R0                                                               
         END                                                                    
         ST    R1,DEFAPPDA         SAVE STRING PTRS                             
         ST    R0,DEFAPPDL                                                      
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SDEFABBR PROC                                                                   
         SET   DEFFABBR            ABBREVIATION OK                              
         PEND                                                                   
*                                                                               
SDEFREP  PROC                                                                   
         PEND                                                                   
*                                                                               
SDEFSYS  PROC                                                                   
         PEND                                                                   
*                                                                               
SDEFNOT  PROC  ,                   NOTRIM IS NOOP                               
         SET   DEFNOTRM                                                         
         PEND                                                                   
         SPACE 4                                                                
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  UNDEFINE - UNDEFINE COMMAND.                                                 
*                                                                               
*                                                                               
UDEFWA   RECORD BEGIN                                                           
UDEFREC  DS    XL(L'DEFRECD)       DEFINE RECORD                                
         END                                                                    
*                                                                               
*                                                                               
UNDEFINE XPROC UDEFWA                                                           
         LA    R6,UDEFREC                                                       
         USING DEFRECD,R6                                                       
*                                                                               
         COMMENT                   INIT WORK AREA                               
         CLEAR DEFRECD                                                          
         MVC   DEFNAME,CVBLANKS                                                 
*                                                                               
         COMMENT                   SCAN FOR NAME TO UNDEFINE                    
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing user-command name.'                                     
         END                                                                    
         IF    (@R1,EQ,''''),'VCALL NOTVALID'                                   
         IF    (@R1,EQ,'"'),'VCALL NOTVALID'                                    
         IF    (@R1,EQ,'('),'VCALL NOTVALID'                                    
         IF    (R0,GT,16),'VCALL NOTVALID'                                      
         SCUPCASE DEFNAME,L'DEFNAME                                             
*                                                                               
         COMMENT                   CHECK IF DEFINED                             
         LA    R1,DEFRECD                                                       
         ACALL CHKDEF                                                           
         IF    NZ,BEGIN                                                         
         LA    R1,DEFRECD                                                       
         ACALL CHKABBR                                                          
         IF    NZ,BEGIN                                                         
         TSEG  DEFNAME,L'DEFNAME,TB                                             
         ERROR 'is not a DEFINED command.'                                      
         END                                                                    
         LA    R1,DEFRECD                                                       
         ACALL CHKDEF                                                           
         IF    NZ,BEGIN                                                         
         BOMB  ABBREV, BUT NO ENTRY                                             
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DEFINED, SO UNDEFINE IT                      
         LA    R1,DEFNAME                                                       
         ACALL DELDEF                                                           
         ACALL DELABBR                                                          
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,          DUMMY, WE EXIT TO CVNEXT ABOVE                        
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLRDEF - CLEAR ALL DEFINES                                                   
*                                                                               
*                                                                               
*                                                                               
CLRDEF   XPROC                                                                  
*                                                                               
         COMMENT                   DELETE DEFINE RECORD SET                     
         LA    R15,CPRG16                                                       
         LA    R0,CP#DEF                                                        
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   DELETE ABBREV RECORD SET                     
         LA    R15,CPRG16                                                       
         LA    R0,CP#ABBR                                                       
         VCALL XDELSET                                                          
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOW DEFINES                                                                 
*                                                                               
*                                                                               
*                                                                               
*                                                                               
SDFWA    RECORD BEGIN ,                                                         
SDFFIRST DS    XL16                FIRST DEF TO SHOW                            
SDFLAST  DS    XL16                LAST DEF TO SHOW                             
SDFRECD  DS    XL(L'DEFRECD)       CURRENT DEFINE RECORD                        
         END                                                                    
*                                                                               
*                                                                               
SHOWDEF  XPROC SDFWA                                                            
         LA    R6,SDFWA                                                         
         USING SDFWA,R6                                                         
         LA    R5,SDFRECD                                                       
         USING DEFRECD,R5                                                       
*                                                                               
         COMMENT                   INITIALIZE/SET DEFAULTS                      
         CLEAR SDFFIRST            ZERO FIRST NAME AS DEFAULT                   
         LA    R2,SDFLAST          X'FF' LAST NAME AS DEFAULT                   
         LA    R3,L'SDFLAST                                                     
         L     R15,=X'FF000000'                                                 
         MVCL  R2,R14                                                           
*                                                                               
         COMMENT                   SCAN COMMAND                                 
         SCAN  SDFPRT                                                           
         SCANCHK                                                                
*                                                                               
         COMMENT                   SET CORRECT DEFINE SET NUMBER                
         LA    R15,CPRG16                                                       
         LA    R0,CP#DEF                                                        
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOOP PRINTING DEFINES                        
         MVC   DEFNAME,SDFFIRST                                                 
         LA    R15,CPRG16                                                       
         LA    R0,L'DEFDATA                                                     
         LA    R1,DEFDATA                                                       
         LA    R2,DEFNAME                                                       
         VCALL XGETFRST            GET FIRST DEFINE IN SET                      
         WHILE (Z,AND,(DEFNAME,LE,SDFLAST)),BEGIN  LOOP THRU DEFS               
         LR    R2,R1               @R2 - DEFINE RECORD                          
         TSEG  DEFNAME,L'DEFNAME,TB                                             
         TSEG  '- '                                                             
         L     R0,DEFTEXTL                                                      
         LA    R1,DEFTEXT                                                       
         TSEG  (R1),(R0)           SEG DEFINE TEXT                              
         TSEG  '...'                                                            
         L     R0,DEFAPPDL                                                      
         L     R1,DEFTEXTL                                                      
         LA    R1,DEFTEXT(R1)                                                   
         TSEG  (R1),(R0)           SEG APPEND TEXT                              
         TCR                                                                    
         BNZ   CVNEXT              IF ATTENTION HIT, QUICK EXIT !!              
         LA    R15,CPRG16                                                       
         LA    R0,L'DEFDATA                                                     
         LA    R1,DEFDATA                                                       
         LA    R2,DEFNAME                                                       
         VCALL XGETNEXT                                                         
         END                                                                    
*                                                                               
         B     CVNEXT              OUT OF HERE BOZO !!                          
*                                                                               
         CLEAR R15                                                              
         PEND  ,          DUMMY, WE EXIT ABOVE                                  
         SPACE 3                                                                
*-                                                                              
*-       SHOW DEFS OPTIONS  (SCANNED)                                           
*-                                                                              
SDFPRT   SCKW  LIKE,SDFLIKE,(A,P)                                               
         SCKW  FROM,SDFFROM,(A,P)                                               
         SCKW  TO,SDFTO,P                                                       
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE 5                                                                
*-                                                                              
*-       LIKE                                                                   
*-                                                                              
SDFLIKE  PROC                                                                   
         SCUPCASE SDFFIRST,L'SDFFIRST                                           
         SCUPCASE SDFLAST,(R0)                                                  
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       FROM                                                                   
*-                                                                              
SDFFROM  PROC                                                                   
         SCUPCASE SDFFIRST,L'SDFFIRST                                           
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       TO                                                                     
*-                                                                              
SDFTO    PROC                                                                   
         SCUPCASE SDFLAST,(R0)                                                  
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DEFCMD - ROUTINE TO DO ANY DEFINE COMMAND SUBSTITUTION                       
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       CPCMD, CPCMDLEN - COMMAND BUFFER, COMMAND LENGTH                        
*                                                                               
*  ON EXIT:                                                                     
*       CPCMD, CPCMDLEN - UPDATED IF SUBSTITUTION DONE                          
*       R15 - CC=Z, COMMAND SUBSTITUTED                                         
*             CC=NZ, COMMAND NOT SUBSTITUTED                                    
*                                                                               
*                                                                               
*                                                                               
DCMDWA   RECORD BEGIN                                                           
DCMDREC  DS    XL(L'DEFRECD)       DEFINE RECORD AREA                           
DCMDAREA DS    XL(4*&LINESZ)       DEFINE CMD ASSEMBLE AREA                     
         END                                                                    
*-                                                                              
DEFCMD   PROC  DCMDWA                                                           
         LA    R6,DCMDREC                                                       
         USING DEFRECD,R6                                                       
*                                                                               
         COMMENT                   INITIALIZE WORK AREA                         
         MVC   DEFNAME,CVBLANKS                                                 
*                                                                               
         COMMENT                   SCAN COMMAND NAME                            
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN  ,                                                                
         IF    (R15,NZ),NODEFINE                                                
         IF    (R0,GT,16),NODEFINE                                              
         SCUPCASE DEFNAME,L'DEFNAME                                             
*                                                                               
         COMMENT                   CHECK IF DEFINED                             
         LA    R1,DEFRECD                                                       
         ACALL CHKDEF                                                           
         IF    NZ,BEGIN                                                         
         LA    R1,DEFNAME                                                       
         ACALL CHKABBR                                                          
         BNZ   NODEFINE                                                         
         LA    R1,DEFRECD                                                       
         ACALL CHKDEF                                                           
         IF    NZ,BEGIN                                                         
         BOMB  ABBREV, BUT NO ENTRY                                             
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DEFINED, DO SUBSTITUTION                     
         COMMENT                   R2,R3 - DEFINE SUB TEXT LOC, LEN             
         COMMENT                   R4,R5 - APPEND TEXT LOC, LEN                 
         SCINIT CPCMD,LH:CPCMDLEN  GET TEXT AFTER COMMAND LOC, LEN              
         SCAN  ,                                                                
         SCINFO ,                                                               
         IF    ^DEFNOTRM,BEGIN                                                  
         VCALL LRTRIM                                                           
         END                                                                    
         LR    R15,R0                                                           
         LR    R0,R1                                                            
         LR    R1,R15                                                           
         COMMENT                   R0,R1 - TEXT AFTER CMD LOC, LEN              
         LA    R14,DCMDAREA                                                     
         LR    R15,R3                                                           
         MVCL  R14,R2              MOVE DEFINE SUBSTITUTION TEXT                
         LR    R15,R1                                                           
         MVCL  R14,R0              MOVE TEXT AFTER COMMAND                      
         LR    R15,R5                                                           
         MVCL  R14,R4              MOVE APPEND TEXT                             
         LA    R15,DCMDAREA                                                     
         SR    R14,R15             R14 - NEW TEXT LEN                           
*                                                                               
         COMMENT                   CHECK NEW TEXT LEN, MOVE TO CPCMD            
         IF    (R14,GT,&LINESZ),BEGIN                                           
         ERROR 'DEFINE substitution exceeds 235 characters. '                   
         END                                                                    
         STH   R14,CPCMDLEN                                                     
         LA    R2,CPCMD                                                         
         LA    R3,L'CPCMD                                                       
         LA    R4,DCMDAREA                                                      
         LR    R5,R14                                                           
         O     R5,=X'40000000'                                                  
         MVCL  R2,R4               MOVE NEW CMD TEXT TO CPCMD                   
         CLEAR R15                                                              
         B     DEFCDONE                                                         
*                                                                               
NODEFINE LABEL                                                                  
         LA    R15,4                                                            
*                                                                               
DEFCDONE LABEL                                                                  
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ADDDEF - ADD DEFINE COMMAND TO DEFINE RECORD GROUP                           
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE RECORD                                                     
*       WE SQUISH COMMAND, APPEND TEXT BY SIMPLE CONCATENATION                  
*                                                                               
*  ON EXIT:                                                                     
*       R15 - ZERO                                                              
*       COMMAND ADDED TO DEFINE RECORD GROUP                                    
*                                                                               
*                                                                               
*                                                                               
ADDDEF   PROC                                                                   
         LR    R6,R1                                                            
         USING DEFRECD,R6                                                       
*                                                                               
         COMMENT                   CHECK TEXT LENGTHS                           
         IF    (DEFTEXTL,GT,&LINESZ),BEGIN                                      
         ERROR 'DEFINE replacement text too long. Define not done.'             
         END                                                                    
         IF    (DEFAPPDL,GT,&LINESZ),BEGIN                                      
         ERROR 'DEFINE append text too long. Define not done.'                  
         END                                                                    
*                                                                               
         COMMENT                   SAVE RECORD                                  
         LA    R15,CPRG16                                                       
         LA    R0,CP#DEF                                                        
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LA    R0,DEFTEXT          R0, CALC ACTUAL DEF RECD LEN                 
         A     R0,DEFTEXTL                                                      
         A     R0,DEFAPPDL                                                      
         LA    R1,DEFDATA                                                       
         SR    R0,R1                                                            
         LA    R1,DEFDATA                                                       
         LA    R2,DEFNAME                                                       
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT DEFFULL,'Session Data Record Group Overflow.'                    
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ADDABBR - ADD DEFINE ABBREVIATION                                            
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE NAME, LEFT JUSTIFIED IN 16 BYTE AREA                       
*            (NOTE: @R1 MUST BE FULL NAME, NOT ABBREVIATED)                     
*                                                                               
*                                                                               
*                                                                               
AABBRWA  RECORD BEGIN                                                           
AABBRREC DS    XL(L'ABBRRECD)                                                   
         END                                                                    
*-                                                                              
ADDABBR  PROC  AABBRWA                                                          
         LA    R6,AABBRREC                                                      
         USING ABBRRECD,R6                                                      
         LR    R3,R1               @R3 - FULL NAME TO ABBREVIATE                
*                                                                               
         COMMENT                   IF NAME LEN < 4, NO ABBREVIATION             
         IF    (@R3+3,EQ,' '),ADDADONE                                          
*                                                                               
         COMMENT                   INITIALIZE WORK AREA                         
         CLEAR ABBRRECD                                                         
         MVC   ABBRNAME,CVBLANKS                                                
         MVC   ABBRNAME(3),@R3                                                  
*                                                                               
         COMMENT                   READ IN ANY EXISTING ABBR RECORD             
         LA    R15,CPRG16                                                       
         LA    R0,CP#ABBR                                                       
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LA    R0,L'ABBRDATA                                                    
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XGET                                                             
*                                                                               
         COMMENT                   IF NO ABBREV RECORD, ADD ONE                 
         IF    NZ,BEGIN                                                         
         MVC   ABBRDATA(L'ABBRNTRY),@R3                                         
         LA    R15,CPRG16                                                       
         LA    R0,L'ABBRNTRY                                                    
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT DEFFULL,'Session Data Record Group Overflow.'                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   WE HAVE ABBREV RECORD, DOES IT               
         COMMENT                   CONTAIN THIS NAME ??                         
         ELSE  BEGIN                                                            
         AR    R1,R0               R1 - END OF RECORD                           
         LA    R4,ABBRDATA                                                      
         WHILE ((R4,LT,R1),AND,(' CLC 0(16,R4),0(R3)',NE)),BEGIN                
         LA    R4,L'ABBRNTRY(R4)                                                
         END                                                                    
         IF    (R4,GE,R1),BEGIN    NO, DOES NOT HAVE THIS NAME, ADD             
         IF    (R4,NE,R1),' BOMB '                                              
         MVC   0(L'ABBRNTRY,R4),0(R3)      MOVE IN NEW NAME                     
         LA    R4,L'ABBRNTRY(R4)                                                
         LA    R5,ABBRDATA                                                      
         SR    R4,R5                       LEN OF NEW RECORD                    
         LA    R15,CPRG16                                                       
         LR    R0,R4                                                            
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT DEFFULL,'Session Data Record Group Overflow.'                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
ADDADONE LABEL                                                                  
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKDEF - CHECK TO SEE IF COMMAND IS DEFINED.                                 
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE RECORD, WITH DEFINE NAME SET UP,                           
*             LEFT JUSTIFIED IN 16 BYTE AREA                                    
*                                                                               
*  ON EXIT:                                                                     
*       R2,R3 - SUBSTITUTION TEXT LOC, LEN,,, IF DEFINED                        
*       R4,R5 - APPEND TEXT LOC, LEN,,,  IF DEFINED                             
*       R15 - CC=Z, NAME IS DEFINED                                             
*             CC=NZ, NAME IS NOT DEFINED                                        
*                                                                               
*                                                                               
CHKDEF   PROC                                                                   
         LR    R6,R1                                                            
         USING DEFRECD,R6                                                       
*                                                                               
         LA    R15,CPRG16                                                       
         LA    R0,CP#DEF                                                        
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LA    R0,L'DEFDATA                                                     
         LA    R1,DEFDATA                                                       
         LA    R2,DEFNAME                                                       
         VCALL XGET                                                             
         IF    Z,BEGIN                                                          
         LA    R2,DEFTEXT                                                       
         L     R3,DEFTEXTL                                                      
         LA    R4,0(R2,R3)                                                      
         L     R5,DEFAPPDL                                                      
         END                                                                    
*                                                                               
         PRETURN (R0,R5)                                                        
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKABBR - CHECK IF ABBREVIATION IS DEFINED                                   
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE NAME, LEFT JUSTIFIED IN 16 BYTE AREA                       
*            (NOTE: NAME MAY BE ANY LEN ABBREVIATION)                           
*                                                                               
*  ON EXIT:                                                                     
*       @R1 - FULL NAME, LEFT JUSTIFIED,,,  IF ABBREVIATED                      
*       R15 - CC=Z, IF NAME IS ABBREVIATED                                      
*             CC=NZ, IF NAME IS NOT ABBREVIATED                                 
*                                                                               
*                                                                               
CABBRWA  RECORD BEGIN                                                           
CABBRREC DS    XL(L'ABBRRECD)                                                   
         END                                                                    
*-                                                                              
CHKABBR  PROC  CABBRWA                                                          
         LA    R6,CABBRREC                                                      
         USING ABBRRECD,R6                                                      
*                                                                               
         COMMENT                   @R4 - NAME TO CHECK                          
         LR    R4,R1               @R4 - NAME TO CHECK                          
*                                                                               
         COMMENT                   GET ABBREVIATED RECORD                       
         MVC   ABBRNAME,CVBLANKS                                                
         MVC   ABBRNAME(3),@R4                                                  
         LA    R15,CPRG16                                                       
         LA    R0,CP#ABBR                                                       
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LA    R0,L'ABBRDATA                                                    
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XGET                                                             
         BNZ   NOTABBR                                                          
*                                                                               
         COMMENT                   GOT ABBREVIATED RECORD, CHECK IT             
         COMMENT                   R1,R0 - RECORD LOC, LEN                      
         LA    R3,@R4+15           R3,R4 - NAME LEN-1, LOC                      
         WHILE (@R3,EQ,' '),BEGIN          GET NAME LEN-1                       
         DECR  R3                                                               
         END                                                                    
         SR    R3,R4                                                            
         AR    R0,R1               SEARCH THRU ABBREV LIST                      
         WHILE (R1,LT,R0),BEGIN                                                 
         EX    R3,' CLC @R1(0),@R4 '                                            
         BE    GOTABBR             FOUND ABBREVIATION ENTRY                     
         LA    R1,16(R1)                                                        
         END                                                                    
         B     NOTABBR             DID NOT FIND ABBREVIATION ENTRY              
*                                                                               
GOTABBR  LABEL                                                                  
         MVC   0(16,R4),0(R1)      MOVE FULL NAME                               
         CLEAR R15                                                              
         B     CHKADONE                                                         
*                                                                               
NOTABBR  LABEL                                                                  
         LA    R15,4                                                            
         B     CHKADONE                                                         
*                                                                               
CHKADONE LABEL                                                                  
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DELDEF - DELETE DEFINE ENTRY                                                 
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE NAME, LEFT JUSTIFIED IN 16 BYTE AREA                       
*            (NOTE: @R1 MUST BE FULL NAME, NOT ABBREVIATED)                     
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO,  WE DO NOT CHECK IF ENTRY EXISTS.                        
*                                                                               
*                                                                               
DELDEF   PROC                                                                   
         LA    R15,CPRG16                                                       
         LA    R0,CP#DEF                                                        
         VCALL XSETSET                                                          
*                                                                               
         LA    R15,CPRG16                                                       
         LR    R2,R1                                                            
         VCALL XDELETE                                                          
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DELABBR - DELETE DEFINE ABBREVIATION ENTRY                                   
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - DEFINE NAME, LEFT JUSTIFIED IN 16 BYTE AREA                       
*            (NOTE: @R1 MUST BE FULL NAME, NOT ABBREVIATED)                     
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO,  WE DO NOT CHECK IF ENTRY EXISTS.                        
*                                                                               
*                                                                               
DABBRWA  RECORD BEGIN                                                           
DABBRREC DS    XL(L'ABBRRECD)      ABBREV RECORD                                
         END                                                                    
*-                                                                              
DELABBR  PROC  DABBRWA                                                          
         LA    R6,DABBRREC                                                      
         USING ABBRRECD,R6                                                      
*                                                                               
         COMMENT                   R4 - DEFINE NAME TO DELETE                   
         LR    R4,R1               R4 - DEFINE NAME TO DELETE                   
*                                                                               
         COMMENT                   GET ABBREV RECORD NAME                       
         MVC   ABBRNAME,CVBLANKS                                                
         MVC   ABBRNAME(3),@R4                                                  
*                                                                               
         COMMENT                   GET ABBREV RECORD                            
         LA    R15,CPRG16                                                       
         LA    R0,CP#ABBR                                                       
         VCALL XSETSET                                                          
         LA    R15,CPRG16                                                       
         LA    R0,L'ABBRDATA                                                    
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XGET                                                             
*                                                                               
         COMMENT                   IF ABBREV RECORD, CHECK FOR ENTRY            
         IF    Z,BEGIN                                                          
         AR    R0,R1               R0 - END OF RECORDD                          
         WHILE ((R1,LT,R0),AND,(' CLC 0(16,R1),0(R4) ',NE)),BEGIN               
         LA    R1,L'ABBRNTRY(R1)                                                
         END                                                                    
         IF    (R1,LT,R0),BEGIN    IF ENTRY FOUND IN RECD, DELETE               
         LR    R2,R1                                                            
         LA    R4,L'ABBRNTRY(R1)                                                
         LR    R3,R0                                                            
         SR    R3,R4                                                            
         LR    R5,R3                                                            
         MVCL  R2,R4               MOVE OLD ENTRIES UP                          
         S     R0,=F'16'                                                        
         LA    R1,ABBRDATA                                                      
         SR    R0,R1               R0 - NEW ABBR RECORD LEN                     
*                                                                               
         COMMENT                   WRITE NEW ENTRY, W/OUT ABBREV                
         IF    (R0,POS),BEGIN                                                   
         LA    R15,CPRG16                                                       
         LR    R0,R0                                                            
         LA    R1,ABBRDATA                                                      
         LA    R2,ABBRNAME                                                      
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT DEFFULL,'Session Data Record Group Overflow. '                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF EMPTY ABBREV RECORD, JUST DELETE          
         ELSE BEGIN                                                             
         LA    R15,CPRG16                                                       
         LA    R2,ABBRNAME                                                      
         VCALL XDELETE                                                          
         END                                                                    
         END , ABBREV ENTRY EXISTS                                              
         END , ABBREV RECORD EXITS                                              
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'HISTORY Routines'                                       **ISU** 
HCMD     RECORD BEGIN                                                           
HCMDID   DS    C'CMD'              Self identification                          
HCMDFLAG FLAG                                                                   
HCMDPREV DS    A                   Previous HCMD entry ptr                      
HCMDNEXT DS    A                   Next HCMD entry ptr                          
HCMDCLCK DS    F                   Clock when command was saved                 
HCMDXLNO DS    F                   EXEC lineno if EXEC command                  
HCMDNO   DS    H                   Command number                               
HCMDLEN  DS    X                   Command length                               
HCMDTEXT DS    0CL(L'CPCMD)        Command text                                 
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SETHIST -- SET HISTORY Command.                                              
*                                                                               
SETHIST  XPROC                                                                  
         LR    R5,R0                                                            
         SCAN  ,                   Nothing else                                 
         IF    (R0,NZ),'VCALL NOTVALID'                                         
*                                                                               
         STH   R5,CPHCMDMX         Max no. of cmds to display                   
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWHIST -- SHOW HISTORY Command.                                            
*                                                                               
SHOWHIST XPROC                                                                  
         LH    R5,CPHCMDMX         Default no. of cmds to show                  
         SCAN  SHHPRT                                                           
         SCANCHK                                                                
*                                                                               
         IF    (CPHCMDQH,Z),BEGIN  No saved commands...                         
         SETMSG 'No commands in history list.'                                  
         B     CVNXTMSG                                                         
         END                                                                    
*                                                                               
         L     R4,CPHCMDQH                                                      
         IF    (R5,NZ),BEGIN       Only the last "n" commands...                
         IF    (R5,GE,CPHCMDCR),EXIT  Display everything, scram                 
         DECR  R5                                                               
*                                                                               
         L     R4,CPHCMDQT         Most recent command                          
         WHILE (R5,POS),BEGIN      Back up...                                   
         WITH  (HCMD,R4)                                                        
         DECR  R5                                                               
         L     R4,HCMDPREV         Previous entry                               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Display saved commands.                                                
*-                                                                              
         WHILE (R4,NZ),BEGIN       Go through list...                           
         WITH  (HCMD,R4)                                                        
         TNUM  LH:HCMDNO,5                                                      
         TSEG  CVBLANKS,2                                                       
         L     R0,HCMDCLCK         Tod clock                                    
         CLEAR R1                                                               
         XPUSH ,,32,PTR=R15                                                     
         VCALL FMTTIME             Returns hh:mm:ss.hh                          
         TSEG  (R1),5              "hh:mm"                                      
         XPOP  ,,32                                                             
         TSEG  CVBLANKS,1                                                       
         TSEG  HCMDTEXT,LC:HCMDLEN,CR  Command text                             
*                                                                               
         L     R4,HCMDNEXT         Next HCMD entry                              
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         SPACE 2                                                                
SHHPRT   SCKW  ,SHHNUM,I,9999                                                   
         SCKW  ALL,SHHALL                                                       
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE 2                                                                
SHHALL   PROC                                                                   
         CLEAR R5                                                               
         PRETURN (R5)                                                           
         PEND                                                                   
*                                                                               
SHHNUM   PROC                                                                   
         LR    R5,R0                                                            
         PRETURN (R5)                                                           
         PEND                                                                   
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ADDHCMD -- Routine to add a command to the user's history list.              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - command loc, len                                                 
*                                                                               
ADDHCMD  PROC                                                                   
         VCALL LRTRIM              Trim blanks                                  
         IF    (R0,NEG),'CLEAR R0'                                              
         CEIL  R0,L'HCMDTEXT       Not too much                                 
         LR    R3,R1                                                            
         LR    R2,R0                                                            
*-                                                                              
*-       Add new command to the user's list.                                    
*-                                                                              
         AH    R0,=AL2(L'HCMD)     Add in prefix length                         
*        GETMAIN RU,LV=(0),LOC=ANY,SP=90  (Added, 2/89, Niz)                    
         VCALL GETCORE                                                          
         LR    R4,R1                                                            
         WITH  (HCMD,R4),BEGIN                                                  
         CLEAR HCMD                                                             
         MVC   HCMDID,=C'CMD'                                                   
         VCALL LOCALTOD            Save tod clock                               
         ST    R0,HCMDCLCK         Save high word                               
         STC   R2,HCMDLEN                                                       
         MOVE  R2,HCMDTEXT,@R3     Save command text                            
*                                                                               
         INCR  R15,CPHCMDNO        Kick command number                          
         STH   R15,HCMDNO          Save current command number                  
*                                                                               
         IF    (CPHCMDQH,Z),BEGIN  First entry...                               
         ST    R4,CPHCMDQH         We are queue head                            
         ST    R4,CPHCMDQT         And we are the queue tail too                
         END                                                                    
*                                                                               
         ELSE  BEGIN               Add to end of queue...                       
         L     R15,CPHCMDQT        Last HCMD entry                              
         ST    R4,HCMDNEXT-HCMD(R15)  Next entry points to us                   
         ST    R15,HCMDPREV        Set our previous ptr                         
         ST    R4,CPHCMDQT         We are the new queue tail                    
         END                                                                    
*                                                                               
         INCR  R15,CPHCMDCR        Number of commands saved                     
         END                                                                    
*-                                                                              
*-       Remove excess commands from the history list.                          
*-                                                                              
         WHILE (CPHCMDCR,GT,=AL2(CPHCMD#)),BEGIN  Remove old cmds...            
         L     R1,CPHCMDQH         Oldest HCMD entry                            
         ACALL FREEHCMD            Release HCMD entry                           
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GETHCMD -- Routine to get first command in the user's history                
*    list that matches the command string.                                      
*                                                                               
*    On entry:                                                                  
*      R1,R0 - command text to match loc, len                                   
*                                                                               
*    On exit, R15 (and cc):                                                     
*     -4 - no match                                                             
*      0 - command matched (R1,R0 - command loc, len)                           
*                                                                               
GETHWA   RECORD BEGIN                                                           
GETHNO   DS    H                   Command number to match                      
GETHMAT  DS    XL(L'HCMDTEXT)      Prefix string to match                       
GETHTEXT DS    XL(L'HCMDTEXT)      Working copy of HCMDTEXT                     
         END                                                                    
*-                                                                              
GETHCMD  XPROC GETHWA                                                           
         CLEAR GETHNO                                                           
*                                                                               
         VCALL LRTRIM                                                           
         IF    (R0,NEG),'CLEAR R0'                                              
         CEIL  R0,L'HCMDTEXT       Not too much now                             
*-                                                                              
*-       Return the most recent command if zero length.                         
*-                                                                              
         IF    (R0,Z),BEGIN        Take most recent command...                  
         LT    R4,CPHCMDQT         Most recent command                          
         BZ    GETHNONE            Nothing, scram                               
         WITH  (HCMD,R4)                                                        
         SETMSG HCMDTEXT,LC:HCMDLEN  Command text                               
         CLEAR R15                 Matched return code                          
         B     GETHEXIT                                                         
         END                                                                    
*-                                                                              
*-       Match partial string in command list.                                  
*-                                                                              
         L     R5,CVUPPTBL         Translate to upper TR table                  
*                                                                               
         LR    R2,R0               String length                                
         LR    R15,R0                                                           
         MOVE  R15,GETHMAT,@R1     Save string                                  
         EX    R15,'TR GETHMAT(0),@R5'  Convert to caps                         
*-                                                                              
*-       Match command number if numeric.                                       
*-                                                                              
         DTB   (R1),(R0)           Number?                                      
         IF    Z,BEGIN             Yes...                                       
         STH   R15,GETHNO          Save command number to match                 
         END                                                                    
*                                                                               
         L     R4,CPHCMDQT         Most recent command ptr                      
         WHILE (R4,NZ),BEGIN       Go through history list...                   
         WITH  (HCMD,R4)                                                        
         FAIL  (HCMDID,NE,'CMD'),HCMDERR2,'History command error.'              
*                                                                               
         IF    (GETHNO,NZ),BEGIN   Match command number...                      
         IF    (GETHNO,NE,HCMDNO),GETHLOOP  No match, keep looking              
         SETMSG HCMDTEXT,LC:HCMDLEN  Command text                               
         CLEAR R15                 Matched return code                          
         B     GETHEXIT                                                         
         END                                                                    
*                                                                               
         MVC   GETHTEXT,CVBLANKS   Preset to blanks                             
         LC    R15,HCMDLEN                                                      
         IF    (R15,POS),BEGIN                                                  
         MOVE  R15,GETHTEXT,HCMDTEXT  Working command text                      
         EX    R15,'TR GETHTEXT(0),@R5'  Convert to caps                        
         END                                                                    
*                                                                               
         LR    R15,R2                                                           
         CMPR  R15,GETHMAT,GETHTEXT  Matched starting text?                     
         IF    EQ,BEGIN            Yes, stop looking...                         
         SETMSG HCMDTEXT,LC:HCMDLEN  Command text                               
         CLEAR R15                 Matched return code                          
         B     GETHEXIT                                                         
         END                                                                    
*                                                                               
GETHLOOP L     R4,HCMDPREV         Previous command                             
         END                                                                    
*                                                                               
GETHNONE LH    R15,=H'-4'          No match return code                         
         CLEAR R0,R1               (Tidy)                                       
*                                                                               
GETHEXIT PRETURN (R0,R1)           Return command ptrs                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CLRHCMDS -- Routine to clear history command list.                           
*                                                                               
CLRHCMDS XPROC                                                                  
         WHILE (CPHCMDCR,NZ),BEGIN  Free all commands...                        
         L     R1,CPHCMDQH                                                      
         ACALL FREEHCMD                                                         
         END                                                                    
         PEND                                                                   
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  FREEHCMD -- Local routine to free the HCMD entry specified.                  
*                                                                               
*    On entry:                                                                  
*      R1 - HCMD ptr                                                            
*                                                                               
FREEHCMD PROC                                                                   
         WITH  (HCMD,R1),BEGIN                                                  
         FAIL  (HCMDID,NE,'CMD'),HCMDERR3,'History command error.'              
         MVC   HCMD(4),=C'OHCM'                                                 
*                                                                               
         LT    R15,HCMDPREV                                                     
         IF    Z,'MVC CPHCMDQH,HCMDNEXT'  Update queue head                     
         ELSE  'MVC HCMDNEXT-HCMD(4,R15),HCMDNEXT'  Dequeue                     
*                                                                               
         LT    R15,HCMDNEXT                                                     
         IF    Z,'MVC CPHCMDQT,HCMDPREV'  Update queue tail                     
         ELSE  'MVC HCMDPREV-HCMD(4,R15),HCMDPREV'  Dequeue                     
*                                                                               
         DECR  R15,CPHCMDCR        Number of commands saved                     
         FAIL  NEG,HCMDERR9,'History command error.'                            
*                                                                               
         MVC   HCMDPREV,=F'-1'     (Safety)                                     
         MVC   HCMDNEXT,=F'-1'     (Safety)                                     
*                                                                               
         LC    R0,HCMDLEN          Command text length                          
         AH    R0,=AL2(L'HCMD)     Total entry length                           
*        FREEMAIN RU,A=(1),LV=(0),SP=90  Release old entry                      
         VCALL FREECORE                                                         
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Process TRY command prefix.'                                    
*box                                                                            
*                                                                               
*  TRY -- Process TRY command prefix                                            
*                                                                               
TRY      XPROC  ,                                                               
*                                                                               
         COMMENT                   CHECK IF COMMAND TO TRY IS                   
         COMMENT                   SPECIAL OR INVALID FOR TRY                   
         ACALL CHECKTRY                                                         
*                                                                               
         COMMENT                   SET TRY FLAG                                 
         SET   CPFTRY                                                           
         SET   CPFXXTRY            *** TEST THIS OUT BUD (SCH)                  
*                                                                               
         COMMENT                   CLEAR TRY ATTN/ERROR VARIABLES               
         CLEAR CPFTATTN                                                         
         CLEAR CPFTERR                                                          
*                                                                               
         COMMENT                   REMOVE "TRY" FROM COMMAND LINE               
         LR    R3,R0                                                            
         MOVE  R3,@R1,CVBLANKS                                                  
*                                                                               
         COMMENT                   RESET SCANNER, TRY COMMAND                   
         SCINFO                                                                 
         SCINIT (R1),(R0)          RESET SCANNER                                
         OSCINIT (R1),(R0)         OLD SCANNER COMPATIBILTY                     
*-                                                                              
*-       Check for %-style network command.                                     
*-         We must do this before the scanner is ever called on                 
*-         the command line to preserve the upper and lower case                
*-         of the first token (damn scanner).                                   
*-                                                                              
         IF    ((@R1,EQ,' '),OR,(@R1,EQ,'%')),BEGIN  Possibly...                
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    (R0,NP),EXIT        Nothing, forget it                           
         IF    (@R1,EQ,'%'),'ACALL NETCMD'  Do % command                        
         END                                                                    
*-                                                                              
*-       The following is for dual-scanner compatibility only!!!                
*-                                                                              
         SCPUSH ,                                                               
         SCAN ,                                                                 
         SCINFO ,                                                               
         OSCINIT (R1),(R0)         OLD SCANNER COMPATIBILTY                     
         SCPOP ,                                                                
*                                                                               
         VCALL XCMDSCAN            CHECK FOR EXEC COMMANDS (EG. X=1)            
         SCAN  PRT                 SCAN AND EXECUTE COMMAND !                   
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHECKTRY - Check if command to TRY is special/invalid                        
*                                                                               
*  Special TRY sets CPFXXTRY,  this flag remains set till next                  
*  exec command from an exec file is executed.  That is the                     
*  CPFXXTRY flag survives a EDTCOM call, CPFTRY does not.  This                 
*  allows VIEW (which executes other commands in PAGE mode) to                  
*  not break if CPFXXTRY is set.  On the other hand, a TRY READ                 
*  that executes a bad command, may cause a break. Which is                     
*  presumably what we want.  GET and PUT are special cases.                     
*  They are the only ORVYL commands that can be TRYed. This is                  
*  tricky code.  All so we can use TRY for GET, PUT, and VIEW.                  
*  And View may not even work. YUK !                                            
*                                                                               
CHECKTRY PROC                                                                   
         SCPUSH ,                                                               
         SCAN  CKTRYPRT                                                         
         SCPOP ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
CKTRYPRT SCKW  GET,TSPECIAL        A PARTIAL LIST OF INVALID CMDS               
         SCKW  PUT,TSPECIAL                                                     
         SCKW  VIEW,TSPECIAL,A1                                                 
         SCKW  EXECUTE,TINVALID,A                                               
         SCKW  UTIL,TINVALID,A                                                  
         SCKW  ADMIN,TINVALID,A                                                 
         SCKW  EMS,TINVALID                                                     
         SCKW  FOLIO,TINVALID                                                   
         SCKW  PRISM,TINVALID                                                   
         SCKW  SPIRES,TINVALID                                                  
         SCKW  CALL,TINVALID,A                                                  
         SCKW  ,TNORMAL                                                         
*                                                                               
TNORMAL  DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
TSPECIAL PROC                                                                   
         SET   CPFXXTRY                                                         
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
TINVALID PROC                                                                   
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.  Command may not be used '                           
         ERROR 'with TRY prefix.'                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETTRY - Set TRY variables                                                   
*                                                                               
*  ON ENTRY:                                                                    
*       CPFERROR, CPFABORT, JCBFATTN flags set                                  
*                                                                               
*  ON EXIT:                                                                     
*       TRY variables set appropriately                                         
*       R15 - CC=ZERO                                                           
*                                                                               
*  This routine is called by the command CLEANUP routine if the                 
*  TRY flag is set.  The routine assumes that SYS.EMSG has been                 
*  set up by the SETEMSG routine.  See CLEANUP routine for details.             
*                                                                               
*  Note: Attention overrides errors.  Do not change this without                
*        much thought !!                                                        
*                                                                               
*                                                                               
SETTRY   PROC                                                                   
         L     R6,CVCURJCB                                                      
         USING JCB,R6                                                           
*                                                                               
         COMMENT                   ASSUME NO ERRORS, ATTN                       
         CLEAR CPFTERR                                                          
         CLEAR CPFTATTN                                                         
*                                                                               
         COMMENT                   IF ATTN, SET TRY ATTN VARS                   
         IF    JCBFATTN,BEGIN                                                   
         SET   CPFTATTN            ATTN DURING TRY                              
*                                                                               
         COMMENT                   GET, SAVE ATTN INFO                          
         IF    CPFEXEC,BEGIN                                                    
         TPUSH ,                                                                
         TSEG  'EXEC break at line '                                            
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         L     R0,CPSEGLENF                                                     
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.TRYINFO     '                                       
         VCALL SAVEDATA                                                         
         TPOP  JUNK                                                             
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                                                               
         CLEAR R1                                                               
         LA    R2,=CL16'SYS.TRYINFO     '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
         END   ,                                                                
*                                                                               
*                                                                               
         COMMENT                   IF ERROR, SET TRY ERROR VARS                 
         ELSEIF (CPFERROR,OR,CPFABORT),BEGIN                                    
         SET   CPFTERR             ERROR DURING TRY                             
*                                                                               
         COMMENT                   GET, SAVE ERROR INFO                         
         COMMENT                   SAVE ERRID                                   
         IF    (CPERRID,EQ,0),BEGIN                                             
         MVC   CPERRID,=CL8'ERROR   '                                           
         END                                                                    
         LA    R0,L'CPERRID                                                     
         LA    R1,CPERRID                                                       
         LA    R2,=CL16'SYS.TRYID       '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   GET, SAVE ERROR MSG                          
         XPUSH ,,&LINESZ,PTR=R1                                                 
         LA    R0,&LINESZ                                                       
         LA    R2,=CL16'SYS.EMSG        '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R2,=CL16'SYS.TRYMSG      '                                       
         VCALL SAVEDATA                                                         
         XPOP  ,,&LINESZ                                                        
*                                                                               
         COMMENT                   GET, SAVE TRY INFO                           
         IF    CPFEXEC,BEGIN                                                    
         TPUSH ,                                                                
         TSEG  'EXEC error at line '                                            
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         L     R0,CPSEGLENF                                                     
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.TRYINFO     '                                       
         VCALL SAVEDATA                                                         
         TPOP  JUNK                                                             
         END                                                                    
         ELSE  BEGIN               IF NOT IN EXEC MODE, NO ERRINFO              
         CLEAR R0                                                               
         CLEAR R1                                                               
         LA    R2,=CL16'SYS.ERRINFO     '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'USERCMD Command Prefix'                                         
*box                                                                            
*                                                                               
*  USERCMD -- Process USERCMD command prefix.                                   
*                                                                               
*    Use this prefix to force user defined command substitions                  
*    to take place.  For example, "usercmd print ..." would first               
*    check for a user defined PRINT command (and then the system                
*    defined PRINT command).                                                    
*                                                                               
*    Normally, typed commands do check for user defined commands                
*    and extended exec commands do not.                                         
*                                                                               
USERCMD  XPROC                                                                  
*-                                                                              
*-       Reset scanner and re-execute command without prefix.                   
*-                                                                              
         SCINFO                                                                 
         STH   R0,CPCMDLEN                                                      
         LR    R2,R0                                                            
         XPUSH ,,L'CPCMD,PTR=R3                                                 
         MOVEL (R3),(R1),(R2)                                                   
         MOVEL CPCMD,(R3),(R2)                                                  
         XPOP  ,,L'CPCMD                                                        
         SCINIT CPCMD,LH:CPCMDLEN  RESET SCANNER                                
         OSCINIT CPCMD,LH:CPCMDLEN  OLD SCANNER COMPATIBILTY                    
*-                                                                              
*-       Scan for assignment statement.                                         
*-                                                                              
         VCALL XCMDSCAN            Pre-scan for exec cmds (eg. x=1)             
*-                                                                              
*-       Do user defined command substitution.                                  
*-                                                                              
         ACALL DEFCMD              Do user defined commands                     
         IF    Z,BEGIN             Command substituted...                       
         SCINIT CPCMD,LH:CPCMDLEN  (allow x=1 to be defined command)            
         VCALL XCMDSCAN            CHECK FOR EXEC COMMANDS (EG. X=1)            
         END                                                                    
         ELSE  'SCINIT CPCMD,LH:CPCMDLEN'                                       
*-                                                                              
*-       Scan and execute command.                                              
*-                                                                              
         SCAN  PRT                 SCAN AND EXECUTE COMMAND !                   
         COMMENT                   DOES NOT USUALLY RETURN !                    
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         TITLE 'SYSCMD Command Prefix'                                          
*box                                                                            
*                                                                               
*  SYSCMD -- Process SYSCMD command prefix.                                     
*                                                                               
*    Use this prefix to avoid conflicts with user defined commands.             
*    For example, "syscmd print ..." would not execute a user                   
*    defined PRINT command -- it would always execute the "system"              
*    PRINT command.                                                             
*                                                                               
SYSCMD   XPROC                                                                  
*-                                                                              
*-       Reset scanner and re-execute command without prefix.                   
*-                                                                              
         SCINFO                                                                 
         STH   R0,CPCMDLEN                                                      
         LR    R2,R0                                                            
         XPUSH ,,L'CPCMD,PTR=R3                                                 
         MOVEL (R3),(R1),(R2)                                                   
         MOVEL CPCMD,(R3),(R2)                                                  
         XPOP  ,,L'CPCMD                                                        
         SCINIT CPCMD,LH:CPCMDLEN  RESET SCANNER                                
         OSCINIT CPCMD,LH:CPCMDLEN  OLD SCANNER COMPATIBILTY                    
*-                                                                              
*-       Scan and execute command.                                              
*-                                                                              
         VCALL XCMDSCAN            CHECK FOR EXEC COMMANDS (EG. X=1)            
         SCAN  PRT                 SCAN AND EXECUTE COMMAND !                   
         COMMENT                   DOES NOT USUALLY RETURN !                    
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         TITLE 'CDUMP Command Prefix'                                           
*box                                                                            
*                                                                               
*  CDUMP -- Process CDUMP command prefix.                                       
*                                                                               
CDUMP    XPROC                                                                  
*                                                                               
         COMMENT                   CHECK IF COMMAND VALID FOR CDUMP             
         ACALL CHKCDUMP                                                         
*-                                                                              
*-       Set DUMP flags.                                                        
*-                                                                              
         MVI   CPDMPLST,X'80'      Lineno not yet set                           
         MVC   CPACLNO,=F'-1000'   No current lineno                            
         SET   CPFDUMP             Turn on the stow flag                        
*-                                                                              
*-       Remove CDUMP from command line.                                        
*-                                                                              
         LR    R3,R0                                                            
         MOVE  R3,@R1,CVBLANKS                                                  
*-                                                                              
*-       Reset scanner and re-execute command without prefix.                   
*-                                                                              
         SCINFO                                                                 
         SCINIT (R1),(R0)         RESET SCANNER                                 
*-                                                                              
*-       The following is for dual-scanner compatibility only                   
*-                                                                              
         SCPUSH                                                                 
         SCAN                                                                   
         SCINFO                                                                 
         OSCINIT (R1),(R0)         OLD SCANNER COMPATIBILTY                     
         SCPOP                                                                  
*                                                                               
         VCALL XCMDSCAN            CHECK FOR EXEC COMMANDS (EG. X=1)            
         SCAN  PRT                 SCAN AND EXECUTE COMMAND !                   
         COMMENT                   DOES NOT USUALLY RETURN !                    
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKCDUMP - Check if command to CDUMP command is invaild.                     
*                                                                               
*  CDUMP command prefix does not work for ORVYL commands.                       
*  It also does not work for XCALL nor EXEC commands.  This                     
*  list is not exhaustive, but catches a few commands that                      
*  people might try without success.                                            
*                                                                               
CHKCDUMP PROC                                                                   
         SCPUSH ,                                                               
         SCAN  CKCDPRT                                                          
         SCPOP ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CKCDPRT  SCKW  GET,CINVALID        A PARTIAL LIST OF INVALID CMDS               
         SCKW  PUT,CINVALID                                                     
         SCKW  EXECUTE,CINVALID,A                                               
         SCKW  UTIL,CINVALID,A                                                  
         SCKW  ADMIN,CINVALID,A                                                 
         SCKW  EMS,CINVALID                                                     
         SCKW  FOLIO,CINVALID                                                   
         SCKW  PRISM,CINVALID                                                   
         SCKW  SPIRES,CINVALID                                                  
         SCKW  CALL,CINVALID,A                                                  
         SCKW  XC,CINVALID                                                      
         SCKW  XCALL,CINVALID,A                                                 
         SCKW  ,CNORMAL                                                         
*-                                                                              
CNORMAL  DPROC ,                   NORMAL CDUMP, DO NOTHING                     
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CINVALID PROC  ,                   INVALID QUIET OPTION                         
         TSEG  (R1),(R0)                                                        
   ERROR ': invalid.  Command may not be used with CDUMP prefix. '              
         PEND                                                                   
         TITLE 'QUIET Command Prefix'                                           
*box                                                                            
*                                                                               
*  QUIET -- Process QUIET command prefix.                                       
*                                                                               
QUIET    XPROC                                                                  
*                                                                               
         COMMENT                   CHECK IF COMMAND VALID FOR QUIET             
         ACALL CHKQUIET                                                         
*                                                                               
         COMMENT                   SET QUIET FLAG                               
         SET   CPFQUIET                                                         
*                                                                               
         COMMENT                   REMOVE "QUIET" FROM COMMAND LINE             
         LR    R3,R0                                                            
         MOVE  R3,@R1,CVBLANKS                                                  
*                                                                               
         COMMENT                   RESET SCANNER, QUIETLY DO COMMAND            
         SCINFO                                                                 
         SCINIT (R1),(R0)          RESET SCANNER                                
*-                                                                              
*-       The following is for dual-scanner compatibility only                   
*-                                                                              
         SCPUSH                                                                 
         SCAN                                                                   
         SCINFO                                                                 
         OSCINIT (R1),(R0)         OLD SCANNER COMPATIBILTY                     
         SCPOP                                                                  
*                                                                               
         VCALL XCMDSCAN            CHECK FOR EXEC COMMANDS (EG. X=1)            
         SCAN  PRT                 SCAN AND EXECUTE COMMAND !                   
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKQUIET - Check if command to QUIET command is invaild.                     
*                                                                               
*  QUIET command prefix does not work for ORVYL commands.                       
*  It also does not work for XCALL nor EXEC commands.  This                     
*  list is not exhaustive, but catches a few commands that                      
*  people might try without success.                                            
*                                                                               
*                                                                               
CHKQUIET PROC                                                                   
         SCPUSH ,                                                               
         SCAN  CKQUIPRT                                                         
         SCPOP ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       CHECK INVALID COMMANDS.                                                
*-                                                                              
CKQUIPRT SCKW  GET,QINVALID        A PARTIAL LIST OF INVALID CMDS               
         SCKW  PUT,QINVALID                                                     
         SCKW  EXECUTE,QINVALID,A                                               
         SCKW  UTIL,QINVALID,A                                                  
         SCKW  ADMIN,QINVALID,A                                                 
         SCKW  EMS,QINVALID                                                     
         SCKW  FOLIO,QINVALID                                                   
         SCKW  PRISM,QINVALID                                                   
         SCKW  SPIRES,QINVALID                                                  
         SCKW  CALL,QINVALID,A                                                  
         SCKW  XC,QINVALID                                                      
         SCKW  XCALL,QINVALID,A                                                 
         SCKW  ,QNORMAL                                                         
*-                                                                              
QNORMAL  DPROC ,                   NORMAL QUIET, DO NOTHING                     
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
QINVALID PROC  ,                   INVALID QUIET OPTION                         
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.  '                                                   
         ERROR 'Command may not be used with QUIET prefix. '                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'DECIDE Command'                                                 
*box                                                                            
*                                                                               
*  DECIDE -- DECIDE command.                                                    
*                                                                               
DECIDE   XPROC                                                                  
         VCALL LOCALTOD                                                         
         STM   R0,R1,CPDOUB                                                     
         L2    R15,CPDOUB+5        Lowish bits                                  
         SRL   R15,8               (Bits that are always zero)                  
         N     R15,=F'7'           Leave last 3 bits                            
         MH    R15,=H'25'          Times 25 for offset                          
         TSEG  REMARKS(R15),25,B                                                
         B     CVNEXT                                                           
         PEND  ,                                                                
*                                                                               
*                   ....v....1....v....2....v                                   
REMARKS  DC    CL25'** Yes **'                                                  
         DC    CL25'** No **'                                                   
         DC    CL25'** Why not? **'                                             
         DC    CL25'** Go for it! **'                                           
         DC    CL25'** Try again later **'                                      
         DC    CL25'** Use Navigator **'                                        
         DC    CL25'** For sure! **'                                            
         DC    CL25'** No WAY! **'                                              
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NOTYET -- Unimplemented command.                                             
*                                                                               
NOTYET   XPROC                                                                  
         ABORT 'Command is not yet implemented.'                                
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'COMMAND Table'                                                  
*-                                                                              
*-       Main command table.                                                    
*-                                                                              
PRT      SCKW  IF,V(IF),S                                                       
         SCKW  SET,V(SET),S                                                     
         SCKW  READ,V(READ),(A,S)                                               
         SCKW  WRITE,V(WRITE),(A,S)                                             
         SCKW  CH,V(CHANGE),S                                                   
         SCKW  CHANGE,V(CHANGE),(A,S)                                           
         SCKW  CDUMP,V(CDUMP),A                                                 
         SCKW  SHOW,V(SHOW),(A,S)                                               
         SCKW  DUMP,V(DUMP),(A,S)                                               
         SCKW  $DUMP,V(DUMP),(A,S)                                              
         SCKW  SV,V(SHOWVAL),S                                                  
         SCKW  EXECUTE,V(EXECUTE),(A,S)                                         
         SCKW  XC,V(XCALL),S                                                    
         SCKW  XA,V(XCACTIVE),S                                                 
         SCKW  XCALL,V(XCALL),(A,S)                                             
         SCKW  XCOMMAND,V(XCOMYUCK),(A,S)                                       
         SCKW  XRETURN,V(XRETURN),(A,S)                                         
         SCKW  XEXIT,V(XEXIT),S                                                 
         SCKW  PCALL,V(PCALL),S                                                 
         SCKW  RETURN,V(PRETURN),S                                              
         SCKW  SYSCALL,V(SYSCALL),S                                             
         SCKW  GOTO,V(GOTO),S                                                   
         SCKW  LIST,V(LIST),(A1,S)                                              
         SCKW  POINT,V(POINT),(A1,S)                                            
         SCKW  BEGIN,V(BEGIN),S                                                 
         SCKW  END,V(END),S                                                     
         SCKW  WHILE,V(WHILE),S                                                 
         SCKW  ELSE,V(ELSE),(A,S)                                               
         SCKW  ELSEIF,V(ELSEIF),S                                               
         SCKW  REPEAT,V(REPEAT),S                                               
         SCKW  UNTIL,V(UNTIL),S                                                 
         SCKW  CASE,V(CASE),S                                                   
         SCKW  CASES,V(CASES),S                                                 
         SCKW  THEN,V(THEN),(A,S)                                               
         SCKW  LOOP,NOTYET,S                                                    
         SCKW  XPROCEDURE,V(XPROC),(A,S)                                        
*        SCKW  PROCEDURE,NOTYET,(A,S)    SPIRES CONFLICT                        
         SCKW  XLOAD,V(XLOAD),(A,S)                                             
         SCKW  XSCAN,V(XSCAN),(A,S)                                             
         SCKW  XFORMAT,V(XFORMAT),(A,S)                                         
         SCKW  XCONVERT,V(XCONVERT),(A,S)                                       
         SCKW  XPAUSE,V(XPAUSE),(A,S)                                           
         SCKW  XRESET,V(XRESET),(A,S)                                           
         SCKW  XGO,V(XGO),S                                                     
         SCKW  SYSCMD,SYSCMD,S                                                  
         SCKW  USERCMD,USERCMD,S                                                
         SCKW  PUBLOAD,V(PUBLOAD),S      DON'T ALLOW PUB ABBREV.                
         SCKW  ON,V(ON),S                                                       
         SCKW  PRINT,V(PRINT),(A,S)                                             
         SCKW  LPRINT,V(PRINT),(A,S)                                            
         SCKW  _FAX,V(PRINT),S                                                  
         SCKW  MODIFY,V(MODIFY),(A1,S)                                          
         SCKW  EDIT,V(EDIT),(A1,S)                                              
         SCKW  SPELL,V(SPELL),(A,S)                                             
         SCKW  CHECK,CMDFIN,(A,S)                                               
         SCKW  GUESS,V(GUESS),(A,S)                                             
         SCKW  LEARN,V(LEARN),(A,S)                                             
         SCKW  UNLEARN,V(UNLEARN),(A,S)                                         
         SCKW  JOIN,V(JOIN),(A1,S)                                              
         SCKW  SPLIT,V(SPLIT),(A,S)                                             
         SCKW  EXCHANGE,V(EXCHANGE),(A,S)                                       
         SCKW  VIEW,V(VIEW),(A1,S)                                              
         SCKW  PUT,CMDFIN          ORVYL's PUT command                          
         SCKW  PUTLINE,V(PUTLCMD),(A,S)                                         
         SCKW  PUTLINET,V(PUTLCMDT),(A,S)                                       
         SCKW  PUTEND,V(PUTEND),S                                               
         SCKW  COLLECT,V(COLLECT),(A1,S)                                        
         SCKW  LOCATE,V(JESCMD),(A,S)                                           
         SCKW  COMPARE,V(COMPARE),(A,S)  must stay ahead of comment             
         SCKW  COMMENT,V(COMMENT),(A,S)                                         
         SCKW  RUN,V(RUN),S                                                     
         SCKW  WAIT,V(WAIT),(A,S)                                               
         SCKW  DEFINE,V(DEFINE),(A,S)                                           
         SCKW  UNDEFINE,V(UNDEFINE),(A,S)                                       
         SCKW  _TO,V(TO)                                                        
         SCKW  CLEAR,V(CLEAR),(A,S)                                             
         SCKW  CLR,V(CLEAR),S                                                   
         SCKW  COPY,V(COPY),(A,S)                                               
         SCKW  COPYA,V(COPYA),(A,S)                                             
         SCKW  COUNT,V(COUNT),(A,S)                                             
         SCKW  DELETE,V(DELETE),(A1,S)                                          
         SCKW  FETCH,V(FETCH),(A,S)                                             
         SCKW  SYNCH,V(JESCMD),(A,S)                                            
         SCKW  CHECKRUN,V(JESCMD),S                                             
         SCKW  INSERT,V(INSERT),(A,S)                                           
         SCKW  I,V(INSERT),(A,S)                                                
         SCKW  MOVE,V(MOVE),(A,S)                                               
         SCKW  NUMBER,V(NUMBER),(A,S)                                           
         SCKW  REPLACE,V(REPLACE),(A1,S)                                        
         SCKW  USE,V(USE),S                                                     
         SCKW  WUSE,V(USE),(A,S)                                                
         SCKW  SQUASH,V(SQUASH),(A,S)                                           
         SCKW  SQUISH,V(SQUASH),(A,S)                                           
         SCKW  DCL,V(DECLARE),S                                                 
         SCKW  DECLARE,V(DECLARE),(A,S)                                         
         SCKW  XD,V(XDEBUG)                                                     
         SCKW  XDEBUG,V(XDEBUG),(A,S)                                           
         SCKW  XMONITOR,V(XMONITOR),(A,S)                                       
         SCKW  TRY,TRY                                                          
         SCKW  QUIET,QUIET,A                                                    
         SCKW  QUIETLY,QUIET                                                    
         SCKW  LOGOFF,V(LOGOFF),S                                               
         SCKW  LOGOUT,V(LOGOFF),S                                               
         SCKW  SIGNOFF,V(LOGOFF),S                                              
         SCKW  LOGON,V(LOGON),S                                                 
         SCKW  LOGIN,V(LOGON),S                                                 
         SCKW  SIGNON,V(LOGON),S                                                
         SCKW  PUNCH,V(PRINT),(A,S)                                             
         SCKW  PURGE,V(JESCMD),(A,S)                                            
         SCKW  CONDENSE,V(CONDENSE),(A,S)                                       
         SCKW  CANCEL,V(JESCMD),(A,S)                                           
         SCKW  HOLD,V(JESCMD),(A,S)                                             
         SCKW  RELEASE,V(JESCMD),(A,S)                                          
         SCKW  ALTER,V(JESCMD),(A,S)                                            
         SCKW  JPRINT,V(JESCMD),(A,S)                                           
         SCKW  XMIT,V(JESCMD),(A,S)                                             
         SCKW  RESTORE,V(RESTORE),(A,S)                                         
         SCKW  ALIGN,V(ALIGN),(A,S)                                             
         SCKW  JUSTIFY,V(ALIGN),(A,S)                                           
         SCKW  CENTER,V(CENTER),(A,S)                                           
         SCKW  PFORMAT,V(PFORMAT),(A,S)                                         
         SCKW  ALLOCATE,V(ALLOCATE),(A,S)                                       
         SCKW  SAVE,V(SAVE),(A,S)                                               
         SCKW  OLDSAVE,V(OLDSAVE),(A,S)                                         
         SCKW  WSAVE,V(SAVE),(A,S)                                              
         SCKW  RESAVE,V(RESAVE),(A,S)        (Must comm after RESTORE)          
*        SCKW  RESAVE,V(OLDSAVE),(A,S)       (Must come after RESTORE)          
         SCKW  SCRATCH,V(SCRATCH),(A,S)                                         
         SCKW  OSRENAME,V(OSRENAME),(A,S)                                       
         SCKW  CATALOG,V(CATALOG),(A,S)                                         
         SCKW  CATLG,V(CATALOG),S                                               
         SCKW  RECATALOG,V(CATALOG),(A,S)                                       
         SCKW  RECATLG,V(CATALOG),S                                             
         SCKW  UNCATALOG,V(CATALOG),(A,S)                                       
         SCKW  UNCATLG,V(CATALOG),S                                             
         SCKW  MAIL,V(MAIL),(A,S)                                               
         SCKW  SEND,V(MAIL),(A,S)                                               
         SCKW  PASS,V(PASS),(A,S)                                               
         SCKW  MICRO,V(MICRO),(A,S)                                             
         SCKW  SAMSON,V(SAMSON),(A,S)                                           
         SCKW  OPEN,V(OPEN),(A,S)                                               
         SCKW  CLOSE,V(CLOSE),(A,S)                                             
         SCKW  PICK,V(PICK),(A,S)                                               
         SCKW  UNCLEAR,V(UNCLEAR),(A,S)                                         
         SCKW  UNCLR,V(UNCLEAR),S                                               
         SCKW  PUSH,NOTYET,(A,S)                                                
         SCKW  POP,NOTYET,(A,S)                                                 
         SCKW  DECIDE,DECIDE,(A,S)                                              
         SCKW  HELP,V(HELP),(A,S)                                               
         SCKW  DESCRIBE,V(HELP),(A,S)                                           
         SCKW  ?,V(HELP),S                                                      
*        SCKW  PUBLIC,UTIL,(A,S)      ****** REMOVE ME??? *******               
         SCKW  UTIL,UTIL,S                                                      
         SCKW  INVOKE,NOTYET,(A,S)                                              
         SCKW  DO,NOTYET,S                                                      
         SCKW  SORT,V(SORT),(A,S)                                               
         SCKW  WSORT,V(SORT),S                                                  
         SCKW  EVALUATE,V(EVALUATE),(A,S)                                       
         SCKW  REC_LOAD,V(RECLOAD),S      (Must come after RECATLG)             
         SCKW  FUNCTION,NOTYET,(A,S)                                            
         SCKW  PROCESS,CMDFIN,(A,S)                                             
         SCKW  RETRY,NOTYET,(A,S)                                               
         SCKW  VARY,V(VARY),(A,S)                                               
         SCKW  SMF,V(SMF),(A,S)                                                 
         SCKW  UPLOAD,V(UPLOAD),(A,S)                                           
         SCKW  DOWNLOAD,V(DOWNLOAD),(A,S)                                       
         SCKW  KERMIT,KERMIT,S                                                  
         SCKW  QUERY,V(QUERY),(A1,S)                                            
         SCKW  QOPEN,V(QOPEN),(A,S)                                             
         SCKW  QSEND,V(QSEND),(A,S)                                             
         SCKW  QEXEC,V(QEXEC),(A,S)                                             
         SCKW  QRUN,V(QRUN),(A,S)                                               
         SCKW  QFETCH,V(QFETCH),(A,S)                                           
         SCKW  QMONITOR,V(QMONITOR),(A,S)                                       
         SCKW  QRESET,V(QRESET),(A,S)                                           
         SCKW  QCLOSE,V(QCLOSE),(A,S)                                           
         SCKW  BROADCAST,V(BROADCAS),(A,S)                                      
         SCKW  OPEN,V(OPEN),(A,S)                                               
         SCKW  CLOSE,V(CLOSE),(A,S)                                             
         SCKW  MOPEN,V(MOPEN),S                                                 
         SCKW  MCLOSE,V(MCLOSE),S                                               
         SCKW  START,V(START),(A,S)                                             
         SCKW  STOP,V(STOP),(A,S)                                               
         SCKW  KILL,V(KILL),(A,S)                                               
         SCKW  OS,V(OS)                                                         
         SCKW  NULLCMD,NULLCMD                                                  
         SCKW  _VLOGON,V(VLOGON),S                                              
         SCKW  _VLOGIN,V(VLOGON),S                                              
         SCKW  _VLOGOFF,V(VLOGOFF),S                                            
         SCKW  _VLOGOUT,V(VLOGOFF),S                                            
         SCKW  _VGO,V(VGO),S                                                    
         SCKW  _VATTN,V(VATTN),S                                                
         SCKW  _VDEBUG,V(VDEBUG),S                                              
         SCKW  _VTAM,V(VTAM),S                                                  
         SCKW  WBREAK,V(WBREAK),S                                               
         SCKW  WBILL,V(WBILL),(A,S)                                             
         SCKW  WCOUNT,V(WCOUNT),(A,S)                                           
         SCKW  WD,V(WDEBUG),(A,S)                                               
         SCKW  WDEBUG,V(WDEBUG),(A,S)                                           
         SCKW  WEVENT,V(WEVENT),(A,S)                                           
         SCKW  WMONITOR,V(WMONITOR),(A,S)                                       
         SCKW  WTEST,V(WTEST),(A,S)                                             
         SCKW  WITEST,V(WITEST)                                                 
         SCKW  WSC,V(WSC),S                                                     
         SCKW  WPC,V(WPC),S                                                     
         SCKW  WSTART,V(WSTART),(A,S)                                           
         SCKW  WSTOP,V(WSTOP),(A,S)                                             
         SCKW  SUBSYS,V(SUBSYS),S                                               
         SCKW  XCTL,V(XCTL),S                                                   
         SCKW  WYLBUR,V(XCTL),S                                                 
         SCKW  DEVWYL,V(XCTL),S                                                 
         SCKW  TESTWYL,V(XCTL),S                                                
         SCKW  OLDWYL,V(XCTL),S                                                 
         SCKW  ,V(IMPLIED),V(MIMPLIED)                                          
         SCKW  ,CMDFIN                                                          
         AGO   .ENDTAB                                                          
PRT      SCKW  ,V(NOTVALID)        TEMPORARY                                    
.ENDTAB  ANOP                                                                   
*                                                                               
         VLTORG                                                                 
         END   .                                                                
