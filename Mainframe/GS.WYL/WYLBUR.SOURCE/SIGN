SIGN     TITLE 'WYLBUR''s Signon/Signoff Processing'                            
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN ,                 Define installation params                   
         GBLC  &GREET,&INSTALN                                                  
         GBLA  &FPDAYS                                                          
         GBLB  &SETLTIM,&KEYUPLO   (See SYSDEFN)                                
*                                                                               
WYLSIGN  CSECT                                                                  
SIGN     IDENT 4072                10:30:08 03/12/04  (SLP)                     
*                                                                               
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'Constants, etc.'                                                
         COPY  CONTROL             Copy CV/CP                                   
*                                                                               
         RECORD 'PDB'                                                           
*                                                                               
         RECORD 'SIB'                                                           
*                                                                               
SEGCB    RECORD 'SEGCB'                                                         
*                                                                               
         CVT   DSECT=YES                                                        
*                                                                               
USERCVT  RECORD 'USERCVT'                                                       
*                                                                               
KWR      RECORD 'KWR'                                                           
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
SMFHDR   RECORD BEGIN                                                           
         COPY  SMFHDR                                                           
         END                                                                    
*                                                                               
SMFKWR   RECORD BEGIN                                                           
         DS    XL(L'SMFHDR)        Standard header                              
*                                                                               
         DS    XL4                 Reserved                                     
*                                                                               
         COPY  SMFKWR                                                           
         END                                                                    
*                                                                               
PFCB     RECORD BEGIN                                                           
         COPY  PFCB                                                             
         END                                                                    
*                                                                               
VOLDSECT VOLUME DSECT=YES                                                       
VOL      EQU   VOLDSECT,*-VOLDSECT                                              
*                                                                               
         POP   DSECTS                                                           
         TITLE 'WYLBUR''s Signon/Signoff Processor'                             
*box                                                                            
*                                                                               
*  SIGNON:  Initial logon entry is here.  If CPSACCT is filled                  
*    in then the user is pre-logged on.  Otherwise, the user must               
*    go through the signon sequence.                                            
*                                                                               
*      On entry:                                                                
*       R1,R0 - initial text                                                    
*                                                                               
SIGNWA   RECORD BEGIN                                                           
SIGNPASS DS    CL(L'KWRPASS)       Pre-entered password                         
*                                                                               
SIGNSYS  DS    CL8                 Subsystem name                               
*                                                                               
SIGNKWR  DS    XL(L'KWR)                                                        
         END                                                                    
*-                                                                              
SIGNON   GENTER                                                                 
         XPUSH ,,L'SIGNWA,PTR=R6                                                
         USING SIGNWA,R6                                                        
         CLEAR SIGNWA                                                           
         CLEAR CPGFLG9             Clear SIGNON process flags           ***ISU  
*                                                                               
         IF    (CPSYS,EQ,'MILTEN  '),'CLEAR CPSYS'  From MILTEN                 
         IF    (CPSYS,Z),'SET CPFSGNML'  This is an initial logon       ***ISU  
         MVC   CPORGSYS,CPSYS      Save originating subsystem name              
*                                                                               
         INCR  R15,CVNUSERS        Kick active user count                       
*                                                                               
         XPUSH ,,&LINESZ,PTR=R4                                                 
         LR    R5,R0                                                            
         CEIL  R5,&LINESZ                                                       
         LR    R15,R5                                                           
         MOVE  R15,@R4,@R1         Save initial text                            
*                                                                               
         ACALL INITCP              Set up CP, record groups, etc.               
         SET   CPGFWR              (Avoid @OK first time thru)                  
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'MVC JCBSUC,CPSUC'                                     
*                                                                               
         STH   R5,CPCMDLEN                                                      
         MOVE  R5,CPCMD,@R4        Move input into command buffer               
         XPOP  ,,&LINESZ                                                        
         B     SIGNB                                                            
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
SIGNB    BASE                                                                   
SIGNBASE LABEL                                                                  
*                                                                               
         MVC   CPCACCT,CPSACCT                                                  
         IF    (CPCACCT,NZ),BEGIN  User is pre-logged on...                     
         SET   CPFGREET                                                         
         IF    (CPCMDLEN,NZ),'SET CPFSGNCM'  First command provided             
         B     SIGNSTD                                                          
         END                                                                    
*                                                                               
         CLEAR CPSKIFL,CPSKAFL,CPSTACF,CPSIDMAX                                 
*                                                                               
         IF    (CVWORD,NZ),SIGNSTD  Skip initial text if sys time               
*                                                                               
         COMMENT                   SET UP SCANCB                                
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*                                                                               
         COMMENT                   OLD SCANNER COMPATABILITY FIX                
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  ,                   Scan off first token                         
         SCINFO ,                                                               
         OSCINIT (R1),(R0)         Old scanner compatability                    
*                                                                               
         COMMENT                   SCAN LOGON INPUT                             
         COMMENT                   WE ARE USING NEW SCANNER                     
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  SIGNPRT1            Scan off first token                         
         IF    (R15,EQ,4),'CLEAR R15'                                           
         IF    (R15,NZ),BEGIN                                                   
         B     SIGNCLR                                                          
         END                                                                    
         IF    ((CPCUSER,Z),OR,(CPCGRP,Z)),SIGNCLR                              
         SET   CPFGREET            Don't do greeting line                       
         B     SIGNK                                                            
*                                                                               
SIGNCLR  CLEAR CPCACCT                                                          
SIGNSTD  LABEL                                                                  
*                                                                               
SIGNRES  GENTER                                                                 
         BASE  SIGNBASE                                                         
         VCALL INITSTK             Re-initialize the stack                      
*                                                                               
         XPUSH ,,L'SIGNWA,PTR=R6                                                
         CLEAR SIGNWA                                                           
*                                                                               
         ACALL GREET               Write initial messages                       
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),BEGIN                                                  
         CLEAR JCBACCT,JCBANO,JCBBFNR,JCBSFV+JCBSFMIC                           
         END                                                                    
*                                                                               
         CLEAR CPZERO              Init local area                              
         MVC   CPLCOL,=H'&LINESZ'                                               
         CLEAR CPOERRID                                                         
         ACALL INITDFLT            Make sure logon defaults are set             
*                                                                               
         SET   CPFCASE,(CPFUPPER,OFF)  Set uplow for logon prompts              
         IF    CPSTCONS,'SET CPFLMSG+CPFWORD'  Console is special               
         IF    (CPSACCT,NZ),'MVC CPCACCT,CPSACCT; B SIGNK'                      
*                                                                               
SIGNLOOP CLEAR CPCACCT,SIGNPASS,CPSPROJ,CPSNAME                                 
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         IF    (CPLGNCT,GE,5),BEGIN                                             
         TSEG  'Valid account not specified, logoff proceeding.'                
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Logoff (eventually)                    
         END                                                                    
*                                                                               
         TSTAT                                                                  
         BM    SIGNLGF             Logoff                                       
*                                                                               
         IF    ^CPFWORD,BEGIN      Get the word, if any...                      
         IF    (CVWORD,Z),EXIT     No word                                      
         CLEAR CPCACCT,SIGNPASS,CPSPROJ,CPSNAME                                 
         SETMSG 'Maintenance'                                                   
         IF    CVFSYST,'SETMSG "System maintenance"'                            
         TSEG  (R1),(R0),B                                                      
         TREAD 'in progress, logon not allowed... '                             
         IF    (CVWORD,Z),EXIT     The word got reset                           
         IF    (R15,NEG),SIGNLOOP                                               
         IF    (R15,POS),SIGNLGF   No soap, fella                               
         CEIL  R0,&LINESZ                                                       
         STH   R0,CPCMDLEN                                                      
         IF    ('LTR R15,R0',P),'MOVE R15,CPCMD,@R1'                            
*                                                                               
         COMMENT                   SET UP SCANCB                                
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*                                                                               
         COMMENT                   OLD SCANNER COMPATABILITY FIX                
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  ,                   Scan off first token                         
         SCINFO ,                                                               
         OSCINIT (R1),(R0)         Old scanner compatability                    
*                                                                               
         SCINIT CPCMD,LH:CPCMDLEN                                               
         SCAN                                                                   
         IF    ((R15,NZ),OR,(R0,Z)),CVNEXT  TRY AGAIN                           
         SCUPTOK R1                                                             
         IF    (CVWORD,EQ,@R1),BEGIN  You guessed it...                         
         SET   CPFWORD                                                          
         CLEAR CPSCNFLG                                                         
         SCAN  SIGNPRT             Allow logon stuff after word                 
         IF    (R15,EQ,4),'CLEAR R15'                                           
         IF    (R15,NZ),BEGIN                                                   
         B     CVNEXT                                                           
         END                                                                    
         B     SIGNLOOP                                                         
         END                                                                    
         SCBKUP ,                  DID NOT GET WORK, CHECK ACCT                 
         SCAN  SIGNPRT             (Looking for sys priv'd acct)                
         IF    (R15,EQ,4),'CLEAR R15'                                           
         IF    (R15,NZ),BEGIN                                                   
         B     CVNEXT                                                           
         END                                                                    
         IF    ((CPCUSER,NZ),AND,(CPCGRP,NZ)),SIGNLOG                           
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         MVC   CPOERRID,=CL8'INITACCT'                                          
         TREAD 'Account',,Q                                                     
         BM    SIGNLOOP                                                         
         BP    SIGNLGF                                                          
         CEIL  R0,&LINESZ                                                       
         STH   R0,CPCMDLEN                                                      
         IF    ('LTR R15,R0',P),'MOVE R15,CPCMD,@R1'                            
*                                                                               
         COMMENT                   SET UP SCANCB                                
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*                                                                               
         COMMENT                   OLD SCANNER COMPATABILITY FIX                
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  ,                   Scan off first token                         
         SCINFO ,                                                               
         OSCINIT (R1),(R0)         Old scanner compatability                    
*                                                                               
         COMMENT                   SCAN LOGON INPUT                             
         COMMENT                   WE ARE USING NEW SCANNER                     
         SCINIT CPCMD,LH:CPCMDLEN  Init for scan                                
         SCAN  SIGNPRT             Scan off first token                         
         IF    (R15,EQ,4),'CLEAR R15'                                           
         IF    (R15,NZ),BEGIN                                                   
         B     CVNEXT                                                           
         END                                                                    
         IF    ((CPCUSER,Z),OR,(CPCGRP,Z)),SIGNLOOP                             
*                                                                               
SIGNLOG  INCR  R15,CPLGNCT         Count attempt to logon                       
         CLEAR CPCHFLAG            Clear special charging flags                 
         CLEAR CPSTACF             Assumptions                                  
*                                                                               
SIGNK    LA    R1,SIGNWA                                                        
         ACALL CKACCT              Check to make sure account is ok             
*                                                                               
         ACALL DOLOGMSG            Do msg-of-day now (if not before)            
*                                                                               
         ACALL CHKWORD             Check for the secret word                    
*                                                                               
         LA    R1,SIGNWA                                                        
         ACALL LOGFIN              User is now officially logged on             
         COMMENT                   LOGFIN ROUTINE DOES NOT RETURN               
*                                                                               
SIGNLGF  L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'                                         
         B     CVNEXT              Logoff (eventually)                          
*                                                                               
         DROP  BR                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*-                                                                              
*-       Logon scan tables                                                      
*-       (used only when MILTEN has supplied initial                            
*-        text).                                                                
*-                                                                              
SIGNPRT1 SCKW  ,V(ACCTPSHL),PUSH                                                
         SCKW  ,SIGNPSH,PUSH                                                    
         SCKW  LOGIN:,ELFLOGIN                                                  
         SCKW  ,SIGNCPSH,PUSH                                                   
         SCKW  ,CHKTRM                                                          
         SPACE 2                                                                
*-                                                                              
*-       Logon scan tables.                                                     
*-                                                                              
SIGNPRT  SCKW  ,V(ACCTPSHL),PUSH                                                
         SCKW  ,SIGNPSH,PUSH                                                    
         SCKW  ,SIGNCPSH,PUSH                                                   
         SCKW  ,CHKTRM                                                          
         SPACE 2                                                                
*-                                                                              
*-       Common Logon scan tables.                                              
*-                                                                              
SIGNCPSH PUSHRTN SIGNCPRT                                                       
SIGNCPRT SCKW  MEDLINE,CHKMED                                                   
         SCKW  SOCRATES,CHKSOC                                                  
         SCKW  FOLIO,CHKFOLIO                                                   
         SCKW  PRISM,CHKPRISM                                                   
         SCKW  PRINTER,CHKPRNTR,(A,P)                                           
         SCKW  CLOAKED,CLOAKED                                                  
         SCKW  ECHO,CHKECHO,A                                                   
         SCKW  NOECHO,CHKNECHO,A                                                
         SCKW  SCANCASE,V(SCNNOOP)                                              
         SCKW  /,CHKKEY,P                                                       
         SCKW  ,,END                                                            
*-                                                                              
*-       List of commands allowed before logon.                                 
*-                                                                              
SIGNPSH  PUSHRTN SIGNCMD                                                        
SIGNCMD  SCKW  XENROLL,V(MACRO),S                                               
         SCKW  ?,CHKHELP                                                        
         SCKW  HELP,CHKHELP,(A,S)                                               
         SCKW  INFO,CHKHELP,(A,S)                                               
         SCKW  SET,V(SET),S                                                     
         SCKW  SHOW,V(SHOW),(A,S)                                               
         SCKW  LOCATE,V(JESCMD),(A,S)  LOCATE                                   
         SCKW  XCTL,V(XCTL),S                                                   
         SCKW  WYLBUR,V(XCTL),S                                                 
         SCKW  DEVWYL,V(XCTL),S                                                 
         SCKW  TESTWYL,V(XCTL),S                                                
         SCKW  OLDWYL,V(XCTL),S                                                 
         SCKW  LOGOFF,LOGOFF,S                                                  
         SCKW  LOGOUT,LOGOFF,S                                                  
         SCKW  SIGNOFF,LOGOFF,S                                                 
         SCKW  LOGON,LOGON,S                                                    
         SCKW  LOGIN,LOGON,S                                                    
         SCKW  SIGNON,LOGON,S                                                   
         SCKW  STOP,LOGOFF,S       (As requested by RLIN)                       
         SCKW  ,,END                                                            
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKHELP -- Handle HELP/? response to initial Account prompt.                 
*                                                                               
*  NOTES:                                                                       
*  WINGS WILL NOT ALLOW DISK ACCESS WITHOUT AN ACCOUNT.                         
*  SO FOR NOW WE HARD CODE THIS LOGON HELP.  ANOTHER                            
*  APPROACH WOULD BE TO INIT A LOGON HELP MEMORY AREA                           
*  AT WYLBUR INIT TIME, THEN WE CAN READ MEMORY                                 
*  AT PRE-LOGON TIME WITHOUT AN ACCOUNT.                                        
*                                                                               
CHKHELP  PROC                                                                   
*        WINGS WILL NOT ALLOW ACOUNTLESS DISK ACCESS.                           
*         MSG   'LOGON HELP'                                                    
*         OSCINIT (R1),(R0)         OLD SCANNER COMPATABILITY                   
*         SCINIT (R1),(R0)                                                      
*         MVC   CPCMNM,=C'HEL'                                                  
*         VCALL HELP                Call HELP                                   
*                                                                               
*                                                                               
         COMMENT GIVE IN LINE HELP                                              
         IF    ^CVFRLG,BEGIN                                                    
         TCR                                                                    
         TSEG  'Enter your Data Center account.  '                              
         TSEG  'The account is a 6-character string in'                         
         TCR                                                                    
         TSEG  'the format "gg.uuu", that is, two '                             
         TSEG  'characters, a period, and three characters.'                    
         TCR                                                                    
         TCR                                                                    
         TSEG  'Other options:'                                                 
         TCR                                                                    
         TSEG  ' - Type FOLIO to search Folio '                                 
         TSEG  'if you don''t have a Forsythe account.'                         
         TCR                                                                    
         TSEG  ' - Type PRISM to enter Prism under general access.'             
         TCR                                                                    
         TSEG  ' - Type a terminal type, to set '                               
         TSEG  'your terminal type before logging on.'                          
         TCR                                                                    
         TSEG  ' - Type SHOW TERMINAL to see the '                              
         TSEG  'current terminal settings.'                                     
         TCR                                                                    
         TCR                                                                    
         TSEG  ' - Type LOGOFF to end your session.'                            
         TCR                                                                    
         TCR                                                                    
         TSEG  'To open a Forsythe account, contact the '                       
         TSEG  'Stanford Data Center''s Account'                                
         TCR                                                                    
         TSEG  'Services office, phone (415)723-4795.'                          
         TCR                                                                    
         TCR                                                                    
         TWRITE                                                                 
         END                                                                    
         ELSE  BEGIN                                                            
         TCR                                                                    
         TSEG  'Type your RLIN account number; for example, '                   
         TSEG  'xx.yyy',,CR                                                     
         TSEG  'You will then be prompted for your password.'                   
         TCR                                                                    
         TCR                                                                    
         TSEG  'At the prompt "Command>"',,CR                                   
         TSEG  '*  type EUREKA for Eureka search access to RLIN BIB'            
         TSEG  ' and CitaDel files',,CR                                         
         TSEG  '*  type CALL RLIN (AUT) for RLIN Authority file '               
         TSEG  'access',,CR                                                     
         TSEG  '*  type CALL RLIN (CAT) for the RLIN Cataloging '               
         TSEG  'access',,CR                                                     
         TSEG  '*  type CALL RLIN (CON) for RLG Conspectus Online '             
         TSEG  'access',,CR                                                     
         TSEG  '*  type CALL RLIN (ILL) for RLIN Interlibrary Loan '            
         TSEG  'access',,CR                                                     
         TSEG  '*  type CALL RLIN (SCI) for SCIPIO Arts Sales Catalog '         
         TSEG  'access',,CR                                                     
         TCR                                                                    
         TSEG  'For information about obtaining an account to search '          
         TSEG  'the online resources',,CR                                       
         TSEG  'of The Research Libraries Group, Inc., contact the '            
         TSEG  'RLIN Information Center:',,CR                                   
         TSEG  'Phone: 800-537-RLIN',,CR                                        
         TSEG  'E-mail: bl.ric@rlg.org',,CR                                     
         TSEG  'Fax: 415.964.0943 (Attn:RIC)',,CR                               
         TCR                                                                    
         TSEG  'or visit our Web site at http://www.rlg.org'                    
         TCR                                                                    
         TCR                                                                    
         TWRITE                                                                 
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       It could be a terminal id.                                             
*-                                                                              
CHKTRM   PROC                                                                   
         SCUPTOK R15                                                            
*-                                                                              
*-       Check terminal type before looking for terminal id.                    
*-                                                                              
CHKTTY   LABEL ,                   TERMINAL NAME                                
         XPUSH R15,R1                                                           
         VCALL CHKTTYPE            Is it a terminal name?                       
         IF    Z,'XPOP R15,R1; B CHKTRMID'  No                                  
         VCALL SETTTYPE            Set related info                             
         XPOP  R15,R1                                                           
         CLEAR R15                                                              
         B     CHKTDONE                                                         
*                                                                               
*  CATCH-ALL, FREE TOKEN MUST BE ONE OF THE FOLLOWING:                          
*        1. TERMINAL ID,  2. TERMINAL TYPE, 3. NAME  4. ERROR                   
*                                                                               
CHKTRMID LABEL ,                   TERMINAL ID                                  
         IF    (CPSTID,NZ),CHKUID                                               
         IF    (R0,NE,3),CHKUID                                                 
         IF    ((@R15,LT,'A'),OR,(@R15,GT,'Z')),CHKUID                          
         IF    ((@R15+1,LT,'0'),OR,(@R15+1,GT,'9')),CHKUID                      
         IF    ((@R15+2,LT,'0'),OR,(@R15+2,GT,'9')),CHKUID                      
         MVC   CPDOUB,@R15         Save terminal id for loop below              
         LOOP  BEGIN                                                            
         MVC   CPSTID,CPDOUB       Set terminal ID                              
         IF    (CPSTID,EQ,'V'),EXIT  Temp/VT100, exit                           
         IF    CPSTPDP,'MVC CPSTTYPE,=CL8"TEK4023"'                             
         IF    (CPSTID,EQ,'U'),'MVC CPSTTYPE,=CL8"TVI912"'                      
         IF    (CPSTID,EQ,'H'),'MVC CPSTTYPE,=CL8"HP2621"'                      
****     IF    (CPSTID,EQ,'V'),'MVC CPSTTYPE,=CL8"VT100"'                       
*                                                                               
         VCALL UPDCPSIB                                                         
         UNTIL Z                                                                
         END                                                                    
         CLEAR R15                                                              
         B     CHKTDONE                                                         
*                                                                               
*  Look for userid                                                              
*                                                                               
CHKUID   LABEL ,                   USER ID                                      
         LA    R15,CPCACCT         Put resolved account here                    
         VCALL GETACCT             Convert userid to account                    
         BZ    CHKTDONE            Got it, scram                                
*                                                                               
*  Look for "name" or 'name'                                                    
*                                                                               
CHKNAME  LABEL ,                   NAME                                         
         IF    (R0,POS),BEGIN                                                   
         IF    ((@R1,EQ,'"'),OR,(@R1,EQ,'''')),BEGIN                            
         SCDQUOTE CPSNAME,L'CPSNAME                                             
         CLEAR R15                                                              
         B     CHKTDONE                                                         
         END                                                                    
         END                                                                    
         ACALL CHKSPID             Check for special ids                        
         IF    (R15,Z),CHKTDONE    Got one                                      
*                                                                               
         COMMENT                   ANY THING THAT FALLS THRU                    
         ACALL SIGNERR             IS AN ERROR,                                 
         BOMB  ,                   NO RETURN FROM SIGNERR                       
*                                                                               
CHKTDONE LABEL ,                                                                
         PEND                                                                   
*                                                                               
CHKMED   PROC                                                                   
         MVC   CPCACCT,=C'XPSMA'   Medline account                              
         PEND                                                                   
*                                                                               
CHKSOC   PROC                                                                   
         MVC   CPCACCT,=C'SCNEC'   EC.SCN in socrates account                   
         PEND                                                                   
*                                                                               
CHKFOLIO PROC                                                                   
         MVC   CPCACCT,=C'FOLEN'   EN.FOL is the Folio account                  
         PEND                                                                   
*                                                                               
CHKPRISM PROC                                                                   
         MVC   CPCACCT,=C'PRMHQ'   HQ.PRM is the Prism account                  
         PEND                                                                   
*                                                                               
CHKPRNTR PROC                                                                   
         SCUPTOK R15                                                            
         VCALL CHKPRT              Is it a printer name?                        
         IF    Z,BEGIN             No...                                        
         TSEG  (R1),(R0),B                                                      
         ERROR 'is not a valid printer name.'                                   
         END                                                                    
*                                                                               
         VCALL SETPRT              Set printer info                             
*                                                                               
         SCINFO ,                  Info after printer name                      
         VCALL LRTRIM              Trim blanks                                  
         VCALL SETPARM             Set exec PARM                                
         SET   CPSSFPRT            Printer session                              
         SET   CPSSFNAR+CPSSFNAT+CPSSFPS  Pseudo-session                        
         SET   CPSSFSEC            Session is secure                            
         SET   CPFSGNCM            First command already set            ***ISU  
         LA    R15,4               Scan done,,                                  
         PEND                                                                   
*                                                                               
CLOAKED  PROC                                                                   
         SET   CPSSFPS                                                          
         PEND                                                                   
*                                                                               
SCANCASE PROC                                                                   
         AIF   (NOT &KEYUPLO).KEYUPL1                                           
         SET   CPFSUPLO            Don't upcase rest                            
.KEYUPL1 ANOP                                                                   
         LA    R15,4               NO MATCH                                     
         PEND                                                                   
*                                                                               
CHKKEY   PROC                                                                   
         SCUPTOK R1                                                             
         MVC   SIGNPASS,@R1                                                     
         PEND                                                                   
*                                                                               
CHKECHO  PROC                                                                   
         LOOP  BEGIN                                                            
         CLEAR CPSFNECH                                                         
         VCALL UPDCPSIB                                                         
         UNTIL Z,END                                                            
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CHKNECHO PROC                                                                   
         LOOP  BEGIN                                                            
         SET   CPSFNECH                                                         
         VCALL UPDCPSIB                                                         
         UNTIL Z,END                                                            
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*  MISC SIGN ON ERROR                                                           
*                                                                               
SIGNERR  PROC                                                                   
*                                                                               
         IF    CVFRLG,BEGIN        RLG version...                               
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.',,CR                                                 
         TSEG  'Enter an RLIN account in the form GG.UUU, '                     
         TSEG  'enter ? for more ',,CR                                          
         TSEG  'information, or enter STOP to leave the '                       
         ERROR 'RLIN system.'                                                   
         END                                                                    
*                                                                               
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid.  Type ? for more information.'                       
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         QLTORG                                                                 
         EJECT                                                                  
*-                                                                              
*-       User is now officially logged on, set defaults and start               
*-         him up.                                                              
*-                                                                              
LOGFIN   BASE                                                                   
FINBASE  LABEL ,                                                                
         MVC   CPSACCT,CPCACCT     Set session account                          
         LR    R5,R1                                                            
         USING SIGNWA,R5                                                        
         LA    R6,SIGNKWR                                                       
         WITH  (KWR,R6),BEGIN                                                   
*-                                                                              
*-       Re-initialize stuff that might have gotten prematurely                 
*-         set up during the logon process.                                     
*-                                                                              
         VCALL PROTFREE            Reinit RACF settings                         
*                                                                               
         L     R1,CVCURJCB         Our JCB ptr                                  
         VCALL FREEBILL            Reinit charges                               
*                                                                               
         MVC   CPNAME,KWRNAME      Save our user name                           
         IF    (CPSNAME,Z),'MVC CPSNAME,KWRNAME'  Default name                  
*                                                                               
         MVC   CPORVYL,CVORVSSN    Set default ORVYL name                       
         IF    (KWRORV,NZ),'MVC CPORVYL,KWRORV'  Use user's default             
*                                                                               
         LA    R1,KWR                                                           
         VCALL HOSTINIT            Set default net host and userid              
         END                                                                    
*                                                                               
         IF    (CPSPROJ,EQ,CVBLANKS),'CLEAR CPSPROJ'  Zot subgroup              
*-                                                                              
*-       Save our userid.                                                       
*-                                                                              
         CLEAR CPUID               Assume no userid                             
*                                                                               
         LA    R15,CPSACCT         Logged on account                            
         SETMSG CPUID               Put userid here                             
         VCALL GETUID              Get corresponding userid                     
         IF    (CPUID,Z),'CLEAR CPUID'  No userid                               
*-                                                                              
*-       The user has requested a special ORVYL on the                          
*-         JES JOBPARM card.                                                    
*-                                                                              
         IF    CPSTVIRT,BEGIN      Virtual terminal session...                  
         L     R15,CPSASCBP        Virtual job's ASCB ptr                       
         HASPSERV ORVYL,@R13,ASCB=(R15)  Get ORVYL name                         
         IF    (R15,NZ),EXIT       No haspserv, scram                           
         IF    (@R13,EQ,X'00'),EXIT  No ORVYL name, scram                       
         IF    (@R13,EQ,C' '),EXIT   No ORVYL name, scram                       
         MVC   CPORVYL,@R13        Set ORVYL name                               
         END                                                                    
*                                                                               
         IF    CVFRLG,BEGIN        RLG machine...                               
         IF    (CPORVYL,EQ,CVORVSSN),'MVC CPORVYL,CVRLGSSN'                     
         END                                                                    
*-                                                                              
*-       Set session information for a new user.                                
*-                                                                              
         MVC   CPSACCT,CPCACCT     Set session account                          
*                                                                               
         TCLEAR                                                                 
         IF    ^CVFVISTA,BEGIN     VISTA doesn't do this...                     
         TSEG  CPSSESS                                                          
         TCNTL CTLPSETS            Set session                                  
         FAIL  NZ,MILERR                                                        
         LR    R15,R0                                                           
         CEIL  R15,L'CPSINFO                                                    
         MOVE  R15,CPSINFO,@R1                                                  
         END                                                                    
*                                                                               
         VCALL CHECKSET            Set up for recovery                          
*                                                                               
         LA    R1,=CL8'LOGON'                                                   
         L     R15,CVCURJCB                                                     
         VCALL DOBILL              Write accounting records                     
*                                                                               
         SETMSG 'LOGON: '                                                       
         VCALL LOGSESS             Log session logon                            
*                                                                               
         IF    CPSTCONS,'CLEAR CPSFUPL'  Upper is console default               
*                                                                               
TCPSLP   INCR  R15,CPSUC           Kick update count                            
         TSEG  CPSOPT                                                           
         TCNTL CTLSETI             Set SIB options                              
         FAIL  NEG,MILERR                                                       
         IF    POS,'MVC CPSUC,CPSUC-CPS(R1); B TCPSLP'                          
*                                                                               
         MVC   CPACCTSV,CPSACCT    Saved user                                   
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),BEGIN                                                   
         MVC   JCBACCT,CPSACCT                                                  
         MVC   JCBANO,CPSANO                                                    
         CLEAR JCBBFPS+JCBBFNR                                                  
         IF    CPSSFPS,'SET JCBBFPS'                                            
         IF    CPSSFNAR,'SET JCBBFNR'                                           
         END                                                                    
*                                                                               
         ACALL INITDFLT            Set logon defaults                           
*-                                                                              
*-       Transfer control to a different WYLBUR (maybe).                        
*-                                                                              
         IF    CPFSGNML,BEGIN      Only if initial logon...             ***ISU  
         LA    R15,SIGNKWR                                                      
         WITH  (KWR,R15),'MVC SIGNSYS,KWRSYS'  WYLBUR system name               
         IF    (SIGNSYS,Z),'MVC SIGNSYS,=CL8"WYLBUR"'  Universal                
*                                                                               
         IF    (SIGNSYS,EQ,CVWYLSSN),EXIT  We are already here                  
*                                                                               
         IF    CPFSGNCM,EXIT       Don't switch if 1st cmd provided             
*-                                                                              
*-       "CPLGFSYS" is the name of the subsystem to receive control             
*-          after we have logged off our subsystem.  For example,               
*-          TESTWYL.                                                            
*-                                                                              
         LA    R1,SIGNSYS                                                       
         VCALL SYSCHECK            Is this subsystem running?                   
         IF    NZ,EXIT             No, forget it                                
*                                                                               
         SET   CPFSYSMV            Transfer control w/o msgs                    
         MVC   CPLGFSYS,SIGNSYS    Set new subsys name                          
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Log off our WYLBUR                     
         B     CVNEXT              Scram                                        
         END                                                                    
*-                                                                              
*-       Update our terminal type information is a terminal type                
*-          has already been set.                                               
*-                                                                              
         IF    (CPSTTYPE,Z),BEGIN  No terminal type...                          
         IF    CPSTVTAM,'MVC CPSTTYPE,=CL8"3278M2"'                             
         IF    (CPSFE,EQ,'11R'),'MVC CPSTTYPE,=CL8"RLG40"'                      
         END                                                                    
*                                                                               
         IF    (CPSTTYPE,NZ),BEGIN  Set terminal type info...                   
         LA    R15,CPSTTYPE        Terminal type                                
         VCALL CHKTTYPE            Look up name                                 
*                                                                               
         IF    NZ,'VCALL SETTTYPE'  Known terminal type                         
*                                                                               
         ELSE  BEGIN               Unknown...                                   
         CLEAR CPSTTYPE,CPSTCLS    Reset terminal info                          
         VCALL UPDCPSIB            Update SIB                                   
         END                                                                    
         END                                                                    
***                                                                             
***  The following is for "real" terminals only.                                
***                                                                             
         IF    ^CPSSFPS,BEGIN      Real terminals only...                       
*-                                                                              
*-       Report previous unsuccessful logon attempts.                           
*-                                                                              
         LA    R6,SIGNKWR                                                       
         WITH  (KWR,R6)                                                         
*                                                                               
         IF    (KWRXCNT,NZ),BEGIN  We had some...                               
         IF    (KWRXCNT,GE,4),BEGIN  Four or more attempts...                   
         TSEG  X'2F'               (Bell)                                       
         TSEG  'Security Note:',,CR                                             
         TSEG  '  There were '                                                  
         TNUM  LC:KWRXCNT                                                       
         TSEG  ' unsuccessful logon attempts '                                  
         TSEG  'since your last session.',,CR                                   
         TSEG  '  The last attempt was on '                                     
         L     R0,KWRXTIM                                                       
         CLEAR R1                                                               
         XPUSH ,,32,PTR=R15                                                     
         VCALL FMTXDATE                                                         
         TSEG  (R1),(R0)           Full time and date                           
         XPOP  ,,32                                                             
         TSEG  '.',,CR                                                          
         TSEG  '  Contact security '                                            
         SETMSG 'at (650) 725-7516'                                             
         IF    CVFRLG,'SETMSG "at 1-800-537-7546"'  RLIN info center            
         IF    (CVMACHID,EQ,'SYSC '),'CLEAR R0'                                 
         TSEG  (R1),(R0)                                                        
         TSEG  ' if you need more information.',,CR                             
         TCR                                                                    
         TWRITE                                                                 
         END                                                                    
*                                                                               
         LOOP  BEGIN                                                            
         CLEAR KWRXCNT,KWRXTIM     Reset fields                                 
         KWRITE KWR                Update keyword record                        
         UNTIL KWRHFL.KWRHFUOK     Keep trying until update works               
         END                                                                    
         CLEAR KWRHFL.KWRHFUOK                                                  
         END                                                                    
*-                                                                              
*-       Create first command.                                                  
*-                                                                              
         IF    KWROFL.KWRFSEC,'SET CPFSEC'  Secure mode                         
         END                                                                    
*                                                                               
         IF    CPFSGNCM,AUTCMD     First command already provided       ***ISU  
*                                                                               
         IF    CPSSFNAT,CVNEXT     No auto processing, scram                    
*                                                                               
         LA    R6,SIGNKWR                                                       
         USING KWR,R6                                                           
*                                                                               
         IF    (KWRCMD,NZ),BEGIN   First command...                             
         SETMSG KWRCMD                                                          
         VCALL LRTRIM                                                           
         STH   R0,CPCMDLEN         Set command length                           
         LR    R15,R0                                                           
         MOVE  R15,CPCMD,@R1       Set command text                             
         B     AUTCMD              Go do first command                          
         END                                                                    
*-                                                                              
*-       Old style auto first command.                                          
*-                                                                              
         IF    KWRTFL.KWRTFAE,BEGIN  EXEC FROM command...                       
         IF    KWRTFL2.KWRTFANE,BEGIN  Newexec...                               
         TSEG  'EXEC FROM LIB#NEWLOGON PUBLIC '                                 
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  'EXEC FROM #LOGON QUIET '                                        
         IF    KWRTFL2.KWRTFAPE,BEGIN  Pubexec...                               
         TSEG  'USER PUB',,B                                                    
         IF    (CPSGRP,EQ,'B'),'TSEG "GROUP BP",,B'  Rlin                       
         END                                                                    
         END                                                                    
*                                                                               
         SETMSG 'PARM='''                                                       
         IF    (^KWRTFL.KWRTFAN,AND,(KWRNEWDT,LT,CVNWDATE)),BEGIN               
         TSEG  (R1),(R0)                                                        
         TSEG  'NEWS'                                                           
         SETMSG ','                                                             
         END                                                                    
         IF    (^KWRTFL.KWRTFAM,AND,KWRSFL.KWRSFMAI),BEGIN                      
         TSEG  (R1),(R0)                                                        
         TSEG  'NEWMAIL'                                                        
         SETMSG ','                                                             
         END                                                                    
         IF    KWRSFL.KWRSFKEP,BEGIN                                            
         TSEG  (R1),(R0)                                                        
         TSEG  'KEPTMAIL'                                                       
         SETMSG ','                                                             
         END                                                                    
         IF    (^KWRTFL.KWRTFAR,AND,KWRAFL.KWRAFASV),BEGIN                      
         TSEG  (R1),(R0)                                                        
         TSEG  'RECOVERY'                                                       
         SETMSG ','                                                             
         END                                                                    
         IF    (R0,EQ,1),BEGIN                                                  
         TSEG  ''''                                                             
         END                                                                    
         END                                                                    
         L     R15,CPSEGLENF                                                    
         STH   R15,CPCMDLEN                                                     
         L     R1,CPSEGLOC                                                      
         MOVE  R15,CPCMD,@R1                                                    
         TCLEAR                                                                 
*                                                                               
         IF    CPSSFNAT,CVNEXT     No auto processing                           
*                                                                               
*                                                                               
*   The following SHOW SPACE command was deleted on 6/16/95                     
*   by M. Durket. WYLBUR no longer blocks sequential saves                      
*   and recovery because it uses WINGS. Nightly accounting                      
*   now will handle the overspace issue.                                        
*                                                                               
*        LA    R0,4                Conditional SHOW SPACE                       
*        LA    R1,KWR              R1 -> KWR                                    
*        VCALL EXIT3               LOCAL exit - show space                      
*                                                                               
         AIF   (&SETLTIM).LTIME1A                                               
         LA    R1,KWR                                                           
         VCALL LTIMDISP                                                         
         AGO   .LTIME1B                                                         
.LTIME1A ANOP                                                                   
         IF    KWRTFL.KWRTFAL,BEGIN  Auto ltime...                              
         LA    R1,KWR                                                           
         VCALL LTIMDISP                                                         
         END                                                                    
.LTIME1B ANOP                                                                   
*                                                                               
         L     R14,KWRKTIM         Time/date of last kw change                  
         CLEAR R15                                                              
         SRDL  R14,12+6            .000001*64 of a sec                          
         D     R14,=A(1000000/64*60*60*24)  No. of days since 1900              
         STCK  CPDOUB                                                           
         LM    R2,R3,CPDOUB        Current time/date                            
         SRDL  R2,12+6                                                          
         D     R2,=A(1000000/64*60*60*24)                                       
         SR    R3,R15              No. of elapsed days                          
*                                                                               
         CLEAR R4                  Clear work reg                               
         IC    R4,KWRIFL           Load privs                                   
         N     R4,=AL4(255-KWRIFVAL)  Mask off unwanted bit                     
         IF   ((R3,GE,&FPDAYS),AND,(R4,NZ)),'LA R1,KWR; VCALL FORCPASS'         
*                                                                               
         LA    R15,180             Always show ktime if over 180 days           
         IF    KWRTFL.KWRTFAK,'LA R15,14'  Auto ktime--over 14 days             
         IF    (R3,GE,R15),'LA R1,KWR; VCALL KTIMDISP'                          
*                                                                               
         IF    KWRTFL.KWRTFAN,BEGIN  Auto news...                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'  Reset attn                           
         SET   CPFVER              Verify                                       
         LA    R0,1                Show news items & prompt for text            
         L     R1,KWRNEWDT         Last date                                    
         VCALL NEWSDISP                                                         
         LR    R4,R1                                                            
         IF    Z,'TSEG CVBLANKS,1,WR'  Skip a line after news                   
         IF    (R4,EQ,KWRNEWDT),EXIT                                            
         LOOP  BEGIN                                                            
         ST    R4,KWRNEWDT         Update date read                             
         KWRITE KWR                                                             
         UNTIL KWRHFL.KWRHFUOK,END  Stop when rewrite works                     
         CLEAR KWRHFL.KWRHFUOK                                                  
         END                                                                    
*                                                                               
*                                                                               
         IF    (KWRTFL.KWRTFAM,AND,KWRSFL.KWRSFMAI),BEGIN  Mail...              
         LC    R15,CVMACH                                                       
         EX    R15,'TM KWRMPRIM,0'  Can we display mail?                        
         IF    Z,EXIT              No, wrong machine, forget it                 
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'  Reset attn                           
         SET   CPFVER              Verify                                       
         MVC   CPCACCT,CPSACCT                                                  
         LA    R15,KWR                                                          
         CLEAR R0,R1               Options                                      
         VCALL MAILDISP                                                         
         IF    NEG,BEGIN           Attn aborted reading mail...                 
         TSEG  'Mail not displayed.',,CR                                        
         END                                                                    
         TSEG  CVBLANKS,1,WR                                                    
         END                                                                    
*                                                                               
*                                                                               
         IF    KWRAFL.KWRAFASV,BEGIN  Active saved...                           
         LOOP  BEGIN                                                            
         CLEAR KWRAFL.KWRAFASV     Turn off save bit                            
         KWRITE KWR                Rewrite password record                      
         UNTIL KWRHFL.KWRHFUOK,END  Stop when rewrite works                     
         CLEAR KWRHFL.KWRHFUOK                                                  
         IF    KWRTFL.KWRTFAR,BEGIN  Auto recovery...                           
         CLEAR CPSCNFLG                                                         
*         MSG   'ACTIVE'                                                        
*         OSCINIT (R1),(R0)                                                     
         SCINIT 'ACTIVE RECOVERY MULTIPLE'                                      
         MVC   CPCMDRTN,=A(RCVYUSE)  Return here                                
         MVC   CPCMNM,=C'USE'                                                   
         SET   CPFAUSE             Auto use in progress                         
         VCALL USE                                                              
RCVYUSE  BASE                                                                   
         BASE  FINBASE                                                          
         CLEAR CPFAUSE             Reset                                        
         IF    (CPATTEXT,NZ),BEGIN  We have Actives...                          
         TSEG  'ACTIVE file(s) recovered.',,WR                                  
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG 'ACTIVE saved by recovery last session.',,WR                      
         END                                                                    
         END                                                                    
         DROP  KWR                                                              
*                                                                               
AUTCMD   TWRITE                                                                 
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
*                                                                               
         SETMSG CPCMD,LH:CPCMDLEN                                               
         IF    (R0,Z),CVNEXT       No command to execute                        
         VCALL EDTCOM                                                           
         DC    H'0'                (No return)                                  
*                                                                               
         DROP  R5                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GREET -- Local routine to write initial logon messages.                      
*                                                                               
GREET    PROC                                                                   
         IF    ^CPFGREET,BEGIN                                                  
         IF    (CPSTPDP,OR,CPSTASYN),'TSEG X"15"'                               
         TSEG  'Connecting to port '                                            
         TSEG  CPSFE,,T                                                         
         TSEG  '-'                                                              
         TSEG  CPSPORT,,T                                                       
         TCR                                                                    
         TCR                                                                    
*                                                                               
         SETMSG '&GREET'                                                        
*                                                                               
         AIF   ('&INSTALN' NE 'STANFORD').NOSGR                                 
         IF    (CVMACHID,EQ,'RLG '),BEGIN                                       
         TSEG  'Welcome to RLIN.  '                                             
         SETMSG 'Enter ? for information on access to RLIN.'                    
         END                                                                    
*                                                                               
         ELSEIF (CVMACHID,EQ,'SYSC '),BEGIN                                     
         TSEG  'Welcome to the '                                                
         TSEG  'Stanford University Hospital System (SUH/'                      
         TSEG  CVMACHID,,T                                                      
         SETMSG ').'                                                            
         END                                                                    
*                                                                               
         ELSEIF (CVMACHID,EQ,'SYSD '),BEGIN                                     
         TSEG  'This is the Development System ('                               
         TSEG  CVMACHID,,T                                                      
         SETMSG ').'                                                            
         END                                                                    
*                                                                               
         ELSEIF (CVMACHID,EQ,'SYSG '),BEGIN                                     
         TSEG  'This is the RLG Development System ('                           
         TSEG  CVMACHID,,T                                                      
         SETMSG ').'                                                            
         END                                                                    
*                                                                               
         ELSEIF (CVMACHID,EQ,'SYSH '),BEGIN                                     
         TSEG  'Welcome to the Y2K test system ('                               
         TSEG  CVMACHID,,T                                                      
         SETMSG ').'                                                            
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  '                     Welcome '                                  
         TSEG  'to Stanford University'                                         
         TCR                                                                    
         TSEG  '                Information '                                   
         TSEG  'Technology Systems && Services'                                 
         TCR                                                                    
         TSEG  '                     Forsythe '                                 
         SETMSG 'Hall Operations Center'                                        
         END                                                                    
.NOSGR   ANOP                                                                   
*                                                                               
         TSEG  (R1),(R0)                                                        
         TCR                                                                    
         TCR                                                                    
         TWRITE                                                                 
*                                                                               
         ACALL DOLOGMSG            Write msg-of-day, etc.                       
*                                                                               
         SET   CPFGREET                                                         
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CKACCT  -- Routine to check validity of account for logon.                   
*    (Returns to caller only if account is ok to use.)                          
*                                                                               
*    On entry:                                                                  
*      R1 - SIGNWA ptr                                                          
*                                                                               
CKACCT   PROC                                                                   
         LR    R6,R1                                                            
         USING SIGNWA,R6                                                        
         LA    R5,SIGNKWR                                                       
         USING KWR,R5                                                           
*-                                                                              
*-       Now read the password record.                                          
*-                                                                              
         CLEAR KWR                                                              
         MVC   KWRGRP,CPCGRP       "gg"                                         
         MVC   KWRUSER,CPCUSER     "uuu"                                        
         KREAD KWR                 (calls KWIO in TERM)                         
         IF    CPSTCONS,'SET KWRIFL.KWRIFOPR'  opr privs if cons                
         IF    ^KWRHFL.KWRHFCK,BEGIN  If no key file, bypass read               
         IF    CPSTCONS,'SET CPSFOPR; B CHKAEXIT'  a-ok for console             
         TSEG  'No account file, password not checked.',,WR                     
         B     CHKAEXIT                                                         
         END                                                                    
         SPACE ,                                                                
         IF    ^KWRIFL.KWRIFVAL,BEGIN                                           
*        Not valid key entry - check whether user knows word                    
         IF    ((CVWORD,NZ),AND,^CPFWORD),CVNEXT                                
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         TSEG  ' does not exist.',,WR                                           
         IF    CPSTCONS,EXIT       Nothing stops a console                      
         INCR  R15,CPLGNCT         Count logon attempt                          
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         MVC   CPSTACF,KWRTACF     Save account flags                           
*                                                                               
         MVC   CPSPFL,KWRPFL       Privilege flags                              
*                                                                               
         IF    ^CPSSFPS,BEGIN      Flat rate for real trms only...              
         IF    ((KWRSOF,NE,'2'),OR,                                    *        
               ((KWRSOF,EQ,'2'),AND,(KWRAR,EQ,'0')),OR,                *        
               ((KWRSOF,EQ,'2'),AND,(KWRAR,EQ,'9'))),BEGIN                      
         SET   CPFFLAT             Flat rate for non-prime time                 
         END                                                                    
         END                                                                    
*                                                                               
         MVC   CPSKIFL,KWRIFL                                                   
         MVC   CPSKAFL,KWRAFL                                                   
         MVC   CPSIDMAX,KWRNTDEF   Set idle session (notime) limit              
         AIF   ('&INSTALN' NE 'STANFORD').NOSOF2A                               
         MVC   CPSOF,KWRSOF        Save source of funds code                    
         MVC   CPAR,KWRAR          Save accounts receivable code                
.NOSOF2A ANOP                                                                   
*                                                                               
         IF    (CPSACCT,NZ),HAVEDO                                              
*                                                                               
         IF    ((CVWORD,NZ),AND,^CPFWORD),BEGIN  Is it a good-guy...            
         IF    (CPSFSPR+CPSFOPR,Z),CVNEXT  No, scram                            
*                                                                               
         IF    ^CVFSYST,CVNEXT     He must supply the word...                   
*                                                                               
         SET   CPFWORD             He knows the word                            
         END                                                                    
*-                                                                              
*-       Process Logon of account that doesn't require a password.              
*-                                                                              
         IF    KWROFL.KWRFNPAS,BEGIN  No logon password needed...               
         SET   CPSSFNAR            Assume no recovery                           
         B     HAVEDO                                                           
         END                                                                    
*-                                                                              
*-       Process normal logon (get password, etc.).                             
*-                                                                              
         LA    R2,2                Retry count                                  
         IF    (SIGNPASS,NZ),BEGIN  Password already entered...                 
         DELAY                     Wait a bit ...                               
         IF    (KWRPASS,EQ,SIGNPASS),HAVEDO                                     
         LA    R15,SIGNPASS                                                     
         B     SIGNKNO                                                          
         END                                                                    
*                                                                               
* If Kerberos authentication has been sent, check the authenticator             
* against the KLOGIN list for the account. If that doesn't succeed,             
* then abort.                                                                   
*                                                                               
         IF    (CPAUTHID,NZ),BEGIN                                              
         VCALL KLOGINCK                                                         
         IF    (R15,Z),HAVEDO                                                   
         END                                                                    
*                                                                               
SIGNKWL  VCALL READPW                                                           
         BZ    CVNOACTN            (ATTENTION)                                  
         DELAY                     Wait a bit ...                               
         IF    (KWRPASS,EQ,@R15),HAVEDO                                         
*                                                                               
SIGNKNO  XPUSH R2,R3                                                            
         LH    R2,CPCMDLEN                                                      
         XPUSH ,,L'CPCMD,PTR=R3                                                 
         MVC   @R3(L'CPCMD),CPCMD  Save last command                            
*                                                                               
         MVC   CPCMDLEN,=AL2(34+18)                                             
         MVC   CPCMD(34),=C'Logon attempted for account ??.???'                 
         MVC   CPCMD+34(18),=C' password 12345678'                              
         MVC   CPCMD+28(2),CPCGRP                                               
         MVC   CPCMD+31(3),CPCUSER                                              
         MVC   CPCMD+43(8),@R15                                                 
         LA    R0,SMFWSECL                                                      
         VCALL LOGKWR              Do security logging                          
*                                                                               
         STH   R2,CPCMDLEN                                                      
         MVC   CPCMD,@R3           Restore last command                         
         XPOP  ,,L'CPCMD                                                        
         XPOP  R2,R3                                                            
*                                                                               
*                                                                               
         LOOP  BEGIN                                                            
         INCR  R15,KWRXCNT         Kick logon attempt counter                   
         IF    (KWRXCNT,Z),'MVI KWRXCNT,X"FF"'  Max attempts                    
         VCALL LOCALTOD                                                         
         ST    R0,KWRXTIM          Save time/date of attempt                    
         KWRITE KWR                                                             
         UNTIL KWRHFL.KWRHFUOK     Loop back until rewrite works                
         END                                                                    
         CLEAR KWRHFL.KWRHFUOK                                                  
*                                                                               
         TSEG  'Incorrect Password'                                             
         IF    (R2,NZ),'DECR R2; TSEG ", Re-enter.",,WR; B SIGNKWL'             
         TSEG  '.',,WR                                                          
         B     CVNEXT                                                           
*                                                                               
HAVEDO   LC    R15,KWRMACC         Machine he is allowed on                     
         EX    R15,'TM CVMACH,0'   Can he use this system?                      
         IF    Z,BEGIN             Not allowed on this system...                
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         TSEG  ' can''t be used on the '                                        
         TSEG  CVMACHID,,TB                                                     
         TSEG  'machine.',,CR                                                   
         B     CVABORT                                                          
         END                                                                    
*                                                                               
         IF    (KWRAFL.KWRAFVAL*KWRAFTOK,^ONES),BEGIN                           
         IF    ((CVWORD,NZ),AND,^CPFWORD),CVNEXT                                
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER,,B                                                       
         SETMSG 'does not have terminal access.'                                
         IF    ^KWRAFL.KWRAFVAL,BEGIN  Suspended...                             
         SETMSG 'suspended.'                                                    
         IF    (KWRSTAT,EQ,KWRSEXP),'SETMSG "has expired."'                     
         IF    (KWRSTAT,EQ,KWRSOVB),'SETMSG "is over budget."'                  
         TSEG  (R1),(R0)                                                        
         TSEG  '  Contact: ITSS Account Services at (650) 723-4795 '            
         SETMSG '  or send email to GB.ACC@forsythe'                            
         END                                                                    
         ABORT (R1),(R0)                                                        
         END                                                                    
*                                                                               
         IF    (KWRHRSAL,NZ),BEGIN  Hour limit account...                       
         TSEG  'This account has used '                                         
         LH    R2,KWRHRSUS                                                      
         TNUM  (R2)                                                             
         TSEG  ' of '                                                           
         TNUM  LH:KWRHRSAL                                                      
         TSEG  ' hours.'                                                        
*                                                                               
         IF    (R2,GE,KWRHRSAL),BEGIN                                           
         ABORT '  Account limit exceeded.'                                      
         END                                                                    
*                                                                               
         TWRITE                                                                 
         CLEAR R0                                                               
         L     R15,CVTPTR                                                       
         WITH  (CVT,R15),'L R15,CVTUSER'                                        
         IF    ((R15,NZ),AND,(@R15,EQ,'SCIP')),BEGIN                            
         WITH  (USERCVT,R15),'IC R0,USEBLOCK'  Current time-block               
         END                                                                    
         IF    (R0,Z),BEGIN        Day block...                                 
         IF    KWRTACF.KWRTNDAY,EXIT  Skip confusing msg if ng                  
         CLEAR CPFFLAT             Reset flat rate                              
         END                                                                    
         END                                                                    
*                                                                               
         IF    KWRTACF.KWRTNDAY,BEGIN  No day time access...                    
         CLEAR R0                                                               
         L     R15,CVTPTR                                                       
         WITH  (CVT,R15),'L R15,CVTUSER'                                        
         IF    ((R15,NZ),AND,(@R15,EQ,'SCIP')),BEGIN                            
         WITH  (USERCVT,R15),'IC R0,USEBLOCK'  Current time-block               
         END                                                                    
         IF    (R0,NZ),EXIT        Not the day block, scram                     
         TSEG  'Account',,B                                                     
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER,,B                                                       
         ABORT 'is not valid for the day block.'                                
         END                                                                    
         EJECT                                                                  
*-                                                                              
*-       If multiple logons for single account,                                 
*-       Ask user if ok not to recover active file.                             
*-       (we do not recover or ask to recover psuedo sessions)                  
*-                                                                              
         IF    ^CPSSFPS,BEGIN      If pseudo session, skip recovery             
         IF    CVFVISTA,EXIT       Forget it if VISTA                           
         CLEAR R3                  R3 - flag, 0 not mult, 1 if mult             
         L     R4,CVFJCB                                                        
         WITH  (JCB,R4),BEGIN                                                   
         WHILE (R4,LE,CVLJCB),BEGIN   Look for duplicate sessions               
         IF    (CPCACCT,EQ,JCBACCT),BEGIN                                       
         COMMENT , We do not care if other ses is pseudo or non-rcvy            
         IF    (^JCBBFPS,AND,^JCBBFNR),BEGIN                                    
         LA    R3,1                                                             
         END                                                                    
         END                                                                    
         LA    R4,JCBNEXT                                                       
         END                                                                    
         END   , WITH (JCB,R4)                                                  
         COMMENT ,                 R3=0 if not multiple session                 
*                                                                               
         COMMENT                   If mult ses., ask to not recover             
         IF    (R3,NZ),BEGIN                                                    
         TSEG  'Account',,B                                                     
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER,,B                                                       
         TSEG  'is already logged on.',,WR                                      
         IF    ^KWROFL.KWRFMULT,CVABORT                                         
         TSEG  'No ACTIVE file recovery will be possible',,B                    
         TSEG  'for this session.',,WR                                          
         IF    ^KWROFL.KWRFMNPR,BEGIN  Prompt...                                
         SETMSG 'Proceed'                                                       
         VCALL YESREQ                                                           
         END                                                                    
         SET   CPSSFNAR            No recovery for us                           
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   Get unique user number,                      
         ACALL GETUSRNO            (this can be a bit complicated!)             
         ST2   R15,CPSANO          Save user number                             
*                                                                               
         COMMENT                   Update JCB now, avoid non-unique             
         COMMENT                   user numbers                                 
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),BEGIN                                                   
         MVC   JCBACCT,CPCACCT                                                  
         MVC   JCBANO,CPSANO                                                    
         CLEAR JCBBFPS+JCBBFNR                                                  
         IF    CPSSFPS,'SET JCBBFPS'                                            
         IF    CPSSFNAR,'SET JCBBFNR'                                           
         END   , WITH (JCB,R4)                                                  
*                                                                               
CHKAEXIT PEND                                                                   
         DROP  R6                                                               
         DROP  R5                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  GET UNIQUE NUMBER FOR DUPLICATE SESSION                                      
*                                                                               
*  RETURNS:                                                                     
*        R15 - UNIQUE SESSION NUMBER FOR DUPLICATE LOGONS                       
*                                                                               
*  BY DUPLICATE LOGON, WE MEAN MULTIPLE SESSIONS FOR A                          
*  SINGLE ACCOUNT                                                               
*                                                                               
*  WE GET UNUSED SESSION NUMBER BY MARKING ALL USED SESSION                     
*  NUMBERS IN AN BYTE ARRAY THAT IS MAX NUMBER OF SESSIONS                      
*  LONG (PLUS A LITTLE BIT).  WE THEN FIND THE FIRST UNUSED                     
*  SESSION NUMBER BY LOOKING FOR THE FIRST ZERO IN THE ARRAY.                   
*                                                                               
*  FOR PSEUDO SESSIONS:                                                         
*  THE SESSION NUMBER OF '9663' SHOULD SIMPLY BE HIGHER                         
*  THAN THE MAXIMUM NUMBER OF USERS WE WOULD EVER HAVE.                         
*  SPIRES FOR SOME REASON WANTS A NUMBER THAT MODULO 64                         
*  IS 63 ...  9663 WORKS, BUT THERE IS NOTHING SPECIAL                          
*  ABOUT THIS NUMBER.  THE ARRAY SCHEME MENTIONED ABOVE                         
*  IS USED TO FIND UNIQUE PSUEDO SESSION NUMBERS ...                            
*  ...  9663, 9662, 9661, 9660, ETC ..                                          
*                                                                               
GETUSRNO PROC                                                                   
*                                                                               
         COMMENT GET MAX SESSIONS BYTES, AND ZERO THEM                          
         LH    R6,CVMAXSES                                                      
         LA    R6,@R6+32                                                        
         XPUSH ,,(R6),PTR=R5                                                    
         LR    R2,R5                                                            
         LH    R3,CVMAXSES                                                      
         CLEAR R15                                                              
         MVCL  R2,R14                                                           
*                                                                               
         COMMENT                   SESSION NUMBERS START AT 1                   
         COMMENT ,                 (EXCEPT FOR PSUEDO SESSIONS)                 
         IF    ^CPSSFPS,BEGIN                                                   
         L     R4,CVFJCB                                                        
         WITH  (JCB,R4),BEGIN                                                   
         COMMENT LOOP THRU JCB'S AND MARK ALL USED SESSION NO.S                 
         WHILE (R4,LE,CVLJCB),BEGIN                                             
         IF    (CPCACCT,EQ,JCBACCT),BEGIN                                       
         L2    R2,JCBANO                                                        
         IF    (R2,NZ),BEGIN                                                    
         DECR  R2                                                               
         END                                                                    
         AR    R2,R5                                                            
         MVI   0(R2),C'X'                                                       
         END                                                                    
         LA    R4,JCBNEXT                                                       
         END                                                                    
         END   , WITH (JCB,R4)                                                  
         L     R2,=F'1'                                                         
         LR    R3,R5                                                            
         WHILE (@R3,NE,0),BEGIN                                                 
         INCR  R3                                                               
         INCR  R2                                                               
         END                                                                    
         LR    R15,R2                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF PSUEDO, START AT 9663                     
         COMMENT                   AND DECREMENT                                
         IF    CPSSFPS,BEGIN                                                    
         L     R4,CVFJCB                                                        
         WITH  (JCB,R4),BEGIN                                                   
         COMMENT LOOP THRU JCB'S AND MARK ALL USED SESSION NO.S                 
         WHILE (R4,LE,CVLJCB),BEGIN                                             
         IF    (CPCACCT,EQ,JCBACCT),BEGIN                                       
         L     R2,=F'9663'                                                      
         L2    R3,JCBANO                                                        
         SR    R2,R3                                                            
         AR    R2,R5                                                            
         MVI   0(R2),C'X'                                                       
         END                                                                    
         LA    R4,JCBNEXT                                                       
         END                                                                    
         END   , WITH (JCB,R4)                                                  
         L     R2,=F'9663'                                                      
         LR    R3,R5                                                            
         WHILE (@R3,NE,0),BEGIN                                                 
         INCR  R3                                                               
         DECR  R2                                                               
         END                                                                    
         LR    R15,R2                                                           
         END                                                                    
*                                                                               
         XPOP  ,,(R6)                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKWORD -- Local routine to check to see if the user knows the               
*    secret word.                                                               
*                                                                               
*    Returns to caller only if the account is ok to use.                        
*                                                                               
CHKWORD  PROC                                                                   
         IF    ((CVWORD,NZ),AND,^CPFWORD),BEGIN  better ask...                  
         IF    CPSSFPS,EXIT        Never ask a pseudo session                   
         TSEG  CVWYLSSN,,TB                                                     
         TREAD 'access is restricted... '                                       
         IF    Z,BEGIN             Normal ending...                             
         IF    (CVFSYST,AND,CPSFSPR),CHKWEXIT  Good guys are allowed            
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         IF    (R15,Z),BEGIN                                                    
         SCUPTOK R1                                                             
         IF    (CVWORD,EQ,@R1),CHKWEXIT  They got it, scram                     
         END                                                                    
         END                                                                    
*-                                                                              
*-      The user couldn't get the correct secret word, kick him out.            
*-                                                                              
         CLEAR CPCACCT,CPSACCT     Clear any account                            
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Logging off                            
         B     CVNEXT              Logoff (eventually)                          
         END                                                                    
*                                                                               
CHKWEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKSPID -- Local routine to check for special userid                         
*             and set account and project appropriately.                        
*                                                                               
*    Input: R1, R0 contain id location and length.                              
*                                                                               
*    Output: R15 = 0 special id found                                           
*                = 4 special id not found                                       
*                                                                               
CHKSPWA  RECORD BEGIN                                                           
CHKSPAC  DS    CL(&LINESZ)                                                      
         END                                                                    
CHKSPID  PROC  CHKSPWA                                                          
*                                                                               
*  By request from Lynn McRae in an email dated July 9, 1998                    
*  we will allow the special ids followed by a project                          
*  field:                                                                       
*                                                                               
*        PRISM.project                                                          
*        SOCRATES.project                                                       
*        FOLIO.project                                                          
*        MEDLINE.project                                                        
*                                                                               
         IF    (R0,POS),BEGIN                                                   
*                                                                               
*  Upper case it for comparisons                                                
*                                                                               
         CEIL  R0,L'CHKSPAC                                                     
         LR    R15,R0                                                           
         MOVE  R15,CHKSPAC,@R1                                                  
         LA    R1,CHKSPAC                                                       
         VCALL TOUPPER                                                          
         LR    R15,R1                                                           
*                                                                               
         IF    ((R0,GE,5),AND,(@R15,EQ,'PRISM')),BEGIN                          
         MVC   CPCACCT,=C'PRMHQ'   HQ.PRM is the Prism account                  
         IF    ((R0,GT,6),AND,(@R15,EQ,'PRISM.')),BEGIN                         
         LR    R2,R0                                                            
         SH    R2,=H'6'                                                         
         CEIL  R2,L'CPSPROJ                                                     
         MVC   CPSPROJ,CVBLANKS                                                 
         MOVE  R2,CPSPROJ,@R15+6                                                
         END                                                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF ((R0,GE,8),AND,(@R15,EQ,'SOCRATES')),BEGIN                      
         MVC   CPCACCT,=C'SCNEC'   EC.SCN in socrates account                   
         IF    ((R0,GT,9),AND,(@R15,EQ,'SOCRATES.')),BEGIN                      
         LR    R2,R0                                                            
         SH    R2,=H'9'                                                         
         CEIL  R2,L'CPSPROJ                                                     
         MVC   CPSPROJ,CVBLANKS                                                 
         MOVE  R2,CPSPROJ,@R15+9                                                
         END                                                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF ((R0,GE,5),AND,(@R15,EQ,'FOLIO')),BEGIN                         
         MVC   CPCACCT,=C'FOLEN'   EN.FOL is the Folio account                  
         IF    ((R0,GT,6),AND,(@R15,EQ,'FOLIO.')),BEGIN                         
         LR    R2,R0                                                            
         SH    R2,=H'6'                                                         
         CEIL  R2,L'CPSPROJ                                                     
         MVC   CPSPROJ,CVBLANKS                                                 
         MOVE  R2,CPSPROJ,@R15+6                                                
         END                                                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF ((R0,GE,7),AND,(@R15,EQ,'MEDLINE')),BEGIN                       
         MVC   CPCACCT,=C'XPSMA'   Medline account                              
         IF    ((R0,GT,8),AND,(@R15,EQ,'MEDLINE.')),BEGIN                       
         LR    R2,R0                                                            
         SH    R2,=H'8'                                                         
         CEIL  R2,L'CPSPROJ                                                     
         MVC   CPSPROJ,CVBLANKS                                                 
         MOVE  R2,CPSPROJ,@R15+8                                                
         END                                                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         LA    R15,4               Not found                                    
         END                                                                    
*                                                                               
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         LA    R15,4               If R0 <= 0, id not found                     
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DOLOGMSG -- Local routine to write logon messages (if not                    
*    already written).                                                          
*                                                                               
DOLOGMSG PROC                                                                   
         IF    ^CPFLMSG,BEGIN                                                   
         IF    CVFTMIL,BEGIN       Test milten...                               
         TSEG  '*** Test MILTEN Version ***',,CR                                
         TCR                                                                    
         TWRITE                                                                 
         END                                                                    
*                                                                               
         CLEAR R0                                                               
         IF    (CVMSG,NZ),BEGIN    Message of the day...                        
         VCALL LOCALTOD                                                         
         LR    R2,R0                                                            
         L     R0,CVMSGCK                                                       
         CLEAR R1,R3                                                            
         XPUSH ,,30,PTR=R15                                                     
         VCALL FMTRCLCK            "hh:mm" or "hh:mm mm/dd/yyyy"                
         TSEG  (R1),(R0)                                                        
         TSEG  CVBLANKS,2                                                       
         SETMSG CVMSG                                                           
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),WR                                                     
         SETMSG CVBLANKS,1                                                      
         END                                                                    
*                                                                               
         LA    R4,CVUMSGCK         User message                                 
         IF    (@R4+4,NE,X'00'),BEGIN  User message...                          
         VCALL LOCALTOD                                                         
         LR    R2,R0                                                            
         L     R0,@R4                                                           
         CLEAR R1,R3                                                            
         XPUSH ,,30,PTR=R15                                                     
         VCALL FMTRCLCK            "hh:mm" or "hh:mm mm/dd/yyyy"                
         TSEG  (R1),(R0)                                                        
         TSEG  CVBLANKS,2                                                       
         SETMSG @R4+4,L'CVMSG                                                   
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),WR                                                     
         SETMSG CVBLANKS,1                                                      
         END                                                                    
*                                                                               
         IF    (R0,NZ),'TSEG (R1),(R0),WR'                                      
         VCALL SHOWBKUP            Display any backup status vols               
         SET   CPFLMSG                                                          
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GETCP -- Routine to get (and initialize) the CP.                             
*                                                                               
*    On entry:                                                                  
*      CPR - zero                                                               
*                                                                               
*    On exit:                                                                   
*      CPR - New CP                                                             
*                                                                               
GETCP    XPROC                                                                  
         FAIL  (CPR,NZ),GETCP,'CP logic error.'                                 
*                                                                               
         LA    R15,CPPOOL          Getmain new CP                               
         VCALL GETPOOL                                                          
         LR    CPR,R1                                                           
         ZOT   CP,LH:=AL2(CPXSIZE)  Zot CP                                      
*                                                                               
         MVC   CP(4),=C'CP  '      Set self identification                      
*                                                                               
         L     R15,CVCPMEM                                                      
         AH    R15,=AL2(CPXSIZE)   New total memory used                        
         ST    R15,CVCPMEM         Save new total                               
*-                                                                              
*-       Set up default seg buffer.                                             
*-                                                                              
         LH    R0,=AL2(CPSEGDF#)   Get default seg buffer                       
         VCALL TSGBUFGT                                                         
         STH   R0,CPSEGBSZ         Save buffer length                           
         ST    R1,CPSEGBP          Save buffer ptr                              
*                                                                               
         EXTRN TERMSEG                                                          
         SEGINIT (R1),LH:CPSEGBSZ,CPSEG,RTN=TERMSEG                             
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FREECP -- Routine to free the CP.                                            
*                                                                               
*    On entry:                                                                  
*      CPR - CP ptr                                                             
*                                                                               
*    On exit:                                                                   
*      CPR - zero                                                               
*                                                                               
FREECP   XPROC                                                                  
         FAIL  (CP,NE,'CP'),FREECP,'CP logic error.'                            
*-                                                                              
*-       Free View extensions to CP.                                            
*-                                                                              
         VCALL FREEVIEW            Free View CP extension                       
*-                                                                              
*-       Free seg buffer.                                                       
*-                                                                              
         IF    ('LT R1,CPSEGBP',NZ),BEGIN  Free seg buffer...                   
         LH    R0,CPSEGBSZ                                                      
         VCALL TSGBUFRE                                                         
         CLEAR CPSEGLOC,CPSEGLENF  Tidy                                         
         CLEAR CPSEGBP,CPSEGBSZ                                                 
         END                                                                    
*-                                                                              
*-       Free CP.                                                               
*-                                                                              
         MVC   CP(4),=C'OCP '      Old CP                                       
*                                                                               
         L     R1,CPGETSTK         Load pointer to GETSTACK                     
         IF    (R1,NZ),BEGIN       Free it if there is one...                   
         VCALL FREECORE                                                         
         END                                                                    
*                                                                               
         LR    R1,CPR                                                           
         LA    R15,CPPOOL          Freemain CP                                  
         VCALL FREEPOOL                                                         
*                                                                               
         CLEAR CPR                 No CP now                                    
*                                                                               
         L     R15,CVCPMEM                                                      
         SH    R15,=AL2(CPXSIZE)   Deduct space from total used                 
         ST    R15,CVCPMEM         Save new total                               
         PEND                                                                   
CPPOOL   CELLBLOK SIZE=CPXSIZE,SUBPOOL=20                                       
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INITCP -- Routine to initialize ALL CP fields.                               
*                                                                               
*  This routine also initializes RECORD GROUPS,                                 
*  various routines, etc. ..                                                    
*                                                                               
INITCP   XPROC                                                                  
         FAIL  (CP,NE,'CP'),BADCP,'WYLBUR logon logic error'                    
*                                                                               
*  Preserve SIGNON flags.  This could be removed if flags were          ***ISU  
*  moved out of CPINIT area.                                            ***ISU  
*                                                                       ***ISU  
         LC    R2,CPGFLG9                                               ***ISU  
         LA    R0,CPINIT                                                        
         LH    R1,=AL2(CPINITL)                                                 
         CLEAR R15                                                              
         MVCL  R0,R14              Zot CP                                       
*                                                                       ***ISU  
         STC   R2,CPGFLG9          Save back SIGNON flags               ***ISU  
*                                                                       ***ISU  
         CLEAR CPCMDLEN            Other cleanup                                
*                                                                               
         COMMENT                   SET UP SCANCB IN CP                          
         LA    R1,CPNSCAN          General purpose SCANCB                       
         ST    R1,CPNSCANP         Save addr in CP                              
         SCCLR                     Clear control block                          
*                                                                               
         IF    ^CVFINIT,BEGIN                                                   
         TCLEAR                                                                 
         TCNTL CTLSNSI             Sense our session info                       
         IF    NZ,EXIT                                                          
         LR    R15,R0                                                           
         CEIL  R15,L'CPSIB                                                      
         MOVE  R15,CPSIB,@R1       Save session info                            
         END                                                                    
*-                                                                              
*-       Get an area for the GETSTACK.                                          
*-                                                                              
         LA    R0,&LINESZ          Get memory for GETSTACK                      
         VCALL GETCORE                                                          
         ST    R1,CPGETSTK         Save GETSTACK address                        
*-                                                                              
*-       Initialize record groups.                                              
*-                                                                              
         ACALL INITRECS            Initialize all Record Groups                 
         VCALL ACTRESET            Initialize Active file RG too                
*                                                                               
         COMMENT                   Initialize various modules                   
         VCALL INITEXEC            Init EXEC module                             
         VCALL INITXCAL            Init XCALL module                            
         VCALL INITFLOW            Init FLOW module                             
         VCALL INITVARS            Init EXEC variables                          
         VCALL INITDBUG            Init DeBUG module                            
*                                                                               
         LCALL INITDFLT            Initialize other defaults                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INITDFLT -- Routine to initialize CP fields to their logon                   
*    defaults.                                                                  
*                                                                               
INITDFLT PROC                                                                   
*  Preserve SIGNON flags.  This could be removed if flags were          ***ISU  
*  moved out of CPINIT area.                                            ***ISU  
*                                                                       ***ISU  
         LC    R2,CPGFLG9                                               ***ISU  
         CLEAR CPGFLGS             Reset global flags                           
*                                                                       ***ISU  
         STC   R2,CPGFLG9          Save back SIGNON flags               ***ISU  
*                                                                       ***ISU  
*                                                                               
         SETMSG 'Command'           Default command prompt text                 
         IF    CVFTEST,BEGIN       Test subsystem...                            
         SET   CPGFCPR             Explicit command prompt text                 
         SETMSG CVWYLSSN                                                        
         VCALL LRTRIM              Use subsystem name                           
         END                                                                    
         STH   R0,CPCPLEN                                                       
         LR    R15,R0                                                           
         MOVE  R15,CPCPRMT,@R1     Set command prompt text                      
*                                                                               
         MVC   CPWCHAR,=CL(L'CPWCHAR)'GENERAL'  Default component               
         MVC   CPLCOL,=H'&LINESZ'  Default ending column                        
         MVC   CPDELTA,=F'1000'    Set global delta to 1                        
         MVC   CPLENGTH,=H'72'     Set default length                           
         MVC   CPHCMDMX,=H'5'      No. of previous cmds to display              
         MVC   CPLIMIT,=H'5000'    No of cmds before exec loop warn             
         SET   CPGFNLST            Global nolist in effect                      
         MVC   CPVMARG,=H'68'      View mode right margin                       
         SET   CPVFWRAP            Page mode defaults                           
         MVI   CPVWCHAR,C' '       Word separator is blank                      
         SET   CPJFFRCE            Jes force mode                               
         MVC   CPJNO,=H'-1'        No local job count                           
         CLEAR CPOERRID            Init "prev" error id                         
         MVC   CPANAME,=CL8'NEW'   Set Active file name                         
         MVC   CPATYPE,=CL8'TEXT'  Set Active file type                         
         MVC   CPACLNO,=F'-1000'   No current line ptr                          
         MVI   CPEXLINE,X'80'      Set no exec line ptr                         
         CLEAR CPEXATTN            No "ON ATTN"                                 
         CLEAR CPEXABRT            No "ON ABORT"                                
         SET   CPFNPARM            Exec parm not set                            
         MVC   CPLIB,=CL8'LIB'                                                  
         COMMENT                   Clear member  CH 5/91                        
         SETMSG CPAFNAME,L:CPAFNAMEL (we recalc name len w/o mem)               
         VCALL RETFNAME                                                         
         ST    R0,CPAFNAMEL                                                     
         MVC   CPWRNCNT,=H'30'     Set default warn count                       
         CLEAR CPACHCT             Clear the change count                       
         MVC   CPRSCNVL,=H'5'      Set rescan limit                             
*                                                                               
         IF    CPSTCONS,BEGIN      CONSOLE defaults...                          
         CLEAR CPCPLEN             No command prompt text                       
         SET   CPGFNATN            Mode noattn                                  
         END                                                                    
*                                                                               
         IF    CPSSFSEC,'SET CPFSEC'  Session is secure                         
*                                                                               
         VCALL SAMINIT             Reset Samson information                     
         VCALL ACTRESET            Clear all actives                            
         VCALL EXREINIT            Clear all execs                              
         LCALL INITSESS            Initialize milten session info               
         VCALL CHECKSET            Keep accurate for recovery                   
*                                                                               
         VCALL BILLCHK             Initialize user accounting                   
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INITSESS -- Initialize MILTEN Session Information Block.                     
*                                                                               
INITSESS PROC                                                                   
         IF    ^CVFINIT,BEGIN                                                   
         IF    (CPSACCT,Z),BEGIN   Clear other session stuff...                 
         TCLEAR                                                                 
         CLEAR CPSSESS             Reset account info                           
         TSEG  CPSSESS                                                          
         TCNTL CTLPSETS            Set session                                  
         FAIL  NZ,MILERR                                                        
         LR    R15,R0                                                           
         CEIL  R15,L'CPS                                                        
         MOVE  R15,CPS,@R1                                                      
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  INITRECS- INITIALIZE ALL RECORD GROUPS                                       
*                                                                               
*                                                                               
*  Called at user start up.  Destroys any existing information                  
*  in the record groups!                                                        
*                                                                               
*  Note:  This routine must NOT initialize the Active file                      
*    record group (CPTEXT).  That function is done by the                       
*    "ACTRESET" routine.                                                        
*                                                                               
*                                                                               
INITRECS PROC                                                                   
*                                                                               
         COMMENT                   Free any current Record Groups               
         COMMENT                   NOTE: this will destroy all data!            
         LA    R15,CPEXEC                                                       
         VCALL FREERG                                                           
         LA    R15,CPFLOW                                                       
         VCALL FREERG                                                           
         LA    R15,CPVARS                                                       
         VCALL FREERG                                                           
         LA    R15,CPDATA                                                       
         VCALL FREERG                                                           
         LA    R15,CPRG16                                                       
         VCALL FREERG                                                           
         LA    R15,CPRG64                                                       
         VCALL FREERG                                                           
*                                                                               
         LA    R15,CPEXEC          EXEC files                                   
         LA    R0,4                                                             
         LA    R1,=CL4'EXEC'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
         LA    R15,CPFLOW          FLOW information                             
         LA    R0,16                                                            
         LA    R1,=CL4'FLOW'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
         LA    R15,CPVARS          Exec Variables                               
         LA    R0,16                                                            
         LA    R1,=CL4'VARS'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
         LA    R15,CPDATA          Exec GETDATA/PUTDATA records                 
         LA    R0,CPDATANL                                                      
         LA    R1,=CL4'DATA'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
*                                                                               
         LA    R15,CPRG16          Misc Record Group 16                         
         LA    R0,CPRG16NL                                                      
         LA    R1,=CL4'RG16'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
         LA    R15,CPRG64          Misc Record Group 64                         
         LA    R0,CPRG64NL                                                      
         LA    R1,=CL4'RG64'                                                    
         LA    R2,=CL4'....'                                                    
         VCALL INITRG                                                           
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FREERECS - FREE ALL RECORD GROUPS (including the Active file                 
*       Record Group.)                                                          
*                                                                               
*                                                                               
*                                                                               
FREERECS PROC                                                                   
*                                                                               
         COMMENT                   Free all current Record Groups               
         COMMENT                   NOTE: this will destroy all data!            
         LA    R15,CPTEXT                                                       
         VCALL FREERG                                                           
         LA    R15,CPEXEC                                                       
         VCALL FREERG                                                           
         LA    R15,CPFLOW                                                       
         VCALL FREERG                                                           
         LA    R15,CPVARS                                                       
         VCALL FREERG                                                           
         LA    R15,CPDATA                                                       
         VCALL FREERG                                                           
         LA    R15,CPRG16                                                       
         VCALL FREERG                                                           
         LA    R15,CPRG64                                                       
         VCALL FREERG                                                           
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'LOGON/LOGOFF Commands'                                          
LOGWA    RECORD BEGIN                                                           
LOGFLAG  FLAG                                                                   
         FLAG  LOGFSAVE            - Save active file                           
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  LOGOFF                                                                       
*                                                                               
LOGOFF   XPROC  ,                  SET CPCMNM AND CALL LOGON                    
         MVI   CPCMNM,X'FF'                                                     
         ACALL LOGON               NO RETURN                                    
         BOMB                                                                   
         PEND                                                                   
*box                                                                            
*                                                                               
*  LOGON                                                                        
*                                                                               
LOGON    XPROC LOGWA                                                            
         LA    R6,LOGWA                                                         
         USING LOGWA,R6                                                         
         CLEAR LOGWA                                                            
*                                                                               
         SCAN LOGPRT                                                            
         SCANCHK                                                                
*                                                                               
         IF    ^LOGFSAVE,BEGIN                                                  
         VCALL CLRACTS             Clear Actives                                
         END                                                                    
*-                                                                              
*-       Vista LOGOFF simply returns to the subsystem which                     
*-         invoked it (no logoff messages, etc.)                                
*-                                                                              
         IF    CVFVISTA,BEGIN      VISTA style...                               
         IF    (CPORGSYS,Z),BEGIN  Originated from MILTEN...                    
         TSEG  'End of VISTA session.',,CR                                      
         TWRITE                                                                 
         END                                                                    
*                                                                               
         ELSE  BEGIN               See if subsys is running...                  
         LA    R1,CPORGSYS                                                      
         VCALL SYSCHECK            Is this subsystem running?                   
         IF    NZ,BEGIN            No...                                        
         TSEG  CPORGSYS,,TB                                                     
         TSEG  ' is not available.',,CR                                         
         TWRITE                                                                 
         END                                                                    
         END                                                                    
*                                                                               
         SET   CPFSYSMV            Transfer control w/o msgs                    
         MVC   CPLGFSYS,CPORGSYS   Set new subsys name                          
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Log off our WYLBUR                     
         B     CVNEXT              Scram                                        
         END                                                                    
*-                                                                              
*-       Normal Wylbur logon/logoff.                                            
*-                                                                              
         VCALL ORVCPASS            Pass command to SPIRES                       
*                                                                               
         SET   CPFNPAU             Don't page/pause                             
*-                                                                              
*-       April fools joke in TESTWYL only.                                      
*-                                                                              
         AGO   .NOJOKE                                                          
         IF    CVFTEST,BEGIN       TESTWYL only...                              
         IF    CPFAPRILFOOL,EXIT   Only once                                    
         SET   CPFAPRILFOOL        Latch                                        
*                                                                               
         IF    CPSFSTOP,EXIT       Skip                                         
         IF    CPSTVIRT,EXIT       Ditto                                        
         SET   CPFNECHO            Set NOECHO                                   
         TSEG  'This account has enhanced LOGOFF security.',,CR                 
         TREAD 'Enter your PIN number to confirm LOGOFF request',,Q             
         CLEAR CPFNECHO            Reset                                        
         TSEG  'Incorrect PIN number.',,CR                                      
         TSEG  'You may not logoff, but try '                                   
         TSEG  'to avoid using additional computer resources.'                  
         ABORT                                                                  
         END                                                                    
.NOJOKE  ANOP                                                                   
*                                                                               
         IF    CPSFSTOP,'MVI CPCMNM,X"FF"'  Force logoff if stopping            
*                                                                               
         IF    (CPCMNM,EQ,X'FF'),BEGIN  Logoff command...                       
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBAFALO'  Logoff                                 
         END                                                                    
         ACALL SIGNOFF             NO RETURN                                    
         BOMB                                                                   
         PEND                                                                   
*                                                                               
LOGPRT   SCKW  SAVE,LGFSAVE,A                                                   
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
LGFSAVE  PROC                                                                   
         SET   LOGFSAVE                                                         
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  ELF LOGIN:  PROCESS ELF DIRECT LOGIN                                         
*                                                                               
*  THIS ROUTINE PROCESSES 'LOGIN: ... ' STRING                                  
*  PASSED BY ELF.  FOR NOW WE ONLY LOOK AT THE                                  
*  ACCOUNT AND COMMAND FIELDS.  ALL OTHER FIELDS                                
*  ARE IGNORED.  SPECIFICALLY, THE PASSWORD FIELD                               
*  IS IGNORED.  IF AN ACCOUNT REQUIRES A PASSWORD,                              
*  THE USER WILL BE PROMPTED, EVEN IF ONE WAS SUPPLIED.                         
*  THE COMMAND WILL BE PASSED AS THE FIRST COMMAND.                             
*  (HMMM).                                                                      
*                                                                               
*  ON ENTRY:                                                                    
*  THE SCAN CONTROL BLOCK POINTS JUST PAST THE 'LOGIN: '                        
*                                                                               
*  ON EXIT:                                                                     
*  CPCACCT AND CPCMD,CPCMDLEN ARE SET APPROPRIATELY                             
*                                                                               
*  ERRORS:                                                                      
*  FOR NOW (EVER),  BAD SCANS SHOULD SET R15=4 (NZ) AND NOT                     
*  GIVE ANY ERROR MSGS.  UNRECOGNIZED OPTIONS FROM ELF SHOULD BE                
*  IGNORED.                                                                     
*                                                                               
*  THIS ROUTINES IS CALLED BY 'SCAN SIGNPRT'.                                   
*                                                                               
*  ADMINISTRATIVE NOTE:                                                         
*  SIGN LOGIN CODE USES CPCMD AREA TO PASS LOGIN                                
*  STRING INFO.. AND YET THE CMD=' ...' OPTION                                  
*  MAY UPDATE THE CPCMD FIELD.  IT MAY NOT BE SUCH                              
*  A GOOD IDEA TO USE THE CPCMD AREA FOR THIS. THE                              
*  LOGIN INFO SHOULD BE PLACED ON THE STACK IN THE                              
*  SIGNON WORK AREA.  BUT FOR NOW WE JUST COPY THE                              
*  STRING BEING SCANNED TO THE STACK AND SCAN IT                                
*  THERE.                                                                       
*                                                                               
*                                                                               
LGNWA    RECORD BEGIN                                                           
LGNACCTL DS    F                                                                
LGNWACCT DS    CL(&LINESZ)                                                      
LGNPRINL DS    F                                                                
LGNPRINC DS    CL(L'CPAUTHID)                                                   
LGNTEXT  DS    CL(&LINESZ*4)                                                    
         END                                                                    
*                                                                               
ELFLOGIN PROC  LGNWA                                                            
*                                                                               
         LA    R6,LGNWA                                                         
         USING LGNWA,R6                                                         
*                                                                               
         CLEAR LGNWA               Zero the work area                           
*                                                                               
         COMMENT                   MOVE SCAN TEXT INTO STACK                    
         SCINFO                                                                 
         LR    R2,R1                                                            
         LR    R3,R0                                                            
         LA    R4,LGNTEXT                                                       
         LR    R5,R0                                                            
         CEIL  R5,L'LGNTEXT                                                     
         MVCL  R4,R2                                                            
         SCCLR                                                                  
         SCINIT LGNTEXT,(R0)                                                    
*                                                                               
         CLEAR CPAUTHID            Clear out principal                          
         CLEAR CPCACCT                                                          
*                                                                               
         COMMENT                   SCAN ELF LOGIN OPTIONS                       
         SCAN  ELFPRT                                                           
         IF    (R15,NZ),BEGIN      IF ERR, STOP SCAN, IGNORE MSG                
         CLEAR CPCACCT                                                          
         CLEAR CPCMDLEN                                                         
         CLEAR CPFSGNCM                                                         
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN               Check out account and principal              
         IF    (LGNACCTL,NZ),BEGIN                                              
         LA    R1,LGNWACCT                                                      
         L     R0,LGNACCTL                                                      
         ACALL CHKSPID             Check for special ids                        
         IF    (R15,Z),ELFLOGEX                                                 
         LA    R1,LGNWACCT                                                      
         L     R0,LGNACCTL                                                      
         LA    R15,CPCACCT         Put resolved account here                    
         VCALL GETACCT             Convert to account                           
         IF    (R15,NZ),BEGIN                                                   
         CLEAR CPCACCT                                                          
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         ELSEIF (LGNPRINL,NZ),BEGIN                                             
         LA    R1,LGNPRINC                                                      
         L     R0,LGNPRINL                                                      
         LA    R15,CPCACCT         Put resolved account here                    
         VCALL GETACCT             Convert to account                           
         IF    (R15,NZ),BEGIN                                                   
         CLEAR CPCACCT                                                          
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         END                                                                    
*                                                                               
* If we managed to find an account, then check to see if the                    
* account has a KLOGIN file and if the Kerberos principal                       
* is listed in it.                                                              
*                                                                               
*                                                                               
*  Warning! - The ELFs can send account information in two cases:               
*             1) The Kerberos authentication case                               
*             2) When the ports for GEOREF, FOLIO, etc are used.                
*                                                                               
*             We need to come up with a way to distinguish these                
*             cases (in the future event that Kerberos                          
*             authentication is applied to the libary ports).                   
*             Otherwise, the test below will cause those ports                  
*             indicated in (2) not to work - they'll get an account             
*             prompt.                                                           
*                                                                               
         IF    ((CPCACCT,NZ),AND,(CPAUTHID,NZ)),BEGIN                           
         VCALL KLOGINCK                                                         
         IF    (R15,NZ),BEGIN                                                   
         CLEAR CPCACCT                                                          
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         END                                                                    
ELFLOGEX PEND  ,                                                                
*                                                                               
*  ELF LOGIN OPTIONS                                                            
*                                                                               
ELFPRT   SCKW  ACCOUNT,LGNACCT                                                  
         SCKW  COMMAND,LGNCMD                                                   
         SCKW  AUTH_IDENTITY,LGNAUTH                                            
         SCKW  ,LGNMISC                                                         
*                                                                               
*  ACCOUNT=<account>                                                            
*                                                                               
LGNACCT  PROC  ,                                                                
         SCAN  ,                   Get the account or userid                    
         IF    (R15,Z),BEGIN                                                    
         CEIL  R0,L'LGNWACCT                                                    
         LR    R15,R0                                                           
         ST    R15,LGNACCTL        Save length                                  
         MOVE  R15,LGNWACCT,@R1    Save account                                 
         CLEAR R15                 Scan the rest of the login string            
         END                                                                    
         PEND                                                                   
*                                                                               
*  COMMAND='ABC ... '                                                           
*                                                                               
LGNCMD   PROC  ,                   SCAN COMMAND VALUE                           
         SCAN  ,                                                                
         IF    (R15,Z),BEGIN       IF ALL OK, SAVE FIRST CMD                    
         SET   CPFSGNCM            FIRST COMMAND PROVIDED                       
         SCDQUOTE CPCMD                                                         
         STH   R0,CPCMDLEN                                                      
         END                                                                    
         PEND                                                                   
*                                                                               
*  AUTH_IDENTITY=string                                                         
*                                                                               
LGNAUTH  PROC  ,                   SCAN IDENTITY STRING                         
         SCAN  ,                                                                
         IF    (R15,Z),BEGIN       IF ALL OK, SAVE IT                           
         IF    (R0,LE,L'CPAUTHID),BEGIN                                         
         MVC   CPAUTHID,CVBLANKS                                                
         LR    R15,R0                                                           
         MOVE  R15,CPAUTHID,@R1                                                 
*                                                                               
* Extract the principal name and save it in the                                 
* work area for later.                                                          
*                                                                               
         XPUSH R1                                                               
         CLEAR R15                                                              
         WHILE ((R15,LT,R0),AND,(@R1,NE,'@')),BEGIN                             
         INCR  R15                                                              
         INCR  R1                                                               
         END                                                                    
         XPOP  R1                                                               
         IF    (R15,LT,R0),BEGIN                                                
         ST    R15,LGNPRINL        Save length                                  
         MOVE  R15,LGNPRINC,@R1    Save principal name                          
         END                                                                    
         CLEAR R15                 Continue scanning                            
         END                                                                    
         ELSE  BEGIN               Stop scan - authid too large                 
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
*  MISC IS IGNORED                                                              
*                                                                               
LGNMISC  PROC  ,                                                                
         SCAN  ,                   SKIP (KEYWORD=) VALUE                        
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         QLTORG                                                                 
         TITLE 'Terminate Session'                                              
*box                                                                            
*                                                                               
*  RECOVERY -- Initial entry point for a user who is going                      
*    through WYLBUR recovery.                                                   
*                                                                               
         PROC                                                                   
         ENTRY RECOVERY                                                         
RECOVERY BASE                                                                   
*                                                                               
         VCALL INITSTK             Initialize stack                             
         ACALL INITRECS            Allocate RGs (except CPTEXT)                 
*                                                                               
         IF    (CPTEXT,Z),BEGIN    No Actives to recover...                     
         VCALL ACTRESET            Initialize Active files rg                   
         END                                                                    
*                                                                               
         ELSE  BEGIN               Otherwise re-build for recovery...           
         VCALL ACTRCVY             Finish setting up Active file                
         END                                                                    
*-                                                                              
*-       Write out a recovery message.                                          
*-                                                                              
         SEGCLR L:CVWTOSGP                                                      
         SEG   'Line '                                                          
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'LH R15,JCBSEQ'                                        
         SEGDC (R15)                                                            
         SEG   ' '                                                              
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
*                                                                               
         IF    CPSSFNAR,BEGIN      No recovery...                               
         SETMSG ' will be skipped'                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         SETMSG ' will be recovered'                                            
         IF    ('LT R15,CPATTEXT',NP),'SETMSG " has no Active files"'           
         END                                                                    
         SEG   (R1),(R0)           Finish the message                           
         SET   CVWTOFNOPFX+CVWTOFWTL  WTO logging flags                         
         XWTO                                                                   
         CLEAR CVWTOFNOPFX+CVWTOFWTL  Reset                                     
*                                                                               
         ACALL SIGNOFF             Join common SIGNOFF code                     
         BOMB                      SIGNOFF does not return                      
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SIGNOFF -- Routine to terminate the session.  This routine                   
*    is responsible for saving the user's ACTIVE file.                          
*                                                                               
         PROC                                                                   
         ENTRY SIGNOFF                                                          
SIGNOFF  BASE                                                                   
         VCALL INITSTK             Initialize stack                             
*-                                                                              
*-       Verify that ACTIVE file recovery is possible.                          
*-                                                                              
         IF    CPSSFNAR,NORCVY     Skip recovery (virtual trm)                  
*                                                                               
         IF    ('LT R15,CPATTEXT',NP),NORCVY  No Active files                   
*-                                                                              
*-       Set up a SAVE command for the ACTIVE file.                             
*-         If the SAVE command doesn't work "SIGNOFF" will be called            
*-         again, and this routine will pick a different volume.                
*-                                                                              
         IF    (CPRCV,Z),BEGIN     First time...                                
         SET   CPFRCVY             Set flags                                    
         CLEAR CPVOLUME            Default volume                               
         MVC   CPRCVEND,CVRCVOL    Remember where we started from               
         MVC   CPCMD,CVBLANKS      Reset previous command                       
         MVC   CPCMD(L'RCVYCMD1),RCVYCMD1  SAVE (without volume)                
*                                                                               
         IF    ((CPSACCT,EQ,'MPDGG'),                                  *        
               OR,(CPSACCT,EQ,'LTMGQ'),                                *        
               OR,(CPSACCT,EQ,'PCJGQ')),                               *        
               BEGIN                                                            
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'LH R15,JCBSEQ'  Line number                           
         A     R15,=F'100000'      Force leading zeros                          
         BTD   CPCMD+(RCVYCLIN-RCVYCMDX),L'RCVYCLIN,(R15)  Format               
         MVC   CPCMD+(RCVYCLIN-RCVYCMDX)(2),=C'.L'  "ACTIVE.L####"              
         END                                                                    
*                                                                               
         MVC   CPCMDLEN,=AL2(L'RCVYCMD1)  Set length                            
         B     RCVYSAVE            Try the catalog first                        
         END                                                                    
*-                                                                              
*-       The SAVE command has failed, go on to the next volume and              
*-         retry the save command.                                              
*-                                                                              
RCVYLOOP MVC   CPCMD,CVBLANKS                                                   
         MVC   CPCMD(L'RCVYCMD),RCVYCMD  SAVE ON <...>                          
*                                                                               
         IF    ((CPSACCT,EQ,'MPDGG'),OR,(CPSACCT,EQ,'LTMGQ')),BEGIN             
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'LH R15,JCBSEQ'  Line number                           
         A     R15,=F'100000'      Force leading zeros                          
         BTD   CPCMD+(RCVYCLIN-RCVYCMDX),L'RCVYCLIN,(R15)  Format               
         MVC   CPCMD+(RCVYCLIN-RCVYCMDX)(2),=C'.L'  "ACTIVE.L####"              
         END                                                                    
*                                                                               
         L     R4,CPRCVCUR         Current recovery volume ptr                  
         IF    (R4,EQ,CPRCVEND),RCVYFAIL  No more volumes, scram                
         IF    (R4,Z),'L R4,CPRCVEND'  Start at the beginning                   
*                                                                               
         LA    R15,@R4+L'VOL       Point to next entry                          
         IF    (@R15,EQ,X'FF'),'L R15,CVVOLIST'  Volume table wraps             
         ST    R15,CPRCVCUR        Save vol ptr for next time                   
         ST    R15,CVRCVOL         Update global starting vol ptr               
*                                                                               
         WITH  (VOL,R4),BEGIN                                                   
         IF    (NOT,VFLG1.VF1AVAIL*VF1PRES*VF1ON),RCVYLOOP  Skip vol            
         AIF   ('&INSTALN' NE 'STANFORD').NOT4U                                 
         IF    CVFRLG,BEGIN        RLG machine...                               
         IF    (VNAME,NE,'RLG'),RCVYLOOP  Skip this vol                         
         END                                                                    
         ELSE  BEGIN               SYSA and SYSC...                             
         IF    (VNAME,NE,'PUB'),RCVYLOOP  Skip this vol                         
         END                                                                    
.NOT4U   MVC   CPCMD+L'RCVYCMD(6),VNAME  Set volume                             
         END                                                                    
*                                                                               
         MVC   CPCMDLEN,=AL2(L'RCVYCMD+6)  Complete SAVE command                
*-                                                                              
*-       Issue the SAVE command.                                                
*-         (Returns to SIGNOFF through the command processor.)                  
*-                                                                              
RCVYSAVE VCALL INITCMD             Re-initialize                                
         CLEAR CPGFLG3.CPFCMDFX    Turn off retry mode                          
         MVC   CPCMNM,=C'SAV'      Set save command name                        
         OSCINIT CPCMD,LH:CPCMDLEN                                              
         VCALL OLDSAVE                Go try save                               
*                                                                               
RCVYFAIL TSEG  'No more volumes -- Recovery not possible for '                  
         TSEG  CPSGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPSUSER,,WR                                                      
         B     NORCVY                                                           
         EJECT                                                                  
*-                                                                              
*-   ACTIVE file has been recovered, now finish terminating session.            
*-                                                                              
NORCVY   CLEAR CPZERO              Reset any local command options              
*                                                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),BEGIN                                                   
         IF    JCBBFSIB,BEGIN      Get new SIB...                               
         TCNTL CTLSNSI             Sense SIB                                    
         FAIL  NZ,MILERR                                                        
         CEIL  R0,L'CPSIB                                                       
         LR    R15,R0                                                           
         MOVE  R15,CPSIB,@R1                                                    
         CLEAR JCBBFSIB                                                         
         MVC   JCBSUC,CPSUC                                                     
         END                                                                    
         END                                                                    
*                                                                               
         PFREE PAR                 Release page A                               
         PFREE PBR                 Release page B                               
*                                                                               
         VCALL CLRHCMDS            Free history commands                        
*                                                                               
         COMMENT                   Clear Exec Monitor, if any                   
         VCALL MCLEAR                                                           
*                                                                               
         SET   CPFCLEAR,CPFNWARN                                                
*        VCALL CLRMULT             Clear all Actives                            
         VCALL ACTRESET            Wipe out all actives - MPD                   
         VCALL EXREINIT            Clear all execs (not necessary?)             
*                                                                               
         VCALL VETSFREE            Free any VTAM terminal info                  
*                                                                               
         VCALL PROTFREE            Release any protection info                  
*                                                                               
         CLEAR R15                 Free ALL dsnames                             
         VCALL DENQFREE            DEQ any enqueued data sets                   
*                                                                               
         VCALL SERVFREE            Close any open server connections            
*                                                                               
         VCALL NPATFREE            Close any open net connections               
*                                                                               
         VCALL PATHFREE            Close any open user paths                    
*                                                                               
         ACALL FREERECS            Clear all Record Groups                      
*                                                                               
         L     R6,CVCURJCB                                                      
         USING JCB,R6                                                           
*-                                                                              
*-       Skip updating last logoff time and billing/logoff messages             
*-         if we are simply transferring control to a test                      
*-         subsystem.                                                           
*-                                                                              
         IF    CPFSYSMV,SIGNDONE   No logoff msgs if XCTL                       
*-                                                                              
*-       VISTA logoffs are short and sweet.                                     
*-                                                                              
         IF    CVFVISTA,SIGNDONE   No logoff msgs for VISTA                     
*-                                                                              
*-       Update last logoff time/date if a real user is logging off.            
*-                                                                              
         IF    ^CPSSFPS,BEGIN      Real user logging off...                     
*                                                                               
         XPUSH ,,L'KWR,PTR=R5                                                   
         WITH  (KWR,R5),BEGIN                                                   
         CLEAR KWR                                                              
         MVC   KWRGRP,CPSGRP       gg                                           
         MVC   KWRUSER,CPSUSER     uuu                                          
*                                                                               
         COMMENT                   Limit loop to 10 times thru we               
         LA    R4,10 LOOP LIMIT      do not want to stop logouts                
*                                                                               
         LOOP  BEGIN                                                            
         KREAD KWR                                                              
         LTR   R4,R4                                                            
         IF    (R4,Z),SKIPWCHK exit loop                                        
         DECR  R4                                                               
*                                                                               
*                                                                               
         IF    ^KWRHFL.KWRHFCK,EXIT  No password processing now                 
         IF    ^KWRIFL.KWRIFVAL,EXIT  Account doesn't exist                     
*-                                                                              
*-       Save port we are using in last logoff information.                     
*-                                                                              
         MVC   KWRLPORT,CVBLANKS   Blank out port information                   
*                                                                               
         IF    ((CPSPLOC,NZ),AND,(CPSPLOC,NE,CVBLANKS)),BEGIN                   
         MVC   KWRLPORT,CPSPLOC    Save port location info                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               Save "fe-port" style...                      
         IF    ((CPSFE,NZ),OR,(CPSPORT,NZ)),BEGIN                               
         XPUSH ,,L'SEGCB,PTR=R2                                                 
         SEGINIT KWRLPORT,,(R2)                                                 
         SEGT  CPSFE               Front-end name                               
         SEG   '-'                                                              
         SEGT  CPSPORT             Port name                                    
         XPOP  ,,L'SEGCB                                                        
         END                                                                    
         END                                                                    
*-                                                                              
*-       Update total number of connect hours in KWR.                           
*-                                                                              
         VCALL LOCALTOD            Current time of "logoff"                     
         ST    R0,KWRLTIM          Save logoff time                             
         IF    ((CPSLCLCK,NZ),AND,(CPCLCK,GT,CPSLCLCK)),BEGIN                   
         SDL   R0,CPSLCLCK         Elapsed time                                 
         D     R0,=A(40960000)     R1 = .01 sec                                 
         CLEAR R0                                                               
         D     R0,=A(60*60*100/10)  R1 = hours/10  (6 mins)                     
         IF    (R0,NZ),'INCR R1'   Kick upwards                                 
*                                                                               
         L2    R15,KWRHRSUS        Old accumulated time                         
         ALR   R15,R1              New total                                    
         L     R0,=A(X'7FFF')                                                   
         CEIL  R15,R0                                                           
         STH   R15,KWRHRSUS        Save it                                      
         END                                                                    
*-                                                                              
*-       Update user's KWR.                                                     
*-                                                                              
         IF    CPGFLG2.CPFACTSV,'SET KWRAFL.KWRAFASV'  Active saved             
         KWRITE KWR                Re-write password record                     
         COMMENT  avoid deadly embrace                                          
         COMMENT  below 2 lines fix axess logout deadlock jan0493               
         IF    (CPSACCT,EQ,'FOLEN'),SKIPWCHK                                    
         IF    (CPSACCT,EQ,'PRMHQ'),SKIPWCHK                                    
         UNTIL KWRHFL.KWRHFUOK,END  Stop when re-write works                    
*                                                                               
SKIPWCHK LABEL ,                                                                
         CLEAR KWR                                                              
         END                                                                    
*                                                                               
         XPOP  ,,L'KWR                                                          
         CLEAR CPFRCVY             Reset recovery flag                          
         END                                                                    
*-                                                                              
*-       Give logoff statistics.                                                
*-                                                                              
         IF    ^CVFINIT,BEGIN      Not doing recovery/news...                   
*                                                                               
         CLEAR R0                                                               
         IF    CPSFTO,'SETMSG "Session terminated due to inactivity."'          
         IF    CPSFMT,'SETMSG "No more time left, session terminated."'         
         IF    CPSFKL,'SETMSG "Session terminated by the operator."'            
         IF    (CPSACCT,Z),'CLEAR R0'                                           
         IF    (R0,NZ),BEGIN                                                    
         XPUSH R0,R1                                                            
         VCALL LOCALTOD                                                         
         XPUSH ,,11,PTR=R15        Put current hh:mm:ss.hh here                 
         VCALL FMTTIME                                                          
         TSEG  (R1),5,B            Hh:mm                                        
         XPOP  ,,11                                                             
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         END                                                                    
*                                                                               
         VCALL TSUBOFF             Exit all other subsystems now                
         IF    (CPSACCT,NZ),BEGIN  Give logoff stats...                         
         LA    R1,=CL8'LOGOFF'                                                  
         L     R15,CVCURJCB                                                     
         VCALL DOBILL              Write accounting records                     
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBFBILLED'  Final bill now written               
*                                                                               
         SETMSG 'LOGOFF:'                                                       
         VCALL LOGSESS             Log session logoff                           
*                                                                               
         VCALL DISPLGF             Display logoff messages                      
         END                                                                    
*                                                                               
         IF    (CPLGFSYS,Z),BEGIN  Real end of session...                       
         SETMSG 'End of session.'                                               
         IF    CVFRLG,'SETMSG "RLIN session ended.  Goodbye."'                  
         IF    CPSFNST,'SETMSG "Good-bye."'                                     
         IF    CPSSFPRT,'SETMSG "End of printer session."'                      
         TSEG  (R1),(R0),CR                                                     
         TCR                                                                    
         TCR                                                                    
*                                                                               
         IF    JCBAFALO,BEGIN      Logoff...                                    
         IF    CPSFSAM,BEGIN       Send Samson disconnect packet...             
         TSEG  X'27'               [ESC]                                        
         TSEG  '\\DD'              Disconnect                                   
         TSEG  X'0D'               [CR]                                         
         END                                                                    
         IF    (CPSFTEL,OR,CPSFTYM),'TSEG =80X"00",80'  It works!               
         END                                                                    
         END                                                                    
*                                                                               
         FAIL  CPFDUMP,LGFERR1,'Logoff processing error.'                       
         TWRITE                                                                 
         END                                                                    
*-                                                                              
*-       Tell MILTEN the user is logged off.                                    
*-                                                                              
SIGNDONE LABEL ,                                                                
         DECR  R15,CVNUSERS        Decr no of users logged on                   
*                                                                               
         CLEAR JCBACCT,JCBANO,JCBBFNR                                           
         LA    R1,JCB                                                           
         VCALL FREEBILL            Free billing information                     
         CLEAR JCBFBILLED          Reset billing record written                 
         CLEAR JCBFCNT             Reset failsoft counter                       
*                                                                               
         CLEAR CPSACCT             Reset user/group for recovery                
         VCALL CHECKSET            Keep accurare for recovery                   
*                                                                               
         IF    ^JCBAFALO,BEGIN     Logon...                                     
         CLEAR CPSSESS             Reset account info                           
         TSEG  CPSSESS                                                          
         TCNTL CTLPSETS            Set session                                  
         FAIL  NZ,MILERR                                                        
*                                                                               
         CLEAR CPSYS               As if it's an initial signon                 
         CLEAR R1,R0               (Signon routine parms)                       
         ACALL SIGNON              Go logon again                               
         END                                                                    
*                                                                               
         IF    ^CVFINIT,'VCALL TLOGOFF'  Tell MILTEN end of session             
*                                                                               
         VCALL EOSDISP             Logged off, enter dispatcher                 
         DC    H'0'                (No return)                                  
*                                                                               
         PEND                                                                   
         DROP  JCB                                                              
*-                                                                              
*-       Recovery SAVE command options.                                         
*-                                                                              
RCVYCMDX DC    C'@.&&.ACTIVE'                                                   
RCVYCLIN DC    C'      '                                                        
         DC    C' MULTIPLE REPLACE RECAT CLEAR'                                 
RCVYCMD1 EQU   RCVYCMDX,*-RCVYCMDX,C'X'                                         
         DC    C' ON '                                                          
RCVYCMD  EQU   RCVYCMDX,*-RCVYCMDX,C'X'                                         
         TITLE 'SET LOGON Command'                                              
*box                                                                            
*                                                                               
*  SET LOGON Command.                                                           
*                                                                               
SLOGWA   RECORD BEGIN                                                           
SLOGKWR  DS    XL(L'KWR)           Keyword record                               
         END                                                                    
*-                                                                              
SETLOGON XPROC SLOGWA                                                           
         IF    ((CPSACCT,NE,'WYLGS'),AND,(CPSACCT,NE,'MPDGG')),BEGIN            
         IF    ((CPSACCT,EQ,'RPCGS'),OR,(CPSACCT,EQ,'RPCGQ')),EXIT              
         IF    (CPSACCT,EQ,'WWWGS'),EXIT                                        
         VCALL NOTVALID            Sorry, only for RPC/WEB servers              
         END                                                                    
*                                                                               
         SCAN  SLOGPRT             Scan for new account                         
         SCANCHK                                                                
*                                                                               
         IF    (CPCACCT,Z),'ERROR "Missing account on SET LOGON."'              
         LA    R6,SLOGKWR                                                       
         WITH  (KWR,R6)                                                         
         CLEAR KWR                                                              
         MVC   KWRACCT,CPCACCT     Set "uuugg"                                  
         KREAD KWR                                                              
*                                                                               
         IF    ^KWRHFL.KWRHFCK,BEGIN  No keyword file...                        
         ERROR 'No account file, no action taken.'                              
         END                                                                    
*                                                                               
         IF    ^KWRIFL.KWRIFVAL,BEGIN  No such account...                       
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         ERROR ' does not exist.'                                               
         END                                                                    
*-                                                                              
*-       Force exit from all other subsystems (ORVYL).                          
*-                                                                              
         VCALL TSUBOFF             Force exit from ORVYL                        
*                                                                               
         IF    (CPSACCT,EQ,'WWWGS'),BEGIN   Set return data                     
         MVC   CPLACCT,CPSACCT                                                  
         END                                                                    
*-                                                                              
*-       Change accounts.                                                       
*-         First calculate the "user number" then change                        
*-         the account.                                                         
*-                                                                              
         MVC   CPSACCT,CPCACCT     Set account                                  
         MVC   CPACCTSV,CPCACCT    Set saved account too                        
         ACALL GETUSRNO            Get a unique user number                     
         ST2   R15,CPSANO          Save user number                             
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'MVC JCBACCT,CPCACCT; MVC JCBANO,CPSANO'               
*                                                                               
         MVC   CPSKIFL,KWRIFL      Set new privs                                
         MVC   CPSKAFL,KWRAFL      Set new privs                                
         AIF   ('&INSTALN' NE 'STANFORD').NOSOF3A                               
*                                                                               
* Copy over new source of funds - it appears that WYLBUR didn't                 
* originally do this, leading to accounting problems - MPD 05/06/98             
*                                                                               
         MVC   CPSOF,KWRSOF        Save source of funds code                    
         MVC   CPAR,KWRAR          Save accounts receivable code                
.NOSOF3A ANOP                                                                   
*-                                                                              
*-       Tell MILTEN about our new account.                                     
*-                                                                              
         TCLEAR                                                                 
         TSEG  CPSSESS                                                          
         TCNTL CTLPSETS            Set session                                  
         FAIL  NZ,MILERR                                                        
         LR    R15,R0                                                           
         CEIL  R15,L'CPSINFO                                                    
         MOVE  R15,CPSINFO,@R1     Save new session info                        
*-                                                                              
*-       Save our new userid.                                                   
*-                                                                              
         CLEAR CPUID               Assume no userid                             
*                                                                               
         LA    R15,CPSACCT         Logged on account                            
         SETMSG CPUID               Put userid here                             
         VCALL GETUID              Get corresponding userid                     
         IF    (CPUID,Z),'CLEAR CPUID'  No userid                               
*-                                                                              
*-       Release RACF privileges based on previous account.                     
*-                                                                              
         VCALL PROTFREE            Reinit RACF settings                         
*-                                                                              
*-       Write "LOGON-SWITCH:" message to log.                                  
*-                                                                              
         SETMSG 'LOGON-SWITCH: '                                                
         VCALL LOGSESS             Log session logon                            
*                                                                               
         B     CVNEXT              It's done                                    
         PEND                                                                   
*-                                                                              
*-       SET LOGON Options.                                                     
*-                                                                              
SLOGPRT  SCKW  RETURN,SLOGRET                                                   
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SLOGRET  PROC                                                                   
         IF    ((CPLACCT,NZ),AND,(CPLACCT,EQ,'WWWGS')),BEGIN                    
         MVC   CPCACCT,CPLACCT   Restore previous account                       
         CLEAR CPLACCT           No return nesting allowed                      
         END                                                                    
         PEND                                                                   
                                                                                
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
