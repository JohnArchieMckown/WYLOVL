ORV      TITLE 'WYLBUR''s ORVYL Interface'                                      
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLORV   CSECT                                                                  
ORV      IDENT 2025                11:49:29 01/25/02  (SLP)                     
         SPACE 2                                                                
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
         SYSDEFN ,                 Define installation params                   
         SPACE 2                                                                
         PUSH  DSECTS                                                           
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
         EJECT                                                                  
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         POP   DSECTS                                                           
         TITLE 'WYLBUR''s ORVYL Interface'                                      
WYLORV   CSECT                                                                  
*box                                                                            
*                                                                               
*  PASS - pass command to orvyl                                                 
*                                                                               
*  on entry:                                                                    
*       command pointed to by scan control block                                
*                                                                               
*  comments:                                                                    
*       command is passed to ORVYL in CPCMD, and CPCMDLEN.                      
*       ORVCMD routine does not return unless ORVYL is unavailable.             
*                                                                               
PASS     XPROC                                                                  
         OSCTELL ,                 Get ORVYL command loc, len                   
         IF    (R0,NP),CVABSENT                                                 
         STH   R0,CPCMDLEN         Set up CPCMD and CPCMDLEN                    
         LR    R2,R0                                                            
         MOVE  R2,CPCMD,@R1                                                     
         ACALL ORVCMD              Pass command to ORVYL                        
         TSEG  (R1),(R0)           Only returns if ORVYL is down                
         TSEG  ': unrecognized command. '                                       
         B     CVERROR                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVCMD -- Routine to pass the current command to ORVYL.                      
*                                                                               
*    This routine only returns if ORVYL is not active.                          
*                                                                               
ORVCMD   XPROC                                                                  
         IF    (CPORVCMD,Z),BEGIN  First communications with ORVYL...           
         SET   CPFREDT             Assume a read edit                           
         END                                                                    
*                                                                               
         CLEAR CPOFRPASS           Sending regular read edit                    
*                                                                               
         IF    CPFREDT,REDTFIN     Finish the read edit                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVCPASS -- Routine to pass the command to ORVYL because it is               
*    possibly a SPIRES command.  SPIRES tells us it wants                       
*    "syntactically incorrect" Wylbur commands passed to it,                    
*    normally a READ EDIT only passes completely unrecognized                   
*    commands.                                                                  
*                                                                               
*    This routine returns if we didn't pass the command to ORVYL.               
*                                                                               
ORVCPASS XPROC                                                                  
*-                                                                              
*-       Only pass commands back if Spires pass flag is set.                    
*-                                                                              
         IF    CPOFNPAS,BEGIN      Only pass if Spires flag set...              
         IF    CPGFNOSPIEDIT,EXIT  Ignore Spires bit in mode word               
         IF    CPFTRY,EXIT         Don't pass if TRY prefix                     
         IF    (CPRFLG,NZ),EXIT    Resources owned, can't pass                  
*                                                                               
         IF    (CPORVCMD,Z),BEGIN  First communications with ORVYL...           
         SET   CPFREDT             Assume a read edit                           
         END                                                                    
*                                                                               
         SET   CPOFRPASS           Sending special read edit                    
*                                                                               
         IF    CPFREDT,REDTFIN     Finish the read edit                         
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVCABRT -- Routine to abort the READ EDIT.  This is done                    
*    by sending a zero length command in response to the                        
*    READ EDIT.  (This routine is called by the command                         
*    processor when it detects a ?break.)                                       
*                                                                               
ORVCABRT XPROC                                                                  
         CLEAR CPCMDLEN                                                         
         ACALL ORVCMD              Send response to ORVYL                       
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVRETN - Return to ORVYL                                                    
*                                                                               
*  ORVYL passed us a WYLBUR command (WRITE MODE=EDIT).  We                      
*  executed it and now we pass control back to ORVYL.  This                     
*  routine does not return.                                                     
*                                                                               
ORVRETN  XPROC                                                                  
*                                                                               
         COMMENT                   CHECK TO MAKE SURE ALL COOL                  
*        IF    CPFEXEC,BEGIN       PASSED CMDS ARE NOT EXEC COMMANDS            
*        CLEAR CPFEXEC                                                          
*        CLEAR CPFVCMD                                                          
*        BOMB                                                                   
*        END                                                                    
*                                                                               
         COMMENT                   PASS CONTROL BACK TO ORVYL                   
         IF    CPFWEDT,BEGIN       (IE. COMPLETE WRITE MODE=EDIT)               
         CLEAR CPFVCMD                                                          
         ACALL WEDTFIN             DOES NOT RETURN                              
         END                                                                    
*                                                                               
         COMMENT                   IF NOT WRITE MODE=EDIT                       
         COMMENT                   WE ARE NOT SYNCHRONIZED W/ORVYL              
         IF    ^CPFWEDT,BEGIN      we return to wylbur and keep going           
         CLEAR CPFVCMD                                                          
*        BOMB                                                                   
         COMMENT                   Keep on going.                               
         TSEG  'ORVYL/WYLBUR re-synchronized.'                                  
         TWRITE                                                                 
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVCHK -- Routine to check if a command from ORVYL is still                  
*    outstanding.   That is, if ORVYL still waiting for us to                   
*    return control back to ORVYL and we are out of sync for                    
*    reason, we get back in sync by returning ORVYL an abend                    
*    completion code.                                                           
*                                                                               
*   NOTE: this routine should not be necessary, but we keep it                  
*    just in case it is.  We log all out of sync w/ORVYL events.                
*                                                                               
ORVCHK   XPROC                                                                  
*                                                                               
         COMMENT                   If extended exec, do not return              
         COMMENT                   control to ORVYL until done.                 
         IF    ^CPFXTEND,BEGIN                                                  
*                                                                               
         IF    ((CPORVCMD,NZ),AND,^CPFREDT),BEGIN                               
*-                                                                              
*-  Due to some sort of error, a previous ORVYL request has not been            
*-      sent a completion.  We must tell ORVYL to abort that request            
*-     before we can continue with a new command, otherwise we would            
*-       be out of synch.                                                       
*-                                                                              
         TCLEAR                                                                 
         LA    R15,=XL8'00'        Closest guess for an abort                   
         IF    CPFRTEXT,'LA R15,=X"0400000000000000"'  rtxt abort               
         IF    CPFWTEXT,'LA R15,=X"1800000000000000"'  wtxt abort               
         CLEAR CPORV               Reset ORVYL control info                     
         TXCTL CPORVYL,(R15)       Send abort completion                        
         IF    NZ,BEGIN            No ORVYL, forget it                          
         B     CVNEXT                                                           
         END                                                                    
         ACALL ORVRQST             Handle the next ORVYL request                
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVRQST -- Routine to process a request from ORVYL.                          
*                                                                               
*    (Entered from the command processor.)                                      
*                                                                               
ORVRQST  GENTER                                                                 
ORVNEXT  VCALL INITCMD             Initialize                                   
*                                                                               
         IF    (CPSYS,EQ,CPORVYL),BEGIN  From ORVYL...                          
         IF    (CPSYS,Z),EXIT      No ORVYL access allowed                      
*                                                                               
*                                                                               
         CLEAR R15                                                              
         IF    CPFCNTL,'L R15,=A(CONTROL)'  Control                             
         IF    CPFREDT,'L R15,=A(RDEDIT)'   Read edit                           
         IF    CPFWEDT,'L R15,=A(WREDIT)'   Write edit                          
         IF    CPFRTEXT,'L R15,=A(RDTEXT)'  Read text                           
         IF    CPFWTEXT,'L R15,=A(WRTEXT)'  Write text                          
         IF    CPFRCTRL,'L R15,=A(RDCTRL)'  Read control (special)              
         IF    (R15,NZ),BEGIN                                                   
         BASR  RAR,R15             Process request                              
         BZ    ORVNEXT             We have another ORVYL request                
         END                                                                    
         END                                                                    
*                                                                               
         TCLEAR                                                                 
         B     CVNEXT                                                           
*                                                                               
         DROP  BR                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
***                                                                             
***  READ EDIT Data Areas                                                       
***                                                                             
REDTORV  RECORD BEGIN              Read edit information from ORVYL             
         DS    X                   Command code                                 
*                                                                               
REDTOFLG FLAG                                                                   
         FLAG  (REDTRSEN,4)        - Resend last cmd (for logon)                
*                                                                               
REDTOFL2 FLAG                                                                   
         FLAG  REDTFCME            - Prev command in error                      
         FLAG  REDTFATT            - Prev command ended with attn               
*                                                                               
         DS    XL5                 Reserved                                     
         END                                                                    
         SPACE 2                                                                
REDTWYL  RECORD BEGIN              Read edit information for ORVYL              
REDTRET  FLAG                                                                   
         FLAG  (REDTFCMD,X'63',EQ)  - Command is being returned                 
*                                                                               
REDTFLAG FLAG                                                                   
         FLAG  REDTFNT             - Command is not-typed                       
*                                                                               
REDTLEN  DS    H                   Command text length                          
*                                                                               
         DS    XL4                 Reserved                                     
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RDEDIT -- Routine to process a READ EDIT request from ORVYL.                 
*                                                                               
*    On entry:                                                                  
*      CPORVINF - contains control information                                  
*                                                                               
*    There is no direct return from this routine.  Instead, when                
*    the command processor gets an unrecognized command it                      
*    (eventually) goes to REDTFIN.                                              
*                                                                               
RDEDIT   BASE                                                                   
         LA    R6,CPORVINF                                                      
         WITH  (REDTORV,R6),BEGIN                                               
         IF    REDTRSEN,BEGIN      Re-send read edit...                         
*-                                                                              
*-       We get here because we tried to pass a syntactically                   
*-         incorrect Wylbur command to Spires, and ORVYL died and               
*-         has since been restarted.  The "resend" request is the               
*-         first response we get from ORVYL.                                    
*-                                                                              
*-         We reset the ORVYL information and then write out an                 
*-         error message.                                                       
*-                                                                              
         IF    CPOFRPASS,BEGIN     Grrr...                                      
         CLEAR CPORV               Reset ORVYL info                             
         TSEG  CPORVYL,,T                                                       
         ABORT ' failure, command interrupted.'  What can we say                
         END                                                                    
*                                                                               
         ACALL ORVCMD              Send command back to ORVYL                   
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         IF    REDTFATT,BEGIN      Attn...                                      
         L     R6,CVCURJCB                                                      
         WITH  (JCB,R6),'SET JCBFATTN'  Set attn                                
         END                                                                    
         TCLEAR                                                                 
         IF    REDTFCME,CVERROR    Command error                                
         END                                                                    
         B     CVNEXT              Go start read edit                           
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*-                                                                              
*-       Handle the completion of the read edit.                                
*-                                                                              
REDTFIN  BASE                                                                   
         XPUSH ,,L'REDTWYL,PTR=R6                                               
         WITH  (REDTWYL,R6),BEGIN                                               
         CLEAR REDTWYL                                                          
         SET   REDTFCMD            Command is being returned                    
*                                                                               
         IF    ^CPFTYPED,'SET REDTFNT'  Not typed command                       
*                                                                               
         TCLEAR                                                                 
         VCALL PAGERES             Reset page mode                              
*                                                                               
         SETMSG CPCMD,LH:CPCMDLEN   Command text                                
         STH   R0,REDTLEN                                                       
*                                                                               
         TSEG  (R1),(R0)                                                        
*                                                                               
         TXCTL CPORVYL,REDTWYL     Send command to ORVYL                        
         IF    Z,'ACALL ORVRQST'   Process next request from ORVYL              
         END                                                                    
*-                                                                              
*-       We get here because we tried to pass a syntactically                   
*-         incorrect Wylbur command to Spires, and ORVYL has died.              
*-                                                                              
*-         This will only happen once because TXCTL zeroed CPORV                
*-         when it discovered that ORVYL died.                                  
*-                                                                              
         IF    CPOFRPASS,BEGIN     Grrr...                                      
         TSEG  CPORVYL,,T                                                       
         ABORT ' failure, command interrupted.'  What can we say                
         END                                                                    
*                                                                               
         VCALL CMDUNREC            Command is unrecognized                      
         DC    H'0'                (no return)                                  
         DROP  BR                                                               
         EJECT                                                                  
***                                                                             
***  WRITE EDIT Data Areas                                                      
***                                                                             
WEDTORV  RECORD BEGIN              Write edit information from ORVYL            
         DS    X                   Command code                                 
*                                                                               
WEDTOFLG FLAG                                                                   
         FLAG  (WEDTNTYP,4)        - Command is not-typed                       
*                                                                               
WEDTOLEN DS    H                   Command length                               
*                                                                               
         DS    XL4                 Reserved                                     
         END                                                                    
         SPACE 2                                                                
WEDTWYL  RECORD BEGIN              Write edit information for ORVYL             
WEDTRET  FLAG                                                                   
         FLAG  (WEDTCERR,16,EQ)    - Command error                              
*                                                                               
         DS    XL6                 Reserved                                     
*                                                                               
WEDTFLAG FLAG                                                                   
         FLAG  (WEDTATTN,4)        - Attn occurred                              
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WREDIT -- Routine to process a WRITE EDIT request from ORVYL.                
*                                                                               
*    On entry:                                                                  
*      CPORVINF - contains control information                                  
*                                                                               
*    There is no direct return from this routine.  Instead, when                
*    the command passed by the WRITE EDIT request has completed,                
*    control (eventually) goes to WEDTFIN.                                      
*                                                                               
WREDIT   BASE                                                                   
         LA    R6,CPORVINF                                                      
         WITH  (WEDTORV,R6),BEGIN                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'CLEAR JCBFATTN'                                        
         TCLEAR                                                                 
*                                                                               
         COMMENT                   ORVYL in control                             
         SET   CPFVCMD             Command for WYLBUR from ORVYL                
*                                                                               
         SET   CPFTYPED            Assume it's a typed command                  
         IF    WEDTNTYP,'CLEAR CPFTYPED'  It's not typed                        
*                                                                               
         SETMSG L:CPSEGLOC,LH:WEDTOLEN  Command text                            
         VCALL EDTCOMMV            Execute the command                          
         END                                                                    
         DC    H'0'                (No return)                                  
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*-                                                                              
*-       Handle the completion of the write edit.                               
*-                                                                              
*-       This routine returns control to ORVYL after ORVYL has                  
*-       passed a command to WYLBUR to execute.  This routine                   
*-       does not return.                                                       
*-                                                                              
WEDTFIN  BASE                                                                   
*                                                                               
         COMMENT                   INITIALIZE WORK AREAS                        
         XPUSH ,,L'WEDTWYL,PTR=R6                                               
         USING WEDTWYL,R6                                                       
         CLEAR WEDTWYL                                                          
         L     R5,CVCURJCB                                                      
         USING JCB,R5                                                           
*                                                                               
         COMMENT                   Reset Cmd from ORVYL flag                    
         CLEAR CPFVCMD                                                          
*                                                                               
         COMMENT                   IF ERROR, SET ERR RETURN FOR ORVYL           
         IF    (JCBFATTN,OR,CPFERROR,OR,CPFABORT),BEGIN                         
         IF    CPFERROR,'SET WEDTCERR'  Command error                           
         IF    CPFABORT,'SET WEDTCERR'  Command error                           
         IF    JCBFATTN,'SET WEDTATTN'  Attn occurred                           
         CLEAR JCBFATTN,CPFABORT+CPFERROR,CPGFNCMD                              
         END                                                                    
*                                                                               
         COMMENT                   RETURN TO ORVYL                              
         TCLEAR                                                                 
         TXCTL CPORVYL,WEDTWYL     Send completion to ORVYL                     
         IF    Z,BEGIN             Process next request from ORVYL              
         ACALL ORVRQST                                                          
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         DROP  BR                                                               
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
***                                                                             
***  READ TEXT Data Areas                                                       
***                                                                             
RTXTORV  RECORD BEGIN              Read text information from ORVYL             
         DS    X                   Command code                                 
*                                                                               
RTXTOFLG FLAG                                                                   
         FLAG  RTXTFNLP            - Don't use locked page passing              
*                                                                               
         DS    XL4                 Reserved                                     
*                                                                               
RTXTRLEN DS    H                   Length of user's receiving area              
         END                                                                    
         SPACE 2                                                                
RTXTWA   RECORD BEGIN                                                           
*-                                                                              
*-       Read text information for ORVYL.                                       
*-                                                                              
RTXTWYL  EQU   *,8,C'X'            Read text information for ORVYL              
RTXTRET  FLAG                                                                   
         FLAG  (RTXTROK,0,EQ)      - Success                                    
         FLAG  (RTXTRNOL,4,EQ)     - No lines in range                          
         FLAG  (RTXTRTOS,8,EQ)     - User area is too small                     
         FLAG  (RTXTRLKP,100,EQ)   - Get ORVYL buffer addr/len                  
*                                                                               
RTXTFLAG FLAG                                                                   
*                                                                               
RTXTLEN  DS    H                   Length of text returned                      
RTXTNUM  DS    H                   Number of lines returned                     
         DS    H                   Reserved                                     
*-                                                                              
*-       Additional READ TEXT information from ORVYL.                           
*-                                                                              
RTXTINF  EQU   *,18,C'X'                                                        
RTXTOPT  FLAG                                                                   
         FLAG  (RTXTOPT0,0,EQ)     - Return RTXTFLIN line                       
         FLAG  (RTXTOPT1,1,EQ)     - Return line before RTXTFLIN                
         FLAG  (RTXTOPT2,2,EQ)     - Return line after RTXTFLIN                 
         FLAG  (RTXTOPT3,3,EQ)     - Return RTXTFLIN/RTXTLLIN                   
         FLAG  (RTXTOPT4,4,EQ)     - Return lines which match string            
         FLAG  (RTXTOPT5,5,EQ)     - Option 4 with starting column              
         FLAG  (RTXTOPT6,6,EQ)     - Option 4 with fcolumn/lcolumn              
*                                                                               
         FLAG  (RTXTFNOT,X'80')    - Notext (just line-numbers)                 
         FLAG  (RTXTFNUM,X'40')    - Card with lineno in 73/80                  
         FLAG  (RTXTFINT,X'20')    - Card with int lineno in 73/80              
*                                                                               
RTXTLREC DS    X                   Lrecl (0=none specified)                     
RTXTMAXL DS    H                   Maximum number of lines to return            
RTXTFLIN DS    F                   Starting lineno                              
RTXTLLIN DS    F                   Ending lineno                                
RTXTFCOL DS    H                   Starting column (option 5,6)                 
RTXTLCOL DS    H                   Ending column (option 6)                     
RTXTSLEN DS    H                   String length (option 4,5,6)                 
RTXTSTR  DS    0C                  String                                       
*-                                                                              
*-       Local work area.                                                       
*-                                                                              
RTXTSTKP DS    A                   Saved stack ptr                              
RTXTLFLG FLAG                                                                   
         FLAG  RTXTLUNP            - Return unpressed lines                     
         FLAG  RTXTLINT            - Seg buffer has been initialized            
         FLAG  RTXTLTRN            - Buffer was truncated                       
*                                                                               
RTXTLINE DS    CL(&LINESZ)         Working line buffer                          
         DS    XL(&PRESSZ-&LINESZ)                                              
*                                                                               
RTSG     SEGCB                                                                  
RTXTBUF  DS    XL256               Working buffer                               
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RDTEXT -- Routine to process a READ TEXT request from ORVYL.                 
*                                                                               
*    On entry:                                                                  
*      CPORVINF - contains control information                                  
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - we have another request from ORVYL                                   
*      nz- ORVYL is not active (see TXCTL return codes)                         
*                                                                               
RDTEXT   XENTER R2,R8,,LOCAL                                                    
RDBASE   LABEL                                                                  
         XPUSH ,,L'RTXTWA,PTR=R6                                                
         ST    R6,CPCWA                                                         
         USING RTXTWA,R6                                                        
         CLEAR RTXTWA                                                           
*                                                                               
         ST    WAR,RTXTSTKP        Save stack ptr (in case of abort)            
*                                                                               
         L     R15,CPSEGLOC                                                     
         MVC   RTXTINF,@R15        Save READ TEXT info                          
*-                                                                              
*-       Process READ TEXT request.                                             
*-                                                                              
         IF    RTXTFNUM+RTXTFINT,'MVI RTXTLREC,80'  Implies lrecl=80            
*                                                                               
         IF    (RTXTFLIN,EQ,-1),BEGIN  Lineno after last read edit...           
         LA    R15,1                                                            
         A     R15,CPORVLNO        Next lineno                                  
         ST    R15,RTXTFLIN                                                     
         END                                                                    
*                                                                               
         IF    (RTXTLLIN,Z),'MVC RTXTLLIN,CVMAXLNO'  Last lineno                
*                                                                               
         IF    ^CPOFPRST,'SET RTXTLUNP'  Unpressed format                       
         IF    (RTXTLREC,NZ),'SET RTXTLUNP'                                     
*                                                                               
         IC    R5,RTXTOPT          Option code                                  
         N     R5,=A(X'F')                                                      
         CEIL  R5,6                Allow 0..6                                   
*-                                                                              
*-       Option 0:  Return line specified.                                      
*-                                                                              
         IF    (R5,Z),BEGIN                                                     
         L     R15,RTXTFLIN        Lineno                                       
         SETMSG RTXTLINE            Line text area                              
         VCALL GETLINE             Get line text                                
         BNZ   RDTDONE             Not an exact match, scram                    
         L     R15,CPLCNO                                                       
         LCALL RDTLINE                                                          
         B     RDTDONE                                                          
         END                                                                    
*-                                                                              
*-       Option 1:  Return line before line-number specified.                   
*-                                                                              
         IF    (R5,EQ,1),BEGIN                                                  
         MVC   CPLCNO,RTXTFLIN                                                  
         VCALL PREVLNO             Get previous lineno                          
         BM    RDTDONE                                                          
         L     R15,CPLCNO          Line-number                                  
         SETMSG RTXTLINE            Text area                                   
         VCALL GETLINE             Get line text                                
         L     R15,CPLCNO          Line-number                                  
         LCALL RDTLINE                                                          
         B     RDTDONE                                                          
         END                                                                    
*-                                                                              
*-       Option 2:  Return line after line-number specified.                    
*-                                                                              
         IF    (R5,EQ,2),BEGIN                                                  
         LA    R15,1                                                            
         A     R15,RTXTFLIN        Next lineno                                  
         SETMSG RTXTLINE            Line text area                              
         VCALL GETLINE                                                          
         BM    RDTDONE             Past end of file, scram                      
         L     R15,CPLCNO                                                       
         LCALL RDTLINE                                                          
         B     RDTDONE                                                          
         END                                                                    
*-                                                                              
*-       Option 3:  Return range of lines specified.                            
*-                                                                              
         IF    (R5,EQ,3),BEGIN                                                  
         L     R4,RTXTFLIN                                                      
         LOOP  BEGIN               Go through lines...                          
         LR    R15,R4                                                           
         SETMSG RTXTLINE                                                        
         VCALL GETLINE                                                          
         IF    NEG,EXIT                                                         
*                                                                               
         L     R15,CPLCNO                                                       
         IF    (R15,GT,RTXTLLIN),EXIT  Past end, scram                          
*                                                                               
         LCALL RDTLINE             Add line to the buffer                       
         IF    NZ,EXIT                                                          
         LA    R4,1                                                             
         A     R4,CPLCNO           Next lineno                                  
         END                                                                    
         B     RDTDONE                                                          
         END                                                                    
*-                                                                              
*-       Option 4:  Return lines which match the associative range.             
*-       Option 5:  Same as option 4, but with a starting column.               
*-       Option 6:  Same as option 4, but with fcolumn/lcolumn.                 
*-                                                                              
         IF    ((R5,GE,4),AND,(R5,LE,6)),BEGIN                                  
         IF    (CPALNCT,Z),RDTDONE  Nothing, scram                              
*                                                                               
         SEGINIT RTXTBUF,,RTSG                                                  
         SEG   '"'                                                              
         L     R2,CPSEGLOC                                                      
         SEG   RTXTSTR-RTXTINF(R2),LH:RTXTSLEN  String                          
         SEG   '"'                                                              
         IF    (R5,GE,5),'SEGDC LH:RTXTFCOL'  First column                      
         IF    (R5,EQ,6),'SEG "/"; SEGDC LH:RTXTLCOL'  Last column              
         SEG   ' IN '                                                           
         L     R0,RTXTFLIN                                                      
         VCALL CVEXNO                                                           
         SEG   (R1),(R0)                                                        
         SEG   '/'                                                              
         L     R0,RTXTLLIN                                                      
         VCALL CVEXNO                                                           
         SEG   (R1),(R0)                                                        
*                                                                               
         OSCINIT RTXTBUF,L:RTSGLENF  Range, as a user would type it             
         VCALL ZDETRNG             Scan range                                   
*                                                                               
         L     R15,RDTXTWRK                                                     
         VCALL DESPOT              (Co-routine is RDTXTRTN)                     
         B     RDTDONE                                                          
         END                                                                    
*-                                                                              
*-       Send response to ORVYL with lines.                                     
*-                                                                              
RDTDONE  TCLEAR                                                                 
*                                                                               
         SET   RTXTRNOL            Assume no lines                              
*                                                                               
         IF    RTXTLINT,BEGIN      We have some lines...                        
         SET   RTXTROK                                                          
         IF    ((RTXTNUM,Z),AND,RTXTLTRN),'SET RTXTRTOS'  Too small             
         END                                                                    
*-                                                                              
*-      For the new ORVYL the read text data is returned in the PDB.            
*-                                                                              
         CLEAR R4                  Flag: Old ORVYL                              
         LA    R5,CPORVINF                                                      
         WITH  (RTXTORV,R5),BEGIN                                               
         IF    RTXTLINT,BEGIN      We have some lines...                        
         IF    ^RTXTFNLP,EXIT      Old style, scram                             
         L     R0,RTSGLENF         Amount of data we need to seg                
         IF    (R0,GT,CPSEGMAXF),'VCALL TSEGBUF'  Allocate larger               
         TSEG  L:RTSGLOC,L:RTSGLENF  Seg READ TEXT data                         
         LA    R4,1                Flag: New ORVYL                              
         END                                                                    
         END                                                                    
*-                                                                              
*-     For the old ORVYL the read text data is in the CSA buffer and            
*-       only the current line-number is returned.                              
*-                                                                              
         IF    (R4,Z),BEGIN        Old ORVYL...                                 
         TSEG  CPORVLNO            Compatability--report last lineno            
         END                                                                    
*                                                                               
*                                                                               
         TXCTL CPORVYL,RTXTWYL     Send response to ORVYL                       
         L     WAR,RTXTSTKP        Restore stack ptr                            
         XEXIT ,,LTR                                                            
         DROP  BR                                                               
         EJECT                                                                  
         DS    0F                                                               
RDTXTWRK DC    AL1(DESRTRN+LOCATRTN+UNPRST)                                     
         DC    AL3(RDTXTRTN)                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  RDTXTRTN -- Local routine to process lines from the range                    
*    processor for complicated READ TEXT requests (e.g.,                        
*    associative ranges).                                                       
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
RDTXTRTN PROC                                                                   
         BASE  RDBASE                                                           
         L     R6,CPCWA                                                         
*                                                                               
         L     R15,CPLCNO                                                       
         LCALL RDTLINE             Add line to buffer                           
         BNZ   RDTDONE             Abort                                        
         PEND                                                                   
         EJECT                                                                  
RDTLWA   RECORD BEGIN                                                           
RDTLBYTE DS    X                   Work byte                                    
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  RDTLINE -- Local routine to add a line to the read text buffer.              
*                                                                               
*    On entry:                                                                  
*      R15 - line-number                                                        
*      R1,R0 - line text loc, len                                               
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      4 - buffer is full                                                       
*      8 - all done                                                             
*                                                                               
RDTLINE  PROC  RDTLWA                                                           
         LR    R3,R1               Text location...                             
         LR    R2,R0               ...and length                                
*-                                                                              
*-       Initialize the seg buffer, if not already initialized.                 
*-                                                                              
         LCALL RDTINIT                                                          
         BNZ   RDLEXIT                                                          
*-                                                                              
*-       Add the line to the read text buffer.                                  
*-                                                                              
         IF    (RTXTLREC,Z),BEGIN                                               
         SEG   CPLCNO              Seg lineno                                   
         BNZ   RDLFULL                                                          
         END                                                                    
*-                                                                              
*-       Return line text in appropriate format.                                
*-                                                                              
         IF    ^RTXTFNOT,BEGIN     Return line text...                          
         IF    RTXTFNUM+RTXTFINT,BEGIN  Add lineno...                           
         IF    RTXTFINT,'SET CPLFLG5.CPNFINT'                                   
         L     R0,CPLCNO                                                        
         VCALL CVEXNO              Make a printable lineno                      
         MVC   @R3+72(8),CPDOUB+1                                               
         CLEAR CPLFLG5.CPNFINT                                                  
         END                                                                    
*-                                                                              
*-       Return a regular (unpressed) line.                                     
*-                                                                              
         IF    RTXTLUNP,BEGIN      Unpressed...                                 
         IF    (RTXTLREC,NZ),'LC R2,RTXTLREC'                                   
*                                                                               
         ELSE  BEGIN               Add length byte to buffer...                 
         STC   R2,RDTLBYTE                                                      
         SEG   RDTLBYTE,1          Seg length byte                              
         BNZ   RDLFULL                                                          
         END                                                                    
*                                                                               
         SETMSG (R3),(R2)           Assume regular line text                    
         END                                                                    
*-                                                                              
*-       For compatibility, if the user wants a pressed line we will            
*-         press the line.                                                      
*-                                                                              
         ELSE  BEGIN               Otherwise return pressed line...             
         L     R15,CPLCNO                                                       
         SETMSG (R3),(R2)                                                       
         VCALL PRESS               Make a pressed line                          
         LA    R1,@R1+4            Point past lineno                            
         A     R0,=F'1'            Length must include length byte              
         END                                                                    
*                                                                               
         SEG   (R1),(R0)           Add line text                                
         BNZ   RDLFULL                                                          
         END                                                                    
*                                                                               
         MVC   CPORVLNO,CPLCNO     Remember last lineno                         
         MVC   RTXTLEN,RTSGLENF+2  Save length                                  
         INCR  R15,RTXTNUM         No. of lines                                 
         CLEAR R15                 Assume good rc                               
*                                                                               
         IF    ('LTH R0,RTXTMAXL',POS),BEGIN  Counting the lines...             
         DECR  R0                  One less now                                 
         IF    (R0,Z),'LA R15,8'   All done                                     
         STH   R0,RTXTMAXL                                                      
         END                                                                    
         B     RDLEXIT                                                          
*                                                                               
RDLFULL  SET   RTXTLTRN            Truncated response                           
         LA    R15,4                                                            
RDLEXIT  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RDTINIT -- Local routine to initialize the READ TEXT buffer.                 
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok                                                                   
*      8 - ORVYL died (or something strange)                                    
*                                                                               
*                                                                               
RDTINIT  XENTER  R2,R8,,LOCAL                                                   
         IF    ^RTXTLINT,BEGIN     Not yet initialized...                       
         LA    R5,CPORVINF                                                      
         WITH  (RTXTORV,R5)                                                     
*-                                                                              
*-      ORVYL 5.0 does not use the old "locked page" passing scheme.            
*-    All READ TEXT requests coming from the new ORVYL will have the            
*-       "no locked page passing" flag on.                                      
*-                                                                              
         IF    RTXTFNLP,BEGIN      No locked page passing mode...               
         LH    R0,RTXTRLEN                                                      
         IF    (R0,NEG),'CLEAR R0'                                              
         IF    (R0,GT,=A(8192)),'L R0,=A(8192)'  Universal maximum              
         VCALL GETHEAP             Get local heap space for buffer              
         SEGINIT (R1),(R0),RTSG                                                 
         END                                                                    
*                                                                               
         ELSE  BEGIN               Locked page style...                         
*-                                                                              
*- ORVYL has a READ TEXT buffer in CSA (commonly addressable memory).           
*-   We use that buffer as our seg buffer for the read text results.            
*-   Passing data this way instead of sending it directly in the PDB            
*-      is called a "locked page pass" for historical reasons.  Some            
*-    poor soul probably even thought this was a temporary solution.            
*-                                                                              
         SET   RTXTRLKP                                                         
         TXCTL CPORVYL,RTXTWYL     Get buffer ptr from ORVYL                    
         BNZ   RDIEXIT             (ORVYL died)                                 
*                                                                               
         LA    R15,8               Assume bad rc                                
         IF    ^CPFLKPP,RDIEXIT    (something weird happened)                   
         IF    (R0,NE,12),RDIEXIT  (response length is wrong)                   
*                                                                               
         LR    R2,R1                                                            
         SEGINIT L:@R2,LH:@R2+8,RTSG                                            
         END                                                                    
*                                                                               
*                                                                               
         SET   RTXTLINT            Seg buffer is initialized                    
         END                                                                    
*                                                                               
*                                                                               
         CLEAR R15                                                              
RDIEXIT  XEXIT ,,LTR                                                            
*                                                                               
         DROP  RTXTWA,BR                                                        
         EJECT                                                                  
***                                                                             
***  WRITE TEXT Data Areas                                                      
***                                                                             
WTXTORV  RECORD BEGIN              Write text information from ORVYL            
         DS    X                   Command code                                 
*                                                                               
WTXTOFLG FLAG                                                                   
         FLAG  (WTXTFLKP,X'04')    - PDB contains buffer ptr                    
*                                                                               
WTXTLEN  DS    H                   Total text length                            
WTXTNUM  DS    H                   No. of lines being passed/deleted            
*                                                                               
WTXTOPT  FLAG                                                                   
         FLAG  (WTXTOPT0,0,EQ)     - Insert/replace lines                       
         FLAG  (WTXTOPT1,1,EQ)     - Replace lines only                         
         FLAG  (WTXTOPT2,2,EQ)     - Insert lines only                          
         FLAG  (WTXTOPT3,3,EQ)     - Delete lines                               
         FLAG  (WTXTOPT4,4,EQ)     - LRECL option (append lines)                
         FLAG  (WTXTOPT5,5,EQ)     - NUMBERED option (insert only)              
         FLAG  (WTXTOPT6,6,EQ)     - (Same as WTXTOPT5)                         
*                                                                               
WTXTLREC DS    X                   Lrecl                                        
*                                                                               
         END                                                                    
         SPACE 2                                                                
WTXTWA   RECORD BEGIN                                                           
*-                                                                              
*-       Write text information for ORVYL.                                      
*-                                                                              
WTXTWYL  EQU   *,8,C'X'            Write text information for ORVYL             
WTXTRET  FLAG                                                                   
         FLAG  (WTXTROK,0,EQ)      - Success                                    
         FLAG  (WTXTRLEN,4,EQ)     - Success; lines over length                 
         FLAG  (WTXTRTRN,8,EQ)     - Lines were truncated                       
         FLAG  (WTXTRFUL,12,EQ)    - ACTIVE file is full                        
         FLAG  (WTXTRNOL,16,EQ)    - No line exists                             
         FLAG  (WTXTRDUP,20,EQ)    - Line already exists                        
         FLAG  (WTXTRBAD,24,EQ)    - Illegal control number/text                
         FLAG  (WTXTRBLN,28,EQ)    - Lineno too big                             
*                                                                               
WTXTFLAG FLAG                                                                   
*                                                                               
         DS    XL4                 Reserved                                     
*                                                                               
WTXTRES  DS    H                   Residual byte count                          
*-                                                                              
*-       Local work area.                                                       
*-                                                                              
WTXTPTRS DS    2A                  Working data len, loc                        
WTXTLBUF DS    CL(&PRESSZ)         Working line buffer                          
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRTEXT -- Routine to process a WRITE TEXT request from ORVYL.                
*                                                                               
*    On entry:                                                                  
*      CPORVINF - contains control information                                  
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - we have another request from ORVYL                                   
*      nz- ORVYL is not active (see TXCTL return codes)                         
*                                                                               
WRTEXT   XENTER R2,R8,,LOCAL                                                    
         XPUSH ,,L'WTXTWA,PTR=R6                                                
         USING WTXTWA,R6                                                        
         CLEAR WTXTWA                                                           
*                                                                               
         LA    R5,CPORVINF                                                      
         USING WTXTORV,R5                                                       
*                                                                               
         SETMSG L:CPSEGLOC,LH:WTXTLEN  data ptrs                                
         IF    WTXTFLKP,'L R1,@R1'  locked page pass                            
         STM   R0,R1,WTXTPTRS      Save data len, loc                           
*-                                                                              
*-       Process WRITE TEXT request.                                            
*-                                                                              
         LC    R4,WTXTOPT          Option code                                  
         CEIL  R4,6                Allow 0..6                                   
*-                                                                              
*-       Option 0:  Insert or Replace lines.                                    
*-       Option 1:  Replace lines only.                                         
*-       Option 2:  Insert lines only.                                          
*-                                                                              
         IF    ((R4,GE,0),AND,(R4,LE,2)),BEGIN                                  
         VCALL ACTFULL             Is the Active file (almost) full?            
         IF    NZ,BEGIN            Yes...                                       
         SET   WTXTRFUL            Active file is full                          
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
         IF    (R4,EQ,1),'SET CPLFLG4.CPFSTREP'                                 
         IF    (R4,EQ,2),'SET CPLFLG4.CPFSTINS'                                 
*                                                                               
         LOOP  BEGIN                                                            
         LCALL WRTLINE             Get a (pressed) line                         
         BNZ   WRTEXIT             All done, scram                              
*                                                                               
         IF    (CPALNCT,Z),BEGIN   First line...                                
         IF    (CPANAME,EQ,=CL8'NEW'),'MVC CPANAME,=CL8"ORVYL"'                 
         IF    (CPATITL,Z),BEGIN                                                
         MVC   CPATITL,=CL(L'CPATITL)'Text from ORVYL'                          
         IF    ((CPSVPGM,Z),OR,(CPSVPGM,EQ,' ')),EXIT                           
         IF    (CPSVPGM,EQ,'DISPLAY'),EXIT  (Avoid embarrassing msg)            
         MVI   CPATITL+15,C'/'                                                  
         MVC   CPATITL+16(8),CPSVPGM  Program name                              
         END                                                                    
         END                                                                    
*                                                                               
         VCALL STOW                Add it to the ACTIVE file                    
         IF    NZ,BEGIN            Can't insert/replace...                      
         SET   WTXTRDUP            Already exists                               
         IF    (R4,EQ,1),'SET WTXTRNOL'  ...or not found                        
         B     WRTEXIT                                                          
         END                                                                    
         END                                                                    
         B     WRTEXIT                                                          
         END                                                                    
*-                                                                              
*-       Option 3:  Delete lines.                                               
*-                                                                              
         IF    (R4,EQ,3),BEGIN                                                  
         LOOP  BEGIN                                                            
         LM    R2,R3,WTXTPTRS                                                   
         IF    (R2,LT,4),EXIT      All done, scram                              
         L     R0,@R3                                                           
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete line                                  
         IF    NZ,BEGIN            Line doesn't exist...                        
         SET   WTXTRNOL                                                         
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
         LA    R3,@R3+4            Next line-number                             
         SH    R2,=H'4'                                                         
         STM   R2,R3,WTXTPTRS      Save updated ptrs (in case abort)            
         END                                                                    
         B     WRTEXIT                                                          
         END                                                                    
*-                                                                              
*-       Option 4:  Append lines.                                               
*-      Option 5:  Insert lines (line-numbers are in columns 73/80).            
*-       Option 6:  (Same as option 5.)                                         
*-                                                                              
         IF    ((R4,GE,4),AND,(R4,LE,6)),BEGIN                                  
         VCALL ACTFULL             Is the Active file (almost) full?            
         IF    NZ,BEGIN            Yes...                                       
         SET   WTXTRFUL            Active file is full                          
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
         LC    R15,WTXTLREC        LRECL to be used                             
         IF    ((R15,Z),OR,(R15,GT,&LINESZ)),BEGIN  Out of bounds...            
         SET   WTXTRBAD                                                         
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
         LOOP  BEGIN               Go through buffer...                         
         LC    R15,WTXTLREC                                                     
         LM    R0,R1,WTXTPTRS      Current buffer len, loc                      
         IF    (R0,LT,R15),EXIT    All done, scram                              
         MOVE  R15,WTXTLBUF,@R1                                                 
*                                                                               
         IF    (R4,EQ,4),BEGIN     Calculate end lineno...                      
         VCALL ENDLNO              Get END line-number                          
         LR    R15,R0              Lineno                                       
         SETMSG WTXTLBUF,LC:WTXTLREC                                            
         END                                                                    
*                                                                               
         ELSE  BEGIN               Numbered card image...                       
         SETMSG WTXTLBUF+72,8       Columns 73/80                               
         LCALL WRTLNO              Get line-number                              
         SETMSG WTXTLBUF,72                                                     
         END                                                                    
*                                                                               
         IF    ((R15,NEG),OR,(R15,GT,CVMAXLNO)),BEGIN  Bad lineno...            
         SET   WTXTRBAD            Bad line-number                              
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
         IF    (CPALNCT,Z),BEGIN   First line...                                
         IF    (CPANAME,EQ,=CL8'NEW'),'MVC CPANAME,=CL8"ORVYL"'                 
         IF    (CPATITL,Z),BEGIN                                                
         MVC   CPATITL,=CL(L'CPATITL)'Text from ORVYL'                          
         IF    ((CPSVPGM,Z),OR,(CPSVPGM,EQ,' ')),EXIT                           
         MVI   CPATITL+15,C'/'                                                  
         MVC   CPATITL+16(8),CPSVPGM  Program name                              
         END                                                                    
         END                                                                    
*                                                                               
         VCALL PRESS                                                            
         VCALL STOW                Add line to the ACTIVE file                  
         IF    NZ,BEGIN                                                         
         SET   WTXTRDUP            Line-number already exists                   
         B     WRTEXIT                                                          
         END                                                                    
*                                                                               
*                                                                               
         LM    R0,R1,WTXTPTRS                                                   
         LC    R15,WTXTLREC        Line length                                  
         AR    R1,R15                                                           
         SR    R0,R15                                                           
         STM   R0,R1,WTXTPTRS      Update buffer ptrs                           
         END                                                                    
         B     WRTEXIT                                                          
         END                                                                    
*-                                                                              
*-       Send response to ORVYL with completion code.                           
*-                                                                              
WRTEXIT  L     R0,WTXTPTRS         Residual length                              
         STH   R0,WTXTRES          Save it in reply                             
*                                                                               
         TXCTL CPORVYL,WTXTWYL     Send response to ORVYL                       
         XEXIT ,,LTR                                                            
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRTLINE -- Local routine to get a line from the WRITE TEXT                   
*    buffer.                                                                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - got a line; R1 - pressed line ptr                                    
*      nz- no lines left or error of some sort                                  
*                                                                               
WRTLINE  XENTER R2,R8,,LOCAL                                                    
         LM    R2,R3,WTXTPTRS                                                   
*                                                                               
         LA    R15,4               Assume eof rc                                
         IF    (R2,LT,5),WRLEXIT   All done, scram                              
*                                                                               
         IF    CPOFPRST,BEGIN      Line is already pressed...                   
         LC    R15,@R3+4           Pressed text length                          
         LA    R15,@R15+5                                                       
         CEIL  R15,&PRESSZ                                                      
         MOVE  R15,WTXTLBUF,@R3    Save line                                    
         LA    R1,WTXTLBUF                                                      
         VCALL EDITCK              Verify correct edit format                   
         IF    NZ,'SET WTXTRBAD; B WRLEXIT'                                     
         END                                                                    
*                                                                               
         ELSE  BEGIN               Line passed to us is unpressed...            
         LC    R15,@R3+4           Text length                                  
         CEIL  R15,&LINESZ                                                      
         MOVE  R15,WTXTLBUF,@R3+5  Save line                                    
*                                                                               
         L     R15,@R3             Line-number                                  
         SETMSG WTXTLBUF,LC:@R3+4                                               
         VCALL PRESS               Press the line                               
         END                                                                    
*                                                                               
         LC    R15,@R3+4           Text length                                  
         LA    R15,@R15+5          Total line length                            
         AR    R3,R15                                                           
         SR    R2,R15                                                           
         STM   R2,R3,WTXTPTRS      Updated buffer ptrs                          
*                                                                               
         LA    R1,WTXTLBUF                                                      
         CLEAR R15                 Good rc                                      
WRLEXIT  XEXIT ,,LTR                                                            
*                                                                               
         DROP  WTXTORV,WTXTWA,BR                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRTLNO -- Local routine to parse line-number from an input                   
*    string.                                                                    
*                                                                               
*    On entry:                                                                  
*      R1,R0 - text loc, len                                                    
*                                                                               
*    On exit:                                                                   
*      R15 - line-number (-1 if invalid)                                        
*                                                                               
WRTLNO   XENTER R2,R8,,LOCAL                                                    
         CLEAR R15                 Line-number accumulator                      
*                                                                               
         CLEAR R3                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,NE,' '),BEGIN                                               
         IF    ((@R1,GE,'0'),AND,(@R1,LE,'9')),BEGIN                            
         IC    R2,@R1                                                           
         N     R2,=A(X'F')                                                      
         MH    R15,=H'10'                                                       
         AR    R15,R2              Add in the next digit                        
         DECR  R3                                                               
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'.'),BEGIN  Decimal format...                           
         LA    R3,3                                                             
         END                                                                    
*                                                                               
         ELSE  BEGIN               This ain't no number...                      
         LH    R15,=H'-1'                                                       
         B     WLNOEXIT            Scram                                        
         END                                                                    
         END                                                                    
*                                                                               
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         WHILE (R3,POS),'MH R15,=H"10"; DECR R3'  Decimal adjust                
*                                                                               
WLNOEXIT XEXIT                                                                  
         DROP  BR                                                               
         EJECT                                                                  
***                                                                             
***  READ CONTROL Data Areas                                                    
***                                                                             
RCTLORV  RECORD BEGIN                                                           
RCTLCMD  DS    X                   Command code                                 
*                                                                               
RCTLOFLG FLAG                                                                   
*                                                                               
RCTLLEN  DS    H                   Input text length                            
*                                                                               
         DS    XL4                 Reserved                                     
         END                                                                    
         SPACE 2                                                                
RDCTRLWA RECORD BEGIN                                                           
RDCINP   DS    CL256               Input text                                   
*                                                                               
RDCOUT   DS    CL256               Output text                                  
RDCWYL   DS    XL(L'RCTLORV)       Output control info                          
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  RDCTRL -- Routine to process a "read control" from ORVYL.                    
*                                                                               
*    On entry:                                                                  
*      CPSEG buffer has read prompt text                                        
*                                                                               
*    Important Note:                                                            
*      ORVYL currently has no support in it for "read control"                  
*      (damn shame too).                                                        
*                                                                               
*      MILTEN changes any terminal read that begins with the                    
*      @@SYSEXPR? prefix to a XCTL to WYLBUR.  It also has                      
*      to change the XCTL from WYLBUR back into a PDB that looks                
*      like a response to a terminal read.  So, to ORVYL, it simply             
*      looks like a terminal read.                                              
*                                                                               
*      The really ugly part of this is in MILTEN's SIFT module.                 
*      If ORVYL ever were to support a "read control"                           
*      directly, none of this code would have to change, and the                
*      code in MILTEN could be removed.                                         
*                                                                               
RDCTRL   PROC  RDCTRLWA                                                         
         CLEAR RDCTRLWA                                                         
*                                                                               
         LA    R6,CPORVINF                                                      
         WITH  (RCTLORV,R6),'LH R5,RCTLLEN'                                     
         FLOOR R5,0                                                             
         CEIL  R5,L'RDCINP         Not too much                                 
         L     R1,CPSEGLOC                                                      
         LR    R15,R5                                                           
         MOVE  R15,RDCINP,@R1      Save input text                              
*                                                                               
         TCLEAR                                                                 
***                                                                             
***  @@SYSEXPR? -- Evaluate WYLBUR expression.                                  
***                                                                             
         IF    (RDCINP,EQ,'@@SYSEXPR?'),BEGIN                                   
         SETMSG RDCINP+10,(R5)      Actual expression text                      
         SH    R0,=H'10'                                                        
         LA    R2,RDCOUT                                                        
         LA    R3,L'RDCOUT                                                      
         VCALL EVALSTR             Evaluate expression                          
         IF    Z,BEGIN             A-ok...                                      
         TSEG  '0;'                Normal Return code                           
         TSEG  (R2),(R3)           Return expression                            
         END                                                                    
*                                                                               
         ELSE  BEGIN               Bad expression...                            
         L     R1,CPSEGLOC                                                      
         L     R2,CPSEGLENF                                                     
         FLOOR R2,0                                                             
         CEIL  R2,L'RDCOUT                                                      
         LR    R15,R2                                                           
         MOVE  R15,RDCOUT,@R1      Save the error message text                  
         TCLEAR                                                                 
*                                                                               
         TSEG  '1;'                Error return code                            
         IF    (R2,Z),'TSEG "Missing expression."'                              
         ELSE  'TSEG RDCOUT,(R2)'  Real error message                           
         END                                                                    
         B     RDCDONE             Finish up                                    
         END                                                                    
***                                                                             
***  Unrecognized control information.                                          
***                                                                             
         TSEG  '9;Unrecognized control information.'                            
*                                                                               
*                                                                               
RDCDONE  LA    R6,RDCWYL                                                        
         WITH  (RCTLORV,R6),BEGIN                                               
         CLEAR RCTLORV                                                          
         SET   RCTLCMD.CPFRCTRL    Responding to a READ CONTROL                 
         L     R15,CPSEGLENF                                                    
         STH   R15,RCTLLEN         Set data length                              
         END                                                                    
*                                                                               
         TXCTL CPORVYL,RDCWYL      Send response to ORVYL                       
         PEND                                                                   
         EJECT                                                                  
***                                                                             
***  CONTROL Data Areas                                                         
***                                                                             
CNTLORV  RECORD BEGIN              Control information from ORVYL               
         DS    X                   Command code                                 
*                                                                               
CNTLCODE FLAG                                                                   
         FLAG  (CNTLSTMW,12,EQ)    - Set WYLBUR mode word                       
         FLAG  (CNTLSNMW,13,EQ)    - Sense WYLBUR mode word                     
         FLAG  (CNTLSNAS,14,EQ)    - Sense ACTIVE file size                     
         FLAG  (CNTLSNFL,15,EQ)    - Sense first lineno in ACTIVE               
         FLAG  (CNTLSNLL,16,EQ)    - Sense last lineno in ACTIVE                
         FLAG  (CNTLCLRA,17,EQ)    - Clear ACTIVE file                          
         FLAG  (CNTLSNDL,28,EQ)    - Sense current DELTA                        
*                                                                               
         DS    XL4                 Reserved                                     
*                                                                               
CNTLMOD  DS    H                   WYLBUR mode word                             
         END                                                                    
         SPACE 2                                                                
CNTLWA   RECORD BEGIN                                                           
         XSA   R2,R8                                                            
*                                                                               
CNTLWYL  EQU   *,8,C'X'            Control information for ORVYL                
CNTLRET  FLAG                                                                   
         FLAG  (CNTLROK,0,EQ)      - Success                                    
         FLAG  (CNTLRNOL,4,EQ)     - No ACTIVE file                             
         FLAG  (CNTLRBAD,128,EQ)   - Bad control number                         
*                                                                               
CNTLFLAG FLAG                                                                   
*                                                                               
         DS    H                   Reserved                                     
*                                                                               
CNTLWORD DS    F                   Returned information word                    
CNTLINIT EQU   CNTLWYL,*-CNTLWYL,C'X'                                           
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CONTROL -- Routine to process a CONTROL request from ORVYL.                  
*                                                                               
*    On entry:                                                                  
*      CPORVINF - contains control information                                  
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - we have another request from ORVYL                                   
*      nz- ORVYL is not active (see TXCTL return codes)                         
*                                                                               
CONTROL  XENTER R2,R8,L'CNTLWA,LOCAL                                            
         USING CNTLWA,WAR                                                       
*                                                                               
         CLEAR CNTLINIT                                                         
*                                                                               
         LA    R6,CPORVINF                                                      
         USING CNTLORV,R6                                                       
*                                                                               
         CLEAR R5                                                               
         SET   CNTLRBAD            Assume it's a bad control number             
*                                                                               
         IF    CNTLSTMW,BEGIN      Set mode word...                             
         MVC   CPORVMOD,CNTLMOD                                                 
         SET   CNTLROK                                                          
         END                                                                    
*                                                                               
         IF    CNTLSNAS,BEGIN      Sense ACTIVE file size...                    
         L     R5,CPALNCT          Get line count                               
         SET   CNTLROK                                                          
         END                                                                    
*                                                                               
         IF    CNTLSNFL,BEGIN      Sense first lineno...                        
         IF    (CPALNCT,Z),'SET CNTLRNOL'  No lines                             
         ELSE  BEGIN                                                            
         L     R5,CPAFLNO          First lineno                                 
         SET   CNTLROK                                                          
         END                                                                    
         END                                                                    
*                                                                               
         IF    CNTLSNLL,BEGIN      Sense last lineno...                         
         IF    (CPALNCT,Z),'SET CNTLRNOL'  No lines                             
         ELSE  BEGIN                                                            
         L     R5,CPALLNO          Last lineno                                  
         SET   CNTLROK                                                          
         END                                                                    
         END                                                                    
*                                                                               
         IF    CNTLCLRA,BEGIN      Clear active...                              
         SET   CPFCLEAR,CPFNWARN   Clear it always                              
         VCALL CLRTEXT             Clear ACTIVE                                 
         SET   CNTLROK                                                          
         END                                                                    
*                                                                               
         IF    CNTLSNDL,BEGIN      Sense delta...                               
         L     R5,CPDELTA          DELTA                                        
         SET   CNTLROK                                                          
         END                                                                    
*                                                                               
         ST    R5,CNTLWORD         Set return info                              
*-                                                                              
*-       Send response to the control back to ORVYL.                            
*-                                                                              
         TXCTL CPORVYL,CNTLWYL     Send response to ORVYL                       
         XEXIT ,,LTR                                                            
*                                                                               
         DROP  CNTLORV,CNTLWA,BR                                                
*                                                                               
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
