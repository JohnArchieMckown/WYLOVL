TAPE     TITLE 'WYLBUR''s SET and SHOW TAPE Commands'                           
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         REGS  FSR,,,,,,IOWAR,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR              
         SPACE 2                                                                
WYLTAPE  CSECT                                                                  
TAPE     IDENT 4072                10:30:09 03/12/04  (SLP)                     
         SPACE 2                                                                
         SYSDEFN ,                 Define installation params                   
         GBLC  &INSTALN            Installation name (see SYSDEFN)              
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
         EJECT                                                                  
IOWA     IOWA  DSECT                                                            
*                                                                               
         POP   DSECTS                                                           
         AIF   ('&INSTALN' NE 'STANFORD').NOTSTAN                               
*                                                                               
         TITLE 'SET/SHOW TAPE Commands'                                         
&SVCTLMS SETA  238                 TLMS svc no                                  
*                                                                               
TAPEWA   RECORD BEGIN                                                           
TAPEFLG1 FLAG                                                                   
         FLAG  TAPEFUPD            - Fields will be updated                     
         FLAG  TAPEFREL            - Release tape request                       
         FLAG  TAPEFRES            - Reserve tape request                       
         FLAG  TAPEFSLT            - Assign new slot number                     
         FLAG  TAPEFRSL            - Reassign a slot number                     
         FLAG  TAPEFNSL            - Unassign the slot number                   
*                                                                               
TAPEFLG2 FLAG                                                                   
         FLAG  TAPEFRET            - Request RETRIEVE of tape                   
         FLAG  TAPEFARC            - Request ARCHIVE of tape                    
         FLAG  TAPEFNAR            - Request NOARCHIVE of tape                  
         FLAG  TAPEFLAB            - Print a tape label                         
         FLAG  TAPEFALL            - Display all fields                         
         FLAG  TAPEFRAW            - Display TDSECT (unformatted)               
         FLAG  TAPEFTYP            - SHOW TAPE vvvvvv TYPE                      
*                                                                               
TAPEDSN  DS    CL44                New dsname                                   
TAPEIN   DS    X                   New incode                                   
TAPEOUT  DS    C                   New outcode                                  
TAPENAME DS    CL10                New programmer name                          
TAPEJOB  DS    CL8                 New jobname                                  
TAPEKEEP DS    XL5                 New "keep" date                              
TAPEFILE DS    CL3                 No. of files on tape                         
*                                                                               
TDSECT   TDSECT                                                                 
TAPETDSC EQU   TDSECT,*-TDSECT,C'X'                                             
         END                                                                    
         EJECT                                                                  
TTPD     RECORD BEGIN                                                           
         TTYPEFMT                                                               
         END                                                                    
         MACRO                                                                  
&L       TMSG  &MSG,&LOC                                                        
&L       SETMSG &MSG                                                            
         LS    R3,R2,&LOC                                                       
         LCALL TMSG                                                             
         MEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TAPE -- SET/SHOW TAPE commands.                                              
*                                                                               
TAPE     XPROC TAPEWA              Entry for SET/SHOW TAPE                      
         LA    R6,TAPEWA                                                        
         USING TAPEWA,R6                                                        
*                                                                               
         CLEAR TAPEWA                                                           
         MVC   TDTYPE,=C'WYL7'     Initialize                                   
         MVC   TUSER(5),CPSACCT    Move in uuugg                                
         MVC   TUSER+5(2),CPSPROJ  Subgroup too                                 
         MVC   TIFL(2),CPSKIFL     Move in kifl & kafl                          
         MVC   TACCT2,CVBLANKS     Assume no second                             
         MVC   TACCT3,CVBLANKS     ...or third account                          
         ST    R6,CPCWA            Save TAPEWA address                          
*-                                                                              
*-       Regular SET/SHOW TAPE command.                                         
*-                                                                              
         SCAN  ,                                                                
         IF    (R0,Z),'ERROR "Missing tape name."'                              
*                                                                               
TAPEOK   IF    (R0,GT,6),'VCALL NOTVALID'                                       
         SCUPCASE TVSN                                                          
*                                                                               
         IF    (CPCMNM,EQ,'SHO'),BEGIN        SCAN SHOW OPTIONS                 
         SCAN  TAPEPRTS                                                         
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                          SCAN SET OPTIONS                  
         SCAN  TAPEPRT                                                          
         SCANCHK                                                                
         END                                                                    
         IF    ((CPCMNM,EQ,'SET'),AND,^TAPEFUPD),BEGIN                          
         ERROR 'Options expected after the tape name.'                          
         END                                                                    
*                                                                               
         IF    CPGFLG3.CPFKWENT,'MVC TACCT2(5),CPUSERSV'                        
         IF    (CPCACCT,NZ),BEGIN  User entered the account...                  
         XPUSH ,,L'KWR,PTR=R2                                                   
         WITH  (KWR,R2)                                                         
         CLEAR KWR                                                              
         MVC   KWRUSER(5),CPCACCT  Move in account to be checked                
         IF    (KWRUSER,Z),'MVC KWRUSER,CPSUSER'                                
         IF    (KWRGRP,Z),'MVC KWRGRP,CPSGRP'                                   
         LA    R1,KWR                                                           
         CLEAR R0                  Getkwr options                               
         VCALL GETKWR                                                           
         MVC   TACCT3,KWRUSER      Put account in TDSECT                        
         CLEAR KWR                                                              
         END                                                                    
*                                                                               
         MVI   DSNWAF2,DSNFSIMP    Set for simple dopen/dclose                  
         DOPEN                                                                  
         IF    NZ,'VCALL NTGDOPN'  Error of some sort                           
*                                                                               
         IF    TAPEFRES+TAPEFREL,BEGIN  Reserve/release request...              
         MVI   TPARM,TPRES         Assume reserve                               
         IF    TAPEFREL,'MVI TPARM,TPREL'  set release                          
         ACALL SENDTLMS                                                         
         TSEG  'Tape',,B                                                        
         SETMSG TVSN                                                            
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         SETMSG 'is now reserved.'                                              
         IF    TAPEFREL,'SETMSG "has been released."'                           
         B     CVNXTMSG                                                         
         END                                                                    
*                                                                               
         IF    TAPEFTYP,BEGIN                                                   
         MVI   TPARM,TPINQTYP                                                   
         END                                                                    
         ELSE  BEGIN                                                            
         MVI   TPARM,TPINQ         Inquiry                                      
         END                                                                    
         ACALL SENDTLMS                                                         
         IF    TAPEFUPD,BEGIN      Update fields & re-write...                  
         IF    (TAPEDSN,NZ),BEGIN                                               
         MVC   TDSN(32),TAPEDSN    Try to preserve the "hhmm"                   
         IF    ((TAPEDSN+33,NE,' '),                                   X        
               OR,(TDSN+33,NE,' '),OR,(TDSN+34.C'0',^ONES)),BEGIN               
         MVC   TDSN,TAPEDSN                                                     
         END                                                                    
         END                                                                    
         IF    (TAPEIN,NZ),'MVC TINCODE,TAPEIN'                                 
         IF    (TAPEOUT,NZ),'MVC TOUTCODE,TAPEOUT'                              
         IF    (TAPENAME,NZ),'MVC TPRGRMMR,TAPENAME'                            
         IF    (TAPEJOB,NZ),'MVC TJOBNAME,TAPEJOB'                              
         IF    (TAPEKEEP,NZ),'MVC TKEEPDT,TAPEKEEP'                             
         IF    (TAPEFILE,NZ),'MVC TLASTFIL,TAPEFILE'                            
         MVI   TPARM,TPUPD         Update record                                
         ACALL SENDTLMS                                                         
*                                                                               
         IF    TAPEFSLT,BEGIN      Assign new slot number...                    
         MVI   TPARM,TPSLOT        Newslot call                                 
         IF    TAPEFRSL,'MVI TPARM,TPRESL'  Reassign slot                       
         IF    TAPEFNSL,'MVI TPARM,TPFREESL'  Unassign slot                     
         ACALL SENDTLMS            Assign the new slot                          
*                                                                               
         MVI   TPARM,TPINQ         Do another inquiry                           
         ACALL SENDTLMS            This will return TSLOTNO                     
         END                                                                    
*                                                                               
         IF    TAPEFRET+TAPEFARC+TAPEFNAR,BEGIN ARC/NOARC/RET...                
         MVI   TPARM,TPONS         Assume RETRIEVE request                      
         IF    TAPEFARC,'MVI TPARM,TPOFFS'  ARCHIVE request                     
         IF    TAPEFNAR,'MVI TPARM,TPNOFFS'  NOARCHIVE request                  
         ACALL SENDTLMS            Request tape ARCHIVE/NOARCHIVE               
*                                                                               
         MVI   TPARM,TPINQ         Do another inquiry                           
         ACALL SENDTLMS            This will return TLOCID                      
         END                                                                    
*                                                                               
         IF    TAPEFLAB,BEGIN      Print a tape label...                        
         MVI   TPARM,TPLABEL       Print label call                             
         ACALL SENDTLMS                                                         
         TSEG  'A label will be printed for tape',,B                            
         SETMSG TVSN                                                            
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         SETMSG '.'                                                             
         B     CVNXTMSG                                                         
         END                                                                    
         END                                                                    
*                                                                               
         VCALL EXTCLOSE                                                         
*-                                                                              
*-       Now display the relevant tape information.                             
*-                                                                              
         ACALL TAPEDISP            Display tape information                     
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
TAPEPRT  SCKW  DSNAME,VDSN,(A,P)                                                
         SCKW  IN,VIN,(A,P,PI),5                                                
         SCKW  INCODE,VIN,(A,P,PI),5                                            
         SCKW  OUTCODE,VOUT,(A,P,PI),5                                          
         SCKW  USE,VUSE,P          Could be "user" or "usecode"                 
         SCKW  USECODE,VOUT,(A,P,PI),5                                          
         SCKW  ON,VON                                                           
         SCKW  ONSITE,VON,A                                                     
         SCKW  OFFSITE,VOFF,A                                                   
         SCKW  ARCHIVE,VARCH,A                                                  
         SCKW  NOARCHIVE,VNOARCH,A                                              
         SCKW  RETRIEVE,VRET,A                                                  
         SCKW  NAME,VNAME,(A,P)                                                 
         SCKW  JOBNAME,VJOB,(A,P)                                               
         SCKW  KEEP,VKEEP,A                                                     
         SCKW  FILES,VFIL,(A,P,I),999                                           
         SCKW  RELEASE,VREL,A                                                   
         SCKW  RESERVE,VRES,A                                                   
         SCKW  SLOT,VSLOT,A                                                     
         SCKW  RESLOT,VRESLOT,A                                                 
         SCKW  NOSLOT,VNOSLOT,A                                                 
         SCKW  LABEL,VLABEL,A                                                   
         SCKW  OWNER,VOWN,A                                                     
         SCKW  ,V(QUIETPSH),PUSH                                                
*                                                                               
TAPEPRTS SCKW  ALL,VALL                                                         
         SCKW  RAW,VRAW                                                         
         SCKW  TYPE,VTYPE                                                       
         SCKW  ,V(INTPSH),PUSH                                                  
         SCKW  ,V(ACCTPSH),PUSH                                                 
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
VDSN     PROC  ,                                                                
         IF ((@R1,EQ,''''),OR,(@R1,EQ,'"'),OR,(@R1,EQ,'(')),BEGIN               
         VCALL NOTVALID                                                         
         END                                                                    
         IF    ((@R1,EQ,'$'),OR,(@R1,EQ,'&&'),OR,(@R1,EQ,'@')),BEGIN            
         ERROR 'Dsname can''t be qualified with "$", "@", OR "&&".'             
         END                                                                    
         SET   TAPEFUPD                                                         
         SCUPCASE TAPEDSN                                                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VUSE     PROC  ,                                                                
         SCUPTOK R1                                                             
         IF    (R0,EQ,3),BEGIN     It's user=xxx...                             
         MVC   CPCUSER,@R1                                                      
         EXIT  VUSE                                                             
         END                                                                    
         VCALL DTB                 It's usecode=x...                            
         IF    NZ,'VCALL NOTVALID'                                              
         IF    ((R15,NP),OR,(R15,GT,5)),'VCALL NOTVALID'                        
         SET   TAPEFUPD                                                         
         BTD   TAPEOUT,,(R15)                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VIN      PROC  ,                                                                
         LR    R15,R0                                                           
         SET   TAPEFUPD                                                         
         BTD   TAPEIN,,(R15)                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VOUT     PROC  ,                                                                
         LR    R15,R0                                                           
         SET   TAPEFUPD                                                         
         BTD   TAPEOUT,,(R15)                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VNAME    PROC  ,                                                                
         SET   TAPEFUPD                                                         
         SCDQUOTE TAPENAME                                                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VJOB     PROC  ,                                                                
         IF ((@R1,EQ,''''),OR,(@R1,EQ,'"'),OR,(@R1,EQ,'(')),BEGIN               
         VCALL NOTVALID                                                         
         END                                                                    
         SET   TAPEFUPD                                                         
         SCUPCASE TAPEJOB                                                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VKEEP    PROC  ,                                                                
         SET   TAPEFUPD                                                         
         VCALL CNVDATE             Get date in YYDDDF format                    
         SRL   R15,4               Get rid of F                                 
         BTX   TAPEKEEP,,(R15)                                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VFIL     PROC  ,                                                                
         SET   TAPEFUPD                                                         
         LR    R15,R0                                                           
         BTD   TAPEFILE,,(R15)                                                  
         OC    TAPEFILE,=C'00000000'                                            
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
VALL     PROC  ,                                                                
         SET   TAPEFALL                                                         
         PEND  ,                                                                
*                                                                               
VRAW     PROC  ,                                                                
         SET   TAPEFRAW                                                         
         PEND  ,                                                                
*                                                                               
VTYPE    PROC  ,                                                                
         SET   TAPEFTYP                                                         
         PEND  ,                                                                
*                                                                               
VREL     PROC  ,                                                                
         IF    TAPEFUPD,BEGIN                                                   
         TSEG  'RELEASE is inconsistent with',,B                                
         SETMSG 'new options'                                                   
         IF    TAPEFRES,'SETMSG "RESERVE"'                                      
         TSEG  (R1),(R0)                                                        
         ERROR '.'                                                              
         END                                                                    
         SET   TAPEFREL+TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VRES     PROC  ,                                                                
         IF    TAPEFUPD,BEGIN                                                   
         TSEG  'RESERVE is inconsistent with',,B                                
         SETMSG 'new options'                                                   
         IF    TAPEFREL,'SETMSG "RELEASE"'                                      
         TSEG  (R1),(R0)                                                        
         ERROR '.'                                                              
         END                                                                    
         SET   TAPEFRES+TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VON      PROC  ,                                                                
         TSEG  'Please use the RETRIEVE option to bring a tape '                
         ERROR 'ONSITE.'                                                        
         PEND  ,                                                                
*                                                                               
VOFF     PROC  ,                                                                
         TSEG  'Please use the ARCHIVE option to place a tape '                 
         ERROR 'OFFSITE.'                                                       
         PEND  ,                                                                
*                                                                               
VRET     PROC  ,                                                                
         IF    TAPEFARC,BEGIN                                                   
         ERROR 'ARCHIVE and RETRIEVE are mutually exclusive options.'           
         END                                                                    
         IF    TAPEFNAR,BEGIN                                                   
         TSEG  'NOARCHIVE and RETRIEVE are mutually exclusive '                 
         ERROR  'options.'                                                      
         END                                                                    
         SET   TAPEFRET,TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VARCH    PROC  ,                                                                
         IF    TAPEFRET,BEGIN                                                   
         ERROR 'RETRIEVE and ARCHIVE are mutually exclusive options.'           
         END                                                                    
         IF    TAPEFNAR,BEGIN                                                   
         TSEG  'NOARCHIVE and RETRIEVE are mutually exclusive '                 
         ERROR  'options.'                                                      
         END                                                                    
         SET   TAPEFARC,TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VNOARCH  PROC  ,                                                                
         IF    TAPEFRET+TAPEFARC,BEGIN                                          
         TSEG  'NOARCHIVE can''t be specified with ARCHIVE or '                 
         ERROR 'RETRIEVE.'                                                      
         END                                                                    
         SET   TAPEFNAR,TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VSLOT    PROC  ,                                                                
         SET   TAPEFSLT+TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
VRESLOT  PROC  ,                                                                
         SET   TAPEFSLT+TAPEFRSL+TAPEFUPD                                       
         PEND  ,                                                                
*                                                                               
VNOSLOT  PROC  ,                                                                
         SET   TAPEFSLT+TAPEFNSL+TAPEFUPD                                       
         PEND  ,                                                                
*                                                                               
VOWN     PROC  ,                                                                
         ABORT 'Tape owner can''t be changed.'                                  
         PEND  ,                                                                
*                                                                               
VLABEL   PROC  ,                                                                
         SET   TAPEFLAB,TAPEFUPD                                                
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TAPEDISP -- Routine to display tape information.                             
*                                                                               
TAPEDISP PROC  ,                                                                
         L     R6,CPCWA                                                         
         USING TAPEWA,R6                                                        
*-                                                                              
*-       RAW (completely unformatted) version.                                  
*-                                                                              
         IF    TAPEFRAW,BEGIN      Raw...                                       
         TSEG  TAREA                                                            
         B     TAPEDXIT                                                         
         END                                                                    
*-                                                                              
*-       INTERNAL (fixed format) version.                                       
*-                                                                              
         IF    CPFINT,BEGIN        Internal...                                  
         ACALL TAPEINTD            Display internal format                      
         B     TAPEDXIT                                                         
         END                                                                    
*-                                                                              
*-       TYPE version.                                                          
*-                                                                              
         IF    TAPEFTYP,BEGIN                                                   
         LA    R1,TDRIVE                                                        
         ACALL DISPTYPE                                                         
         B     TAPEDXIT                                                         
         END                                                                    
*-                                                                              
*-       Regular version.                                                       
*-                                                                              
         TSEG  TVSN,,B                                                          
*                                                                               
         TSEG  '- Owner='                                                       
         TSEG  TACCT+3,2                                                        
         TSEG  '.'                                                              
         TSEG  TACCT,3,B                                                        
*                                                                               
         TSEG  'Dsname='                                                        
         SETMSG TDSN                                                            
         IF    (TDSN+33,EQ,' '),'LA R0,32'  Get rid of hhmm                     
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,9                                                       
         XPUSH ,,L'TLASTFIL,PTR=R2                                              
         MVC   @R2(L'TLASTFIL),TLASTFIL                                         
         OC    @R2(L'TLASTFIL),=(L'TLASTFIL)C'0'                                
         CLC   @R2(L'TLASTFIL),=(L'TLASTFIL)C'0'                                
         IF    EQ,'TSEG "Files=0 "'                                             
         ELSE  'TMSG "Files",TLASTFIL'                                          
         XPOP  ,,L'TLASTFIL                                                     
*                                                                               
         IF    (TJOBNAME,NE,CVBLANKS),BEGIN                                     
         TSEG  'Jobname='                                                       
         TSEG  TJOBNAME,,TB                                                     
         END                                                                    
*                                                                               
         IF    (TPRGRMMR,NE,CVBLANKS),BEGIN                                     
         TSEG  'Name='                                                          
         SETMSG TPRGRMMR                                                        
         TSEG  (R1),(R0),TB                                                     
         END                                                                    
*                                                                               
         IF    (TSCT,EQ,'Y'),BEGIN  Tape is in scratch status...                
         TSEG  '** Released **'                                                 
         END                                                                    
         ELSE  BEGIN               Otherwise, show in- outcodes...              
         TCR                                                                    
         TSEG  CVBLANKS,9                                                       
         TSEG  'Incode='                                                        
         TSEG  TINCODE,,B                                                       
         CLEAR R0                                                               
         IF    (TINCODE,EQ,'1'),'SETMSG "(No Read) "'                           
         IF    (TINCODE,EQ,'2'),'SETMSG "(Owner Read) "'                        
         IF    (TINCODE,EQ,'3'),'SETMSG "(Group Read) "'                        
         IF    (TINCODE,EQ,'4'),'SETMSG "(Community Read) "'                    
         IF    (TINCODE,EQ,'5'),'SETMSG "(Public Read) "'                       
         TSEG  (R1),(R0)                                                        
         TSEG  'Outcode='                                                       
         TSEG  TOUTCODE,,B                                                      
         CLEAR R0                                                               
         IF    (TOUTCODE,EQ,'1'),'SETMSG "(No Write) "'                         
         IF    (TOUTCODE,EQ,'2'),'SETMSG "(Owner Write) "'                      
         IF    (TOUTCODE,EQ,'3'),'SETMSG "(Group Write) "'                      
         IF    (TOUTCODE,EQ,'4'),'SETMSG "(Keep Date Protected) "'              
         IF    (TOUTCODE,EQ,'5'),'SETMSG "(Public Write) "'                     
         TSEG  (R1),(R0)                                                        
         END                                                                    
*                                                                               
         TCR                                                                    
         TSEG  CVBLANKS,9                                                       
         TSEG  'Location='                                                      
         TSEG  TLOCID                                                           
         CLEAR R0                                                               
         IF    (TLOCID,EQ,'10'),'SETMSG " (Forsythe)"'                          
         IF    (TLOCID,EQ,'15'),'SETMSG " (Forsythe)"'                          
         IF    (TLOCID,EQ,'20'),'SETMSG " (Information Desk)"'                  
         IF    (TLOCID,EQ,'25'),'SETMSG " (Plot tape)"'                         
         IF    (TLOCID,EQ,'40'),'SETMSG " (Arcus)"'                             
         IF    (TLOCID,EQ,'51'),'SETMSG " (Searsville)"'                        
         IF    (TLOCID,EQ,'55'),'SETMSG " (Production Control)"'                
         IF    (TLOCID,EQ,'80'),'SETMSG " (Archive)"'                           
         IF  (TLOCID,EQ,'81'),'SETMSG " (Forsythe -- Pending archive)"'         
         IF  (TLOCID,EQ,'82'),'SETMSG " (Archive -- Pending retrieve)"'         
         IF    (TLOCID,EQ,'83'),'SETMSG " (Forsythe -- No archive)"'            
         IF    (TLOCID,EQ,'90'),'SETMSG " (User''s possession)"'                
         IF    (TLOCID,EQ,'99'),'SETMSG " (Unknown)"'                           
         TSEG  (R1),(R0)                                                        
*                                                                               
         IF    (TSER,NE,'Y'),BEGIN                                              
         TSEG  ' (Not in service)'                                              
         END                                                                    
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,9                                                       
         IF    (TSLOTNO,NE,' '),BEGIN                                           
         TSEG  'Slot='                                                          
         TSEG  TSLOTNO,2                                                        
         TSEG  '-'                                                              
         TSEG  TSLOTNO+2,1                                                      
         TSEG  '-'                                                              
         TSEG  TSLOTNO+3,2,B                                                    
         END                                                                    
*                                                                               
         IF    (TPRVSLOT,NE,' '),BEGIN                                          
         TSEG  '(Previous slot was '                                            
         TSEG  TPRVSLOT,2                                                       
         TSEG  '-'                                                              
         TSEG  TPRVSLOT+2,1                                                     
         TSEG  '-'                                                              
         TSEG  TPRVSLOT+3,2                                                     
         TSEG  ' on '                                                           
         TM    TSLTYDDD,X'F0'      Test for new format slot-date                
         IF    Z,BEGIN             Yes - new format 0cyydddf                    
         ICM   R0,15,TSLTYDDD      Set packed date into R0                      
         LA    R1,CPDOUB           Set output work area address                 
         VCALL DATE                Convert date to MM/DD/YY                     
         MVC   6(2,R1),8(R1)       Eliminate century for now                    
         TSEG  (R1),8              Set into output buffer                       
         END   ELSE,BEGIN          Process old style date                       
         XPUSH ,,5,PTR=R1                                                       
         CLI   TLOCDATE,C'9'       Generation date after 1990?                  
         IF    GE,'MVI @R1,C"9"'   Yes - set slot date to >1990                 
         ELSE  'MVI @R1,C"8"'      Else assume decade 1980.                     
         MVC   @R1+1(4),TSLTYDDD   Complete slot change date                    
         LCALL SEGDATE             Convert and seg character date               
         XPOP  ,,5                 Restore stack                                
         END   ,                                                                
         TSEG  ') '                Complete message                             
         END                                                                    
*                                                                               
         IF    ((TSLOTNO,EQ,' '),AND,(TPRVSLOT,EQ,' ')),BEGIN                   
         TSEG  '(No slot information available)'                                
         END                                                                    
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,9                                                       
         TSEG  'CR:',,B                                                         
         IF    ((TDSN+33,EQ,' '),AND,(TDSN+34.C'0',ONES)),BEGIN                 
         TSEG  TDSN+34,2                                                        
         TSEG  ':'                                                              
         TSEG  TDSN+36,2,B                                                      
         END                                                                    
         LA    R1,TGENDT           Creation date                                
         LCALL SEGDATE                                                          
         TSEG  '  LA:',,B                                                       
         LA    R1,TLUSED           Last accessed date                           
         LCALL SEGDATE                                                          
*                                                                               
         TSEG  '  Keep='                                                        
         LA    R1,TKEEPDT          Keep date                                    
         LCALL SEGDATE                                                          
         TCR                                                                    
*                                                                               
         IF    TAPEFALL,BEGIN      Display various counts...                    
         TSEG  CVBLANKS,9                                                       
         TSEG  'Label='                                                         
         SETMSG '???'                                                           
         IF    ((TLAB,NE,' '),AND,(TLAB,NZ)),'SETMSG TLAB'                      
         IF    (TLAB,EQ,'A'),'SETMSG "AL"'                                      
         IF    (TLAB,EQ,'N'),'SETMSG "NL"'                                      
         IF    (TLAB,EQ,'S'),'SETMSG "SL"'                                      
         IF    (TLAB,EQ,'B'),'SETMSG "BLP"'                                     
         TSEG  (R1),(R0),B                                                      
         TMSG  'Density',TDEN                                                   
         TMSG  'Drive',TDRIVE                                                   
         TMSG  'Usecount',TUSECT                                                
         TMSG  'SIO',TSIO                                                       
         TMSG  'ERG',TERG                                                       
         TMSG  'TRE',TTRE                                                       
         TCR                                                                    
         END                                                                    
*                                                                               
TAPEDXIT PEND                                                                   
         EJECT                                                                  
TMSG     PROC  ,                                                                
         LR    R15,R2                                                           
         CMPR  R15,@R3,CVBLANKS    Anything?                                    
         IF    NE,BEGIN            Yes...                                       
         EX    R15,'CLC @R3(0),=C"00000000"'  Zeros?                            
         IF    EQ,EXIT             Yes.. forget it                              
         TSEG  (R1),(R0)                                                        
         TSEG  '='                                                              
         SETMSG (R3),(R2)                                                       
         VCALL LRTRIM                                                           
         WHILE ((R0,POS),AND,(@R1,EQ,'0')),'LA R1,@R1+1; DECR R0'               
         TSEG  (R1),(R0),B                                                      
         END                                                                    
         PEND  ,                                                                
         SPACE 2                                                                
SEGDATE  PROC  ,                                                                
         XTB   (R1),5                                                           
         LA    R1,=C'??/??/??'                                                  
         IF    (Z,AND,(R15,NZ)),BEGIN  Good date...                             
         LR    R0,R15                                                           
         SRL   R0,12               Isolate YY                                   
*                                                                               
* Window dates between 00 and 69 to 2000-2069                                   
*                                                                               
         IF    (R0,LT,=F'70'),BEGIN                                             
         L     R0,=XL4'0100000F'                                                
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R0,X'F'                                                          
         END                                                                    
         SLL   R15,4                                                            
         OR    R0,R15              Form "0CYYDDDF"                              
         LA    R1,CPDOUB                                                        
         VCALL DATE                Convert to "mm/dd/yy"                        
         MVC   6(2,R1),8(R1)       Eliminate century for now                    
         END                                                                    
         TSEG  (R1),8                                                           
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DISPTYPE -- Routine to display SHOW TAPE TYPE response.                      
*                                                                               
DISPTYPE PROC  ,                                                                
         WITH  (TTPD,R1)                                                        
         TSEG  (R1),TTYLNGTH                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TAPEINTD -- Routine to display SHOW TAPE INTERNAL response.                  
*                                                                               
TAPEINTD PROC  ,                                                                
         TSEG  TVSN,,B             Tape volser                                  
*                                                                               
         IF    (TACCT,EQ,CVBLANKS),'TSEG "??.???",,B'                           
         ELSE  BEGIN                                                            
         TSEG  TACCT+3,2           Account                                      
         TSEG  '.'                                                              
         TSEG  TACCT,3,B                                                        
         END                                                                    
*                                                                               
         TSEG  TJOBNAME,,B         Jobname                                      
*                                                                               
         TSEG  TPRGRMMR            User name                                    
         TSEG  CVBLANKS,20-L'TPRGRMMR,B                                         
*                                                                               
         TSEG  TDSN,33             Filename (fixed part)                        
         SETMSG TDSN+33,L'TDSN-33   Assume continuation of filename             
         IF    (@R1,EQ,' '),'LA R1,CVBLANKS'                                    
         TSEG  (R1),(R0),B                                                      
*                                                                               
         TSEG  ' '                 (Reserved for field expansion)               
         TSEG  TLASTFIL,,B         Number of files                              
*                                                                               
         IF    ((TDSN+33,EQ,' '),AND,(TDSN+34.C'0',ONES)),BEGIN                 
         TSEG  TDSN+34,2           Creation time                                
         TSEG  ':'                                                              
         TSEG  TDSN+36,2,B                                                      
         END                                                                    
         ELSE  'TSEG "??:??",,B'                                                
*                                                                               
         IF    (TGENDT,EQ,CVBLANKS),'TSEG "??.???",,B'                          
         ELSE  BEGIN                                                            
         TSEG  TGENDT,2            Creation date                                
         TSEG  '.'                                                              
         TSEG  TGENDT+2,3,B                                                     
         END                                                                    
*                                                                               
         IF    (TLUSED,EQ,CVBLANKS),'TSEG "??.???",,B'                          
         ELSE  BEGIN                                                            
         TSEG  TLUSED,2            Last accessed date                           
         TSEG  '.'                                                              
         TSEG  TLUSED+2,3,B                                                     
         END                                                                    
*                                                                               
         IF    (TKEEPDT,EQ,CVBLANKS),'TSEG "??.???",,B'                         
         ELSE  BEGIN                                                            
         TSEG  TKEEPDT,2           Keep date                                    
         TSEG  '.'                                                              
         TSEG  TKEEPDT+2,3,B                                                    
         END                                                                    
*                                                                               
         IF    ((TDSN+38,EQ,' '),AND,(TDSN+39.C'0',ONES)),BEGIN                 
         TSEG  TDSN+39,2           Release date [Scratch tape]                  
         TSEG  '.'                                                              
         TSEG  TDSN+41,3,B                                                      
         END                                                                    
         ELSE  'TSEG "00.000",,B'                                               
*                                                                               
         IF    (TINCODE,EQ,CVBLANKS),'TSEG "?",,B'                              
         ELSE  'TSEG TINCODE,,B'   Incode                                       
*                                                                               
         IF    (TOUTCODE,EQ,CVBLANKS),'TSEG "?",,B'                             
         ELSE  'TSEG TOUTCODE,,B'  Outcode                                      
*                                                                               
         IF    (TDEN,EQ,CVBLANKS),'TSEG "????",,B'                              
         ELSE  'TSEG TDEN,,B'      Density                                      
*                                                                               
         CLEAR R0                                                               
         IF    (TLAB,EQ,'A'),'SETMSG "AL "'                                     
         IF    (TLAB,EQ,'N'),'SETMSG "NL "'                                     
         IF    (TLAB,EQ,'S'),'SETMSG "SL "'                                     
         IF    (TLAB,EQ,'B'),'SETMSG "BLP"'                                     
         IF    ((TLAB,NE,' '),AND,(R0,Z)),'TSEG TLAB; SETMSG "??"'              
         IF    (R0,Z),'SETMSG "???"'                                            
         TSEG  (R1),(R0),B         Label type                                   
*                                                                               
         IF    (TLNGTH,EQ,CVBLANKS),'TSEG "????",,B'                            
         ELSE  'TSEG TLNGTH,,B'    Length (e.g., 2400 feet)                     
*                                                                               
         IF    (TVSQ,EQ,CVBLANKS),'TSEG " ?",,B'                                
         ELSE  'TSEG TVSQ,,B'      Volume sequence number                       
*                                                                               
         IF    (TVCT,EQ,CVBLANKS),'TSEG " ?",,B'                                
         ELSE  'TSEG TVCT,,B'      Total number of tapes                        
*                                                                               
         IF    (TDRIVE,EQ,CVBLANKS),'TSEG " ???",,B'                            
         ELSE  BEGIN                                                            
         TSEG  ' '                 (Reserved for field expansion)               
         TSEG  TDRIVE,,B           Last written unit address                    
         END                                                                    
*                                                                               
         OI    TUSECT+L'TUSECT-1,C'0'  (Force zero)                             
         TSEG  TUSECT,,B           Use count                                    
*                                                                               
         OI    TSIO+L'TSIO-1,C'0'                                               
         TSEG  TSIO,,B             Start I/O count                              
*                                                                               
         OI    TERG+L'TERG-1,C'0'                                               
         TSEG  TERG,,B             Number of erase gaps                         
*                                                                               
         OI    TMAXPWE+L'TMAXPWE-1,C'0'                                         
         TSEG  TMAXPWE,,B          Maximum number of write errors               
*                                                                               
         OI    TTRE+L'TTRE-1,C'0'                                               
         TSEG  TTRE,,B             Number of temporary read errors              
*                                                                               
         TSEG  TSER                In service flag                              
*                                                                               
         TSEG  TSCT                In scratch status flag                       
*                                                                               
         TSEG  TSLTFLG             Slot flag                                    
*                                                                               
*        TSEG  TAPEINAT            **OBSOLETE**In ATL flag                      
         TSEG  ' '                   Just say Ahhh.                             
*                                                                               
         TSEG  TCHRGFLG,,B         Archive charge flag                          
*                                                                               
         TSEG  TLOCID,,B           Current TLMS location code                   
*                                                                               
         IF    (TPRELOC,EQ,CVBLANKS),'TSEG "??",,B'                             
         ELSE  'TSEG TPRELOC,,B'   Previous TLMS location code                  
*                                                                               
         IF    (TLOCDATE,EQ,CVBLANKS),'TSEG "??.???",,B'                        
         ELSE  BEGIN               Date of previous location change             
         TSEG  TLOCDATE,2                                                       
         TSEG  '.'                                                              
         TSEG  TLOCDATE+2,3,B                                                   
         END                                                                    
*                                                                               
         IF    (TSLOTNO,EQ,' '),'TSEG "??-?-??",,B'                             
         ELSE  BEGIN                                                            
         TSEG  TSLOTNO,2           Current slot number                          
         TSEG  '-'                                                              
         TSEG  TSLOTNO+2,1                                                      
         TSEG  '-'                                                              
         TSEG  TSLOTNO+3,2,B                                                    
         END                                                                    
*                                                                               
         IF    (TPRVSLOT,EQ,' '),'TSEG "??-?-??",,B'                            
         ELSE  BEGIN                                                            
         TSEG  TPRVSLOT,2          Previous slot number                         
         TSEG  '-'                                                              
         TSEG  TPRVSLOT+2,1                                                     
         TSEG  '-'                                                              
         TSEG  TPRVSLOT+3,2,B                                                   
         END                                                                    
*                                                                               
         IF    (TSLTYDDD,EQ,' '),'TSEG "??.???",,B'                             
         ELSE  BEGIN               Output last slot change date                 
         TM    TSLTYDDD,X'F0'      Test for new format dat                      
         IF    NZ,BEGIN            Old format                                   
         CLI   TLOCDATE,C'9'       Test for >1990                               
         IF    GE,'TSEG "9"'       Set decade to 1990                           
         ELSE  'TSEG "8"'          Set decade to 1980                           
         TSEG  TSLTYDDD,1          Add the year                                 
         TSEG  '.'                 Set separator                                
         TSEG  TSLTYDDD+1,3,B      Add the days                                 
         END   ELSE,BEGIN          Convert new format date                      
         UNPK  CPDOUB(8),TSLTYDDD  Convert to zoned decimal                     
         OI    CPDOUB,X'F0'          set first zone to x'f0'                    
         MVZ   CPDOUB+1(7),CPDOUB      insure all valid zone codes              
         TRT   CPDOUB,TRTTAB-240         test for valid number                  
         IF    Z,BEGIN             Valid number                                 
         TSEG  CPDOUB+3,2          Set year into message buffer                 
         TSEG  '.'                 Set delimeter                                
         TSEG  CPDOUB+5,3,B        Set day into message buffer                  
         END   ELSE,BEGIN                                                       
         TSEG  '??.???',,B         Invalid date from TLMS                       
         END   ,                                                                
         END   ,                                                                
         END                                                                    
*                                                                               
         IF    (TLOCBOX,EQ,' '),'TSEG "????",,B'                                
         ELSE  BEGIN                                                            
         TSEG  TLOCBOX,,B          TOSS box number                              
         END                                                                    
*                                                                               
         IF    (TPRLOCDT,EQ,' '),'TSEG "??.???",,B'                             
         ELSE  BEGIN                                                            
         TSEG  TPRLOCDT,2          TOSS date                                    
         TSEG  '.'                                                              
         TSEG  TPRLOCDT+2,3,B                                                   
         END                                                                    
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
TRTTAB   DC    10X'00',6X'01'                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SENDTLMS -- Routine to send communications block (TDSECT) to                 
*    TLMS and wait for a response.  TAPEWA addressability is                    
*    assumed.                                                                   
*                                                                               
SENDTLMS PROC  ,                                                                
         L     IOWAR,CPIOWAP                                                    
         USING IOWA                                                             
         L     R5,CPCWA            TAPEWA ptr                                   
         USING TAPEWA,R5                                                        
*                                                                               
         LA    R0,L'TAPETDSC       Room for TDSECT                              
         FLOOR R0,256              (At least 256 bytes, for DG)                 
         ICM   R0,8,=AL1(231)      Set subpool 231 (CSA)                        
         GETMAIN R,LV=(0)                                                       
         LR    R4,R1                                                            
         MVC   @R4(L'TAPETDSC),TAPETDSC                                         
         USING TDSECT,R4                                                        
*                                                                               
         WAITOFF X                 Enter subtask                                
         LA    R1,TECB                                                          
         ST    R1,TECBPNTR         Ecb addr                                     
         MVC   TASCBPTR,CVASCBP    Set ASCB ptr                                 
         CLEAR TCODE,TECB                                                       
         LA    R0,2                TLMSQMGR 'put to queue' code                 
         LA    R1,TDSECT                                                        
         SVC   &SVCTLMS            TLMSQMGR svc                                 
         IF    (R0,NZ),'LH R15,=H"-1"; B SENDON'  No TLMS                       
         WAIT  ECB=TECB            Wait for TLMS                                
         CLEAR R15                 Assume unknown code                          
         IF    (TECBCODE,EQ,TECBCOK),'LA R15,4'    Ok                           
         IF    (TECBCODE,EQ,TECBCVNF),'LA R15,8'   Vol not found                
         IF    (TECBCODE,EQ,TECBCVNY),'LA R15,12'  Vol not yours                
         IF    (TECBCODE,EQ,TECBCVNX),'LA R15,12'  Vol not yours/mod            
         IF    (TECBCODE,EQ,TECBCVNS),'LA R15,16'  Vol not here                 
         IF    (TECBCODE,EQ,TECBCVNL),'LA R15,20'  Not in Forsythe              
         IF    (TECBCODE,EQ,TECBCVNU),'LA R15,24'  Not in Archives              
SENDON   WAITON X                  Return to mother                             
         LR    R2,R15              Save return code                             
         DROP  TDSECT                                                           
         MVC   TAPETDSC,@R4        Move TDSECT back                             
         LA    R0,L'TAPETDSC       Length of TDSECT                             
         FLOOR R0,256              (The same as for GETMAIN)                    
         ICM   R0,8,=AL1(231)      Set subpool 231 (CSA)                        
         FREEMAIN R,A=(R4),LV=(R0)  Free TDSECT area                            
*                                                                               
*  Log updates                                                                  
*                                                                               
         IF    ((TPARM,NE,TPINQ),AND,(TPARM,NE,TPINQTYP)),BEGIN                 
         XPUSH ,,200,PTR=R1                                                     
         MVC   @R1(200),CVBLANKS                                                
         LR    R15,R2                                                           
         IF    (R15,EQ,4),'CLEAR R15'                                           
         BTD   @R1,4,(R15)         Return code                                  
         OC    @R1(4),=C'0000'                                                  
         MVC   @R1+4(6),TVSN       Volser                                       
         MVC   @R1+16(200-16),CPCMD  User's command                             
         LA    R0,16                                                            
         AH    R0,CPCMDLEN                                                      
         CEIL  R0,200                                                           
         LA    R15,=CL8'TLMS'      Record type                                  
         VCALL SMFLOG              Write SMF record                             
         XPOP  ,,200                                                            
         END                                                                    
*                                                                               
         IF    (R2,NEG),SENDNT     No TLMS                                      
*                                                                               
         B     *+4(R2)                                                          
         B     SENDERR             +0:  WYLBUR/TLMS problem                     
         B     SENDEXIT            +4:  Ok                                      
         B     SENDNF              +8:  Not found                               
         B     SENDNY              +12: Not yours                               
         B     SENDNE              +16: Not here (in library)                   
         B     SENDNAF             +20: Not at Forsythe                         
         B     SENDNIA             +24: Not in Archives                         
*                                                                               
SENDERR  TSEG  'WYLBUR/TLMS error (CMD='                                        
         BTX   CPDOUB,2,LC:TPARM                                                
         TSEG  (R1),(R0)                                                        
         TSEG  ', RC='                                                          
         BTX   CPDOUB,2,LC:TECBCODE                                             
         TSEG  (R1),(R0)                                                        
         ERROR ').  Please report to Systems.'                                  
*                                                                               
SENDNF   TSEG  'Tape',,B                                                        
         SETMSG TVSN                                                            
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         ERROR 'not found.'                                                     
*                                                                               
SENDNY   IF    (TPARM,EQ,TPLABEL),BEGIN                                         
         ABORT 'You can''t print a label from this account.'                    
         END                                                                    
*                                                                               
         TSEG  'Tape',,B                                                        
         SETMSG TVSN                                                            
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         SETMSG 'is not yours to modify.'                                       
         IF    (TECBCODE,EQ,TECBCVNY),'SETMSG "is not yours."'                  
         ERROR (R1),(R0)                                                        
*                                                                               
SENDNE   TSEG  'can''t release.  Tape',,B                                       
         SETMSG TVSN                                                            
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         ABORT 'is not currently in the library.'                               
*                                                                               
SENDNAF  TSEG  'Tape '                                                          
         TSEG  TVSN,,TB                                                         
         ABORT 'is not at Forsythe.  No action taken.'                          
*                                                                               
SENDNIA  TSEG  'Tape '                                                          
         TSEG  TVSN,,TB                                                         
         ABORT 'is not in Archive storage.  No action taken.'                   
*                                                                               
SENDNT   ABORT 'TLMS is not up.',,NOTLMS                                        
*                                                                               
SENDEXIT PEND  ,                                                                
         DROP  TAPEWA,IOWA                                                      
         AGO   .DONE                                                            
*                                                                               
.NOTSTAN ANOP                                                                   
         TITLE 'Dummy SET and SHOW TAPE Commands'                               
*box                                                                            
*                                                                               
*  TAPE -- SET/SHOW TAPE Commands.                                              
*                                                                               
TAPE     XPROC ,                                                                
         ABORT 'SET and SHOW TAPE commands are not available.'                  
         PEND  ,                                                                
*                                                                               
.DONE    LTORG                                                                  
*                                                                               
         VLTORG                                                                 
         END   .                                                                
