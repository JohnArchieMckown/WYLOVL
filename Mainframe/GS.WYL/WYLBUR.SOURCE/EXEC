EXEC     TITLE 'EXEC File Routines'                                             
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         EJECT                                                                  
WYLEXEC  CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
EXEC     IDENT 2193                15:11:44 07/12/02  (SLP)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         COPY  FFINFO                                                           
         POP   DSECTS                                                           
WYLEXEC  CSECT                                                                  
         DC    C'Go for it!!'                                                   
*                                                                               
         EJECT                                                                  
         COPY  XDSNAME                                                          
         EJECT                                                                  
         COPY  VNENTRY                                                          
         COPY  VPARM                                                            
         EJECT                                                                  
         COPY  XSENTRY                                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ONENTRY - ON CONDITION INFORMATION DSECT                                     
*                                                                               
*  USED FOR PUSHING, POPING ON CONDITION INFO IN VARIABLE STACK                 
*                                                                               
*                                                                               
*  CAUTION:                                                                     
*       SYS.ONERROR VALID ONLY IF CPFONERR OR CPFDDERR SET                      
*       SYS.ONATTN VALID ONLY IF CPFONATN OR CPFDDATN SET                       
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
ONENTRY  RECORD BEGIN                                                           
NNFLAGS  FLAG                                                                   
         FLAG  NNFONERR            ON ERROR SPECIFIED                           
         FLAG  NNFDDERR            ON ERROR SPECIFIED, SUSPENDED                
         FLAG  NNFONATN            ON ATTN SPECIFIED                            
         FLAG  NNFDDATN            ON ATTN SPECIFIED, SUSPENDED                 
NNERRLEN DS    F                   ON ERROR COMMAND LENGTH                      
NNERRCMD DS    CL(&LINESZ)         ON ERROR COMMAND                             
NNATNLEN DS    F                   ON ATTN COMMAND LENGTH                       
NNATNCMD DS    CL(&LINESZ)         ON ATTN COMMAND                              
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ERRENTRY - ERROR INFORMATION DSECT                                           
*                                                                               
*  USED FOR PUSHING, POPING ERROR/ATTENTION INFO ON VARIABLE STACK              
*                                                                               
*                                                                               
*  CAUTION:                                                                     
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
ERRENTRY RECORD BEGIN                                                           
RRFLAGS  FLAG                                                                   
         FLAG  RRFXXERR            ERROR                                        
         FLAG  RRFXXATN            ATTENTION                                    
RRSETNO  DS    F                   ERROR SET NUMBER                             
RRERRID  DS    CL8                 ERROR ID                                     
RREMSGL  DS    F                   ERROR MSG LENGTH                             
RREMSG   DS    CL(&LINESZ)         ERROR MSG                                    
RREINFOL DS    F                   ERROR INFO LENGTH                            
RREINFO  DS    CL(&LINESZ)         ERROR INFO                                   
RRATTNID DS    CL8                 ATTN ID                                      
RRAINFOL DS    F                   ATTN INFO LENGTH                             
RRAINFO  DS    CL(&LINESZ)         ATTN INFO                                    
         END                                                                    
*                                                                               
*                                                                               
ATTNIDLN EQU   8                   ATTN ID MUST BE LENGTH 8!!                   
         DS    0S(8-L'CPERRID)     ERR/ATTN ID MUST BE 8!!                      
         DS    0S(L'CPERRID-8)     ERR/ATTN ID MUST BE 8!!                      
         TITLE ' EXEC File Routines'                                            
*box                                                                            
*                                                                               
*                                                                               
*  EXECINIT - INITIALIZE EXEC FILE PROCESSING                                   
*                                                                               
*                                                                               
INITEXEC XPROC                                                                  
*                                                                               
*                                                                               
         COMMENT                   SET DEFAULT EXEC POINTERS                    
         LA    R15,CPEXEC                                                       
         ST    R15,CPRGEXEC                                                     
         LA    R15,CPFLOW                                                       
         ST    R15,CPRGFLOW                                                     
         LA    R0,CP#XOLD                                                       
         ST    R0,CP#XSET                                                       
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         TITLE 'EXECUTE Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  EXECUTE -- EXEC COMMAND.                                                     
*                                                                               
*                                                                               
*  NOTE: IF EXEC FROM <...>, COMMAND WILL CHECK FOR XPROC.                      
*    IF XPROC FOUND, CODE WILL GENERATE 'XCALL <..>' COMMAND                    
*                                                                               
*                                                                               
EXWA     RECORD BEGIN                                                           
EXFLAG   FLAG  ,                                                                
         FLAG  EXACT               EXEC ACTIVE                                  
         FLAG  EXFROM              EXEC FROM                                    
         FLAG  EXLINE              EXEC <LINE>                                  
EXFLAG2  FLAG  ,                                                                
         FLAG  EXFSAVE             EXEC SAVE                                    
         FLAG  EXFPARM             EXEC PARM SPECIFIED                          
         FLAG  EXFNOPAR            EXEC NOPARM                                  
         FLAG  EXFLNO              SCANNED LINENO                               
         FLAG  EXFRANGE            EXEC RANGE SPECIFIED                         
         FLAG  EXFDEL              EXEC DELETE                                  
         FLAG  EXFPAUSE            EXEC PAUSE                                   
         FLAG  EXNOXCHK            DO NOT CHECK FOR EXTENDED EXEC               
EXLINENO DS    F                   EXEC LINENO, OR START LINENO                 
EXPARM   DS    A                   EXEC PARM LOC                                
EXPARMLN DS    F                   EXEC PARM LEN                                
EXXNAME  DS    CL(L'XDSNAME)       EXEC NAME                                    
EXTXNAME DS    CL(L'XDSNAME)       TEMP EXEC NAME                               
         END                                                                    
*                                                                               
*                                                                               
EXECUTE  XPROC EXWA                                                             
         LA    R6,EXWA                                                          
         USING EXWA,R6                                                          
*                                                                               
         COMMENT                   ENTRANCE EXAM                                
         IF    CPFXTEND,BEGIN                                                   
         ERROR 'EXEC command invalid in extended EXEC mode.'                    
         END                                                                    
*                                                                               
         CLEAR EXWA                CLEAR EXEC WORK AREA                         
         SET   CPLFLG1.CPFALL      DEFAULT RANGE IS ALL                         
         SET   EXFDEL              DEFAULT IS 'DELETE' ON EXE ACT               
         SET   CPFRDEXE            SET SELECT FROM EXEC FILE                    
*                                                                               
         VCALL SCNEXFR             SEE IF ACTIVE OR FROM                        
         IF    CPFRDACT,'SET EXACT '   EXEC ACT                                 
         IF    CPFRDEXT,'SET EXFROM'   EXEC FROM                                
         IF    CPFRDEXE,'SET EXLINE'   EXEC LINENO                              
*                                                                               
XXSCAN   LABEL ,                   SCAN EXEC COMMAND                            
         COMMENT                   WE USE DIFFERENT SCAN TABLES FOR             
         COMMENT                   EXEC ACT, EXEC FROM.., EXEC <LNO>            
         IF     EXACT,BEGIN                                                     
         SCAN   EXECTBLA                                                        
         SCANCHK                                                                
         END                                                                    
         ELSEIF EXFROM,BEGIN                                                    
         VCALL  MKFFINFO                                                        
         SCAN   EXECTBLF                                                        
         SCANCHK                                                                
         END                                                                    
         ELSEIF EXLINE,BEGIN                                                    
         SCAN   EXECTBLC                                                        
         SCANCHK                                                                
         END                                                                    
*                                                                               
*  process common EXEC options,                                                 
         IF    (EXACT,OR,EXFROM),BEGIN                                          
         IF    ^EXFNOPAR,BEGIN           PROCESS PARM FIELD                     
         L     R1,EXPARM                                                        
         L     R0,EXPARMLN                                                      
         VCALL SETPARM                                                          
         END                                                                    
         END                                                                    
         IF    (EXLINE,AND,EXFPARM),BEGIN                                       
         L     R1,EXPARM                                                        
         L     R0,EXPARMLN                                                      
         VCALL SETPARM                                                          
         END                                                                    
         IF    EXFSAVE,BEGIN       IF SAVE OPTION SPECIFIED                     
         LT    R0,CPEXLINE         GET EXEC CURRENT LINE                        
         IF    M,BEGIN             IF NONE, CAN'T SAVE RETURN                   
         ERROR 'No EXEC file, can''t set return.',,RETERR                       
         END                                                                    
         ACALL EXNEXTLN            GET NEXT LINE NUMBER                         
         IF    NZ,BEGIN            IF NONE, CAN'T SAVE RETURN                   
         ERROR 'No EXEC file, can''t set return.',,RETERR                       
         END                                                                    
         ST    R0,CPRETURN         SAVE LINENO FOR LOCATE                       
         END                                                                    
*                                                                               
*  handle special EXEC command types, options                                   
         IF    EXLINE,BEGIN        IF EXEC <LINE NUMBER>,                       
         ACALL XXLINE                 DO IT TO IT                               
         END                                                                    
         IF    EXACT,BEGIN         IF EXEC ACTIVE,                              
         ACALL XXACT                  READ IN ACTIVE FILE, ETC                  
         END                                                                    
         IF    EXFROM,BEGIN        IF EXEC FROM ...,                            
         ACALL XXFROM                 READ IN EXTERNAL FILE, ETC                
         END                                                                    
*                                                                               
*  get set, then start executing commands                                       
         SET   CPFEXEC             TURN ON EXEC MODE                            
         SET   CPFXOLD             AND TURN ON OLD EXEC MODE                    
         IF    EXFLNO,BEGIN        IF EXEC LINENO SPECIFIED                     
         MVC   CPEXNEXT,EXLINENO   SET NEW LINENO                               
         END                                                                    
         IF    EXFPAUSE,BEGIN      IF EXEC PAUSE,,,                             
         ACALL SETPAUSE            GET SET TO PAUSE                             
         B     CVNEXT                                                           
         END                                                                    
         B     CVNEXT              NOW EXEC NEXT EXEC COMMAND !                 
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       process EXEC <LINE NUMBER> .. special options                          
*-                                                                              
XXLINE   PROC                                                                   
*                                                                               
         COMMENT                   CHECK FOR EXEC FILE                          
         ACALL EXECHK                                                           
*                                                                               
         COMMENT                   ???                                          
         IF    CPFKEEP,BEGIN                                                    
         ABORT 'PUSH is an illegal option here.'   Help me Chaz!!               
         END                                                                    
*                                                                               
         IF    CPFCLEAR,BEGIN      AND CLEAR SPECIFIED                          
*        ACALL CHKXOLD                                                          
*        IF    Z,'ACALL NOEXEC  '  NO, GO ISSUE MESSAGE, NO RETURN              
         ACALL CLREX               GO CLEAR EXEC FILE                           
         ACALL SETPAUSE            PAUSE EXEC                                   
         B     CVNEXT                                                           
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XXACT - LOAD OLD EXEC FROM ACTIVE                                            
*                                                                               
*  ON ENTRY:                                                                    
*       NO INPUT PARMS,,, (EXEC ACTIVE .... SCANNED)                            
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*       ALL ERRORS WILL EXIT VIA CVERROR                                        
*                                                                               
*                                                                               
*                                                                               
XXACT    PROC                                                                   
*                                                                               
         COMMENT                   CHECK FOR ACTIVE FILE                        
         VCALL ACTCHK                                                           
*                                                                               
         COMMENT                   SET OLD EXEC SET NUMBER                      
         LA    R15,CPEXEC                                                       
         ST    R15,CPRGLOAD                                                     
         LA    R15,CPFLOW                                                       
         ST    R15,CPRGFC                                                       
         LA    R0,CP#XOLD                                                       
         ST    R0,CP#XLOAD                                                      
*                                                                               
         COMMENT                   CLEAR OLD EXEC, IF NECESSARY                 
         VCALL CHKXOLD                                                          
         IF    NZ,BEGIN            IF EXISTING EXEC, WE MUST CLEAR              
         IF    ^CPFCLEAR,BEGIN                                                  
         SETMSG 'Clear EXEC'                                                    
         VCALL YESREQ                                                           
         END                                                                    
         ACALL CLREX                                                            
         END                                                                    
         CLEAR CPFCLEAR                                                         
*                                                                               
         COMMENT                   CLEAR VAR STACK, USE BASE VARS               
         VCALL CLRVSTK                                                          
*                                                                               
         COMMENT                   MISC INITIALIZATION                          
         ACALL INITERR             INIT/CLEAR ON ERROR, ERR VARS                
         ACALL INITATTN            INIT/CLEAR ON ATTN, ATTN VARS                
         VCALL INITXDMP            TURN OFF EXEC DUMP INFO                      
         CLEAR CPFXQT              CLEAR QUIET EXEC FLAG                        
         CLEAR CPFXAUTHLIB         NOT AUTHORIZED                               
         CLEAR CPFXAUTH                                                         
         CLEAR CPFXOVER                                                         
         MVC   CPLLIMIT,CPLIMIT    LOCAL LIMIT DEFAULTS TO GLOBAL               
*                                                                               
         COMMENT                   IF NO RANGE SPECFIED, SET 'ALL'              
         COMMENT                                                                
         IF    ^EXFRANGE,BEGIN     IF NO RANGE SPECIFIED                        
         VCALL NDETRNG             CALL NDETRNGTO SET 'ALL'                     
         END                                                                    
*                                                                               
         COMMENT                   SAVE EXEC FILE NAME                          
         MVC   EXTXNAME,=CL(L'EXTXNAME)'(ACTIVE file) '                         
         MVC   EXXNAME,=CL(L'EXXNAME)'(ACTIVE file) '                           
*        VCALL SETXNAME            ACT FILE NAME SET ABOVE                      
         LA    R0,L'EXXNAME                                                     
         LA    R1,EXXNAME                                                       
         LA    R2,=CL16'SYS.EXECNAME    '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   LOAD EXEC FILE                               
         ACALL LDOLDACT                                                         
*                                                                               
         COMMENT                   CLEAR ACTIVE, IF DELETE                      
         IF    EXFDEL,BEGIN                                                     
         IF    ^EXFRANGE,BEGIN     CLEAR ENTIRE ACTIVE...                       
         SET   CPFCLEAR+CPFNWARN                                                
         VCALL CLRTEXT             Clear Active                                 
         END                                                                    
*                                                                               
         ELSE  BEGIN               Only delete lines selected...                
         SET   CPLFLG5.CPFNLST      DELETE NOLIST                               
         VCALL DELSUB              GO DELETE RANGE                              
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   SET EXEC RECD GROUP, SET, AND                
         COMMENT                       STARTING LINE NUMBER                     
         MVC   CPRGEXEC,CPRGLOAD                                                
         MVC   CPRGFLOW,CPRGFC                                                  
         MVC   CP#XSET,CP#XLOAD                                                 
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         CLEAR CP#XLOAD                                                         
         CLEAR CPEXLINE            EXEC FIRST LINE NUMBER                       
         CLEAR CPEXNEXT            STARTING AT ZERO                             
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XXFROM - LOAD OLD EXEC FROM DSN                                              
*                                                                               
*  ON ENTRY:                                                                    
*       NO INPUT PARMS,,, (EXEC FROM ... SCANNED)                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO                                                           
*       ALL ERRORS WILL EXIT VIA CVERROR                                        
*                                                                               
*                                                                               
*                                                                               
XXFROM   PROC                                                                   
*                                                                               
         COMMENT                   SET OLD EXEC SET NUMBER                      
         LA    R15,CPEXEC                                                       
         ST    R15,CPRGLOAD                                                     
         LA    R15,CPFLOW                                                       
         ST    R15,CPRGFC                                                       
         LA    R0,CP#XOLD                                                       
         ST    R0,CP#XLOAD                                                      
*                                                                               
         COMMENT                   CLEAR OLD EXEC, IF NECESSARY                 
         VCALL CHKXOLD                                                          
         IF    NZ,BEGIN            IF EXISTING EXEC, WE MUST CLEAR              
         IF    ^CPFCLEAR,BEGIN                                                  
         SETMSG 'Clear EXEC'                                                    
         VCALL YESREQ                                                           
         END                                                                    
         ACALL CLREX                                                            
         END                                                                    
         CLEAR CPFCLEAR                                                         
*                                                                               
         COMMENT                   CLEAR VAR STACK, USE BASE VARS               
         VCALL CLRVSTK                                                          
*                                                                               
         COMMENT                   MISC INITIALIZATION                          
         ACALL INITERR             INIT/CLEAR ON ERROR, ERR VARS                
         ACALL INITATTN            INIT/CLEAR ON ATTN, ATTN VARS                
         VCALL INITXDMP            TURN OFF EXEC DUMP INFO                      
         CLEAR CPFXQT              CLEAR EXEC QUIET FLAG                        
         CLEAR CPFXAUTHLIB         NOT AUTHORIZED                               
         CLEAR CPFXAUTH                                                         
         CLEAR CPFXOVER                                                         
         MVC   CPLLIMIT,CPLIMIT    LOCAL LIMIT DEFAULTS TO GLOBAL               
*                                                                               
         COMMENT                   GET SET TO OPEN EXERNAL FILE                 
         COMMENT                                                                
         VCALL FNAMEFIN            FINISH UP FILE NAME PROCESSING               
         VCALL NRESOLVE            WE NEED FULL NAME FOR EXEC FILE              
         IF    ^EXFRANGE,BEGIN     IF NO RANGE SPECIFIED                        
         VCALL NDETRNG             CALL NDETRNGTO SET 'ALL'                     
         END                                                                    
*                                                                               
         COMMENT                   OPEN EXTERNAL FILE                           
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
*                                                                               
         COMMENT                   SAVE EXEC FILE NAME                          
         LA    R1,EXXNAME                                                       
         VCALL SETXNAME                                                         
         LA    R0,L'EXXNAME                                                     
         LA    R1,EXXNAME                                                       
         LA    R2,=CL16'SYS.EXECNAME    '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   IF LOAD W/OUT EXTENDED CHECK,                
         IF    EXNOXCHK,BEGIN                                                   
         ACALL LDOLDDSN            DO LOAD !                                    
         END                                                                    
*                                                                               
         COMMENT                   IF LOAD WITH XPROC CHECK,                    
         ELSE  BEGIN                                                            
         ACALL LDDSNCHK            LOAD W/CHECK FOR XPROC                       
         IF    NZ,BEGIN            IF XPROC, DO XCALL FOR USER                  
         VCALL EXTCLOSE                                                         
         L     R0,EXPARMLN                                                      
         L     R1,EXPARM                                                        
         ACALL CALXPROC            SET UP, DO XCALL                             
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CLOSE EXTERNAL FILE                          
         VCALL EXTCLOSE                                                         
*                                                                               
         COMMENT                   SET EXEC RECD GROUP, SET, AND                
         COMMENT                       STARTING LINE NUMBER                     
         MVC   CPRGEXEC,CPRGLOAD                                                
         MVC   CPRGFLOW,CPRGFC                                                  
         MVC   CP#XSET,CP#XLOAD                                                 
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         CLEAR CP#XLOAD                                                         
         CLEAR CPEXLINE            EXEC FIRST LINE NUMBER                       
         CLEAR CPEXNEXT            STARTING AT ZERO                             
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'EXEC Command - Scan Table, Scan Routines'                       
         USING EXWA,R6                                                          
*                                                                               
*  EXEC .. STUFF                                                                
*                                                                               
EXECTBLA SCKW  ACTIVE,V(SCNNOOP),A EXEC ACTIVE SCAN TABLE                       
         SCKW  DELETE,SCNDEL,A                                                  
         SCKW  NODELETE,SCNNODEL,A                                              
         SCKW  LINES,SCNLINS,A                                                  
         SCKW  LINS,SCNLINS                                                     
         SCKW  PAUSE,SCANPA,A                                                   
         SCKW  SAVE,SCNSAVE,A                                                   
         SCKW  LOG,SCANLOG                                                      
         SCKW  NOLOG,SCANNLOG,A                                                 
         SCKW  TERSE,V(SCNNOOP),A   (Compatibility--ignored)                    
         SCKW  VERBOSE,V(SCNNOOP),A (Compatibility--ignored)                    
         SCKW  ,SCANLNO,SYMLN                                                   
         SCKW  START,SCANLNO,(A,V(MPSYMLN))                                     
         SCKW  PARM,SCANPARM,(A,P)                                              
         SCKW  NOPARM,SCANNPRM,A                                                
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
EXECTBLF SCKW  FROM,SCANFROM,A     EXEC FROM SCAN TABLE                         
         SCKW  ,V(FNAMPSHF),PUSH                                                
         SCKW  LINES,SCNLINS,A                                                  
         SCKW  LINS,SCNLINS                                                     
         SCKW  PAUSE,SCANPA,A                                                   
         SCKW  SAVE,SCNSAVE,A                                                   
         SCKW  LOG,SCANLOG                                                      
         SCKW  NOLOG,SCANNLOG,A                                                 
         SCKW  TERSE,V(SCNNOOP),A   (Compatibility--ignored)                    
         SCKW  VERBOSE,V(SCNNOOP),A (Compatibility--ignored)                    
         SCKW  ,SCANLNO,SYMLN                                                   
         SCKW  START,SCANLNO,(A,V(MPSYMLN))                                     
         SCKW  PARM,SCANPARM,(A,P)                                              
         SCKW  NOPARM,SCANNPRM,A                                                
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
EXECTBLC SCKW  PAUSE,SCANPA,A                                                   
         SCKW  SAVE,SCNSAVE,A                                                   
         SCKW  LOG,SCANLOG                                                      
         SCKW  NOLOG,SCANNLOG,A                                                 
         SCKW  TERSE,V(SCNNOOP),A   (Compatibility--ignored)                    
         SCKW  VERBOSE,V(SCNNOOP),A (Compatibility--ignored)                    
         SCKW  ,SCANLNO,SYMLN                                                   
         SCKW  START,SCANLNO,(A,V(MPSYMLN))                                     
         SCKW  PARM,SCANPARM,(A,P)                                              
         SCKW  NOPARM,SCANNPRM,A                                                
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(SCNINVAL)                                                     
         EJECT                                                                  
*-                                                                              
*-  NOTE: EXNOXCHK FLAG IS SET IF WE ARE NOT TO CHECK DURING LOADING            
*-       TO SEE IF EXEC IS AN XPROC.  IF EXEC PAUSE, EXEC SAVE, ETC.            
*-       ARE SPECIFIED WE DO NOT CHECK.  THESE DO NOT WORK FOR                  
*-       EXTENDED EXECS.                                                        
*-                                                                              
SCANPA   DPROC ,                                                                
         SET   EXNOXCHK                                                         
         SET   EXFPAUSE            SET EXEC PAUSE                               
         PEND                                                                   
*                                                                               
SCNNODEL DPROC ,                                                                
         SET   EXNOXCHK                                                         
         CLEAR EXFDEL              TURN OFF 'DELETE' OPTION                     
         PEND                                                                   
*                                                                               
SCNDEL   DPROC ,                                                                
         SET   EXNOXCHK                                                         
         SET   EXFDEL              TURN ON 'DELETE' OPTION                      
         PEND                                                                   
*                                                                               
SCNSAVE  DPROC ,                                                                
         SET   EXNOXCHK                                                         
         SET   EXFSAVE             SET SAVE OPTION SPECIFIED                    
         PEND                                                                   
*                                                                               
SCNLINS  PROC  ,                                                                
         SET   EXNOXCHK                                                         
         IF    EXFRANGE,BEGIN      HAS RANGE ALREADY BEEN GIVEN                 
         ERROR 'LINES option specifed twice. '                                  
         END                                                                    
         SET   EXFRANGE            SET RANGE SPECIFIED                          
         VCALL NDETRNG             GO SCAN RANGE                                
         TM    CPLFLG1,CPFALL      WAS SOMETHING SPECIFIED                      
         IF    Z,BEGIN                                                          
         ERROR 'Missing range after LINES keyword. '                            
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANPARM PROC  ,                                                                
         SET   EXFPARM                                                          
         VCALL DEQUOTE             DEQUOTE, IF STRING                           
         IF    NEG,BEGIN           NOT A STRING...                              
*-                                                                              
*-       In addition to PARM="string", also allow PARM /string.                 
*-                                                                              
         IF    (@R1,EQ,'/'),BEGIN  Allow /string...                             
         SCINFO                                                                 
         ST    R1,EXPARM                                                        
         ST    R0,EXPARMLN                                                      
         LA    R15,4               Return to SCAN caller                        
         B     SCPMDONE                                                         
         END                                                                    
         IF    (@R1,NE,'('),BEGIN  NO GOOD                                      
         VCALL NOTVALID                                                         
         END                                                                    
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         DECR  R0                                                               
         END                                                                    
         ST    R1,EXPARM           SAVE PARM PTRS                               
         ST    R0,EXPARMLN                                                      
         CLEAR R15                                                              
SCPMDONE LABEL ,                                                                
         PEND                                                                   
*                                                                               
SCANNPRM DPROC  ,                                                               
         SET   EXNOXCHK                                                         
         SET   EXFNOPAR            DO NOT SET PARM                              
         PEND                                                                   
*                                                                               
SCANLNO  PROC  ,                                                                
         SET   EXNOXCHK                                                         
         ST    R0,EXLINENO         SAVE SPECIFIED LINE NO.                      
         IF    EXFLNO,BEGIN        HAS EXEC LINE BEEN GIVEN                     
         ERROR 'Exec LINENO specifed twice. '                                   
         END                                                                    
         SET   EXFLNO              SET LINE NO. SPECIFIED                       
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANFROM PROC  ,                                                                
         VCALL DOFNAME             GET THE FILE NAME                            
         CLEAR R15                                                              
         PEND  ,                   GO RESTART SCAN                              
*                                                                               
SCANLOG  DPROC ,                                                                
         SET   EXNOXCHK                                                         
         SET   CPFLOG                                                           
         PEND                                                                   
*                                                                               
SCANNLOG DPROC ,                                                                
         SET   EXNOXCHK                                                         
         CLEAR CPFLOG                                                           
         PEND                                                                   
*                                                                               
SCANNLIM DPROC ,                   SET NO EXEC LIMIT                            
         SET   EXNOXCHK                                                         
         CLEAR CPLIMIT             LIMIT=0 IS NOLIMIT                           
         CLEAR CPLLIMIT                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANLIM  PROC  ,                   SET EXEC LIMIT                               
         SET   EXNOXCHK                                                         
         STH   R0,CPLIMIT                                                       
         IF    (CPLLIMIT,LT,CPLIMIT),BEGIN                                      
         MVC   CPLLIMIT,CPLIMIT                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANNLLM DPROC ,                   SET NO LOCAL EXEC LIMIT                      
         SET   EXNOXCHK                                                         
         CLEAR CPLLIMIT            LIMIT=0 IS NOLIMIT                           
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANLLM  PROC  ,                   SET LOCAL EXEC LIMIT                         
         SET   EXNOXCHK                                                         
         IF    (R0,Z),BEGIN                                                     
         CLEAR CPLLIMIT                                                         
         END                                                                    
         ELSEIF (R0,GT,CPLLIMIT),BEGIN                                          
         STH   R0,CPLLIMIT                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCANAUTO PROC  ,                                                                
         TSEG  'AUTO OPTION HAS BEEN REMOVED, USE THE',,B                       
         SETMSG 'SET AUTO EXEC COMMAND INSTEAD.'                                
         B     CVABTMSG                                                         
         PEND                                                                   
         SPACE 2                                                                
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*  SET EXEC STUFF                                                               
*                                                                               
STEXEPSH XPUSHRTN STEXEKWS                                                      
*                                                                               
STEXEKWS SCKW  AUTO,SCANAUTO,(A,S)                                              
         SCKW  NOAUTO,SCANAUTO,(A,S)                                            
         SCKW  LOG,SETELOG                                                      
         SCKW  NOLOG,SETENLOG,A    (NOL=NOLOG-- NOT NOLIMIT)                    
         SCKW  LIMIT,SETELIM,(A,P,PI),32000                                     
         SCKW  NOLIMIT,SETENLIM,A                                               
         SCKW  LLIMIT,SETELLM,(A,P,PI),32000                                    
         SCKW  NOLLIMIT,SETENLLM,A                                              
         SCKW  TERSE,V(SCNNOOP),A   (Compatibility--ignored)                    
         SCKW  VERBOSE,V(SCNNOOP),A (Compatibility--ignored)                    
         SCKW  ,,END                                                            
         EJECT                                                                  
*                                                                               
SETELOG  DPROC ,                                                                
         SET   CPFLOG                                                           
         PEND                                                                   
*                                                                               
SETENLOG DPROC ,                                                                
         CLEAR CPFLOG                                                           
         PEND                                                                   
*                                                                               
SETENLIM DPROC ,                   SET NO EXEC LIMIT                            
         CLEAR CPLIMIT             LIMIT=0 IS NOLIMIT                           
         CLEAR CPLLIMIT                                                         
         PEND                                                                   
*                                                                               
SETELIM  PROC  ,                   SET EXEC LIMIT                               
         STH   R0,CPLIMIT                                                       
         IF    (CPLLIMIT,LT,CPLIMIT),BEGIN                                      
         MVC   CPLLIMIT,CPLIMIT                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETENLLM DPROC ,                   SET NO LOCAL EXEC LIMIT                      
         CLEAR CPLLIMIT            LIMIT=0 IS NOLIMIT                           
         PEND                                                                   
*                                                                               
SETELLM  PROC  ,                   SET LOCAL EXEC LIMIT                         
         IF    (R0,Z),BEGIN                                                     
         CLEAR CPLLIMIT                                                         
         END                                                                    
         ELSEIF (R0,GT,CPLLIMIT),BEGIN                                          
         STH   R0,CPLLIMIT                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
         TITLE 'EXEC (old) Load Routines'                                       
*box                                                                            
*                                                                               
*                                                                               
*  LDOLDACT - LOAD EXEC FILE (OLD)                                              
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - OLD EXEC SET NUMBER                                          
*      @CPRGEXEC - EXEC RECORD GROUP                                            
*      @CPRGFC - FLOW INFO RECORD GROUP                                         
*                                                                               
*  ON EXIT:                                                                     
*       ACTIVE FILE LOADED,                                                     
*       ERRORS EXIT VIA CVERROR                                                 
*                                                                               
*  NOTE: EXEC ACT WILL NOT HANDLE EXTENDED EXECS                                
*  NOTE: EXTENDED EXECS ARE LOADED IN XCALL MODULE                              
*                                                                               
*                                                                               
LDWA     RECORD BEGIN                                                           
LDFLAGS  FLAG  ,                   LOAD FLAGS                                   
         FLAG  LDXPROC             TRYING TO EXEC XPROC,,, A NO-NO              
         FLAG  LDFIRST             READ 1ST NON-COMMENT LINE OF EXEC            
LDLNCNT  DS    F                   TOTAL EXEC LINE COUNT (NOT USED)             
LDLNO    DS    F                   LINE NUMBER                                  
LDLEN    DS    F                   LINE LENGTH                                  
LDLINE   DS    CL(&LINESZ)         LINE TEXT                                    
         END                                                                    
*                                                                               
*                                                                               
LDOLDACT PROC  LDWA                                                             
         LA    R1,LDWA                                                          
         ST    R1,CPCWA            SAVE WORK AREA FOR CO-ROUTINE                
*                                                                               
         CLEAR CPFNCUR             DO NOT RESET CURRENT                         
         RDRNG OLDLOOP,(DESRTRN+LOCATRTN+DESABORT+UNPRST)                       
         IF    NZ,BEGIN            IF ATTN, QUIT EXEC LOAD                      
         ACALL CLREX                                                            
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LDOLDDSN - LOAD EXEC FILE (OLD)                                              
*                                                                               
*  NOTE, DOES NOT CHECK FOR XPROC.                                              
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - OLD EXEC SET NUMBER                                          
*      @CPRGEXEC - EXEC RECORD GROUP                                            
*      @CPRGFC - FLOW INFO RECORD GROUP                                         
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, EXEC FILE LOADED OK                                       
*                                                                               
*                                                                               
LDOLDDSN PROC  LDWA                                                             
*                                                                               
         LA    R1,LDWA                                                          
         ST    R1,CPCWA            SAVE WORK AREA FOR CO-ROUTINE                
*                                                                               
         CLEAR CPFNCUR             DO NOT RESET CURRENT                         
         RDRNG OLDLOOP,(DESRTRN+LXCATRTN+DESABORT+UNPRST)                       
         IF    NZ,BEGIN            IF ATTN (ERR),  QUIT EXEC LOAD               
         ACALL CLREX                                                            
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         B     CVERROR             EXIT VIA CVERROR                             
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       LOAD EXEC FILE CO-ROUTINE !                                            
*-       ON ENTRY: R1,R0 - LINE TEXT LOC, LENGTH                                
*-           CPLCNO - LINE NUMBER                                               
*-                                                                              
OLDLOOP  PROC                                                                   
*                                                                               
         L     R6,CPCWA                                                         
         USING LDWA,R6                                                          
*                                                                               
         COMMENT                   MOVE INPUT LINE TO STACK                     
         COMMENT ,                      (OFF OF MOVABLE PAGE)                   
         L     R15,CPLCNO                                                       
         ST    R15,LDLNO                                                        
         ST    R0,LDLEN                                                         
         LR    R3,R0                                                            
         MOVE  R3,LDLINE,@R1                                                    
*                                                                               
         COMMENT                   NOW MOVE LINE TO EXEC FILE                   
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
         L     R15,CPRGLOAD                                                     
         L     R0,LDLEN                                                         
         LA    R1,LDLINE                                                        
         LA    R2,LDLNO                                                         
         VCALL XPUT                                                             
         IF    NEG,BEGIN                                                        
         ERROR 'Too many EXEC files loaded.'                                    
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LDOLDDSN - LOAD EXEC FILE (OLD)                                              
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - OLD EXEC SET NUMBER                                          
*      @CPRGEXEC - EXEC RECORD GROUP                                            
*      @CPRGFC - FLOW INFO RECORD GROUP                                         
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, EXEC FILE LOADED OK                                       
*            CC=NZ, EXEC IS EXTENDED, EXEC NOT LOADED                           
*          .. ANY OTHER ERRORS (ATTN) EXIT VIA CVERROR                          
*                                                                               
*  NOTE: IF EXTENDED EXEC IS ENCOUNTERED, WE ABORT EXEC LOAD                    
*       AND PASS XCALL COMMAND TO LOAD EXTENDED EXEC                            
*                                                                               
LDDSNCHK PROC  LDWA                                                             
*                                                                               
         LA    R1,LDWA                                                          
         ST    R1,CPCWA            SAVE WORK AREA FOR CO-ROUTINE                
         CLEAR LDFLAGS                                                          
         CLEAR LDLNCNT                                                          
*                                                                               
         CLEAR CPFNCUR             DO NOT RESET CURRENT                         
         RDRNG CHKLOOP,(DESRTRN+LXCATRTN+DESABORT+UNPRST)                       
*                                                                               
         COMMENT                   IF LOADED OK, CC=ZERO                        
         IF    ((R15,Z),AND,^LDXPROC),BEGIN                                     
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF EXEC IS XPROC,                            
         ELSEIF LDXPROC,BEGIN      QUIT EXEC LOAD,                              
         ACALL CLREX                                                            
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         TCLEAR                    CLEAR RANGE ABORT MESSAGE                    
         LA    R15,4               RETURN CC=NZ                                 
         END                                                                    
*                                                                               
         ELSE  BEGIN               IF ATTN (ERR), QUIT EXEC LOAD                
         ACALL CLREX                                                            
         CLEAR CP#XLOAD                                                         
         CLEAR CPRGLOAD                                                         
         CLEAR CPRGFC                                                           
         B     CVERROR             EXIT VIA CVERROR                             
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       LOAD EXEC FILE CO-ROUTINE !                                            
*-       ON ENTRY: R1,R0 - LINE TEXT LOC, LEN                                   
*-           CPCLNO - LINE NUMBER                                               
*-                                                                              
CHKLOOP  PROC                                                                   
         L     R6,CPCWA                                                         
         USING LDWA,R6                                                          
*                                                                               
         COMMENT                   MOVE INPUT LINE TO STACK                     
         COMMENT ,                      (OFF OF MOVABLE PAGE)                   
         L     R15,CPLCNO                                                       
         ST    R15,LDLNO                                                        
         ST    R0,LDLEN                                                         
         LR    R3,R0                                                            
         MOVE  R3,LDLINE,@R1                                                    
*                                                                               
         COMMENT                   NOW MOVE LINE TO EXEC FILE                   
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
         L     R15,CPRGLOAD                                                     
         L     R0,LDLEN                                                         
         LA    R1,LDLINE                                                        
         LA    R2,LDLNO                                                         
         VCALL XPUT                                                             
         IF    NEG,BEGIN                                                        
         ERROR 'Too many EXEC files loaded.'                                    
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK IF EXTENDED EXEC                       
         COMMENT                   R1,R0 - LINE LOC, LEN                        
         COMMENT                   R6 - LDWA, PASS COMMON WORK AREA             
         ACALL CHKXPROC            CHECK IF XPROC                               
         IF    LDXPROC,BEGIN       IF XPROC, ABORT RANGE, ETC..                 
         SET   CPFRQUIT            ABORT RANGE                                  
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKXPROC - CHECK IF FIRST STATEMENT IS XPROC                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF EXEC LINE                                                
*       R1 - LOCATION OF EXEC LINE                                              
*       R6 - COMMON WORK AREA                                                   
*                                                                               
*  ON EXIT:                                                                     
*      LDXPROC FLAG SET IF XPROC                                                
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
CHKXPROC PROC                                                                   
*                                                                               
         L     R6,CPCWA                                                         
         USING LDWA,R6                                                          
*                                                                               
         COMMENT                   IF HAVE NOT YET SEEN 1ST LINE                
         IF    ^LDFIRST,BEGIN      WE CHECK IT OUT NOW                          
*                                                                               
         COMMENT                   SCAN LINE                                    
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R1                R1,R0 - TOKEN IN UPPER CASE                  
         IF    (R0,NZ),BEGIN                                                    
         SET   LDFIRST             WE HAVE READ OUR FIRST LINE                  
         IF    (@R1,EQ,'XPROC   '),BEGIN                                        
         SET   LDXPROC                                                          
         END                                                                    
         END   , OF NON NULL SCAN                                               
         END   , OF HAVE NOT YET READ FIRST                                     
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CALXPROC - WE HAVE EXTENDED EXEC, XCALL IT !                                 
*                                                                               
*  ON ENTRY:                                                                    
*       DSNWADSN - EXEC FILE NAME                                               
*       R0,R1 - EXEC PARM                                                       
*                                                                               
*  ON EXIT:                                                                     
*       CPCMD, CPCMDLEN - LOC, LEN OF NEW COMMAND                               
*       WE EXIT BY CALLING EDTCOM TO EXCUTE XCALL COMMAND.                      
*                                                                               
*                                                                               
*                                                                               
CLXWA    RECORD BEGIN                                                           
CLXCMD   DS    CL(L'CPCMD)         FORMATTING AREA FOR XCALL CMD                
         END                                                                    
*                                                                               
*                                                                               
CALXPROC PROC   CLXWA                                                           
*                                                                               
         COMMENT                   XCALL <DSN NAME> / <PARM>                    
*                                                                               
         COMMENT                   INIT CP COMMAND AREA                         
         MVC   CLXCMD,CVBLANKS                                                  
         MVC   CLXCMD(6),=CL6'XCALL '                                           
*                                                                               
         COMMENT                   MOVE IN <DSN NAME>[#LIB NAME]                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
         SETMSG FFRNAME,L:FFRNAMEL  FULL NAME                                   
         IF    (R0,Z),BEGIN        IF NONE, SPECIFIED NAME                      
         SETMSG FFNAME,L:FFNAMEL    ** DEBUG ** CNAME SHOULD BE NZ!             
         END                                                                    
         DROP  R6                                                               
         LA    R2,CLXCMD+6                                                      
         VCALL RETLIBMB                                                         
         AR    R2,R0                                                            
         MVI   @R2,C' '                                                         
         INCR  R2                  R2 - END OF NAME +1 FOR BLANK                
*                                                                               
         COMMENT                   ADD PARM                                     
         MVI   @R2,C'/'                                                         
         LA    R2,2(R2)                                                         
         LA    R4,L'CLXCMD-6-44-8-6 MAX PARM SIZE                               
         IF    (R0,GT,R4),BEGIN                                                 
         ERROR 'Parameter too large.  Command length overflow.'                 
         END                                                                    
         LR    R3,R0                                                            
         MOVE  R3,@R2,@R1                                                       
*                                                                               
         COMMENT                   CALCULATE CMD LENGTH,                        
         AR    R2,R0                                                            
         LA    R4,CLXCMD                                                        
         SR    R2,R4                                                            
         STH   R2,CPCMDLEN                                                      
         MVC   CPCMD,CLXCMD        MOVE COMMAND TO CPCMD                        
*                                                                               
         COMMENT                   EXECUTE COMMAND                              
         CLEAR CPFTYPED            COMMAND NOT TYPED                            
         LA    R1,CPCMD            R1,R0 - COMMAND LOC, LEN                     
         LH    R0,CPCMDLEN                                                      
         VCALL EDTCOM              EXECUTE COMMAND, DOES NOT RETURN             
*                                                                               
         BOMB  ,                   NEVER HIT, WE EXIT ABOVE                     
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'EXEC File Related Commands'                                     
*box                                                                            
*                                                                               
*                                                                               
*  XPAUSE - PAUSE EXEC (NEW OR OLD)                                             
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - 'XPAUSE' SCANNED                                               
*                                                                               
*                                                                               
*                                                                               
XPAUSE   XPROC ,                                                                
*                                                                               
         COMMENT                   SCAN/PROCESS OPTIONS                         
         SCAN EXPPRT                                                            
         SCANCHK                                                                
*                                                                               
         COMMENT                   PAUSE !                                      
         ACALL EXPAUSE             ---> DOES NOT RETURN                         
*                                                                               
         PEND  ,                   WE EXIT ABOVE,                               
         SPACE 2                                                                
EXPPRT   SCKW  MSG,EXPMSG,P                                                     
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 2                                                                
EXPMSG   PROC  ,                                                                
         VCALL DEQUOTE             DEQUOTE MESSAGE                              
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid; quotes required.',,SYNTAX                            
         END                                                                    
         TSEG  (R1),(R0)           SEG MESSAGE                                  
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XRESET - RESET EXEC (EXTENDED OR OLD)                                        
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - 'XRESET' SCANNED                                               
*                                                                               
*                                                                               
*                                                                               
XRESET   XPROC                                                                  
*                                                                               
         COMMENT                   RESET EXEC                                   
         ACALL EXRESET                                                          
         ACALL EXREINIT            MAKE 'XRESET ALL' THE DEFAULT                
*                                                                               
         COMMENT                   SCAN/PROCESS OPTIONS                         
         SCAN  EXRPRT                                                           
         SCANCHK                                                                
*                                                                               
         COMMENT                   PAUSE !                                      
         ACALL SETPAUSE                                                         
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                   WE EXIT ABOVE                                
         SPACE 2                                                                
EXRPRT   SCKW  MSG,EXRMSG,P                                                     
         SCKW  ALL,EXRALL                                                       
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 2                                                                
EXRMSG   PROC  ,                   XRESET MSG='...'                             
         VCALL DEQUOTE             DEQUOTE MESSAGE                              
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid; quotes required.',,SYNTAX                            
         END                                                                    
         TSEG  (R1),(R0)           SEG MESSAGE                                  
         CLEAR R15                                                              
         PEND                                                                   
         SPACE 2                                                                
EXRALL   PROC  ,                   EXEC RESET ALL                               
         ACALL EXREINIT            REINITIALIZE EXEC FILES                      
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XGO - RESTART EXEC COMMAND                                                   
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - 'XGO' SCANNED                                                  
*                                                                               
*                                                                               
*                                                                               
XGO      XPROC                                                                  
*                                                                               
         COMMENT                   IF IN EXEC, WHY USE IT ?                     
         IF    CPFEXEC,BEGIN                                                    
         ERROR 'XGO: command invalid from within EXEC file.'                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR EXEC FILE                          
         ACALL EXECHK                                                           
*                                                                               
         COMMENT                   RESTART EXEC                                 
         SET   CPFEXEC             TURN ON EXEC MODE FLAG                       
         B     CVNEXT              AND GO GET NEXT EXEC COMMAND                 
*                                                                               
         PEND                                                                   
         EJECT                                                                  
***                                                                             
***  XCMDAREA is the format of the area pointed to by CPXCMDP.                  
***  Warning:  COMM and FUNC reference this area without using a                
***            mapping DSECT.                                                   
***                                                                             
XCMDAREA RECORD BEGIN                                                           
XCMDLEN  DS    H                   Length of text which follows                 
XCMDCMD  DS    XL(L'CPCMD)         Command text                                 
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  XCOMMAND -- Set pending command.                                             
*                                                                               
XCOMYUCK XPROC                                                                  
*-                                                                              
*-       Get area to save XCOMMAND text in.                                     
*-                                                                              
         IF    ('LT R6,CPXCMDP',Z),BEGIN  Get XCMDAREA...                       
         LA    R0,L'XCMDAREA                                                    
         VCALL GETCORE                                                          
         LR    R6,R1                                                            
         ST    R6,CPXCMDP          Save XCMD ptr                                
         END                                                                    
*                                                                               
         USING XCMDAREA,R6                                                      
         CLEAR XCMDAREA                                                         
*-                                                                              
*-       Get text after the command verb.                                       
*-                                                                              
         SCINFO                                                                 
         VCALL LRTRIM                                                           
*-                                                                              
*-       Nothing means reset any previous XCOMMAND text.                        
*-                                                                              
         IF    (R0,NP),BEGIN       No command, free XCMDAREA...                 
         LA    R1,XCMDAREA                                                      
         VCALL FREECORE            Free it                                      
         CLEAR CPXCMDP                                                          
         END                                                                    
*-                                                                              
*-       Save new command to execute.                                           
*-                                                                              
         ELSE  BEGIN               Save command text...                         
         CEIL  R0,L'CPCMD                                                       
         STH   R0,XCMDLEN                                                       
         LR    R15,R0                                                           
         MOVE  R15,XCMDCMD,@R1                                                  
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         DROP  XCMDAREA                                                         
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHOWEXEC -- SHOW EXEC command.                                               
*  EXECDISP - subroutine (called by SHOW OPTIONS).                              
*                                                                               
*                                                                               
SHOWEXEC XPROC                                                                  
         SCAN  SHOXPRT             Allow collect options                        
         SCANCHK                                                                
         ACALL EXECDISP                                                         
         B     CVNEXT                                                           
         PEND                                                                   
SHOXPRT  SCKW  ,V(COLLPSH),(PUSH,END)                                           
         SPACE 2                                                                
EXECDISP XPROC                                                                  
         IF    ^CPFLOG,'TSEG "NO"'                                              
         TSEG  'LOG',,B                                                         
*                                                                               
         IF    (CPLIMIT,Z),'SETMSG "NOLIMIT"'                                   
         ELSE  'TSEG "LIMIT="; BTD CPDOUB,0,LH:CPLIMIT'                         
         TSEG  (R1),(R0),B                                                      
         TSEG  '- EXEC Options',,WR                                             
         BNZ   CVABORT                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLEAR OLD EXEC FILE                                                          
*                                                                               
*  CLEARS ONLY OLD EXEC FILES !!  NEW EXEC FILES MAY NOT                        
*  BE CLEARED !!  ONLY EXITED.                                                  
*                                                                               
*                                                                               
CLREX    XPROC                                                                  
*                                                                               
         COMMENT                   EXTENDED EXEC MAY NOT BE CLEARED             
         IF    CPFXTEND,BEGIN                                                   
         ERROR 'Extended EXEC files may not be cleared.'                        
         END                                                                    
*                                                                               
         COMMENT                   DELETE OLD EXEC RECORD SET                   
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XOLD                                                       
         VCALL XDELSET                                                          
         LA    R15,CPFLOW                                                       
         LA    R0,CP#XOLD                                                       
         VCALL XDELSET                                                          
*                                                                               
         MVC   CPEXLINE,=F'-1000'  NO CURRENT EXEC LINENO                       
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  RESTORE -- RESTORE COMMAND.                                                  
*                                                                               
*                                                                               
RSWA     RECORD BEGIN                                                           
RSLNO    DS    F                   LINE NUMBER                                  
RSLINE   DS    XL(&LINESZ)         LINE BUFFER                                  
         END                                                                    
*                                                                               
*                                                                               
RESTORE  XPROC RSWA                                                             
*                                                                               
         COMMENT                   EXTENDED EXECS MAY NOT BE RESTORED           
         IF    CPFXTEND,BEGIN                                                   
         ERROR 'Extended EXEC files may not be restored.'                       
         END                                                                    
         COMMENT                   IF BREAK IN EXTEND EXEC,                     
         IF    ((CP#XSET,NE,=A(CP#XOLD)),AND,(CP#XSET,NE,0)),BEGIN              
         ERROR 'Extended EXEC files may not be restored.'                       
         END                                                                    
         IF    (CP#XSET,EQ,0),BEGIN  WEIRD CASE, SHOULD NOT OCCUR               
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NO EXEC FILE, ERROR                       
         ACALL CHKXOLD                                                          
         IF    Z,BEGIN                                                          
         ACALL SETPAUSE                                                         
         ERROR 'No EXEC file.',,NOEXEC                                          
         END                                                                    
*                                                                               
         COMMENT                   SCAN RESTORE OPTIONS                         
         SCAN  RESTBL1             SCAN RESTORE OPTIONS                         
         SCANCHK                                                                
         COMMENT                                                                
         VCALL CLRTEXT             CLEAR ACTIVE FILE IF NECES                   
*                                                                               
         COMMENT                   COPY EXEC TO ACTIVE                          
         CLEAR RSLNO                                                            
         LA    R1,RSLINE                                                        
         LA    R2,RSLNO                                                         
         ACALL EXFRST              GET FIRST EXEC LINE                          
         WHILE Z,BEGIN                                                          
         L     R15,RSLNO                                                        
         VCALL PUTLINE             PUT LINE IN ACTIVE FILE                      
         ACALL EXNEXT              GET NEXT EXEC LINE                           
         END   ,                   LOOP THRU ENTIRE EXEC FILE                   
*                                                                               
         COMMENT                   SET MISC INFO FIELDS                         
         MVC   CPEXLINE,=F'-1000'  NO CURRENT EXEC LINENO                       
         CLEAR CPACHCT             CLEAR CHANGE COUNT                           
*                                                                               
         COMMENT                   CLEAR OLD EXEC RECORD SET                    
         VCALL CLREX               CLEAR OLD EXEC RECORD SET                    
*                                                                               
         ACALL SETPAUSE            TURN OFF EXEC                                
         B     CVNEXT              GET NEXT COMMAND FROM USER                   
         PEND                                                                   
*                                                                               
*                                                                               
*  RESTORE SCKW STUFF                                                           
*                                                                               
RESTBL1  SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
         QLTORG                                                                 
         TITLE 'EXEC File Related Routines'                                     
*box                                                                            
*                                                                               
*                                                                               
*  SETPAUSE - PAUSE EXEC FILE                                                   
*                                                                               
*  NOTE: THIS ROUTINE SHOULD BE USED BY ANYONE WISHING TO TURN                  
*        OFF CPEXEC FLAG.  IF THIS FLAG IS CLEARED, EXEC WILL                   
*        PAUSE.                                                                 
*                                                                               
*                                                                               
SETPAUSE XPROC                                                                  
         CLEAR CPFEXEC             CLEAR CPFEXEC FLAG                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXPAUSE - PAUSE EXEC FILE                                                    
*                                                                               
*  THIS ROUTINE DOES NOT RETURN !!                                              
*                                                                               
*                                                                               
EXPAUSE  XPROC                                                                  
*                                                                               
         ACALL SETPAUSE            SET PAUSE FLAGS                              
         B     CVNEXT              GET NEXT COMMAND                             
*                                                                               
         COMMENT                   THIS ROUTINE DOES NOT RETURN                 
         BOMB                                                                   
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXRESET - RESET EXEC, RESET TO BASE VARIABLES                                
*                                                                               
*                                                                               
*                                                                               
EXRESET  XPROC                                                                  
*                                                                               
         COMMENT                   SET DEFAULT EXEC POINTERS                    
         LA    R1,CPEXEC                                                        
         ST    R1,CPRGEXEC                                                      
         LA    R1,CPFLOW                                                        
         ST    R1,CPRGFLOW                                                      
         LA    R1,CP#XOLD                                                       
         ST    R1,CP#XSET                                                       
*                                                                               
         COMMENT                   CLR ACTIVE EXEC'S LIST                       
         VCALL CLRALIST                                                         
*                                                                               
         COMMENT                   CLR VAR STACK, POINT TO BASE VARS            
         VCALL CLRVSTK                                                          
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXREINIT - RE-INITIALIZE EXEC FILE SYSTEM                                    
*                                                                               
*  REINITIALIZE EXEC FILE SYSTEM !! OOOOHHH YEAH !                              
*                                                                               
*  THIS ROUTINE IS CODED ON PRACTICAL LINES, ADD WHATEVER IS                    
*  NEEDED TO RESTORE THE EXEC TO THE STATE IT WAS IN AT LOGON                   
*  TIME (MORE OR LESS BUD!)  THIS ROUTINE DOES UNLOAD ALL                       
*  EXECS.                                                                       
*                                                                               
*                                                                               
EXREINIT XPROC                                                                  
*                                                                               
         COMMENT                   CLEAR EXEC, FLOW, AND VARS RECORD            
         COMMENT                     GROUPS                                     
         LA    R15,CPEXEC                                                       
         VCALL XDELGRP                                                          
         LA    R15,CPFLOW                                                       
         VCALL XDELGRP                                                          
         LA    R15,CPVARS                                                       
         VCALL XDELGRP                                                          
*                                                                               
         COMMENT                   CLEAR EXEC FILE TABLES                       
         LA    R15,CPRG16                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XDELSET                                                          
         LA    R15,CPRG64                                                       
         LA    R0,CP#XFILE                                                      
         VCALL XDELSET                                                          
*                                                                               
         COMMENT                   INITIALIZE BASE VARIABLES                    
         VCALL INITVARS                                                         
*                                                                               
         COMMENT                   INITIALIZE EXEC POINTERS                     
         VCALL INITEXEC                                                         
*                                                                               
         COMMENT                   INITIALIZE FLOW STUFF                        
         VCALL INITFLOW                                                         
*                                                                               
         COMMENT                   INITIALIZE LOAD LISTS                        
         COMMENT ,                 (LRU EXEC, CALLED EXECS)                     
         VCALL INITXCAL                                                         
*                                                                               
         COMMENT                   INIT ERROR, ATTN STUFF                       
         ACALL INITERR                                                          
         ACALL INITATTN                                                         
*                                                                               
         COMMENT                   CLEAR EXEC DUMP, QUIET FLAGS                 
         VCALL INITXDMP                                                         
         CLEAR CPFXQT                                                           
*                                                                               
         COMMENT                   DEBUG STUFF IS NOT RESET,,                   
         COMMENT                   XRESET IS OFTEN USED IN DEBUGGING            
*                                                                               
         COMMENT                   CLEAR EXEC MONITOR, IF ANY                   
         VCALL MCLEAR                                                           
*                                                                               
         COMMENT                   CLEAR ESC, SKIP CHARS                        
         CLEAR CPESCAPE                                                         
         CLEAR CPSKIP                                                           
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXECHK - CHECK FOR NULL EXEC FILE                                            
*                                                                               
*  IF NULL EXEC FILE, GIVE MESSAGE AND EXIT VIA                                 
*  CVERROR.                                                                     
*                                                                               
*                                                                               
EXECHK   XPROC                                                                  
         ACALL CHKEXEC                                                          
         IF    Z,BEGIN                                                          
         ERROR 'No EXEC file.',,NOEXEC                                          
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKEXEC - CHECK FOR NULL EXEC FILE                                           
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=Z, NULL EXEC FILE                                              
*             CC=NZ, EXEC FILE EXISTS                                           
*                                                                               
*                                                                               
CHKEXEC  XPROC                                                                  
*                                                                               
         L     R15,CPRGEXEC                                                     
         L     R0,CP#XSET                                                       
         VCALL XCHKSET                                                          
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKXOLD - CHECK FOR NULL OLD EXEC FILE                                       
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=Z, NULL EXEC FILE                                              
*             CC=NZ, OLD EXEC FILE EXISTS                                       
*                                                                               
*                                                                               
CHKXOLD  XPROC                                                                  
*                                                                               
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XOLD                                                       
         VCALL XCHKSET                                                          
*                                                                               
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'READ Command'                                                   
*box                                                                            
*                                                                               
*                                                                               
*  READ COMMANDS                                                                
*                                                                               
*  READ, READ VALUE, AND READ STRING COMMAND                                    
*        ALL OPTIONS ARE SCANNED FIRST, THEN THE INPUT LINE IS                  
*        OBTAINED FROM THE TYPEWRITER, ACTIVE FILE, OR EXEC FILE,               
*        AND FINALLY THE VALUE, STRING, OR COMMAND IS PROCESSED.                
*                                                                               
*                                                                               
*                                                                               
RDWA     RECORD BEGIN                                                           
RDFLAGS  FLAG  ,                   READ FLAG BYTE                               
         FLAG  RDFLINE             - READ LINE NUMBER GIVEN                     
         FLAG  RDFDEL              - DELETE OPTION GIVEN                        
         FLAG  RDFVAL              - READ VALUE OR READ STRING                  
         FLAG  RDFSTR              - READ STRING                                
         FLAG  RDFATTN             - ATTN LINENO GIVEN                          
         FLAG  RDFPRMPT            - PROMPT OPTION EXPLICIT                     
*                                                                               
RDTYPE   FLAG                                                                   
         FLAG  RDFACT              - READ FROM ACTIVE FILE                      
         FLAG  RDFEXEC             - READ FROM EXEC                             
         FLAG  RDFFROM             - READ FROM EXTERNAL FILE                    
*                                                                               
RDCOL1   DS    F                   FIRST COLUMN NO. - 1                         
RDCOL2   DS    F                   SECOND COL NO. - FIRST + 1                   
RDLINENO DS    F                   READ LINE NUMBER                             
RDBUFLEN DS    F                   READ TEXT LENGTH                             
RDBUF    DS    CL(&LINESZ)         READ TEXT                                    
RDPRMPTL DS    F                   READ PROMPT LENGTH                           
RDPRMPT  DS    CL(&LINESZ)         READ PROMPT AREA                             
RDBUD    DS    0F                  NO BIG DEAL, NOT USED                        
RDMAXVAL EQU   8                   MAX # OF VARS IF OLD READ VAL CMD            
RDVLIST  DS    CL(2*(RDMAXVAL+2))  VAR LIST IF OLD READ VAL CMD                 
RDVNAME  DS    XL(L'VPARM)         VARIABLE ENTRY WORK AREA                     
RDVVALUE DS    XL(L'XSLENTRY)      EXPRESSION ENTRY WORK AREA                   
         END                                                                    
         SPACE 2                                                                
*-                                                                              
*-       READ CODE                                                              
*-                                                                              
READ     XPROC RDWA                ESTABLISH AND WORK AREA                      
         LA    R5,RDWA             POINT R6 TO WORK AREA                        
         USING RDWA,R5                                                          
         CLEAR RDWA                CLEAR THE WORK AREA                          
         VCALL SCNEXFR             SCAN TO SEE IF READ FROM EXEC                
         IF    CPFRDACT,'SET RDFACT '   READ ACT                                
         IF    CPFRDEXT,'SET RDFFROM'   READ FROM                               
         IF    CPFRDEXE,'SET RDFEXEC'   READ EXEC                               
*                                                                               
         COMMENT                   SCAN READ OPERANDS                           
         SCAN  READKWS                                                          
         SCANCHK                                                                
*                                                                               
         IF    RDFLINE,BEGIN       IF LINE NUMBER SPECIFIED,,                   
         ACALL READLINE            READ FROM ACTIVE, EXEC                       
         END                                                                    
         IF    ^RDFLINE,BEGIN      IF NO LINE NUMBER GIVEN,,                    
         ACALL READTERM            READ FROM TERMINAL                           
         END                                                                    
*                                                                               
* NOW DO STRING, VALUE, OR COMMAND                                              
         IF    RDFVAL,BEGIN        IF READ VALUE,,                              
         ACALL READVAL             DO IT TO IT                                  
         END                                                                    
         ELSEIF RDFSTR,BEGIN       IF READ STRING,,                             
         ACALL READSTR             DO IT TO IT                                  
         END                                                                    
         COMMENT                   ELSE DO READ (COMMAND)                       
         ELSE  BEGIN                                                            
         ACALL READCMD                                                          
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       READ LINE FROM TERMINAL                                                
*-                                                                              
*-       ON EXIT:                                                               
*-       R0 - INPUT TEXT LENGTH                                                 
*-       R1 - INPUT TEXT ADDRESS                                                
*-                                                                              
READTERM PROC  ,                                                                
         IF    ^RDFLINE,BEGIN                                                   
         IF    RDFDEL,BEGIN                                                     
         ERROR 'DELETE option invalid when line-number is not given.'           
         END                                                                    
         IF    (RDCOL1,GT,0),BEGIN                                              
         ERROR 'COLUMN option invalid when line-number is not given.'           
         END                                                                    
         END                                                                    
*                                                                               
REREADT  LABEL ,,,                 IF ATTN DURING READ, WE REREAD               
         MVC   CPTRMIN,=AL2(MAXSTRLN)   Set maximum read length                 
         SETMSG RDPRMPT,L:RDPRMPTL       SET UP PROMPT                          
         IF    ^RDFPRMPT,BEGIN          IF NO PROMPT, SET DEFAULT               
         SETMSG 'Enter? '                                                       
         IF    CPSFUPL,'SETMSG "Enter> "'  UPLOW STYLE                          
         IF    CPFCASE,BEGIN            CASE OVERRIDE...                        
         IF    CPFUPPER,'SETMSG "Enter? "'                                      
         ELSE  'SETMSG "Enter> "'                                               
         END                                                                    
         END                                                                    
         TREAD (R1),(R0)           TERMINAL READ                                
         BM    CVABORT             LOGGING OFF                                  
*                                                                               
         LR    R2,R0               SAVE INPUT TEXT                              
         CEIL  R2,L'RDBUF                                                       
         ST    R2,RDBUFLEN                                                      
         MOVE  R2,RDBUF,@R1                                                     
         SETMSG RDBUF,L:RDBUFLEN                                                
*                                                                               
         LTR   R15,R15             TEST TREAD RETURN CODE                       
         IF    POS,BEGIN           ENDED WITH PF...                             
         IF    (R15,EQ,1),BEGIN    BREAK...                                     
         IF    ^RDFATTN,CVERROR    NO ATTN LINENO, EXEC BREAK                   
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         MVC   CPEXNEXT,RDLINENO   SET NEW LINENO                               
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,2),BEGIN    ?BREAK...                                    
         IF    (R0,Z),CVABORT      FORCE EXEC BREAK                             
         B     REREADT                                                          
         END                                                                    
*                                                                               
         IF    (R15,EQ,3),BEGIN    @BREAK...                                    
         SET   CPFNUMXP+CPFLISXP+CPFCMDMD                                       
         SET   CPLFLG5.CPFNONUM    NONUMBER                                     
         VCALL LINEMOD             ALLOW MODS OF INPUT TEXT                     
         BNZ   CVABORT                                                          
         END                                                                    
         END                                                                    
*                                                                               
         SET   CPFTYPED            SET COMMAND LOGGED                           
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-        READ LINE FROM ACTIVE OR EXEC FILE                                    
*-                                                                              
*-       ON EXIT:                                                               
*-       R0 - INPUT TEXT LENGTH                                                 
*-       R1 - INPUT TEXT ADDRESS                                                
*-                                                                              
READLINE PROC  ,                                                                
         IF    (RDFLINE,AND,RDFPRMPT),BEGIN                                     
         ERROR 'PROMPT option invalid when line-number is given.'               
         END                                                                    
         IF    (RDFLINE,AND,RDFATTN),BEGIN                                      
         ERROR 'ATTENTION option invalid when line-number is given.'            
         END                                                                    
*                                                                               
         IF    RDFACT,BEGIN        IF READ USING ACTIVE FILE,,                  
         VCALL ACTCHK              REQUIRES ACTIVE FILE                         
         L     R15,RDLINENO                                                     
         LA    R1,RDBUF                                                         
         VCALL GETLINE             GET ACTIVE LINE                              
         END                                                                    
         IF    RDFEXEC,BEGIN       IF READ USING EXEC FILE,,                    
         ACALL EXECHK              REQUIRES EXEC FILE                           
         LA    R1,RDBUF                                                         
         LA    R2,RDLINENO                                                      
         ACALL EXGET               GET EXEC LINE                                
         END                                                                    
         ST    R0,RDBUFLEN                                                      
*                                                                               
         IF    (R15,NZ),BEGIN      IF LINE DOES NOT EXIST                       
         TSEG  'Line '                                                          
         L     R0,RDLINENO         GET LINE NUMBER                              
         VCALL CVEXNO              CONVERT TO EXTERNAL                          
         TSEG  (R1),(R0),B                                                      
         ERROR 'not in file.'                                                   
         END                                                                    
*                                                                               
         IF    (RDCOL1,GT,0),BEGIN IF COLUMN OPTIONS SPECIFIED                  
         L     R2,RDCOL1                                                        
         DECR  R2                                                               
         AR    R1,R2               R1 - TEXT START = LOC+1ST COL-1              
         SR    R0,R2               R0 - REMAINING TEXT LENGTH                   
         COMMENT                                                                
         IF    (RDCOL2,GT,0),BEGIN IF 2ND COL SPECIFIED                         
         L     R2,RDCOL1                                                        
         L     R3,RDCOL2                                                        
         SR    R3,R2                                                            
         LA    R3,1(R3)            R0 = LEN = 2ND COL-1ST COL+1                 
         LR    R0,R3                                                            
         END                                                                    
         IF    (R0,NEG),'CLEAR R0'                                              
         END                                                                    
*                                                                               
         STH   R0,CPCMDLEN         SAVE COUNT FOR WRITECOM                      
         LR    R2,R0               SAVE LINE COUNT                              
         LR    R3,R1               SAVE LINE POINTER                            
*                                                                               
         IF    RDFDEL,BEGIN        IF DELETE SPECIFIED,,                        
         L     R0,RDLINENO         LINE NUMBER TO DELETE                        
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete line                                  
         IF    CPFTYPED,'INCR R15,CPACHCT'                                      
         END                                                                    
*                                                                               
         LR    R0,R2               RESET R0 COUNT                               
         LR    R1,R3               RESET R1 POINTER                             
         IF    CPFCASE*CPFUPPER,BEGIN  UPPER CASE WANTED...                     
         XPUSH R0,R1                                                            
         L     R15,CVUPPTBL                                                     
         LR    R2,R0                                                            
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' TR @R1(0),@R15 '                                            
         END                                                                    
         XPOP  R0,R1               R1,R0 - COMMAND LOC,LEN                      
         COMMENT                           COMMAND IS IN CPBUF                  
         END                                                                    
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*-                                                                              
*-       READ SCAN KEYWORDS AND ROUTINES                                        
*-                                                                              
READKWS  SCKW  NODELETE,NRDELETE,A                                              
         SCKW  DELETE,RDELETE,A                                                 
         SCKW  STRING,RDSCNSTR,A                                                
         SCKW  PROMPT,RDSCNPRM,(A,P)                                            
         SCKW  USING,RDLNENO,(A,V(MPSYMLN))                                     
         SCKW  ,RDLNENO,SYMLN                                                   
         SCKW  COLUMNS,RDSCNCOL,(A,V(MPCOLS))                                   
         SCKW  COLS,RDSCNCOL,(V(MPCOLS))                                        
         SCKW  VALUE,RDSCNVAL,A                                                 
         SCKW  ECHO,RDSCNECH,A                                                  
         SCKW  NOECHO,RDSCNNOE,A                                                
         SCKW  TRANSPARENT,RDSCNTRN,A                                           
         SCKW  NOTRANSPARENT,RDSCNNOT,A                                         
         SCKW  MODEL,RDSCNMOD,A                                                 
         SCKW  NOMODEL,RDSCNNOM,A                                               
         SCKW  FORMATTED,RDSCFMT,A                                              
         SCKW  UNFORMATTED,RDSCUFMT,A                                           
         SCKW  ATTN,RDATTN,A                                                    
         SCKW  ,V(CASEPSH),PUSH                                                 
         SCKW  EXECUTE,V(SCNNOOP),A                                             
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
NRDELETE DPROC ,                                                                
         CLEAR RDFDEL              RESET READ DELETE FLAG                       
         PEND                                                                   
*                                                                               
RDELETE  PROC  ,                                                                
         SET   RDFDEL              SET READ DELETE OPTION                       
         IF    RDFEXEC,BEGIN       CAN NOT DELETE LINE IN EXEC                  
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
RDLNENO  DPROC ,                   ***FIX                                       
         ST    R0,RDLINENO         SAVE READ LINE NUMBER                        
         SET   RDFLINE             SET LINE NUMBER OPTION FLAG                  
         CLEAR R15                 GO SCAN ON                                   
         PEND                                                                   
*                                                                               
RDATTN   PROC  ,                                                                
         SET   RDFATTN             ATTN LINENO GIVEN                            
         VCALL ATTNLINE            GET LINENO                                   
         ST    R0,RDLINENO         SAVE LINENO                                  
         ACALL ALLOWATN            ALLOW ATTENTIONS                             
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
RDSCNCOL PROC  ,                   SET BETTER COLUMN NO.                        
         ST    R0,RDCOL1                                                        
         ST    R1,RDCOL2                                                        
         PEND                                                                   
*                                                                               
* PROCESS PROMPT OPTION            *** FIX, SET PARM FLAG                       
RDSCNPRM PROC  ,                                                                
         VCALL DEQUOTE             STRIP QUOTES                                 
         IF    M,BEGIN             IF NOT QUOTED, ..                            
         VCALL NOTVALID                                                         
         END                                                                    
         SET   RDFPRMPT            SET PROMPT EXPLICIT                          
         C     R0,=A(L'RDPRMPT)    IS PROMPT TOO LONG?                          
         IF    H,BEGIN                                                          
         LA    R0,L'RDPRMPT        SET MAX LENGTH                               
         END                                                                    
         ST    R0,RDPRMPTL         SAVE LENGTH OF PROMPT                        
         LR    R2,R0               PUT LENGTH IN R2 AND TEST                    
         MOVE  R2,RDPRMPT,@R1      MOVE PROMPT TO PROMPT BUFFER                 
         CLEAR R15                 CONTINUE SCAN                                
         PEND                                                                   
*                                                                               
RDSCNECH DPROC ,                                                                
         CLEAR CPFNECHO                                                         
         PEND                                                                   
*                                                                               
RDSCNNOE DPROC ,                                                                
         SET   CPFNECHO                                                         
         PEND                                                                   
*                                                                               
RDSCNTRN DPROC ,                                                                
         SET   CPFTRAN                                                          
         PEND                                                                   
*                                                                               
RDSCNNOT DPROC ,                                                                
         CLEAR CPFTRAN                                                          
         PEND                                                                   
*                                                                               
RDSCNMOD DPROC ,                                                                
         CLEAR CPFNMOD                                                          
         PEND                                                                   
*                                                                               
RDSCNNOM DPROC ,                                                                
         SET   CPFNMOD                                                          
         PEND                                                                   
*                                                                               
RDSCFMT  DPROC ,                                                                
         CLEAR CPFUFMT                                                          
         PEND                                                                   
*                                                                               
RDSCUFMT DPROC                                                                  
         SET   CPFUFMT                                                          
         PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-       SCANNED 'READ STRING'                                                  
*-                                                                              
*-       GET VARIABLE NAME (MAY BE ARRAY) AND CONTNUE SCAN                      
*-                                                                              
RDSCNSTR PROC  ,                                                                
         USING VNENTRY,R2                                                       
         LA    R2,RDVNAME                                                       
         SET   RDFSTR                                                           
         CLEAR VNENTRY                                                          
*                                                                               
         COMMENT                   SCAN VARIABLE NAME                           
         SCINFO ,                                                               
         LA    R2,VNENTRY                                                       
         VCALL SCNVNAME                                                         
         IF    M,BEGIN                                                          
         ERROR 'STRING: missing option.'                                        
         END                                                                    
         IF    NZ,BEGIN                                                         
         TSEG  RDVNAME,L'VNNAME,T                                               
         ERROR ': invalid variable name.'                                       
         END                                                                    
*                                                                               
         COMMENT                   CHECK TO SEE IF NAME DECLARED                
         XPUSH R0,R1                                                            
         LA    R1,RDVNAME                                                       
         LA    R2,RDVNAME                                                       
         VCALL GETVAR                                                           
         IF    NZ,BEGIN                                                         
         VCALL SEGVNAME                                                         
         ERROR ': undefined.'                                                   
         END                                                                    
         XPOP  R0,R1                                                            
*                                                                               
         COMMENT                   IF ARRAY, READ SUBSCRIPTS                    
         IF    VNARRAY,BEGIN                                                    
         LA    R2,RDVNAME                                                       
         VCALL GETSUBS                                                          
         BNZ   CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   CONTINUE SCAN                                
         SCINIT (R1),(R0)                                                       
         SETSCKWS READKWS                                                       
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R2                                                               
         DROP  R5                                                               
         EJECT                                                                  
*-                                                                              
*-                                                                              
*-       SCANNED 'READ VALUE'                                                   
*-                                                                              
*-       GET VARIABLE NAME (MAY BE ARRAY) AND CONTNUE SCAN                      
*-       NOTE: WE ARE PHASING OUT VARIABLE LIST FORM OF COMMAND                 
*-       (EG. N0,N1,...). VARIABLE LIST NOT ALLOWED IN                          
*-       EXTENDED EXEC MODE BECAUSE THERE ARE NO PREDIFINED                     
*-       VARIABLES.                                                             
*-                                                                              
RSVWA    RECORD BEGIN                                                           
RSVAREA  DS    XL(L'NSCNCB)                                                     
         END                                                                    
*                                                                               
RDSCNVAL PROC  RSVWA                                                            
         USING RDWA,R5                                                          
         USING VNENTRY,R2                                                       
         LA    R2,RDVNAME                                                       
         SET   RDFVAL                                                           
         CLEAR VNENTRY                                                          
         CLEAR RDVLIST                                                          
*                                                                               
         COMMENT                   CHECK FOR VARIABLE LIST,,                    
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCPOP ,                   MUST NOT, DOES NOT DIDDLE R15                
         IF    (R0,NP),BEGIN                                                    
         ERROR 'VALUE: missing operand.'                                        
         END                                                                    
         ACALL CHECKNSW            IF NOT N0-N9, RETURNS R15=NZ                 
         IF    ^CPFXOLD,BEGIN      IN NEW EXEC MODE (IE NOT OLD)                
         LA    R15,4               VARIABLE LISTS NOT ALLOWED                   
         END                                                                    
*                                                                               
         COMMENT          GOT VARIABLE LIST (EG.  N0,N1,N2,... )                
         COMMENT                                                                
         COMMENT                   PROCESS VARIABLE LIST                        
         IF    (R15,Z),BEGIN       IF OLD TYPE,, IE MULT VAR NAMES,,,           
         LA    R3,RDMAXVAL+1                                                    
         LA    R4,RDVLIST                                                       
         WHILE (R3,POS),BEGIN      LOOP READING IN VAR NAMES                    
         SCSAVE AREA=RSVAREA                                                    
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,NP),BEGIN                                                    
         SCRSTR AREA=RSVAREA                                                    
         B     RSVCONT                                                          
         END                                                                    
         ACALL CHECKNSW                                                         
         IF    Z,BEGIN             IF VALID VAR NAME,,                          
         SCUPTOK R1                                                             
         MVC   0(2,R4),0(R1)          SAVE IT                                   
         LA    R4,2(R4)                                                         
         DECR  R3                                                               
         END                                                                    
         ELSE  BEGIN ,                                                          
         SCRSTR AREA=RSVAREA          IF NOT VALID VAR NAME,                    
         B     RSVCONT               RESTORE SCAN PTRS, AND GO SCAN             
         END                                                                    
         END                                                                    
         ERROR 'Too many variables in value list.'                              
         END                                                                    
*-                                                                              
*-       GOT NEW READ VALUE, ONLY SINGLE VARIABLE NAME ALLOWED                  
*-                                                                              
         ELSE  BEGIN ,             SCAN VARIABLE NAME                           
         SCINFO ,                                                               
         LA    R2,VNENTRY                                                       
         VCALL SCNVNAME                                                         
         IF    M,BEGIN                                                          
         ERROR 'VALUE: missing option.'                                         
         END                                                                    
         IF    NZ,BEGIN                                                         
         TSEG  RDVNAME,L'VNNAME,T                                               
         ERROR ': invalid variable name.'                                       
         END                                                                    
*                                                                               
         COMMENT                   CHECK TO SEE IF NAME DECLARED                
         XPUSH R0,R1                                                            
         LA    R1,RDVNAME                                                       
         LA    R2,RDVNAME                                                       
         VCALL GETVAR                                                           
         IF    NZ,BEGIN                                                         
         VCALL SEGVNAME                                                         
         ERROR ': undefined.'                                                   
         END                                                                    
         XPOP  R0,R1                                                            
*                                                                               
         COMMENT                   IF ARRAY, READ SUBSCRIPTS                    
         IF    VNARRAY,BEGIN                                                    
         LA    R2,RDVNAME                                                       
         VCALL GETSUBS                                                          
         BNZ   CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   CONTINUE SCAN                                
         SCINIT (R1),(R0)                                                       
         END                                                                    
*                                                                               
RSVCONT  LABEL ,                   CONTINUE SCAN                                
         SETSCKWS READKWS                                                       
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R2                                                               
         DROP  R5                                                               
         TITLE 'READSTR- READ STRING ROUTINE'                                   
*-                                                                              
*-       READ STRING COMMAND EXECUTION.                                         
*-                                                                              
*-       ON ENTRY:                                                              
*-       R1,R0 - LOC, LEN OF STRING TO ASSIGN                                   
*-       RDVNAME - NAME OF VARIABLE TO SAVE !                                   
*-                                                                              
READSTR  PROC  ,                                                                
         USING RDWA,R5                                                          
         USING VNENTRY,R6                                                       
         USING XSENTRY,R4                                                       
         LA    R6,RDVNAME          R6 - VARIABLE ENTRY W/NAME                   
         LA    R4,RDVVALUE         R4 - VARIABLE ENTRY W/VALUE                  
*                                                                               
         CLEAR XSENTRY                                                          
         SET   XSSTRING            SET STRING FLAG                              
         LR    R3,R0                                                            
         STH   R3,XSSTRLEN         SAVE STRING LENGTH                           
         MOVE  R3,XSSTRLOC,@R1     SAVE STRING TEXT                             
         CLEAR R0                                                               
         LA    R1,XSENTRY                                                       
         LA    R2,VNENTRY                                                       
         VCALL SAVEVAR             SAVE VARIABLE                                
         BNZ   CVERROR                                                          
*                                                                               
         B     CVNEXT              GO GET NEXT COMMAND                          
         PEND                                                                   
         DROP  R6                                                               
         DROP  R5                                                               
         DROP  R4                                                               
         TITLE 'READVAL- READ VALUE ROUTINE'                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*       READ VALUE COMMAND EXECUTION.                                           
*                                                                               
*  ON ENTRY:                                                                    
*       R1,R0 - LOC LENGTH OF VALUES READ                                       
*       RDVNAME - NAME OF VARIABLE TO SAVE (NEW TYPE, SINGLE VALUE)             
*       RDVLIST - NAMES OF VARS TO ASSIGN (OLD TYPE FOR N0,S0,W0,..)            
*                                                                               
*                                                                               
*                                                                               
READVAL  PROC  ,                                                                
         USING RDWA,R5                                                          
         USING VNENTRY,R6                                                       
         USING XSENTRY,R4                                                       
         LA    R6,RDVNAME          R6 - VARIABLE ENTRY W/NAME                   
         LA    R4,RDVVALUE         R4 - VARIABLE ENTRY W/VALUE                  
         CLEAR XSENTRY                                                          
*-                                                                              
*-       NEW TYPE READ VALUE, ACCEPTS A SINGLE NEW VARIABLE NAME                
*-                                                                              
         IF    (RDVLIST,EQ,0),BEGIN                                             
         COMMENT                   R1,R0 - EXPRESSION LOC, LEN                  
         LA    R2,XSENTRY          R2 - EXPRESSION WORK AREA                    
         VCALL EVALEXPR            EVALUATE EXPRESSION                          
         BH    CVERROR                                                          
         IF    NEG,BEGIN           IF NULL, RETURN NULL STRING                  
         CLEAR XSENTRY                                                          
         SET   XSSTRING                                                         
         CLEAR XSSTRLEN                                                         
         END                                                                    
         VCALL IGNOREND                                                         
         COMMENT                                                                
         CLEAR R0                                                               
         LA    R1,XSENTRY          R1 - AREA CONTAINING VALUE                   
         LA    R2,VNENTRY          R2 - AREA CONTAINING NAME                    
         VCALL SAVEVAR             SAVE VARIABLE                                
         BNZ   CVERROR                                                          
         END                                                                    
*-                                                                              
*-   OLD TYPE READ VALUE, ACCEPTS A VARIABLE LIST, (N0,N1,S0 ...ETC)            
*-                                                                              
         IF    (RDVLIST,NE,0),BEGIN                                             
         CLEAR VNENTRY                                                          
         MVC   VNNAME,CVBLANKS                                                  
         LA    R3,RDVLIST          R3 - LIST OF VAR NAMES                       
         WHILE (@R3,NE,0),BEGIN    LOOP                                         
         COMMENT                   R1,R0 - EXPRESSION LOC, LEN                  
         LA    R2,XSENTRY          R2 - EXPRESSION WORK, RETURN AREA            
         VCALL EVALEXPR                                                         
         BH    CVERROR                                                          
         IF    NEG,BEGIN           IF NULL RETURN NULL STRING                   
         CLEAR XSENTRY                                                          
         SET   XSSTRING                                                         
         CLEAR XSSTRLEN                                                         
         END                                                                    
         XPUSH R0,R1                                                            
         MVC   VNNAME,CVBLANKS     MERGE VARIABLE NAME AND VALUE                
         MVC   VNNAME(2),0(R3)                                                  
         CLEAR R0                                                               
         LA    R1,XSENTRY                                                       
         LA    R2,VNENTRY          R1,R2 - VARIABLE ENTRY                       
         VCALL SAVEVAR             SAVE VARIABLE                                
         BNZ   CVERROR                                                          
         XPOP  R0,R1               POP EXPRESSION POINTERS                      
         IF    ((@R1,EQ,C','),AND,(R0,POS)),BEGIN                               
         LA    R1,1(R1)            SKIP COMMA BETWEEN EXPRNS, IF ANY            
         DECR  R0                                                               
         END                                                                    
         LA    R3,2(R3)            POINT AT NEXT VARIABLE NAME                  
         END   ,                   LOOP TO GET NEXT VARIABLE                    
         VCALL IGNOREND                                                         
         END                                                                    
*                                                                               
         B     CVNEXT              DONE,,  SAY BYE BYE !                        
         PEND                                                                   
         DROP  R6                                                               
         DROP  R5                                                               
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*  READ COMMAND -- EXECUTE COMMAND                                              
*                                                                               
*  R1,R0 - COMMAND LOC, LEN                                                     
*                                                                               
READCMD  PROC  ,                                                                
         COMMENT                   R1,R0 - COMMAND LOC LENGTH                   
         STH   R0,CPCMDLEN                                                      
         LR    R2,R0               SAVE CMD LOC, LEN IN CPCMD                   
         MOVE  R2,CPCMD,@R1                                                     
*        MVC   CPLCNO,RDLINENO     SET LINE NUMBER FOR WRITECOM                 
*        IF    CPFLOG,BEGIN       IF EXEC LOG MODE                              
*        IF    CPFXTEND,EXIT       Don't do exec log for new execs              
*        ACALL WRITECOM            GO WRITE OUT COMMAND                         
*        END                                                                    
         ACALL EXLOOPCK            GO CHECK FOR LOOP IN EXEC                    
         COMMENT                                                                
         LA    R1,CPCMD            R1,R0 - COMMAND LOC LEN                      
         LH    R0,CPCMDLEN                                                      
         LR    R13,R8              RESET R13 FOR NEW COMMAND                    
         VCALL EDTCOM              GO EXECUTE COMMAND                           
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'WRITE Command'                                                  
*box                                                                            
*                                                                               
*                                                                               
*  WRITE -- WRITE Command.                                                      
*                                                                               
*                                                                               
*                                                                               
WRITWA   RECORD BEGIN                                                           
WRITALN  DS    F                   Attn line-number (or -1 if none)             
WRITXVAL DS    XL(L'XSLENTRY)      EXPRESSION WORK, RETURN AREA                 
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  WRITE -- WRITE Command.                                                      
*                                                                               
WRITE    XPROC WRITWA                                                           
         LA    R6,WRITWA                                                        
         USING WRITWA,R6                                                        
         LA    R2,WRITXVAL                                                      
         USING XSENTRY,R2                                                       
*                                                                               
         MVC   WRITALN,=F'-1'      NO ATTN LINENO                               
*                                                                               
         COMMENT                   SCAN EXPRESSION                              
         SCINFO ,                  R1,R0 - EXPRESSION LOC, LENGTH               
         COMMENT                   R2 - EXPRESSION VALUE RET AREA               
         VCALL EVALEXPR            EVALUATE EXPRESSION                          
         BH    CVERROR                                                          
         IF    NEG,BEGIN                                                        
         ERROR 'Missing expression.'                                            
         END                                                                    
*                                                                               
         COMMENT                   SCAN WRITE OPTIONS                           
         SCINIT (R1),(R0)                                                       
         SCAN WRPRT                                                             
         SCANCHK                                                                
*                                                                               
         COMMENT                   WRITE EXPRESSION VALUE                       
         VCALL MAKESTR             CONVERT TO A STRING                          
         IF    NZ,CVERROR                                                       
         LA    R1,XSSTRLOC                                                      
         LH    R0,XSSTRLEN         R1,R0 - STRING LOC, LEN                      
         TSEG  (R1),(R0)                                                        
         TMARK  ,                                                               
         IF    ^CPFNONL,'TCR'                                                   
         TWRITE                    WRITE STRING                                 
         COMMENT                                                                
         IF    NZ,BEGIN            IF ATTENTION HIT,,                           
         CLEAR CPFNONL                                                          
         IF    (WRITALN,EQ,-1),CVABORT                                          
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         MVC   CPEXNEXT,WRITALN    Set new lineno                               
         END                                                                    
         B     CVNEXT                                                           
         PEND                                                                   
         DROP  R2                                                               
         EJECT                                                                  
WRPRT    SCKW  ATTN,WRATTN,A                                                    
         SCKW  NONL,WRNONL,A                                                    
         SCKW  TRANSPARENT,WRTRAN,A                                             
         SCKW  NOTRANSPARENT,WRNTRAN,A                                          
         SCKW  MODEL,WRMOD,A                                                    
         SCKW  NOMODEL,WRNMOD,A                                                 
         SCKW  FORMATTED,WRFMT,A                                                
         SCKW  UNFORMATTED,WRUFMT,A                                             
         SCKW  CLEAN,WRCLEAN,A                                                  
         SCKW  FORCE,WRFORCE,A                                                  
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
WRATTN   PROC  ,                                                                
         VCALL ATTNLINE                                                         
         ST    R0,WRITALN          Save attn lineno                             
         ACALL ALLOWATN            Allow attn                                   
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
WRNONL   DPROC ,                                                                
         SET   CPFNONL                                                          
         PEND                                                                   
*                                                                               
WRTRAN   DPROC ,                                                                
         SET   CPFTRAN,CPFNONL                                                  
         PEND                                                                   
*                                                                               
WRNTRAN  DPROC ,                                                                
         CLEAR CPFTRAN                                                          
         PEND                                                                   
*                                                                               
WRMOD    DPROC ,                                                                
         CLEAR CPFNMOD                                                          
         PEND                                                                   
*                                                                               
WRNMOD   DPROC ,                                                                
         SET   CPFNMOD                                                          
         PEND                                                                   
*                                                                               
WRFMT    DPROC ,                                                                
         CLEAR CPFUFMT                                                          
         PEND                                                                   
*                                                                               
WRUFMT   DPROC ,                                                                
         SET   CPFUFMT                                                          
         PEND                                                                   
*                                                                               
WRCLEAN  DPROC                                                                  
         SET   CPFCLEAN                                                         
         PEND                                                                   
*                                                                               
WRFORCE  DPROC ,                                                                
         SET   CPFFORCE                                                         
         PEND                                                                   
         DROP  R6                                                               
         TITLE 'COMMENT Command'                                                
*box                                                                            
*                                                                               
*                                                                               
*  COMMENT -- COMMENT Command.                                                  
*                                                                               
*                                                                               
*                                                                               
COMMENT  XPROC ,                                                                
         SCINFO                                                                 
         IF    ((R0,POS),AND,(@R1,EQ,' ')),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         TSEG  (R1),(R0),CR                                                     
         B     CVNEXT                                                           
         PEND                                                                   
         TITLE 'WAIT Command'                                                   
WAITWA   RECORD BEGIN                                                           
WAITFLAG FLAG                                                                   
         FLAG  WAITFINT            - Interruptible                              
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  WAIT -- WAIT Command.                                                        
*                                                                               
WAIT     XPROC WAITWA                                                           
         LA    R6,WAITWA                                                        
         USING WAITWA,R6                                                        
         CLEAR WAITWA                                                           
*                                                                               
         CLEAR R3                  No wait time                                 
         L     R4,=F'-1'           No ATTN line number                          
         SCAN  WAITPRT             Scan for options                             
         SCANCHK                                                                
         IF    (R3,Z),BEGIN                                                     
         ERROR 'Missing number of seconds to wait.'                             
         END                                                                    
*                                                                               
         CLEAR R2                                                               
         D     R2,=A(10)           Convert .001 to .01                          
         IF    (R2,GE,5),'LA R3,@R3+1'                                          
*                                                                               
         IF    (R3,GT,=A(60*60*24*100)),BEGIN  Be reasonable...                 
         TSEG  'I can''t wait that long.  '                                     
         ERROR 'That''s over a day!'                                            
         END                                                                    
         LR    R15,R3              Load the wait time                           
         IF    WAITFINT,'LNR R15,R15'  Wait is interruptible                    
         VCALL WAITER              Wait a bit                                   
*                                                                               
         IF    ((R15,NZ),AND,(R4,NE,-1)),BEGIN                                  
         L     R15,CVCURJCB        Reset ATTN status                            
         WITH  (JCB,R15),'CLEAR JCBFATTN'                                       
         ST    R4,CPEXNEXT         Set new EXEC line number                     
         END                                                                    
         B     CVNEXT                                                           
         PEND                                                                   
         SPACE 2                                                                
***                                                                             
***  Wait options.                                                              
***                                                                             
WAITPRT  SCKW  ,WAITSEC,LN                                                      
         SCKW  ATTN,WAITATTN,A                                                  
         SCKW  ATTENTION,WAITATTN,A                                             
         SCKW  INTERRUPT,WAITINT,A                                              
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 1                                                                
WAITSEC  PROC  ,                                                                
         LR    R3,R0               Copy wait value                              
         PRETURN (R3)                                                           
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
WAITINT  DPROC ,                                                                
         SET   WAITFINT            Interruptable                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
WAITATTN PROC  ,                                                                
         VCALL ATTNLINE                                                         
         LR    R4,R0               R4 attn line number                          
         ACALL ALLOWATN            Allow attn                                   
         PRETURN (R4)                                                           
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'ON ERROR/ON ATTENTION routines'                                 
*box                                                                            
*                                                                               
*                                                                               
*  ON ERROR --                                                                  
*  ON ATTENTION --                                                              
*  ON HOST --                                                                   
*                                                                               
*  NOTE:                                                                        
*    THIS CMD PROCESSES ON ERROR OR ON ATTN,  THE 'ON HOST'                     
*    COMMAND WAS ADDED LATER, IT IS PROCESSED ELSEWHERE.                        
*    SEE ONHOST ROUTINE BELOW. 'ON HOST' IS A NETWORK COMMAND.                  
*                                                                               
*  NOTE: EACH EXEC (XPROC) CAN SET UP ITS OWN ON ERROR/ATTN                     
*    EXITS AND PROCESSIONG.  ON ERROR/ATTN INFO IS PUSHED AND                   
*    POPPED FOR EACH EXEC ON THE VARIABLE STACK.  SEE DETAILS                   
*    BELOW.                                                                     
*                                                                               
*                                                                               
*                                                                               
*-                                                                              
ON       XPROC                                                                  
*                                                                               
         COMMENT                   SCAN ON CONDITION                            
         SCAN  ONCPRT                                                           
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         SPACE 4                                                                
*-                                                                              
*-  IF NOT 'ON ATTN' OR 'ON ERROR' ,,, WE HAVE 'ON HOST ' ...                   
*-                                                                              
*-  WE DO IT TO IT !                                                            
*-                                                                              
ONHOST   PROC ,                                                                 
         SCBKUP                                                                 
         SETMSG 'PUB.SYSCMD_ON'    EXEC COMMAND STORED HERE                     
         VCALL GBLCMD                                                           
         COMMENT                   DOES NOT RETURN                              
         PEND ,                                                                 
         SPACE 4                                                                
*-                                                                              
*-       NULL ON ATTN SCAN                                                      
*-                                                                              
ONCNULL  PROC  ,                                                                
         ERROR 'Missing option after ON.',,SYNTAX                               
         PEND                                                                   
         SPACE 4                                                                
*-                                                                              
*-       SCAN TABLE FOR ON CONDITION                                            
*-                                                                              
ONCPRT   SCKW  ERROR,ONERROR       ON COMMAND OPTIONS                           
         SCKW  ERR,ONERROR                                                      
         SCKW  ATTENTION,ONATTN                                                 
         SCKW  ATTN,ONATTN                                                      
         SCKW  ,ONCNULL,NULL                                                    
         SCKW  ,ONHOST             IF UNRECOGNIZED, WE HAVE 'ON HOST'           
         SPACE 1                                                                
ONEPRT   SCKW  COMMAND,ONECMD,(P)  ON ERROR OPTIONS                             
         SCKW  CMD,ONECMD,(P)                                                   
         SCKW  CLR,ONECLEAR                                                     
         SCKW  CLEAR,ONECLEAR,A                                                 
         SCKW  REENABLE,ONEREEN,A                                               
         SCKW  RESET,ONEREEN,A                                                  
         SCKW  ,ONENULL,NULL                                                    
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 1                                                                
ONAPRT   SCKW  COMMAND,ONACMD,(P)  ON ATTN OPTIONS                              
         SCKW  CMD,ONACMD,(P)                                                   
         SCKW  CLR,ONACLEAR                                                     
         SCKW  CLEAR,ONACLEAR,A                                                 
         SCKW  REENABLE,ONAREEN,A                                               
         SCKW  RESET,ONAREEN,A                                                  
         SCKW  INTERRUPTIBLE,ONAINT,A                                           
         SCKW  NOINTERRUPTIBLE,ONANOINT,A                                       
         SCKW  ,ONANULL,NULL                                                    
         SCKW  ,V(SCNINVAL)                                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ON ERROR  ...                                                                
*                                                                               
*                                                                               
*                                                                               
ONERROR  PROC  ,                                                                
*                                                                               
         IF    ^CPFEXEC,BEGIN      In command mode...                           
         TSEG  'ON <condition>: command ignored.',,CR                           
         TSEG  'Flow control statements may be used only in '                   
         ERROR 'EXEC files.'                                                    
         END                                                                    
*                                                                               
         COMMENT                   SCAN ON ERROR OPTIONS                        
         SCAN  ONEPRT                                                           
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         SPACE 1                                                                
*-                                                                              
*-        ON ERROR COMMAND=' ...'                                               
*-                                                                              
ONECMD   PROC  ,                   ON ERROR COMMAND SPECIFIED                   
         COMMENT                   GET QUOTED COMMAND                           
         VCALL DEQUOTE                                                          
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid.  Quotes required.',,SYNTAX                           
         END                                                                    
         COMMENT                   SAVE COMMAND                                 
         COMMENT                   R0 - COMMAND LENGTH                          
         COMMENT                   @R1 - COMMAND TEXT                           
         LA    R2,=CL16'SYS.ONERROR     '                                       
         VCALL SAVEDATA                                                         
         COMMENT                                                                
         IF    ^CPFDDERR,BEGIN     IF ON ERROR NOT SUSPENDED,                   
         SET   CPFONERR            TURN ON ON ERROR CONDITION SET               
         END                                                                    
         CLEAR R15                 KEEP SCANNING                                
         PEND                                                                   
*-                                                                              
*-        ON ERROR CLEAR                                                        
*-                                                                              
ONECLEAR PROC  ,                   IF CLEAR, TURN OFF ON ERROR                  
         CLEAR CPFONERR                                                         
         CLEAR CPFDDERR                                                         
         CLEAR R15                 KEEP SCANNING                                
         PEND                                                                   
*-                                                                              
*-       ON ERROR REENABLE                                                      
*-                                                                              
ONEREEN  PROC  ,                   IF RESET, RE-ENABLE ON ERROR CMD             
         IF    (CPFONERR,OR,CPFDDERR),BEGIN    IF ON ERROR,                     
         SET   CPFONERR            SET ON ERROR                                 
         CLEAR CPFDDERR            CLEAR ON ERROR SUSPENDED                     
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'ON ERROR condition not disabled: unable to reenable.'           
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       NULL ON ERROR SCAN                                                     
*-                                                                              
ONENULL  PROC  ,                                                                
         ERROR 'Missing ON ERROR options.',,SYNTAX                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ON ATTENTION ...                                                             
*                                                                               
*                                                                               
*                                                                               
ONATTN   XPROC ,                                                                
*                                                                               
         IF    ^CPFEXEC,BEGIN      In command mode...                           
         TSEG  'ON <condition>: command ignored.',,CR                           
         TSEG  'Flow control statements may be used only in '                   
         ERROR 'EXEC files.'                                                    
         END                                                                    
*                                                                               
         COMMENT                   SCAN ON ATTN OPTIONS                         
         SCAN ONAPRT                                                            
         SCANCHK                                                                
         B     CVNEXT                                                           
         PEND                                                                   
         SPACE 1                                                                
*-                                                                              
*-        ON ATTENTION COMMAND=' ...'                                           
*-                                                                              
ONACMD   PROC   ,                  ON ATTN COMMAND SPECIFIED                    
         COMMENT                   GET QUOTED COMMAND                           
         VCALL DEQUOTE                                                          
         IF    NEG,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid.  Quotes required.',,SYNTAX                           
         END                                                                    
         COMMENT                   SAVE COMMAND                                 
         COMMENT                   R0 - COMMAND LENGTH                          
         COMMENT                   @R1 - COMMAND TEXT                           
         LA    R2,=CL16'SYS.ONATTN      '                                       
         VCALL SAVEDATA                                                         
         IF    ^CPFDDATN,BEGIN     IF ON ATTN NOT SUSPENDED,                    
         SET   CPFONATN            TURN ON ON ATTN CONDITION                    
         CLEAR CPATTNCT            CLEAR ATTN COUNT                             
         END                                                                    
         CLEAR R15                 KEEP SCANNING                                
         PEND                                                                   
*-                                                                              
*-        ON ATTN CLEAR                                                         
*-                                                                              
ONACLEAR PROC  ,                   IF BREAK, TURN OFF ON ATTN                   
         CLEAR CPFONATN            CLEAR ALL ON ATTN FLAGS                      
         CLEAR CPFDDATN                                                         
         CLEAR CPFINATN                                                         
         CLEAR CPATTNCT                                                         
         CLEAR R15                 KEEP SCANNING                                
         PEND                                                                   
         SPACE 1                                                                
*-                                                                              
*-       ON ATTN REENABLE                                                       
*-                                                                              
ONAREEN  PROC  ,                   IF RESET, RE-ENABLE ON ATTN CMD              
         IF    (CPFONATN,OR,CPFDDATN),BEGIN    IF ON ATTN,                      
         SET   CPFONATN            SET ON ATTN                                  
         CLEAR CPFDDATN            CLEAR ON ATTN SUSPENDED                      
         CLEAR CPATTNCT            CLEAR ATTN COUNT                             
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'ON ATTN condition not disabled; unable to reenable.'            
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       ON ATTN CMD=' ...' INTERRUPTIBLE/NOINTERRPTIBLE                        
*-                                                                              
ONAINT   DPROC ,                   INTERRUPTIBLE                                
         SET   CPFINATN                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
ONANOINT DPROC ,                   NOINTERRUPTIBLE                              
         CLEAR CPFINATN                                                         
         CLEAR R15                                                              
         PEND                                                                   
*-                                                                              
*-       NULL ON ATTN SCAN                                                      
*-                                                                              
ONANULL  PROC  ,                                                                
         ERROR 'Missing ON ATTENTION options.',,SYNTAX                          
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
* INITERR - INITIALIZE/CLEAR ON ERROR, ERROR VARIABLES                          
*                                                                               
*                                                                               
*                                                                               
INITERR  XPROC                                                                  
         CLEAR CPFONERR            CLEAR ON ERROR                               
         CLEAR CPFDDERR                                                         
         ACALL CLRERROR            CLEAR ERROR VARIABLES                        
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
* INITATTN - INITIALIZE/CLEAR ON ATTN, ATTN VARIABLES                           
*                                                                               
*                                                                               
*                                                                               
INITATTN XPROC                                                                  
         CLEAR CPFONATN            CLEAR ON ATTN                                
         CLEAR CPFDDATN                                                         
         CLEAR CPFINATN                                                         
         CLEAR CPATTNCT            CLEAR ATTN COUNT                             
         ACALL CLRATTN             CLEAR ATTN VARIABLES                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLRERROR - CLEAR ERROR VARIABLES                                             
*                                                                               
*  ON EXIT:                                                                     
*       SYS.ERROR, SYS.ERRID, SYS.ERRMSG SYS.ERRINFO CLEARED                    
*                                                                               
*  CAUTION:                                                                     
*       SYS.ONERROR VALID ONLY IF CPFONERR OR CPFDDERR SET                      
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*                                                                               
*                                                                               
CLRERROR XPROC                                                                  
*                                                                               
         COMMENT                   CLEAR ERROR VARIABLES                        
         CLEAR CPFXXERR                                                         
         COMMENT                   WHEN CPFXXERR IS CLEAR,                      
         COMMENT                   SYS.ERRID AND SYS.ERRMSG ARE                 
         COMMENT                   INTERPRETED AS CLEARED TOO                   
         COMMENT                                                                
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CLRATTN - CLEAR ATTN VARIABLES                                               
*                                                                               
*  ON EXIT:                                                                     
*       SYS.ATTN, SYS.ATTNID, SYS.ATTNINFO CLEARED                              
*                                                                               
*                                                                               
*  NOTE: IF THE CURRENT ON ATTN CONDITION IS SUSPENDED,                         
*     CLEAR ATTN RESETS IT !                                                    
*                                                                               
*  CAUTION:                                                                     
*       SYS.ATTNID  VALID ONLY IF CPFXXATN SET                                  
*       SYS.ATTNINFO  VALID ONLY IF CPFXXATN SET                                
*                                                                               
*                                                                               
*                                                                               
CLRATTN  XPROC                                                                  
*                                                                               
         COMMENT                   CLEAR ATTN VARIABLES                         
         CLEAR CPFXXATN                                                         
         COMMENT                   WHEN CPFXXATN IS CLEAR,                      
         COMMENT                   SYS.ATTNID AND SYS.ATTNINFO ARE              
         COMMENT                   INTERPRETED AS CLEARED TOO                   
         COMMENT                                                                
*                                                                               
         COMMENT                   CLEAR ATTN COUNT                             
         CLEAR CPATTNCT                                                         
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETEMSG - SET ERROR MESSAGE                                                  
*                                                                               
*  ON EXIT:                                                                     
*       SYS.EMSG IS SET  (EMSG IS TEMP SYSTEM ERROR MSG)                        
*box                                                                            
*        R15 - CC=ZERO                                                          
*                                                                               
*                                                                               
*  WARNING!!: CPFERROR OR CPFABORT FLAG MUST BE SET WHEN THIS                   
*       ROUTINE IS CALLED !!                                                    
*                                                                               
*                                                                               
SETEMSG  XPROC                                                                  
*                                                                               
         COMMENT                   ENTRACE EXAMS                                
         IF    (^CPFERROR,AND,^CPFABORT),BEGIN                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE ERRMSG                                  
         COMMENT                   CHECK IF ONE LINE MSG,,,                     
         L     R2,CPSEGLOC                                                      
         L     R3,CPSEGLENF        R2,R3 - START,END OF MSG                     
         IF    (R3,POS),BEGIN                                                   
         AR    R3,R2                                                            
         DECR  R3                                                               
         IF    (@R3,NE,X'15'),BEGIN   IF TRAILING NL, STRIP IT                  
         INCR  R3                                                               
         END                                                                    
         WHILE ((@R2,NE,X'15'),AND,(R2,LT,R3)),BEGIN   FIND 1ST NL              
         LA    R2,1(R2)                                                         
         END                                                                    
         IF    (R2,EQ,R3),BEGIN    IF ONE LINE ERROR MSG                        
         L     R1,CPSEGLOC         R2 - LENGTH                                  
         SR    R2,R1                                                            
         END                                                                    
         ELSE  BEGIN               IF MULTILPLE LINE ERR MSG                    
         CLEAR R2                  R2 - ZERO                                    
         END                                                                    
         IF    (R2,GT,&LINESZ),' CLEAR R2 '                                     
         END                                                                    
*                                                                               
         ELSE  'CLEAR R2'                                                       
*                                                                               
         COMMENT                   SAVE ERROR MSG                               
         IF    (R2,POS),BEGIN      IF SINGLE LINE ERROR MSG                     
         LR    R0,R2                                                            
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.EMSG        '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         COMMENT                   SAVE ERROR MSG                               
         ELSE  BEGIN               IF MULTIPLE LINE ERROR MSG, WE               
         TPUSH ,                   CREATE SINGLE LINE ERR MSG                   
         TSEG  'Command failed: "'                                              
         LA    R1,CPCMD                                                         
         LH    R0,CPCMDLEN                                                      
         CEIL  R0,&LINESZ-20                                                    
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  '"'                                                              
         L     R0,CPSEGLENF                                                     
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.EMSG        '                                       
         VCALL SAVEDATA                                                         
         TPOP  JUNK                                                             
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETERROR - SET ERROR VARIABLES                                               
*                                                                               
*  ON EXIT:                                                                     
*       SYS.ERROR, SYS.ERRID, SYS.ERRMSG SYS.ERRINFO ARE SET                    
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
*                                                                               
*  CAUTION:                                                                     
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*                                                                               
*                                                                               
*  WARNING!!: CPFERROR OR CPFABORT FLAG MUST BE SET WHEN THIS                   
*       ROUTINE IS CALLED !!                                                    
*                                                                               
*                                                                               
SETERROR XPROC                                                                  
*                                                                               
         COMMENT                   ENTRACE EXAMS                                
         IF    (^CPFERROR,AND,^CPFABORT),BEGIN                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SET ERROR OCCURRED FLAG                      
         SET   CPFXXERR                                                         
*                                                                               
         COMMENT                   SAVE ERRID                                   
         IF    (CPERRID,EQ,0),BEGIN                                             
         MVC   CPERRID,=CL8'ERROR   '                                           
         END                                                                    
         LA    R0,L'CPERRID                                                     
         LA    R1,CPERRID                                                       
         LA    R2,=CL16'SYS.ERRID       '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         COMMENT                   GET, SAVE ERROR MSG                          
         XPUSH ,,&LINESZ,PTR=R1                                                 
         LA    R0,&LINESZ                                                       
         LA    R2,=CL16'SYS.EMSG        '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R2,=CL16'SYS.ERRMSG      '                                       
         VCALL SAVEDATA                                                         
         XPOP  ,,&LINESZ                                                        
*                                                                               
         COMMENT                   GET, SAVE ERROR INFO                         
         IF    CPFEXEC,BEGIN                                                    
         TPUSH ,                                                                
         TSEG  'EXEC error at line '                                            
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         L     R0,CPSEGLENF                                                     
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.ERRINFO     '                                       
         VCALL SAVEDATA                                                         
         TPOP  JUNK                                                             
         END                                                                    
         ELSE  BEGIN               IF NOT IN EXEC MODE, NO ERRINFO              
         CLEAR R0                                                               
         LA    R1,0                                                             
         LA    R2,=CL16'SYS.ERRINFO     '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET SAVE ERROR VARS SET NUMBER               
         IF    CPFEXEC,BEGIN       SET ONLY IF IN EXEC MODE                     
         LA    R0,4                                                             
         LA    R1,CPVSET#                                                       
         LA    R2,=CL16'SYS.ERRSETNO    '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
         ELSE  BEGIN               IF NOT IN EXEC MODE,                         
         LA    R0,4                CLEAR ERROR SET #                            
         LA    R1,=F'0'                                                         
         LA    R2,=CL16'SYS.ERRSETNO    '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETATTN - SET ATTN VARIABLES                                                 
*                                                                               
*  ON EXIT:                                                                     
*       SYS.ATTN, SYS.ATTNID, SYS.ATTNINFO SET                                  
*                                                                               
*                                                                               
*  CAUTION:                                                                     
*       SYS.ONATTN VALID ONLY IF CPFONATN OR CPFDDATN SET                       
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
*  WARNING!!: JCBFATTN FLAG MUST BE SET WHEN THIS ROUTINE IS                    
*       CALLED !!                                                               
*                                                                               
*                                                                               
SETATTN  XPROC                                                                  
         USING JCB,R6                                                           
         L     R6,CVCURJCB                                                      
*                                                                               
         COMMENT                   ENTRACE EXAMS                                
         IF    ^JCBFATTN,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SET ATTN OCCURRED FLAG                       
         SET   CPFXXATN                                                         
*                                                                               
         COMMENT                   SAVE ATTNID,,                                
         IF    CPFIDSET,BEGIN      IF ATTNID XRETURNED, SAVE IT                 
         XPUSH ,,ATTNIDLN,PTR=R4                                                
         LA    R0,ATTNIDLN                                                      
         LR    R1,R4                                                            
         LA    R2,=CL16'SYS.XRET_ATTNID '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R0,ATTNIDLN                                                      
         LR    R1,R4                                                            
         LA    R2,=CL16'SYS.ATTNID      '                                       
         VCALL SAVEDATA                                                         
         XPOP  ,,ATTNIDLN                                                       
         END                                                                    
*                                                                               
         COMMENT                   IF NO ATTN ID SET,                           
         IF    ^CPFIDSET,BEGIN     SET DEFAULT ATTNID = 'ATTN'                  
         COMMENT                                                                
         LA    R0,ATTNIDLN                                                      
         LA    R1,=CL8'ATTN    '                                                
         LA    R2,=CL16'SYS.ATTNID      '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         COMMENT                   SAVE ATTNINFO,,                              
         IF    CPFEXEC,BEGIN       SAVE ONLY IF IN EXEC MODE                    
         TPUSH ,                                                                
         TSEG  'EXEC break at line '                                            
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         L     R0,CPSEGLENF                                                     
         L     R1,CPSEGLOC                                                      
         LA    R2,=CL16'SYS.ATTNINFO    '                                       
         VCALL SAVEDATA                                                         
         TPOP  JUNK                                                             
         END                                                                    
*                                                                               
         COMMENT                   IF NOT IN EXEC, CLEAR ATTNINFO               
         IF    ^CPFEXEC,BEGIN                                                   
         CLEAR R0                                                               
         LA    R1,0                                                             
         LA    R2,=CL16'SYS.ATTNINFO    '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ABORT EXEC FILE                                                              
*                                                                               
*  WE HANDLE EXEC ERRORS IN THE FOLLOWING ORDER ,,,,                            
*    ON ATTENTION --                                                            
*    ON ERROR --                                                                
*    EXEC COMMAND RETRY --                                                      
*    EXEC BREAK ---                                                             
*                                                                               
*  NOTE: WE MUST SET THE USER ATTENTION VARIABLES BELOW,                        
*    AS WE MAY HAVE A VERY LATE POSTING OF THE ATTENTION                        
*    FLAG.  FOR SYMETRY REASONS WE ALSO SET USERS ERROR                         
*    VARIABLES TOO.  (THEY BOTH MAY ALREADY BE SET, BUT                         
*    IT DOES NOT HURT TO SET THEM TWICE.)                                       
*                                                                               
*                                                                               
EXECABRT XPROC                                                                  
         L     R6,CVCURJCB                                                      
         USING JCB,R6                                                           
*                                                                               
         COMMENT                   ENTRANCE EXAMS !                             
         IF    ^CPFEXEC,BEGIN      IF NOT EXEC MODE, BOMB                       
         BOMB  ,                                                                
         END                                                                    
         IF    (^JCBFATTN,AND,^CPFERROR,AND,^CPFABORT),BEGIN                    
         BOMB  ,                   IF NOT ERROR OR ATTN, BOMB                   
         END                                                                    
*                                                                               
         COMMENT                   MAKE DOUBLY SURE USERS                       
         COMMENT                   ERROR/ATTN VARIABLES SET !                   
         IF    JCBFATTN,BEGIN                                                   
         ACALL SETATTN                                                          
         CLEAR CPFERROR            ATTN OVERRIDES ERROR                         
         CLEAR CPFABORT                                                         
         END                                                                    
         IF    (CPFERROR,OR,CPFABORT),BEGIN                                     
         ACALL SETERROR                                                         
         END                                                                    
*-                                                                              
*-       Check for ON ATTENTION condition,,,                                    
*-                                                                              
         COMMENT                   IF ATTENTION, AND ON ATTN SET                
         IF    (JCBFATTN,AND,CPFONATN),BEGIN                                    
* Memorial to set exec log !    If exec log set and we are going                
* to take the users 'on attn exit', give the user a chance to                   
* turn off exec logging.  *DO NOT allow the user to break if an                 
* attention exit is to be taken, otherwise it is impossible to                  
* write a non-breakable exec. however user may comment out 'on attn'            
         COMMENT                   BEGIN 'ON ATTN' 'EXEC LOG' YUCK              
         IF    (CPFLOG,OR,CPFXLOG),BEGIN                                        
         SETMSG 'Continue command logging'                                      
         VCALL YESRTN                                                           
         IF    NZ,BEGIN                                                         
         CLEAR CPFLOG                                                           
         CLEAR CPFXLOG                                                          
         END                                                                    
         END   ,                   END 'ON ATTN' 'EXEC LOG' YUCK                
*                                                                               
*  Got ON ATTNENTION condition,, process it                                     
         CLEAR JCBFATTN            CLEAR ATTENTION, ERR FLAGS                   
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         LA    R0,L'CPCMD          GET ON ATTENTION COMMAND                     
         LA    R1,CPCMD                                                         
         LA    R2,=CL16'SYS.ONATTN      '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         STH   R0,CPCMDLEN                                                      
         CLEAR CPFONATN            SUSPEND ON ATTN                              
         SET   CPFDDATN                                                         
         LH    R0,CPCMDLEN         GO EXECUTE 'ON ATTN' COMMAND                 
         LA    R1,CPCMD                                                         
         VCALL EDTCOM              DOES NOT RETURN !  --->>>                    
         END                                                                    
*-                                                                              
*-       Check for ON ERROR condition ,,,                                       
*-                                                                              
         COMMENT                   IF ERROR, AND ON ERROR,,,,                   
         IF    (CPFERROR,OR,CPFABORT),BEGIN                                     
         IF    CPFONERR,BEGIN                                                   
         CLEAR JCBFATTN            CLEAR ERROR FLAGS                            
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         LA    R0,L'CPCMD          GET ON ERROR COMMAND                         
         LA    R1,CPCMD                                                         
         LA    R2,=CL16'SYS.ONERROR     '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         STH   R0,CPCMDLEN                                                      
         CLEAR CPFONERR            SUSPEND ON ERROR ACTIVE                      
         SET   CPFDDERR                                                         
         LH    R0,CPCMDLEN         GO EXECUTE 'ON ERROR' COMMAND                
         LA    R1,CPCMD                                                         
         VCALL EDTCOM              DOES NOT RETURN !  --->>>                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       If none of the above,, EXEC BREAK                                      
*-                                                                              
         COMMENT                   IF ATTN, EXEC BREAK AT LINE ....             
         IF    JCBFATTN,BEGIN                                                   
         CLEAR JCBFATTN            CLEAR ERROR FLAGS                            
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         TSEG  'EXEC break at line ' GIVE BREAK MSG                             
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         TCR                                                                    
         ACALL SETPAUSE                                                         
         B     CVNEXT              GO GET NEXT ERROR FROM TERMINAL              
         END                                                                    
*                                                                               
         COMMENT                   IF ERROR, EXEC BREAK AT LINE ....            
         CLEAR JCBFATTN            CLEAR ERROR FLAGS                            
         CLEAR CPFERROR                                                         
         CLEAR CPFABORT                                                         
         TSEG  'EXEC error at line ' GIVE ERROR MSG                             
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO                                                          
         TCR                                                                    
         ACALL SETPAUSE                                                         
         B     CVNEXT              GO GET NEXT CMD FROM TERMINAL                
         PEND                                                                   
         DROP  JCB                                                              
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PUSHON - PUSH ON CONDITION INFO                                              
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VAR STACK SET NUMBER FOR INFO TO PUSH                              
*                                                                               
*  ON EXIT:                                                                     
*      ON CONDITION INFO PUSHED                                                 
*      R15 - CC=ZERO                                                            
*                                                                               
*  CAUTION:                                                                     
*       SYS.ONERROR VALID ONLY IF CPFONERR OR CPFDDERR SET                      
*       SYS.ONATTN VALID ONLY IF CPFONATN OR CPFDDATN SET                       
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
PHONWA   RECORD BEGIN                                                           
PHON#SET DS    F                  STACK SET # FOR POP                           
PHONNTRY DS    XL(L'ONENTRY)      ERROR INFO DSECT                              
         END                                                                    
*                                                                               
*                                                                               
PUSHON   XPROC PHONWA                                                           
         USING ONENTRY,R6                                                       
         LA    R6,PHONNTRY                                                      
         ST    R0,PHON#SET                                                      
*                                                                               
         COMMENT                   IF ON CONDITIONS EXIST,,,                    
         IF    (CPFONERR,OR,CPFDDERR,OR,CPFONATN,OR,CPFDDATN),BEGIN             
*                                                                               
         COMMENT                   GET ON CONDITION INFORMATION                 
         CLEAR ONENTRY                                                          
         IF    CPFONERR,' SET NNFONERR '                                        
         IF    CPFDDERR,' SET NNFDDERR '                                        
         IF    CPFONATN,' SET NNFONATN '                                        
         IF    CPFDDATN,' SET NNFDDATN '                                        
*                                                                               
         COMMENT                   GET ON ERROR CMD                             
         LA    R0,L'NNERRCMD                                                    
         LA    R1,NNERRCMD                                                      
         LA    R2,=CL16'SYS.ONERROR     '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         CLEAR R0                                                               
         END                                                                    
         ST    R0,NNERRLEN                                                      
*                                                                               
         COMMENT                   GET ON ATTN CMD                              
         LA    R0,L'NNATNCMD                                                    
         LA    R1,NNATNCMD                                                      
         LA    R2,=CL16'SYS.ONATTN      '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         CLEAR R0                                                               
         END                                                                    
         ST    R0,NNATNLEN                                                      
*                                                                               
         COMMENT                   SAVE PUSHED ON CONDITION INFO                
         LA    R0,L'ONENTRY                                                     
         LA    R1,ONENTRY                                                       
         LA    R2,=CL16'SYS.ONINFO      '                                       
         L     R3,PHON#SET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   IF ON ERR SET, SET DEFAULT                   
         IF    (CPFONERR,OR,CPFDDERR),BEGIN                                     
         CLEAR CPFONERR            DEFAULT IS NO ON ERROR SET!                  
         CLEAR CPFDDERR                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF ON ATTN SET, SET DEFAULT                  
         IF    (CPFONATN,OR,CPFDDATN),BEGIN                                     
         SETMSG 'XRETURN ATTN '                                                 
         LA    R2,=CL16'SYS.ONATTN      '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         END   , OF ON COND EXISTS                                              
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  POPON - POP ON CONDITION INFO                                                
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VAR STACK SET NUMBER FOR INFO TO POP                               
*                                                                               
*  ON EXIT:                                                                     
*      ON CONDITION INFO RESET                                                  
*      R15 - CC=ZERO                                                            
*                                                                               
*  CAUTION:                                                                     
*       SYS.ONERROR VALID ONLY IF CPFONERR OR CPFDDERR SET                      
*       SYS.ONATTN VALID ONLY IF CPFONATN OR CPFDDATN SET                       
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
PPONWA   RECORD BEGIN                                                           
PPON#SET DS    F                   STACK SET # FOR POP                          
PPONNTRY DS    XL(L'ONENTRY)      ERROR INFO DSECT                              
         END                                                                    
*                                                                               
*                                                                               
POPON    XPROC PPONWA                                                           
         USING ONENTRY,R6                                                       
         LA    R6,PPONNTRY                                                      
         ST    R0,PPON#SET                                                      
*                                                                               
         COMMENT                   GET PUSHED ERROR INFORMATION                 
         LA    R0,L'ONENTRY                                                     
         LA    R1,ONENTRY                                                       
         LA    R2,=CL16'SYS.ONINFO      '                                       
         L     R3,PPON#SET                                                      
         VCALL GETVSTK                                                          
*                                                                               
         COMMENT                   IF NO  PUSHED ON CONDITIONS,                 
         IF    NZ,BEGIN                                                         
         CLEAR CPFONERR                                                         
         CLEAR CPFDDERR                                                         
         CLEAR CPFONATN                                                         
         CLEAR CPFDDATN                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PUSHED ON CONDITIONS, POP EM              
         ELSE  BEGIN                                                            
         CLEAR CPFONERR            SET FLAGS                                    
         CLEAR CPFDDERR                                                         
         CLEAR CPFONATN                                                         
         CLEAR CPFDDATN                                                         
         IF    NNFONERR,' SET CPFONERR '                                        
         IF    NNFDDERR,' SET CPFDDERR '                                        
         IF    NNFONATN,' SET CPFONATN '                                        
         IF    NNFDDATN,' SET CPFDDATN '                                        
         IF    (NNFONERR,OR,NNFDDERR),BEGIN    SET ON ERROR                     
         L     R0,NNERRLEN                                                      
         LA    R1,NNERRCMD                                                      
         LA    R2,=CL16'SYS.ONERROR     '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
         IF    (NNFONATN,OR,NNFDDATN),BEGIN    SET ON ATTN                      
         L     R0,NNATNLEN                                                      
         LA    R1,NNATNCMD                                                      
         LA    R2,=CL16'SYS.ONATTN      '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PUSHERR - PUSH ERROR/ATTN INFO                                               
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VAR STACK SET NUMBER FOR INFO TO PUSH                              
*                                                                               
*  ON EXIT:                                                                     
*      ERROR/ATTN INFO PUSHED                                                   
*      R15 - CC=ZERO                                                            
*                                                                               
*  CAUTION:                                                                     
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
PHRRWA   RECORD BEGIN                                                           
PHRR#SET DS    F                  STACK SET # FOR POP                           
PHRRNTRY DS    XL(L'ERRENTRY)     ERROR INFO DSECT                              
         END                                                                    
*                                                                               
*                                                                               
PUSHERR  XPROC PHRRWA                                                           
         USING ERRENTRY,R6                                                      
         LA    R6,PHRRNTRY                                                      
         ST    R0,PHRR#SET                                                      
*                                                                               
         COMMENT                   IF ERROR/ATTN VARS SET,,,                    
         IF    (CPFXXERR,OR,CPFXXATN),BEGIN                                     
*                                                                               
         COMMENT                   GET ERROR/ATTN VARIABLES                     
         CLEAR ERRENTRY                                                         
         IF    CPFXXERR,' SET RRFXXERR '                                        
         IF    CPFXXATN,' SET RRFXXATN '                                        
*                                                                               
         COMMENT                   IF ERROR, SAVE ERROR ID                      
         IF    CPFXXERR,BEGIN                                                   
         LA    R0,L'RRERRID                                                     
         LA    R1,RRERRID                                                       
         LA    R2,=CL16'SYS.ERRID       '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF ERROR, SAVE ERROR MESSAGE                 
         IF    CPFXXERR,BEGIN                                                   
         LA    R0,L'RREMSG                                                      
         LA    R1,RREMSG                                                        
         LA    R2,=CL16'SYS.ERRMSG      '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ST    R0,RREMSGL                                                       
         END                                                                    
*                                                                               
         COMMENT                   IF ERROR, SAVE ERROR INFO                    
         IF    CPFXXERR,BEGIN                                                   
         LA    R0,L'RREINFO                                                     
         LA    R1,RREINFO                                                       
         LA    R2,=CL16'SYS.ERRINFO     '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ST    R0,RREINFOL                                                      
         END                                                                    
*                                                                               
         COMMENT                   IF ERROR, SAVE ERROR SET NUMBER              
         IF    CPFXXERR,BEGIN                                                   
         LA    R0,L'RRSETNO                                                     
         LA    R1,RRSETNO                                                       
         LA    R2,=CL16'SYS.ERRSETNO    '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF ATTN, SAVE ATTN ID                        
         IF    CPFXXATN,BEGIN                                                   
         LA    R0,L'RRATTNID                                                    
         LA    R1,RRATTNID                                                      
         LA    R2,=CL16'SYS.ATTNID      '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF ATTN, SAVE ATTN INFO                      
         IF    CPFXXATN,BEGIN                                                   
         LA    R0,L'RRAINFO                                                     
         LA    R1,RRAINFO                                                       
         LA    R2,=CL16'SYS.ATTNINFO    '                                       
         VCALL GETDATA                                                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ST    R0,RRAINFOL                                                      
         END                                                                    
*                                                                               
         COMMENT                   SAVE PUSHED ERROR/ATTN INFO                  
         LA    R0,L'ERRENTRY                                                    
         LA    R1,ERRENTRY                                                      
         LA    R2,=CL16'SYS.ERRATTNINFO '                                       
         L     R3,PHRR#SET                                                      
         VCALL SAVEVSTK                                                         
*                                                                               
         COMMENT                   DEFAULT FOR NEXT XPROC IS:                   
         CLEAR CPFXXERR            NO ERROR SET                                 
         CLEAR CPFXXATN            NO ATTN SET                                  
*                                                                               
         END   , IF ERROR OR ATTN                                               
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  POPERR - POP ERROR/ATTENTRION INFO                                           
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VAR STACK SET NUMBER FOR INFO TO POP                               
*                                                                               
*  ON EXIT:                                                                     
*      ERROR/ATTENTION INFO RESET                                               
*      R15 - CC=ZERO                                                            
*                                                                               
*  NOTE:                                                                        
*       ERROR/ATTENTION VARIABLES ARE SET HERE (POPPED), BUT                    
*       'XRETURN ATTENTION' OR 'XRETURN ERROR' VALUES WILL                      
*       OVERRIDE THESE POPPED VALUES, AS THEY ARE PROCESSED                     
*       AFTER THIS CALL TO 'POPERR' ROUTINE.                                    
*                                                                               
*  CAUTION:                                                                     
*       SYS.ERRID VALID ONLY IF CPFXXERR SET                                    
*       SYS.ERRMSG VALID ONLY IF CPFXXERR SET                                   
*       SYS.ERRINFO VALID ONLY IF CPFXXERR SET                                  
*       SYS.ERRSETNO VALID ONLY IF CPFXXERR SET                                 
*       SYS.ATTNID VALID ONLY IF CPFXXATN SET                                   
*       SYS.ATTNINFO VALID ONLY IF CPFXXATN SET                                 
*                                                                               
*                                                                               
PPRRWA   RECORD BEGIN                                                           
PPRR#SET DS    F                   STACK SET # FOR POP                          
PPRRNTRY DS    XL(L'ERRENTRY)      ERROR INFO DSECT                             
         END                                                                    
*                                                                               
*                                                                               
POPERR   XPROC PPRRWA                                                           
         USING ERRENTRY,R6                                                      
         LA    R6,PPRRNTRY                                                      
         ST    R0,PPRR#SET                                                      
*                                                                               
         COMMENT                   GET PUSHED ERROR INFORMATION                 
         LA    R0,L'ERRENTRY                                                    
         LA    R1,ERRENTRY                                                      
         LA    R2,=CL16'SYS.ERRATTNINFO '                                       
         L     R3,PPRR#SET                                                      
         VCALL GETVSTK                                                          
*                                                                               
         COMMENT                   IF NO PUSHED ERR/ATTN VARS,                  
         IF    NZ,BEGIN                                                         
         CLEAR CPFXXERR                                                         
         CLEAR CPFXXATN                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PUSHED ERROR/ATTN, POP EM                 
         ELSE  BEGIN                                                            
         CLEAR CPFXXERR            SET FLAGS                                    
         CLEAR CPFXXATN                                                         
         IF    RRFXXERR,' SET CPFXXERR '                                        
         IF    RRFXXATN,' SET CPFXXATN '                                        
*                                                                               
         COMMENT                   IF ERROR,                                    
         IF    RRFXXERR,BEGIN                                                   
         LA    R0,L'RRERRID        POP ERRID                                    
         LA    R1,RRERRID                                                       
         LA    R2,=CL16'SYS.ERRID       '                                       
         VCALL SAVEDATA                                                         
         L     R0,RREMSGL          POP ERRMSG                                   
         LA    R1,RREMSG                                                        
         LA    R2,=CL16'SYS.ERRMSG      '                                       
         VCALL SAVEDATA                                                         
         L     R0,RREINFOL         POP ERRINFO                                  
         LA    R1,RREINFO                                                       
         LA    R2,=CL16'SYS.ERRINFO     '                                       
         VCALL SAVEDATA                                                         
         LA    R0,L'RRSETNO                                                     
         LA    R1,RRSETNO                                                       
         LA    R2,=CL16'SYS.ERRSETNO    '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF ATTN,                                     
         IF    RRFXXATN,BEGIN                                                   
         LA    R0,L'RRATTNID       POP ATTNID                                   
         LA    R1,RRATTNID                                                      
         LA    R2,=CL16'SYS.ATTNID      '                                       
         VCALL SAVEDATA                                                         
         L     R0,RRAINFOL         POP ATTNINFO                                 
         LA    R1,RRAINFO                                                       
         LA    R2,=CL16'SYS.ATTNINFO    '                                       
         VCALL SAVEDATA                                                         
         END                                                                    
*                                                                               
         END   , OF IF ERR/ATTN TO POP                                          
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'HOLD/ALLOW ATTENTION INTERRUPT ROUTINES'                        
*box                                                                            
*                                                                               
*                                                                               
*  HOLDATN - HOLD ATTENTIONS,                                                   
*                                                                               
*  DO NOT ALLOW ATTENTIONS TO INTERRUPT COMMANDS.                               
*                                                                               
*                                                                               
*                                                                               
HOLDATN  XPROC                                                                  
         USING JCB,R6                                                           
         L     R6,CVCURJCB                                                      
*                                                                               
         SET   JCBFNATN            DON'T ALLOW ATTNS TO STOP COMMANDS           
         CLEAR JCBFPATN                                                         
         IF    JCBFATTN,BEGIN      IF ALREADY HAVE ATTN,                        
         SET   JCBFPATN            SET PENDING ATTN                             
         END                                                                    
         CLEAR JCBFATTN                                                         
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ALLOWATN - ALLOW ATTENTION                                                   
*                                                                               
*  ALLOW ATTENTIONS, SET ATTENTION FLAG IF WE HAVE PENDING ATTENTION            
*                                                                               
*                                                                               
*                                                                               
ALLOWATN XPROC                                                                  
         USING JCB,R6                                                           
         L     R6,CVCURJCB                                                      
*                                                                               
         CLEAR JCBFNATN            ALLOW ATTENTIONS                             
         IF    JCBFPATN,BEGIN      IF PENDING ATTENTION,                        
         SET   JCBFATTN            SET ATTN FLAG                                
         CLEAR JCBFPATN                                                         
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  TRASHATN - SUPPRESS (THROW OUT) ATTN                                         
*                                                                               
*  WE DO MORE THAN SIMPLY CLEAR THE JCBFATTN FLAG !!!                           
*  WE ALSO COUNT THE NUMBER OF DISCAREDED ATTNS, IF IT                          
*  EXCEEDS SOME NUMBER (ABOUT 5) WE ALLOW EXEC BREAKS.                          
*                                                                               
*                                                                               
*                                                                               
TRASHATN XPROC                                                                  
         USING JCB,R6                                                           
         L     R6,CVCURJCB                                                      
*                                                                               
         IF    JCBFATTN,BEGIN      IF ATTN,,                                    
         CLEAR JCBFATTN            SUPRESS ATTN                                 
         LH    R1,CPATTNCT         COUNT SUPPRESSED ATTNS                       
         LA    R1,1(R1)                                                         
         STH   R1,CPATTNCT                                                      
         IF    (CPATTNCT,GT,5),BEGIN  IF TOO MANY SUPPRESSED ATTNS              
         CLEAR CPFONATN               ALLOW BREAK ON NEXT ATTN                  
         CLEAR CPFDDATN                                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Command Pre-Processor'                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PREPROC - PREPROCESS WYLBUR COMMAND                                          
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - COMMAND LOCATION                                                   
*            (MUST BE AT LEAST &LINESZ LONG !!)                                 
*       R0 - COMMAND LENGTH                                                     
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NEW COMMAND LENGTH                                                 
*       R1 - COMMAND LOCATION (SAME AS ABOVE!)                                  
*                                                                               
*  REGISTER USAGE:                                                              
*       R0 - END OF INPUT COMMAND                                               
*       R1 - INDEX INTO INPUT COMMAND                                           
*       R2 - FUNCTION BYTE CODE (ESCAPE/SKIP)                                   
*       R3 - TEMPORARY LENGTH REGISTER                                          
*       R4 - OUTPUT COMMAND INDEX                                               
*       R5 - INPUT COMMAND INDEX, POINTS TO LAST CHARACTER                      
*            MOVED FROM INPUT COMMAND TO OUTPUT COMMAND                         
*       R6 - EXPRESSION STACK POINTER                                           
*      R15 - WORK REGISTER                                                      
*                                                                               
*  *  FOR MORE INFO SEE SECTION ON WYLBUR PREPROCESSOR                          
*     IN "WYLBUR EXEC FILE MANUAL."                                             
*                                                                               
*  *  ALL ERRORS WILL EXIT DIRECTLY VIA CV ERROR EXIT.                          
*                                                                               
*                                                                               
PREWA    RECORD BEGIN                                                           
PRELOC   DS    F                   INPUT COMMAND LOCATION                       
PRETRT   DS    XL256               TRT AREA                                     
PREXVAL  DS    XL(L'XSLENTRY)      EXPRESSION VALUE RETURN AREA                 
PRECMD   DS    (&LINESZ+256)X      OUTPUT COMMAND WORK AREA                     
PRESCNCT DS    F                   COUNT OF RESCANS                             
PREPTR   DS    F                   RESCAN POINTER IN OUTPUT CMD                 
PREFLAGS FLAG  ,                                                                
         FLAG  PCHANGED            COMMAND CHANGED ON THIS SCAN                 
         FLAG  PRESCAN             RESCAN COMMAND                               
         FLAG  PGOTNSW             EVALUATE &N0-&S0-&W0,,, ETC                  
         FLAG  PGOTEXPR            EVALUATE &(..EXPR..)                         
         END                                                                    
ESCAPE   EQU   4                   ESCAPE CHAR FUNCTION CODE                    
SKIP     EQU   8                   SKIP CHAR FUNCTION CODE                      
         SPACE 2                                                                
*-                                                                              
*-       PREPROCESSOR CODE                                                      
*-                                                                              
PREPROC  XPROC PREWA                                                            
         ST    R1,PRELOC           INITIALIZE WORK AREA                         
         CLEAR PRESCNCT                                                         
         CLEAR PREFLAGS                                                         
         CLEAR PRETRT              SET UP TRT TABLE                             
         LA    R2,ESCAPE                                                        
         LC    R3,CPESCAPE                                                      
         IF    (R3,NZ),BEGIN       IF ESC CHAR, MARK TRT TABLE                  
         STC   R2,PRETRT(R3)                                                    
         END                                                                    
         LA    R2,SKIP                                                          
         LC    R3,CPSKIP                                                        
         IF    (R3,NZ),BEGIN       IF SKIP CHAR, MARK TRT TABLE                 
         STC   R2,PRETRT(R3)                                                    
         END                                                                    
*                                                                               
         COMMENT                   SCAN COMMAND                                 
         LR    R5,R1               R5  - START OF COMMAND                       
         LA    R4,PRECMD           R4  - OUTPUT COMMAND INDEX                   
         LR    R3,R0               R3  - COMMAND LENGTH                         
         SR    R2,R2               R2  - FUNCTION BYTE                          
         COMMENT                   R1  - COMMAND LOCATION                       
         AR    R0,R1               R0  - END OF COMMAND                         
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' TRT 0(0,R1),PRETRT '   SCAN FOR ESC/SKIP                    
         IF    Z,BEGIN                                                          
         LR    R1,R5                       IF NONE, WE DONE                     
         B     PREPDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         WHILE (R2,NZ),BEGIN       WHILE ESC/SKIP FOUND, PROCESS 'EM            
*                                                                               
         IF    (R2,EQ,SKIP),BEGIN      IF SKIP,,                                
         LR    R3,R1                                                            
         SR    R3,R5                                                            
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' MVC @R4(0),@R5 '     MOVE TEXT BEFORE SKIP TO               
         LA    R4,1(R3,R4)                OUTPUT                                
         END                                                                    
         LA    R1,@R1+1                                                         
*                                                                               
         IF    (R1,GE,R0),BEGIN    Last character in line...                    
         MVC   @R4(1),CPSKIP                                                    
         LA    R4,@R4+1                                                         
         END                                                                    
*                                                                               
         ELSEIF ((@R1,EQ,CPESCAPE),OR,(@R1,EQ,CPSKIP)),BEGIN Skip...            
         SET   PCHANGED                   We removed skip char                  
         LC    R3,@R1                     OUTPUT                                
         STC   R3,@R4                                                           
         LA    R1,@R1+1                                                         
         LA    R4,@R4+1                                                         
         END                                                                    
*                                                                               
         ELSE  BEGIN               Keep skip character...                       
         MVC   @R4(1),CPSKIP                                                    
         LA    R4,@R4+1                                                         
         END                                                                    
*                                                                               
         LR    R5,R1                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF ESCAPE,,                                  
         COMMENT                                                                
         IF    (R2,EQ,ESCAPE),BEGIN                                             
         CLEAR PRESCAN                                                          
         CLEAR PGOTNSW             CLEAR ESCAPE PROCESSING FLAGS                
         CLEAR PGOTEXPR            THEN SET ESC PROCESSING FLAGS                
         LA    R3,1(R1)                                                         
         IF    ((@R3,EQ,CPESCAPE),AND,(R3,LT,R0)),BEGIN                         
         SET   PRESCAN             SET RESCAN FLAG                              
         END                                                                    
         WHILE ((@R1,EQ,CPESCAPE),AND,(R1,LT,R0)),' LA R1,1(R1) '               
         SR    R0,R1                                                            
         LA    R15,4               Assume N/S/W vars not allowed                
         IF    ^CPFXTEND,'ACALL CHECKNSW'  Check for N/S/W var                  
         AR    R0,R1                                                            
         IF    (R15,Z),BEGIN                                                    
         SET   PGOTNSW             SET &N0,&S0,&W0 ,,, FLAG                     
         END                                                                    
         IF    ((@R1,EQ,X'4D'),AND,(R1,LT,R0)),BEGIN                            
         SET   PGOTEXPR            SET &( ... )  FLAG                           
         END                                                                    
*                                                                               
         COMMENT                   IF THIS ESCAPE IS PREPROCESSED,              
         IF    (PGOTNSW,OR,PGOTEXPR),BEGIN                                      
         LR    R3,R1                                                            
         SR    R3,R5                  MOVE TEXT BEFORE ESCAPE TO                
         DECR  R3                     OUTPUT BUFFER                             
         IF    PRESCAN,' DECR R3 '                                              
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' MVC @R4(0),@R5 '                                            
         LA    R4,1(R3,R4)                                                      
         END                                                                    
         ST    R4,PREPTR              SAVE RESCAN POINTER                       
*                                                                               
         IF    PGOTNSW,BEGIN       IF &N0-&S0-&W0, ETC,,,                       
         USING XSENTRY,R2                                                       
         SET   PCHANGED                                                         
         LA    R2,PREXVAL                                                       
         SR    R0,R1                                                            
         ACALL GETNSW                 GET VALUE                                 
         BNZ   CVERROR                (RETURNS VALUE @R2)                       
         AR    R0,R1                                                            
         LR    R5,R1                                                            
         COMMENT                      R2 - VARIABLE VALUE                       
         VCALL MAKESTR                CHANGE VALUE TO STRING                    
         IF    NZ,' BOMB '                                                      
         LH    R3,XSSTRLEN                                                      
         IF    (R3,POS),BEGIN         MOVE STRING INTO COMMAND                  
         DEX   R3,' MVC  @R4(0),XSSTRLOC '                                      
         LA    R4,1(R3,R4)                                                      
         END                                                                    
         LA    R15,PRECMD+&LINESZ      CHECK OUTPUT CMD LENGTH                  
         IF    (R4,GE,R15),' ACALL MAXSTR '                                     
         DROP  R2                                                               
         END                                                                    
*                                                                               
         IF    PGOTEXPR,BEGIN      IF &( ... ),,,                               
         USING XSENTRY,R2                                                       
         SET   PCHANGED                                                         
         LA    R2,PREXVAL              EXPR VALUE RETURN AREA                   
         LA    R1,1(R1)                SKIP LEADING '('                         
         SR    R0,R1                                                            
         VCALL EVALEXPR                EVALUATE EXPRESSION                      
         AR    R0,R1                                                            
         IF    (R15,H),BEGIN                                                    
         B     CVERROR                                                          
         END                                                                    
         IF    (R15,NEG),BEGIN                                                  
         ERROR 'Missing expression.'                                            
         END                                                                    
         IF    (@R1,NE,')'),BEGIN                                               
         SR    R0,R1                                                            
         XPUSH R0,R1                                                            
         TSEG  'Expecting ")".  ',,CR                                           
         XPOP  R0,R1                                                            
         VCALL CHECKEND                                                         
         BNZ   CVERROR                                                          
         ERROR 'Unmatched parenthesis in expression.'                           
         END                                                                    
         LA    R1,1(R1)                SKIP TRAILING ')'                        
         LR    R5,R1                                                            
         COMMENT                       R2 - EXPR VALUE AREA                     
         VCALL MAKESTR                 MAKE STRING                              
         IF    NZ,' BOMB '                                                      
         LH    R3,XSSTRLEN                                                      
         IF    (R3,POS),BEGIN          MOVE STRING INTO COMMAND                 
         DEX   R3,' MVC  @R4(0),XSSTRLOC '                                      
         LA    R4,1(R3,R4)                                                      
         END                                                                    
         LA    R15,PRECMD+&LINESZ      CHECK OUTPUT CMD LENGTH                  
         IF    (R4,GE,R15),' ACALL MAXSTR '                                     
         DROP  R2                                                               
         END                                                                    
*                                                                               
         IF    PRESCAN,BEGIN       IF WE MUST RESCAN,,,                         
         L     R3,PRESCNCT                                                      
         LA    R3,1(R3)               UPDATE RESCAN COUNT                       
         ST    R3,PRESCNCT                                                      
         IF    (R3,GT,50),' ACALL MAXSCAN '                                     
         LR    R3,R0                                                            
         SR    R3,R1                                                            
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' MVC @R4(0),@R5 '     MOVE END OF INPUT TO OUTPUT            
         LA    R4,1(R3,R4)            COMMAND,  R4 - END OF CMD                 
         END                                                                    
         LA    R1,PRECMD                                                        
         LR    R3,R4                                                            
         SR    R3,R1                                                            
         IF    (R3,GT,&LINESZ),' ACALL MAXSTR '                                 
         IF    (R3,POS),BEGIN                                                   
         L     R1,PRELOC              MOVE OUTPUT COMMAND TO INPUT              
         DEX   R3,' MVC @R1(0),PRECMD '  COMMAND, AND RESCAN                    
         END                                                                    
         LA    R3,PRECMD              RESET INPUT COMMAND POINTERS              
         SR    R4,R3                  R4 - LENGTH OF CMD                        
         L     R0,PRELOC                                                        
         AR    R0,R4                  R0 - END OF NEW INPUT COMMAND             
         L     R1,PREPTR                                                        
         LA    R3,PRECMD                                                        
         SR    R1,R3                                                            
         A     R1,PRELOC              R1 - INDEX INTO NEW INPUT CMD             
         LR    R5,R1                  R5 -  DITTO                               
         L     R4,PREPTR              R4 - INDEX INTO OUTPUT CMD                
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR NEXT ESCAPE/SKIP, WE                
         COMMENT                   CONTINUE LOOPING IF ONE IS FOUND             
         COMMENT                                                                
         LR    R3,R0               R3 - LENGTH OF REMAINING STRING              
         SR    R3,R1                                                            
         SR    R2,R2               R2 - FUNCTION BYTE                           
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' TRT @R1(0),PRETRT '   SCAN FOR NEXT ESC/SKIP                
         END                                                                    
         IF    Z,BEGIN                 IF NO MORE ESC/SKIP IN LINE              
         IF    PCHANGED,BEGIN          AND IF COMMAND CHANGED                   
         LR    R1,R0                                                            
         LR    R3,R0                                                            
         SR    R3,R5                                                            
         IF    (R3,POS),BEGIN          MOVE LAST PART OF CMD                    
         DEX   R3,' MVC @R4(0),@R5 '                                            
         LA    R4,1(R3,R4)                                                      
         END                                                                    
         LA    R15,PRECMD+&LINESZ      CHECK OUTPUT CMD LENGTH                  
         IF    (R4,GE,R15),' ACALL MAXSTR '                                     
         LR    R0,R4                   R0 - END OF NEW CMD IN INPUT BUF         
         LA    R3,PRECMD                                                        
         SR    R0,R3                                                            
         A     R0,PRELOC                                                        
         LA    R4,PRECMD               MOVE NEW COMMAND TO INPUT BUF            
         L     R1,PRELOC                                                        
         LR    R3,R0                                                            
         SR    R3,R1                                                            
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' MVC @R1(0),@R4 '      MOVE IT                               
         END                                                                    
         END                                                                    
         IF    ^PCHANGED,BEGIN                                                  
         L     R1,PRELOC                                                        
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
PREPDONE LABEL ,                                                                
         COMMENT                   RETURN R1,R0 - COMMAND LOC, LEN              
         SR    R0,R1                                                            
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PREPROCESSOR ERRORS                                                          
*                                                                               
*                                                                               
*                                                                               
MAXSCAN  PROC  ,                                                                
         ERROR 'Too many rescans.'                                              
         PEND                                                                   
         SPACE 2                                                                
*                                                                               
*                                                                               
MAXSTR   PROC  ,                                                                
         ERROR 'Command length overflow during preprocessing.'                  
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'EXEC Command Scanner'                                           
*box                                                                            
*                                                                               
*                                                                               
*  XCMDSCAN - SCAN FOR SPECIAL EXEC STATEMENTS (COMMANDS)                       
*                                                                               
*  WE SCAN FOR THE FOLLOWING SPECIAL EXEC STATMENTS:                            
*       1. ASSIGNMENT, INDICATED BY <VAR> = ...                                 
*       2. LABEL, INDICATED BY <LABEL VAR>: ...                                 
*       3. ARRAY ASSIGNMENT, INDICATED BY <ARRAY NAME>[ ...                     
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       SCAN CONTROL BLOCK POINTS TO COMMAND                                    
*                                                                               
*  ON EXIT:                                                                     
*       1. IF LINE IS SPECICAL EXEC STATMENT,  WE CALL PROCESSING               
*          THE APPROPRIATE PROCESSING ROUTINE.  WE DO NOT RETURN.               
*       2. IF THE LINE IS NOT SPECIAL EXEC STATEMENT,,, WE JUST                 
*          RETURN                                                               
*                                                                               
*                                                                               
*                                                                               
XCMDWA   RECORD BEGIN                                                           
XCMDVAR  DS    XL(L'VPARM)         VAR, PROC, LABEL, ARRAY ENTRY                
         END                                                                    
*                                                                               
XCMDSCAN XPROC XCMDWA                                                           
         USING VNENTRY,R2                                                       
*-                                                                              
*-      Skip over leading blanks and also skip over a single leading            
*-         comma.  Starting Wylbur commands with a leading comma is             
*-       a Spires convention so it is supported here for consistency            
*-         (sort of).                                                           
*-                                                                              
         SCINFO ,                  RETURNS R1,R0 LOC,LEN OF CMD STR             
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    ((R0,POS),AND,(@R1,EQ,',')),'LA R1,@R1+1; DECR R0'               
         SCINIT ,                  Update scan ptrs                             
*                                                                               
         LA    R2,XCMDVAR                                                       
         VCALL SCNVNAME            SCAN FOR VARIABLE NAME                       
         IF    (Z,AND,(R0,POS)),BEGIN                                           
*-                                                                              
*-       GOT <VAR NAME>:                 *** GOT LABEL                          
*-                                                                              
         IF    (@R1,EQ,':'),BEGIN                                               
         IF    ((R0,EQ,1),OR,(@R1,EQ,': ')),BEGIN                               
         COMMENT                   SCANCB - POINTS TO ENTIRE COMMAND            
         VCALL LABEL               PROCESS LABEL,  DOES NOT RETURN              
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                      SKIP ANY BLANKS                           
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*-                                                                              
*-       GOT <VAR NAME> = ...            *** SET VARIABLE                       
*-                                                                              
         IF    ((@R1,EQ,'='),AND,(R0,POS)),BEGIN                                
         COMMENT                   ZAP FOR "COMM =" COMPATABILITY               
         ACALL ZAPCOMM             ZAP FOR "END = " COMPATIBILITY               
         IF    Z,BEGIN                                                          
         VCALL SETVAL                                                           
         B     CVNEXT                                                           
         END                                                                    
         END                                                                    
*-                                                                              
*-       GOT <VAR NAME>[...              *** GOT ARRAY                          
*-                                                                              
         IF    ((@R1,EQ,'['),AND,(R0,POS)),BEGIN                                
         COMMENT                   ZAP FOR "COMM[..." COMPATABILITY             
         ACALL ZAPCOMM             ZAP FOR "END[..." COMPATIBILITY              
         IF    Z,BEGIN                                                          
         VCALL SETVAL                                                           
         B     CVNEXT                                                           
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       IF NOT SPECIAL EXEC COMMAND                                            
*-                                                                              
         COMMENT                   IF NONE OF THE ABOVE WE RETURN               
         COMMENT                                                                
         SR    R15,R15                                                          
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ZAPCOMM - COMMPATIBLITY ZAP FOR COMM =,, END =,,                             
*                                                                               
*  ON ENTRY: SCANNER CONTROL BLOCK POINTS TO COMMAND.                           
*                                                                               
*  ON EXIT:                                                                     
*       CC = ZERO, NOT "COMMENT ='  OR 'END ='                                  
*       CC = NZ, IF "COMMENT =" OR 'END ='                                      
*                                                                               
*                                                                               
ZAPCOMM  PROC  XCMDWA                                                           
         USING VNENTRY,R2                                                       
         SCINFO ,                                                               
         LA    R2,XCMDVAR                                                       
         VCALL SCNVNAME                                                         
         IF    (Z,AND,(R0,POS)),BEGIN                                           
         CLC   VNNAME(8),=CL8'COMM'                                             
         IF    EQ,'LA R15,4'                                                    
         CLC   VNNAME(8),=CL8'COMME'                                            
         IF    EQ,'LA R15,4'                                                    
         CLC   VNNAME(8),=CL8'COMMEN'                                           
         IF    EQ,'LA R15,4'                                                    
         CLC   VNNAME(8),=CL8'COMMENT'                                          
         IF    EQ,'LA R15,4'                                                    
         CLC   VNNAME(8),=CL8'END'                                              
         IF    EQ,'LA R15,4'                                                    
         CLC   VNNAME(8),=CL8'PUTEND'                                           
         IF    EQ,'LA R15,4'                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXCEPTNS - HANDLE PROCEDURE NAME EXCEPTIONS !                                
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - VARIABLE ENTRY,,, WITH NAME SET                                    
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=0 IF NAME IS AN EXCEPTION,,, (EG. IF, WHILE,,,)                
*       R15 - CC=NZ, IF NAME IS NOT EXCEPTION                                   
*                                                                               
*                                                                               
EXCEPTNS PROC  ,                                                                
         USING VNENTRY,R2                                                       
         LA    R15,4                                                            
         IF    (VNNAME,EQ,'IF    '),' CLEAR R15 '                               
         IF    (VNNAME,EQ,'WHILE '),' CLEAR R15 '                               
         IF    (VNNAME,EQ,'CASE  '),' CLEAR R15 '                               
         IF    (VNNAME,EQ,'UNTIL '),' CLEAR R15 '                               
         PEND  ,                                                                
         DROP  R2                                                               
         TITLE 'EXEC File Processing Routines.'                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETEXCMD - GET NEXT EXEC COMMAND !                                           
*                                                                               
*  THIS ROUTINE IS RESPONSIBLE FOR GETTING THE NEXT EXEC                        
*  COMMAND AND UPDATING THE EXEC LINE POINTER.                                  
*                                                                               
*                                                                               
*                                                                               
GETEXCMD XPROC                                                                  
*                                                                               
         COMMENT                   RE-INITIALIZE, CHECK FOR LOOP                
         VCALL INITCMD                                                          
         ACALL EXLOOPCK                                                         
*                                                                               
         COMMENT                   CHECK FOR EXEC FILE,                         
         ACALL EXECHK                                                           
*                                                                               
*$ THE FOLLOWING BIT OF CODE SHOULD BE REMOVED AND THOSE SETTING                
*$ CVNEXT NEGATIVE SHOULD INSTEAD PUT CL4'BAD ' IN CPEXNEXT                     
         COMMENT                   DELETE WHEN YOU CAN !!                       
         L     R1,CPEXLINE                                                      
         IF    (R1,LT,0),BEGIN                                                  
         ACALL SETPAUSE                                                         
         ABORT 'EXEC pointer is not set.',,NOEXEPTR                             
         END                                                                    
*                                                                               
         COMMENT                   GET NEXT LINE                                
         COMMENT                                                                
         L     R1,CPEXNEXT                                                      
         IF    (R1,GE,0),BEGIN     IF LINE NUMBER SPECIFIED, GET IT             
         MVC   CPEXLINE,CPEXNEXT                                                
         MVC   CPEXNEXT,=CL4'NEXT'                                              
         LA    R1,CPCMD                                                         
         LA    R2,CPEXLINE                                                      
         ACALL EXFRST                                                           
         STH   R0,CPCMDLEN                                                      
         END                                                                    
         ELSEIF (CPEXNEXT,EQ,'NEXT'),BEGIN   IF GET NEXT LINE                   
         LA    R1,CPCMD                                                         
         LA    R2,CPEXLINE                                                      
         ACALL EXNEXT                                                           
         STH   R0,CPCMDLEN                                                      
         END                                                                    
         ELSEIF (CPEXNEXT,EQ,'BAD '),BEGIN  NEXT LINE POINTER NOT SET           
         MVC   CPEXNEXT,=CL4'NEXT'                                              
         ACALL SETPAUSE                                                         
         ABORT 'EXEC pointer is not set.',,NOEXEPTR                             
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
         MVC   CPEXNEXT,=CL4'NEXT'                                              
*                                                                               
         COMMENT                   FOR EXTENDED EXECS CHECK SCOPE               
         IF    (CP#XSET,NE,=A(CP#XOLD)),BEGIN                                   
         IF    ((CPEXLINE,LE,CPSCOPE),OR,(CPEXLINE,GT,CPSCOPD)),BEGIN           
         ACALL SETPAUSE                                                         
         ABORT 'EXEC POINTER: not in scope of current procedure.'               
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR END OF FILE                        
         IF    (CPEXLINE,EQ,'EOF '),BEGIN                                       
         CLEAR CPEXLINE                                                         
         ACALL SETPAUSE                                                         
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   DO COMMAND LOGGING, IF REQUESTED             
         COMMENT                     SET EXEC (CPFLOG) LOGS OLD ONLY            
         COMMENT                     XD LOG (CPFXLOG) LOGS BOTH                 
         COMMENT                     WE NEVER LOG PUBLIC XPROCS                 
         CLEAR CPFTYPED              Non-typed                                  
         IF    (' CLC CP#XSET,=AL4(CV#FEXEC) ',LT),BEGIN                        
         IF    (CPFLOG,AND,CPFXOLD),BEGIN                                       
         ACALL WRITECOM                                                         
         END                                                                    
         ELSEIF CPFXLOG,BEGIN                                                   
         ACALL WRITECOM                                                         
         END                                                                    
         END                                                                    
         SET   CPFNPT              DON'T ALLOW READ TEXT FROM P/P               
         COMMENT                   WHAT DOES ABOVE LINE DO ?? GOT ME            
         COMMENT                   ABOVE LINE LEFT FROM OLD CODE                
*                                                                               
         COMMENT                   IF EXEC DEBUG ON,                            
         IF    CPFDEBUG,BEGIN         CHECK FOR TRAPS                           
         VCALL DBLINE                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF EXEC MONITOR ON,                          
         IF    CPFMON,BEGIN           SET EXEC LINE INFO FOR MON                
         VCALL SETXLINE                                                         
         END                                                                    
*                                                                               
         COMMENT                   GO EXECUTE COMMAND                           
         LH    R0,CPCMDLEN                                                      
         LA    R1,CPCMD                                                         
         VCALL EDTCOM              DOES NOT RETURN !  --->>>                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  WRITE OUT COMMAND TO BE EXECUTED                                             
*                                                                               
*  THIS ROUTINE IS CALLED WHEN EXEC LOGGING IS IN EFFECT.                       
*                                                                               
WRITECOM PROC                                                                   
         TSEG  '-> '                                                            
         L     R0,CPEXLINE         LINE NO OF COMMAND                           
         VCALL CVEXNO              CONVERT TO EXTERNAL FORMAT                   
         TSEG  (R1),(R0),B                                                      
*                                                                               
         TSEG  CPCMD,LH:CPCMDLEN,WR                                             
         BP    CVABORT             ABORT EXEC IF ATTN                           
         PEND                                                                   
         EJECT                                                                  
*                                                                               
* IF CPLIMIT COMMANDS HAVE BEEN EXECUTED WITHOUT TYPING THEN                    
* WRITE OUT MESSAGE BEFORE CONTINUING SO USER CAN CHECK FOR LOOP.               
*                                                                               
* IN ADDITION THIS ROUTINE MAKES SURE ANOTHER USER IS DISPATCHED                
* IF THE TIMESLICE HAS EXPIRED.                                                 
*                                                                               
EXLOOPCK PROC                                                                   
         INCR  R15,CPEXCMCT        COUNT THE COMMAND                            
         IF    ((CPLLIMIT,NZ),AND,(R15,GE,CPLLIMIT)),BEGIN                      
         CLEAR CPEXCMCT                                                         
         TNUM  (R15)                                                            
         TSEG  ' commands executed'                                             
         IF    ^CPEXLINE.X'80',BEGIN                                            
         TSEG  ' at line '                                                      
         L     R0,CP#XSET                                                       
         L     R1,CPRGEXEC                                                      
         LA    R2,CPEXLINE                                                      
         VCALL SEGXLNO             Current EXEC lineno                          
         END                                                                    
         TWRITE                                                                 
         BNZ   CVABORT             GO BREAK EXEC IF ATTENTION                   
         END                                                                    
*                                                                               
         VCALL SLICECHK            CHECK FOR TIMESLICE EXPIRATION               
         PEND                                                                   
         TITLE 'Get EXEC Lines Routines'                                        
*box                                                                            
*                                                                               
*                                                                               
*  EXGET - GET LINE FROM EXEC                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*                                                                               
EXGET    XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGET                                                             
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    Z,BEGIN                                                          
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXSPGET - SPECIAL PURPOSE GET LINE FROM EXEC                                 
*                                                                               
*  FOR SPECIAL PURPOSE GET, ONE MUST SPECIFY EXEC FILE SET NUMBER.              
*  CURRENT EXEC FILE IS NOT ASSUMED !  (SEE R0 ON ENTRY.)                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*                                                                               
EXSPGET  XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         COMMENT R0-EXEC SET#                                                   
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGET                                                             
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    Z,BEGIN                                                          
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXFRST - GET FIRST EXEC LINE IN RANGE                                        
*                                                                               
*       WE GET LINE SPECIFIED, OR NEXT LINE, WHICHEVER IS FIRST.                
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER OF START OF RANGE                                      
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER READ                                                   
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, IF NO LINE, (AND @R2 = 'EOF ')                              
*                                                                               
*                                                                               
EXFRST   XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETFRST                                                         
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    (R15,Z),BEGIN                                                    
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF FILE                               
         IF    (R15,NZ),BEGIN                                                   
         MVC   0(4,R2),=CL4'EOF '                                               
         CLEAR R0                                                               
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXLAST - GET LAST EXEC LINE IN RANGE                                         
*                                                                               
*       WE GET LINE SPECIFIED, OR PREV LINE, WHICHEVER IS LAST.                 
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER OF START OF RANGE                                      
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER READ                                                   
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, IF NO LINE, (AND @R2 = 'EOF ')                              
*                                                                               
*                                                                               
EXLAST   XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETLAST                                                         
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    (R15,Z),BEGIN                                                    
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF FILE                               
         IF    (R15,NZ),BEGIN                                                   
         MVC   0(4,R2),=CL4'EOF '                                               
         CLEAR R0                                                               
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXNEXT - GET LINE FROM EXEC                                                  
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER (CURRENT)                                              
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER (NEXT)                                                 
*      R15 - CC=ZERO, LINE FOUND                                                
*            CC=NZ, IF LINE NOT FOUND, (AND @R2='EOF ')                         
*                                                                               
*                                                                               
EXNEXT   XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R15,CPRGEXEC                                                     
         L     R0,CP#XSET                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETNEXT                                                         
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    (R15,Z),BEGIN                                                    
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF FILE                               
         IF    (R15,NZ),BEGIN                                                   
         MVC   0(4,R2),=CL4'EOF '                                               
         CLEAR R0                                                               
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXPREV - GET PREVIOUS LINE FROM EXEC                                         
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA  (LEN = &LINESZ)                                         
*      @R2 - LINE NUMBER (CURRENT)                                              
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER (PREVIOUS)                                             
*      R15 - CC=ZERO, LINE GOT                                                  
*                                                                               
*                                                                               
EXPREV   XPROC ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R15,CPRGEXEC                                                     
         L     R0,CP#XSET                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET EXEC LINE                                
         L     R15,CPRGEXEC                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETPREV                                                         
*                                                                               
         COMMENT                   BLANK OUT REST OF BUFFER                     
         IF    Z,BEGIN                                                          
         LR    R2,R1                                                            
         AR    R2,R0                                                            
         LA    R3,&LINESZ                                                       
         SR    R3,R0                                                            
         L     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXFRSTLN - GET FIRST LINE NUMBER OF EXEC                                     
*                                                                               
*  ON EXIT:                                                                     
*       R0 - FIRST LINE NUMBER                                                  
*      R15 - CC=ZERO, FIRST LINE EXISTS                                         
*            CC=NZ, NO FIRST LINE !                                             
*                                                                               
EXFRSTWA RECORD BEGIN                                                           
EXFLINE  DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
EXFRSTLN XPROC EXFRSTWA                                                         
         CLEAR EXFLINE                                                          
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGEXEC                                                     
         LA    R2,EXFLINE                                                       
         VCALL XLOCFRST                                                         
*                                                                               
         IF    Z,BEGIN                                                          
         L     R0,EXFLINE                                                       
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXLASTLN - GET LAST LINE NUMBER OF EXEC                                      
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LAST LINE NUMBER                                                   
*      R15 - CC=ZERO, FIRST LINE EXISTS                                         
*            CC=NZ, NO LAST LINE !                                              
*                                                                               
EXLASTWA RECORD BEGIN                                                           
EXLLINE  DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
EXLASTLN XPROC EXLASTWA                                                         
         MVC   EXLLINE(4),=XL4'FFFFFFFF'                                        
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGEXEC                                                     
         LA    R2,EXLLINE                                                       
         VCALL XLOCLAST                                                         
*                                                                               
         IF    Z,BEGIN                                                          
         L     R0,EXLLINE                                                       
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXNEXTLN - GET NEXT LINE NUMBER OF EXEC                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - CURRENT LINE NUMBER                                                
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NEXT LINE NUMBER                                                   
*      R15 - CC=ZERO, NEXT LINE EXISTS                                          
*            CC=NZ, NO NEXT LINE !                                              
*                                                                               
EXNEXTWA RECORD BEGIN                                                           
EXNLINE  DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
EXNEXTLN XPROC EXNEXTWA                                                         
         ST    R0,EXNLINE                                                       
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGEXEC                                                     
         LA    R2,EXNLINE                                                       
         VCALL XLOCNEXT                                                         
*                                                                               
         IF    Z,BEGIN                                                          
         L     R0,EXNLINE                                                       
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EXPREVLN - GET PREV LINE NUMBER OF EXEC                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - CURRENT LINE NUMBER                                                
*                                                                               
*  ON EXIT:                                                                     
*       R0 - PREV LINE NUMBER                                                   
*      R15 - CC=ZERO, NEXT LINE EXISTS                                          
*            CC=NZ, NO NEXT LINE !                                              
*                                                                               
EXPREVWA RECORD BEGIN                                                           
EXPLINE  DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
EXPREVLN XPROC EXPREVWA                                                         
         ST    R0,EXPLINE                                                       
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XSET                                                       
         L     R15,CPRGEXEC                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGEXEC                                                     
         LA    R2,EXPLINE                                                       
         VCALL XLOCPREV                                                         
*                                                                               
         IF    Z,BEGIN                                                          
         L     R0,EXPLINE                                                       
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                                                               
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Common Routines'                                                
*$ MOVE TO ACT !!!!!!!!!                                                        
*$ MOVE TO ACT !!!!!!!!!                                                        
*$ MOVE TO ACT !!!!!!!!!                                                        
*box                                                                            
*                                                                               
*                                                                               
*  XUNPRESS - TEXT UNPRESS                                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - unpress area (at least &LINESZ bytes)                              
*       R2 - text to unpress                                                    
*            (ie. 1 byte len n, n bytes pressed text)                           
*                                                                               
*  On exit:                                                                     
*       R1,R0 - unpress loc, len                                                
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
XUNPRESS XPROC ,                                                                
*                                                                               
         LR    R4,R1               blank unpress area                           
         LA    R5,&LINESZ                                                       
         L     R15,=X'40000000'                                                 
         MVCL  R4,R14                                                           
*                                                                               
         LC    R3,0(R2)            R3 - end of text                             
         LA    R3,1(R3,R2)                                                      
         LA    R2,1(R2)            R2 - start of text                           
         SR    R4,R4               R4 - blank count                             
         SR    R5,R5               R5 - text count                              
         LR    R6,R1               R6 - unpress buffer pointer                  
         COMMENT                                                                
         WHILE (R2,LT,R3),BEGIN    loop doing it to it                          
         IC    R4,0(R2)                                                         
         IC    R5,0(R2)                                                         
         LA    R2,1(R2)                                                         
         SRL   R4,4                                                             
         SLL   R5,28                                                            
         SRL   R5,28                                                            
         AR    R6,R4               skip blanks                                  
         IF    (R5,POS),BEGIN                                                   
         DEX   R5,' MVC 0(0,R6),0(R2) '                                         
         INCR  R5                                                               
         END                                                                    
         AR    R2,R5                                                            
         AR    R6,R5                                                            
         END                                                                    
*                                                                               
         LR    R0,R6               return length in R0                          
         SR    R0,R1                                                            
*                                                                               
         CLEAR R15                                                              
         PRETURN R0                                                             
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SETPARM - SET EXEC PARAMETER                                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - PARM LENGTH                                                        
*       R1 - PARM ADDRESS                                                       
*                                                                               
*                                                                               
*                                                                               
SETPARM  XPROC                                                                  
*                                                                               
         COMMENT                   SAVE EXEC FILE NAME                          
         COMMENT                   R1,R0 - PARM LOC, LEN                        
         COMMENT                   R2 - DATA ITEM NAME                          
         LA    R2,=CL16'SYS.PARM        '                                       
         VCALL SAVEDATA                                                         
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHECKNSW - CHECK FOR N0-N9, S0-S9, AND W0-W9                                 
*                                                                               
*  CHECK IF @R1 POINTS TO PREDEFINED VARIABLE,,, (EG. N0,S2..)                  
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - STRING LENGTH                                                      
*       R1 - STRING LOCATION                                                    
*                                                                               
*  ON EXIT:                                                                     
*       R0,R1 - UNCHANGED,                                                      
*       R15 - CC=ZERO - IF @R1 POINTS TO N,W,S VARIABLE                         
*             CC=NZ - IF NOT THE ABOVE                                          
*                                                                               
*                                                                               
CHECKNSW PROC                                                                   
         LA    R15,4               ASSUME NOT                                   
         IF    (R0,GE,2),BEGIN                                                  
         SR    R2,R2                                                            
         IC    R2,@R1              R2 - 1ST CHAR OF PREDEFINED VAR              
         O     R2,=X'00000040'          IN UPPER CASE                           
         LA    R3,C'N'                                                          
         LA    R4,C'S'                                                          
         LA    R5,C'W'                                                          
         IF    ((R2,EQ,R3),OR,(R2,EQ,R4),OR,(R2,EQ,R5)),BEGIN                   
         IF    ((@R1+1,GE,'0'),AND,(@R1+1,LE,'9')),BEGIN                        
         CLEAR R15                                                              
         END                                                                    
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETNSW - GET VALUE OF N0-N9, S0-S9, OR W0-W9                                 
*                                                                               
*  GET VALUE OF PREDEFINED VARIABLE,,, IT IS ASSUMED THAT @R1                   
*  POINTS TO A PREDEFINED VARIABLE. !                                           
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - STRING LENGTH                                                      
*       R1 - STRING LOCATION                                                    
*       R2 - RETURN VARIABLE AREA                                               
*                                                                               
*  ON EXIT:                                                                     
*       R0 - UPDATED LENGTH                                                     
*       R1 - UPDATED LOCATION                                                   
*       R6 - VARIABLE VALUE                                                     
*      R15 - CC=ZERO, IF OK                                                     
*            CC=NZ, IF ERROR ,,, ERROR MSG IS TSEG'D                            
*                                                                               
*                                                                               
*                                                                               
GETNSW   PROC                                                                   
         USING VNENTRY,R2                                                       
         ACALL CHECKNSW                                                         
         IF    Z,BEGIN             IF @R1, IS PREDEFINED VAR,,,                 
         XPUSH R0,R1                                                            
         CLEAR VNENTRY             CLEAR VARIABLE ENTRY                         
         MVC   VNNAME(L'VNNAME),CVBLANKS                                        
         MVC   VNNAME(2),0(R1)     SET VARIABLE ENTRY NAME                      
         OC    VNNAME(2),=X'4040'  IN UPPER CASE                                
         LA    R1,VNENTRY                                                       
         VCALL GETVAR              GET VARIABLE VALUE                           
         IF    NZ,BEGIN                                                         
         TSEG  '"'                                                              
         TSEG  VNNAME,,T                                                        
         TSEG  '" is undefined.'  IF VAR UNDEFINED, GIVE ERROR MSG              
         LA    R15,4                                                            
         END                                                                    
         XPOP  R0,R1                                                            
         LA    R1,2(R1)                                                         
         DECR  R0                                                               
         DECR  R0                                                               
         END                                                                    
         ELSE  BEGIN              IF @R1 IS NOT PREDEFINED VAR,,                
         SCINIT (R1),(R0)         GIVE ERROR MESSAGE                            
         SCAN  ,                                                                
         SCANCHK                                                                
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.'                                                     
         LA    R15,4                                                            
         END                                                                    
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         DROP  R2                                                               
         TITLE 'LOST CAUSES'                                                    
*box                                                                            
*                                                                               
*                                                                               
* ROUTINES THAT MUST BE CODED !                                                 
*                                                                               
*   oh yeah !!                                                                  
*                                                                               
NONE     PROC  ,,                                                               
         PEND  ,                                                                
*                                                                               
*                                                                               
         EJECT                                                                  
         QLTORG                                                                 
         VLTORG                                                                 
         END   .                                                                
