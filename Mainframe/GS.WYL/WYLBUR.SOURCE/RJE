RJE      TITLE 'WYLBUR''s Job Entry Commands'                                   
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
*                                                                               
*   Modification history:                                                       
*                                                                               
*     13-Apr-1995 - M. Durket                                                   
*       Fixed error in SETPRIO routine. The message TSEGed was                  
*       usually garbage because the CEIL statement used R15 instead             
*       of R0.                                                                  
*                                                                               
         HIBAL                                                                  
*                                                                               
WYLRJE   CSECT                                                                  
RJE      IDENT 4072                10:30:08 03/12/04  (SLP)                     
*                                                                               
         SYSDEFN                                                                
         GBLC  &INSTALN            (See SYSDEFN)                                
         GBLB  &JESMAIL,&PGMNAME   (See SYSDEFN)                                
         GBLB  &JCLPRIO            (See SYSDEFN)                                
         GBLC  &STDCLAS,&DOCFORM,&DOCCLAS,&FICCLAS                              
*                                                                               
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
*                                                                               
         COPY  SYSDSNS                                                          
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         EJECT                                                                  
KWR      RECORD 'KWR'                                                           
         EJECT                                                                  
SEGCB    RECORD 'SEGCB'                                                         
         EJECT                                                                  
         COPY  FFINFO                                                           
*                                                                               
         POP   DSECTS                                                           
         TITLE 'SET WPRINTERS Command'                                          
*box                                                                            
*                                                                               
*  Printer Information                                                          
*                                                                               
PRT      RECORD BEGIN                                                           
PRTLINK  DS    A                   Next PRT ptr                                 
*                                                                               
PRTFLAG  FLAG                                                                   
         FLAG  PRTFNKEY            - Password not needed to logon               
*                                                                               
PRTMAX#  EQU   6                                                                
PRTNAME  DS    (PRTMAX#)CL8        Printer name (primary is first)              
*                                                                               
PRTACCT  DS    CL5                 Logon account (UUUGG)                        
PRTCMD   DS    CL80                First command to execute                     
         END                                                                    
         SPACE 2                                                                
SPRTWA   RECORD BEGIN                                                           
SPRTFLAG FLAG                                                                   
*                                                                               
SPRTQH   DS    A                   New PRT queue head ptr                       
SPRTQT   DS    A                   New PRT queue tail ptr                       
*                                                                               
SPRT     DS    XL(L'PRT)           Working PRT entry                            
*                                                                               
SPRTLINE DS    CL(&LINESZ)         Working line buffer                          
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SETWPRT -- SET WPRINTERS Command.  (Priv'd.)                                 
*                                                                               
*    Note:  This routine is also called after a successful save                 
*           of "PRINTERS ACCOUNT GS.WYL".                                       
*                                                                               
SETWPRT  XPROC SPRTWA                                                           
         LA    R6,SPRTWA                                                        
         USING SPRTWA,R6                                                        
         CLEAR SPRTWA                                                           
         ST    R6,CPCWA                                                         
*                                                                               
         IF    (CPCMNM,NE,'W'),BEGIN  Entered after SAVE...                     
         TCCR                                                                   
         TSEG  'Type SET WPRINTERS to reset printer tables.'                    
         B     CVNEXT                                                           
         END                                                                    
         ELSE  BEGIN               Command entry...                             
         IF    ^CPSFSPR,'VCALL NOTVALID'                                        
         END                                                                    
*                                                                               
         LA    R0,SDSNPRIN         Want PRINTERS dsn                            
         VCALL EXIT0               LOCAL exit - Format system dsname            
         SCINIT (R1),(R0)                                                       
         VCALL DOFNAME                                                          
         VCALL FNAMOPTS                                                         
         VCALL FNAMEFIN                                                         
*                                                                               
         SET   CPLFLG1.CPFALL                                                   
         SET   CPFRDEXT                                                         
         VCALL NDETRNG                                                          
*                                                                               
         VCALL EXTOPEN             Open the file                                
         IF    (R15,NZ),CVNEXT                                                  
*                                                                               
         RDRNG SPRTRTN,(DESRTRN+LXCATRTN+UNPRST)                                
*                                                                               
         VCALL EXTCLOSE            All done                                     
*-                                                                              
*-       Now do post-processing.                                                
*-                                                                              
         IF    (SPRTQH,Z),BEGIN    Nothing there...                             
         IF    CVFINIT,EXIT                                                     
         TCCR                                                                   
         TSEG  'No printers defined.',,CR                                       
         END                                                                    
*-                                                                              
*-       Make new printer table the system default.                             
*-                                                                              
         L     R5,CVPRTQH          Save old table ptr                           
         MVC   CVPRTQH,SPRTQH      Make new version available                   
*-                                                                              
*-       Release old printer table.                                             
*-                                                                              
         WHILE (R5,NZ),BEGIN       Freemain each entry...                       
         WITH  (PRT,R5)                                                         
         L     R4,PRTLINK                                                       
*                                                                               
         CLEAR PRT                 (Neatness)                                   
         FREEMAIN R,A=(R5),LV=L'PRT  Release printer entry                      
*                                                                               
         LR    R5,R4                                                            
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SPRTRTN -- Co-routine to process lines from the PRINTER file.                
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
SPRTRTN  PROC  ,                                                                
         L     R6,CPCWA                                                         
         USING SPRTWA,R6                                                        
*                                                                               
         LA    R5,SPRT                                                          
         USING PRT,R5                                                           
*                                                                               
         CEIL  R0,L'SPRTLINE                                                    
         LR    R15,R0                                                           
         MOVE  R15,SPRTLINE,@R1                                                 
         SCINIT SPRTLINE,(R0)      Input line                                   
*                                                                               
         SCAN SPRTPRT              Look for options                             
         COMMENT                   Ignore all errors                            
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*  PRINTER DEFINITIONS SCAN ROUTINES                                            
*                                                                               
SPRTPRT  SCKW  PRINTER,SPRTNAME,(A,P)                                           
         SCKW  ACCOUNT,SPRTACCT,(A,P)                                           
         SCKW  COMMAND,SPRTCMD,(A,P)                                            
         SCKW  KEYWORD,SPRTKEY,A                                                
         SCKW  PASSWORD,SPRTKEY,A                                               
         SCKW  NOKEYWORD,SPRTNKEY,A                                             
         SCKW  NOPASSWORD,SPRTNKEY,A                                            
         SCKW  END,SPRTEND                                                      
         SCKW  ,V(SCNNOOP),                                                     
*                                                                               
*                                                                               
SPRTNAME PROC  ,                                                                
         CLEAR PRT                                                              
         SCUPCASE PRTNAME                                                       
*                                                                               
         LA    R3,1                                                             
         LA    R4,PRTNAME+L'PRTNAME                                             
         SCAN                                                                   
         WHILE ((R15,Z),AND,(R0,POS)),BEGIN                                     
         SCUPTOK (R1)                                                           
         IF    (@R1,EQ,'BEGIN'),EXIT     End of printer list                    
         IF    (R3,GE,PRTMAX#),EXIT      Too many names                         
         MVC   @R4(L'PRTNAME),@R1                                               
         LA    R4,@R4+L'PRTNAME                                                 
         LA    R3,@R3+1                                                         
         SCAN  ,                                                                
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
*                                                                               
SPRTACCT PROC  ,                                                                
         IF    ((R0,EQ,6),AND,(@R1+2,EQ,'.')),BEGIN  Valid acct...              
         SCUPTOK (R1)                                                           
         MVC   PRTACCT(3),@R1+3   UUU                                           
         MVC   PRTACCT+3(2),@R1   GG                                            
         END                                                                    
         PEND                                                                   
*                                                                               
SPRTCMD  PROC                                                                   
         SCDQUOTE PRTCMD                                                        
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SPRTKEY  PROC  ,                                                                
         CLEAR PRTFNKEY                                                         
         PEND  ,                                                                
*                                                                               
SPRTNKEY PROC  ,                                                                
         SET   PRTFNKEY                                                         
         PEND  ,                                                                
*                                                                               
SPRTEND  PROC  ,                                                                
         GETMAIN R,LV=L'PRT                                                     
         LR    R2,R1               Newly getmained PRT entry ptr                
         MOVEL @R2,SPRT,L'PRT      Save new PRT entry                           
         IF    (SPRTQH,Z),'ST R2,SPRTQH; ST R2,SPRTQT'  First                   
         ELSE  BEGIN                                                            
         L     R15,SPRTQT                                                       
         WITH  (PRT,R15),'ST R2,PRTLINK'  Link us to last entry                 
         ST    R2,SPRTQT                                                        
         END                                                                    
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKPRT -- Routine to check the printer name to see if it's                   
*    one we know about.                                                         
*                                                                               
*    On entry:                                                                  
*      R15 - printer name loc (8 chars)                                         
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - unknown printer name                                                 
*      nz- printer name is known (R15=PRT loc)                                  
*                                                                               
CHKPRT   XPROC                                                                  
         LR    R5,R15                                                           
         LA    R6,CVPRTQH-(PRTLINK-PRT)                                         
*                                                                               
         CLEAR R15                                                              
         LOOP  BEGIN                                                            
         WITH  (PRT,R6)                                                         
         WHILE ('LT R6,PRTLINK',NZ)                                             
         CLEAR R2                                                               
         LA    R3,PRTNAME                                                       
         WHILE (R2,LT,PRTMAX#),BEGIN                                            
         IF    ('CLC @R3(8),@R5',EQ),BEGIN  Matched...                          
         LR    R15,R6                                                           
         B     CHKPEXIT                                                         
         END                                                                    
*                                                                               
         LA    R3,@R3+L'PRTNAME                                                 
         LA    R2,@R2+1                                                         
         END                                                                    
         END                                                                    
*                                                                               
CHKPEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SETPRT -- Routine to set logon information based on the printer              
*    entry.                                                                     
*                                                                               
*    On entry:                                                                  
*      R15 - PRT entry                                                          
*                                                                               
SETPRT   XPROC                                                                  
         LR    R6,R15                                                           
         USING PRT,R6                                                           
*                                                                               
         MVC   CPCACCT,PRTACCT     Set logon account                            
*                                                                               
         MVC   CPCMDLEN,=AL2(L'PRTCMD)                                          
         MVC   CPCMD,CVBLANKS                                                   
         MVC   CPCMD(L'PRTCMD),PRTCMD  Set first command                        
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         TITLE 'SET and SHOW LPRINTER commands'                                 
*box                                                                            
*                                                                               
*  SETLPR -- SET LPRINTER Command.                                              
*                                                                               
SETLPR   XPROC                                                                  
         SCINFO                                                                 
         VCALL LRTRIM              Save rest of command line                    
         IF    (R0,NEG),'CLEAR R0'                                              
         LA    R2,=CL16'SYS.LPRINTER'  Save under this name                     
         VCALL SAVEDATA            Save it                                      
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWLPR -- SHOW LPRINTER command.                                            
*                                                                               
SHOWLPR  XPROC                                                                  
         VCALL COLLOPTS                                                         
*                                                                               
         LCALL LPRDISP                                                          
         B     CVNEXT                                                           
         PEND                                                                   
*box                                                                            
*                                                                               
*  LPRDISP -- SHOW LPRINTER subroutine (also called by SHOW                     
*     OPTIONS).                                                                 
*                                                                               
LPRDISP  XPROC                                                                  
         XPUSH ,,&LINESZ,PTR=R1                                                 
         LA    R0,&LINESZ                                                       
         LA    R2,=CL16'SYS.LPRINTER'                                           
         VCALL GETDATA             Get lprinter info                            
         IF    (NZ,OR,(R0,Z)),BEGIN                                             
         TSEG  'No local printer type set.'                                     
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  (R1),(R0)                                                        
         TSEG  ' - local printer.'                                              
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'RJE Work Area'                                                  
*                                                                               
RJEWA    RECORD BEGIN                                                           
RJECMD   FLAG                                                                   
         FLAG  (RJECFPRI,1,EQ)     - Print command                              
         FLAG  (RJECFPUN,2,EQ)     - Punch command                              
         FLAG  (RJECFMAI,3,EQ)     - Print mail                                 
         FLAG  (RJECFRUN,4,EQ)     - Run                                        
         FLAG  (RJECFCOND,5,EQ)    - Condense                                   
*                                                                               
RJEFLAG  FLAG                                                                   
         FLAG  RJEFHOLD            - Hold output                                
         FLAG  RJEFXHLD            - Hold execution                             
         FLAG  RJEFNTFY            - Notify                                     
         FLAG  RJEFDUP             - Duplex                                     
         FLAG  RJEFNDUP            - NoDuplex                                   
         FLAG  RJEFDUMB            - DUMB option (Sys priv'd)                   
         FLAG  RJEFSPEC            - Special option (submit priv'd)             
*                                                                               
RJEFLAG2 FLAG                                                                   
         FLAG  RJEFNTFX            - Notify option explicit                     
         FLAG  RJEFHOLX            - Print hold option explicit                 
         FLAG  RJEFXHOX            - Execution hold option explicit             
         FLAG  RJEFTRC             - TRC (char select) chars present            
         FLAG  RJEFPMAI            - PUNCH mail (gateway processing)            
         FLAG  RJEFPBSMTP          - BSMTP format                               
         FLAG  RJEFPSUNMAIL        - SUNMAIL format                             
*                                                                               
RJEFLGP  FLAG                                                                   
         FLAG  RJEFCCS             - CC option explicit                         
         FLAG  RJEFCC              - CC                                         
         FLAG  RJEFMC              - MC                                         
         FLAG  RJEFBACK            - Back                                       
         FLAG  RJEFFMT             - Formatted                                  
         FLAG  RJEFDOUB            - Double                                     
         FLAG  RJEFTRIP            - Triple                                     
*                                                                               
RJEFLGP2 FLAG                                                                   
         FLAG  RJEFDOC             - Document                                   
         FLAG  RJEFMEMO            - Memo                                       
         FLAG  RJEFHEX             - Hex                                        
         FLAG  RJEFMIX             - Mixed                                      
         FLAG  RJEFNSEP            - NoSep                                      
         FLAG  RJEFMAN             - Manual                                     
         FLAG  RJEFFMAN            - FManual                                    
*                                                                               
RJEFLGP3 FLAG                                                                   
         FLAG  RJEFLPR             - LPRINT                                     
         FLAG  RJEFEXT             - PRINT TO user@dest                         
         FLAG  RJEFBRST            - Burst                                      
         FLAG  RJEFKEEP            - Keep                                       
         FLAG  RJEFFAX             - Fax command                                
*                                                                               
RJEFLGP4 FLAG                                                                   
         FLAG  RJENOLOG            - NOLOG                                      
         FLAG  RJENOJCL            - NOJCL                                      
*                                                                               
RJEFLAGX FLAG                                                                   
         FLAG  RJEFOPEN            - Submit path is open                        
         FLAG  RJEFOSCN            - Option line scan                           
         FLAG  RJEFSOME            - First line has been sent                   
*                                                                               
RJEXFL   FLAG  ,                   Print formatting options:                    
         FLAG  RJEXFUND            - Underlining                                
         FLAG  RJEXFBLD            - Bold                                       
         FLAG  RJEXFITL            - Italics                                    
*                                                                               
RJENACCT DS    CL5                 Notify account                               
*                                                                               
RJEJCLS  DS    C                   Job class                                    
RJEBLOCK DS    C                   Time block                                   
RJEOBLCK DS    C                   Output time block                            
RJEPRIO  DS    C                   Priority                                     
RJEOPRIO DS    C                   Output priority                              
RJESYSCL DS    C                   Sysout class                                 
RJESYSAF DS    CL4                 Sysaff name                                  
RJEPFORM DS    CL6                 Format                                       
RJEMARK  DS    C                   Marker char                                  
RJENAME  DS    CL8                 Job name                                     
RJEPFX   DS    CL8                 Prefix                                       
RJEID    DS    CL8                 Id                                           
RJEDEST  DS    CL8                 Destination                                  
RJEBIN   DS    CL3                 Bin                                          
RJEFORMS DS    CL8                 Forms                                        
RJEFLASH DS    CL4                 Flash                                        
RJEFCB   DS    CL4                 Fcb (carriage)                               
RJEPRINT DS    CL8                 Printer                                      
RJECHARS DS    CL(4*4)             Character sets                               
RJECHAR0 EQU   RJECHARS,4,C'C'                                                  
RJECHAR1 EQU   RJECHARS+4,4,C'C'                                                
RJECHAR2 EQU   RJECHARS+8,4,C'C'                                                
RJECHAR3 EQU   RJECHARS+12,4,C'C'                                               
RJECHARM EQU   RJECHARS+4,3*4,C'C'                                              
RJETITLE DS    CL128               Title                                        
RJEHEAD  DS    0CL20               Header text (for JOB card)                   
RJEHEADL DS    CL128               Long header text (for FAX cmd)               
RJEMAIL  DS    CL128               Id mail text                                 
RJESEND  DS    CL180               Bitnet/fax sender id                         
RJEFXRCP DS    CL128               Fax recipient text                           
RJEFXNOT DS    CL64                Fax email notify address                     
RJEFXAUT DS    CL7                 Fax phone auth code                          
RJECOPY  DS    H                   Copies                                       
RJEIND   DS    H                   Indent (-1 = indent=0)                       
RJEWRAP  DS    H                   Wrap count (-1 = wrap=0)                     
RJEDARK  DS    H                   Dark count                                   
RJEEJECT DS    H                   Eject count (-1 = noeject)                   
*                                                                               
RJETOACC DS    CL5                 uuugg (for directed print)                   
RJETO    DS    CL128               User name (for PRINT TO)                     
*                                                                               
RJEFLAGS FLAG                                                                   
         FLAG  RJEFSNT             - Control cards sent      (RUN)              
         FLAG  RJEFDDS             - Processing dd *         (RUN)              
         FLAG  RJEFCTRL            - Sending control lines   (PRINT)            
*                                                                               
RJEJOBNM DS    CL8                 Jobname                                      
RJEACCT  DS    CL7                 Submission account (uuuggss)                 
RJEAUSR  EQU   RJEACCT,3,C'C'      .uuu                                         
RJEAGRP  EQU   RJEACCT+3,4,C'C'    .ggss                                        
*                                                                               
RJEKJFL  DS    X                   KWRJFL              (from KWR)               
RJEKPFX  DS    CL8                 Job prefix          (from KWR)               
RJEKJNO  DS    H                   Job count           (from KWR)               
RJEKDEST DS    CL8                 Our destination     (from KWR)               
RJEKBIN  DS    CL3                 Our bin             (from KWR)               
RJEKLLIM DS    F                   Printed line limit  (from KWR)               
RJEKPLIM DS    F                   Punched line limit  (from KWR)               
*                                                                               
RJELCNT  DS    F                   Current line count                           
RJEPCNT  DS    F                   Current punch count                          
*                                                                               
RJEDELIM DS    CL2                 DD data delimiter                            
RJENEWCC DS    C                   New CC/MC char to use                        
RJESWA   EQU   RJEFLAGS,*-RJEFLAGS,C'X'  Area cleared after each job            
*                                                                               
RJESG    SEGCB                                                                  
*                                                                               
RJESGBUF DS    CL(&LINESZ)                                                      
RJELINE  DS    CL(&LINESZ)         READ RANGE INPUT BUFFER                      
         END                                                                    
         TITLE 'CONDENSE Command'                                               
*box                                                                            
*                                                                               
*  CONDENSE -- CONDENSE command.  (Note that CPCMNM is set                      
*    to "C*" if entered because pds is full.)                                   
*                                                                               
*  If wings found pds full, we will get a 'savefailed' option                   
*  on the condense command.                                                     
*                                                                               
*  NOTE:                                                                        
*  CONDENSE IS FORCED TO OLDIBM.                                                
*  WINGS WILL/MUST USE OTHER CODE/METHOD FOR CONDENSE.                          
*                                                                               
CDWA     RECORD BEGIN                                                           
CDRJEWA  DS    XL(L'RJEWA)                                                      
CDFLAGS  FLAG                                                                   
         FLAG  CDFSFAIL            PDS FULL, SAVE FAILED                        
         END                                                                    
*                                                                               
*                                                                               
CONDENSE XPROC CDWA                                                             
         LA    R6,CDRJEWA                                                       
         USING RJEWA,R6                                                         
         CLEAR RJEWA                                                            
         ST    R6,CPWK5                                                         
         LA    R4,CDWA                                                          
         USING CDWA,R4                                                          
         CLEAR CDFLAGS                                                          
*                                                                               
         SET   RJECFCOND           Condense command                             
         SET   RJEFHOLD,RJEFHOLX   Assume print hold                            
         SET   RJEFNTFY,RJEFNTFX   Assume notify                                
         MVC   RJENACCT,CPJNACCT   Notification account                         
         SET   CPLFLG5.CPFNLST     Assume nolist                                
         MVC   RJENAME(3),CPSUSER                                               
         MVC   RJENAME+3(5),=C'COND '  Set jobname                              
         MVC   RJEHEAD(20),=CL20'CONDENSE'  Header                              
         MVI   RJEJCLS,C'F'        Assume CLASS=F                               
*                                                                               
         SEGINIT RJESGBUF,71,RJESG,RTN=RSEG                                     
*                                                                               
         IF    (CPCMNM,NE,'C*'),BEGIN  Command...                               
         VCALL MKFFINFO                                                         
         VCALL DOFNAME             Get dsname                                   
         SCAN  CONDPRT                                                          
         SCANCHK                                                                
         COMMENT                   FORCE TO OLDIBM FOR CONDENSE                 
         L     R3,CPFINFOP                                                      
         WITH  (FFINFO,R3),' SET FFFOLD '                                       
         VCALL FNAMEFIN            Finish up dsname processing                  
         IF    CPLFLG3.CPFSET,'VCALL SETPFXNS'                                  
         IF    ^CDFSFAIL,BEGIN     Failed sav #xxx already asked                
         IF    ^DSNWAF1.DSNFMYDS,BEGIN  Not yours...                            
         SET   CPFKOK                                                           
         VCALL DSNAUTH             Just ask if "OK?"                            
         END                                                                    
         END                                                                    
         END                                                                    
         ELSE  BEGIN               Entered because pds full...                  
         SCAN  CONDPRT1            Allow rje options                            
         SCANCHK                                                                
         END                                                                    
*-                                                                              
*-       If we are condensing someone else's library then we need               
*-         to run the job under the other person's account so that              
*-         the security system will allow the file access.                      
*-                                                                              
         IF    DSNWAF1.DSNFSTD,BEGIN  Wylbur data set...                        
         IF    ((CPSGRP,EQ,DSNWAGRP),AND,(CPSUSER,EQ,DSNWAUSR)),EXIT            
         MVC   RJEACCT,CVBLANKS    Pre-blank                                    
         MVC   RJEACCT(3),DSNWAUSR    "uuu"                                     
         MVC   RJEACCT+3(2),DSNWAGRP  "gg"                                      
         END                                                                    
*                                                                               
         XPUSH ,,44,PTR=R5                                                      
         MVC   @R5(44),CVBLANKS    Blank a dsn area                             
         LH    R1,DSNWANL          Load dsn length                              
         DEX   R1,'MVC @R5(0),DSNWADSN' Move in dsn                             
         SETMSG '.'                 Point to a low member name                  
         LR    R15,R5              Point to dsn                                 
         XPOP  ,,44                                                             
         ACALL JOBOPEN             Get submit path                              
         CLEAR R1                  Build job card                               
         ACALL JOBCARD                                                          
         ACALL JOBCNTL                                                          
*        SEGWR '// EXEC CONDENSE'                                               
*        SEG   '//SYSUT1 DD DSN='''                                             
         SEG   '// EXEC CONDENSE,DSN='''                                        
         SEGT  DSNWADSN,LH:DSNWANL                                              
         SEG   ''''                                                             
         IF    (DSNON,NZ),BEGIN    Give volser also...                          
*        SEGWR ','                                                              
*        SEG   '// VOL=SER='                                                    
         SEG   ',VOL='                                                          
         SEGT  DSNON                                                            
         END                                                                    
         SEGWR                                                                  
         IF    CPLFLG5.CPFNLST,'SEGWR "// EXEC NOLIST"'                         
         ACALL JOBCLOSE            Close path & locate job                      
*                                                                               
         IF    CDFSFAIL,BEGIN      Wings save #xxx failed, pds full             
         ERROR 'Library member not saved.'                                      
         END                                                                    
         IF    (CPCMNM,NE,'C*'),CVNEXT  Scram, if condense command              
         TSEG  'Member',,B                                                      
         VCALL MEMBERED                                                         
         ERROR 'not saved.'                                                     
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
CONDPRT  SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  SAVEFAILED,CONDNSAV                                              
CONDPRT1 SCKW  ,RJEPSH,PUSH                                                     
         SCKW  ,V(LISTPSH),PUSH                                                 
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
CONDNSAV PROC  ,                                                                
         SET   CDFSFAIL                                                         
         PEND                                                                   
         DROP  R4                                                               
         QLTORG                                                                 
         TITLE 'RUN Command'                                                    
*box                                                                            
*                                                                               
*  RUN -- RUN command.                                                          
*                                                                               
RUN      XPROC RJEWA                                                            
         LA    R6,RJEWA                                                         
         USING RJEWA,R6                                                         
         CLEAR RJEWA                                                            
         ST    R6,CPWK5                                                         
         SET   RJECFRUN                                                         
         SEGINIT RJESGBUF,,RJESG,RTN=RSEG                                       
         MVI   RJEDELIM,X'FF'      First line must be a job card                
******** IF    CPJFHOLD,'SET RJEFHOLD'                                          
******** IF    CPJFXHLD,'SET RJEFXHLD'                                          
******** IF    CPJFNTFY,'SET RJEFNTFY'                                          
         MVC   RJENACCT,CPJNACCT   Defalut notification account                 
         VCALL SCNEXFR             Allow active, exec, or external              
         SET   CPLFLG1.CPFALL      Allow default of ALL                         
         SET   CPFNCUR             Don't update current line ptr                
         VCALL NDETRNG                                                          
         IF    CPFRDEXT,BEGIN                                                   
         VCALL MKFFINFO                                                         
         SCAN  RUNPRTF                                                          
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  RUNPRT                                                           
         SCANCHK                                                                
         END                                                                    
*                                                                               
         ACALL RUNOPT              Do final option checking                     
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         L     R3,CPFINFOP                                                      
         USING FFINFO,R3                                                        
         IF    (FFFSAM),BEGIN                                                   
         ERROR 'RUN FROM <samson file> not allowed.'                            
         END                                                                    
         DROP  R3                                                               
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         RDRNG RUNRTN,(DESRTRN+LXCATRTN+UNPRST)                                 
         VCALL EXTCLOSE                                                         
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN                                                  
         RDRNG RUNRTN,(DESRTRN+LEXATRTN+UNPRST)                                 
         END                                                                    
         ELSE  BEGIN                                                            
         RDRNG RUNRTN,(DESRTRN+LOCATRTN+UNPRST)                                 
         END                                                                    
         ACALL JOBCLOSE            Close path & locate job                      
         B     CVNEXT                                                           
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RUNMSG -- Routine to run a BATMAIL job.                                      
*    Called by MAIL to send a mail message as a job to another                  
*    machine.                                                                   
*                                                                               
*    On entry:                                                                  
*      R1 - Header subroutine addr                                              
*      R15- Co-routine addr (Co-routine will be called to get the               
*                            next line.)                                        
*                                                                               
*    ** DEBUG **                                                                
*    old scanner points to 'print command'                                      
*    we make new scanner point to it. we must                                   
*    eventually change WYLMAIL to use new scanner                               
*    or delete this routine !                                                   
*                                                                               
*                                                                               
RUNMSG   XPROC RJEWA                                                            
         LA    R6,RJEWA                                                         
         USING RJEWA,R6                                                         
         CLEAR RJEWA                                                            
         ST    R6,CPWK5                                                         
*                                                                               
         COMMENT ,                 ** DEBUG ** SET UP NEW SCANNER               
         XPUSH R0,R1                                                            
         OSCINFO                                                                
         SCINIT (R1),(R0)                                                       
         XPOP  R0,R1                                                            
*                                                                               
         LR    R4,R1               Save header routine addr                     
         LR    R5,R15              Save co-routine addr                         
*                                                                               
         MVC   RJENAME,=C'BATMAIL ' Set jobname                                 
         MVC   RJEACCT,=C'WYLGS  ' Use account GS.WYL                           
         SET   RJEFSPEC            Allow non-user account                       
         MVC   RJEHEAD,=CL20'Batmail'  Header                                   
*                                                                               
         SCAN  RJEPRT              Allow rje options                            
         SCANCHK                                                                
*                                                                               
         SEGINIT RJESGBUF,,RJESG,RTN=RSEG                                       
*-                                                                              
*-       Send initial JCL.                                                      
*-                                                                              
         ACALL JOBOPEN             Get submit path                              
         CLEAR R1                  Build job card                               
         ACALL JOBCARD                                                          
         ACALL JOBCNTL                                                          
         SEGWR '/*JOBPARM LINES=50'  Allow 50,000 lines out output              
         SEGWR '// EXEC BATMAIL,OPT=''ESC=%,OUT=%'''                            
         SEGWR '//SYSIN DD *,DCB=LRECL=150'                                     
*-                                                                              
*-       Build "XCALL ...#BATMAILX" command.                                    
*-                                                                              
         SEG   'XCALL '                                                         
         LA    R0,SDSNBATM         Want lib with BATMAIL exec                   
         VCALL EXIT0               LOCAL exit - Supply system dsname            
         SEG   (R1),(R0)           SEG out dsname                               
         SEGWR '#BATMAILX'         Finish up with BATMAIL member                
*-                                                                              
*-       Send header lines.                                                     
*-                                                                              
         LA    R15,RJESG                                                        
         BASR  RAR,R4              Send header lines                            
*-                                                                              
*-       Send main text.                                                        
*-                                                                              
RUNMLL   BASR  RAR,R5              Go get line                                  
         IF    NZ,BEGIN            We have the line...                          
         XPUSH R0,R1                                                            
         SEG   ';DATA:'            Data line follows                            
         XPOP  R0,R1                                                            
         SEGWR (R1),(R0)           Write line of text                           
         B     RUNMLL                                                           
         END                                                                    
*-                                                                              
*-       Send final line and remaining JCL.                                     
*-                                                                              
         SEGWR ';END:'             Input ends with this line                    
         SEGWR 'LOGOFF CLEAR'                                                   
         SEGWR '//* (End of BATMAIL)'                                           
*                                                                               
         ACALL JOBCLOSE                                                         
         PEND                                                                   
         EJECT                                                                  
RUNPRTF  SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  FORMS,FORMS,(A,P)   Dup--ahead of "DSNPRTF" table                
         SCKW  ,V(RNUMPSH),PUSH    Unn--ahead of "FNAMEPSH" table               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
RUNPRT   SCKW  ACTIVE,V(SCNNOOP),A       (Handled elsewhere)                    
         SCKW  EXECUTE,V(SCNNOOP),A      (Handled elsewhere)                    
         SCKW  CHECK,V(SCNNOOP),A        Ignored (compatability)                
         SCKW  CHK,V(SCNNOOP)            Ignored (compatability)                
         SCKW  SPECIAL,RUNSPEC,A                                                
         SCKW  DUMB,RUNDUMB,A                                                   
         SCKW  N,V(NOTVALID)             Don't allow N = NOLIST                 
         SCKW  ,V(LISTPSH),PUSH                                                 
         SCKW  ,V(RNUMPSH),PUSH                                                 
         SCKW  ,RJEPSH,PUSH                                                     
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
RUNDUMB  PROC  ,                                                                
         IF    ^CPSFSPR,'VCALL NOTVALID'  NEED SYSTEM PRIVS                     
         SET   RJEFDUMB                                                         
         PEND                                                                   
*                                                                               
RUNSPEC  PROC  ,                                                                
         SET   RJEFSPEC                                                         
         IF    ^CPSFSBM,'VCALL NOTVALID'  NEED SUBMIT PRIVS                     
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RUNOPT -- Routine to do final run option setting and                         
*    checking for inconsistencies.                                              
*                                                                               
RUNOPT   PROC                                                                   
         L     R6,CPWK5                                                         
         USING RJEWA,R6                                                         
         IF    CPLFLG5.CPFNLST,'MVC RJECOPY,=H"-1"'                             
*-                                                                              
*-       Directed run.                                                          
*-                                                                              
         IF    (RJETOACC,NZ),BEGIN  Directed run...                             
         XPUSH ,,L'KWR,PTR=R3                                                   
         WITH  (KWR,R3)                                                         
         MVC   KWRUSER(5),RJETOACC                                              
         KREAD KWR                                                              
         IF    ^KWRHFL.KWRHFCK,EXIT  No kw checking                             
         IF    ^KWRIFL.KWRIFVAL,BEGIN                                           
         TSEG  'Account',,B                                                     
         TSEG  RJETOACC+3,2                                                     
         TSEG  '.'                                                              
         TSEG  RJETOACC,3,B                                                     
         ERROR 'does not exist.',,BADACCT                                       
         END                                                                    
*                                                                               
         IF    (RJEDEST,Z),'MVC RJEDEST,KWRDEST'                                
         IF    (RJEBIN,Z),'MVC RJEBIN,KWRBIN'                                   
         IF    (RJEFORMS,Z),BEGIN                                               
         MVC   RJEFORMS,CVBLANKS                                                
         MVC   RJEFORMS(L'KWRFORM),KWRFORM                                      
         END                                                                    
         CLEAR KWR                                                              
         XPOP  ,,L'KWR                                                          
         END                                                                    
*                                                                               
         IF    (RJESYSAF,Z),'MVC RJESYSAF,CPSYSAFF'  Sysaff                     
         IF    (RJEDEST,Z),'MVC RJEDEST,CPDEST'                                 
         IF    (RJEBIN,Z),'MVC RJEBIN,CPBIN'                                    
         IF    (RJEFORMS,Z),'MVC RJEFORMS,CPFORMS'                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RUNRTN -- Entered from the range processor with line loc in R1               
*    and len in R0.                                                             
*                                                                               
RUNRTN   PROC                                                                   
         L     R6,CPWK5            Rjewa ptr                                    
         USING RJEWA,R6                                                         
*                                                                               
         COMMENT                   Move line into blank padded area             
         LA    R2,RJELINE                                                       
         LA    R3,L'RJELINE                                                     
         LR    R4,R1                                                            
         L     R5,=X'40000000'                                                  
         OR    R5,R0                                                            
         MVCL  R2,R4                                                            
         LA    R1,RJELINE                                                       
*                                                                               
         LR    R3,R1                                                            
         LR    R2,R0                                                            
*                                                                               
         IF    RJEFDUMB,BEGIN      Submit lines intact...                       
         IF    (RJEDELIM,Z),RUNWR  Not the first time, branch                   
         CLEAR RJEDELIM                                                         
         ACALL JOBOPEN             Open submit path                             
         B     RUNWR               Write lines unchanged                        
         END                                                                    
*                                                                               
         ACALL JOBCRDCK            Is this a job card?                          
         IF    POS,BEGIN           New (or first) job...                        
         ACALL JOBCLOSE            Close & do locate                            
         CLEAR RJESWA              Reset submit stuff                           
         ACALL JOBOPEN             Open submission path                         
         SETMSG (R3),(R2)                                                       
         ACALL JOBCARD             Verify & send job card                       
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (R15,M),'ACALL JOBCNTL'  Send control cards                      
         IF    ^CPLFLG5.CPFUNUM,BEGIN                                           
         IF    CPNFRNUM,'CEIL R2,72'  Replace with line-number                  
         IF    (R2,GT,72),EXIT     Don't overlay text                           
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         MVC   @R3+72(8),CPDOUB+1  Move in seq field                            
         LA    R2,80                                                            
         END                                                                    
*-                                                                              
*-      We always pad a JCL line to 80 characters because JES2 looks            
*-         at this length to set the LRECL of "DD *" input.                     
*-                                                                              
RUNWR    IF    ((R2,GE,2),AND,(@R3,EQ,'//')),'FLOOR R2,80'  Pad                 
*                                                                               
         SEG   (R3),(R2)                                                        
         SEGWR                                                                  
         END                                                                    
         PEND                                                                   
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'PRINT/PUNCH Commands'                                           
*box                                                                            
*                                                                               
*  PRINT -- PRINT/PUNCH command.                                                
*                                                                               
PRINT    XPROC RJEWA                                                            
         LA    R6,RJEWA                                                         
         USING RJEWA,R6                                                         
         CLEAR RJEWA                                                            
         ST    R6,CPWK5                                                         
*                                                                               
         SEGINIT RJESGBUF,,RJESG,RTN=RSEG                                       
         SET   RJECFPRI            Assume print command                         
         SET   RJEFFMT             Default is formatted                         
*                                                                               
         IF    (CPCMNM,EQ,'PUN'),BEGIN  PUNCH command defaults...               
         SET   RJECFPUN            Punch                                        
         SET   CPLFLG5.CPFNLST     Nolist                                       
         CLEAR RJEFFMT             Unformatted                                  
         END                                                                    
*                                                                               
         IF    (CPCMNM,EQ,'LPR'),'SET RJEFLPR'  LPRINT                          
*                                                                               
         IF    (CPCMNM,EQ,'_FA'),'SET RJEFFAX'  _FAX command                    
*                                                                               
******** IF    CPJFNTFY,'SET RJEFNTFY'                                          
         VCALL SCNEXFR             Allow active, exec, or external              
         SET   CPLFLG1.CPFALL      Allow default of ALL                         
         SET   CPFNCUR             Don't update current line ptr                
         VCALL NDETRNG                                                          
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL MKFFINFO                                                         
         SCAN  PRINTPRF                                                         
         SCANCHK                                                                
         END                                                                    
         ELSE  BEGIN                                                            
         SCAN  PRINTPRT                                                         
         SCANCHK                                                                
         END                                                                    
*                                                                               
         COMMENT                   HELP ** DEBUG ** REMOVE DSN OPTS             
         IF    (DSNFUFMT,OR,CPFUFMT,OR,^RJEFFMT),BEGIN Unfmt...                 
         SET   DSNFUFMT,CPFUFMT,(RJEFFMT,OFF)                                   
         END                                                                    
*                                                                               
         IF    CPFRDEXT,BEGIN                                                   
         VCALL FNAMEFIN                                                         
         L     R3,CPFINFOP                                                      
         USING FFINFO,R3                                                        
         IF    (FFFSAM),BEGIN                                                   
         ERROR 'RUN FROM <samson file> not allowed.'                            
         END                                                                    
         DROP  R3                                                               
         VCALL EXTOPEN                                                          
         IF    (R15,NZ),CVNEXT                                                  
         RDRNG PRINTRTN,(DESRTRN+LXCATRTN+UNPRST)                               
         VCALL EXTCLOSE                                                         
         END                                                                    
         ELSEIF CPFRDEXE,BEGIN                                                  
         RDRNG PRINTRTN,(DESRTRN+LEXATRTN+UNPRST)                               
         END                                                                    
         ELSE  BEGIN                                                            
         RDRNG PRINTRTN,(DESRTRN+LOCATRTN+UNPRST)                               
         END                                                                    
         ACALL JOBCLOSE            Close path & locate job                      
         B     CVNEXT                                                           
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTMSG -- Routine to print a message.                                      
*                                                                               
*    On entry:                                                                  
*      R0 - Options...  0=normal                                                
*                       1=LPRINT                                                
*                       2=inter-network mail                                    
*                       3=BSMTP network mail                                    
*                       4=SUNMAIL network mail                                  
*      R1 - Header subroutine addr                                              
*      R15- Co-routine addr (Co-routine will be called to get the               
*                            next line.)                                        
*    ** DEBUG **                                                                
*    old scanner points to 'print command'                                      
*    we make new scanner point to it. we must                                   
*    eventually change WYLMAIL to use new scanner                               
*    or delete this routine !                                                   
*                                                                               
*                                                                               
PRINTMSG XPROC RJEWA                                                            
         LA    R6,RJEWA                                                         
         USING RJEWA,R6                                                         
         CLEAR RJEWA                                                            
         ST    R6,CPWK5                                                         
*                                                                               
         COMMENT ,                 ** DEBUG ** SET UP NEW SCANNER               
         XPUSH R0,R1                                                            
         OSCINFO                                                                
         SCINIT (R1),(R0)                                                       
         XPOP  R0,R1                                                            
*                                                                               
         LR    R3,R0               Save options                                 
         LR    R4,R1               Header subroutine addr                       
         LR    R5,R15              Save co-routine addr                         
*                                                                               
         MVC   RJENAME(3),CPSUSER                                               
         MVC   RJENAME+3(5),=C'MAIL '                                           
         SEGINIT RJESGBUF,,RJESG,RTN=RSEG                                       
         SET   RJECFMAI            Print mail                                   
*                                                                               
         IF    (R3,LT,2),'SET RJEFFMT'  Formatted (if not netmail)              
*                                                                               
         IF    (R3,EQ,1),'SET RJEFLPR'  LPRINT option                           
*-                                                                              
*-       This is to handle mail destined for an internet gateway.               
*-         It will go as a PRINT job so that JES will make class M              
*-         instead of B, but lines will be limited to 80 char as                
*-         for punch.                                                           
*-                                                                              
         IF    ((R3,EQ,2),OR,(R3,EQ,3)),BEGIN  NETMAIL...                       
         SET   RJEFPMAI            This is punched mail                         
         IF    (R3,EQ,3),'SET RJEFPBSMTP'  BSMTP mail                           
         END                                                                    
*                                                                               
         IF    (R3,EQ,4),'SET RJEFPSUNMAIL'  SUNMAIL mail                       
*                                                                               
         SCAN  PRINTPRT                                                         
         IF    (R15,NZ),BEGIN                                                   
         L     R3,CPNSCANP                                                      
         USING NSCNCB,R3                                                        
         TSEG  NSCNMSG,L:NSCNMSGL                                               
         DROP  R3                                                               
         B     PRINTMX                                                          
         END                                                                    
*                                                                               
         SET   (CPLFLG5.CPFFOPT,OFF),CPLFLG5.CPFUNUM  Unnumbered                
*                                                                               
         CLEAR R0,R1               Dummy first line                             
         ACALL PRINTJCL            Build print job                              
*                                                                               
         SET   RJEFSOME            JCL has been sent (see PRINTRTN)             
*-                                                                              
*-       Generate appropriate header lines.                                     
*-                                                                              
         IF    (R4,NZ),BEGIN       Call header subroutine...                    
         LA    R15,RJESG                                                        
         BASR  RAR,R4              Call header subroutine                       
         END                                                                    
*-                                                                              
*-       Print main text.                                                       
*-                                                                              
PRINTMLL BASR  RAR,R5              Go get line                                  
         IF    NZ,BEGIN            Print line...                                
         ACALL PRINTRTN            Send line to be printed                      
         B     PRINTMLL                                                         
         END                                                                    
*                                                                               
         IF    RJEFPBSMTP+RJEFPSUNMAIL,BEGIN  BSMTP/SUNMAIL...                  
         CLEAR RJEFPBSMTP+RJEFPSUNMAIL  Reset (so '.' not doubled)              
         SETMSG '.'                 Add '.' line                                
         ACALL PRINTRTN                                                         
         SETMSG 'QUIT'              Add 'QUIT' line                             
         ACALL PRINTRTN                                                         
         END                                                                    
*                                                                               
         ACALL JOBCLOSE                                                         
*                                                                               
PRINTMX  LABEL ,                                                                
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTJCL -- Routine to generate print job JCL.  This routine                 
*    also does the final print option checking.                                 
*                                                                               
         USING RJEWA,R6                                                         
PRINTJCL PROC                                                                   
*-                                                                              
*-       Build PRINT job.                                                       
*-                                                                              
         ACALL PRINTOPT            Finish up option setting                     
*                                                                               
         ACALL JOBOPEN             Get submit path                              
         CLEAR R1                                                               
         ACALL JOBCARD             Build & send job card                        
         INCR  R15,CVNPRINT        Count no. of prints                          
         ACALL OPTLINE             Send option comment card                     
         ACALL JOBCNTL             Send control cards                           
*                                                                               
         SET   RJEFCTRL            We are sending control lines                 
         SEG   '/*LIST '                                                        
*                                                                               
         SETMSG 'CC'                                                            
         IF    RJEFMC,'SETMSG "MC"'                                             
         SEG   (R1),(R0)           CC or MC                                     
*                                                                               
         IF    (RJEFTRC,OR,RJEFFMT),'SEG ",TRC=Y"'                              
*                                                                               
         IF    RJECFPUN,BEGIN                                                   
         SEG   ',PUNCH'                                                         
         END                                                                    
*                                                                               
         SEGWR                                                                  
         CLEAR RJEFCTRL            End of control lines                         
*                                                                               
         MVI   RJENEWCC,C'1'       Start at first possible line                 
         IF    (RJETITLE,NZ),BEGIN  Print a title line...                       
         IF    RJECFPUN,EXIT                                                    
         IF    (RJEPFORM,EQ,'POST  '),EXIT  Postscript                          
         IF    RJEFMC,'SEGWR X"8B"; MVI RJENEWCC,X"01"'                         
         SEG   RJENEWCC                                                         
         IF    RJEFTRC,'SEGB'                                                   
         IF    CPLFLG5.CPFNONUM,'SEG CVBLANKS,11'                               
*                                                                               
         IF    (RJETOACC,NZ),BEGIN  Print to another account...                 
         VCALL LOCALTOD                                                         
         XPUSH ,,32,PTR=R15                                                     
         VCALL FMTCLCK                                                          
         SEG   (R1),20             "hh:mm:ss mm/dd/yyyy "                       
         XPOP  ,,32                                                             
         SEGB  'FROM'                                                           
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
         IF    (CPNAME,NZ),BEGIN                                                
         SEG   ' "'                                                             
         SEGT  CPNAME                                                           
         SEG   '"'                                                              
         END                                                                    
         SEGB  ':'                                                              
         END                                                                    
*                                                                               
         SETMSG RJETITLE                                                        
         ACALL BACKSEG                                                          
         SEGWR                                                                  
         MVI   RJENEWCC,C'-'                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTOPT -- Routine to do final print option setting and                     
*    checking for inconsistencies.                                              
*                                                                               
         USING RJEWA,R6                                                         
PRINTOPT PROC  ,                                                                
         CLEAR R15                                                              
         IF    RJEFCC+RJEFMC,'LA R15,@R15+1'                                    
         IF    RJEFTRC,'LA R15,@R15+1'                                          
         IF    (R15,GT,CPFCOL),'STH R15,CPFCOL'  skip cc/mc/cs chars            
*                                                                               
         IF    (^RJECFPUN,AND,CPLFLG5.CPFNLST),BEGIN                            
         ERROR 'NOLIST option is invalid for PRINT.'                            
         END                                                                    
*                                                                               
         IF    ((RJEBLOCK,NZ),AND,(RJEOBLCK,Z)),BEGIN                           
         MVC   RJEOBLCK,RJEBLOCK   Output time block                            
         MVI   RJEBLOCK,C' '       No execution time block                      
         END                                                                    
*                                                                               
         IF    ((RJEPRIO,NZ),AND,(RJEOPRIO,Z)),BEGIN                            
         MVC   RJEOPRIO,RJEPRIO    Output priority                              
         MVI   RJEPRIO,C' '        No execution priority                        
         END                                                                    
*                                                                               
         IF    RJEFXHLD,'SET RJEFHOLD,RJEFHOLX' (Xeq hold ineffective)          
*                                                                               
         IF    RJEFDOC,BEGIN       Document option...                           
         IF    ^CPLFLG5.CPFFOPT,'SET CPLFLG5.CPFNONUM'  Nonumber                
         IF    (RJEFORMS,Z),BEGIN                                               
         MVC   RJEFORMS,=CL(L'RJEFORMS)'&DOCFORM'                               
         END                                                                    
         IF    (RJESYSCL,Z),'MVI RJESYSCL,C"&DOCCLAS"'  Uplow                   
         IF    RJEFMEMO,BEGIN      Memo has more...                             
         IF    (RJEFLASH,Z),'MVC RJEFLASH,=C"MEMO"'     Flash=MEMO              
         IF    (RJEFCB,Z),'MVC RJEFCB,=C"MEM6"'         Fcb=MEM6                
         END                                                                    
         END                                                                    
*                                                                               
         IF    (RJETOACC,NZ),BEGIN  Directed print/punch...                     
         XPUSH ,,L'KWR,PTR=R3                                                   
         WITH  (KWR,R3)                                                         
         MVC   KWRUSER(5),RJETOACC                                              
         KREAD KWR                                                              
         IF    ^KWRHFL.KWRHFCK,EXIT  No kw checking                             
         IF    ^KWRIFL.KWRIFVAL,BEGIN                                           
         TSEG  'Account',,B                                                     
         TSEG  RJETOACC+3,2                                                     
         TSEG  '.'                                                              
         TSEG  RJETOACC,3,B                                                     
         ERROR 'does not exist.',,BADACCT                                       
         END                                                                    
         MVC   RJENAME(5),RJETOACC                                              
         MVC   RJENAME+5(3),CPSUSER                                             
         IF    (RJEDEST,Z),'MVC RJEDEST,KWRDEST'                                
         IF    (RJEBIN,Z),'MVC RJEBIN,KWRBIN'                                   
         IF    (RJEFORMS,Z),BEGIN                                               
         MVC   RJEFORMS,CVBLANKS                                                
         MVC   RJEFORMS(L'KWRFORM),KWRFORM                                      
         END                                                                    
         CLEAR KWR                                                              
         XPOP  ,,L'KWR                                                          
         END                                                                    
*                                                                               
         IF    RJEFFAX,BEGIN       If fax job,                                  
         IF    (RJETO,Z),BEGIN                                                  
         ERROR 'Fax phone number not specified'                                 
         END                                                                    
         IF    (RJEDEST,NE,'FAX'),BEGIN                                         
         MVC   RJEDEST,=CL(L'RJEDEST)'FAX'                                      
         END                                                                    
         IF    (RJEFORMS,Z),BEGIN                                               
         MVC   RJEFORMS,=CL(L'RJEFORMS)'BOND'                                   
         END                                                                    
         END                                                                    
*                                                                               
         IF    (RJEDEST,Z),'MVC RJEDEST,CPDEST'                                 
         IF    (RJEBIN,Z),'MVC RJEBIN,CPBIN'                                    
         IF    (RJEFORMS,Z),'MVC RJEFORMS,CPFORMS'                              
*-                                                                              
*-       Set up print headers.                                                  
*-                                                                              
         IF    (RJEHEAD,Z),BEGIN   No header...                                 
         IF    (RJETITLE,NZ),'MVC RJEHEADL,RJETITLE'                            
         ELSEIF RJEFFAX,BEGIN           If fax job,                             
         MVC   RJEHEAD,=CL20'*NETFAX*'  Set dummy header                        
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   RJEHEAD,=CL20'PRINT'                                             
         IF    RJECFPUN,'MVC RJEHEAD(5),=C"PUNCH"'                              
         AIF   (NOT &JESMAIL).JMAILE                                            
         IF    RJECFMAI,'MVC RJEHEAD(5),=C"MAIL "'                              
.JMAILE  ANOP                                                                   
         IF    ((RJETOACC,Z),AND,(CPNAME,NZ)),'MVC RJEHEAD,CPNAME'              
         IF    (RJETOACC,NZ),BEGIN  Directed print/punch...                     
         MVC   RJEHEAD+6(5),=C'(From'                                           
         MVC   RJEHEAD+12(2),CPSGRP                                             
         MVI   RJEHEAD+14,C'.'                                                  
         MVC   RJEHEAD+15(3),CPSUSER                                            
         MVI   RJEHEAD+18,C')'                                                  
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Tidy up LPRINT defaults.                                               
*-                                                                              
         IF    RJEFLPR,BEGIN       LPRINT defaults...                           
         SET   RJEFHOLD            Must have output hold                        
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTRTN -- Co-routine to print lines.  Entered from the range               
*    processor with the line loc in R1 and the length in R0.                    
*                                                                               
PRINTRTN PROC                                                                   
         L     R6,CPWK5            RJEWA ptr                                    
         USING RJEWA,R6                                                         
*                                                                               
         COMMENT                   Move line into blank padded area             
         LA    R2,RJELINE                                                       
         LA    R3,L'RJELINE                                                     
         LR    R4,R1                                                            
         L     R5,=X'40000000'                                                  
         OR    R5,R0                                                            
         MVCL  R2,R4                                                            
         LA    R1,RJELINE                                                       
*                                                                               
         LR    R5,R1               Save line loc...                             
         LR    R4,R0               ...and len                                   
*-                                                                              
*-       First line.                                                            
*-                                                                              
         IF    ^RJEFSOME,BEGIN     Do first line processing...                  
*-                                                                              
*-       Process OPTIONS line(s).                                               
*-                                                                              
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         MVC   CPDOUB,@R1                                                       
         OC    CPDOUB,CVBLANKS                                                  
         IF    (CPDOUB,EQ,'OPTIONS:'),BEGIN                                     
         SH    R0,=H'8'                                                         
         SCINIT @R1+8,(R0)                                                      
         SET   RJEFOSCN            Flag as option line scan                     
         CLEAR R2                  Assume no explicit numbering                 
         IF    CPLFLG5.CPFFOPT,'IC R2,CPLFLG5'  Save number options             
         SCAN  PRINTPRT                                                         
         SCANCHK                                                                
         IF    (R2,NZ),'STC R2,CPLFLG5'  Restore number options                 
         B     PRINTXIT            Don't print this line                        
         END                                                                    
*-                                                                              
*-       If fax job, scan SET FAX options                                       
*-                                                                              
         IF    RJEFFAX,BEGIN                                                    
         IF    ((@R1,EQ,'%!'),AND,  If '%!' at start of first line,    *        
               (RJEPFORM,Z)),BEGIN     and no PFORMAT set,                      
         MVC   RJEPFORM,=CL(L'RJEPFORM)'POST'  Flag as Postscript               
         SET   CPLFLG5.CPFFOPT+CPFUNUM         Set unnumbered                   
         END                                                                    
*                                                                               
         XPUSH ,,256,PTR=R2         Get variable result wa                      
         TPUSH                                                                  
         LA    R3,256                                                           
         SETMSG 'SES.FAX_OPTIONS'                                               
         VCALL EVALSTR             Get expression value                         
         TPOP  JUNK                                                             
*                                                                               
         IF    (R3,P),BEGIN        If got value,                                
         SCINIT (R2),(R3)          Init scanner                                 
         SET   RJEFOSCN            Flag as option line scan                     
         CLEAR R2                  Assume no explicit numbering                 
         IF    CPLFLG5.CPFFOPT,'IC R2,CPLFLG5'  Save number options             
         SCAN  PRINTPRT                                                         
         SCANCHK                                                                
         IF    (R2,NZ),'STC R2,CPLFLG5'  Restore number options                 
         END                                                                    
         XPOP  ,,256                                                            
*                                                                               
         IF    (CPLFLG5.CPFUNUM+CPFFOPT,Z),BEGIN  If no num option,             
         SET   CPLFLG5.CPFFOPT+CPFUNUM    Set unnumbered                        
         IF    ((RJEIND,Z),AND,           If indent not set,           *        
               (RJEPFORM,NE,'L')),BEGIN    and not landscape mode,              
         MVC   RJEIND,=H'6'               Set default indent                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         ACALL PRINTJCL            Send print job JCL                           
*                                                                               
         IF    (RJENEWCC,EQ,'1'),BEGIN                                          
         IF    (RJEFCC,AND,(@R5,EQ,'1')),'CLEAR RJENEWCC'                       
         IF    (RJEFCC,AND,(@R5,EQ,'F')),'CLEAR RJENEWCC'                       
         IF    (RJEFMC,AND,((@R5,EQ,X'89'),OR,(@R5,EQ,X'8B'))),BEGIN            
         CLEAR RJENEWCC                                                         
         END                                                                    
         END                                                                    
*                                                                               
         SET   RJEFSOME            First line has been processed                
         END                                                                    
*                                                                               
         IF    ((@R5,EQ,RJEMARK),AND,(RJEMARK,NZ)),BEGIN                        
         IF    (RJEMARK,EQ,' '),BEGIN                                           
         IF    (R4,NZ),NOMARK      Must be all blanks                           
         END                                                                    
*                                                                               
         MVI   RJENEWCC,C'1'       Page eject pending                           
         B     PRINTXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Do TEXTONLY style conversion of our multiple character                 
*-         sequences.                                                           
*-                                                                              
NOMARK   IF    RJEFFMT,BEGIN       Formatted...                                 
         SETMSG @R5,(R4)            Input line                                  
         LA    R15,&LINESZ         Maximum line size                            
         VCALL TEXTONLY            Convert to TEXTONLY style                    
         LR    R4,R0               Update new line length                       
         END                                                                    
*-                                                                              
*-       Handle selecting just the columns we want.                             
*-                                                                              
         LR    R3,R5                                                            
         AH    R3,CPFCOL                                                        
         LR    R2,R4                                                            
         CEIL  R2,CPLCOL                                                        
         IF    ('SH R2,CPFCOL',NEG),'CLEAR R2'                                  
*-                                                                              
*-       Punch format.                                                          
*-                                                                              
         IF    (RJECFPUN,OR,RJEFPMAI),BEGIN   Punch format                      
         SETMSG CVBLANKS,1                                                      
         IF    RJEFMC,'LA R1,=X"09"'                                            
         SEG   (R1),(R0)                                                        
         IF    RJEFTRC,'SEGB'                                                   
         IF    ^CPLFLG5.CPFUNUM,BEGIN                                           
         IF    CPNFRNUM,'CEIL R2,72'  Replace with line-number                  
         IF    (R2,GT,72),EXIT     Don't overlay text                           
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         MVC   @R5+72(8),CPDOUB+1                                               
         LA    R2,80                                                            
         END                                                                    
*                                                                               
         LA    R4,80               Record break count                           
         IF    (RJEFPBSMTP,AND,(@R3,EQ,'.')),BEGIN   If leading '.'             
         LA    R4,79                               Reset break cnt              
         SEG   '.'                                 Add extra dot                
         END                                                                    
*                                                                               
         IF    (RJEFPMAI,AND,(R2,GT,R4)),BEGIN  Gateway mail...                 
         WHILE (R2,GT,R4),BEGIN    Break lines at column 80...                  
         SEGWR (R3),(R4)           Send first/next line                         
         AR    R3,R4               Incr text pointer                            
         SR    R2,R4               Decr length                                  
         LA    R4,80               Force 80 char break count                    
*                                                                               
         SETMSG CVBLANKS,1                                                      
         IF    RJEFMC,'LA R1,=X"09"'                                            
         SEG   (R1),(R0)           CC/MC character                              
         IF    RJEFTRC,'SEGB'      TRC character (if needed)                    
*                                                                               
         IF    (@R3,EQ,'.'),BEGIN  If leading '.'                               
         SEG   '.'                 Double it                                    
         LA    R4,79               Set 79 char break count                      
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         IF    (R2,GT,80),BEGIN    80 character limit on punch lines            
         VCALL JESODEL             Delete job                                   
         ABORT 'Punched lines can not be longer than 80 characters.'            
         END                                                                    
         SEGT (R3),(R2)            Add the output card                          
         SEGWR                                                                  
         INCR  R15,RJEPCNT         Kick punched card count                      
*-                                                                              
*-       Punch limit no longer enforced as per Jim Stosick and                  
*-         Lynne Sinclair.  11/6/91                                             
*-                                                                              
*****    IF    ((RJEKPLIM,NZ),AND,(R15,GT,RJEKPLIM)),'ACALL PRINTDLP'           
*-                                                                              
*-       Check print limit.                                                     
*-                                                                              
         IF    ^CPLFLG5.CPFNLST,BEGIN  Also check print limit...                
         INCR  R15,RJELCNT         Kick printed line count                      
         IF    ((RJEKLLIM,NZ),AND,(R15,GT,RJEKLLIM)),'ACALL PRINTDEL'           
         END                                                                    
         B     PRINTXIT            All done                                     
         END                                                                    
         EJECT                                                                  
         IF    RJEFCC,BEGIN        Check for valid CC char...                   
         IF    (@R5,EQ,' '),EXIT                                                
         IF    (@R5,EQ,'+'),EXIT                                                
         IF    (@R5,EQ,'-'),EXIT                                                
         IF    ((@R5,GE,'A'),AND,(@R5,LE,'C')),EXIT                             
         IF    ((@R5,EQ,'F'),OR,(@R5,EQ,'f')),EXIT                              
         IF    ((@R5,LT,'0'),OR,(@R5,GT,'9')),PRINTBCC                          
         END                                                                    
*                                                                               
         MVI   CPDOUB,C' '                                                      
         LR    R1,R5                                                            
         IF    RJEFCC+RJEFMC,'MVC CPDOUB(1),@R1; LA R1,@R1+1'                   
         IF    RJEFTRC,'MVC CPDOUB+1(1),@R1'                                    
         IF    (RJENEWCC,NZ),BEGIN  Pending CC char...                          
         IF    RJEFMC,BEGIN        MC style...                                  
         CLEAR R0                                                               
         IF    (RJENEWCC,EQ,'1'),'SETMSG X"8B"'                                 
         IF    (RJENEWCC,EQ,'0'),'SETMSG X"13"'                                 
         IF    (RJENEWCC,EQ,'-'),'SETMSG X"1B"'                                 
         IF    (R0,NZ),'SEGWR (R1),(R0)'                                        
         END                                                                    
         ELSE  BEGIN               CC style...                                  
         IF    (CPDOUB,EQ,' '),'MVC CPDOUB(1),RJENEWCC'                         
         ELSE  BEGIN                                                            
         SEGWR RJENEWCC                                                         
         IF    (CPDOUB,EQ,'0'),'MVI CPDOUB,C" "'                                
         IF    (CPDOUB,EQ,'-'),'MVI CPDOUB,C"0"'                                
         END                                                                    
         END                                                                    
         CLEAR RJENEWCC                                                         
         END                                                                    
         IF    RJEFDOUB+RJEFTRIP,BEGIN                                          
         IF    RJEFMC,EXIT                                                      
         IF    (CPDOUB,NE,' '),EXIT                                             
         MVI   CPDOUB,C'0'                                                      
         IF    RJEFTRIP,'MVI CPDOUB,C"-"'                                       
         END                                                                    
*                                                                               
         LA    R0,1                                                             
         IF    RJEFTRC,'A R0,CVONE'  Include TRC char                           
*-                                                                              
*-       CC character.                                                          
*-                                                                              
         SEG   CPDOUB,(R0)                                                      
*-                                                                              
*-       SUNMAIL leading dot check.                                             
*-                                                                              
         IF    RJEFPSUNMAIL,BEGIN   SUNMAIL style...                            
         IF    ((R2,POS),AND,(@R3,EQ,'.')),BEGIN  Leading dot...                
         SEG   '.'                 Double it                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Line text.                                                             
*-                                                                              
         CLEAR R0                  Unnumbered                                   
         IF    CPLFLG5.CPFNONUM,'SETMSG CVBLANKS,11'  Nonumber                  
         IF    (CPLFLG5.CPFUNUM,Z),BEGIN  Numbered...                           
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         SEG   CPDOUB,9                                                         
         SETMSG CVBLANKS,2                                                      
         END                                                                    
         SEG   (R1),(R0)                                                        
         IF    (^CPLFLG5.CPFNTEX,OR,CPLFLG5.CPFNONUM),BEGIN                     
         SETMSG (R3),(R2)           Line text                                   
         IF    RJEFHEX,'L R15,RJESGLENF; ACALL HEXSEG; B PRINTC'                
         IF    RJEFMIX,'ACALL MIXSEG; B PRINTC'                                 
         ACALL BACKSEG             Process any backspaces & write line          
         END                                                                    
*                                                                               
         LH    R4,RJEDARK          Dark count                                   
         IF    (R4,GT,1),BEGIN     Must overprint line...                       
         L     R3,RJESGLENF        Current buffer len                           
         IC    R2,RJESGBUF         Cc/mc char                                   
         IF    RJEFMC,'MVI RJESGBUF,X"01"'  Write no space                      
         DECR  R4                                                               
         WHILE (R4,POS),BEGIN                                                   
         SEGWR                                                                  
         IF    ^RJEFMC,'MVI RJESGBUF,C"+"'                                      
         ST    R3,RJESGLENF        Restore seg buffer                           
         DECR  R4                                                               
         END                                                                    
         IF    RJEFMC,'STC R2,RJESGBUF'  Orig MC char                           
         END                                                                    
         SEGWR                                                                  
PRINTC   INCR  R15,RJELCNT         Kick printed line count                      
         IF    ((RJEKLLIM,NZ),AND,(R15,GT,RJEKLLIM)),'ACALL PRINTDEL'           
*                                                                               
PRINTXIT PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
         USING RJEWA,R6                                                         
*box                                                                            
*                                                                               
*  PRINTBCC -- Branched to to abort a print because an invalid cc               
*    character was found.                                                       
*                                                                               
*  ERROR ROUTINE, THIS ROUTINE DOES NOT RETURN TO CALLER                        
*                                                                               
PRINTBCC PROC  ,                                                                
         VCALL JESODEL             Delete job                                   
         TSEG  (R5),1                                                           
         TSEG  ': invalid CC char in '                                          
         IF    CPFRDEXT,'TSEG "external",,B'                                    
         TSEG  'line '                                                          
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0),CR                                                     
         B     CVNOACTN                                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PRINTDLP/PRINTDEL are branched to when an attempt to print                   
*     or punch too many lines has been made.  the routine deletes               
*     the job (which has been partially submitted) and gives the                
*     user an error message.                                                    
*                                                                               
*  ERROR ROUTINE, THIS ROUTINE DOES NOT RETURN TO CALLER                        
*                                                                               
*                                                                               
PRINTDLP PROC  ,                                                                
         SETMSG 'Punched'                                                       
         L     R2,RJEKPLIM                                                      
         XPUSH R0,R2                                                            
         VCALL JESODEL             Delete job                                   
         XPOP  R0,R2                                                            
         TSEG  (R1),(R0),B         Printed/punched                              
         TSEG  'output exceeds the account limit of',,B                         
         BTD   CPDOUB,0,(R2)                                                    
         TSEG  (R1),(R0),CR                                                     
         B     CVNOACTN                                                         
         PEND                                                                   
*                                                                               
*                                                                               
*                                                                               
PRINTDEL PROC  ,                                                                
         SETMSG 'Printed'                                                       
         L     R2,RJEKLLIM                                                      
         XPUSH R0,R2                                                            
         VCALL JESODEL             Delete job                                   
         XPOP  R0,R2                                                            
         TSEG  (R1),(R0),B         Printed/punched                              
         TSEG  'output exceeds the account limit of',,B                         
         BTD   CPDOUB,0,(R2)                                                    
         TSEG  (R1),(R0),CR                                                     
         B     CVNOACTN                                                         
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
         USING RJEWA,R6            Global assumption                            
*                                                                               
BACKWA   RECORD BEGIN                                                           
BACKPTR  DS    A                   Location of input text                       
BACKINDX DS    XL(&LINESZ)         Character column map                         
BACKLINE DS    CL(&LINESZ)                                                      
BACKCLENF DS   F                   Length of continuation prefix                
BACKCONT DS    CL20                Continuation area                            
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  BACKSEG -- Routine to process imbedded backspaces and seg line.              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
BACKSEG  PROC  BACKWA                                                           
         IF    ^RJEFBACK,BACKFIN   Don't look for backspaces                    
         IF    ('LTR R15,R0',NP),BACKFIN  Null line                             
         ST    R1,BACKPTR          Save input line ptr                          
         DEX   R15,'TRT @R1(0),BACKTBL'                                         
         BZ    BACKFIN             No backspaces                                
*                                                                               
         CLEAR BACKINDX            Init char/col table                          
         L     R5,BACKPTR          Text loc                                     
         LR    R4,R0               Len                                          
         LA    R3,BACKINDX         Start of index map                           
         LA    R2,1                Starting at col 1                            
BACKLP   IF    (@R5,EQ,X'16'),'DECR R2'  Backspace                              
         ELSE  BEGIN                                                            
         IF    (@R5,NE,' '),BEGIN  Non-blank char...                            
         STC   R2,@R3              Save column for this char                    
         IF    (R2,NEG),'MVI @R3,0'  Backspaced too far                         
         END                                                                    
         A     R2,CVONE            Next column                                  
         END                                                                    
         LA    R3,@R3+1            Next pos in the column table                 
         LA    R5,@R5+1            Next char                                    
         BCT   R4,BACKLP                                                        
*                                                                               
BACKRTRY MVC   BACKLINE,CVBLANKS                                                
         LA    R5,BACKINDX                                                      
         LA    R4,L'BACKINDX                                                    
         L     R3,BACKPTR          Original line ptr                            
         CLEAR R2                  For "ic" in loop                             
BACKPLP  IC    R2,@R5              Got col no of char                           
         IF    (R2,NZ),BEGIN       Have char...                                 
         LA    R15,BACKLINE-1(R2)  Addr of char position                        
         IF    (@R15,EQ,' '),BEGIN  Put char on this line...                    
         MVC   @R15(1),@R3            Move char                                 
         MVI   @R5,0               Mark char as processed                       
         END                                                                    
         END                                                                    
         LA    R3,@R3+1            Next char                                    
         LA    R5,@R5+1            Next col entry                               
         BCT   R4,BACKPLP                                                       
*                                                                               
         IF    (BACKINDX,NZ),BEGIN  another pass will be needed...              
         MVC   BACKCONT,CVBLANKS                                                
         MVC   BACKCLENF,RJESGLENF                                              
         MVI   BACKCONT,C'+'       "no space" cc char                           
         IF    RJEFMC,BEGIN        MC style...                                  
         L     R1,RJESGLOC                                                      
         MVC   BACKCONT(1),@R1     Put users mc char in next line               
         MVI   @R1,X'01'           "write no space" on first line               
         END                                                                    
         IF    RJEFTRC,'L R1,RJESGLOC; MVC BACKCONT+1(1),@R1+1'                 
         SEGT  BACKLINE            Write current line                           
*                                                                               
         LH    R4,RJEDARK          Dark count                                   
         IF    (R4,GT,1),BEGIN     Must overprint line...                       
         L     R3,RJESGLENF        Current buffer len                           
         IC    R2,RJESGBUF         CC/MC char                                   
         IF    RJEFMC,'MVI RJESGBUF,X"01"'  Write no space                      
         DECR  R4                                                               
         WHILE (R4,POS),BEGIN                                                   
         SEGWR                                                                  
         IF    ^RJEFMC,'MVI RJESGBUF,C"+"'                                      
         ST    R3,RJESGLENF        Restore seg buffer                           
         DECR  R4                                                               
         END                                                                    
         IF    RJEFMC,'STC R2,RJESGBUF'  Orig MC char                           
         END                                                                    
         SEGWR                                                                  
         SEG   BACKCONT,L:BACKCLENF                                             
         B     BACKRTRY                                                         
         END                                                                    
*                                                                               
         SETMSG BACKLINE            Final line                                  
BACKFIN  SEGT  (R1),(R0)                                                        
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
BACKTBL  DC    XL256'00'                                                        
         ORG   BACKTBL+X'16'                                                    
         DC    X'FF'                                                            
         ORG                                                                    
         EJECT                                                                  
HEXWA    RECORD BEGIN                                                           
HEXWORK  DS    XL8                                                              
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  HEXSEG -- Routine to seg line in hex format.                                 
*                                                                               
*    On entry:  R1,R0 - line loc,len                                            
*               R15 - amount to indent continuation lines                       
*                                                                               
HEXSEG   PROC  HEXWA                                                            
*                                                                               
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         LR    R5,R15                                                           
*                                                                               
         WHILE (R3,POS),BEGIN                                                   
         BTX   HEXWORK,,L:@R4                                                   
         LR    R0,R3                                                            
         CEIL  R0,4                                                             
         AR    R0,R0                                                            
         LTH   R15,RJEIND                                                       
         IF    NEG,'CLEAR R15'                                                  
         AR    R15,R0                                                           
         A     R15,RJESGLENF                                                    
         IF    (R15,GT,120),BEGIN                                               
         XPUSH R0,R1                                                            
         SEGWR                                                                  
         SEG   CVBLANKS,(R5)       Indent                                       
         XPOP  R0,R1                                                            
         END                                                                    
         SEGB  (R1),(R0)                                                        
         LA    R4,@R4+4                                                         
         SH    R3,=H'4'                                                         
         END                                                                    
         SEGWR                                                                  
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MIXSEG -- Routine to seg line in mixed EBCDIC and HEX                        
*    format.                                                                    
*                                                                               
*    On entry:                                                                  
*      R1, R0 - line loc, len                                                   
*                                                                               
MIXSEG   PROC                                                                   
         VCALL MIXFMT                                                           
         SEGWR L:CPSEGLOC,L:CPSEGLENF                                           
         TCLEAR                                                                 
         PEND                                                                   
         EJECT                                                                  
PRINTPRF SCKW  FROM,V(FNAMESCN),A                                               
         SCKW  FORMS,FORMS,(A,P)   Dup--ahead of "FNAMEPSH" table               
         SCKW  ,V(RNUMPSH),PUSH    Unn--ahead of "FNAMEPSH" table               
         SCKW  UNFORMATTED,PUFMT,A Unf--ahead of "FNAMEPSH" table               
         SCKW  NOSEPARATOR,NOSEP,A Nos--ahead of "FNAMEPSH" table               
         SCKW  ,V(FNAMPSHF),PUSH                                                
*                                                                               
PRINTPRT SCKW  ACTIVE,V(SCNNOOP),A           (handled elsewhere)                
         SCKW  EXECUTE,V(SCNNOOP),A          (handled elsewhere)                
         SCKW  TITLE,PTITLE,(A,P)                                               
         SCKW  HEADER,PHEADER,(A,P)                                             
         SCKW  SUBJECT,PHEADER,(A,P)                                            
         SCKW  RECIPIENT,PFAXRCPT,(A,P)                                         
         SCKW  AUTH,PFAXAUTH,(A,P)                                              
         SCKW  EMAIL,PFAXEM,(P)                                                 
         SCKW  NOPROMPT,V(SCNNOOP)                                              
         SCKW  MARKER,PMARKER,(A,P)                                             
         SCKW  CC,PCC                                                           
         SCKW  MC,PCC                                                           
         SCKW  NOCC,PNOCC                                                       
         SCKW  NOMC,PNOCC                                                       
         SCKW  DOUBLE,PDOUBLE,A                                                 
         SCKW  TRIPLE,PDOUBLE,A                                                 
         SCKW  HEX,PHEX                                                         
         SCKW  MIXED,PMIXED,A                                                   
         SCKW  BACK,PBACK,A                                                     
         SCKW  NOBACK,PNOBACK,A                                                 
         SCKW  UNFORMATTED,PUFMT,A                                              
         SCKW  MEMO,PMEMO,A                                                     
         SCKW  DOCUMENT,PDOC,A                                                  
         SCKW  DARK,PDARK,A                                                     
         SCKW  ,V(LISTPSH),PUSH                                                 
         SCKW  ,V(TEXTPSH),PUSH                                                 
         SCKW  ,V(RNUMPSH),PUSH                                                 
         SCKW  ,V(COLSPSH),PUSH                                                 
         SCKW  ,RJEPSH,PUSH                                                     
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(QUIETPSH),PUSH                                                
         SCKW  FORMATTED,PFMT,A    (Must come after FORMS=)                     
         SCKW  ,UNREC                                                           
         EJECT                                                                  
*                                                                               
UNREC    PROC                                                                   
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         TSEG  'Use TITLE=''title'' option instead of just',,B                  
         TSEG  'specifying ''title''.',,WR                                      
         B     CVNOACTN                                                         
         END                                                                    
         IF    (@R1,EQ,'('),BEGIN                                               
         TSEG  'Use INDENT=n option instead of specifying "(n)".',,WR           
         B     CVNOACTN                                                         
         END                                                                    
         VCALL SCNINVAL                                                         
         PEND  ,                                                                
*                                                                               
PTITLE   PROC  ,                                                                
         IF    (^RJEFOSCN,OR,(RJETITLE,Z)),BEGIN                                
         MVC   RJETITLE,CVBLANKS                                                
         VCALL DEQUOTE                                                          
         IF    M,'VCALL NOTVALID'                                               
         CEIL  R0,L'RJETITLE                                                    
         LR    R15,R0                                                           
         MOVE  R15,RJETITLE,@R1                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PHEADER  PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEHEAD,Z)),BEGIN                                 
         MVC   RJEHEADL,CVBLANKS                                                
         VCALL DEQUOTE                                                          
         IF    M,'VCALL NOTVALID'                                               
         CEIL  R0,L'RJEHEADL                                                    
         LR    R15,R0                                                           
         MOVE  R15,RJEHEADL,@R1                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PFAXRCPT PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEFXRCP,Z)),BEGIN                                
         SCDQUOTE RJEFXRCP                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PFAXAUTH PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEFXAUT,Z)),BEGIN                                
         SCDQUOTE RJEFXAUT                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PFAXEM   PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEFXNOT,Z)),BEGIN                                
         SCDQUOTE RJEFXNOT                                                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PMARKER  PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEMARK,Z)),BEGIN                                 
         IF    (((@R1,EQ,''''),AND,(R0,EQ,3)),OR,(R0,EQ,1)),BEGIN               
         SCDQUOTE RJEMARK                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL NOTVALID                                                         
         END                                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PHEX     PROC  ,                                                                
         SET   RJEFHEX                                                          
         PEND                                                                   
*                                                                               
PMIXED   PROC                                                                   
         SET   RJEFMIX                                                          
         PEND  ,                                                                
*                                                                               
PCC      PROC                                                                   
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,^RJEFCCS),BEGIN                                    
         SET   RJEFCCS                                                          
         SET   RJEFCC,(RJEFMC,OFF)                    cc                        
         IF    (@R1,EQ,'M'),'FLIP RJEFCC+RJEFMC'      mc                        
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PNOCC    PROC  ,                                                                
         IF    (^RJEFOSCN,OR,^RJEFCCS),BEGIN                                    
         SET   RJEFCCS                                                          
         CLEAR RJEFCC+RJEFMC                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PDOUBLE  PROC                                                                   
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEFDOUB+RJEFTRIP,Z)),BEGIN                       
         SET   RJEFDOUB,(RJEFTRIP,OFF)                double                    
         IF    (@R1,EQ,'T'),'FLIP RJEFDOUB+RJEFTRIP'  triple                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PBACK    PROC  ,                                                                
         SET   RJEFBACK                                                         
         PEND                                                                   
*                                                                               
PNOBACK  PROC  ,                                                                
         CLEAR RJEFBACK                                                         
         PEND                                                                   
*                                                                               
PFMT     PROC  ,                                                                
         SET   RJEFFMT                                                          
         PEND                                                                   
*                                                                               
PUFMT    PROC  ,                                                                
         CLEAR RJEFFMT                                                          
         PEND                                                                   
*                                                                               
PMEMO    PROC  ,                                                                
         SET   RJEFMEMO                                                         
         SET   RJEFDOC                                                          
         PEND                                                                   
*                                                                               
PDOC     PROC  ,                                                                
         SET   RJEFDOC                                                          
         PEND                                                                   
*                                                                               
PDARK    PROC                                                                   
         LA    R4,2                Assume dark=2                                
         SCAN  PDARKPRT                                                         
         IF    (R15,NE,4),BEGIN                                                 
         SCBKUP                                                                 
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (^RJEFOSCN,OR,(RJEDARK,Z)),'STH R4,RJEDARK'                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PDARKPRT SCKW  ,PDARKCNT,PI,5                                                   
         SCKW  ,PDARKRST                                                        
*                                                                               
PDARKCNT PROC                                                                   
         LR    R4,R0                                                            
         PRETURN (R4)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
PDARKRST PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OPTLINE -- Routine to format options used on a /*COMMENT card.               
*                                                                               
OPTLINE  PROC                                                                   
         SET   RJEFCTRL            Sending control cards                        
*                                                                               
         SEGB  '/*COMMENT'                                                      
         AIF   (NOT &JESMAIL).JMAILD                                            
         IF    RJECFMAI,'SEGB "(MAIL)"'                                         
.JMAILD  ANOP                                                                   
         SETMSG 'PRINT'                                                         
         IF    RJECFPUN,'SETMSG "PUNCH"'                                        
         SEGB  (R1),(R0)                                                        
         IF    (RJETO,NZ),'SEGB "TO"; SEGTB RJETO'                              
         IF    CPFRDEXT,BEGIN  External...                                      
         VCALL NRESOLVE                                                         
         SEGB  'FROM'                                                           
         L     R3,CPFINFOP                                                      
         USING FFINFO,R3                                                        
         SEG   FFRNAME,L:FFRNAMEL                                               
         IF    (FFHOSTL,NE,0),BEGIN                                             
         SEG   ' ON '                                                           
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
         SEG   ' '                                                              
         DROP  R3                                                               
         END                                                                    
         ELSEIF CPFRDEXE,'SEGB "EXEC"'                                          
         IF    RJEFHEX,'SEGB "HEX"'                                             
         ELSEIF RJEFMIX,'SEGB "MIXED"'                                          
         IF    ^RJEFFMT,BEGIN                                                   
         IF    (RJEPFORM,EQ,'POST  '),EXIT                                      
         SEGB  'UNFORMATTED'                                                    
         END                                                                    
         IF    (RJEDEST,NZ),BEGIN                                               
         SEG   'DEST='                                                          
         SEGTB RJEDEST                                                          
         END                                                                    
         IF    RJEFDOC,BEGIN                                                    
         SETMSG 'DOCUMENT'                                                      
         IF    RJEFMEMO,'SETMSG "MEMO"'                                         
         SEGB  (R1),(R0)                                                        
         IF    (RJEFORMS,NE,'&DOCFORM'),'SEG "FORMS="; SEGTB RJEFORMS'          
         IF    (RJESYSCL,NE,'&DOCCLAS'),'SEG "SYSOUT="; SEG RJESYSCL'           
         IF    RJEFMEMO,BEGIN                                                   
         IF    (RJEFLASH,NE,'MEMO'),'SEG "FLASH="; SEGTB RJEFLASH'              
         IF    (RJEFCB,NE,'MEM6'),'SEG "FCB="; SEGTB RJEFCB'                    
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (RJEFLASH,NZ),'SEG "FLASH="; SEGTB RJEFLASH'                     
         IF    (RJEFCB,NZ),'SEG "FCB="; SEGTB RJEFCB'                           
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (RJEFORMS,NZ),'SEG "FORMS="; SEGTB RJEFORMS'                     
         IF    (RJESYSCL,NZ),BEGIN                                              
         IF    (RJESYSCL,EQ,'&STDCLAS'),EXIT                                    
         IF    (RJESYSCL,EQ,'&DOCCLAS'),'SEGB "UPLOW"'                          
         ELSEIF (RJESYSCL,EQ,'&FICCLAS'),'SEGB "FICHE"'                         
         ELSE  'SEG "SYSOUT="; SEGB RJESYSCL'                                   
         END                                                                    
         IF    (RJEFLASH,NZ),'SEG "FLASH="; SEGTB RJEFLASH'                     
         IF    (RJEFCB,NZ),'SEG "FCB="; SEGTB RJEFCB'                           
         END                                                                    
*                                                                               
         IF    (RJEPFORM,NZ),BEGIN  PFORMAT...                                  
         IF    (RJEPFORM,EQ,'LAND  '),'SEGB "LANDSCAPE"'                        
         ELSEIF (RJEPFORM,EQ,'POST  '),'SEGB "POSTSCRIPT"'                      
         ELSE  'SEG "PFORMAT="; SEGTB RJEPFORM'                                 
         END                                                                    
*                                                                               
         IF    (RJECFPUN,AND,^CPLFLG5.CPFNLST),'SEGB "LIST"'                    
         IF    CPLFLG5.CPFFOPT,BEGIN  Number option...                          
         IF    CPLFLG5.CPFNONUM,BEGIN                                           
         IF    ^RJEFDOC,'SEGB "NONUMBER"'                                       
         END                                                                    
         ELSEIF CPLFLG5.CPFUNUM,'SEGB "UNNUMBERED"'                             
         ELSEIF CPLFLG5.CPFNTEX,'SEGB "NOTEXT"'                                 
         ELSEIF CPLFLG5.CPNFINT,'SEGB "INTEGER"'                                
         END                                                                    
         IF    RJEFBACK,'SEGB "BACK"'                                           
         IF    RJEFCC,'SEGB "CC"'                                               
         IF    RJEFMC,'SEGB "MC"'                                               
         IF    (RJEFTRC,AND,(RJECHARM,Z)),'SEG "TRC "'                          
*                                                                               
         IF    (RJECHARS,NZ),BEGIN                                              
         SEG   'CHARS='                                                         
         IF    (RJECHARM,NZ),'SEG "("'                                          
         SEGT  RJECHAR0                                                         
         IF    (RJECHAR1,NZ),'SEG ","; SEGT RJECHAR1'                           
         IF    (RJECHAR2,NZ),'SEG ","; SEGT RJECHAR2'                           
         IF    (RJECHAR3,NZ),'SEG ","; SEGT RJECHAR3'                           
         IF    (RJECHARM,NZ),'SEG ")"'                                          
         SEGB                                                                   
         END                                                                    
         IF    RJEFBRST,'SEGB "BURST"'                                          
         IF    RJEFDUP,'SEGB "DUPLEX"'                                          
         IF    RJEFNDUP,'SEGB "NODUPLEX"'                                       
         IF    RJEFMAN,'SEGB "MANUAL"'                                          
         IF    RJEFFMAN,'SEGB "FMANUAL"'                                        
         IF    (RJEPRINT,NZ),'SEG "PRINTER="; SEGTB RJEPRINT'                   
         IF    (RJEMARK,NZ),'SEG "MARKER="; SEGB RJEMARK'                       
         IF    RJEFDOUB,'SEGB "DOUBLE"'                                         
         IF    RJEFTRIP,'SEGB "TRIPLE"'                                         
         IF    (RJECOPY,GT,1),'SEG "COPIES="; SEGDC LH:RJECOPY; SEGB'           
         IF    (RJEDARK,EQ,2),'SEG "DARK"'                                      
         IF    (RJEDARK,GT,2),'SEG "DARK="; SEGDC LH:RJEDARK; SEGB'             
         IF    ('LTH R2,RJEEJECT',NZ),BEGIN                                     
         IF    POS,'SEG "EJECT="; SEGDC (R2); SEGB'                             
         ELSE  'SEGB "NOEJECT"'                                                 
         END                                                                    
         IF    ('LTH R2,RJEIND',POS),BEGIN                                      
         SEG   'INDENT='                                                        
         SEGDC (R2)                                                             
         SEGB                                                                   
         END                                                                    
         IF    ('LTH R2,RJEWRAP',POS),BEGIN                                     
         SEG   'WRAP='                                                          
         SEGDC (R2)                                                             
         SEGB                                                                   
         END                                                                    
*                                                                               
         SEGWR                                                                  
*                                                                               
         CLEAR RJEFCTRL            Done sending control cards                   
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'RJE Options'                                                    
********************************************************************            
***      Common options for all rje commands.                                   
********************************************************************            
         SPACE                                                                  
RJEPRT   SCKW  ,RJEPSH,PUSH                                                     
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
RJEPSH   PUSHRTN RJEKWS                                                         
*                                                                               
RJEKWS   SCKW  JOBNAME,JOBNAME,(A,P)                                            
         SCKW  NAME,JOBNAME,(A,P)                                               
         SCKW  PREFIX,PREFIX,(A,P)                                              
         SCKW  ID,ID,(A,P)                                                      
         SCKW  HOLD,HOLD,A                                                      
         SCKW  H,HOLD                                                           
         SCKW  NOHOLD,NOHOLD,A                                                  
         SCKW  XHOLD,XHOLD,A                                                    
         SCKW  XH,XHOLD,A                                                       
         SCKW  NOXHOLD,NOXHOLD,A                                                
         SCKW  NOTIFY,NOTIFY,A                                                  
         SCKW  NONOTIFY,NOTIFY,A                                                
         SCKW  CLASS,CLASS,(A,P)                                                
         SCKW  C,CLASS,P                                                        
         SCKW  COPIES,COPIES,(A,P,PI),255                                       
         SCKW  CO,COPIES,(A,P,PI),255                                           
         SCKW  EJECT,EJECT,(A,P,PI),255                                         
         SCKW  NOEJECT,NOEJECT,A                                                
         SCKW  INDENT,INDENT,(A,P,I),&LINESZ                                    
         SCKW  NOINDENT,NOINDENT,A                                              
         SCKW  WRAP,WRAP,(A,P,PI),&LINESZ                                       
         SCKW  NOWRAP,NOWRAP,A                                                  
         SCKW  BIN,BIN,P                                                        
         SCKW  B,BIN,P                                                          
         SCKW  PRIORITY,PRIORITY,(A,P)                                          
         SCKW  P,PRIORITY,(A,P)                                                 
         SCKW  BLOCK,BLOCK,(A,P)                                                
         SCKW  KEEP,KEEP,A                                                      
         SCKW  SEPARATOR,SEP,A                                                  
         SCKW  NOSEPARATOR,NOSEP,A                                              
         SCKW  SENDER,SENDER,(A,P)                                              
         AIF   (NOT &JESMAIL).JMAILA                                            
         SCKW  MAIL,MAIL,(A,P)                                                  
.JMAILA  ANOP                                                                   
         SCKW  TO,TO                                                            
         SCKW  DESTINATION,DEST,(A,P)                                           
         SCKW  FORMS,FORMS,(A,P)                                                
         SCKW  FCB,FCB,P                                                        
         SCKW  CARRIAGE,FCB,(A,P)                                               
         SCKW  UCS,FCB,(A,P)                                                    
         SCKW  PRINTER,PRINTER,(A,P)                                            
         SCKW  PRT,PRINTER,P                                                    
         SCKW  BURST,BURST,A                                                    
         SCKW  DUPLEX,DUPLEX,A                                                  
         SCKW  NODUPLEX,NODUPLEX,A                                              
         SCKW  PFORMAT,PFORMAT,(A,P)                                            
         SCKW  POSTSCRIPT,POST,A                                                
         SCKW  PORTRAIT,PORT,A                                                  
         SCKW  LANDSCAPE,LAND,A                                                 
         SCKW  FLASH,FLASH,(A,P)                                                
         SCKW  CHARS,CHARS,(A,P)                                                
         SCKW  CHARACTERS,CHARS,(A,P)                                           
         SCKW  TRC,TRC                                                          
         SCKW  MANUAL,MANUAL,A                                                  
         SCKW  FMANUAL,FMANUAL,A                                                
         SCKW  UPLOW,UPLOW,A                                                    
         SCKW  FICHE,FICHE,A                                                    
         SCKW  SYS,SYS,(A,P)                                                    
         SCKW  SYSOUT,SYSOUT,(A,P)                                              
         SCKW  SYSAFF,SYSAFF,(A,P)                                              
         SCKW  NOLOG,NOLOG,A                                                    
         SCKW  NOJCL,NOJCL,A                                                    
         SCKW  NOMSG,NOMSG,A                                                    
         SCKW  NOMESSAGE,NOMSG,A                                                
         SCKW  ,,END                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
JOBNAME  PROC                                                                   
         IF    (R0,GT,L'RJENAME),'VCALL NOTVALID'                               
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJENAME,Z)),'MVC RJENAME,@R1'                     
         VCALL CHARCHKA                                                         
         IF    POS,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in name.',,INVCHARS                        
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PREFIX   PROC                                                                   
         IF    (R0,GT,L'RJEPFX),'VCALL NOTVALID'                                
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEPFX,Z)),'MVC RJEPFX,@R1'                       
         VCALL CHARCHKA                                                         
         IF    POS,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in prefix.',,INVCHARS                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
IDWA     RECORD BEGIN                                                           
IDWAID   DS    CL(L'RJEID)                                                      
         END                                                                    
*                                                                               
ID       PROC                                                                   
         SCUPTOK (R1)                                                           
         VCALL DEQUOTE             Allow quoted id for compatability            
         IF    (R0,GT,L'RJEID),'VCALL NOTVALID'                                 
         LR    R15,R0                                                           
         IF    (R0,EQ,8),'VCALL CHARCHKA'                                       
         ELSE  'VCALL CHARCHK'                                                  
         IF    POS,BEGIN                                                        
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid characters in ID.',,INVCHARS                          
         END                                                                    
         IF    (^RJEFOSCN,OR,(RJEID,Z)),BEGIN                                   
         LR    R15,R0                                                           
         MVC   RJEID,CVBLANKS                                                   
         MOVE  R15,RJEID,@R1                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
HOLD     PROC                                                                   
         SCAN  HOLDPRT                                                          
         IF    (R15,NE,4),BEGIN                                                 
         SET   RJEFHOLD,RJEFHOLX   Assume print hold                            
         SCBKUP                                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
HOLDPRT  SCKW  JOB,HOLDJOB                                                      
         SCKW  OUTPUT,HOLDOUT,A                                                 
         SCKW  ALL,HOLDALL                                                      
         SCKW  ,HOLDRSTR                                                        
*                                                                               
HOLDJOB  PROC  ,                                                                
         SET   RJEFXHLD,RJEFXHOX   Xeq hold                                     
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
HOLDOUT  PROC  ,                                                                
         SET   RJEFHOLD,RJEFHOLX   Print hold                                   
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
HOLDALL  PROC  ,                                                                
         SET   RJEFXHLD+RJEFHOLD,RJEFXHOX+RJEFHOLX                              
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
HOLDRSTR PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
NOHOLD   PROC  ,                                                                
         CLEAR RJEFHOLD                                                         
         SET   RJEFHOLX                                                         
         PEND                                                                   
*                                                                               
XHOLD    PROC  ,                                                                
         SET   RJEFXHLD,RJEFXHOX                                                
         PEND                                                                   
*                                                                               
NOXHOLD  PROC  ,                                                                
         CLEAR RJEFXHLD                                                         
         SET   RJEFXHOX                                                         
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
NOTIFY   PROC                                                                   
         IF    (^RJEFOSCN,OR,^RJEFNTFX),BEGIN                                   
         SCUPTOK (R1)                                                           
         SET   RJEFNTFY,RJEFNTFX   Notify                                       
         IF    (@R1,EQ,'NON'),'CLEAR RJEFNTFY'  nonotify                        
         SCAN                                                                   
         IF    (R0,EQ,6),BEGIN                                                  
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),BEGIN  gg.uuu...              
         MVC   RJENACCT(3),@R1+3                                                
         MVC   RJENACCT+3(2),@R1                                                
         B     NOTEXIT                                                          
         END                                                                    
         IF    (@R1+3,EQ,'$'),BEGIN  uuu$gg...                                  
         MVC   RJENACCT(3),@R1                                                  
         MVC   RJENACCT+3(2),@R1+4                                              
         B     NOTEXIT                                                          
         END                                                                    
         END                                                                    
         COMMENT                   FALL THRU IF NOT ACCT                        
         SCBKUP                                                                 
         END                                                                    
NOTEXIT  LABEL ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CLASS    PROC                                                                   
         SCUPTOK (R1)                                                           
         MVC   RJEJCLS,@R1                                                      
         LA    R15,L'RJEJCLS                                                    
         IF    (R0,GT,R15),'VCALL NOTVALID'                                     
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
COPIES   PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJECOPY,Z)),'STH R0,RJECOPY'                      
         PEND                                                                   
*                                                                               
NOEJECT  PROC                                                                   
         CLEAR R0                                                               
         DECR  R0                  Eject=-1 flags noeject                       
         IF    (^RJEFOSCN,OR,(RJEEJECT,Z)),'STH R0,RJEEJECT'                    
         PEND                                                                   
*                                                                               
EJECT    PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEEJECT,Z)),'STH R0,RJEEJECT'                    
         PEND                                                                   
*                                                                               
NOINDENT PROC ,                                                                 
         CLEAR R0                                                               
         DECR  R0                  Indent=0 is stored as -1                     
         IF    (^RJEFOSCN,OR,(RJEIND,Z)),'STH R0,RJEIND'                        
         PEND                                                                   
*                                                                               
INDENT   PROC                                                                   
         IF    (R0,Z),'DECR R0'     Indent=0 is stored as -1                    
         IF    (^RJEFOSCN,OR,(RJEIND,Z)),'STH R0,RJEIND'                        
         PEND                                                                   
*                                                                               
NOWRAP   PROC  ,                                                                
         CLEAR R0                                                               
         DECR  R0                   Indent=0 is stored as -1                    
         IF    (^RJEFOSCN,OR,(RJEWRAP,Z)),'STH R0,RJEWRAP'                      
         PEND                                                                   
*                                                                               
WRAP     PROC                                                                   
         IF    (R0,Z),'DECR R0'    Indent=0 is stored as -1                     
         IF    (^RJEFOSCN,OR,(RJEWRAP,Z)),'STH R0,RJEWRAP'                      
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
BIN      PROC                                                                   
         IF    (R0,GT,L'RJEBIN),'VCALL NOTVALID'                                
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEBIN,Z)),'MVC RJEBIN,@R1'                       
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
PRIORITY PROC                                                                   
         SCUPTOK (R15)                                                          
         IF    (^RJEFOSCN,OR,(RJEPRIO,Z)),BEGIN                                 
         CLEAR RJEOPRIO                                                         
         IF    (@R1,NE,'('),BEGIN                                               
         LA    R1,RJEPRIO                                                       
         ACALL SETPRIO                                                          
         END                                                                    
         ELSE  BEGIN                                                            
         SCPUSH                                                                 
         SH    R0,=H'2'                                                         
         SCINIT @R1+1,(R0)                                                      
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK (R15)                                                          
         IF   (R0,Z),CVABSENT                                                   
         LA    R1,RJEPRIO                                                       
         ACALL SETPRIO                                                          
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK (R15)                                                          
         IF    (R0,NZ),BEGIN                                                    
         LA    R1,RJEOPRIO                                                      
         ACALL SETPRIO                                                          
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,NZ),'VCALL NOTVALID'  Too many operands                      
         END                                                                    
         SCPOP                                                                  
         END                                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETPRIO  PROC                                                                   
         MVC   @R1(1),@R15            Set priority                              
         LR    R1,R0                                                            
         DEX   R1,'CLC @R15(0),=CL8"STANDARD"'                                  
         BE    SETPRIOX                                                         
         EX    R1,'CLC @R15(0),=CL8"EXPRESS"'                                   
         BE    SETPRIOX                                                         
         EX    R1,'CLC @R15(0),=CL8"URGENT"'                                    
         BE    SETPRIOX                                                         
         EX    R1,'CLC @R15(0),=CL8"LOW"'                                       
         BE    SETPRIOX                                                         
         CEIL  R0,8                                                             
         TSEG  (R15),(R0)                                                       
         ERROR ': invalid priority',,INVPRIO                                    
SETPRIOX LABEL                                                                  
         PEND                                                                   
*                                                                               
BLOCK    PROC                                                                   
         SCUPTOK (R15)                                                          
         IF    (^RJEFOSCN,OR,(RJEBLOCK,Z)),BEGIN                                
         CLEAR RJEOBLCK                                                         
         IF    (@R1,NE,'('),BEGIN                                               
         LA    R1,RJEBLOCK                                                      
         ACALL SETBLOCK                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         SCPUSH                                                                 
         SH    R0,=H'2'                                                         
         SCINIT @R1+1,(R0)                                                      
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK (R15)                                                          
         IF    (R0,Z),CVABSENT                                                  
         LA    R1,RJEBLOCK                                                      
         ACALL SETBLOCK                                                         
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK (R15)                                                          
         IF    (R0,NZ),BEGIN                                                    
         LA    R1,RJEOBLCK                                                      
         ACALL SETBLOCK                                                         
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,NZ),'VCALL NOTVALID' Too many operands                       
         END                                                                    
         SCPOP                                                                  
         END                                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETBLOCK PROC  ,                                                                
         MVC   @R1(1),@R15            Set block                                 
         LR    R1,R0                                                            
         DEX   R1,'CLC @R15(0),=CL8"ANY"'                                       
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"DAY"'                                       
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"PEAK"'                                      
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"NIGHT"'                                     
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"NITE"'                                      
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"EVENING"'                                   
         BE    SETBLKX                                                          
         EX    R1,'CLC @R15(0),=CL8"WEEKEND"'                                   
         BE    SETBLKX                                                          
         TSEG  (R15),(R0)                                                       
         ERROR ': invalid time block.',,INVTMBLK                                
SETBLKX  LABEL ,                                                                
         PEND                                                                   
*                                                                               
KEEP     PROC                                                                   
         SET   RJEFKEEP            Keep output after printing                   
         SCAN  KEEPPRT                                                          
         SCANCHK                                                                
         IF    (R15,NE,4),BEGIN                                                 
         SCBKUP                                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
KEEPPRT  SCKW  Y,KEEPYES                                                        
         SCKW  YES,KEEPYES                                                      
         SCKW  ,KEEPBACK                                                        
*                                                                               
KEEPYES  PROC                                                                   
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
KEEPBACK PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
SEP      PROC                                                                   
         CLEAR RJEFNSEP                                                         
         SCAN  SEPPRT                                                           
         SCANCHK                                                                
         IF    (R15,NE,4),BEGIN                                                 
         SCBKUP                                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SEPPRT   SCKW  Y,SEPYES                                                         
         SCKW  YES,SEPYES                                                       
         SCKW  N,SEPNO                                                          
         SCKW  NO,SEPNO                                                         
         SCKW  ,SEPBACK                                                         
*                                                                               
SEPYES   PROC                                                                   
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SEPNO    PROC                                                                   
         SET   RJEFNSEP                                                         
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SEPBACK  PROC                                                                   
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
NOSEP    PROC                                                                   
         SET   RJEFNSEP            NOSEPARATOR                                  
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         AIF   (NOT &JESMAIL).JMAILB                                            
MAIL     PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEMAIL,Z)),BEGIN                                 
         SCDQUOTE RJEMAIL                                                       
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
.JMAILB  ANOP                                                                   
*                                                                               
SENDER   PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJESEND,Z)),BEGIN                                 
         SCDQUOTE RJESEND                                                       
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
RJETOWA  RECORD BEGIN                                                           
RJETOA   DS    CL5                                                              
RJETOXX  DS    CL(L'RJETO+L'RJEDEST+64)                                         
         END                                                                    
*                                                                               
TO       PROC  RJETOWA                                                          
*                                                                               
         SCANSTR ,                 Allow funny chars                            
         SCANCHK                                                                
         IF    (R0,NP),'ERROR "Missing account after TO."'                      
         SCDQUOTE RJETOXX                                                       
*                                                                               
         IF    (^RJEFOSCN,OR,(RJETO,Z)),BEGIN                                   
         IF    RJEFFAX,BEGIN       If fax job this is phone number              
         MVC   RJETO,CVBLANKS                                                   
         LR    R15,R0                                                           
         CEIL  R15,L'RJETO                                                      
         MOVE  R15,RJETO,@R1                                                    
         B     TOEXIT                                                           
         END                                                                    
*                                                                               
         LR    R5,R1                                                            
         LR    R4,R0                                                            
*-                                                                              
*-       First check for "user@dest".                                           
*-                                                                              
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,'@'),BEGIN  It's user@dest...                            
         SR    R4,R0               Length of user part only                     
         LA    R1,@R1+1            Text past "@"                                
         DECR  R0                                                               
*                                                                               
         IF    (R0,GE,7),BEGIN     Check for ".BITNET" suffix...                
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         SH    R15,=H'7'           Last seven chars of id                       
         IF    (@R15,EQ,'.BITNET'),'SH R0,=H"7"'  Ignore suffix                 
         END                                                                    
*                                                                               
         MVC   RJEDEST,CVBLANKS                                                 
         LR    R15,R0                                                           
         CEIL  R15,L'RJEDEST                                                    
         MOVE  R15,RJEDEST,@R1     Save dest                                    
*                                                                               
         LA    R1,RJEDEST                                                       
         VCALL DESTCHK                                                          
         IF    NZ,BEGIN                                                         
         TSEG  RJEDEST,,TB                                                      
         TSEG  'is an unknown address.',,CR                                     
         SET   CPFBDEST            (Kludge: info need in MAIL)                  
         SET   CPFERROR            Error message                                
         END                                                                    
*                                                                               
         MVC   RJETO,CVBLANKS                                                   
         CEIL  R4,L'RJETO                                                       
         MOVE  R4,RJETO,@R5        Save user name                               
         CLEAR RJETOACC                                                         
         SET   RJEFEXT                                                          
*                                                                               
         CLEAR RJEFFMT             (Unformatted output for BITNET)              
         B     TOEXIT                                                           
         END                                                                    
*                                                                               
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*-                                                                              
*-       Check for userid or gg.uuu style account.                              
*-                                                                              
         SETMSG (R5),(R4)           Userid info                                 
         LA    R15,RJETOACC        Put "uuugg" here                             
         VCALL GETACCT             Get account                                  
         IF    Z,BEGIN             We have the account...                       
         MVC   RJETO,CVBLANKS                                                   
         CEIL  R4,L'RJETO                                                       
         MOVE  R4,RJETO,@R5        Save TO userid part too                      
         END                                                                    
*-                                                                              
*-       Accept uuu.                                                            
*-                                                                              
         ELSEIF (R4,EQ,3),BEGIN    Just uuu...                                  
         MVC   RJETOACC(3),@R5                                                  
         MVC   RJETOACC+3(2),CPSGRP                                             
         END                                                                    
*-                                                                              
*-       Accept gg.uuu, gg$uuu, or uuu$gg.                                      
*-                                                                              
         ELSE  BEGIN               Check for complete account...                
         IF    (R4,NE,6),'VCALL NOTVALID'                                       
         IF    ((@R5+2,NE,'.'),AND,(@R5+2,NE,'$'),AND,                 *        
               (@R5+3,NE,'$')),'VCALL NOTVALID'                                 
  IF  (@R5+2,EQ,'.'),'MVC RJETOACC(3),@R5+3; MVC RJETOACC+3(2),@R5'             
  IF  (@R5+2,EQ,'$'),'MVC RJETOACC(3),@R5+3; MVC RJETOACC+3(2),@R5'             
  IF  (@R5+3,EQ,'$'),'MVC RJETOACC(3),@R5; MVC RJETOACC+3(2),@R5+4'             
         MVC   RJETO,CVBLANKS                                                   
         MVC   RJETO(2),RJETOACC+3  Form "gg.uuu"...                            
         MVI   RJETO+2,C'.'                                                     
         MVC   RJETO+3(3),RJETOACC                                              
         END                                                                    
         END                                                                    
*                                                                               
TOEXIT   LABEL                                                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
DEST     PROC                                                                   
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),'VCALL NOTVALID'                 
         IF    (@R1,EQ,'('),'VCALL NOTVALID'                                    
         IF    (R0,GT,L'RJEDEST),'VCALL NOTVALID'                               
         IF    (^RJEFOSCN,OR,(RJEDEST,Z)),BEGIN                                 
         SCUPTOK (R1)                                                           
         MVC   RJEDEST,@R1                                                      
         LA    R1,RJEDEST                                                       
         VCALL DESTVER                                                          
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
FORMS    PROC                                                                   
         IF    (R0,GT,L'RJEFORMS),'VCALL NOTVALID'                              
         IF    (^RJEFOSCN,OR,(RJEFORMS,Z)),BEGIN                                
         SCUPTOK (R1)                                                           
         MVC   RJEFORMS,@R1                                                     
         IF    (RJEFORMS,EQ,'SEL'),BEGIN                                        
         TSEG  'SELF is not a forms code.  '                                    
         ERROR 'Use DEST=SELF for self-service printer.'                        
         END                                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
FCB      PROC   ,                                                               
         IF    (R0,GT,L'RJEFCB),'VCALL NOTVALID'                                
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEFCB,Z)),'MVC RJEFCB,@R1'                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
PRINTER  PROC                                                                   
         IF    (R0,GT,L'RJEPRINT),'VCALL NOTVALID'                              
         SCUPTOK (R15)                                                          
         IF    ((@R15,NE,'3800 '),AND,(@R15,NE,'IMPACT '),AND,         *        
               (@R15,NE,'ANY ')),BEGIN                                          
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid printer',,INVPRT                                      
         END                                                                    
         IF    (^RJEFOSCN,OR,(RJEPRINT,Z)),'MVC RJEPRINT,@R15'                  
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
BURST    PROC                                                                   
         SET   RJEFBRST                                                         
         SCAN  BURSTPRT                                                         
         SCANCHK                                                                
         IF    (R15,NE,4),BEGIN                                                 
         SCBKUP                                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
BURSTPRT SCKW  Y,BURSTY                                                         
         SCKW  YES,BURSTY                                                       
         SCKW  ,BURSTRST                                                        
*                                                                               
BURSTY   PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
BURSTRST PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
DUPLEX   PROC  ,                                                                
         SET   RJEFDUP,(RJEFNDUP,OFF)                                           
         SCAN  DUPPRT                                                           
         SCANCHK                                                                
         IF    (R15,NE,4),BEGIN                                                 
         SCBKUP                                                                 
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DUPPRT   SCKW  Y,DUPY                                                           
         SCKW  YES,DUPY                                                         
         SCKW  ,DUPRST                                                          
*                                                                               
DUPY     PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
DUPRST   PROC  ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
NODUPLEX PROC                                                                   
         SET   RJEFNDUP,(RJEFDUP,OFF)                                           
         PEND                                                                   
*                                                                               
PFORMAT  PROC                                                                   
         IF    (R0,GT,L'RJEPFORM),'VCALL NOTVALID'                              
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEPFORM,Z)),'MVC RJEPFORM,@R1'                   
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
LAND     PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEPFORM,Z)),BEGIN  PFORMAT...                    
         MVC   RJEPFORM,=CL(L'RJEPFORM)'LAND'  Landscape mode                   
         END                                                                    
         PEND                                                                   
*                                                                               
PORT     PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEPFORM,Z)),BEGIN  PFORMAT...                    
         MVC   RJEPFORM,=CL(L'RJEPFORM)'PORT'  Portrait mode                    
         END                                                                    
         PEND                                                                   
*                                                                               
POST     PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEPFORM,Z)),BEGIN  PFORMAT...                    
         MVC   RJEPFORM,=CL(L'RJEPFORM)'POST'  Postscript                       
         CLEAR RJEFFMT             Unformatted                                  
         SET   CPLFLG5.CPFUNUM     Unnumbered                                   
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
FLASH    PROC                                                                   
         IF    (R0,GT,L'RJEFLASH),'VCALL NOTVALID'                              
         SCUPTOK (R1)                                                           
         IF    (^RJEFOSCN,OR,(RJEFLASH,Z)),'MVC RJEFLASH,@R1'                   
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
MANUAL   PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEFMAN+RJEFFMAN,Z)),BEGIN                        
         SET   RJEFMAN,(RJEFFMAN,OFF)                                           
         END                                                                    
         PEND                                                                   
*                                                                               
FMANUAL  PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJEFMAN+RJEFFMAN,Z)),BEGIN                        
         SET   RJEFFMAN,(RJEFMAN,OFF)                                           
         END                                                                    
         PEND                                                                   
*                                                                               
CHARS    PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJECHARS,Z)),BEGIN                                
         CLEAR RJECHARS                                                         
         IF    (@R1,NE,'('),BEGIN                                               
         IF    (R0,GT,L'RJECHAR0),'VCALL NOTVALID'                              
         SCUPTOK (R15)                                                          
         MVC   RJECHAR0,@R15                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         SCPUSH                                                                 
         SH    R0,=H'2'                                                         
         SCINIT @R1+1,(R0)                                                      
         LA    R3,RJECHARS                                                      
         LA    R2,4                Max. no of char sets                         
CHARLOOP SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,Z),CHARDONE                                                  
         IF    (R0,GT,L'RJECHAR0),'VCALL NOTVALID'                              
         SCUPTOK (R15)                                                          
         MVC   @R3(L'RJECHAR0),@R15                                             
         LA    R3,@R3+L'RJECHAR0                                                
         BCT   R2,CHARLOOP                                                      
         SCAN                                                                   
         SCANCHK                                                                
         IF    (R0,NZ),'ERROR "Only 4 character sets allowed."'                 
CHARDONE IF    (RJECHARS,Z),CVABSENT                                            
         SCPOP                                                                  
         END                                                                    
         IF    (RJECHARM,NZ),'SET RJEFTRC'  Mult char sets                      
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
TRC      PROC                                                                   
         IF    (^RJEFOSCN,OR,^RJEFTRC),'SET RJEFTRC'                            
         PEND                                                                   
*                                                                               
FICHE    PROC                                                                   
         SETMSG '&FICCLAS'          Fiche = sysout=m                            
         ACALL SYSOUTX                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
UPLOW    PROC                                                                   
         SETMSG '&DOCCLAS'          Uplow = sysout=d                            
         ACALL SYSOUTX                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SYSOUT   PROC                                                                   
         SCUPTOK (R1)                                                           
         ACALL SYSOUTX                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SYSOUTX  PROC                                                                   
         IF    (R0,GT,L'RJESYSCL),'VCALL NOTVALID'                              
         IF    (^RJEFOSCN,OR,(RJESYSCL,Z)),BEGIN                                
         MVC   RJESYSCL,@R1                                                     
         IF    (RJESYSCL,EQ,'B'),BEGIN                                          
         ABORT 'Use the PUNCH command instead of SYSOUT=B.'                     
         END                                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SYS      PROC                                                                   
         SCUPTOK (R1)                                                           
         IF    (R0,EQ,L'RJESYSCL),'ACALL SYSOUTX'                               
         ELSE  'ACALL SYSAFF'                                                   
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SYSAFF   PROC                                                                   
         SCUPTOK (R15)                                                          
         IF    ((@R15,NE,'RLG '),AND,(@R15,NE,'3081 '),AND,            *        
               (@R15,NE,'SYS'),AND,                                    *        
               (@R15,NE,'ANY ')),'VCALL NOTVALID'                               
         IF    (^RJEFOSCN,OR,(RJESYSAF,Z)),BEGIN                                
         MVC   RJESYSAF,@R15                                                    
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
NOLOG    PROC                                                                   
         IF    (^RJEFOSCN,OR,^RJENOLOG),'SET RJENOLOG'                          
         PEND                                                                   
*                                                                               
NOJCL    PROC                                                                   
         IF    (^RJEFOSCN,OR,^RJENOJCL),'SET RJENOJCL'                          
         PEND                                                                   
*                                                                               
NOMSG    PROC                                                                   
         IF    (^RJEFOSCN,OR,(RJENOLOG+RJENOJCL,Z)),BEGIN                       
         SET   RJENOLOG,RJENOJCL                                                
         END   ,                                                                
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'RJE Subroutines'                                                
*box                                                                            
*                                                                               
*  JOBOPEN -- Routine to set things up for job submission.                      
*                                                                               
JOBOPEN  PROC                                                                   
         INCR  R15,CVNRJE          Total wylbur submission count                
         SET   RJEFOPEN                                                         
         PEND                                                                   
         EJECT                                                                  
JCLSWA   RECORD BEGIN                                                           
JCLSX    SEGCB                                                                  
JCLSBUF  DS    CL80                Working command buffer                       
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  JOBCLOSE -- Routine to close job submission path (if open) and               
*    locate job.                                                                
*                                                                               
JOBCLOSE PROC  JCLSWA                                                           
*                                                                               
         IF    RJEFOPEN,BEGIN                                                   
         ACALL JOBCNTL             Send control cards if needed                 
         VCALL JESOCLS             Close submission path (R15=jobno)            
         LR    R5,R15              Save jobno                                   
         CLEAR RJEFOPEN                                                         
*                                                                               
         IF    RJEFDUMB,BEGIN      Submit lines asis...                         
         TSEG  'Lines submitted to JES2.',,WR                                   
         B     JOBCLXIT            Scram                                        
         END                                                                    
*                                                                               
         IF    (R5,NZ),BEGIN       We have a jobno...                           
         IF    RJEFLPR,EXIT        Don't update jobno for LPRINT                
         STH   R5,CPDOUB           New jobno                                    
         MVC   CPDOUB+2(6),CPJOBS  Old jobnos                                   
         MVC   CPJOBS,CPDOUB                                                    
         END                                                                    
*                                                                               
         IF    (R5,NZ),BEGIN       Locate jobno...                              
         SEGINIT JCLSBUF,,JCLSX                                                 
*-                                                                              
*-       Build "LOCATE nnnn" command.                                           
*-                                                                              
         IF    ^RJEFLPR,BEGIN      Not LPRINT...                                
         IF    CPFQUIET,JOBCLXIT   Skip locate if QUIET                         
         SEG   'LOCATE '                                                        
         SEGDC (R5)                Jobno                                        
         END                                                                    
*-                                                                              
*-       Build "JPRINT nnnn LPRINT PRINTER=xxxx" for LPRINT command.            
*-                                                                              
         ELSE  BEGIN               LPRINT command...                            
         SEG   'JPRINT '                                                        
         SEGDC (R5)                Jobno                                        
         SEG   ' LPRINT'                                                        
         XPUSH ,,&LINESZ,PTR=R1                                                 
         LA    R0,&LINESZ                                                       
         LA    R2,=CL16'SYS.LPRINTER'                                           
         VCALL GETDATA                                                          
         IF    (Z,AND,(R0,POS)),BEGIN                                           
         XPUSH R0,R1                                                            
         SEG   ' PRINTER='                                                      
         XPOP  R0,R1                                                            
         SEG   (R1),(R0)           Lprinter info                                
         END                                                                    
         XPOP  ,,&LINESZ                                                        
         END                                                                    
*                                                                               
         SETMSG L:JCLSXLOC,L:JCLSXLENF  JES command loc, len                    
         LA    R15,8               Don't scan for wyl opts                      
         VCALL JESRTN              Do locate                                    
         IF    (R15,NEG),SAYSUB    Couldn't do locate, fake it                  
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
SAYSUB   TSEG  'Job '                                                           
         TSEG  RJEJOBNM,,T                                                      
         TSEG  ' submitted.',,WR                                                
         END                                                                    
         END                                                                    
JOBCLXIT PEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       JOBCRDCK - routine to check to see if the card image passed            
*-         in r0,r1 (len,loc) on entry is data or a job card.  also,            
*-         the routine will indicate the data is a job card if it is            
*-        the first line in the range (RJEDELIM = ff).  this routine            
*-         depends on the accuracy of RJEDELIM in the common work               
*-         area (RJEWA).  the condition code (and R15) if the                   
*-         following on exit:                                                   
*-                  z - data passed (i.e., non-jobcard)                         
*-                  p - possible job-card passed                                
*-                  m - possible control card insert point                      
*-                                                                              
JCKWA    RECORD BEGIN                                                           
JCKDLM   DS    CL16                DLM WORK AREA                                
         END                                                                    
*                                                                               
JOBCRDCK PROC  JCKWA                                                            
         IF    (RJEDELIM,EQ,X'FF'),'ACALL UPPJCL; B JOBCRFND'                   
         MVC   CPDOUB,@R1                                                       
         OC    CPDOUB,CVBLANKS     Caps                                         
         IF    ((CPDOUB,EQ,'/*EOF'),OR,(CPDOUB,EQ,'/*DEL'),            *        
               OR,(CPDOUB,EQ,'/*PUR')),BEGIN                                    
         OC    @R1(5),CVBLANKS     Make sure it's in caps                       
         MVI   RJEDELIM,X'FF'      Next line must be job-card                   
         B     JUSTDATA                                                         
         END                                                                    
*                                                                               
         CLI   RJEDELIM,0          Processing input stream?                     
         BE    TESTJCL             No, branch                                   
         CLC   @R1(2),RJEDELIM     Does card(2) = dlm?                          
         IF    EQ,BEGIN            Yes...                                       
         CLEAR RJEDELIM            Set end of dd data                           
         ACALL CHKNUM              Check for num/unn in seq field               
         B     JUSTDATA            This card is data                            
         END                                                                    
*                                                                               
         IF    ^RJEFDDS,CHKHCTL    Can't stop on "//"                           
*                                                                               
TESTJCL  IF    (@R1,NE,'//'),BEGIN  Not JCL...                                  
CHKHCTL  MVC   CPDOUB(9),@R1                                                    
         OC    CPDOUB(9),CVBLANKS                                               
         IF    (CPDOUB,EQ,'/*'),BEGIN  Control card...                          
         ACALL CHKNUM              Check for unn/num in seq field               
         AIF   (NOT &JESMAIL).JMAILF                                            
         IF    (CPDOUB,NE,'/*MAIL'),'ACALL UPPJCL'                              
.JMAILF  ANOP                                                                   
         END                                                                    
         AIF   (NOT &JCLPRIO).JCLPLOW                                           
         IF    (@R1,EQ,'/*'),CCINSPT  WYLBUR's /*control BEFORE user's          
.JCLPLOW ANOP                                                                   
         IF    (@R1,EQ,'/*LIST'),CCINSPT                                        
         B     JUSTDATA                                                         
         END                                                                    
         IF    RJEFDDS,'CLEAR RJEFDDS,RJEDELIM'  Reset DD * stuff               
         ACALL CHKNUM              Check seq field for 'unn/num'                
         CLC   @R1(3),=C'//*'      Is it a comment?                             
         BE    JUSTDATA            Br if so                                     
         ACALL UPPJCL              Convert JCL to caps                          
         CH    R0,=H'71'           Length gt 71?                                
         BNH   CRDLNOK             No, branch                                   
         LH    R0,=H'71'           Only scan 71 chars                           
CRDLNOK  SH    R0,=H'2'            Decr for "//"                                
         BNP   JUSTDATA            Branch if nothing left                       
         LA    R1,@R1+2            Bump for "//"                                
*                                                                               
ENDOFNM  CLI   @R1,C' '            Blank?                                       
         BE    FIELD2              Yes, branch                                  
         LA    R1,@R1+1            Incr for next char                           
         BCT   R0,ENDOFNM          Loop back                                    
         B     JUSTDATA            Branch if nothing left                       
*                                                                               
FIELD2   CLI   @R1,C' '            Blank?                                       
         BNE   TESTF2              No, branch                                   
         LA    R1,@R1+1            Bump to next char                            
         BCT   R0,FIELD2           Loop back                                    
         B     JUSTDATA            Branch if nothing left                       
*                                                                               
TESTF2   CLC   =C'JOB ',@R1        Is it a job card?                            
         BE    JOBCRFND            Yes, branch                                  
*                                                                               
         CLC   =C'EXEC ',@R1       Exec card?                                   
         BE    CCINSPT             Yes insert point                             
*                                                                               
         CLC   =C'PROC ',@R1       Inline proc?                                 
         BE    CCINSPT             Yes, branch                                  
TESTDD   CLC   =C'DD ',@R1         Is it a dd card?                             
         BNE   JUSTDATA            No, branch                                   
         SH    R0,=H'3'            SUBTRACT FOR 'DD '                           
         BNP   JUSTDATA            Branch if nothing left                       
         LA    R1,3(,R1)           bump for 'dd '                               
*                                                                               
FIELD3   CLI   @R1,C' '            Blank?                                       
         BNE   TESTDATA            No, branch                                   
         LA    R1,@R1+1            Bump                                         
         BCT   R0,FIELD3           Loop back                                    
         B     JUSTDATA            Branch if nothing left                       
*                                                                               
TESTDATA CLC   =C'DATA',@R1        Dd data?                                     
         BE    ISDATA              Yes, branch                                  
         CLI   @R1,C'*'            Is it dd *                                   
         BNE   JUSTDATA            No, branch                                   
         SET   RJEFDDS             Processing dd * stream                       
         LA    R1,@R1+1            point past '*'                               
         DECR  R0                  Decrement count left in card                 
         B     DATACHK             Check further                                
ISDATA   SH    R0,=H'4'            DECR FOR 'DATA'                              
         LA    R1,@R1+4            skip past 'data'                             
DATACHK  MVC   RJEDELIM,=C'/*'     Default dlm is /*                            
         LTR   R0,R0               Anything after data or *                     
         BNP   JUSTDATA            No, so we can exit now - dlm                 
*                                  is properly set to /*                        
         CLI   @R1,C','            Is there a comma after data or *?            
         BNE   JUSTDATA            No, exit without searching for dlm           
*                                                                               
TESTDLM  CLC   =C',DLM=',@R1       Search for dlm=xx                            
         BE    GOTDLM              Branch if we found it                        
         LA    R1,@R1+1            Bump ptr                                     
         BCT   R0,TESTDLM          Loop back                                    
         B     JUSTDATA            Branch if nothing left                       
*                                                                               
GOTDLM   LA    R1,@R1+5            Point at dlm                                 
         SH    R0,=H'5'            Decrement count remaining                    
         BNP   JUSTDATA            Its all over - nothing left                  
         CLI   @R1,C' '            Is there a blank next?                       
         BE    JUSTDATA            Yes- just leave                              
*-                                                                              
*-       get dlm                                                                
*-                                                                              
         SCINIT (R1),(R0)          Initialize scanner                           
         SCAN  ,                   No prt - just give me token                  
         IF    (R15,NZ),JUSTDATA   Error                                        
         IF    (R0,NP),JUSTDATA    Nothing there                                
         IF    (R0,GT,6),JUSTDATA  Worst case is DLM='&&'''                     
         IF    (@R1,NE,''''),BEGIN                                              
         SCUPTOK (R1)                                                           
         L2    R3,@R1              Load 1st two chars as DLM                    
         END                                                                    
         IF    (@R1,EQ,''''),BEGIN                                              
         MVC   JCKDLM(6),@R1       Move 6 chars                                 
         LA    R1,JCKDLM           Point to it                                  
         VCALL DEQUOTE             Dequote it                                   
         BNP   JUSTDATA            (null or bad string)                         
         L2    R3,@R1              R3 holds two char DLM                        
         IF    ((@R1,EQ,'&&'),AND,(@R1+1,EQ,'&&')),'IC R3,@R1+2'                
         END                                                                    
         CLEAR RJEFDDS             Not DD * case                                
         ST2   R3,RJEDELIM         Save two DLM characters                      
         IF    (RJEDELIM,EQ,'//'),BEGIN                                         
         SEGDEF RJESG                                                           
         SEGWR '/*DEL'                                                          
         TSEG  '"DLM=//" not allowed on DD card.',,WR                           
         ERROR 'Job will be canceled.',,INVDLM                                  
         END                                                                    
JUSTDATA CLEAR R15                 Set zero return code                         
         B     JCHKEXIT            Return                                       
*                                                                               
CCINSPT  LH    R15,=H'-4'          Control card insertion point                 
         B     JCHKEXIT            All finished                                 
*                                                                               
JOBCRFND CLEAR RJEDELIM            Set no dd data now                           
         LA    R15,4               Set non-zero return code                     
JCHKEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  UPPJCL -- Routine to convert text to upper case.  Text inside                
*     single quotes will not be converted.  On entry:                           
*                                                                               
*        R1,R0 - line loc,len                                                   
*                                                                               
UPPJCL   PROC                                                                   
         WHILE (R0,POS),BEGIN                                                   
UPPJCLQ  IF    (@R1,EQ,''''),BEGIN  Starting quote                              
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         WHILE ((R0,POS),AND,(@R1,NE,'''')),'LA R1,@R1+1; DECR R0'              
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         IF    (R0,POS),UPPJCLQ                                                 
         B     UPPJCLX             All done                                     
         END                                                                    
         IF    (@R1.X'40',OFF),BEGIN  maybe lower case char...                  
         IF    ((@R1,LT,'a'),OR,(@R1,GT,'z')),EXIT                              
         IF    ((@R1,GT,'i'),AND,(@R1,LT,'j')),EXIT                             
         IF    ((@R1,GT,'r'),AND,(@R1,LT,'s')),EXIT                             
         OI    @R1,C' '            Convert to upper case                        
         END                                                                    
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
UPPJCLX  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKNUM -- Routine to check cols 73-80 for number options.                    
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc,len                                                     
*                                                                               
CHKNUM   PROC                                                                   
         IF    (R0,GE,73),BEGIN                                                 
         XPUSH ,,8,PTR=R2                                                       
         MVC   @R2(8),@R1+72       Seq field                                    
         SCPUSH                                                                 
         SCINIT @R2,8                                                           
         SCAN  NCHKPRT                                                          
         COMMENT                   Ignore errors                                
         SCPOP                                                                  
         XPOP  ,,8                                                              
         END                                                                    
         PEND                                                                   
         SPACE 2                                                                
NCHKPRT  SCKW  ,V(RNUMPSH),PUSH                                                 
         SCKW  ,V(SCNINVAL)                                                     
         QLTORG                                                                 
         EJECT                                                                  
JOBWA    RECORD BEGIN                                                           
JOBCHAR  DS    C                                                                
JOBPTRS  DS    2A                  Line len, loc                                
JOBFIELD DS    CL9                 Cont and seq fields                          
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  JOBCARD -- Routine to build or validity check/expand job card.               
*                                                                               
*    On entry:                                                                  
*      R1,R0 - job card loc, len (if R0=0 then job card is built)               
*      CPLCNO- lineno of line                                                   
*                                                                               
JOBCARD  PROC  JOBWA                                                            
*                                                                               
         SET   RJEFCTRL            Sending control cards                        
*                                                                               
         ACALL CHKNUM              check seq field for 'unn/num'                
         LR    R5,R1                                                            
         CEIL  R0,71                                                            
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         DECR  R15                 Last char of card                            
         WHILE ((R0,POS),AND,(@R15,EQ,' ')),'DECR R0; DECR R15'                 
         STM   R0,R1,JOBPTRS                                                    
*                                                                               
         IF    (RJEACCT,Z),BEGIN   Use logged on user's account...              
         MVC   RJEACCT(5),CPSACCT                                               
         MVC   RJEACCT+5(2),CPSPROJ                                             
         OC    RJEACCT+5(2),CVBLANKS  (Convert nulls to blanks)                 
         END                                                                    
*-                                                                              
*-    The following puts the submitter's acct in the sequence field.            
*-                                                                              
         MVC   JOBFIELD,CVBLANKS                                                
         MVC   JOBFIELD+3(2),CPSGRP                                             
         MVI   JOBFIELD+5,C'.'                                                  
         MVC   JOBFIELD+6(3),CPSUSER                                            
*-                                                                              
*-       Now either build or expand the JOB card.                               
*-                                                                              
         IF    (R5,Z),BEGIN        Build job card...                            
         IF    RJEFLPR,BEGIN       Use LPRINT defaults...                       
         MVC   RJEJOBNM,=CL8'LPRINT'  Jobname                                   
         MVC   RJEACCT(5),CVWYLACC    WYLBUR's account                          
         MVC   RJEACCT+5(2),CVBLANKS  No subgroup                               
         END                                                                    
*                                                                               
         ELSE  'ACALL CKACCT'      Set up PRINT defaults...                     
*                                                                               
         SEGDEF RJESG                                                           
         SEG   '//'                                                             
         SEGTB RJEJOBNM                                                         
         SEG   'JOB '''                                                         
         SEG   RJEAUSR                                                          
         SEG   '$'                                                              
         SEGT  RJEAGRP                                                          
         SEG   ''''                                                             
         AIF   (NOT &PGMNAME).PGMNAM1                                           
         IF    (RJEHEAD,Z),'MVC RJEHEAD,CPSNAME'                                
         IF    (RJEHEAD,Z),BEGIN   No programmer name yet ...                   
         MVC   RJEHEAD,CVBLANKS    Clean up                                     
         MVC   RJEHEAD(5),CPSACCT  Use "UUUGG"                                  
         END                                                                    
.PGMNAM1 ANOP                                                                   
         IF    (RJEHEAD,NZ),BEGIN                                               
         SETMSG RJEHEAD                                                         
         VCALL LRTRIM                                                           
         IF    (R0,Z),EXIT                                                      
         LR    R2,R1                                                            
         LR    R3,R0                                                            
         SEG   ','''                                                            
         WHILE (R3,P),BEGIN                                                     
         SEG   (R2),1                                                           
         IF    (@R2,EQ,''''),'SEG (R2),1'                                       
         INCR  R2                                                               
         DECR  R3                                                               
         END                                                                    
         SEG   ''''                                                             
         END                                                                    
*                                                                               
         IF    (RJESYSCL,NZ),BEGIN                                              
         SEG   ',MSGCLASS='                                                     
         SEG   RJESYSCL                                                         
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               Expand job card...                           
         MVC   JOBFIELD(1),@R5+71  Preserve continuation field                  
         MVI   @R5+71,X'FF'        Flag end of card (for scanning)              
         IF    (@R5,NE,'//'),JCBAD                                              
         LA    R5,@R5+2            Start of jobname                             
         LR    R4,R5               Jobname ptr                                  
         CLEAR R3                  Len                                          
         WHILE (@R5,NE,' '),'LA R5,@R5+1; LA R3,@R3+1'                          
         WHILE (@R5,EQ,' '),'LA R5,@R5+1'                                       
         IF    (@R5,NE,'JOB '),JCBAD                                            
         LA    R5,@R5+4                                                         
         WHILE (@R5,EQ,' '),'LA R5,@R5+1'                                       
         CLEAR JOBCHAR                                                          
         IF    ((@R5,EQ,','),OR,(@R5,EQ,X'FF')),JCACCOK                         
         IF    ((@R5,EQ,''''),OR,(@R5,EQ,'(')),BEGIN                            
         MVC   JOBCHAR,@R5                                                      
         LA    R5,@R5+1                                                         
         IF    (@R5,EQ,','),JCACCOK                                             
         END                                                                    
         LR    R1,R5                                                            
         CLEAR R0                                                               
         WHILE ((@R5,NE,','),AND,(@R5,NE,' '),AND,(@R5,NE,''''),AND,   *        
               (@R5,NE,')')),'LA R5,@R5+1; A R0,CVONE'                          
* R1 points to the account, and R0 has the len...                               
         IF    ((R0,LT,6),OR,(R0,GT,8)),JCACCBAD                                
*                                                                               
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),BEGIN  gg.uuu...              
         IF    (R0,NE,6),JCACCBAD                                               
         MVC   RJEAGRP(2),@R1      Gg (keep "ss" from signon acct)              
         MVC   RJEAUSR,@R1+3                                                    
         B     JCACCOK                                                          
         END                                                                    
*                                                                               
         IF    (@R1+3,EQ,'.'),BEGIN  ggs.uuu...                                 
         IF    (R0,NE,7),JCACCBAD                                               
         MVC   RJEAGRP(3),@R1                                                   
         MVI   RJEAGRP+3,C' '                                                   
         MVC   RJEAUSR,@R1+4                                                    
         B     JCACCOK                                                          
         END                                                                    
*                                                                               
         IF    (@R1+4,EQ,'.'),BEGIN  ggss.uuu...                                
         IF    (R0,NE,8),JCACCBAD                                               
         MVC   RJEAGRP,@R1                                                      
         MVC   RJEAUSR,@R1+5                                                    
         B     JCACCOK                                                          
         END                                                                    
*                                                                               
         IF    (@R1+3,EQ,'$'),BEGIN  uuu$gg, uuu$ggs, uuu$ggss...               
         MVC   RJEAUSR,@R1                                                      
         IF    (R0,GT,6),'MVC RJEAGRP+2(2),CVBLANKS'                            
         LR    R15,R0                                                           
         SH    R15,=AL2(4+1)       Len of group code (in ex form)               
         EX    R15,'MVC RJEAGRP(0),@R1+4'                                       
         B     JCACCOK                                                          
         END                                                                    
*                                                                               
         B     JCACCBAD                                                         
*                                                                               
JCACCOK  OC    RJEACCT,CVBLANKS    Make sure acct is in caps                    
         ACALL CKACCT              Do kw checking, etc.                         
*                                                                               
         IF    (R3,NZ),BEGIN       Process jobname now...                       
         IF    (@R4,EQ,'&&'),BEGIN                                              
         IF    (R3,GT,6),JCNAML                                                 
         MVC   RJEJOBNM,CVBLANKS                                                
         MVC   RJEJOBNM(3),RJEACCT                                              
         DECR  R3                  Account for "&"                              
         MOVE  R3,RJEJOBNM+3,@R4+1                                              
         END                                                                    
         ELSEIF (@R4,EQ,'+'),BEGIN                                              
         IF    (R3,GT,9),JCNAML                                                 
         DECR  R3                                                               
         MVC   RJEJOBNM,RJEKPFX                                                 
         LA    R1,RJEJOBNM+7                                                    
         CLEAR R15                                                              
         WHILE (@R1,EQ,' '),'DECR R1; LA R15,@R15+1'                            
         IF    (R3,GT,R15),BEGIN                                                
         LA    R1,RJEJOBNM+7                                                    
         SR    R1,R3                                                            
         END                                                                    
         MOVE  R3,@R1+1,@R4+1                                                   
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (R3,GT,8),JCNAML                                                 
         MVC   RJEJOBNM,CVBLANKS                                                
         MOVE  R3,RJEJOBNM,@R4                                                  
         IF    (RJEAUSR,NE,RJEJOBNM),BEGIN                                      
         IF    (^CPJFFRCE,OR,^RJEKJFL.KWRJFFOR),EXIT                            
         MVC   RJEJOBNM(3),RJEAUSR                                              
         TSEG  'Jobname changed to',,B                                          
         TSEG  RJEJOBNM,,CR                                                     
         END                                                                    
         END                                                                    
         SETMSG RJEJOBNM                                                        
         VCALL LRTRIM                                                           
         VCALL CHARCHKA                                                         
         BP    JCNAMBAD                                                         
         END                                                                    
*-                                                                              
*-       Now expand the JOB card.                                               
*-                                                                              
         SEG   '//'                                                             
         SEGTB RJEJOBNM                                                         
         SEGB  'JOB'                                                            
         IF    (JOBCHAR,NZ),'SEG JOBCHAR'                                       
         SEG   RJEAUSR                                                          
         SEG   '$'                                                              
         SEGT  RJEAGRP                                                          
         LR    R0,R5                                                            
         S     R0,JOBPTRS+4                                                     
         S     R0,JOBPTRS                                                       
         LCR   R0,R0               Remaining len                                
         IF    POS,BEGIN                                                        
         L     R15,RJESGLENF       Current buffer len                           
         AR    R15,R0              Plus remainder for new total                 
         IF    (R15,GE,71),JCLONG  Too long                                     
         SEGT  (R5),(R0)           Add remainder of card                        
         END                                                                    
         AIF   (NOT &PGMNAME).PGMNM2                                            
         ELSE  BEGIN                                                            
         IF    (@R5,NE,C','),'SEG ","'                                          
         IF    (CPSNAME,NZ),BEGIN                                               
         SEG   ''''                                                             
         SEGT  CPSNAME                                                          
         SEG   ''''                                                             
         END   ,                                                                
         ELSE  'SEG CPSACCT'                                                    
         END   ,                                                                
.PGMNM2  ANOP                                                                   
         END                                                                    
*                                                                               
         SEGCOL 72                                                              
         SEGT  JOBFIELD            Add cont & seq field                         
         SEGWR                                                                  
*                                                                               
         MVC   CPJOBNM,RJEJOBNM    Save jobname                                 
*                                                                               
         IF    RJECFRUN,BEGIN                                                   
         IF    CPLFLG5.CPFFOPT,EXIT  don't change number options                
         CLEAR CPLFLG5.CPFNONUM                                                 
         IF    CPJFNOPT,BEGIN      Jes override...                              
         IF    CPJFUNN,'SET CPLFLG5.CPFUNUM'                                    
         END                                                                    
         ELSEIF ^RJEKJFL.KWRJFNUM,'SET CPLFLG5.CPFUNUM'                         
         END                                                                    
*-                                                                              
*-       Only use HOLD, XHOLD defaults on the RUN command.                      
*-                                                                              
         IF    RJECFRUN,BEGIN      Run command defaults...                      
         IF    ^RJEFHOLX,BEGIN                                                  
         IF    ^CPJFHOLX,BEGIN                                                  
         IF    RJEKJFL.KWRJFHOL,'SET RJEFHOLD'                                  
         END                                                                    
         ELSE  BEGIN                                                            
         IF    CPJFHOLD,'SET RJEFHOLD'                                          
         END                                                                    
         END                                                                    
*                                                                               
         IF    ^RJEFXHOX,BEGIN                                                  
         IF    ^CPJFXHOX,BEGIN                                                  
         CLEAR RJEFXHLD                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         IF    CPJFXHLD,'SET RJEFXHLD'                                          
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Supply correct NOTIFY default.                                         
*-                                                                              
         IF    ^RJEFNTFX,BEGIN                                                  
         IF    ^CPJFNTFX,BEGIN                                                  
         IF    RJEKJFL.KWRJFNOT,'SET RJEFNTFY'                                  
         END                                                                    
         ELSE  BEGIN                                                            
         IF    CPJFNTFY,'SET RJEFNTFY'                                          
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR RJEFCTRL            No longer sending control cards              
*                                                                               
         B     JCDDONE                                                          
*                                                                               
*  EXITS                                                                        
*                                                                               
JCBAD    SETMSG 'Bad job card'                                                  
         MVC   CPERRID,=CL8'BADJOB'                                             
         B     JCCOM                                                            
*                                                                               
JCACCBAD TSEG  (R1),(R0)                                                        
         SETMSG ': invalid account format on job card'                          
         MVC   CPERRID,=CL8'INVACCT'                                            
         B     JCCOM                                                            
*                                                                               
JCNAMBAD TSEG  (R1),(R0)                                                        
         SETMSG ': invalid characters in jobname'                               
         MVC   CPERRID,=CL8'INVCHARS'                                           
         B     JCCOM                                                            
*                                                                               
JCNAML   TSEG  (R4),(R3)                                                        
         SETMSG ': jobname too long'                                            
         MVC   CPERRID,=CL8'BIGJOBNM'                                           
         B     JCCOM                                                            
*                                                                               
JCLONG   SETMSG 'Expanded JOB card too long'                                    
         MVC   CPERRID,=CL8'LONGJOB'                                            
*       *B     JCCOM                                                            
*                                                                               
JCCOM    TSEG  (R1),(R0)                                                        
         TSEG  ' in '                                                           
         IF    CPFRDEXT,'TSEG "external",,B'                                    
         TSEG  'line',,B                                                        
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         B     CVABTMSG                                                         
*                                                                               
JCDDONE  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CKACCT  -- Routine to verify that it's ok for the user to                    
*    submit a job with the account in "RJEACCT".  This routine                  
*    also builds the default jobname.                                           
*                                                                               
CKACCT   PROC                                                                   
*-                                                                              
*-       Validate account and read the keyword file entry.                      
*-                                                                              
         XPUSH ,,L'KWR,PTR=R5                                                   
         USING KWR,R5                                                           
         CLEAR KWR                                                              
         MVC   KWRUSER(5),RJEACCT                                               
         CLEAR R0                  Assume normal check                          
         IF    RJEFSPEC,'LA R0,1'  Special means no password prompt             
         IF    RJECFCOND,'LA R0,1'  No password prompt on CONDENSE              
         LNR   R1,R5               KWR ptr (neg flags run entry)                
         VCALL JOBKWCHK            Read kw, check access, etc.                  
*-                                                                              
*-       Check for batch access for the RUN command.                            
*-                                                                              
         IF    RJECFRUN,BEGIN      Only check for the RUN command...            
         IF    ^KWRBFL.KWRBFVAL,BEGIN  No batch access...                       
         TSEG  'Account '                                                       
         TSEG  KWRGRP                                                           
         TSEG  '.'                                                              
         TSEG  KWRUSER                                                          
         SETMSG ' is not valid for batch.'                                      
         B     CVABTMSG                                                         
         END                                                                    
*                                                                               
         IF    ^KWRAFL.KWRAFVAL,BEGIN  Suspended...                             
         TSEG  'Account '                                                       
         TSEG  KWRGRP                                                           
         TSEG  '.'                                                              
         TSEG  KWRUSER                                                          
         TSEG  ' can''t run jobs.  This account '                               
         SETMSG 'is suspended.'                                                 
         IF    (KWRSTAT,EQ,KWRSEXP),'SETMSG "has expired."'                     
         IF    (KWRSTAT,EQ,KWRSOVB),'SETMSG "is over budget."'                  
         B     CVABTMSG                                                         
         END                                                                    
         END                                                                    
*-                                                                              
*-       Fill in account defaults.                                              
*-                                                                              
         MVC   RJEKJFL,KWRJFL      Save job flags                               
         MVC   RJEKDEST,KWRDEST    Save submitters destination                  
         IF    (RJEKDEST,NZ),'OC RJEKDEST,CVBLANKS'  Correct for PACT           
         MVC   RJEKBIN,KWRBIN      Save submitters bin                          
         MVC   RJEKPFX,KWRJPFX     Job prefix                                   
         XC    RJEKPFX,=C'JOBMASK.'  Unscramble                                 
         IF    (KWRJPFX,Z),BEGIN   Figure out a default...                      
         MVC   RJEKPFX(5),RJEACCT  Uuugg                                        
         MVC   RJEKPFX+5(3),CVBLANKS                                            
         END                                                                    
         LH    R15,KWRLLC          Print line limit (in thousands)              
         MH    R15,=H'1000'                                                     
         ST    R15,RJEKLLIM                                                     
         LH    R15,KWRLCP          Punch card limit (in hundreds)               
         MH    R15,=H'100'                                                      
         ST    R15,RJEKPLIM                                                     
         MVC   RJEKJNO,KWRJNO      Job count                                    
         LA    R4,1                Need to kick KWRJNO by 1                     
         IF    ('CLC RJEACCT(5),CPSACCT',EQ),BEGIN                              
         IF    (CPJPFX,NZ),'MVC RJEKPFX,CPJPFX'  Session override               
         IF    (CPJNO,NE,-1),BEGIN                                              
         MVC   RJEKJNO,CPJNO       Session override                             
         INCR  R15,CPJNO           Kick local counter                           
         CLEAR R4                  But don't kick KWR counter                   
         END                                                                    
         END                                                                    
         IF    (RJEPFX,NZ),'MVC RJEKPFX,RJEPFX'  Local override                 
*                                                                               
         WHILE KWRIFL.KWRIFVAL,BEGIN                                            
         L2    R15,KWRJNO                                                       
         AR    R15,R4              Kick job count                               
         IF    NEG,'CLEAR R15'                                                  
         STH   R15,KWRJNO          Save new count                               
         INCR  R15,KWRRJE          Always kick total job count                  
         IF    (R15,GT,20000),'CLEAR KWRRJE'                                    
         KWRITE KWR                Re-write KWR                                 
         IF    KWRHFL.KWRHFUOK,EXIT  Record updated, scram                      
         END                                                                    
         CLEAR KWR                                                              
         XPOP  ,,L'KWR                                                          
         DROP  KWR                                                              
*-                                                                              
*-       Now build the default jobname.                                         
*-                                                                              
         IF    (RJEJOBNM,Z),BEGIN  Nothing yet...                               
         MVC   RJEJOBNM,RJENAME                                                 
         IF    (RJEJOBNM,NZ),EXIT  Complete job name specified                  
         MVC   RJEJOBNM,RJEKPFX    Start with prefix                            
         IF    (RJEID,NZ),BEGIN                                                 
         LA    R3,RJEID+7                                                       
         LA    R2,8                                                             
CHKIDL   IF    (@R3,EQ,' '),'DECR R3; BCT R2,CHKIDL'                            
         LA    R1,RJEJOBNM+7                                                    
         CLEAR R15                                                              
         WHILE (@R1,EQ,' '),'DECR R1; LA R15,@R15+1'                            
         SR    R2,R15                                                           
         IF    POS,'SR R1,R2; AR R15,R2'                                        
         MOVE  R15,@R1+1,RJEID                                                  
         END                                                                    
         ELSE  BEGIN                                                            
         BTD   CPDOUB,,L2:RJEKJNO                                               
         OC    CPDOUB,=C'00000000'                                              
         LA    R4,RJEJOBNM+7                                                    
         LA    R3,CPDOUB+7                                                      
         LA    R15,8                                                            
CHKDFLP  IF    (@R4,EQ,' '),BEGIN                                               
         MVC   @R4(1),@R3                                                       
         DECR  R4                                                               
         DECR  R3                                                               
         BCT   R15,CHKDFLP                                                      
         END                                                                    
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  JOBCNTL -- Routine to format control cards.                                  
*                                                                               
         SPACE                                                                  
         MACRO                                                                  
&L       JCSEG &O,&F                                                            
         AIF   ('&F' EQ '').O                                                   
&L       IF    (&F,NZ),BEGIN                                                    
         SEG   (R1),(R0)                                                        
         SEG   &O                                                               
         SEGT  &F                                                               
         SETMSG ','                                                             
         END                                                                    
         MEXIT                                                                  
.O       ANOP                                                                   
&L       SEG   (R1),(R0)                                                        
         SEG   &O                                                               
         SETMSG ','                                                             
         MEND                                                                   
         SPACE 2                                                                
JOBCNTL  PROC                                                                   
         IF    RJEFDUMB,JOBCNTLX   Don't add control cards if DUMB              
*                                                                               
         IF    RJEFSNT,JOBCNTLX    Control cards already sent                   
         SET   RJEFSNT                                                          
*                                                                               
*                                                                               
         SET   RJEFCTRL            Sending control lines                        
*-                                                                              
*-       Add JOBPARM lines with rje options.                                    
*-                                                                              
         SETMSG '/*JOBPARM '                                                    
         IF    RJEFNTFY,BEGIN                                                   
         SEG   (R1),(R0)                                                        
         SEG   'NOTIFY='                                                        
         LA    R2,RJENACCT                                                      
         IF    (RJENACCT,Z),'LA R2,CPSACCT'                                     
         SEG   @R2+3,2             Gg                                           
         SEG   '.'                                                              
         SEG   @R2,3               Uuu                                          
         SETMSG ','                                                             
         END                                                                    
         IF    RJEFXHLD*RJEFHOLD,'JCSEG "HOLD=ALL"'                             
         ELSEIF RJEFXHLD,'JCSEG "HOLD=JOB"'                                     
         ELSEIF RJEFHOLD,'JCSEG "HOLD=OUTPUT"'                                  
         IF    (RJEBLOCK,NZ),BEGIN                                              
         SEG   (R1),(R0)                                                        
         SEG   'BLOCK='                                                         
         SETMSG RJEBLOCK                                                        
         IF    (RJEOBLCK,NZ),BEGIN                                              
         SEG   '('                                                              
         SEGT  RJEBLOCK                                                         
         SEG   ','                                                              
         SEGT  RJEOBLCK                                                         
         SETMSG ')'                                                             
         END                                                                    
         SEG   (R1),(R0)                                                        
         SETMSG ','                                                             
         END                                                                    
         IF    (RJEPRIO,NZ),BEGIN                                               
         SEG   (R1),(R0)                                                        
         SEG   'PRIO='                                                          
         SETMSG RJEPRIO                                                         
         IF    (RJEOPRIO,NZ),BEGIN                                              
         SEG   '('                                                              
         SEGT  RJEPRIO                                                          
         SEG   ','                                                              
         SEGT  RJEOPRIO                                                         
         SETMSG ')'                                                             
         END                                                                    
         SEG   (R1),(R0)                                                        
         SETMSG ','                                                             
         END                                                                    
         IF    ^RJEFEXT,'JCSEG "DEST=",RJEDEST'                                 
         JCSEG 'BIN=',RJEBIN                                                    
         IF    (R0,EQ,1),'SEGCOL 73; SEGWR "WYLGEN"'                            
*                                                                               
         SETMSG '/*JOBPARM '                                                    
         IF    RJENOLOG,'JCSEG "LOG=NO"'                                        
         IF    RJENOJCL,'JCSEG "JCL=NO"'                                        
         IF    (R0,EQ,1),'SEGCOL 73; SEGWR "WYLGEN"'                            
*                                                                               
         SETMSG '/*JOBPARM '                                                    
         JCSEG 'FCB=',RJEFCB                                                    
         JCSEG 'FORMS=',RJEFORMS                                                
         JCSEG 'FLASH=',RJEFLASH                                                
         IF    RJEFBRST,'JCSEG "BURST=YES"'                                     
         IF    RJEFKEEP,'JCSEG "KEEP=YES"'                                      
         IF    RJEFNSEP,'JCSEG "SEP=NO"'                                        
         IF    RJEFMAN,'JCSEG "FEED=MANUAL"'                                    
         IF    RJEFFMAN,'JCSEG "FEED=(MANUAL,FIRST)"'                           
         IF    (R0,EQ,1),'SEGCOL 73; SEGWR "WYLGEN"'                            
*                                                                               
         SETMSG '/*JOBPARM '                                                    
*        JCSEG 'SYSAFF=',RJESYSAF                                               
         JCSEG 'CLASS=',RJEJCLS                                                 
*                                                                               
         IF    (RJECHARS,NZ),BEGIN  CHARS option...                             
         SEG   (R1),(R0)                                                        
         SEG   'CHARS='                                                         
         IF    (RJECHARM,NZ),'SEG "("'                                          
         SEGT  RJECHAR0                                                         
         IF    (RJECHAR1,NZ),'SEG ","; SEGT RJECHAR1'                           
         IF    (RJECHAR2,NZ),'SEG ","; SEGT RJECHAR2'                           
         IF    (RJECHAR3,NZ),'SEG ","; SEGT RJECHAR3'                           
         IF    (RJECHARM,NZ),'SEG ")"'                                          
         SETMSG ','                                                             
         END                                                                    
*                                                                               
         IF    RJEFDUP,'JCSEG "DUPLEX=YES"'                                     
         IF    RJEFNDUP,'JCSEG "DUPLEX=NO"'                                     
         JCSEG 'PFORMAT=',RJEPFORM                                              
         IF    (R0,EQ,1),'SEGCOL 73; SEGWR "WYLGEN"'                            
*                                                                               
         SETMSG '/*JOBPARM '                                                    
         LH    R2,RJECOPY          No. of copies                                
         IF    (R2,NZ),BEGIN                                                    
         IF    NEG,'CLEAR R2'                                                   
         SEG   (R1),(R0)                                                        
         SEG   'COPIES='                                                        
         SEGDC (R2)                                                             
         SETMSG ','                                                             
         END                                                                    
         IF    ('LTH R2,RJEEJECT',NZ),BEGIN                                     
         IF    NEG,'CLEAR R2'                                                   
         SEG   (R1),(R0)                                                        
         SEG   'LINECT='                                                        
         SEGDC (R2)                                                             
         SETMSG ','                                                             
         END                                                                    
         IF    ('LTH R2,RJEIND',POS),BEGIN                                      
         SEG   (R1),(R0)                                                        
         SEG   'INDENT='                                                        
         SEGDC (R2)                                                             
         SETMSG ','                                                             
         END                                                                    
         IF    (R0,EQ,1),'SEGCOL 73; SEGWR "WYLGEN"'                            
*-                                                                              
*-       Add ROUTE lines if user@host was specified.                            
*-                                                                              
         IF    RJEFEXT,BEGIN       Print to user@host...                        
         SEG   '/*ROUTE PRINT '                                                 
         SEGT  RJEDEST                                                          
         SEG   '.'                                                              
         SEGT  RJETO,8                                                          
         SEGWR                                                                  
*                                                                               
         SEG   '/*ROUTE PUNCH '                                                 
         SEGT  RJEDEST                                                          
         SEG   '.'                                                              
         SEGT  RJETO,8                                                          
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Add SENDER line for Bitnet mail information.                           
*-                                                                              
         IF    RJEFPBSMTP,BEGIN    If BSMTP mail                                
         IF    ((CVMACHID,EQ,'SYSA'),OR,                               *        
               (CVMACHID,EQ,'SYSC')),BEGIN                                      
         SEGWR '/*SENDER MAILER@STANFORD'  Sender id is MAILER                  
         END                                                                    
         ELSEIF CVFRLG,BEGIN                                                    
         SEGWR '/*SENDER MAILER@RLG'                                            
         END                                                                    
         END                                                                    
***      IF    (RJESEND,NZ),BEGIN  We have sender info...                       
***      SEG   '/*SENDER '                                                      
***      SEGT  RJESEND                                                          
***      SEGWR                                                                  
***      END                                                                    
*-                                                                              
*-       Add MAIL line for ID mail.                                             
*-                                                                              
         AIF   (NOT &JESMAIL).JMAILC                                            
         IF    RJEFFAX,BEGIN       If fax job, make special /*MAIL              
         SEGB  '/*MAIL FAX TO'     Start fax info                               
         SEGT  RJETO               Add phone number                             
         IF    (RJEFXAUT,NZ),BEGIN  If have authorization code,                 
         SEGB  '\\AUTH'             Add 'AUTH nnnnnnnn' to record               
         SEGT  RJEFXAUT                                                         
         END                                                                    
         SEGWR '\\'                                                             
*                                                                               
         IF    (RJEFXNOT,NZ),BEGIN     If have email notify addr,               
         SEG   '/*MAIL Notify:'                                                 
         SEGT  RJEFXNOT                                                         
         SEGWR '\\'                                                             
         END                                                                    
*                                                                               
         IF    (RJEFXRCP,NZ),BEGIN     If recipient text specified,             
         SEG   '/*MAIL To:'            Add 'To:xxxxx' to record                 
         SEGT  RJEFXRCP                                                         
         SEGWR '\\'                                                             
         END                                                                    
*                                                                               
         SEG   '/*MAIL From:'          Add 'From:xxxxx' to record               
         IF    (RJESEND,GT,CVBLANKS),'SEGT RJESEND'                             
         ELSEIF (CPSNAME,GT,CVBLANKS),'SEGT CPSNAME'                            
         SEGWR '\\'                                                             
*-                                                                              
*-       Build cover page text (date and time, followed by any                  
*-       user HEADER or TITLE text).                                            
*-                                                                              
         XPUSH ,,32,PTR=R2             Get area for date routine                
         LR    R15,R2                                                           
         VCALL LOCALTOD                                                         
         VCALL FMTXDATE                Get formatted date                       
         SEG   '/*MAIL Date:'          Start 'Date:' text                       
         SEGB  @R2+8,3                 Month (3 chars)                          
         IF    (@R2+5,GT,'0'),'SEG @R2+5,1'  Day                                
         SEG   @R2+6,1                                                          
         SEG   ', '                                                             
         SEG   @R2+12,4                Year                                     
         SEG   CVBLANKS,4                                                       
         DTB   @R2+17,2                      Convert hour                       
         IF    (R15,GT,12),'SH R15,=Y(12)'   Adjust                             
         SEGDC (R15)                                                            
         SEGB  @R2+19,3                      Add ':mm'                          
         IF    (@R2+17,LE,'11'),'SEG "AM"'                                      
         ELSE  'SEG "PM"'                                                       
         SEG   ';;;;'                        Add line spacers                   
         XPOP  ,,32                                                             
*                                                                               
         IF    (RJEHEAD,NE,'*NETFAX*'),BEGIN  If not dummy hdr text,            
         SEGT  RJEHEADL                       Add to record                     
         END                                                                    
         ELSEIF (RJETITLE,NZ),BEGIN           Else if have title text,          
         SEGT  RJETITLE                       Add to record                     
         END                                                                    
         SEGWR ,                                                                
         END                                                                    
*                                                                               
         ELSEIF    (RJEMAIL,NZ),'SEGB "/*MAIL"; SEGT RJEMAIL; SEGWR'            
.JMAILC  ANOP                                                                   
*                                                                               
* Add /*ROUTE XEQ card. This is at the request of LDW for the                   
* JES conversion.                                                               
*                                                                               
         IF    (RJESYSAF,NZ),BEGIN                                              
         SEG   '/*ROUTE XEQ '                                                   
         SEGT  RJESYSAF                                                         
         SEGWR                                                                  
         END                                                                    
*                                                                               
         CLEAR RJEFCTRL            Done sending control cards                   
*                                                                               
JOBCNTLX PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         DROP  RJEWA                                                            
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RSEG -- Routine to write contents of seg buffer to JES2.                     
*                                                                               
*     On entry:                                                                 
*       R15 - SEGCB addr                                                        
*                                                                               
RSEG     PROC                                                                   
         L     R6,CPWK5                                                         
         WITH  (RJEWA,R6)                                                       
*                                                                               
         LR    R5,R15              Segcb ptr                                    
         WITH  (SEGCB,R5)                                                       
*-                                                                              
*-       SEGWR                                                                  
*-                                                                              
         IF    SEGCBWR,BEGIN       SEGWR...                                     
*-                                                                              
*-       Do final re-formatting if requested.                                   
*-                                                                              
         IF    RJEFCTRL,'VCALL JESWR'  Send line as-is to JES                   
         ELSEIF RJEFFMT,'ACALL FMTWR'  Do formatting                            
         ELSE  'ACALL WRAPWR'      Send unformatted line to JES                 
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
FMTWA    RECORD BEGIN                                                           
FMTFLAG  FLAG                                                                   
         FLAG  FMTFSHOW            - Just show sequence                         
         FLAG  FMTFSKIP            - Don't output this line                     
*                                                                               
FMTWPTRS DS    2A                  Working line ptrs                            
*                                                                               
FMTSG    SEGCB                                                                  
*                                                                               
FMTLINE  DS    CL(2+&LINESZ)       Normal line text                             
FMTCTRL  EQU   FMTLINE,2,C'C'                                                   
FMTTEXT  EQU   FMTLINE+2,&LINESZ,C'C'                                           
*                                                                               
FMTULINE DS    CL(2+&LINESZ)       Underlines                                   
FMTUCTRL EQU   FMTULINE,2,C'C'                                                  
FMTUTEXT EQU   FMTULINE+2,&LINESZ,C'C'                                          
*                                                                               
FMTBLINE DS    CL(2+&LINESZ)       Bold text                                    
FMTBCTRL EQU   FMTBLINE,2,C'C'                                                  
FMTBTEXT EQU   FMTBLINE+2,&LINESZ,C'C'                                          
*                                                                               
FMTILINE DS    CL(2+&LINESZ)       Italic text                                  
FMTICTRL EQU   FMTILINE,2,C'C'                                                  
FMTITEXT EQU   FMTILINE+2,&LINESZ,C'C'                                          
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FMTWR -- Routine to process print formatting options and then                
*    write the line to JES.                                                     
*                                                                               
*      On entry:                                                                
*        R1,R0 - line loc, len                                                  
*                                                                               
FMTWR    PROC  FMTWA                                                            
         WITH  (RJEWA,R6)          (Entry assumption)                           
         CLEAR FMTFLAG                                                          
*                                                                               
         IF    (R0,LE,1),BEGIN     Simple case...                               
         ACALL WRAPWR              Send line to JES                             
         EXIT  FMTWR                                                            
         END                                                                    
*-                                                                              
*-       Set up our output lines.                                               
*-                                                                              
         MVC   FMTCTRL(1),@R1      Save CC/MC char                              
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
*                                                                               
         MVI   FMTCTRL+1,C' '      Normal is char set 0                         
         IF    RJEFTRC,BEGIN       Use user's TRC char...                       
         IF    (R0,NP),EXIT        Nope, scram                                  
         MVC   FMTCTRL+1(1),@R1                                                 
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
         MVC   FMTTEXT,CVBLANKS                                                 
*                                                                               
         MVC   FMTUCTRL,=C'+ '     Underlines line                              
         MVC   FMTUTEXT,CVBLANKS                                                
*                                                                               
         MVC   FMTBCTRL,=C'+1'     Bold line                                    
         MVC   FMTBTEXT,CVBLANKS                                                
*                                                                               
         MVC   FMTICTRL,=C'+2'     Italics line                                 
         MVC   FMTITEXT,CVBLANKS                                                
*-                                                                              
*-       Seg text into appropriate output buffers.                              
*-                                                                              
         LR    R5,R1               Line loc...                                  
         LR    R4,R0               ...and len                                   
         CEIL  R4,L'FMTTEXT        (Not too much)                               
*                                                                               
         SEGINIT 0,L'FMTTEXT,RTN=FSEG  Buffer loc set below                     
         LA    R15,FMTWA                                                        
         ST    R15,FMTSGUSR        Save FMTWA ptr                               
*-                                                                              
*-       Scan line and process imbedded formatting commands.                    
*-                                                                              
FMTLOOP  WHILE (R4,POS),BEGIN                                                   
         LA    R1,FMTTEXT                 Normal                                
         IF    RJEXFITL,'LA R1,FMTITEXT'  Italics                               
         IF    RJEXFBLD,'LA R1,FMTBTEXT'  Bold                                  
         ST    R1,FMTSGLOC         Set approp. buffer ptr                       
*                                                                               
         LR    R15,R4                                                           
         DEX   R15,'TRT @R5(0),FMTTBL'  Stop at next "<" char                   
         IF    Z,BEGIN             No "<" found...                              
         SEG   (R5),(R4)           Add remaining text                           
         EXIT  FMTLOOP                                                          
         END                                                                    
*                                                                               
         LR    R2,R1                                                            
         SR    R2,R5               Length of text up to "<"                     
         SEG   (R5),(R2)           Add text to buffer                           
         AR    R5,R2                                                            
         SR    R4,R2                                                            
         IF    (R4,LE,3),BEGIN     Too few chars left...                        
         SEG   (R5),(R4)           Add final text                               
         EXIT  FMTLOOP             Scram                                        
         END                                                                    
*-                                                                              
*-       Check for valid formatting sequence.                                   
*-                                                                              
         STM   R4,R5,FMTWPTRS                                                   
         LA    R5,@R5+1            Skip over "<" char                           
         DECR  R4                                                               
*                                                                               
         IF    (@R5,NE,'.'),FMTBKUP                                             
         LA    R5,@R5+1            Skip past prefix char                        
         DECR  R4                                                               
         CLEAR FMTFSHOW            Assume we will process sequence              
*                                                                               
         IF    (@R5,EQ,'.'),BEGIN  "<.." means don't process...                 
         LA    R5,@R5+1            Skip past second dot                         
         DECR  R4                                                               
         IF    (R4,LE,1),FMTBKUP   Too few chars, skip it                       
         SET   FMTFSHOW            Don't process sequence                       
         END                                                                    
*                                                                               
         IF    (@R5,EQ,'>'),FMTBKUP  No command, skip it                        
*-                                                                              
*-       Validate characters in (possible) formatting sequence.                 
*-                                                                              
         WHILE (R4,POS),BEGIN      Validate characters...                       
         IF    (@R5,EQ,'>'),BEGIN  End of formatting command...                 
         L     R1,FMTWPTRS+4                                                    
         LA    R1,@R1+1                                                         
         LR    R0,R5                                                            
         SR    R0,R1                                                            
*                                                                               
         LA    R5,@R5+1            Update scan ptrs                             
         DECR  R4                                                               
*                                                                               
         IF    FMTFSHOW,BEGIN      Just show the sequence...                    
         XPUSH R0,R1                                                            
         SEG   L:FMTWPTRS+4,1      "<" char                                     
         XPOP  R0,R1                                                            
         SEG   @R1+1,(R0)          ...followed by dot command                   
         NEXT  FMTLOOP                                                          
         END                                                                    
*                                                                               
         LA    R15,FMTWA                                                        
         ACALL FMTCMD              Process formatting command                   
         NEXT  FMTLOOP                                                          
         END                                                                    
*                                                                               
         LC    R2,@R5              Get char                                     
         LC    R15,FMTVTBL(R2)     Non-zero if invalid char                     
         IF    (R15,NZ),FMTBKUP    Invalid character, skip it                   
*                                                                               
         LA    R5,@R5+1            Next char                                    
         DECR  R4                                                               
         END                                                                    
*                                                                               
FMTBKUP  LM    R4,R5,FMTWPTRS      Restore scan ptrs                            
         SEG   (R5),1              Show "<" char                                
         LA    R5,@R5+1                                                         
         DECR  R4                                                               
         END                                                                    
*-                                                                              
*-       Write the output line(s).                                              
*-                                                                              
         IF    FMTFSKIP,EXIT       Skip output of this line                     
*                                                                               
         CLEAR R2                  Flag                                         
         IF    (FMTTEXT,EQ,CVBLANKS),BEGIN  Nothing on main line...             
         IF    (FMTBTEXT,NE,CVBLANKS),BEGIN  Bold line now main...              
         MVC   FMTBCTRL(1),FMTCTRL                                              
         LA    R2,1                                                             
         END                                                                    
         ELSEIF (FMTITEXT,NE,CVBLANKS),BEGIN  Italic line now main...           
         MVC   FMTICTRL(1),FMTCTRL                                              
         LA    R2,1                                                             
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
         CLEAR R4                  Assume no user TRC character                 
         IF    RJEFTRC,'LA R4,1'   Remember flag setting                        
         SET   RJEFTRC             Set flag (for WRAPWR)                        
*                                                                               
         LA    R3,L'FMTCTRL                                                     
         A     R3,FMTSGLENF        Max possible length                          
*-                                                                              
*-       Write main line.                                                       
*-                                                                              
         IF    (R2,Z),BEGIN        Write main line...                           
         SETMSG FMTLINE,(R3)                                                    
         VCALL RTRIM                                                            
         ACALL WRAPWR              Write line to JES                            
         END                                                                    
*-                                                                              
*-       Write bold text line.                                                  
*-                                                                              
         IF    (FMTBTEXT,NE,CVBLANKS),BEGIN  Write bold line...                 
         SETMSG FMTBLINE,(R3)                                                   
         VCALL RTRIM                                                            
         ACALL WRAPWR              Write line to JES                            
         END                                                                    
*-                                                                              
*-       Write italic text line.                                                
*-                                                                              
         IF    (FMTITEXT,NE,CVBLANKS),BEGIN  Write italic line...               
         SETMSG FMTILINE,(R3)                                                   
         VCALL RTRIM                                                            
         ACALL WRAPWR              Write line to JES                            
         END                                                                    
*-                                                                              
*-       Write underline line.                                                  
*-                                                                              
         IF    (FMTUTEXT,NE,CVBLANKS),BEGIN  Write underline line...            
         SETMSG FMTULINE,(R3)                                                   
         VCALL RTRIM                                                            
         ACALL WRAPWR              Write line to JES                            
         END                                                                    
*                                                                               
         IF    (R4,Z),'CLEAR RJEFTRC'  Restore user TRC flag                    
*                                                                               
         PEND                                                                   
*                                                                               
*                                                                               
FMTTBL   DC    256X'00'                                                         
         ORG   FMTTBL+C'<'                                                      
         DC    X'FF'                                                            
         ORG                                                                    
*                                                                               
FMTVTBL  DC    256X'FF'                                                         
         ORG   FMTVTBL+C'a'                                                     
         DC    X'000000000000000000'    a-i                                     
         ORG   FMTVTBL+C'j'                                                     
         DC    X'000000000000000000'        j-r                                 
         ORG   FMTVTBL+C's'                                                     
         DC    X'0000000000000000'               s-z                            
         ORG   FMTVTBL+C'A'                                                     
         DC    X'000000000000000000'    A-I                                     
         ORG   FMTVTBL+C'J'                                                     
         DC    X'000000000000000000'        J-R                                 
         ORG   FMTVTBL+C'S'                                                     
         DC    X'0000000000000000'               S-Z                            
         ORG   FMTVTBL+C'0'                                                     
         DC    X'00000000000000000000'  0-9                                     
         ORG   FMTVTBL+C'+'                                                     
         DC    X'00'                    +                                       
         ORG   FMTVTBL+C'-'                                                     
         DC    X'00'                    -                                       
         ORG                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FSEG -- Routine to handle FMTWR's SEG buffer.                                
*                                                                               
*     On entry:                                                                 
*       R15 - SEGCB addr                                                        
*                                                                               
FSEG     PROC                                                                   
         L     R6,CPWK5                                                         
         WITH  (RJEWA,R6)                                                       
*                                                                               
         LR    R5,R15              Segcb ptr                                    
         WITH  (SEGCB,R5)                                                       
*                                                                               
         L     R4,SEGCBUSR         FMTWA ptr                                    
         WITH  (FMTWA,R4)                                                       
*-                                                                              
*-       SEG                                                                    
*-                                                                              
         IF    SEGCBSEG,BEGIN      SEG...                                       
         IF    RJEXFUND,BEGIN      Need underlines...                           
         LA    R3,FMTUTEXT                                                      
         A     R3,SEGCBLENF        Current index                                
         LR    R15,R0                                                           
         MOVE  R15,@R3,FSEGUND     Move in underlines                           
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
FSEGUND  DC    (L'FMTUTEXT)C'_'    Underlines                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FMTCMD -- Routine to process formatting command.                             
*                                                                               
*    On entry:                                                                  
*      R15   - FMTWA ptr                                                        
*      R1,R0 - formatting command location, length                              
*                                                                               
FMTCMD   PROC                                                                   
         USING RJEWA,R6            Entry assumption                             
*                                                                               
         LR    R5,R15                                                           
         USING FMTWA,R5                                                         
*                                                                               
         SCPUSH                                                                 
         SCINIT (R1),(R0)                                                       
         SCANSTR FCMDPRT           Scan                                         
         COMMENT                   Ignore errors                                
         SCPOP                                                                  
         PEND                                                                   
         SPACE 2                                                                
***                                                                             
***  Print formatting options.                                                  
***                                                                             
FCMDPRT  SCKW  .PAGE,FCMDPAGE                                                   
         SCKW  .FRONT,FCMDFPAG                                                  
         SCKW  .FRONTPAGE,FCMDFPAG                                              
         SCKW  .NORMAL,FCMDNORM                                                 
         SCKW  .U+,FCMDUP                                                       
         SCKW  .U-,FCMDUM                                                       
         SCKW  .B+,FCMDBP                                                       
         SCKW  .B-,FCMDBM                                                       
         SCKW  .I+,FCMDIP                                                       
         SCKW  .I-,FCMDIM                                                       
         SCKW  ,FCMDERR                                                         
*                                                                               
FCMDERR  PROC                                                                   
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
FCMDPAGE PROC                                                                   
         MVI   RJENEWCC,C'1'       Page eject pending                           
         SET   FMTFSKIP            Don't output this line                       
         PEND                                                                   
*                                                                               
FCMDFPAG PROC                                                                   
         MVI   RJENEWCC,C'F'       Forces new sheet (duplex)                    
         SET   FMTFSKIP            Don't output this line                       
         PEND                                                                   
*                                                                               
FCMDNORM PROC                                                                   
         CLEAR RJEXFBLD+RJEXFITL+RJEXFUND                                       
         PEND                                                                   
*                                                                               
FCMDUP   PROC                                                                   
         SET   RJEXFUND                                                         
         PEND                                                                   
*                                                                               
FCMDUM   PROC                                                                   
         CLEAR RJEXFUND                                                         
         PEND                                                                   
*                                                                               
FCMDBP   PROC                                                                   
         SET   RJEXFBLD                                                         
         PEND                                                                   
*                                                                               
FCMDBM   PROC                                                                   
         CLEAR RJEXFBLD                                                         
         PEND                                                                   
*                                                                               
FCMDIP   PROC                                                                   
         SET   RJEXFITL                                                         
         PEND                                                                   
*                                                                               
FCMDIM   PROC                                                                   
         CLEAR RJEXFITL                                                         
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
WRAPWA   RECORD BEGIN                                                           
WRAPCLEN DS    H                   CC/TRC length (1 or 2)                       
WRAPCCS  DS    CL2                 CC (and possibly TRC) chars                  
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  WRAPWR -- Local routine to write a line to JES being careful                 
*    to wrap the line so it does not exceed the maximum wrap                    
*    line specified.                                                            
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line loc, len                                                    
*                                                                               
WRAPWR   XPROC WRAPWA                                                           
         WITH  (RJEWA,R6)          (Entry assumption)                           
*                                                                               
         CLEAR WRAPWA                                                           
*-                                                                              
*-       Check to see if a maximum wrap length was specified.                   
*-                                                                              
         LTH   R5,RJEWRAP          Max wrap length                              
         IF    POS,BEGIN           Maximum wrap length specified...             
         MVC   WRAPCCS,@R1         Save CC/TRC characters                       
         LA    R15,1               Assume only CC char                          
         IF    RJEFTRC,'LA R15,2'  We have both CC and TRC char                 
         STH   R15,WRAPCLEN        Length of CC/TRC character                   
*                                                                               
         LR    R4,R1               Start of text                                
         AH    R4,WRAPCLEN                                                      
         LR    R3,R0                                                            
         SH    R3,WRAPCLEN         Length of text only                          
*-                                                                              
*-       Check to see if we need to wrap the line.                              
*-                                                                              
         WHILE (R3,GT,R5),BEGIN    Need to wrap line...                         
*-                                                                              
*-       We need to wrap the line and send it as multiple lines.                
*-        (The following code will alter the line buffer area passed            
*-         to us--but that's ok because it is just a work buffer.)              
*-                                                                              
         LR    R0,R5               Maximum length                               
         AH    R0,WRAPCLEN         Account for CC/TRC characters                
         VCALL JESWR               Send line to JES                             
*                                                                               
         MVI   WRAPCCS,C' '        Always use blank as cont CC char             
         AR    R4,R5               Kick ptr to next piece of line               
         SR    R3,R5               Adjust remaining length                      
*                                                                               
         LR    R1,R4                                                            
         SH    R1,WRAPCLEN         Ptr to CC/TRC characters                     
         LH    R15,WRAPCLEN                                                     
         MOVE  R15,@R1,WRAPCCS     Set CC/TRC chars for cont line               
         END                                                                    
*-                                                                              
*-       Write final piece of wrapped line (or entire line if not               
*-         wrapped).                                                            
*-                                                                              
         LR    R1,R4               Line text                                    
         SH    R1,WRAPCLEN         Actual start of line                         
         LR    R0,R3               Text length                                  
         AH    R0,WRAPCLEN         Complete line length                         
         END                                                                    
*-                                                                              
*-       Finally, send the line to JES.                                         
*-                                                                              
         VCALL JESWR               Send line to JES                             
         PEND                                                                   
*                                                                               
         VLTORG                                                                 
*                                                                               
         END   .                                                                
