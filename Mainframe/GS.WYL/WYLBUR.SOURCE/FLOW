FLOW     TITLE 'WYLBUR''s Exec File Flow Control Routines'                      
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         EJECT                                                                  
WYLFLOW  CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
FLOW     IDENT 2025                11:49:23 01/25/02  (SLP)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
         POP   DSECTS                                                           
WYLFLOW  CSECT                                                                  
         DC    C'Always. '                                                      
         EJECT                                                                  
*-                                                                              
*-       LOCAL MACROS                                                           
*-                                                                              
         SPACE 3                                                                
*-                                                                              
*-       FCERR - FLOW CONTROL ERROR !                                           
*-                                                                              
         MACRO                                                                  
&L       FCERR &LOC,&LEN,&LPTR=                                                 
&L       LABEL ,                                                                
         SETMSG &LOC,&LEN                                                       
         AIF   ('&LPTR' EQ '').NOLPTR                                           
         AIF   ('&LPTR' NE '').LPTR                                             
.LPTR    ANOP                                                                   
         $L    R15,&LPTR,OP=L                                                   
         ACALL FCERROR                                                          
         MEXIT                                                                  
.NOLPTR  ANOP                                                                   
         CLEAR R15                                                              
         ACALL FCERROR                                                          
         MEXIT                                                                  
         MEND                                                                   
         EJECT                                                                  
*-                                                                              
*-       COPY DSECTS TO HERE                                                    
*-                                                                              
XLINESZ  EQU   &LINESZ                                                          
         EJECT                                                                  
         COPY  VNENTRY                                                          
         COPY  VPARM                                                            
         SPACE 5                                                                
*-                                                                              
*-       PARM LIST DEFINITIONS                                                  
*-                                                                              
*-       NLIST - PARAMETER NAME LIST (AS DEFINED ON PROC STATEMENT)             
*-       VLIST - PARAMETER VALUE LIST                                           
*-     PLIST - PARAMETER NAME LIST (AS SPECIFIED ON PCALL STATEMENT)            
*-                                                                              
*-       NOTE: THE DIFFERENCE BETWEEN A NLIST AND A PLIST (IN THIS              
*-       PROGRAM) IS AS FOLLOWS:  A NLIST IS A LIST OF NAMES                    
*-       SPECIFIED ON A PROCEDURE DEFINITION,, THE NAMES MUST                   
*-       SIMPLE VARIABLE NAMES.  THESE VARIABLES WILL BECOME                    
*-       LOCAL VARIABLES OF THE PROCEDURE.  A PLIST IS A LIST                   
*-       OF PARM VARIABLE NAMES THAT ARE PASSED TO A PROCEDURE,, AS             
*-       SOME OF THE PARAMETERS MAY BE EXPRESSIONS OR CONSTANTS                 
*-       NOT ALL THE ENTRIES IN A PLIST EXIST,, ONLY VARIABLE                   
*-       NAME ENTRIES EXIST,,, CONSTANT OR EXPRESSION ENTRIES ARE               
*-       ZERO.                                                                  
*-                                                                              
NLIST    RECORD BEGIN ,            LIST OF PARAMETER NAMES                      
         DS    XL(CPPLIMIT*L'VNDESC)                                            
         END                                                                    
*                                                                               
VLIST    RECORD BEGIN ,            LIST OF PARAMETER VALUES                     
         DS    XL(CPPLIMIT*L'VPARM)                                             
         END                                                                    
*                                                                               
PLIST    RECORD BEGIN ,            LIST OF PARM NAMES                           
         DS    XL(CPPLIMIT*L'VNDESC)                                            
         END                                                                    
         EJECT                                                                  
         COPY  XSENTRY                                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FLOW ENTRY (COMMON)                                                          
*                                                                               
*                                                                               
*                                                                               
FWENTRY  RECORD BEGIN ,            FLOW ENTRY                                   
*-                                                                              
*-       FLOW NAME                                                              
*-                                                                              
FWNAME   DS    XL16                FLOW ENTRY NAME                              
         ORG   FWNAME                                                           
FWNLINE  DS    F                   FLOW NAME LINE NUMBER                        
FWNNAME  DS    CL8                 FLOW NAME NAME                               
         ORG                                                                    
*-                                                                              
*-       FLOW INFO  (COMMON)                                                    
*-                                                                              
FWINFO   DS    XL16                                                             
*                                                                               
         ORG   FWINFO                                                           
FWTYPES  FLAG                      FLOW ENTRY TYPES                             
         FLAG  (FWXPROC,1,EQ)      XPROC                                        
         FLAG  (FWPROC,2,EQ)       PROC                                         
         FLAG  (FWLABEL,3,EQ)      LABEL                                        
         FLAG  (FWIF,4,EQ)         IF                                           
         FLAG  (FWELSE,5,EQ)       ELSE                                         
         FLAG  (FWELSEIF,6,EQ)     ELSEIF                                       
         FLAG  (FWDO,7,EQ)         DO                                           
         FLAG  (FWWHILE,8,EQ)      WHILE                                        
         FLAG  (FWCASES,9,EQ)      CASES                                        
         FLAG  (FWCASE,10,EQ)      CASE                                         
         FLAG  (FWREPEAT,11,EQ)    REPEAT                                       
         FLAG  (FWUNTIL,12,EQ)     UNTIL                                        
         FLAG  (FWBEGIN,13,EQ)     BEGIN (AS SEPARATE COMMAND)                  
         FLAG  (FWEND,14,EQ)       END                                          
FWCOMMON DS    XL15                COMMON AREA                                  
         DS    0S(FWINFO+L'FWINFO-*)                                            
*                                                                               
         ORG   FWCOMMON                                                         
         COMMENT ,                 (XPROC, PROC)                                
FWSCOPE  DS    F                   START OF SCOPE                               
FWSCOPND DS    F                   END OF SCOPE                                 
FWALISTL DS    F                   ARG LIST LENGTH                              
*FWALIST  EQU   FWINFO+L'FWINFO,ALIMIT*(L'FWAENTRY) ARG LIST                    
         DS    0S(FWINFO+L'FWINFO-*)                                            
*                                                                               
         ORG   FWCOMMON                                                         
         COMMENT ,                 (LABEL)                                      
FWLBLINE DS    F                   LINE NUMBER OF LABEL                         
         DS    0S(FWINFO+L'FWINFO-*)                                            
         ORG   FWCOMMON                                                         
         COMMENT ,                 (IF,WHILE,UNTIL,CASE)                        
FWTRUE   DS    F                   TRUE BRANCH LINE NUMBER                      
FWFALSE  DS    F                   FALSE BRANCH LINE NUMBER                     
         DS    0S(FWINFO+L'FWINFO-*)                                            
*                                                                               
         ORG   FWCOMMON                                                         
         COMMENT ,                 (DO)                                         
FWDOEND  DS    F                   MATCHING END LINE NUMBER                     
*                                                                               
         ORG   FWCOMMON                                                         
         COMMENT ,                 (REPEAT,CASES,BEGIN,ELSE)                    
         COMMENT                   NO ENTRIES                                   
         DS    0S(FWINFO+L'FWINFO-*)                                            
*                                                                               
         ORG   FWCOMMON                                                         
         COMMENT ,                 (END)                                        
FWDFLAGS FLAG  ,                   END FLAGS                                    
         FLAG  (FWNOOP,1,EQ)       END IS NOOP                                  
         FLAG  (FWBRANCH,2,EQ)     END IS BRANCH                                
         FLAG  (FWXRETN,3,EQ)      END IS XRETURN                               
         FLAG  (FWPRETN,4,EQ)      END IS PRETURN                               
         FLAG  (FWDOLOOP,5,EQ)     END IS DO LOOP INCR AND TEST                 
FWBLINE  DS    F                   BRANCH LINE NUMBER                           
FWLIMIT  DS    F                   DO LOOP LIMIT                                
FWDOVARL DS    F                   LENGTH OF VARIABLE DESCRIPTOR                
FWDOVAR  EQU   FWINFO+L'FWINFO,L'VNDESC  DO LOOP VARIABLE DESCRIPTOR            
         DS    0S(FWINFO+L'FWINFO-*)                                            
*                                                                               
         ORG                                                                    
         END                                                                    
*                                                                               
         EJECT                                                                  
*-                                                                              
*-       CODE GOES HERE                                                         
*-                                                                              
         TITLE 'INITIALIZE/FREE FLOW RECORD GROUP'                              
*box                                                                            
*                                                                               
*                                                                               
*  INITFLOW - INITIALIZE FLOW                                                   
*                                                                               
*                                                                               
*                                                                               
INITFLOW XPROC ,              NOOP                                              
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'FLOW CONTROL COMMANDS'                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  WHILE --                                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER WHILE                                   
*       CPEXLINE - LINE NUMBER CONTAINING WHILE                                 
*                                                                               
*                                                                               
WHILEWA  RECORD BEGIN                                                           
WHILFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
WHILE    XPROC WHILEWA                                                          
         USING FWENTRY,R6                                                       
         LA    R6,WHILFLOW                                                      
*                                                                               
      COMMENT                      WHILE, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'WHILE: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      WHILE, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'WHILE: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS WHILE COMMAND                                                  
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET WHILE FLOW CONTROL INFO                  
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'WHILE   '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid WHILE statement.',,CR                                   
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWWHILE,BEGIN                                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         ACALL EVALCOND                                                         
*                                                                               
         COMMENT                   IF TRUE,,,                                   
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPEXNEXT,FWTRUE     SET NEXT COMMAND                             
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF WHILE (...) COMMAND                       
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                 R1,R0 - COMMAND LOC, LEN                     
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF WHILE (...) BEGIN                         
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         MVC   CPEXNEXT,FWFALSE    SET NEXT COMMAND                             
         B     CVNEXT              AND GO DO IT !                               
         END                                                                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  IF --                                                                        
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER IF                                      
*       CPEXLINE - LINE NUMBER CONTAINING IF                                    
*                                                                               
*                                                                               
IFWA     RECORD BEGIN                                                           
IFFLOW   DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
IF       XPROC IFWA                                                             
         USING FWENTRY,R6                                                       
         LA    R6,IFFLOW                                                        
*                                                                               
         COMMENT                   CHECK FOR SPIRES IF ...                      
         SCPUSH ,                                                               
         SCAN                                                                   
         SCANCHK                                                                
         SCPOP  ,                                                               
         IF    ((@R1,NE,'('),AND,(R0,POS)),BEGIN                                
         VCALL ORVCPASS                                                         
         END                                                                    
*-                                                                              
*-                                                                              
*-       OLD MODE IF                                                            
*-                                                                              
*-                                                                              
         COMMENT                   IF OLD MODE OR FROM TERMINAL                 
         IF   (^CPFEXEC,OR,CPFXOLD),BEGIN                                       
*                                                                               
         ACALL ZAPIF               COMPATIBILITY ZAP FOR IF                     
*                                                                               
         COMMENT                   EVALUATE IF CONDITIONAL EXPR                 
         ACALL EVALCOND            RETURNS: CC=Z IS TRUE, CC=NZ FALSE           
*                                                                               
         COMMENT                   IF TRUE                                      
         IF    (R15,NZ),BEGIN                                                   
         SET   CPGFLG4.CPF4TRUE    SET TRUE FLAG                                
         SET   CPGFLG4.CPFNPREP    DON'T REPREPROCESS THIS COMMAND              
         SCINFO ,,                 R1,R0 - COMMAND LOC, LEN                     
         VCALL EDTCOMMV            EXECUTE COMMAND AFTER "IF (EXPR)"            
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         CLEAR CPGFLG4.CPF4TRUE    CLEAR TRUE FLAG                              
         B     CVNEXT              EXECUTE NEXT COMMAND                         
         END                                                                    
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-                                                                              
*-       EXTENDED MODE IF                                                       
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET IF FLOW CONTROL INFO                     
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'IF      '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid IF statement.',,CR                                      
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWIF,BEGIN                                                      
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         ACALL EVALCOND                                                         
*                                                                               
         COMMENT                   IF TRUE,,,                                   
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPEXNEXT,FWTRUE     SET NEXT COMMAND                             
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF IF (...) COMMAND                          
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                 R1,R0 - COMMAND LOC, LEN                     
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF IF (...) BEGIN                            
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         MVC   CPEXNEXT,FWFALSE    SET NEXT COMMAND                             
         B     CVNEXT              AND GO DO IT !                               
         END                                                                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*   ZAPIF - COMPATIBILITY ZAP TO IF,,,                                          
*                                                                               
*   ALLOW (TERM, OPERATOR, TERM) AS VALID IF EXPRESSION                         
*   E.G.  (5,EQ,5) WE ACCEPT, IN REALITY WE CONVERT IT                          
*   TO (5 EQ 5) AND RETURN.  DETAILS BELOW                                      
*                                                                               
*                                                                               
*                                                                               
ZAPWA    RECORD BEGIN ,                                                         
ZAPVALUE DS    XL(L'XSLENTRY)      WORK AREA FOR EXPR VALUE TYPE                
         END                                                                    
*                                                                               
*                                                                               
ZAPIF    PROC  ZAPWA                                                            
         USING XSENTRY,R2                                                       
         LA    R2,ZAPVALUE                                                      
         SCINFO ,                  R1,R0 - LOC LEN OF EXPRESSION                
         OSCINFO ,                 ** DEBUG ** OLD-NEW COMPATIBILTY             
         IF    (R0,NP),ZAPDONE                                                  
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN  SKIP BLANKS                 
         LA    R1,1(R1)                                                         
         END                                                                    
         CLI   @R1,C'('                             SKIP "("                    
         IF    NE,ZAPDONE                                                       
         LA    R1,1(R1)                                                         
         SR    R0,R1                                                            
         LA    R2,ZAPVALUE                                                      
         SET   CPFZAPIF            Set kludge flag                      **ISU** 
         VCALL SCANEXPR            SCAN PAST 1ST EXPRESSION                     
         CLEAR CPFZAPIF            Clear kludge flag                    **ISU** 
         IF    (R15,POS),CVERROR                                                
         AR    R0,R1                                                            
         IF    (@R1,NE,','),ZAPDONE                                             
         IF    (@R1,EQ,','),BEGIN  IF (X, ...       REMOVE COMMA                
         MVI   @R1,C' '                                                         
         END                                                                    
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN  SKIP BLANKS                 
         LA    R1,1(R1)                                                         
         END                                                                    
         LA    R1,2(R1)                             SKIP OPERATOR,              
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN  SKIP BLANKS                 
         LA    R1,1(R1)                                                         
         END                                                                    
         IF    (@R1,EQ,','),BEGIN                   REMOVE COMMA                
         MVI   @R1,C' '                                                         
         END                                                                    
*                                                                               
ZAPDONE  LABEL ,                                                                
         PEND  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ELSE --                                                                      
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER ELSE                                    
*       CPEXLINE - LINE NUMBER CONTAINING ELSE                                  
*                                                                               
*                                                                               
ELSEWA   RECORD BEGIN                                                           
ELSEFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
ELSE     XPROC ELSEWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,ELSEFLOW                                                      
*-                                                                              
*-                                                                              
*-       OLD MODE ELSE                                                          
*-                                                                              
*-                                                                              
         COMMENT                   IF OLD MODE OR FROM TERMINAL                 
         IF   (^CPFEXEC,OR,CPFXOLD),BEGIN                                       
         IF    ^CPGFLG4.CPF4TRUE,BEGIN    IF LAST IF WAS FALSE,,,               
         SET   CPGFLG4.CPFNPREP             DON'T REPREPROCESS                  
         SCINFO ,                                                               
         VCALL EDTCOMMV                     AND EXECUTE COMMAND                 
         END                                                                    
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-                                                                              
*-       EXTENDED MODE ELSE                                                     
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET IF FLOW CONTROL INFO                     
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'ELSE    '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid ELSE statement.',,CR                                    
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWELSE,BEGIN                                                    
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   PROCESS ELSE,,                               
         MVC   CPEXNEXT,=CL4'NEXT' SET NEXT COMMAND                             
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF ELSE COMMAND                              
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                R1,R0 - COMMAND LOC, LEN                      
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF ELSE BEGIN                                
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ELSEIF --                                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER ELSEIF                                  
*       CPEXLINE - LINE NUMBER CONTAINING ELSEIF                                
*                                                                               
*                                                                               
LSIFWA   RECORD BEGIN                                                           
LSIFFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
ELSEIF   XPROC LSIFWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,LSIFFLOW                                                      
*                                                                               
      COMMENT                      ELSEIF,  INVALID FROM TERMINAL               
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'ELSEIF: statement ignored.',,CR                                    
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      ELSEIF, INVALID IN OLD EXEC MODE             
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'ELSEIF: statement invalid.',,CR                                    
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS ELSEIF COMMAND                                                 
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET ELSEIF FLOW CONTROL INFO                 
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'ELSEIF  '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid ELSEIF statement.',,CR                                  
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWELSEIF,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         ACALL EVALCOND                                                         
*                                                                               
         COMMENT                   IF TRUE,,,                                   
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPEXNEXT,FWTRUE     SET NEXT COMMAND                             
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF ELSEIF (...) COMMAND                      
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                R1,R0 - COMMAND LOC, LEN                      
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF ELSEIF (...) BEGIN                        
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         MVC   CPEXNEXT,FWFALSE    SET NEXT COMMAND                             
         B     CVNEXT              AND GO DO IT !                               
         END                                                                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  REPEAT --                                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER REPEAT                                  
*       CPEXLINE - LINE NUMBER CONTAINING REPEAT                                
*                                                                               
*                                                                               
REPEATWA RECORD BEGIN                                                           
REPTFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
REPEAT   XPROC REPEATWA                                                         
         USING FWENTRY,R6                                                       
         LA    R6,REPTFLOW                                                      
*                                                                               
      COMMENT                      REPEAT, INVALID FROM TERMINAL                
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'REPEAT: statement ignored.',,CR                                    
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      REPEAT, INVALID IN OLD EXEC MODE             
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'REPEAT: statement invalid.',,CR                                    
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS REPEAT COMMAND                                                 
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET REPEAT FLOW CONTROL INFO                 
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'REPEAT  '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid REPEAT statement.',,CR                                  
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWREPEAT,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   PROCESS REPEAT                               
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF REPEAT COMMAND                            
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                R1,R0 - COMMAND LOC, LEN                      
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF REPEAT BEGIN                              
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  UNTIL --                                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER UNTIL                                   
*       CPEXLINE - LINE NUMBER CONTAINING UNTIL                                 
*                                                                               
*                                                                               
UNTILWA  RECORD BEGIN                                                           
UNTLFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
UNTIL    XPROC UNTILWA                                                          
         USING FWENTRY,R6                                                       
         LA    R6,UNTLFLOW                                                      
*                                                                               
      COMMENT                      UNTIL, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'UNTIL: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      UNTIL, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'UNTIL: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS UNTIL COMMAND                                                  
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET UNTIL FLOW CONTROL INFO                  
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'UNTIL   '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid UNTIL statement.',,CR                                   
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWUNTIL,BEGIN                                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         ACALL EVALCOND                                                         
*                                                                               
         COMMENT                   IF TRUE,,,                                   
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPEXNEXT,FWTRUE     SET NEXT COMMAND                             
         VCALL NOOPTS              GARBAGE CHECK AFTER UNTIL (...)              
         B     CVNEXT              DO NEXT COMMAND                              
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         MVC   CPEXNEXT,FWFALSE    SET NEXT COMMAND                             
         VCALL NOOPTS              GARBAGE CHECK AFTER UNTIL (...)              
         B     CVNEXT              DO NEXT COMMAND                              
         END                                                                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CASES --                                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER CASES                                   
*       CPEXLINE - LINE NUMBER CONTAINING CASES                                 
*                                                                               
*                                                                               
CASESWA RECORD BEGIN                                                            
CASESFLW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
CASES    XPROC CASESWA                                                          
         USING FWENTRY,R6                                                       
         LA    R6,CASESFLW                                                      
*                                                                               
      COMMENT                      CASES, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'CASES: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      CASES, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'CASES: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS CASES COMMAND                                                  
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET CASES FLOW CONTROL INFO                  
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'CASES   '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid CASES statement.',,CR                                   
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWCASES,BEGIN                                                   
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   PROCESS CASES BEGIN, (NOOP)                  
         ACALL CHKBEGIN            CHECK CASES BEGIN                            
         BZ    CVNEXT              GOT BEGIN                                    
         COMMENT                   NO BEGIN,                                    
         ACALL CHKOPTS             GIVE ERROR                                   
         ERROR 'CASES BEGIN must be specified.'                                 
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CASE --                                                                      
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER CASE                                    
*       CPEXLINE - LINE NUMBER CONTAINING CASE                                  
*                                                                               
*                                                                               
CASEWA   RECORD BEGIN                                                           
CASEFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
CASE     XPROC CASEWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,CASEFLOW                                                      
*                                                                               
      COMMENT                      CASE,  INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'CASE: statement ignored.',,CR                                      
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      CASE, INVALID IN OLD EXEC MODE               
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'CASE: statement invalid.',,CR                                      
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS CASE COMMAND                                                   
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET CASE FLOW CONTROL INFO                   
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'CASE    '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid CASE statement.',,CR                                    
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWCASE,BEGIN                                                    
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         ACALL EVALCOND                                                         
*                                                                               
         COMMENT                   IF TRUE,,,                                   
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPEXNEXT,FWTRUE     SET NEXT COMMAND                             
         ACALL CHKBEGIN                                                         
         IF    NZ,BEGIN            IF CASE (...) COMMAND                        
         COMMENT                   EXECUTE COMMAND                              
         SET   CPGFLG4.CPFNPREP    DON'T REPROCESS THIS COMMAND                 
         SCINFO ,,                R1,R0 - COMMAND LOC, LEN                      
         VCALL EDTCOMMV            EXEC CMD PART OF WHILE (..) CMD              
         END                                                                    
         COMMENT                   IF CASE (...) BEGIN                          
         B     CVNEXT              JUST EXECUTE NEXT COMMAND                    
         END                                                                    
*                                                                               
         COMMENT                   IF FALSE                                     
         ELSE  BEGIN                                                            
         MVC   CPEXNEXT,FWFALSE    SET NEXT COMMAND                             
         B     CVNEXT              AND GO DO IT !                               
         END                                                                    
*                                                                               
         COMMENT                   WE EXIT ABOVE, VIA CVNEXT                    
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BEGIN --                                                                     
*                                                                               
*  NOTE: BEGIN COMMAND IS NEVER EXECUTED, IT IS ALWAYS EXECUTED                 
*    AFTER AN IF, WHILE, ELSEIF, ELSE, ETC.  ANY ONE REACHING                   
*    HERE HAS AN ERROR.                                                         
*                                                                               
*                                                                               
*                                                                               
BEGIN    XPROC ,                                                                
*                                                                               
      COMMENT                      BEGIN, INVLID FROM TERMINAL                  
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'BEGIN: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      BEGIN, INVALID IN OLD EXECS                  
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'BEGIN: statement invalid.',,CR                                     
      ERROR 'BEGIN-END blocks valid only in Extended EXEC mode.'                
      END                                                                       
*                                                                               
      COMMENT                      BEGIN NEVER DIRECTLY EXECUTED                
      IF    ^CPFXTEND,BEGIN                                                     
      BOMB                                                                      
      END                                                                       
      TSEG  'BEGIN: statement invalid.',,CR                                     
      ERROR 'BEGIN must follow IF, ELSE, WHILE, ..etc. statement.'              
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                   NEVER EXECUTED, EXIT ABOVE                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  END --                                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER END                                     
*       CPEXLINE - LINE NUMBER CONTAINING END                                   
*                                                                               
*                                                                               
ENDWA    RECORD BEGIN                                                           
ENDFLOW  DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
END      XPROC ENDWA                                                            
         USING FWENTRY,R6                                                       
         LA    R6,ENDFLOW                                                       
*                                                                               
         COMMENT                   END, INVALID FROM TERMINAL                   
         IF    ^CPFEXEC,BEGIN                                                   
*$ END AT TERMINAL MAY BE FROM SPIRES XEQ, SO WE TREAT                          
*$ IT AS AN IMPLIED DELETE OR INSERT                                            
         COMMENT                   TREAT 'END' FROM TERMINAL AS                 
         COMMENT ,                 IMPLIED DELETE OR INSERT                     
         VCALL IMPLYEND            DOES NOT RETURN                              
         B     CVNEXT                                                           
*        TSEG  'Flow Control Statements may be used only '                      
*        TSEG  'in EXEC files.  Statement ignored.',,CR                         
*        TSEG  '(Note: Use the PUTLINE statement to add lines to the '          
*        TSEG  'ACTIVE file.) ',,CR                                             
*        B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   TREAT 'END' IN OLD EXECS AS                  
         COMMENT ,                 IMPLIED DELETE OR INSERT                     
         IF    CPFXOLD,BEGIN                                                    
         VCALL IMPLYEND            DOES NOT RETURN                              
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       PROCESS 'BEGIN-END BLOCK'  END                                         
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR GARBAGE AFTER END                  
         VCALL NOOPTS                                                           
*                                                                               
         COMMENT                   GET END FLOW CONTROL INFO                    
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,CPEXLINE                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid END.  '                                                 
         ERROR 'Dynamic flow control statements invalid.'                       
         END                                                                    
         IF    ^FWEND,BEGIN                                                     
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NOOP, GET NEXT INSTRUCTION                
         IF    FWNOOP,BEGIN                                                     
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF BRANCH, BRANCH                            
         IF    FWBRANCH,BEGIN                                                   
         MVC   CPEXNEXT,FWBLINE    SET NEW NEXT LINE                            
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF XRETURN, XRETURN                          
         IF    FWXRETN,BEGIN                                                    
         LA    R1,=CL4'    '                                                    
         CLEAR R0                                                               
         SCINIT (R1),(R0)                                                       
         VCALL XRETURN                                                          
         BOMB  , (XRETURN DOES NOT RETURN)                                      
         END                                                                    
*                                                                               
         COMMENT                   IF RETURN, RETURN                            
         IF    FWPRETN,BEGIN                                                    
         LA    R1,=CL4'    '                                                    
         CLEAR R0                                                               
         SCINIT (R1),(R0)                                                       
         VCALL PRETURN                                                          
         BOMB  , (XRETURN DOES NOT RETURN)                                      
         END                                                                    
*                                                                               
         COMMENT                   IF DO LOOP END,,,                            
         IF    FWDOLOOP,BEGIN                                                   
         BOMB                      NOT YET IMPLEMENTED                          
         END                                                                    
*                                                                               
         COMMENT                   IF NONE OF THE ABOVE                         
         BOMB                                                                   
*                                                                               
         PEND  ,              A HABIT, NEVER EXECUTED                           
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LABEL: --                                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - POINTS TO ENTIRE COMMAND                                       
*                                                                               
*  NOTE:                                                                        
*       THIS ROUTINE IS CALLED FROM XCMDSCAN                                    
*                                                                               
*                                                                               
LABEL    XPROC ,                                                                
*                                                                               
         COMMENT                   CHECK FOR EXEC FILE                          
         IF    ^CPFEXEC,BEGIN                                                   
         TSEG  'Labels may be used only in EXEC files.  '                       
         ERROR 'Command ignored.'                                               
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR VALID LABEL                        
         SCINFO ,                                                               
         ACALL SCNLABEL                                                         
         IF    M,BEGIN                                                          
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid label name.'                                          
         END                                                                    
         IF    POS,BEGIN                                                        
         SCAN  ,                                                                
         SCANCHK                                                                
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid label name.'                                          
         END                                                                    
*                                                                               
*$ WE DO NOT ALLOW STATEMENTS ON SAME LINE AS LABEL                             
*$ FOR NOW !!                                                                   
*                                                                               
         COMMENT                   CHECK FOR VALID LABEL STATEMENT              
         ACALL CHKLABEL            WE ALLOW LABEL OR NOTHING.                   
         BNZ   CVERROR                                                          
         B     CVNEXT              ----->>>> WE EXIT HERE !!!                   
*                                                                               
*$ WE DO NOT ALLOW STATEMENTS ON SAME LINE AS LABEL                             
*$ FOR NOW !!                                                                   
         COMMENT                   SET UP COMMAND WITHOUT LABEL                 
         STH   R0,CPCMDLEN         IN CP COMMAND AREA                           
         LR    R3,R0                                                            
         MOVE  R3,CPCMD,@R1                                                     
         LA    R1,CPCMD                                                         
         SET   CPGFLG4.CPFNPREP    DON'T REPREPROCESS                           
         VCALL EDTCOM              GO EXECUTE COMMAND                           
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                   NEVER EXECUTED, WE EXIT ABOVE                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GOTO --                                                                      
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER GOTO                                    
*       CPEXLINE - LINE NUMBER CONTAINING GOTO                                  
*                                                                               
*                                                                               
GOTOWA   RECORD BEGIN                                                           
GOTOFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
GOTO     XPROC GOTOWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,GOTOFLOW                                                      
*                                                                               
      COMMENT                      GOTO, INVALID FROM TERMINAL                  
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'GOTO: statement ignored.',,CR                                      
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      GOTO, INVALID IN OLD EXEC MODE               
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'GOTO: statement invalid.',,CR                                      
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS GOTO COMMAND                                                   
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET LINE NUMBER OF LABEL                     
         ACALL LABELLNO                                                         
         LR    R2,R0               R2 - LABEL LINE NUMBER                       
         VCALL NOOPTS              CHECK FOR JUNK AFTER LABEL                   
*                                                                               
         ST    R2,CPEXNEXT         SET NEXT LINE NUMBER                         
         B     CVNEXT              EXECUTE NEW NEXT LINE NUMBER                 
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XEXIT --                                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - REMAINING STRING AFTER XEXIT                                   
*       CPEXLINE - LINE NUMBER CONTAINING XEXIT                                 
*                                                                               
*                                                                               
XITWA    RECORD BEGIN                                                           
XITFLOW  DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
XEXIT    XPROC XITWA                                                            
         USING FWENTRY,R6                                                       
         LA    R6,XITFLOW                                                       
*                                                                               
      COMMENT                      XEXIT, INVALID FROM TERMINAL                 
      IF    ^CPFEXEC,BEGIN                                                      
      TSEG  'XEXIT: statement ignored.',,CR                                     
      ERROR 'Flow control statements may be used only in EXEC files.'           
      END                                                                       
*                                                                               
      COMMENT                      XEXIT, INVALID IN OLD EXEC MODE              
      IF    CPFXOLD,BEGIN                                                       
      TSEG  'XEXIT: statement invalid.',,CR                                     
      ERROR 'Statement may be used only in Extended EXEC mode.'                 
      END                                                                       
*-                                                                              
*-                                                                              
*-       PROCESS XEXIT COMMAND                                                  
*-                                                                              
*-                                                                              
         COMMENT                   ENTRANCE EXAM                                
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET LINE NUMBER OF LABEL                     
         ACALL XLBLLNO                                                          
         LR    R2,R0               R2 - LABEL LINE NUMBER                       
         VCALL NOOPTS              CHECK FOR JUNK AFTER LABEL                   
*                                                                               
         COMMENT                   RESET XPROC ENVIRONMENT AND                  
         COMMENT                   GOTO NEW LINE NUMBER                         
         LR    R0,R2               R0 - NEW LINE NUMBER                         
         VCALL XXXEXIT             ROUTINE DOES NOT RETURN                      
*                                                                               
         BOMB  ,                   NEVER EXECUTED, WE EXIT ABOVE                
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                   NEVER EXECUTED, WE EXIT ABOVE                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  THEN --                                                                      
*                                                                               
*                                                                               
*                                                                               
THEN     XPROC ,                                                                
*                                                                               
         COMMENT                   IF OLD MODE OR FROM TERMINAL,                
         IF    (^CPFEXEC,OR,CPFXOLD),BEGIN                                      
         IF    CPGFLG4.CPF4TRUE,BEGIN    IF LAST IF WAS TRUE,,,                 
         SET   CPGFLG4.CPFNPREP            DON'T PREPROCESS                     
         SCINFO                                                                 
         VCALL EDTCOMMV                    AND EXECUTE COMMAND                  
         END                                                                    
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF EXTENDED MODE,                            
         IF    ^CPFXTEND,BEGIN                                                  
         BOMB                                                                   
         END                                                                    
         ERROR 'THEN statement invalid in Extended EXEC files.'                 
*                                                                               
         COMMENT                   WE EXIT ABOVE                                
         BOMB                                                                   
         PEND  ,              A HABIT, NEVER EXECUTED                           
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'FLOW CONTROL COMMANDS - Common Routines'                        
*box                                                                            
*                                                                               
*                                                                               
*  EVALCOND - EVALUATE CONDITIONAL EXPRESSION                                   
*                                                                               
*  USED TO EVALUATE:                                                            
*     IF ( ..CONDITIONAL EXPRESSION.. )  ... COMMAND ..                         
*     WHILE ( ..CONDITIONAL EXPRESSION.. )  ... COMMAND ..                      
*     ETC..                                                                     
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - SCAN CONTROL BLOCK, POINTS AT OR BEFORE '('                    
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UPDATED, POINTS PAST ')'                                       
*       R15 - CC=ZERO, FALSE  (R15=0)                                           
*             CC=NZ, TRUE     (R15=1)                                           
*             ANY ERRORS IN EXPRESSION SYNTAX EXIT VIA CVERROR.                 
*                                                                               
*                                                                               
ECWA     RECORD BEGIN                                                           
ECVAL    DS    XL(L'VPARM)                                                      
         END                                                                    
*                                                                               
*                                                                               
EVALCOND PROC  ECWA                                                             
         USING XSENTRY,R6                                                       
         LA    R6,ECVAL                                                         
*                                                                               
         COMMENT                   SCAN CONDITIONAL EXPRESSION                  
         SCAN ,                                                                 
         SCANCHK                                                                
         IF    (@R1,NE,'('),BEGIN                                               
         VCALL NOTVALID                                                         
         END                                                                    
         IF    (R0,NP),CVABSENT                                                 
*                                                                               
         COMMENT                   EVALUATE CONDITIONAL EXPRESSION              
         INCR  R1                                                               
         DECR  R0                  SKIP "("                                     
         LA    R2,XSENTRY                                                       
         VCALL EVALEXPR            EVALUATE EXPRESSION                          
         BP    CVERROR                                                          
         BM    CVABSENT                                                         
*                                                                               
         COMMENT                   CHECK FOR ERRORS                             
         IF    ((R0,NP),OR,(@R1,NE,')')),BEGIN                                  
         ACALL NOPAREN ERROR                                                    
         END                                                                    
         IF    ^XSBOOL,BEGIN                                                    
         ACALL NOTBOOL ERROR                                                    
         END                                                                    
*                                                                               
         COMMENT                   GET BOOLEAN VALUE                            
         L     R15,=F'-1'                                                       
         LA    R4,CVPKONE                                                       
         CP    XSVALUE(NL),0(NL,R4)                                             
         IF    EQ,BEGIN            IF TRUE                                      
         LA    R15,1                                                            
         END                                                                    
         LA    R4,CVPKZERO                                                      
         CP    XSVALUE(NL),0(NL,R4)                                             
         IF    EQ,BEGIN            IF FALSE                                     
         CLEAR R15                                                              
         END                                                                    
         IF    (R15,NEG),BEGIN                                                  
         BOMB IF NEITHER                                                        
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKBEGIN - CHECK FOR BEGIN                                                   
*                                                                               
*  THIS ROUTINE SCANS FOR A "BEGIN".  IT ALSO CHECKS FOR                        
*  COMPOUND CONTROL STATEMENTS, FOR EXAMPLE,  'IF (...) IF                      
*  (...), BEGIN '.  COMPOUND CONTROL STATEMENTE ARE                             
*  INVALID AND DETECTED AS ERRORS.                                              
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - JUST PAST CONDITIONAL EXPRESSION                               
*                EG. FOR," IF (EXPR)  CMD", SCANCB POINTS TO CMD                
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UNCHANGED                                                      
*       R15 - CC=Z, BEGIN FOUND                                                 
*             CC=NZ, BEGIN NOT FOUND                                            
*             INVALID COMMANDS (IE. COMPOUND CONTROL STATEMENTS)                
*             EXIT VIA CVERROR.                                                 
*                                                                               
*                                                                               
CHKBEGIN PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR BEGIN                               
         SCPUSH ,                                                               
         SCAN CHECKPRT                                                          
         SCANCHK                                                                
         IF    (R15,EQ,4),' CLEAR R15 '    BEGIN FOUND                          
         IF    (R15,EQ,8),' LA R15,4  '    BEGIN NOT FOUND                      
         SCPOP ,                                                                
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       COMMAND KEYWORDS                                                       
*-                                                                              
CHECKPRT SCKW  ,NOTBEGIN,NULL                                                   
         SCKW  BEGIN,CMDBEGIN                                                   
         SCKW  IF,BADCMD                                                        
         SCKW  ELSE,BADCMD                                                      
         SCKW  THEN,BADCMD                                                      
         SCKW  ELSEIF,BADCMD                                                    
         SCKW  DO,BADCMD                                                        
         SCKW  WHILE,BADCMD                                                     
         SCKW  PROC,BADCMD                                                      
         SCKW  XPROC,BADCMD                                                     
         SCKW  CASES,BADCMD                                                     
         SCKW  CASE,BADCMD                                                      
         SCKW  REPEAT,BADCMD                                                    
         SCKW  UNTIL,BADCMD                                                     
         SCKW  END,BADCMD                                                       
         SCKW  ,NOTBEGIN                                                        
         SPACE 5                                                                
*-                                                                              
*-       begin found                                                            
*-                                                                              
CMDBEGIN PROC  ,                   GOT BEGIN                                    
         VCALL NOOPTS              CHECK FOR GARBAGE BEYOND 'BEGIN'             
         LA    R15,4               FOUND VALID 'BEGIN'                          
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       got regular command, not begin                                         
*-                                                                              
NOTBEGIN PROC  ,                                                                
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       invalid keyword found                                                  
*-                                                                              
BADCMD   PROC  ,                                                                
         SCBKUP ,                  BACKUP SCAN                                  
         ACALL CHKOPTS                                                          
         TCCR                                                                   
         ERROR 'Compound control statements not allowed.'                       
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHKOPTS - CHECK FOR OPTIONS,  THEY ARE INVALID                               
*                                                                               
*  IF INVALID OPTIONS, TSEG ERROR MESSAGE AND RETURN                            
*                                                                               
*  ON ENTRY:                                                                    
*        SCANCB - SCAN CONTROL BLOCK                                            
*  ON EXIT:                                                                     
*        R15 - ZERO IF NONE,                                                    
*        R15 - NZ, IF INVALID OPTIONS                                           
*                                                                               
*                                                                               
CHKOPTS  PROC  ,                                                                
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,NZ),BEGIN                                                    
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.',,CR                                                 
         LA    R15,4                                                            
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LABELLNO - GET LINE NUMBER OF LABEL                                          
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - SET UP TO POINT AT LABEL                                       
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UPDATED                                                        
*       R0 - LINE NUMBER OF LABEL                                               
*       ANY ERRORS EXIT VIA CVERROR                                             
*                                                                               
*  NOTE: WE CHECK FOR VALID SCOPE, LABEL MUST BE IN CURRENT PROC                
*                                                                               
*                                                                               
*                                                                               
LLWA     RECORD BEGIN                                                           
LLFLOW   DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
LABELLNO XPROC LLWA                                                             
         USING FWENTRY,R6                                                       
         LA    R6,LLFLOW                                                        
*                                                                               
         COMMENT                   SCAN LABEL NAME                              
         CLEAR FWENTRY                                                          
         LA    R2,FWNAME                                                        
         ACALL SCNLNAME                                                         
*                                                                               
         COMMENT                   GET LABEL FLOW CONTROL INFO                  
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': undefined label.'                                             
         END                                                                    
         IF    ^FWLABEL,BEGIN                                                   
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': undefined label.'                                             
         END                                                                    
*                                                                               
         COMMENT                   CHECK LABEL SCOPE                            
         L     R1,FWLBLINE                                                      
         IF    ((R1,LT,CPSCOPE),OR,(R1,GT,CPSCOPD)),BEGIN                       
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': not in scope of current procedure.'                           
         END                                                                    
*                                                                               
         COMMENT                   GET LABEL LINE NUMBER, RETURN                
         L     R0,FWLBLINE                                                      
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  XLBLLNO - GET LINE NUMBER OF LABEL                                           
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - SET UP TO POINT AT LABEL                                       
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UPDATED                                                        
*       R0 - LINE NUMBER OF LABEL                                               
*       ANY ERRORS EXIT VIA CVERROR                                             
*                                                                               
*  NOTE: WE CHECK FOR VALID SCOPE, LABEL MUST BE IN XPROC BODY                  
*                                                                               
*                                                                               
*                                                                               
XLLWA    RECORD BEGIN                                                           
XLLFLOW  DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
XLBLLNO  XPROC XLLWA                                                            
         USING FWENTRY,R6                                                       
         LA    R6,XLLFLOW                                                       
*                                                                               
         COMMENT                   SCAN LABEL NAME                              
         CLEAR FWENTRY                                                          
         LA    R2,FWNAME                                                        
         ACALL SCNLNAME                                                         
*                                                                               
         COMMENT                   GET LABEL FLOW CONTROL INFO                  
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
         IF    NZ,BEGIN                                                         
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': undefined label.'                                             
         END                                                                    
         IF    ^FWLABEL,BEGIN                                                   
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': undefined label.'                                             
         END                                                                    
*                                                                               
         COMMENT                   CHECK LABEL SCOPE                            
         L     R1,FWLBLINE                                                      
         IF    ((R1,LT,CPXSCOPE),OR,(R1,GT,CPXSCOPD)),BEGIN                     
         TSEG  FWNAME,L'FWNAME,T                                                
         ERROR ': not in scope of XPROC procedure.'                             
         END                                                                    
*                                                                               
         COMMENT                   GET LABEL LINE NUMBER, RETURN                
         L     R0,FWLBLINE                                                      
         CLEAR R15                                                              
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNLNAME - SCAN LABEL NAME                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      SCANCB - POINTS AT LABEL                                                 
*      @R2 - LABEL RETURN AREA                                                  
*                                                                               
*  ON EXIT:                                                                     
*      SCANCB - UPDATED                                                         
*      @R2 - LABEL, IF ANY  (LEFT JUSTIFIED PADDED W/BLANKS)                    
*      R15 - CC=ZERO                                                            
*      ALL ERRORS EXIT VIA CVERROR                                              
*                                                                               
*  NOTE: CALLED BY GOTO COMMAND                                                 
*                                                                               
*                                                                               
SCNLWA   RECORD BEGIN                                                           
SCNLLOC  DS    F                   LOCATION OF LINE AT ENTRY                    
SCNLLEN  DS    F                   LENGTH OF LINE AT ENTRY                      
SCNLAREA DS    F                   ADDRESS OF LABEL SAVE AREA                   
SCNLTEMP DS    XL(L'VNENTRY)       TEMP WORK AREA FOR NAME                      
         DS    0S(L'VNNAME-L'FWNAME)    FLOW,VAR NAMES MUST                     
         DS    0S(L'FWNAME-L'VNNAME)    BE SAME LENGTH                          
         END                                                                    
*                                                                               
*                                                                               
SCNLNAME PROC  SCNLWA                                                           
         USING VNENTRY,R6                                                       
         LA    R6,SCNLTEMP                                                      
         SCINFO                                                                 
         ST    R0,SCNLLEN                                                       
         ST    R1,SCNLLOC                                                       
         ST    R2,SCNLAREA                                                      
*                                                                               
         COMMENT                   SCAN FOR LABEL NAME                          
         L     R0,SCNLLEN                                                       
         L     R1,SCNLLOC                                                       
         LA    R2,SCNLTEMP                                                      
         VCALL SCNVNAME                                                         
*                                                                               
         COMMENT                   IF VALID LABEL                               
         COMMENT                                                                
         IF    ((R15,Z),AND,(VNPREFIX,EQ,0)),BEGIN                              
         L     R2,SCNLAREA                                                      
         MVC   0(L'FWNAME,R2),VNNAME     SAVE NAME                              
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF NO LABEL                                  
         ELSEIF (R15,NEG),BEGIN                                                 
         ERROR  'Missing label name.'                                           
         END                                                                    
*                                                                               
         COMMENT                   IF INVALID LABEL                             
         ELSEIF (R15,POS),BEGIN                                                 
         L     R0,SCNLLEN                                                       
         L     R1,SCNLLOC                                                       
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid label name.'                                          
         END                                                                    
*                                                                               
         COMMENT                   ANYTHING ELSE IS INVALID TOO                 
         ELSE  BEGIN               (IE. IF PREFIXED LABELS)                     
         L     R0,SCNLLEN                                                       
         L     R1,SCNLLOC                                                       
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid label name.'                                          
         END                                                                    
*                                                                               
         SCINIT (R1),(R0)                                                       
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNPNAME - SCAN PROCEDURE NAME                                               
*                                                                               
*  ON ENTRY:                                                                    
*      SCANCB - POINTS AT PROCEDURE NAME                                        
*      @R2 - LABEL RETURN AREA                                                  
*                                                                               
*  ON EXIT:                                                                     
*      SCANCB - UPDATED                                                         
*      @R2 - NAME, IF VALID (LEFT JUSTIFIED PADDED W/BLANKS)                    
*      R15 - CC=ZERO                                                            
*            CC=NZ, IF NAME INVALID, ERR MSG IS TSEG'D                          
*                                                                               
*  NOTE: CALLED BY PCALL COMMAND                                                
*                                                                               
*                                                                               
SCNPWA   RECORD BEGIN                                                           
SCNPLOC  DS    F                   LOCATION OF LINE AT ENTRY                    
SCNPLEN  DS    F                   LENGTH OF LINE AT ENTRY                      
SCNPAREA DS    F                   ADDRESS OF NAME SAVE AREA                    
SCNPTEMP DS    XL(L'VNENTRY)       TEMP WORK AREA FOR NAME                      
         DS    0S(L'VNNAME-L'FWNAME)    FLOW,VAR NAMES MUST                     
         DS    0S(L'FWNAME-L'VNNAME)    BE SAME LENGTH                          
         END                                                                    
*                                                                               
*                                                                               
SCNPNAME XPROC SCNPWA                                                           
         USING VNENTRY,R6                                                       
         LA    R6,SCNPTEMP                                                      
         SCINFO ,                                                               
         ST    R0,SCNPLEN                                                       
         ST    R1,SCNPLOC                                                       
         ST    R2,SCNPAREA                                                      
*                                                                               
         COMMENT                   SCAN FOR PROCEDURE NAME                      
         L     R0,SCNPLEN                                                       
         L     R1,SCNPLOC                                                       
         LA    R2,SCNPTEMP                                                      
         VCALL SCNVNAME                                                         
*                                                                               
         COMMENT                   IF VALID NAME                                
         COMMENT                                                                
         IF    ((R15,Z),AND,(VNPREFIX,EQ,0)),BEGIN                              
         L     R2,SCNPAREA                                                      
         MVC   0(L'FWNAME,R2),VNNAME     SAVE NAME                              
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF NO NAME                                   
         ELSEIF (R15,NEG),BEGIN                                                 
         TSEG   'Missing Procedure Name.',,CR                                   
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF INVALID LABEL                             
         ELSEIF (R15,POS),BEGIN                                                 
         L     R0,SCNPLEN                                                       
         L     R1,SCNPLOC                                                       
         SCINIT (R1),(R0)                                                       
         SCAN ,                                                                 
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid procedure name.',,CR                                  
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   ANYTHING ELSE IS INVALID TOO                 
         ELSE  BEGIN               (IE. IF PREFIXED LABELS)                     
         L     R0,SCNPLEN                                                       
         L     R1,SCNPLOC                                                       
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid procedure name.',,CR                                  
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF ALL OK, UPDATE SCANCB                     
         IF    (R15,Z),BEGIN                                                    
         SCINIT (R1),(R0)                                                       
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   ALL DONE                                     
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'EXEC Formatting Routines'                                       
*box                                                                            
*                                                                               
*                                                                               
*  FFORMAT - FORMAT XPROC                                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - OPTIONS                                                            
*             C'    ' - DEFAULT FORMATTING                                      
*             C'M   ' - MARK ACTIVE, FORMATED (DEFAULT FOR MARK)                
*             C'MU  ' - MARK ACTIVE, UNFORMATED                                 
*       R1 - INDENT VALUE                                                       
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO,  ALL ERRORS EXIT VIA CVERROR                             
*       ACTIVE FILE FORMATED  AND/OR  MARKED.                                   
*                                                                               
*  NOTES:                                                                       
*    FILE MUST BE XSCANNED PRIOR TO CALLING THIS ROUTINE.                       
*    THIS ACCOMPLISHES THE FOLLOWING:                                           
*       1. SYNTAX IS ALREADY SCANNED AND OK                                     
*       2. CONTINUATATION LINES ARE CONCATENATED                                
*       3. SCANNED FILE IS IN TEMP EXEC FILE                                    
*                                                                               
*    WE NOTE THAT THE TEMP EXEC FILE IS DIFFERENT THAN THE                      
*    ACTIVE FILE IN THE FOLLOWING RESPECTS:                                     
*       1. CONTINUATION LINES ARE CONCATENATED.                                 
*       2. COMMENT LINES ARE DELETED                                            
*       3. BLANK (NULL) LINES ARE DELETED                                       
*                                                                               
*    THE STRATEGY IS:                                                           
*    WE SCAN THE TEMP EXEC FILE FOR BEGIN/END NESTING.                          
*    THIS NESTING DETERMINES THE ACTIVE FILE INDENTING..                        
*    WE READ FROM THE ACTIVE AND REWRITE TO THE ACTIVE                          
*    PROPERLY INDENTED LINES.   LINES THAT EXIST IN THE                         
*    ACTIVE FILE BUT ARE NOT IN THE TEMP LOADED EXEC                            
*    ARE CONTINUATION LINES, UNLESS THEY ARE COMMENT                            
*    LINES OR BLANK LINES.                                                      
*                                                                               
*    NOTE:  IT MAY BE BEST AT SOME POINT TO RECODE THIS                         
*    ROUTINE TO SCAN FOR BEGIN/END, CONTINUATIONS, ETC. ETC.                    
*    DIRECTLY FROM THE ACTIVE FILE.  IF YOU GOT THE TIME,,,                     
*                                                                               
*  *** CAUTION ***                                                              
*  CAUTION:  ACTIVE FILE MUST BE XSCANNED (INSURES SYNTACTICAL                  
*    VALIDITY) BEFORE CALLING FFORMAT.   IE. TEMP LOAD FILE                     
*    MUST CONTAIN XSCANNED ACTIVE FILE.                                         
*                                                                               
*                                                                               
FFWA     RECORD BEGIN                                                           
FFOPTS   DS    CL4                 FORMAT OPTIONS                               
FFINDENT DS    F                   FORMAT INDENT SPACING                        
FFOPTION FLAG  ,                   FORMAT OPTIONS                               
         FLAG  FFFMARK             MARK                                         
         FLAG  FFFUNFMT            UNFORMATED                                   
FFLEVEL  DS    F                   LEVEL OF NESTING                             
FFSPACE  DS    F                   CURRENT SPACING                              
FFLNO    DS    F                   ACTIVE FILE LINE NUMBER                      
FFINLEN  DS    F                   INPUT LINE LENGTH                            
FFINBUF  DS    CL(&LINESZ)         INPUT BUFFER                                 
FFOUTLEN DS    F                   OUTPUT LINE LENGTH (NOT INCL MARK)           
FFMARK   DS    CL6                 MARK           -- KEEP TOGETHER !!           
FFOUTBUF DS    CL(&LINESZ)         OUTPUT BUFFER  -- KEEP TOGETHER !!           
FFMARKID EQU   FFMARK+1,1          MARK ID                                      
FFMARKLV EQU   FFMARK+3,2          MARK LEVEL                                   
FFXLNO   DS    F                   EXEC INPUT LINE NUMBER                       
FFXLEN   DS    F                   EXEC INPUT LINE LENGTH                       
FFXBUF   DS    CL(&LINESZ)         EXEC INPUT LINE BUFFER                       
         END                                                                    
*                                                                               
*                                                                               
FFORMAT  XPROC FFWA                                                             
*                                                                               
         COMMENT                   SET UP WORK AREA                             
         ST    R0,FFOPTS                                                        
         ST    R1,FFINDENT                                                      
         CLEAR FFOPTION                                                         
         CLEAR FFLEVEL                                                          
         CLEAR FFSPACE                                                          
         CLEAR FFLNO                                                            
         CLEAR FFXLNO                                                           
         MVC   FFMARK,=CL6'      '                                              
         IF    (FFOPTS,EQ,'    '),BEGIN                                         
         END                                                                    
         ELSEIF (FFOPTS,EQ,'M   '),BEGIN                                        
         SET   FFFMARK                                                          
         END                                                                    
         ELSEIF (FFOPTS,EQ,'MU  '),BEGIN                                        
         SET   FFFMARK                                                          
         SET   FFFUNFMT                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   GET FIRST EXEC FILE LINE,,                   
         LA    R0,L'FFXBUF                                                      
         LA    R1,FFXBUF                                                        
         LA    R2,FFXLNO                                                        
         ACALL FFXFIRST                                                         
         ST    R0,FFXLEN                                                        
         IF    (R15,NZ),BEGIN                                                   
         MVC   FFXLNO,CVMAXLNO                                                  
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR LEADING COMMENTS,                  
         COMMENT                   OR CONTIUATION LINES                         
         LA    R0,L'FFINBUF        GET FIRST ACTIVE FILE LINE                   
         LA    R1,FFINBUF                                                       
         LA    R2,FFLNO                                                         
         ACALL FFFIRST                                                          
         ST    R0,FFINLEN                                                       
         COMMENT                   IF IN ACTIVE AND NOT EXEC,,,                 
         WHILE ((R15,Z),AND,(FFLNO,LT,FFXLNO)),BEGIN     COMM. LINE             
         IF    ('ACALL FFCCOMM',Z),BEGIN                                        
         LA    R1,FFWA                                                          
         ACALL FFPCOMM                                                          
         END                                                                    
         ELSEIF ('ACALL FFCBLANK',Z),BEGIN                                      
         LA    R1,FFWA                                                          
         ACALL FFPLINE                                                          
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R1,FFWA                                                          
         ACALL FFPCONT                                                          
         END                                                                    
         LA    R0,L'FFINBUF                                                     
         LA    R1,FFINBUF                                                       
         LA    R2,FFLNO                                                         
         ACALL FFNEXT                                                           
         ST    R0,FFINLEN                                                       
         END                                                                    
*-                                                                              
*-        MAIN FORMATING LOOP                                                   
*-                                                                              
         COMMENT                   LOOP PROCESSING ALL LINES                    
         WHILE (R15,Z),BEGIN       WHILE LINES !                                
*                                                                               
         COMMENT                   SCAN EXEC LINES                              
         L     R0,FFXLEN           R1,R0 - EXEC LINE LOC, LEN                   
         LA    R1,FFXBUF                                                        
*                                                                               
*                                                                               
         COMMENT                   IF BEGIN LINE                                
         IF    ('ACALL FFCBEGIN',Z),BEGIN                                       
         LA    R1,FFWA                                                          
         ACALL FFPBEGIN                                                         
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   IF END LINE                                  
         ELSEIF ('ACALL FFCEND',Z),BEGIN                                        
         LA    R1,FFWA                                                          
         ACALL FFPEND                                                           
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   IF COMMENT LINE                              
         ELSEIF ('ACALL FFCCOMM',Z),BEGIN                                       
         LA    R1,FFWA                                                          
         ACALL FFPCOMM                                                          
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   IF LABEL LINE                                
         ELSEIF ('ACALL FFCLABEL',Z),BEGIN                                      
         LA    R1,FFWA                                                          
         ACALL FFPLABEL                                                         
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   ALL OTHER LINES, GET SPACED                  
         ELSE  BEGIN                                                            
         LA    R1,FFWA                                                          
         ACALL FFPLINE                                                          
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   GET NEXT EXEC FILE LINE,,                    
         LA    R0,L'FFXBUF                                                      
         LA    R1,FFXBUF                                                        
         LA    R2,FFXLNO                                                        
         ACALL FFXNEXT                                                          
         ST    R0,FFXLEN                                                        
         IF    (R15,NZ),BEGIN                                                   
         MVC   FFXLNO,CVMAXLNO                                                  
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR ANY CONTINUATION LINES             
         LA    R0,L'FFINBUF        GET NEXT ACTIVE FILE LINE                    
         LA    R1,FFINBUF                                                       
         LA    R2,FFLNO                                                         
         ACALL FFNEXT                                                           
         ST    R0,FFINLEN                                                       
         COMMENT                   IF IN ACTIVE AND NOT EXEC,,,                 
         WHILE ((R15,Z),AND,(FFLNO,LT,FFXLNO)),BEGIN     CONT. LINE             
         IF    ('ACALL FFCCOMM',Z),BEGIN                                        
         LA    R1,FFWA                                                          
         ACALL FFPCOMM                                                          
         END                                                                    
         ELSEIF ('ACALL FFCBLANK',Z),BEGIN                                      
         LA    R1,FFWA                                                          
         ACALL FFPLINE                                                          
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R1,FFWA                                                          
         ACALL FFPCONT                                                          
         END                                                                    
         LA    R0,L'FFINBUF                                                     
         LA    R1,FFINBUF                                                       
         LA    R2,FFLNO                                                         
         ACALL FFNEXT                                                           
         ST    R0,FFINLEN                                                       
         END                                                                    
*                                                                               
         COMMENT                   LOOP PROCESSING NEXT LINE                    
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPLINE - PROCESS NORMAL LINE                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPLINE  PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS NORMAL LINE                          
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         A     R4,FFSPACE                                                       
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         L     R4,FFSPACE                                                       
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C' '       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPBEGIN - PROCESS BEGIN LINE                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPBEGIN PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS BEGIN LINE                           
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         A     R4,FFSPACE                                                       
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         L     R4,FFSPACE                                                       
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C'B'       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         A     R0,=F'1'                                                         
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   UPDATE LEVEL, SPACING, VALUES                
         L     R1,FFLEVEL                                                       
         A     R1,=F'1'                                                         
         ST    R1,FFLEVEL                                                       
         L     R1,FFSPACE                                                       
         A     R1,FFINDENT                                                      
         ST    R1,FFSPACE                                                       
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPEND - PROCESS END LINE                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPEND   PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS END LINE                             
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         A     R4,FFSPACE                                                       
         S     R4,FFINDENT                                                      
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         L     R4,FFSPACE                                                       
         S     R4,FFINDENT                                                      
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C'E'       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   UPDATE LEVEL, SPACING, VALUES                
         L     R1,FFLEVEL                                                       
         S     R1,=F'1'                                                         
         ST    R1,FFLEVEL                                                       
         L     R1,FFSPACE                                                       
         S     R1,FFINDENT                                                      
         ST    R1,FFSPACE                                                       
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPCOMM - PROCESS COMMENT LINE                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPCOMM  PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS COMMENT LINE                         
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         A     R4,FFSPACE                                                       
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         L     R4,FFSPACE                                                       
         IF    (FFINBUF,EQ,';'),BEGIN  IF ';'1 ,, DO NOT SPACE                  
         CLEAR R4                                                               
         L     R5,FFOUTLEN                                                      
         S     R5,FFSPACE                                                       
         ST    R5,FFOUTLEN                                                      
         END                                                                    
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C'C'       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPLABEL - PROCESS LABEL LINE                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPLABEL PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS LABEL LINE                           
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C'L'       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPCONT - PROCESS CONTINUE LINE                                              
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FFWA, FORMATTING WORK AREA                                         
*                                                                               
*                                                                               
*                                                                               
FFPCONT  PROC  ,                                                                
         USING FFWA,R6                                                          
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   PROCESS CONTINUE LINE                        
         IF    ^FFFMARK,BEGIN                                                   
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         ACALL FFLTRIM             TRIM LINE                                    
         LR    R4,R0                                                            
         A     R4,FFSPACE                                                       
         A     R4,FFINDENT                                                      
         ST    R4,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF                                                    
         L     R4,FFSPACE                                                       
         A     R4,FFINDENT                                                      
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              SPACE OUT LINE                               
         L     R0,FFOUTLEN                                                      
         LA    R1,FFOUTBUF                                                      
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
         COMMENT                   OR IF MARK SPECIFIED                         
         ELSE  BEGIN                                                            
         MVI   FFMARKID,C'+'       CREATE MARK                                  
         L     R0,FFLEVEL                                                       
         LA    R1,FFMARKLV                                                      
         ACALL FFMRKLVL                                                         
         L     R0,FFINLEN                                                       
         LA    R1,FFINBUF                                                       
         IF    ^FFFUNFMT,BEGIN                                                  
         ACALL FFLTRIM             TRIM LINE, UNLESS UNFORMATED MARK            
         END                                                                    
         ST    R0,FFOUTLEN                                                      
         LA    R2,FFOUTBUF                                                      
         LA    R3,L'FFOUTBUF-L'FFMARK                                           
         CLEAR R4                                                               
         LA    R5,FFLNO                                                         
         ACALL FFMOVE              MOVE LINE                                    
         L     R0,FFOUTLEN                                                      
         A     R0,=A(L'FFMARK)                                                  
         LA    R1,FFMARK                                                        
         LA    R2,FFLNO                                                         
         ACALL FFPUT               WRITE OUT LINE                               
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFCBEGIN - CHECK LINE FOR BEGIN                                              
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, BEGIN FOUND                                              
*             CC=NZ, BEGIN NOT FOUND                                            
*                                                                               
*  NOTE: IT IS ASSUMED LINE IS ALREADY SCANNED (BY XSCAN)                       
*       FOR SYNTAX.                                                             
*                                                                               
*                                                                               
FFCBEGIN PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR BEGIN BLOCKS                        
         SCINIT (R1),(R0)                                                       
         SCAN FFBEPRT                                                           
         SCANCHK                                                                
         IF    (R15,EQ,4),' CLEAR R15 '    BEGIN FOUND                          
         IF    (R15,EQ,8),' LA R15,4  '    BEGIN NOT FOUND                      
         PEND  ,                                                                
         SPACE 4                                                                
*-                                                                              
*-       SCAN TABLE FOR BEGIN BLOCKS                                            
*-                                                                              
FFBEPRT  SCKW  ,FFSNONE,NULL                                                    
         SCKW  WHILE,FFSXPR                                                     
         SCKW  IF,FFSXPR                                                        
         SCKW  ELSEIF,FFSXPR                                                    
         SCKW  ELSE,FFSNOXPR                                                    
         SCKW  REPEAT,FFSNOXPR                                                  
         SCKW  CASES,FFSNOXPR                                                   
         SCKW  CASE,FFSXPR                                                      
         SCKW  XPROC,FFSXPR                                                     
         SCKW  PROC,FFSPROC                                                     
         SCKW  ,FFSNONE                                                         
         SPACE 4                                                                
*-                                                                              
*-       SCAN ROUTINES FOR BEGIN BLOCKS                                         
*-                                                                              
FFSNONE  PROC  ,                   NO BEGIN                                     
         LA    R15,8                                                            
         PEND  ,                                                                
*                                                                               
FFSXPR   PROC  ,                   LOOK FOR BEGIN, AFTER EXPRESSION             
         SCINFO                                                                 
         ACALL SKIPEXPR                                                         
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         IF    Z,BEGIN                                                          
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,8                                                            
         END                                                                    
         PEND  ,                                                                
*                                                                               
FFSNOXPR PROC  ,                   LOOK FOR BEGIN, WITHOUT EXPR                 
         ACALL SCNBEGIN                                                         
         IF    Z,BEGIN                                                          
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,8                                                            
         END                                                                    
         PEND  ,                                                                
*                                                                               
FFSPROC  PROC  ,                   LOOK FOR BEGIN, ON PROC STATEMENT            
         SCAN  ,                   SKIP PAST PROC NAME                          
         SCANCHK                                                                
         SCINFO                                                                 
         ACALL SKIPEXPR                                                         
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         IF    Z,BEGIN                                                          
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,8                                                            
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFCEND - CHECK LINE FOR END                                                  
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, END FOUND                                                
*           - CC=NZ, END NOT FOUND                                              
*                                                                               
*  NOTE: IT IS ASSUMMED LINE IS ALREADY SCANNED (BY XSCAN)                      
*       FOR SYNTAX.                                                             
*                                                                               
*                                                                               
FFCEND   PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR END                                 
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCUPTOK R3                                                             
         IF    ((R0,NZ),AND,(@R3,EQ,'END ')),BEGIN                              
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFCLABEL - SCAN FOR LABEL                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, LABEL FOUND                                              
*           - CC=NZ, LABEL NOT FOUND                                            
*                                                                               
*  NOTE: IT IS ASSUMMED LINE IS ALREADY SCANNED (BY XSCAN)                      
*       FOR SYNTAX.                                                             
*                                                                               
*                                                                               
FFLAWA   RECORD BEGIN                                                           
FFLATEMP DS    XL(L'FWNAME)        TEMP LABEL NAME AREA                         
         END                                                                    
*                                                                               
*                                                                               
FFCLABEL PROC  FFLAWA                                                           
*                                                                               
         COMMENT                    SCAN FOR LABEL                              
         LA    R2,FFLATEMP                                                      
         ACALL SCNLABEL                                                         
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFCCOMM - SCAN FOR COMM                                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, COMM FOUND                                               
*           - CC=NZ, COMM NOT FOUND                                             
*                                                                               
*  NOTE: IT IS ASSUMMED LINE IS ALREADY SCANNED (BY XSCAN)                      
*       FOR SYNTAX.                                                             
*                                                                               
*                                                                               
FFCCOMM  PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR ';'                                 
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN                              
         LA    R1,1(R1)                                                         
         END                                                                    
         IF    ((@R1,EQ,';'),AND,(R1,LT,R0)),BEGIN                              
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFCBLANK - SCAN FOR BLANK LINE                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, BLANK FOUND                                              
*           - CC=NZ, BLANK NOT FOUND                                            
*                                                                               
*  NOTE: IT IS ASSUMMED LINE IS ALREADY SCANNED (BY XSCAN)                      
*       FOR SYNTAX.                                                             
*                                                                               
*                                                                               
FFCBLANK PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR ';'                                 
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN                              
         LA    R1,1(R1)                                                         
         END                                                                    
         IF    (R1,GE,R0),BEGIN                                                 
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFLTRIM - TRIM LEFT HAND BLANKS                                              
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - INPUT LENGTH                                                       
*       R1 - INPUT LOCATION                                                     
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NEW LINE LENGTH                                                    
*       R1 - NEW LINE LOCATION                                                  
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
FFLTRIM  PROC  ,                                                                
*                                                                               
         COMMENT                   TRIM IT DUDE                                 
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN                              
         LA    R1,1(R1)                                                         
         END                                                                    
         SR    R0,R1                                                            
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFMOVE - MOVE AND ADD SPACES                                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - INPUT LENGTH                                                       
*       R1 - INPUT LOCATION                                                     
*       R2 - OUTPUT LOCATION                                                    
*       R3 - MAXIMUM OUTPUT LENGTH                                              
*       R4 - AMOUNT OF SPACES TO ADD                                            
*      @R5 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       SPACES THEN TEXT MOVED TO OUTPUT BUFFER                                 
*       R15 - CC=ZERO, LENGTH ERRORS EXIT VIA CVERROR                           
*                                                                               
*                                                                               
*                                                                               
FFMOVE   PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK FOR LENGTH OVERFLOW                    
         LR    R6,R0                                                            
         AR    R6,R4                                                            
         IF    (R6,GT,R3),BEGIN                                                 
         TSEG  'Text overflow in line '                                         
         L     R0,0(R5)                                                         
         VCALL CVEXNO                                                           
         TSEG  CPDOUB,9                                                         
         ERROR ', formatting stopped.'                                          
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR VALID SPACING VALUE                
         IF    (R4,NEG),BEGIN                                                   
         TSEG  'BEGIN-END mismatch detected at line '                           
         L     R0,0(R5)                                                         
         VCALL CVEXNO                                                           
         TSEG  CPDOUB,9                                                         
         ERROR ', formatting stopped.'                                          
         END                                                                    
*                                                                               
         COMMENT                   MOVE IN SPACES, TEXT                         
         COMMENT ,                 REGS JUGGLED, SEE ENTRY COMMENTS             
         LR    R3,R4                                                            
         L     R15,=X'40000000'                                                 
         MVCL  R2,R14              SPACES                                       
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         MVCL  R2,R4               TEXT                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFMRKLVL - MARK LEVEL                                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LEVEL NUMBER (0-99)                                                
*       R1 - PLACE TO PUT 2 DIGIT LEVEL NUMBER (00-99)                          
*                                                                               
*  ON EXIT:                                                                     
*       LEVEL NUMBER STUFFED                                                    
*       R15 - CC=ZERO                                                           
*                                                                               
*                                                                               
FFMRKLVL PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK LEVEL RANGE                            
         IF    (R0,NEG),BEGIN                                                   
         BOMB                                                                   
         END                                                                    
         CEIL  R0,99               RARE BIRD THAT 99                            
*                                                                               
         COMMENT                   DIVIDE AND STUFF NUMBER                      
         LR    R3,R0                                                            
         CLEAR R2                                                               
         LA    R4,10                                                            
         DR    R2,R4               DIVIDE NUMBER BY 10                          
         STC   R3,@R1              QUOTIENT IN 1ST BYTE                         
         OI    @R1,C'0'                                                         
         STC   R2,@R1+1            REMAINDER IN 2ND BYTE                        
         OI    @R1+1,C'0'                                                       
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFFIRST - GET FIRST LINE TO FORMAT FROM ACTIVE                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - BUFFER LENGTH  (=&LINESZ)                                          
*       R1 - BUFFER LOCATION                                                    
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       BUFFER CONTAINS FIRST LINE OF ACTIVE                                    
*       @R2 - LINE NUMBER                                                       
*       R15 - CC=ZERO, IF EXISTS                                                
*             CC=NZ, LINE DOES NOT EXIST                                        
*                                                                               
*                                                                               
*                                                                               
FFFIRST  PROC  ,                                                                
*                                                                               
         CLEAR R3                  USE CURRENT ACTIVE FILE                      
         VCALL GTFRST                                                           
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFNEXT - GET NEXT LINE TO FORMAT FROM ACTIVE                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - BUFFER LENGTH  (=&LINESZ)                                          
*       R1 - BUFFER LOCATION                                                    
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       BUFFER CONTAINS FIRST LINE OF ACTIVE                                    
*       @R2 - LINE NUMBER                                                       
*       R15 - CC=ZERO, IF EXISTS                                                
*             CC=NZ, LINE DOES NOT EXIST                                        
*                                                                               
*                                                                               
*                                                                               
FFNEXT   PROC  ,                                                                
*                                                                               
         CLEAR R3                  USE CURRENT ACTIVE FILE                      
         VCALL GTNEXT                                                           
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFPUT - PUT FORMATTED LINE INTO ACTIVE FILE                                  
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       LINE PUT                                                                
*       R15 - CC=ZERO, IF ERRORS, PUTLINE ROUITINE DOES NOT RETURN              
*                                                                               
*                                                                               
*                                                                               
FFPUT    PROC  ,                                                                
*                                                                               
         L     R15,0(R2)           R15 - LINE NUMBER                            
         COMMENT                   R1,R0 - LINE LOC, LEN                        
         VCALL PUTLINE             PUT LINE                                     
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFXFIRST - GET FIRST LINE TO FORMAT FROM TEMP EXEC                           
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - BUFFER LENGTH  (=&LINESZ)                                          
*       R1 - BUFFER LOCATION                                                    
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       BUFFER CONTAINS FIRST LINE OF TEMP EXEC                                 
*       @R2 - LINE NUMBER                                                       
*       R15 - CC=ZERO, IF EXISTS                                                
*             CC=NZ, LINE DOES NOT EXIST                                        
*                                                                               
*                                                                               
*                                                                               
FFXFIRST PROC  ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XTEMP                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         LA    R15,CPEXEC                                                       
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETFRST                                                         
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FFXNEXT - GET NEXT LINE TO FORMAT FROM TEMP EXEC                             
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - BUFFER LENGTH  (=&LINESZ)                                          
*       R1 - BUFFER LOCATION                                                    
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       BUFFER CONTAINS FIRST LINE OF TEMP EXEC                                 
*       @R2 - LINE NUMBER                                                       
*       R15 - CC=ZERO, IF EXISTS                                                
*             CC=NZ, LINE DOES NOT EXIST                                        
*                                                                               
*                                                                               
*                                                                               
FFXNEXT  PROC  ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         LA    R15,CPEXEC                                                       
         LA    R0,CP#XTEMP                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         LA    R15,CPEXEC                                                       
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETNEXT                                                         
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'FLOW CONTROL SCANNING/PROCESSING'                               
*box                                                                            
*                                                                               
*                                                                               
*  FLOWCTRL - FLOW CONTROL SCANNING/PROCESSING                                  
*                                                                               
*  ON ENTRY:                                                                    
*       CP#XLOAD - SET NUMBER OF EXEC/FLOW INFO                                 
*       CPRGLOAD - EXEC RECORD GROUP INFO POINTER                               
*       CPRGFC   - FLOW CONTROL RECORD GROUP POINTER                            
*                                                                               
*  ON EXIT:                                                                     
*      R15 - ZERO                                                               
*            ALL ERRORS CALL 'LDABORT' ROUTINE AND                              
*            EXIT VIA CVERROR.                                                  
*                                                                               
*  THIS ROUTINE EVALUATES THE CONTROL FLOW OF AN EXEC FILE.                     
*  FLOW CONTROL INFORMATION IS SAVED IN THE FLOW RECORD GROUP                   
*  WHICH IS ESSENTIALLY A SHADOW FILE OF THE EXEC FILE.                         
*                                                                               
*                                                                               
XFCWA    RECORD BEGIN                                                           
XFCLNO   DS    F                   LINE NUMBER                                  
XFCLINE  DS    XL(XLINESZ)         LINE BUFFER                                  
         END                                                                    
*                                                                               
*                                                                               
FLOWCTRL XPROC XFCWA                                                            
         CLEAR XFCLNO                                                           
*                                                                               
         COMMENT                   SET EXEC AND FLOW CONTROL FILES              
         L     R0,CP#XLOAD                                                      
         L     R15,CPRGLOAD                                                     
         VCALL XSETSET                                                          
         L     R0,CP#XLOAD                                                      
         L     R15,CPRGFC                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   CHECK FOR XPROC                              
         LA    R1,XFCLINE                                                       
         LA    R2,XFCLNO                                                        
         ACALL CHKXPROC                  (IE. EXTENDED EXEC)                    
*                                                                               
         COMMENT                   IF EXTENDED EXEC,,                           
         COMMENT                      WE SCAN FOR ALL CONSTRUCTS                
         IF    Z,BEGIN                                                          
         LA    R1,XFCLINE                                                       
         LA    R2,XFCLNO                                                        
         ACALL FCXPROC             SCAN FLOW CONTROL FOR XPROC                  
         ACALL CHKPROC                                                          
         WHILE Z,BEGIN             WHILE PROCS FOUND,                           
         LA    R1,XFCLINE                                                       
         LA    R2,XFCLNO                                                        
         ACALL FCPROC              SCAN FLOW CONTROL FOR PROCS                  
         ACALL CHKPROC                                                          
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF SIMPLE EXEC,                              
         COMMENT                       SCAN FOR LABELS ONLY                     
         ELSE  BEGIN                                                            
         FCERR 'Missing XPROC statement. '                                      
         LA    R1,XFCLINE                                                       
         LA    R2,XFCLNO                                                        
         ACALL FCSIMPLE            SCAN SIMPLE CONSTRUCTS ONLY                  
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCXPROC - SCAN XPROC FLOW CONTROL                                            
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF XPROC)                                             
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ALL ERROR EXIT VIA CVERROR                                         
*                                                                               
*                                                                               
*                                                                               
FXPWA    RECORD BEGIN                                                           
FXPAREA  DS    F                   LINE AREA POINTER                            
FXPLPTR  DS    F                   CURRENT LINE NUMBER POINTER                  
FXPLINE  DS    F                   XPROC LINE NUMBER                            
FXPLINE2 DS    F                   XPROC ENDING LINE NUMBER                     
FXPNTRY  DS    XL(L'FWENTRY)       XPROC FLOW ENTRY                             
FXPNTRY2 DS    XL(L'FWENTRY)       XPROC ENDING FLOW ENTRY                      
FXPNLIST DS    XL(L'NLIST)         XPROC PARAMETER NAME LIST                    
FXPNAME  DS    XL(L'VPARM)         XPROC NAME AREA (IF ANY)                     
         END                                                                    
*                                                                               
*                                                                               
FCXPROC  PROC  FXPWA                                                            
         USING FWENTRY,R6                                                       
         LA    R6,FXPNTRY                                                       
         ST    R1,FXPAREA                                                       
         ST    R2,FXPLPTR                                                       
*                                                                               
         COMMENT                   FORMAT XPROC FLOW ENTRY                      
         CLEAR FWENTRY                                                          
         SET   FWXPROC                                                          
         MVC   FWNAME,=CL16'XPROC           '                                   
         MVC   FWSCOPE,0(R2)                                                    
*                                                                               
         COMMENT                   GET XPROC COMMAND                            
         L     R1,FXPAREA                                                       
         L     R2,FXPLPTR                                                       
         ACALL FCGET                                                            
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         SCINIT (R1),(R0)                                                       
         SCAN   ,                                                               
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,NE,'XPROC   '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN OPTIONAL XPROC NAME                     
         COMMENT                   NOT YET IMPLEMENTED                          
         COMMENT                                                                
*                                                                               
         COMMENT                   PROCESS PARAMETER NAME LIST                  
         SCAN                                                                   
         SCANCHK                                                                
         IF    ((R0,Z),OR,(@R1,NE,'(')),BEGIN                                   
         FCERR 'Missing XPROC parameter list. ',LPTR=FXPLPTR                    
         END                                                                    
         LA    R2,FXPNLIST         PROCESS ARGUMENT LIST                        
         VCALL SCNNLIST                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',LPTR=FXPLPTR                                                 
         END                                                                    
         LA    R0,L'NLIST                                                       
         LA    R1,FXPNLIST                                                      
         LA    R2,=CL16'XPROC_NLIST     '                                       
         L     R3,CP#XLOAD                                                      
         L     R4,CPRGFC                                                        
         ACALL SAVFINFO            SAVE PARAMETER NAME LIST                     
*                                                                               
         COMMENT                   LOOK FOR BEGIN                               
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,NE,'BEGIN   '),BEGIN                                        
         FCERR 'Missing BEGIN on XPROC command. ',,LPTR=FXPLPTR                 
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR XPROC END                           
         L     R1,FXPAREA                                                       
         L     R2,FXPLPTR                                                       
         ACALL FINDEND                                                          
*                                                                               
         COMMENT                   FIX XPROC END LINENO,                        
         COMMENT                   SAVE XPROC ENTRY                             
         L     R2,FXPLPTR                                                       
         MVC   FWSCOPND,0(R2)                                                   
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   CREATE XPROC END ENTRY                       
         LA    R6,FXPNTRY2                                                      
         CLEAR FWENTRY                                                          
         SET   FWEND                                                            
         L     R2,FXPLPTR                                                       
         MVC   FWNLINE,0(R2)                                                    
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWXRETN                                                          
*                                                                               
         COMMENT                   SAVE XPROC END ENTRY                         
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCPROC - SCAN PROC FLOW CONTROL                                              
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF PROC)                                              
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*            ALL ERROR EXIT VIA CVERROR VIA FCERR                               
*                                                                               
*                                                                               
*                                                                               
FPWA     RECORD BEGIN                                                           
FPAREA   DS    F                   LINE AREA POINTER                            
FPLPTR   DS    F                   CURRENT LINE NUMBER POINTER                  
FPLINE   DS    F                   PROC LINE NUMBER                             
FPLINE2  DS    F                   PROC ENDING LINE NUMBER                      
FPNTRY   DS    XL(L'FWENTRY)       PROC FLOW ENTRY                              
FPNTRY2  DS    XL(L'FWENTRY)       PROC ENDING FLOW ENTRY                       
FPNLIST  DS    XL(L'NLIST)         PROC PARAMETER NAME LIST                     
FPNLISTN DS    CL(L'FWNAME)        NAME LIST NAME, '....PROC_NLIST'             
         END                                                                    
*                                                                               
*                                                                               
FCPROC   PROC  FPWA                                                             
         USING FWENTRY,R6                                                       
         LA    R6,FPNTRY                                                        
         ST    R1,FPAREA                                                        
         ST    R2,FPLPTR                                                        
         MVC   FPLINE,0(R2)                                                     
*                                                                               
         COMMENT                   FORMAT PROC FLOW ENTRY                       
         CLEAR FWENTRY                                                          
         SET   FWPROC                                                           
         MVC   FWNAME,=CL16'PROC NAME       '                                   
         MVC   FWSCOPE,0(R2)                                                    
*                                                                               
         COMMENT                   GET PROC COMMAND                             
         L     R1,FPAREA                                                        
         L     R2,FPLPTR                                                        
         ACALL FCGET                                                            
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,NE,'PROC    '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN, SAVE PROC NAME                         
         LA    R2,FWNAME                                                        
         ACALL SCNPNAME                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=FPLPTR                                                 
         END                                                                    
*                                                                               
         COMMENT                   CHECK IF PREVIOUSLY DEFINED                  
         LA    R1,FWNAME                                                        
         ACALL CHKDEF                                                           
         IF    Z,BEGIN                                                          
         TSEG  FWNAME,L'FWNAME,T                                                
         FCERR ': previously defined label/procedure name. '                    
         END                                                                    
*                                                                               
         COMMENT                   PROCESS PARAMETER NAME LIST                  
         SCAN                                                                   
         SCANCHK                                                                
         IF    ((R0,Z),OR,(@R1,NE,'(')),BEGIN                                   
         FCERR 'Missing PROC parameter list. ',LPTR=FPLPTR                      
         END                                                                    
         LA    R2,FPNLIST         PROCESS ARGUMENT LIST                         
         VCALL SCNNLIST                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',LPTR=FPLPTR                                                  
         END                                                                    
         MVC   FPNLISTN,=CL16'....PROC_NLIST  '                                 
         MVC   FPNLISTN(4),FPLINE                                               
         LA    R0,L'NLIST                                                       
         LA    R1,FPNLIST                                                       
         LA    R2,FPNLISTN                                                      
         L     R3,CP#XLOAD                                                      
         L     R4,CPRGFC                                                        
         ACALL SAVFINFO            SAVE PARAMETER NAME LIST                     
*                                                                               
         COMMENT                   LOOK FOR BEGIN                               
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,NE,'BEGIN   '),BEGIN                                        
         FCERR 'Missing BEGIN on PROC command. ',,LPTR=FPLPTR                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR PROC END                            
         L     R1,FPAREA                                                        
         L     R2,FPLPTR                                                        
         ACALL FINDEND                                                          
*                                                                               
         COMMENT                   CHECK IF PREVIOUSLY DEFINED                  
         COMMENT ,                 (WE CHECK TWICE, LABEL MAY HAVE              
         COMMENT                    BEEN DEFINED IN THIS PROC)                  
         LA    R1,FWNAME                                                        
         ACALL CHKDEF                                                           
         IF    Z,BEGIN                                                          
         TSEG  FWNAME,L'FWNAME,T                                                
         FCERR ': previously defined label/procedure name. '                    
         END                                                                    
*                                                                               
         COMMENT                   FIX PROC END LINENO,                         
         COMMENT                   SAVE PROC ENTRY                              
         L     R2,FPLPTR                                                        
         MVC   FWSCOPND,0(R2)                                                   
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   CREATE PROC END ENTRY                        
         LA    R6,FPNTRY2                                                       
         CLEAR FWENTRY                                                          
         SET   FWEND                                                            
         L     R2,FPLPTR                                                        
         MVC   FWNLINE,0(R2)                                                    
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWPRETN                                                          
*                                                                               
         COMMENT                   SAVE PROC END ENTRY                          
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCWHILE - WHILE FLOW PROCESSING                                              
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - LINE AREA                                                         
*       @R2 - LINE NUMBER (OF WHILE)                                            
*                                                                               
*  ON EXIT:                                                                     
*       @R2 - LINE NUMBER (OF END OF WHILE STRUCTURE)                           
*       R15 - CC=ZERO                                                           
*             ERRORS EXIT VIA FCERR, THEN CVERROR                               
*                                                                               
*                                                                               
*                                                                               
FCWHWA   RECORD BEGIN                                                           
FCWHAREA DS    F                   LINE AREA POINTER                            
FCWHLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCWHLINE DS    F                   WHILE LINE NUMBER                            
FCWHLIN2 DS    F                   WHILE-END LINE NUMBER IF ANY                 
FCWHLIN3 DS    F                   WHILE-END LINE NUMBER OR WHILE++             
FCWHNTRY DS    XL(L'FWENTRY)       WHILE FLOW ENTRY                             
FCWHNTR2 DS    XL(L'FWENTRY)       WHILE-END FLOW ENTRY                         
         END                                                                    
*                                                                               
*                                                                               
FCWHILE  PROC  FCWHWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCWHAREA                                                      
         ST    R2,FCWHLPTR                                                      
         MVC   FCWHLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS WHILE                 
         ACALL FCGET                                                            
*                                                                               
         COMMENT                   SCAN PAST WHILE                              
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'WHILE   '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SKIP CONDITIONAL EXPR AFTER WHILE            
         ACALL SKIPEXPR                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCWHAREA                                                      
         L     R2,FCWHLPTR                                                      
*-                                                                              
*-       if we have:   while (expression), begin                                
*-                                                                              
         COMMENT                   FIND END OF WHILE BEGIN                      
         IF    Z,BEGIN                                                          
         L     R1,FCWHAREA                                                      
         L     R2,FCWHLPTR                                                      
         ACALL FINDEND             FIND MATCHING END                            
         MVC   FCWHLIN2,0(R2)      SAVE MATCHING END LINE NUMBER                
*                                                                               
         COMMENT                   SAVE LINE AFTER WHILE-END                    
         ACALL FCNEXTLN                                                         
         ST    R0,FCWHLIN3                                                      
*                                                                               
         COMMENT                   SAVE WHILE ENTRY                             
         LA    R6,FCWHNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCWHLINE                                                 
         MVC   FWNNAME,=CL8'WHILE   '                                           
         SET   FWWHILE                                                          
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCWHLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE END ENTRY                               
         LA    R6,FCWHNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCWHLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWBRANCH                                                         
         MVC   FWBLINE,FCWHLINE                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF ELSE BEGIN                                
*-                                                                              
*-       if we have:   while (expression), statement                            
*-                                                                              
         ELSE BEGIN                                                             
*                                                                               
         COMMENT                   SAVE LINE AFTER WHILE-END                    
         L     R1,FCWHAREA                                                      
         L     R2,FCWHLPTR                                                      
         ACALL FCNEXTLN                                                         
         ST    R0,FCWHLIN3                                                      
*                                                                               
         COMMENT                   SAVE WHILE ENTRY                             
         LA    R6,FCWHNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCWHLINE                                                 
         MVC   FWNNAME,=CL8'WHILE   '                                           
         SET   FWWHILE                                                          
         MVC   FWTRUE,FCWHLINE                                                  
         MVC   FWFALSE,FCWHLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCIF -  IF FLOW PROCESSING                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF IF STATEMENT)                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER (OF END OF IF STRUCTURE)                               
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  IF FLOW PROCESSING IS A BIT COMPLICATED BY TWO UNUSUAL                       
*  CONDITIONS.  FIRST, AN IF DOES NOT REQUIRE A BEGIN-END BLOCK.                
*  THAT IS AN IF MAY BE FOLLOWED DIRECTLY BY A SINGLE STATEMENT.                
*  THIS IS ALLOWED FOR CONVENIENCE REASONS.  SECOND, AN IF BLOCK                
*  MAY BE FOLLOWED BY AN 'ELSE' OR 'ELSEIF' BLOCK.  DIG IT.                     
*                                                                               
*                                                                               
FCIFWA   RECORD BEGIN ,                                                         
FCIFAREA DS    F                   LINE AREA POINTER                            
FCIFLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCIFLINE DS    F                   IF LINE NUMBER                               
FCIFLIN2 DS    F                   IF-END LINE NUMBER (IF ANY)                  
FCIFLIN3 DS    F                   ELSE LINE NUMBER OR IF-END++                 
FCIFLIN4 DS    F                   ELSE-END LINE NUMBER                         
FCIFNTRY DS    XL(L'FWENTRY)       IF FLOW ENTRY                                
FCIFNTR2 DS    XL(L'FWENTRY)       IF-END FLOW ENTRY IF ANY                     
         END                                                                    
*                                                                               
*                                                                               
FCIF     PROC  FCIFWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCIFAREA         CLEAR/INIT WORK AREA                         
         ST    R2,FCIFLPTR                                                      
         MVC   FCIFLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS IF                    
         ACALL  FCGET                                                           
*                                                                               
         COMMENT                   SCAN PAST IF                                 
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'IF      '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SKIP CONDITIONAL EXPR AFTER IF               
         ACALL SKIPEXPR                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCIFAREA                                                      
         L     R2,FCIFLPTR                                                      
*-                                                                              
*-       if we have:  if (expression), begin                                    
*-                                                                              
         COMMENT                   FIND END OF IF BEGIN-END                     
         IF    (R15,Z),BEGIN                                                    
         L     R1,FCIFAREA                                                      
         L     R2,FCIFLPTR                                                      
         ACALL FINDEND             FIND MATCHING END                            
         MVC   FCIFLIN2,0(R2)      SAVE MATCHING END LINE NUMBER                
*                                                                               
         COMMENT                   SAVE LINE AFTER IF-END                       
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN3                                                      
*                                                                               
         COMMENT                   CHEKC FOR ELSE/ELSEIF                        
         IF    (' ACALL CHKELSE ',Z),BEGIN    IF ELSE                           
         ACALL FCELSE                                                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN4                                                      
         END                                                                    
         ELSEIF (' ACALL CHKELSF ',Z),BEGIN   IF ELSEIF                         
         ACALL FCELSEIF                                                         
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN4                                                      
         END                                                                    
         ELSE  BEGIN                          IF NO ELSE/ELSEIF,                
         MVC   FCIFLIN4,FCIFLIN3              ELSE-END IS IF-END++              
         END                                                                    
*                                                                               
         COMMENT                   SAVE IF ENTRY                                
         LA    R6,FCIFNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCIFLINE                                                 
         MVC   FWNNAME,=CL8'IF      '                                           
         SET   FWIF                                                             
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCIFLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE IF-END ENTRY                            
         LA    R6,FCIFNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCIFLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWBRANCH                                                         
         MVC   FWBLINE,FCIFLIN4                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF IF <EXPRESSION>, BEGIN                    
*-                                                                              
*-       if we have:   if (expression), statement                               
*-                                                                              
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   SAVE LINE AFTER IF-END                       
         L     R1,FCIFAREA                                                      
         L     R2,FCIFLPTR                                                      
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN3                                                      
*                                                                               
         COMMENT                   CHEKC FOR ELSE/ELSEIF                        
         IF    (' ACALL CHKELSE ',Z),BEGIN    IF ELSE                           
         ACALL FCELSE                                                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN4                                                      
         END                                                                    
         ELSEIF (' ACALL CHKELSF ',Z),BEGIN   IF ELSEIF                         
         ACALL FCELSEIF                          WE LOVE RECURSION              
         ACALL FCNEXTLN                                                         
         ST    R0,FCIFLIN4                                                      
         END                                                                    
         ELSE  BEGIN                          IF NO ELSE/ELSEIF,                
         MVC   FCIFLIN4,FCIFLIN3              ELSE-END IS IF-END++              
         END                                                                    
*                                                                               
         COMMENT                   SAVE IF ENTRY                                
         LA    R6,FCIFNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCIFLINE                                                 
         MVC   FWNNAME,=CL8'IF      '                                           
         SET   FWIF                                                             
         MVC   FWTRUE,FCIFLIN4                                                  
         MVC   FWFALSE,FCIFLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCELSE - ELSE FLOW PROCESSING                                                
*                                                                               
*  ON ENTRY:                                                                    
*       @R1 - LINE AREA                                                         
*       @R2 - LINE NUMBER (OF ELSE)                                             
*                                                                               
*  ON EXIT:                                                                     
*       @R2 - LINE NUMBER (OF END OF ELSE STRUCTURE)                            
*       R15 - CC=ZERO                                                           
*             ERRORS EXIT VIA FCERR, THEN CVERROR                               
*                                                                               
*  THIS ROUTINE IS CALLED BY THE 'IF' FLOW CONTROL ROUTINE.                     
*  IT IS COMPLICATED BY ONE ITEM,  WE ALLOW BOTH 'ELSE BEGIN'                   
*  AND 'ELSE <statement>'.                                                      
*                                                                               
*                                                                               
FCELWA   RECORD BEGIN                                                           
FCELAREA DS    F                   LINE AREA POINTER                            
FCELLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCELLINE DS    F                   ELSE LINE NUMBER                             
FCELLIN2 DS    F                   ELSE-END LINE NUMBER OR ELSE++               
FCELNTRY DS    XL(L'FWENTRY)       ELSE FLOW ENTRY                              
FCELNTR2 DS    XL(L'FWENTRY)       ELSE-END FLOW ENTRY                          
         END                                                                    
*                                                                               
*                                                                               
FCELSE   PROC  FCELWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCELAREA                                                      
         ST    R2,FCELLPTR                                                      
         MVC   FCELLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS ELSE                  
         ACALL FCGET                                                            
*                                                                               
         COMMENT                   PROCESS LABEL, IF ANY                        
         ACALL FCLABEL             ***LABEL TRICK                               
*                                                                               
         COMMENT                   SCAN PAST ELSE                               
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'ELSE    '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCELAREA                                                      
         L     R2,FCELLPTR                                                      
*-                                                                              
*-       if we have:   else begin                                               
*-                                                                              
         COMMENT                   FIND END OF ELSE BEGIN                       
         IF    Z,BEGIN                                                          
         L     R1,FCELAREA                                                      
         L     R2,FCELLPTR                                                      
         ACALL FINDEND                                                          
         MVC   FCELLIN2,0(R2)                                                   
*                                                                               
         COMMENT                   SAVE ELSE ENTRY                              
         LA    R6,FCELNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCELLINE                                                 
         MVC   FWNNAME,=CL8'ELSE    '                                           
         SET   FWELSE                                                           
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE END ENTRY                               
         LA    R6,FCELNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCELLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWNOOP                                                           
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF ELSE BEGIN                                
*-                                                                              
*-       if we have:   else statement                                           
*-                                                                              
         ELSE BEGIN                                                             
*                                                                               
         COMMENT                   SIMPLY SAVE ELSE ENTRY                       
         LA    R6,FCELNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCELLINE                                                 
         MVC   FWNNAME,=CL8'ELSE    '                                           
         SET   FWELSE                                                           
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCELSEIF -  ELSEIF FLOW PROCESSING                                           
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF ELSEIF STATEMENT)                                  
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER (OF END OF ELSEIF STRUCTURE)                           
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  THIS ROUTINE IS VERY SIMILAR TO THE 'IF' FLOW CONTROL ROUTINE.               
*  IT IS (AS IS THE 'IF' ROUTINE) COMPLICATED BY TWO UNUSUAL                    
*  CONDITIONS.  FIRST, AN ELSEIF DOES NOT REQUIRE A BEGIN-END BLOCK.            
*  THAT IS AN ELSEIF MAY BE FOLLOWED DIRECTLY BY A SINGLE STATEMENT.            
*  THIS IS ALLOWED FOR CONVENIENCE REASONS.  SECOND, AN ELSEIF                  
*  BLOCK MAY BE FOLLOWED BY AN 'ELSE' OR 'ELSEIF' BLOCK. DIG IT.                
*                                                                               
*                                                                               
FCEFWA   RECORD BEGIN ,                                                         
FCEFAREA DS    F                   LINE AREA POINTER                            
FCEFLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCEFLINE DS    F                   ELSEIF LINE NUMBER                           
FCEFLIN2 DS    F                   IF-END LINE NUMBER (IF ANY)                  
FCEFLIN3 DS    F                   ELSE LINE NUMBER OR IF-END++                 
FCEFLIN4 DS    F                   ELSE-END LINE NUMBER                         
FCEFNTRY DS    XL(L'FWENTRY)       ELSEIF FLOW ENTRY                            
FCEFNTR2 DS    XL(L'FWENTRY)       ELSEIF-END FLOW ENTRY IF ANY                 
         END                                                                    
*                                                                               
*                                                                               
FCELSEIF PROC  FCEFWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCEFAREA         CLEAR/INIT WORK AREA                         
         ST    R2,FCEFLPTR                                                      
         MVC   FCEFLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS ELSEIF                
         ACALL  FCGET                                                           
*                                                                               
         COMMENT                   PROCESS LABEL, IF ANY                        
         ACALL FCLABEL             ***LABEL TRICK                               
*                                                                               
         COMMENT                   SCAN PASE ELSEIF                             
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'ELSEIF  '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SKIP CONDITIONAL EXPR AFTER ELSEIF           
         ACALL SKIPEXPR                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCEFAREA                                                      
         L     R2,FCEFLPTR                                                      
*-                                                                              
*-       if we have:  elseif (expression), begin                                
*-                                                                              
         COMMENT                   FIND END OF ELSEIF BEGIN-END                 
         IF    Z,BEGIN                                                          
         L     R1,FCEFAREA                                                      
         L     R2,FCEFLPTR                                                      
         ACALL FINDEND             FIND MATCHING END                            
         MVC   FCEFLIN2,0(R2)      SAVE MATCHING END LINE NUMBER                
*                                                                               
         COMMENT                   SAVE LINE AFTER ELSEIF-END                   
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN3                                                      
*                                                                               
         COMMENT                   CHEKC FOR ELSE/ELSEIF                        
         IF    (' ACALL CHKELSE ',Z),BEGIN    IF ELSE                           
         ACALL FCELSE                                                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN4                                                      
         END                                                                    
         ELSEIF (' ACALL CHKELSF ',Z),BEGIN   IF ELSEIF                         
         ACALL FCELSEIF                          WE LOVE RECURSION              
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN4                                                      
         END                                                                    
         ELSE  BEGIN                          IF NO ELSE/ELSEIF,                
         MVC   FCEFLIN4,FCEFLIN3              JUST DEFAULT IT                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE ELSEIF ENTRY                            
         LA    R6,FCEFNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCEFLINE                                                 
         MVC   FWNNAME,=CL8'ELSEIF  '                                           
         SET   FWELSEIF                                                         
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCEFLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE ELSEIF-END ENTRY                        
         LA    R6,FCEFNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCEFLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWBRANCH                                                         
         MVC   FWBLINE,FCEFLIN4                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF ELSEIF <EXPRESSION>, BEGIN                
*-                                                                              
*-       if we have:   elseif (expression), statement                           
*-                                                                              
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   SAVE LINE AFTER ELSEIF-END                   
         L     R1,FCEFAREA                                                      
         L     R2,FCEFLPTR                                                      
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN3                                                      
*                                                                               
         COMMENT                   CHEKC FOR ELSE/ELSEIF                        
         IF    (' ACALL CHKELSE ',Z),BEGIN    IF ELSE                           
         ACALL FCELSE                                                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN4                                                      
         END                                                                    
         ELSEIF (' ACALL CHKELSF ',Z),BEGIN   IF ELSEIF                         
         ACALL FCELSEIF                          WE LOVE RECURSION              
         ACALL FCNEXTLN                                                         
         ST    R0,FCEFLIN4                                                      
         END                                                                    
         ELSE  BEGIN                          IF NO ELSE/ELSEIF,                
         MVC   FCEFLIN4,FCEFLIN3              JUST DEFAULT IT                   
         END                                                                    
*                                                                               
         COMMENT                   SAVE ELSEIF ENTRY                            
         LA    R6,FCEFNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCEFLINE                                                 
         MVC   FWNNAME,=CL8'ELSEIF  '                                           
         SET   FWELSEIF                                                         
         MVC   FWTRUE,FCEFLIN4                                                  
         MVC   FWFALSE,FCEFLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCREPEAT - REPEAT FLOW PROCESSING                                            
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF IF STATEMENT)                                      
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER (OF END OF REPEAT STRUCTURE)                           
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  REPEAT FLOW PROCESSING HAS ONE COMPLICATION. EACH REPEAT                     
*  OR REPEAT BLOCK MUST BE FOLLOWED BY A 'UNTIL' STATEMENT.                     
*  OR ELSE !!                                                                   
*                                                                               
*                                                                               
FCRPWA   RECORD BEGIN ,                                                         
FCRPAREA DS    F                   LINE AREA POINTER                            
FCRPLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCRPLINE DS    F                   REPEAT LINE NUMBER                           
FCRPLIN2 DS    F                   REPEAT-END LINE NUMBER, IF ANY               
FCRPLIN3 DS    F                   UNTIL LINE NUMBER                            
FCRPNTRY DS    XL(L'FWENTRY)       REPEAT FLOW ENTRY                            
FCRPNTR2 DS    XL(L'FWENTRY)       REPEAT-END FLOW ENTRY                        
FCRPNTR3 DS    XL(L'FWENTRY)       UNTIL FLOW ENTRY                             
         END                                                                    
*                                                                               
*                                                                               
FCREPEAT PROC  FCRPWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCRPAREA         CLEAR/INIT WORK AREA                         
         ST    R2,FCRPLPTR                                                      
         MVC   FCRPLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS REPEAT                
         ACALL  FCGET                                                           
*                                                                               
         COMMENT                   SCAN PAST REPEAT                             
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'REPEAT  '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCRPAREA                                                      
         L     R2,FCRPLPTR                                                      
*-                                                                              
*-       if we have:  repeat begin                                              
*-                                                                              
         COMMENT                   FIND END OF IF BEGIN-END                     
         IF    (R15,Z),BEGIN                                                    
         L     R1,FCRPAREA                                                      
         L     R2,FCRPLPTR                                                      
         ACALL FINDEND             FIND MATCHING END                            
         MVC   FCRPLIN2,0(R2)      SAVE MATCHING END LINE NUMBER                
*                                                                               
         COMMENT                   GET UNTIL                                    
         L     R1,FCRPAREA                                                      
         L     R2,FCRPLPTR                                                      
         ACALL CHKUNTIL                                                         
         MVC   FCRPLIN3,0(R2)      SAVE UNTIL LINE NUMBER                       
*                                                                               
         COMMENT                   SAVE REPEAT ENTRY                            
         LA    R6,FCRPNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCRPLINE                                                 
         MVC   FWNNAME,=CL8'REPEAT  '                                           
         SET   FWREPEAT                                                         
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE REPEAT END ENTRY                        
         LA    R6,FCRPNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCRPLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWNOOP                                                           
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE UNTIL ENTRY                             
         LA    R6,FCRPNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCRPLIN3                                                 
         MVC   FWNNAME,=CL8'UNTIL   '                                           
         SET   FWUNTIL                                                          
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCRPLINE                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF REPEAT BEGIN                              
*-                                                                              
*-       if we have:   repeat statement                                         
*-                                                                              
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   GET UNTIL                                    
         L     R1,FCRPAREA                                                      
         L     R2,FCRPLPTR                                                      
         ACALL CHKUNTIL                                                         
         MVC   FCRPLIN3,0(R2)      SAVE UNTIL LINE NUMBER                       
*                                                                               
         COMMENT                   SAVE REPEAT ENTRY                            
         LA    R6,FCRPNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCRPLINE                                                 
         MVC   FWNNAME,=CL8'REPEAT  '                                           
         SET   FWREPEAT                                                         
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE UNTIL ENTRY                             
         LA    R6,FCRPNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCRPLIN3                                                 
         MVC   FWNNAME,=CL8'UNTIL   '                                           
         SET   FWUNTIL                                                          
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCRPLINE                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   , ######                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCCASES -  CASES BEGIN FLOW PROCESSING                                       
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF CASES STATEMENT)                                   
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER (OF END OF CASES STRUCTURE)                            
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  CASES FLOW PROCESSING IS A BIT COMPLICATED BY THE FACT THAT IT               
*  MUST BE FOLLOWES BY ONE OR MORE CASE STATEMENTS.  ALL OTHER                  
*  STATEMENTS ARE INVALID UNLESS THEY ARE PART OF A CASE BEGIN-END              
*  BLOCK.                                                                       
*                                                                               
*                                                                               
FCCBWA   RECORD BEGIN ,                                                         
FCCBAREA DS    F                   LINE AREA POINTER                            
FCCBLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCCBLINE DS    F                   CASES LINE NUMBER                            
FCCBLIN2 DS    F                   CASES END LINE NUMBER                        
FCCBNTRY DS    XL(L'FWENTRY)       CASES FLOW ENTRY                             
FCCBNTR2 DS    XL(L'FWENTRY)       CASES-END FLOW ENTRY                         
         END                                                                    
*                                                                               
*                                                                               
FCCASES  PROC  FCCBWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCCBAREA         CLEAR/INIT WORK AREA                         
         ST    R2,FCCBLPTR                                                      
         MVC   FCCBLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS IF                    
         ACALL  FCGET                                                           
*                                                                               
         COMMENT                   SCAN PAST CASES                              
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'CASES   '),BEGIN                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         IF    NZ,BEGIN                                                         
         FCERR 'CASES BEGIN must be specified. '                                
         END                                                                    
         L     R1,FCCBAREA                                                      
         L     R2,FCCBLPTR                                                      
*-                                                                              
*-       we have: cases begin                                                   
*-                                                                              
         COMMENT                   CHECK FOR CASE STATEMENTS                    
         ACALL CHKCASE                                                          
         IF    Z,BEGIN             IF CASE,                                     
         L     R1,FCCBAREA                                                      
         L     R2,FCCBLPTR                                                      
         ACALL FCCASE              FCCASE WILL RECURSE THRU ALL CASES           
         END                                                                    
*                                                                               
         COMMENT                   GET LINE AFTER ALL CASES                     
         L     R0,FCCBLINE                                                      
         L     R1,FCCBAREA                                                      
         L     R2,FCCBLPTR                                                      
         ACALL CHKEND                                                           
         MVC   FCCBLIN2,0(R2)      SAVE CASES-END LINE NUMBER                   
*                                                                               
         COMMENT                   SAVE CASES ENTRY                             
         LA    R6,FCCBNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCCBLINE                                                 
         MVC   FWNNAME,=CL8'CASES   '                                           
         SET   FWCASES                                                          
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE CASES-END ENTRY                         
         LA    R6,FCCBNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCCBLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWNOOP                                                           
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCCASE -  CASE FLOW PROCESSING                                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF CASE STATEMENT)                                    
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER (OF END OF CASE STRUCTURE)                             
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  CASE FLOW PROCESSING IS A BIT COMPLICATED BY THE FACT THAT IT                
*  MUST BE FOLLOWED BY ONE OR MORE CASE STATEMENTS. ALL OTHER                   
*  STATEMENTS ARE INVALID UNLESS THEY ARE PART OF A CASE BEGIN-END              
*  BLOCK.                                                                       
*                                                                               
*                                                                               
FCCSWA   RECORD BEGIN ,                                                         
FCCSAREA DS    F                   LINE AREA POINTER                            
FCCSLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCCSLINE DS    F                   CASE LINE NUMBER                             
FCCSLIN2 DS    F                   CASE-END LINE NUMBER (IF ANY)                
FCCSLIN3 DS    F                   CASE-END++ LINE NUMBER (IF ANY)              
FCCSLIN4 DS    F                   CASES-END LINE NUMBER                        
FCCSNTRY DS    XL(L'FWENTRY)       CASE FLOW ENTRY                              
FCCSNTR2 DS    XL(L'FWENTRY)       CASE-END FLOW ENTRY IF ANY                   
         END                                                                    
*                                                                               
*                                                                               
FCCASE   PROC  FCCSWA                                                           
         USING FWENTRY,R6                                                       
         ST    R1,FCCSAREA         CLEAR/INIT WORK AREA                         
         ST    R2,FCCSLPTR                                                      
         MVC   FCCSLINE(4),0(R2)                                                
*                                                                               
         COMMENT                   GET LINE THAT CONTAINS CASE                  
         ACALL  FCGET                                                           
*                                                                               
         COMMENT                   PROCESS LABEL, IF ANY                        
         ACALL FCLABEL             ***LABEL TRICK                               
*                                                                               
         COMMENT                   SCAN PAST CASE                               
         ACALL SCANCMD                                                          
         SCUPTOK R3                                                             
         IF    (@R3,NE,'CASE  '),BEGIN                                          
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   SKIP CONDITIONAL EXPR AFTER CASE             
         ACALL SKIPEXPR                                                         
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR 'BEGIN'                             
         SCINIT (R1),(R0)                                                       
         ACALL SCNBEGIN                                                         
         L     R1,FCCSAREA                                                      
         L     R2,FCCSLPTR                                                      
*-                                                                              
*-       if we have:  elseif (expression), begin                                
*-                                                                              
         COMMENT                   FIND END OF CASE BEGIN-END                   
         IF    Z,BEGIN                                                          
         L     R1,FCCSAREA                                                      
         L     R2,FCCSLPTR                                                      
         ACALL FINDEND             FIND MATCHING END                            
         MVC   FCCSLIN2,0(R2)      SAVE MATCHING END LINE NUMBER                
*                                                                               
         COMMENT                   SAVE LINE AFTER CASE-END                     
         ACALL FCNEXTLN                                                         
         ST    R0,FCCSLIN3                                                      
*                                                                               
         COMMENT                   CHECK FOR MORE CASE STATEMENTS               
         ACALL CHKCASE                                                          
         IF    (R15,Z),BEGIN       IF CASE,                                     
         ACALL FCCASE               WE LOVE RECURSION                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCCSLIN4                                                      
         END                                                                    
         ELSEIF (R15,NZ),BEGIN     IF NOT CASE,                                 
         MVC   FCCSLIN4,FCCSLIN3                                                
         END                                                                    
*                                                                               
         COMMENT                   SAVE CASE ENTRY                              
         LA    R6,FCCSNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCCSLINE                                                 
         MVC   FWNNAME,=CL8'CASE  '                                             
         SET   FWCASE                                                           
         MVC   FWTRUE,=CL4'NEXT'                                                
         MVC   FWFALSE,FCCSLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
*                                                                               
         COMMENT                   SAVE CASE-END ENTRY                          
         LA    R6,FCCSNTR2                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCCSLIN2                                                 
         MVC   FWNNAME,=CL8'END     '                                           
         SET   FWEND                                                            
         SET   FWBRANCH                                                         
         MVC   FWBLINE,FCCSLIN4                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END   ,                   OF CASE <EXPRESSION>, BEGIN                  
*-                                                                              
*-       if we have:   elseif (expression), statement                           
*-                                                                              
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   SAVE LINE AFTER CASE-END                     
         L     R1,FCCSAREA                                                      
         L     R2,FCCSLPTR                                                      
         ACALL FCNEXTLN                                                         
         ST    R0,FCCSLIN3                                                      
*                                                                               
*                                                                               
         COMMENT                   CHECK FOR MORE CASE STATMENTS                
         ACALL CHKCASE                                                          
         IF    (R15,Z),BEGIN       IF CASE,                                     
         ACALL FCCASE               WE LOVE RECURSION                           
         ACALL FCNEXTLN                                                         
         ST    R0,FCCSLIN4                                                      
         END                                                                    
         ELSEIF (R15,NZ),BEGIN     IF NOT CASE,                                 
         MVC   FCCSLIN4,FCCSLIN3                                                
         END                                                                    
*                                                                               
         COMMENT                   SAVE CASE ENTRY                              
         LA    R6,FCCSNTRY                                                      
         CLEAR FWENTRY                                                          
         MVC   FWNLINE,FCCSLINE                                                 
         MVC   FWNNAME,=CL8'CASE  '                                             
         SET   FWCASE                                                           
         MVC   FWTRUE,FCCSLIN4                                                  
         MVC   FWFALSE,FCCSLIN3                                                 
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCLABEL - PROCESS LABEL, IF ANY                                              
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH EXCLUDING LABEL, IF ANY                                
*       R1 - LINE LOCATION EXCLUDING LABEL, IF ANY                              
*      R15 - CC=ZERO                                                            
*            ERRORS EXIT VIA FCERROR, THEN CVERROR                              
*                                                                               
*                                                                               
FCLBWA   RECORD BEGIN                                                           
FCLBLPTR DS    F                   CURRENT LINE NUMBER POINTER                  
FCLBLOC  DS    F                   LINE LOCATION WITHOUT LABEL                  
FCLBLEN  DS    F                   LINE LENGTH WITHOUT LABEL                    
FCLBL    DS    XL(L'FWENTRY)       LABEL FLOW ENTRY                             
FCLBNAME DS    XL(L'FWNAME)        NAME OF LABEL, IF ANY                        
         END                                                                    
*                                                                               
*                                                                               
FCLABEL  PROC  FCLBWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,FCLBL                                                         
         ST    R2,FCLBLPTR                                                      
*                                                                               
         COMMENT                   SCAN FOR LABEL                               
         LA    R2,FCLBNAME                                                      
         ACALL SCNLABEL                                                         
         ST    R0,FCLBLEN          SAVE LINE LOC,LEN WITHOUT LABEL              
         ST    R1,FCLBLOC                                                       
*                                                                               
         COMMENT                   IF VALID LABEL                               
         IF    (R15,Z),BEGIN                                                    
         ACALL CHKLABEL            CHECK FOR VALID LABEL STATEMENT              
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=FCLBLPTR                                               
         END                                                                    
         LA    R1,FCLBNAME         CHECK IF PREVIOUSLY DEFINED                  
         ACALL CHKDEF                                                           
         IF    Z,BEGIN                                                          
         TSEG  FCLBNAME,L'FCLBNAME,T                                            
         FCERR ': previously defined label/procedure name. '                    
         END                                                                    
         CLEAR FWENTRY             SAVE LABEL FLOW ENTRY                        
         MVC   FWNAME,FCLBNAME                                                  
         SET   FWLABEL                                                          
         L     R2,FCLBLPTR                                                      
         MVC   FWLBLINE(4),0(R2)                                                
         LA    R1,FWENTRY                                                       
         ACALL SAVEFLOW                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF INVALID LABEL, GIVE ERROR                 
         ELSEIF (R15,M),BEGIN                                                   
         TSEG  (R1),(R0)                                                        
         FCERR ': invalid label name.',,LPTR=FCLBLPTR                           
         END                                                                    
*                                                                               
         COMMENT                   IF NO LABEL, JUST RETURN                     
         ELSEIF (R15,POS),BEGIN                                                 
         END                                                                    
*                                                                               
         L     R0,FCLBLEN          RETURN LINE LOC,LEN WITHOUT LABEL            
         L     R1,FCLBLOC                                                       
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FINDEND - FIND MATCHING END                                                  
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - FLAGS                                                              
*            NOT USED...                                                        
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (OF INSTRUCTION CONTAINING BEGIN)                      
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER OF MATCHING END                                        
*      R15 - CC=ZERO, MATCHING END FOUND                                        
*            ERRORS EXIT VIA FCERR, THEN CVERROR                                
*                                                                               
*  COMMENTS: THIS ROUTINE IS THE HEART OF THE FLOW CONTROL SCAN                 
*  ROUTINES.  IT IS CALLED RECURSIVELY.  AS THIS ROUTINE SCANS                  
*  FOR ITS MATCHING END,, IT WILL BRANCH OFF TO PROCESS ANY FLOW                
*  CONTROL STATEMENTS IT ENCOUNTERS.  (EG. IF, WHILE, DO ...)                   
*  EACH OF THESE ROUTINES IN TURN WILL CALL THIS ROUTINE TO FIND                
*  THEIR MATCHING END.  DIG IT !                                                
*                                                                               
*                                                                               
FDDWA    RECORD BEGIN                                                           
FDDFLAGS DS    4CL1                FIND FLAGS (PASSED IN R0)                    
FDDAREA  DS    F                   LINE AREA POINTER                            
FDDLPTR  DS    F                   CURRENT LINE NUMBER POINTER                  
FDDBEGIN DS    F                   LINE NUMBER FOR BEGIN LINE                   
         END                                                                    
FDFIF    EQU   10                                                               
FDFDO    EQU   11                                                               
FDFWHILE EQU   12                                                               
FDFCASES EQU   13                                                               
FDFBEGIN EQU   14                                                               
FDFRPEAT EQU   15                                                               
*                                                                               
*                                                                               
FINDEND  PROC  FDDWA                                                            
         LA    R6,FDDWA                                                         
         USING FDDWA,R6                                                         
         ST    R0,FDDFLAGS                                                      
         ST    R1,FDDAREA                                                       
         ST    R2,FDDLPTR                                                       
         MVC   FDDBEGIN(4),0(R2)                                                
*                                                                               
         COMMENT                   SCAN LINES FOR END,                          
         COMMENT                      AND OTHER CONTROL STATEMENTS              
         L     R1,FDDAREA                                                       
         L     R2,FDDLPTR                                                       
         ACALL FCNEXT                                                           
         WHILE Z,BEGIN             WHILE LINES, LOOP SCANNING FOR END           
         ACALL FCLABEL             PROCESS LABEL, IF ANY                        
         SCINIT (R1),(R0)                                                       
         CLEAR R4                  SCKW ROUTINES WILL SET R4                    
         SCAN  FINDPRT             SCAN FOR END, ETC.                           
*        SCANCHK                   SKIP ERRORS WITHIN CONSTRUCTS                
         IF    (R15,EQ,4),FINDYES     R15=4,   END FOUND                        
         CLEAR R15                 R15=8,   RECURSE LOOKING FOR END             
*                                                                               
         COMMENT                   IF CONTROL CONSTRUCT, RECURSE                
         IF    (R4,NZ),BEGIN                                                    
         L     R1,FDDAREA                                                       
         L     R2,FDDLPTR                                                       
         IF     (R4,EQ,FDFIF),'ACALL FCIF'                                      
         ELSEIF (R4,EQ,FDFDO),'ACALL FCDO'                                      
         ELSEIF (R4,EQ,FDFWHILE),'ACALL FCWHILE'                                
         ELSEIF (R4,EQ,FDFCASES),'ACALL FCCASES'                                
         ELSEIF (R4,EQ,FDFBEGIN),'ACALL FCBEGIN'                                
         ELSEIF (R4,EQ,FDFRPEAT),'ACALL FCREPEAT'                               
         CLEAR  R4                                                              
         END                                                                    
         L     R1,FDDAREA                                                       
         L     R2,FDDLPTR                                                       
         ACALL FCNEXT                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF NO END FOUND                              
         ACALL NOENDMSG                                                         
         FCERR 'Missing END occurs before END OF FILE. '                        
         BOMB  ,,                  NO RETURN FROM FCERR                         
*                                                                               
FINDNO   LABEL ,                                                                
         LA    R15,4                                                            
         B     FINDDONE                                                         
*                                                                               
FINDYES  LABEL ,                                                                
         CLEAR R15                                                              
         B     FINDDONE                                                         
*                                                                               
FINDDONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       FLOW CONTROL SCAN TABLE                                                
*-                                                                              
FINDPRT  SCKW  ,FDNONE,NULL                                                     
         SCKW  END,FDEND                                                        
         SCKW  WHILE,FDWHILE                                                    
         SCKW  IF,FDIF                                                          
         SCKW  ELSE,FDELSE,A                                                    
         SCKW  ELSEIF,FDELSEIF                                                  
         SCKW  REPEAT,FDREPEAT                                                  
         SCKW  UNTIL,FDUNTIL                                                    
         SCKW  CASES,FDCASES                                                    
         SCKW  CASE,FDCASE                                                      
         SCKW  BEGIN,FDBEGIN                                                    
         SCKW  DO,FDDO                                                          
         SCKW  XPROC,FDXPROC                                                    
         SCKW  PROC,FDPROC                                                      
         SCKW  EXEC,FDEXEC,A                                                    
         SCKW  ,FDNONE                                                          
         SPACE 5                                                                
*-                                                                              
*-       IF NOT CONTROL CONSTRUCT,,, IF NOT 'END',,,                            
*-       LOOP SCANNING LINES FOR END                                            
*-                                                                              
FDNONE   PROC  ,                   IF NONE OF THE ABOVE                         
         LA    R15,8                                                            
         PEND  , ,                  LOOP SCANNING                               
         SPACE 5                                                                
*-                                                                              
*-       END                                                                    
*-                                                                              
FDEND    PROC  ,                   FOUND END !! HOORAY                          
         LA    R15,4                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       IF                                                                     
*-                                                                              
FDIF     PROC  ,                                                                
         LA    R4,FDFIF                                                         
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       DO                                                                     
*-                                                                              
FDDO     PROC  ,                                                                
         LA    R4,FDFDO                                                         
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       WHILE                                                                  
*-                                                                              
FDWHILE  PROC  ,                                                                
         LA    R4,FDFWHILE                                                      
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       CASES                                                                  
*-                                                                              
FDCASES  PROC  ,                                                                
         LA    R4,FDFCASES                                                      
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       BEGIN                                                                  
*-                                                                              
FDBEGIN  PROC  ,                                                                
         LA    R4,FDFBEGIN                                                      
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       REPEAT                                                                 
*-                                                                              
FDREPEAT PROC  ,                                                                
         LA    R4,FDFRPEAT                                                      
         PRETURN (R4)                                                           
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       UNTIL - ERROR                                                          
*-                                                                              
FDUNTIL  PROC  ,                                                                
         COMMENT                   IF UNTIL NOT ALLOWED ,,,                     
         TSEG  'UNTIL statement must follow REPEAT statement '                  
         FCERR '(block). ',,LPTR=FDDLPTR                                        
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       XPROC - ILLEGAL                                                        
*-                                                                              
FDXPROC  PROC  ,                                                                
         FCERR 'Multiple XPROC statements are invalid. ',,LPTR=FDDLPTR          
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       PROC - ERROR                                                           
*-                                                                              
FDPROC   PROC  ,                                                                
         ACALL NOENDMSG                                                         
         TSEG 'Missing END before PROCEDURE statement found in line '           
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         L     R2,FDDLPTR                                                       
         VCALL SEGXLNO                                                          
         FCERR '. '                                                             
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       ELSE - INVALID                                                         
*-                                                                              
FDELSE   PROC  ,                                                                
         TSEG  'ELSE statement must follow IF statement (block). '              
         FCERR ' ',,LPTR=FDDLPTR                                                
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       ELSEIF - INVALID                                                       
*-                                                                              
FDELSEIF PROC  ,                                                                
         TSEG  'ELSEIF statement must follow IF statement (block). '            
         FCERR ' ',,LPTR=FDDLPTR                                                
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       CASE - INVALID                                                         
*-                                                                              
FDCASE   PROC  ,                                                                
         TSEG  'CASE statement must follow CASE(S) statement '                  
         FCERR '(block). ',,LPTR=FDDLPTR                                        
         PEND  ,                                                                
         SPACE 2                                                                
*-                                                                              
*-       EXEC - INVALID                                                         
*-                                                                              
FDEXEC   PROC  ,                                                                
         TSEG  'EXEC statement not allowed within PROCEDURES.  '                
         FCERR ' ',,LPTR=FDDLPTR                                                
         PEND  ,                                                                
         SPACE 2                                                                
         SPACE 5                                                                
*-                                                                              
*-                                                                              
*-       NOENDMSG - NO END FOUND ERROR MESSAGE                                  
*-                                                                              
*-                                                                              
NOENDMSG PROC  ,                                                                
         TSEG  'Missing END.',,CR                                               
         TSEG  'No matching END for BEGIN in '                                  
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         LA    R2,FDDBEGIN                                                      
         VCALL SEGXLNO                                                          
         TSEG  '. ',,CR                                                         
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'FLOW CONTROL SCANNING/PROCESSING - Common Routines'             
*box                                                                            
*                                                                               
*                                                                               
*  SKIPEXPR - SKIP CONDITIONAL EXPRESSION                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF TEXT                                                     
*       R1 - LOCATION OF TEXT, (AT OR BEFORE '(')                               
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LENGTH OF REMAINING TEXT                                           
*       R1 - LOCATION OF REMAINING TEXT, (JUST AFTER ')')                       
*      R15 - CC=ZERO, EXPRESSION SKIPPED OK                                     
*            IF ERROR, WE EXIT VIA CVERROR, VIA FCERROR.                        
*                                                                               
*  NOTE, EXPRESSION SKIPPED MUST BE IN PARENTHESIS                              
*                                                                               
*                                                                               
SKIPEXPR PROC  ,                                                                
*                                                                               
         COMMENT                   SKIP LEADING BLANKS AND '('                  
         WHILE ((@R1,EQ,' '),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         IF    ((@R1,EQ,'('),AND,(R0,POS)),BEGIN                                
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL CHECKEND                                                         
         TSEG  'Missing conditional expression. (Expression '                   
         FCERR 'must be in parenthesis.) ',,LPTR=(R2)                           
         END                                                                    
*                                                                               
         COMMENT                   FIND CLOSE PARENTHESIS                       
         LR    R4,R2                                                            
         ACALL FINDPREN                                                         
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       FIND CLOSE PARENTHESIS                                                 
*-                                                                              
*-       ON ENTRY:                                                              
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST '(')                              
*-       @R4 - LINE NUMBER                                                      
*-       ON EXIT:                                                               
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST ')')                              
*-       ERRORS EXIT VIA CVERROR, VIA FCERROR                                   
*-                                                                              
FINDPREN PROC  ,                                                                
*                                                                               
         COMMENT                   LOOK FOR ')'                                 
         IF    (R0,POS),BEGIN                                                   
         AR    R0,R1               R0 - END OF STRING                           
         LR    R3,R0                                                            
         SR    R3,R1               R3 - LENGTH OF STRING                        
         CLEAR R2                                                               
         DEX   R3,' TRT 0(0,R1),FINDTABL '                                      
         IF    Z,BEGIN                                                          
         LR    R1,R0                                                            
         END                                                                    
         SR    R0,R1                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF TEXT, BEGIN                        
         IF    (R0,Z),BEGIN                                                     
         FCERR 'Unmatched Parenthesis. Missing '')''.',,LPTR=(R4)               
         END                                                                    
*                                                                               
         COMMENT                   PROCESS SINGLE QUOTE                         
         ELSEIF (@R1,EQ,''''),BEGIN                                             
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDSQUO                                                         
         ACALL FINDPREN                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS DOUBLE QUOTE                         
         ELSEIF (@R1,EQ,'"'),BEGIN                                              
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDDQUO                                                         
         ACALL FINDPREN                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS PARENTHESIS PAIR                     
         ELSEIF (@R1,EQ,'('),BEGIN                                              
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDPREN                                                         
         ACALL FINDPREN                                                         
         END                                                                    
*                                                                               
         COMMENT                   GOT IT !!, SKIP IT, AND EXIT                 
         ELSEIF (@R1,EQ,')'),BEGIN                                              
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                    IF NONE OF THE ABOVE, BOMB                  
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       FIND SINGLE QUOTE                                                      
*-                                                                              
*-       ON ENTRY:                                                              
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST '''')                             
*-       @R4 - LINE NUMBER                                                      
*-       ON EXIT:                                                               
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST '''')                             
*-       ERRORS EXIT VIA CVERROR, VIA FCERROR                                   
*-                                                                              
FINDSQUO PROC  ,                                                                
*                                                                               
         COMMENT                   LOOK FOR ''''                                
         IF    (R0,POS),BEGIN                                                   
         AR    R0,R1               R0 - END OF STRING                           
         LR    R3,R0                                                            
         SR    R3,R1               R3 - LENGTH OF STRING                        
         CLEAR R2                                                               
         DEX   R3,' TRT 0(0,R1),FINDTABL '                                      
         IF    Z,BEGIN                                                          
         LR    R1,R0                                                            
         END                                                                    
         SR    R0,R1                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF TEXT, BEGIN                        
         IF    (R0,Z),BEGIN                                                     
         FCERR 'Unmatched Quotes. ',,LPTR=(R4)                                  
         END                                                                    
*                                                                               
         COMMENT                   PROCESS SINGLE QUOTE                         
         ELSEIF (@R1,EQ,''''),BEGIN  GOT IT, SKIP IT, EXIT                      
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   PROCESS DOUBLE QUOTE                         
         ELSEIF (@R1,EQ,'"'),BEGIN WITHIN QUOTES, JUST SKIP                     
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDSQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS PARENTHESIS PAIR                     
         ELSEIF (@R1,EQ,'('),BEGIN WITHIN QUOTES, JUST SKIP                     
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDSQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS CLOSING PARENTHESIS                  
         ELSEIF (@R1,EQ,')'),BEGIN  WITHIN QUOTES, JUST SKIP                    
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDSQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                    IF NONE OF THE ABOVE, BOMB                  
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       FIND DOUBLE QUOTE                                                      
*-                                                                              
*-       ON ENTRY:                                                              
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST '"')                              
*-       @R4 - LINE NUMBER                                                      
*-       ON EXIT:                                                               
*-       R0,R1 - TEXT LOC, LENGTH  (JUST PAST '"')                              
*-       ERRORS EXIT VIA CVERROR, VIA FCERROR                                   
*-                                                                              
FINDDQUO PROC  ,                                                                
*                                                                               
         COMMENT                   LOOK FOR '"'                                 
         IF    (R0,POS),BEGIN                                                   
         AR    R0,R1               R0 - END OF STRING                           
         LR    R3,R0                                                            
         SR    R3,R1               R3 - LENGTH OF STRING                        
         CLEAR R2                                                               
         DEX   R3,' TRT 0(0,R1),FINDTABL '                                      
         IF    Z,BEGIN                                                          
         LR    R1,R0                                                            
         END                                                                    
         SR    R0,R1                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF END OF TEXT, BEGIN                        
         IF    (R0,Z),BEGIN                                                     
         FCERR 'Unmatched Quotes. ',,LPTR=(R4)                                  
         END                                                                    
*                                                                               
         COMMENT                   PROCESS SINGLE QUOTE                         
         ELSEIF (@R1,EQ,''''),BEGIN WITHIN QUOTES, JUST SKIP                    
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDDQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS DOUBLE QUOTE                         
         ELSEIF (@R1,EQ,'"'),BEGIN GOT IT, SKIP IT, EXIT !                      
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         COMMENT                   PROCESS PARENTHESIS PAIR                     
         ELSEIF (@R1,EQ,'('),BEGIN WITHIN QUOTES, JUST SKIP                     
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDDQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                   PROCESS CLOSING PARENTHESIS                  
         ELSEIF (@R1,EQ,')'),BEGIN  WITHIN QUOTES, JUST SKIP                    
         INCR  R1                                                               
         DECR  R0                                                               
         ACALL FINDDQUO                                                         
         END                                                                    
*                                                                               
         COMMENT                    IF NONE OF THE ABOVE, BOMB                  
         ELSE  BEGIN                                                            
         BOMB                                                                   
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         CLEAR R15                                                              
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       FINDTABL - EXPRESSION SKIPPING TABLE                                   
*-                                                                              
*-       USED FOR FINDING AND MATCHING QUOTE AND PARENTHESIS PAIRS.             
*-                                                                              
*-                                                                              
FINDTABL LABEL ,                                                                
         DC    256X'0'                                                          
         ORG   FINDTABL+C'('                                                    
         DC    X'4'                                                             
         ORG   FINDTABL+C')'                                                    
         DC    X'4'                                                             
         ORG   FINDTABL+C'"'                                                    
         DC    X'4'                                                             
         ORG   FINDTABL+C''''                                                   
         DC    X'4'                                                             
         ORG                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SKIPNULL - SKIP NULL LINES                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER TO START CHECKING/SKIPING                              
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LINE NUMBER OF FIRST NON NULL (;) LINE                             
*      R15 - CC=ZERO, NON NULL LINE FOUND                                       
*            CC=NZ, NO NON NULL LINES                                           
*                                                                               
*                                                                               
*                                                                               
SKIPNULL PROC  ,                                                                
         LR    R5,R1               R5 - LINE AREA POINTER                       
         LR    R6,R2               R6 - LINE NUMBER POINTER                     
*                                                                               
         COMMENT                   GET FIRST LINE IN RANGE                      
         ACALL FCFIRST                                                          
         BNZ   NOLINES                                                          
*                                                                               
         COMMENT                   SKIP NULL LINES                              
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         WHILE (R0,Z),BEGIN                                                     
         LR    R1,R5                                                            
         LR    R2,R6                                                            
         ACALL FCNEXT                                                           
         BNZ   NOLINES                                                          
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         END                                                                    
         CLEAR R15                 GOT NON-NULL LINES                           
         B     SKIPDONE                                                         
*                                                                               
NOLINES  LABEL ,                                                                
         LA    R15,4                                                            
         B     SKIPDONE                                                         
*                                                                               
SKIPDONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKXPROC - CHECK FOR XPROC AS FIRST LINE                                     
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - EXEC LINE NUMBER, WE START AT ZERO                                 
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING XPROC                                              
*      @R2 - LINE NUMBER OF LINE CONTAINING XPROC, IF ANY                       
*      R15 - CC=ZERO, XPROC FOUND                                               
*            CC=NZ, XPROC NOT FOUND                                             
*                                                                               
*                                                                               
*                                                                               
CHKWA    RECORD BEGIN                                                           
CHKINFO  DS    F                                                                
CHKAREA  DS    F                                                                
CHKLPTR  DS    F                                                                
CHKTEMP  DS    XL(L'FWNAME)                                                     
         END                                                                    
*                                                                               
*                                                                               
CHKXPROC PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   SKIP NULL LINES                              
         CLEAR R0                       STARTING AT LINE ZERO                   
         ST    R0,0(R2)                                                         
         ACALL SKIPNULL                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET NON-NULL LINE                            
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET                                                            
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR LABEL                              
         COMMENT                   R1,R0 - LOC,LEN OF LINE                      
         LA    R2,CHKTEMP          R2 - WORK AREA FOR LABEL NAME                
         ACALL SCNLABEL                                                         
         XPUSH R0,R1                                                            
         IF    (R15,NP),BEGIN                                                   
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'XPROC   '),BEGIN                                        
         FCERR 'Labels are invalid on XPROC statement. '                        
         END                                                                    
         END                                                                    
         XPOP R0,R1                                                             
*                                                                               
         COMMENT                   CHECK FOR XPROC                              
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'XPROC   '),BEGIN                                        
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKPROC - GET LINE CONTAINING PROC STATEMENT, IF ANY                         
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING PROC                                               
*      @R2 - LINE NUMBER OF LINE CONTAINING PROC, IF ANY                        
*      R15 - CC=ZERO, PROC FOUND                                                
*            CC=NZ, NO MORE LINE FOUND                                          
*      IF NEXT NON-NULL LINE IS NOT 'PROC' WE EXIT VIA FCERROR!                 
*                                                                               
*                                                                               
*                                                                               
CHKPROC  PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
         LR    R6,R2               R6 - LINE NUMBER POINTER                     
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
         IF    (R15,Z),BEGIN       WE GOT NON-NULL LINE                         
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET                                                            
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR LABEL                              
         COMMENT                   R1,R0 - LOC, LEN OF LINE                     
         LA    R2,CHKTEMP          R2 - WORK AREA FOR LABEL NAME                
         ACALL SCNLABEL                                                         
         XPUSH R0,R1                                                            
         IF    (R15,NP),BEGIN                                                   
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'PROC    '),BEGIN                                        
         FCERR 'Labels are invalid on PROC statements. ',,LPTR=(R6)             
         END                                                                    
         END                                                                    
         XPOP R0,R1                                                             
*                                                                               
         COMMENT                   CHECK FOR PROC                               
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'PROC    '),BEGIN                                        
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG (R1),(R0)                                                         
         TSEG ': invalid.  Statement must occur within a PROC/XPROC.'           
         FCERR ' ',,LPTR=(R6)                                                   
         END                                                                    
*                                                                               
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKELSE - CHECK IF NEXT LINE IS ELSE STATEMENT                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING ELSE                                               
*      @R2 - LINE NUMBER OF LINE CONTAINING ELSE, IF ANY                        
*      R15 - CC=ZERO, ELSE FOUND                                                
*            CC=NZ, ELSE NOT FOUND, LINENO RESET                                
*                                                                               
*                                                                               
CHKELSE  PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF GOT NON-NULL LINE                         
         IF    (R15,Z),BEGIN                                                    
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET               GET LINE                                     
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ACALL SCANCMD             CHECK FOR ELSE                               
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'ELSE    '),BEGIN                                        
         CLEAR R15                 IF ELSE                                      
         END                                                                    
         ELSE  BEGIN               IF NOT ELSE, RESET LINE NUMBER               
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCPREV                                                           
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKELSF - CHECK IF NEXT LINE IS ELSEIF STATEMENT                             
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING ELSEIF                                             
*      @R2 - LINE NUMBER OF LINE CONTAINING ELSEIF, IF ANY                      
*      R15 - CC=ZERO, ELSEIF FOUND                                              
*            CC=NZ, ELSEIF NOT FOUND, LINENO RESET                              
*                                                                               
*                                                                               
CHKELSF  PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF GOT NON-NULL LINE                         
         IF    (R15,Z),BEGIN                                                    
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET               GET LINE                                     
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ACALL SCANCMD             CHECK FOR ELSEIF                             
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'ELSEIF  '),BEGIN                                        
         CLEAR R15                 IF ELSEIF                                    
         END                                                                    
         ELSE  BEGIN               IF NOT ELSEIF, RESET LINE NUMBER             
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCPREV                                                           
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKUNTIL - CHECK IF NEXT LINE IS UNTIL STATEMENT                             
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING UNTIL                                              
*      @R2 - LINE NUMBER OF LINE CONTAINING UNTIL, IF ANY                       
*      R15 - CC=ZERO, UNTIL FOUND                                               
*      IF UNTIL NOT FOUND, WE GIVE ERROR MESSAGE AND EXIT VIA FCERR             
*                                                                               
*                                                                               
CHKUNTIL PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF GOT NON-NULL LINE                         
         IF    (R15,Z),BEGIN                                                    
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET               GET LINE                                     
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ACALL SCANCMD             CHECK FOR UNTIL                              
         SCUPTOK R3                                                             
         IF    (@R3,NE,'UNTIL   '),BEGIN                                        
         TSEG  'REPEAT statement (block) not followed by UNTIL. '               
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF EOF, AND NO UNTIL FOUND                   
         ELSE  BEGIN                                                            
         TSEG  'REPEAT statement (block) not followed by UNTIL. '               
         FCERR ' ',,LPTR=(R2)                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKCASE - CHECK IF NEXT LINE IS CASE STATEMENT                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING CASE                                               
*      @R2 - LINE NUMBER OF LINE CONTAINING CASE, IF ANY                        
*      R15 - CC=ZERO, CASE FOUND                                                
*            CC=NZ, CASE NOT FOUND, LINENO RESET                                
*                                                                               
*                                                                               
CHKCASE  PROC  CHKWA                                                            
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF GOT NON-NULL LINE                         
         IF    (R15,Z),BEGIN                                                    
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET               GET LINE                                     
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ACALL SCANCMD             CHECK FOR CASE                               
         SCUPTOK R3                                                             
         IF    (@R3,EQ,'CASE    '),BEGIN                                        
         CLEAR R15                 IF CASE                                      
         END                                                                    
         ELSE  BEGIN               IF NOT CASE, RESET LINE NUMBER               
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCPREV                                                           
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKEND - CHECK IF NEXT LINE IS END STATEMENT                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LINE NUMBER OF BEGIN TO MATCH                                      
*      @R1 - EXEC LINE AREA                                                     
*      @R2 - LINE NUMBER, START SEARCH AT NEXT LINE NUMBER                      
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - LINE CONTAINING END                                                
*      @R2 - LINE NUMBER OF LINE CONTAINING END, IF ANY                         
*      R15 - CC=ZERO, END FOUND                                                 
*      IF END NOT FOUND, WE GIVE ERROR MESSAGE AND EXIT VIA FCERR               
*                                                                               
*  NOTE: CALLED FROM 'FCCASES' TO GET 'END' OF 'CASES BEGIN'                    
*                                                                               
*                                                                               
CHKEND   PROC  CHKWA                                                            
         ST    R0,CHKINFO                                                       
         ST    R1,CHKAREA                                                       
         ST    R2,CHKLPTR                                                       
*                                                                               
         COMMENT                   GET NEXT NON-NULL LINE                       
         ACALL FCNEXT                                                           
         IF    (R15,Z),BEGIN                                                    
         ACALL SKIPNULL                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF GOT NON-NULL LINE                         
         IF    (R15,Z),BEGIN                                                    
         L     R1,CHKAREA                                                       
         L     R2,CHKLPTR                                                       
         ACALL FCGET               GET LINE                                     
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         ACALL SCANCMD             CHECK FOR END                                
         SCUPTOK R3                                                             
         IF    (@R3,NE,'END     '),BEGIN                                        
         TSEG  'Missing END.',,CR                                               
         TSEG  'No matching END for BEGIN in '                                  
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         LA    R2,CHKINFO                                                       
         VCALL SEGXLNO                                                          
         TSEG  '.',,CR                                                          
         TSEG  'Missing END expected at or before line '                        
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         L     R2,CHKLPTR                                                       
         VCALL SEGXLNO                                                          
         FCERR '. '                                                             
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF END OF FILE, AND NO END FOUND             
         ELSE  BEGIN                                                            
         TSEG  'Missing END.',,CR                                               
         TSEG  'No matching END for BEGIN in '                                  
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         LA    R2,CHKINFO                                                       
         VCALL SEGXLNO                                                          
         TSEG  '.',,CR                                                          
         FCERR 'Missing END occurs before END OF FILE. '                        
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKLABEL - CHECK LABEL STATMENT,                                             
*             WE ALLOW:     <label name>:  LABEL                                
*                           <label name>:                                       
*             NOT ALLOWED:  <label name>:  COMMAND                              
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF STRING AFTER LABEL:                                      
*       R1 - LOCATION OF STRING (POINTS JUST PAST ':')                          
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, ALL OK                                                   
*             CC=NZ, INVALID LABEL, ERROR MESSAGE TSEG'D                        
*                                                                               
*  THIS ROUTINE CHECKS LABEL STATMENT.  A COMMAND MAY NOT FOLLOW                
*  A LABEL.  SEE EXEC FILE MANUAL FOR LABEL RESTRICTIONS.                       
*                                                                               
*                                                                               
CHKLABEL PROC  ,                                                                
*                                                                               
         SCINIT (R1),(R0)                                                       
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R3                                                             
*                                                                               
         COMMENT                   NO LABEL SPECIFICATION                       
         IF    (R0,Z),BEGIN                                                     
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR LABEL SPECIFICATION                
         ELSEIF (@R3,NE,'LABEL   '),BEGIN                                       
         SCTOK                                                                  
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.  '                                                   
         TSEG  'Command may not follow label. '                                 
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR GARBAGE AFTER LABEL                
         ELSEIF  (@R3,EQ,'LABEL   '),BEGIN                                      
         ACALL CHKOPTS                                                          
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKDEF - CHECK IF DEFINED                                                    
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - NAME OF LABEL/PROCEDURE                                            
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, DEFINED                                                   
*            CC=NZ, NOT DEFINED                                                 
*                                                                               
*  NOTE: ROUTINE CALLS GETFINFO TO TRY TO GET A PREVIOUS ENTRY.                 
*        THE PARMS FOR GETFINFO ARE A BIT WILD AND WOOLLY                       
*        THE GETFLOW ROUTINE CANNOT BE USED HERE BECAUSE IT                     
*        READS FROM THE EXECUTING EXEC FILE NOT THE LOADING                     
*        EXEC FILE.                                                             
*                                                                               
*  CAUTION: THIS ROUTINE IS USED FOR CHECKING TO SEE IF A NAME                  
*        IS PREVIOUSLY DEFINED.  THIS ROUTINE SHOULD BE USED                    
*        EXCLUSIVELY DURING FLOW CONTROL PROCESSING AS IT CHECKS                
*        THE FLOW CONTROL INFO FOR THE EXEC BEING LOADED AND NOT                
*        THE EXEC THAT IS BEING EXECUTED.                                       
*                                                                               
*                                                                               
CHKDWA   RECORD BEGIN                                                           
CHKDNTRY DS    XL(L'FWENTRY)       FLOW ENTRY FOR NAME                          
         END                                                                    
*                                                                               
*                                                                               
CHKDEF   PROC  CHKDWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,CHKDNTRY                                                      
*                                                                               
         COMMENT                   CREATE ENTRY FOR NAME                        
         CLEAR FWENTRY                                                          
         MVC   FWNAME,@R1                                                       
*                                                                               
         COMMENT                   GET ENTRY FOR NAME IF ANY                    
         LA    R0,L'FWENTRY-L'FWNAME                                            
         LA    R1,FWENTRY+L'FWNAME                                              
         LA    R2,FWNAME                                                        
         L     R3,CP#XLOAD                                                      
         L     R4,CPRGFC                                                        
         ACALL GETFINFO                                                         
*                                                                               
         COMMENT                   RETURNS R15 - CC=Z, IF DEFINED               
         COMMENT                           R15 - CC=NZ, IF NOT                  
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHKPDEF - CHECK IF PROC IS DEFINED                                           
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - NAME OF LABEL/PROCEDURE                                            
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, DEFINED AS PROCEDURE                                      
*            CC=NZ, NOT DEFINED OR NOT PROCEDURE                                
*                                                                               
*  NOTE: THIS ROUTINE IS CALLED TO VERIFY THAT NAME IS DEFINED                  
*        IN CURRENTLY EXECUTING EXEC AS A PROCEDURE.                            
*                                                                               
*                                                                               
CHKPWA   RECORD BEGIN                                                           
CHKPNTRY DS    XL(L'FWENTRY)       FLOW ENTRY FOR NAME                          
         END                                                                    
*                                                                               
*                                                                               
CHKPDEF  XPROC CHKPWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,CHKPNTRY                                                      
*                                                                               
         COMMENT                   CREATE ENTRY WITH NAME                       
         CLEAR FWENTRY                                                          
         MVC   FWNAME,@R1                                                       
*                                                                               
         COMMENT                   GET ENTRY FOR NAME IF ANY                    
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW                                                          
*                                                                               
         COMMENT                   RETURNS R15 - CC=Z, IF DEFINED               
         COMMENT                           R15 - CC=NZ, IF NOT                  
*                                                                               
         COMMENT                   IF DEFINED, MAKE SURE IT IS PROC             
         IF    ((R15,Z),AND,^FWPROC),BEGIN                                      
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNBEGIN - SCAN FOR BEGIN                                                    
*                                                                               
*  ON ENTRY:                                                                    
*       @R2 - LINE NUMBER                                                       
*       SCANCB - SCAN CONTROL BLOCK SET                                         
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - CHANGED                                                        
*       R15 - CC=Z, 'BEGIN' FOUND                                               
*             CC=NZ, 'BEGIN' NOT FOUND                                          
*                                                                               
*  THIS ROUTINE ALSO CHECKS FOR COMPOUND CONTROL STATEMENTS,                    
*  EG 'IF (...) IF (...), BEGIN ' , IF THIS ERROR IS DETECTED                   
*  WE EXIT VIA CVERROR VIA FCERR.                                               
*                                                                               
*                                                                               
SCNBEGIN PROC  ,                                                                
*                                                                               
         COMMENT                   SCAN FOR BEGIN                               
         LR    R4,R2               R4 - LINE NUMBER POINTER                     
         SCAN  BEGINPRT                                                         
*        SCANCHK                   SKIP ERRORS WITHIN CONSTRUCTS                
         IF    (R15,NEG),'LA R15,4'     IF ERR, TREAT AS NO BEGIN               
         IF    (R15,EQ,4),'CLEAR R15'   FOUND BEGIN                             
         ELSE  'LA R15,4'               BEGIN NOT FOUND                         
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       BEGIN KEYWORDS                                                         
*-                                                                              
BEGINPRT SCKW  ,NOBEGIN,NULL                                                    
         SCKW  BEGIN,GOTBEGIN                                                   
         SCKW  IF,BADBEGIN                                                      
         SCKW  ELSE,BADBEGIN                                                    
         SCKW  THEN,BADBEGIN                                                    
         SCKW  ELSEIF,BADBEGIN                                                  
         SCKW  DO,BADBEGIN                                                      
         SCKW  WHILE,BADBEGIN                                                   
         SCKW  PROC,BADBEGIN                                                    
         SCKW  XPROC,BADBEGIN                                                   
         SCKW  CASES,BADBEGIN                                                   
         SCKW  CASE,BADBEGIN                                                    
         SCKW  REPEAT,BADBEGIN                                                  
         SCKW  UNTIL,BADBEGIN                                                   
         SCKW  END,BADBEGIN                                                     
         SCKW  ,NOBEGIN                                                         
         SPACE 5                                                                
*-                                                                              
*-       begin found                                                            
*-                                                                              
GOTBEGIN PROC  ,                   GOT BEGIN                                    
         ACALL CHKOPTS                  CHECK ADDIIONAL TRASH                   
         IF    NZ,BEGIN                                                         
         FCERR ' ',,LPTR=(R4)                                                   
         END                                                                    
         LA    R15,4                                                            
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       no begin                                                               
*-                                                                              
NOBEGIN  PROC  ,                                                                
         LA    R15,8                                                            
         PEND  ,                                                                
         SPACE 5                                                                
*-                                                                              
*-       invalid keyword found                                                  
*-                                                                              
BADBEGIN PROC  ,                                                                
         SCTOK                                                                  
         TSEG  (R1),(R0)                                                        
         TSEG  ': invalid.',,CR                                                 
         TSEG  'Compound control statements not allowed. '                      
         FCERR ' ',,LPTR=(R4)                                                   
         COMMENT                   BYE BYE. WE DO NOT RETURN !                  
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCANCMD - SCAN COMMAND, SKIP ANY LABEL                                       
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - TEXT LENGTH                                                        
*       R1 - TEXT LOCATION                                                      
*                                                                               
*  ON EXIT:                                                                     
*       R0 - REMAINING LENGTH                                                   
*       R1 - REMAINING TEXT                                                     
*       SCANCB - SET UP                                                         
*       SCAN CB - COMMAND NAME IN NSCNCTOK (USE SCUPTOK RX FOR PTR)             
*                                                                               
*  NOTE: ROUTINE WILL SKIP ANY LABEL, BUT DOES NOT CHECK FOR                    
*    LABEL VALIDITY.                                                            
*                                                                               
*                                                                               
SCANCMD  PROC  ,                                                                
*                                                                               
         SCINIT (R1),(R0)         SET UP SCAN CONTROL BLOCK                     
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,POS),BEGIN                                                   
         AR    R1,R0                                                            
         DECR  R1                                                               
         IF    (@R1,EQ,':'),BEGIN  IF LABEL, SCAN AGAIN                         
         SCAN  ,                                                                
         SCANCHK                                                                
         END                                                                    
         END                                                                    
*                                                                               
         SCINFO ,                 SET UP R0,R1 RETURNS                          
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCNLABEL - SCAN LABEL,  IF ANY                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF LINE                                                     
*       R1 - LOCATION OF LINE                                                   
*      @R2 - LABEL RETURN AREA                                                  
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - LABEL, IF ANY  (LEFT JUSTIFIED PADDED W/BLANKS)                    
*      R15 - CC=ZERO, LABEL FOUND                                               
*            R1, R0 - LINE LOC, LEN EXCLUDING LABEL                             
*      R15 - CC=NEG, INVALID LABEL                                              
*            R1, R0 - LOC LEN OF INVALID LABEL                                  
*      R15 - CC=POS, NO LABEL FOUND                                             
*            R1, R0 - LOC, LEN OF LINE; SAME AS ON ENTRY                        
*                                                                               
*                                                                               
*                                                                               
SCLBWA   RECORD BEGIN                                                           
SCLBLOC  DS    F                   LOCATION OF LINE AT ENTRY                    
SCLBLEN  DS    F                   LENGTH OF LINE AT ENTRY                      
SCLBAREA DS    F                   ADDRESS OF LABEL SAVE AREA                   
SCLBTEMP DS    XL(L'VNENTRY)       TEMP WORK AREA FOR NAME                      
         DS    0S(L'VNNAME-L'FWNAME)    FLOW,VAR NAMES MUST                     
         DS    0S(L'FWNAME-L'VNNAME)    BE SAME LENGTH                          
         END                                                                    
*                                                                               
*                                                                               
SCNLABEL PROC  SCLBWA                                                           
         USING VNENTRY,R6                                                       
         LA    R6,SCLBTEMP                                                      
         ST    R0,SCLBLEN                                                       
         ST    R1,SCLBLOC                                                       
         ST    R2,SCLBAREA                                                      
*                                                                               
         COMMENT                   SCAN FOR LABEL                               
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
*                                                                               
         COMMENT                   IF NULL STRING, NO LABEL                     
         IF    (R0,Z),BEGIN                                                     
         L     R0,SCLBLEN                                                       
         L     R1,SCLBLOC                                                       
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   IF NO ':', NO LABEL                          
         AR    R1,R0                                                            
         DECR  R1                  R1 - AT ':'                                  
         IF    (@R1,NE,':'),BEGIN                                               
         L     R0,SCLBLEN                                                       
         L     R1,SCLBLOC                                                       
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
*                                                                               
         COMMENT                   WE HAVE <name>: ,,,                          
         LR    R5,R1               R5 - END OF LABEL                            
*                                                                               
         COMMENT                   RESCAN LABEL NAME,,                          
         L     R0,SCLBLEN                                                       
         L     R1,SCLBLOC                                                       
         LA    R2,SCLBTEMP                                                      
         VCALL SCNVNAME                                                         
*                                                                               
         COMMENT                   IF VALID LABEL                               
         COMMENT                                                                
         IF    ((R15,Z),AND,(VNPREFIX,EQ,0),AND,                       X        
               (@R1,EQ,':'),AND,(R0,POS),AND,(R1,EQ,R5)),BEGIN                  
         L     R2,SCLBAREA                                                      
         MVC   0(L'FWNAME,R2),VNNAME     SAVE NAME                              
         INCR  R1                        SKIP ':'                               
         DECR  R0                                                               
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF INVALID LABEL                             
         ELSE  BEGIN                                                            
         L     R0,SCLBLEN                                                       
         L     R1,SCLBLOC                                                       
         SCINIT (R1),(R0)                                                       
         SCAN  ,                                                                
         SCANCHK                                                                
         L     R15,=F'-4'                                                       
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCGET - GET LINE FROM EXEC                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER                                                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*                                                                               
FCGET    PROC  ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XLOAD                                                      
         L     R15,CPRGLOAD                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGLOAD                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGET                                                             
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCFIRST - GET FIRST LINE IN RANGE FROM EXEC                                  
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (START OF RANGE)                                       
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER                                                        
*      R15 - CC=ZERO, LINE GOT                                                  
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*                                                                               
FCFIRST  PROC  ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R0,CP#XLOAD                                                      
         L     R15,CPRGLOAD                                                     
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE EXEC LINE                             
         L     R15,CPRGLOAD                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETFRST                                                         
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCNEXT - GET LINE FROM EXEC                                                  
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (CURRENT)                                              
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER (NEXT)                                                 
*      R15 - CC=ZERO, LINE FOUND                                                
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*                                                                               
FCNEXT   PROC  ,                                                                
         LR    R3,R1               LINE AREA                                    
         L     R4,0(R2)            CURRENT LINE NUMBER                          
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE NEXT LINE NUMBER                      
         L     R15,CPRGLOAD                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETNEXT                                                         
*                                                                               
         COMMENT                   IF NO NEXT,                                  
         IF    NZ,BEGIN               RESET POINTERS TO CURRENT                 
         LR    R1,R3                                                            
         ST    R4,0(R2)                                                         
         ACALL FCGET                                                            
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCNEXTLN - GET NEXT LINE NUMBER IN EXEC                                      
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (CURRENT)                                              
*                                                                               
*  ON EXIT:                                                                     
*       R0 - NEXT LINE NUMBER                                                   
*      @R1 - UNCHANGED                                                          
*      @R2 - UNCHANGED                                                          
*      R15 - CC=ZERO, LINE FOUND                                                
*            CC=NZ, LINE NOT FOUND                                              
*                                                                               
*  IF NO NEXT,,, WE RETURN MAX+1 SIZED LINENO  (IE. R0=C'END ')                 
*                                                                               
FCNEXTLN PROC  ,                                                                
         L     R4,0(R2)            CURRENT LINE NUMBER                          
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE NEXT LINE                             
         L     R15,CPRGLOAD                                                     
         COMMENT @R2 - LINENO                                                   
         VCALL XLOCNEXT                                                         
         L     R0,0(R2)                                                         
         ST    R4,0(R2)            RESET LINE NUMBER                            
         IF    NZ,BEGIN                                                         
         L     R0,=CL4'END '                                                    
         END                                                                    
*                                                                               
         PRETURN (R0)                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCPREV - GET PREVIOUS LINE FROM EXEC                                         
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - LINE AREA                                                          
*      @R2 - LINE NUMBER (CURRENT)                                              
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LINE LENGTH                                                        
*       R1 - LINE LOCATION                                                      
*      @R2 - LINE NUMBER (PREVIOUS)                                             
*      R15 - CC=ZERO, LINE GOT                                                  
*            IF NO PREVIOUS LINE, WE BOMB !!!@#$%*&%$#!!!!!                   
*                                                                               
*                                                                               
FCPREV   PROC  ,                                                                
*                                                                               
         COMMENT                   SET EXEC SET NUMBER                          
         L     R15,CPRGLOAD                                                     
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   LOCATE PREV LINE NUMBER                      
         L     R15,CPRGLOAD                                                     
         LA    R0,&LINESZ                                                       
         COMMENT @R1 - LINE AREA                                                
         COMMENT @R2 - LINENO                                                   
         VCALL XGETPREV                                                         
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'GET/SAVE Flow Control Entries, Information '                    
*box                                                                            
*                                                                               
*                                                                               
*  GETFLOW - GET FLOW INFORMATION ENTRY                                         
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FLOW ENTRY, WITH ENTRY NAME FILLED IN                              
*       CP#XSET - FLOW SET NUMBER (CURRENT EXEC SET # TOO!)                     
*       CPRGFLOW - CURRENT FLOW RECORD GROUP POINTER                            
*                                                                               
*  ON EXIT:                                                                     
*      @R1 - FLOW ENTRY RETURNED                                                
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, ENTRY NOT FOUND                                             
*                                                                               
*  NOTE: GETFLOW AND SAVEFLOW ROUTINES ARE DIFFERENT IN THAT:                   
*    SAVEFLOW ROUTINE IS ONLY CALLED DURING EXEC LOAD AND USES                  
*    SPECIAL LOAD RECORD GROUP POINTERS, WHILE GETFLOW IS                       
*    CALLED ONLY DURING EXEC EXCUTION AND USES CURRENT EXEC FLOW                
*    RECORD GROUP POINTERS.                                                     
*                                                                               
*                                                                               
GETFLOW  PROC  ,                                                                
         USING FWENTRY,R6                                                       
         LR    R6,R1                                                            
*                                                                               
         COMMENT                   SET FLOW SET NUMBER                          
         L     R15,CPRGFLOW                                                     
         L     R0,CP#XSET                                                       
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                                                                
         L     R15,CPRGFLOW                                                     
         LA    R0,L'FWENTRY-L'FWNAME                                            
         LA    R1,FWENTRY+L'FWNAME                                              
         LA    R2,FWNAME                                                        
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVEFLOW - SAVE FLOW INFORMATION                                             
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - FLOW ENTRY                                                         
*       CP#XLOAD - FLOW SET NUMBER                                              
*       CPRGFC   - FLOW RECORD GROUP INFO POINTER                               
*                                                                               
*  ON EXIT:                                                                     
*       ENTRY SAVED, R15 - CC=ZERO                                              
*                                                                               
*  NOTE, FLOW INFO IS SAVED ONLY DURING THE LOADING PHASE.                      
*     LOAD RECD GROUP, NOT CURRENT RECD GROUP POINTERS USED.                    
*                                                                               
*                                                                               
SAVEFLOW PROC  ,                                                                
*                                                                               
         COMMENT                   SET FLOW SET NUMBER                          
         L     R15,CPRGFC                                                       
         L     R0,CP#XLOAD                                                      
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   SAVE FLOW INFO                               
         L     R15,CPRGFC                                                       
         LA    R0,L'FWENTRY-L'FWNAME                                            
         LR    R2,R1                                                            
         LA    R1,L'FWNAME(R1)                                                  
         VCALL XPUT                                                             
         IF    NEG,BEGIN                                                        
         KAPUT FLOWFULL,'EXEC flow control information overflow. '              
         END                                                                    
         IF    POS,BEGIN                                                        
         BOMB                                                                   
         END                                                                    
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GETFINFO - GET FLOW INFORMATION (UNFORMATED)                                 
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF INFO AREA                                                
*       R1 - LOCATION OF INFO AREA                                              
*       R2 - 16 CHARACTER INFO NAME                                             
*       R3 - FLOW SET #                                                         
*       R4 - FLOW RECORD GROUP INFORMATION DSECT ADDRESS                        
*                                                                               
*  ON EXIT:                                                                     
*       R0 - LENGTH OF INFO RETURNED                                            
*      @R1 - AREA CONTAINING INFO RETURNED                                      
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, ENTRY NOT FOUND                                             
*                                                                               
*  NOTE: THIS ROUTINE IS USED TO GET UNFORMATTED FLOW INFO (IE.                 
*    NLISTS) AND AND INFO FROM NON-CURRENT SET NUMBERS, ETC.                    
*                                                                               
*                                                                               
GETFINFO PROC  ,                                                                
         USING FWENTRY,R6                                                       
         LR    R5,R0               ALL REGISTER/PARMS DESCRIBED ABOVE           
*                                                                               
         COMMENT                   SET FLOW SET NUMBER                          
         LR    R15,R4                                                           
         LR    R0,R3                                                            
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   GET SPECIFIED FLOW INFO                      
         LR    R15,R4                                                           
         LR    R0,R5                                                            
         VCALL XGET                                                             
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SAVFINFO - SAVE FLOW INFORMATION (UNFORMATTED)                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - LENGTH OF INFO AREA                                                
*       R1 - LOCATION OF INFO AREA                                              
*       R2 - 16 CHARACTER INFO NAME                                             
*       R3 - FLOW SET #                                                         
*       R4 - FLOW RECORD GROUP INFORMATION DSECT ADDRESS                        
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO, ALL OK                                                    
*                                                                               
*                                                                               
*  NOTE: THIS ROUTINE IS USED TO SAVE UNFORMATTED FLOW INFO (IE.                
*    NLISTS) AND TO SAVE INFO INTO NON-CURRENT SET NUMBERS, ETC.                
*                                                                               
*                                                                               
SAVFINFO PROC  ,                                                                
         USING FWENTRY,R6                                                       
         LR    R5,R0               ALL REGISTER/PARMS DESCRIBED ABOVE           
*                                                                               
         COMMENT                   SET FLOW SET NUMBER                          
         LR    R15,R4                                                           
         LR    R0,R3                                                            
         VCALL XSETSET                                                          
*                                                                               
         COMMENT                   SAVE SPECIFIED FLOW INFO                     
         LR    R15,R4                                                           
         LR    R0,R5                                                            
         VCALL XPUT                                                             
         IF    NZ,BEGIN                                                         
         KAPUT FLOWFULL,'Exec Flow Control info overflow. '                     
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTXSCOPE - GET SCOPE OF XPROC                                                
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - XPROC SET NUMBER                                                   
*       R1 - FLOW RECORD GROUP POINTER ADDRESS                                  
*                                                                               
*  ON EXIT:                                                                     
*       R0 - START OF SCOPE (LINE CONTAINING XPROC STATEMENT)                   
*       R1 - END OF SCOPE                                                       
*                                                                               
*  NOTE: EXEC MUST EXIST AND BE LOADED, OTHERWISE WE BOMB !                     
*  NOTE2: WE USE ACKWARD FORM OF GET FLOW INFO, 'GETFINFO'.                     
*    THIS IS NECESSARY AS WE ARE NOT NECESSARILY GETTING                        
*    SCOPE INFO ABOUT THE CURRENT EXEC.                                         
*                                                                               
*                                                                               
GTXSWA   RECORD BEGIN                                                           
GTXSFLOW DS    XL(L'FWENTRY)       XPROC FLOW INFORMATION ENTRY                 
         END                                                                    
*                                                                               
*                                                                               
GTXSCOPE XPROC GTXSWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,GTXSFLOW                                                      
         LR    R3,R0                                                            
         LR    R4,R1                                                            
*                                                                               
         MVC   FWNAME,CVBLANKS                                                  
         MVC   FWNAME,=CL16'XPROC           '                                   
         LA    R0,L'FWENTRY-L'FWNAME                                            
         LA    R1,FWENTRY+L'FWNAME                                              
         LA    R2,FWNAME                                                        
         COMMENT R3 - XPROC SET#                                                
         COMMENT R4 - FLOW RG POINTER                                           
         ACALL GETFINFO            GET XPROC FLOW INFO ENTRY                    
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RETURN SCOPE START, END                      
         L     R0,FWSCOPE                                                       
         L     R1,FWSCOPND                                                      
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTPSCOPE - GET SCOPE OF PROCEDURE                                            
*                                                                               
*  ON ENTRY:                                                                    
*       R1 - PROCEDURE NAME                                                     
*       CP#XSET - SET NUMBER                                                    
*       CPRGFLOW - FLOW RECORD GROUP                                            
*                                                                               
*  ON EXIT:                                                                     
*       R0 - START OF SCOPE (LINE CONTAINING XPROC STATEMENT)                   
*       R1 - END OF SCOPE                                                       
*                                                                               
*                                                                               
GTPSWA   RECORD BEGIN                                                           
GTPSFLOW DS    XL(L'FWENTRY)       XPROC FLOW INFORMATION ENTRY                 
         END                                                                    
*                                                                               
*                                                                               
GTPSCOPE XPROC GTPSWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,GTPSFLOW                                                      
*                                                                               
         MVC   FWNAME,0(R1)                                                     
         LA    R1,FWENTRY                                                       
         ACALL GETFLOW             GET PROCEURE FLOW INFO ENTRY                 
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         COMMENT                   RETURN SCOPE START, END                      
         L     R0,FWSCOPE                                                       
         L     R1,FWSCOPND                                                      
*                                                                               
         CLEAR R15                                                              
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTXNLIST - GET XPROC NAME LIST                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - EXEC SET NUMBER                                                    
*      @R1 - FLOW RECORD GROUP INFO BLOCK                                       
*      @R2 - NAME LIST AREA                                                     
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - XPROC NAME LIST                                                    
*      R15 - CC=ZERO                                                            
*            ANY INVALID REQUESTS BOMB !                                        
*                                                                               
*                                                                               
GTXNWA   RECORD BEGIN                                                           
GTXNFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
GTXNLIST XPROC GTXNWA                                                           
*                                                                               
         COMMENT                   GET XPROC NAME LIST                          
         LR    R4,R1               (SEE GETFINFO CONVENTIONS)                   
         LR    R3,R0                                                            
         LA    R0,L'NLIST                                                       
         LR    R1,R2                                                            
         LA    R2,=CL16'XPROC_NLIST     '                                       
         ACALL GETFINFO            GET XPROC NAME LIST                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  GTPNLIST - GET XPROC NAME LIST                                               
*                                                                               
*  ON ENTRY:                                                                    
*      @R1 - PROCEDURE NAME                                                     
*      @R2 - NAME LIST AREA                                                     
*       CP#XSET - EXEC SET NUMBER                                               
*       CPRGFLOW - FLOW RECORD GROUP INFO BLOCK                                 
*                                                                               
*  ON EXIT:                                                                     
*      @R2 - PROC NAME LIST                                                     
*      R15 - CC=ZERO                                                            
*            ANY INVALID REQUESTS BOMB !                                        
*                                                                               
*                                                                               
GTPNWA   RECORD BEGIN                                                           
GTPNFLOW DS    XL(L'FWENTRY)                                                    
         END                                                                    
*                                                                               
*                                                                               
GTPNLIST XPROC GTPNWA                                                           
         USING FWENTRY,R6                                                       
         LA    R6,GTPNFLOW                                                      
*                                                                               
         COMMENT                   GET PROC LINENO                              
         ACALL GTPSCOPE                                                         
         COMMENT                   RETURNS R0 - PROC LINENO                     
*                                                                               
         COMMENT                   CREATE NLIST NAME                            
         MVC   FWNAME,=CL16'....PROC_NLIST  '                                   
         ST    R0,FWNAME                                                        
*                                                                               
         COMMENT                   GET XPROC NAME LIST                          
         L     R4,CPRGFLOW         (SEE GETFINFO CONVENTIONS)                   
         L     R3,CP#XSET                                                       
         LA    R0,L'NLIST                                                       
         LR    R1,R2                                                            
         LA    R2,FWNAME                                                        
         ACALL GETFINFO            GET XPROC NAME LIST                          
         IF    NZ,BEGIN                                                         
         BOMB                                                                   
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DELFLOW - DELETE FLOW INFO                                                   
*                                                                               
*  ON ENTRY:                                                                    
*      R0 - FLOW SET NUMBER                                                     
*      R1 - FLOW RECORD GROUP INFO                                              
*                                                                               
*  ON EXIT:                                                                     
*      R15 - CC=ZERO                                                            
*                                                                               
*                                                                               
DELFLOW  XPROC ,                                                                
         LR    R15,R1                                                           
         LR    R0,R0                                                            
         VCALL XDELSET                                                          
         CLEAR R15                                                              
         PEND  ,                                                                
         TITLE 'FLOW ERROR ROUTINES'                                            
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FCERROR - FLOW CONTROL ERROR PROCESSING                                      
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - ERROR MSG LENGTH                                                   
*       R1 - ERROR MSG LOCATION                                                 
*     @R15 - LINE NUMBER (ZERO IF NONE)                                         
*                                                                               
*  ON EXIT:                                                                     
*       WE PRINT ERROR MESSAGES AND CALL LDABORT ROUTINE                        
*       WHICH EXITS VIA CVERROR.                                                
*                                                                               
*                                                                               
*                                                                               
FCERROR  PROC  ,                                                                
         LR    R2,R15              @R2 - LINE NUMBER                            
*                                                                               
         COMMENT                   SEG ERROR MESSAGE                            
         TSEG  (R1),(R0),CR                                                     
*                                                                               
         COMMENT                   SEG ERROR LINE NUMBER                        
         IF    (R2,NZ),BEGIN       IF LINE NUMBER SPECIFIED                     
         TSEG  'Error in line '       ERROR IN LINE ...                         
         L     R0,CP#XLOAD                                                      
         L     R1,CPRGLOAD                                                      
         COMMENT @R2-LINENO                                                     
         VCALL SEGXLNO                                                          
         TCR                                                                    
         L     R0,CP#XLOAD         SEG ERROR LINE                               
         L     R1,CPRGLOAD                                                      
         COMMENT @R2-LINENO                                                     
         VCALL SEGXLINE                                                         
         END                                                                    
*                                                                               
         COMMENT                   ABORT XLOAD                                  
         VCALL LDABORT             DELETES EXEC/FLOW LINES                      
         BOMB                        AND EXITS VIA CVERROR !@!!                 
*                                                                               
         BOMB                      NOT USED                                     
         PEND  ,              NOT USED                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  NOPAREN - Missing closing parenthesis                                        
*                                                                               
*  ON ENTRY:                                                                    
*       R1,R0 - TEXT LOC,LEN WHERE ")" IS EXPECTED                              
*                                                                               
*                                                                               
NOPAREN  PROC  ,                                                                
         XPUSH R0,R1                                                            
         TSEG  'Expecting ")".  '                                               
         XPOP  R0,R1                                                            
         VCALL CHECKEND                                                         
         BNZ   CVERROR                                                          
         ERROR 'Unmatched parenthesis in expression.'                           
         BOMB  ,                   ERROR DOES NOT RETURN                        
         PEND  ,              NEVER EXECUTED                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  NOTBOOL - invalid boolean expression                                         
*                                                                               
*                                                                               
NOTBOOL  PROC  ,                                                                
         TSEG  'Expression does not evaluate to boolean '                       
         TSEG  '(i.e. true or false). '                                         
         B     CVERROR             DOES NOT RETURN                              
         BOMB                                                                   
         PEND  ,              NEVER EXECUTED                                    
         EJECT                                                                  
*-                                                                              
*-       LEFT TO WRITE !@!!!                                                    
*-                                                                              
FCSIMPLE PROC  ,                                                                
         PEND  ,                                                                
FCDO     PROC  ,                                                                
         PEND  ,                                                                
FCBEGIN  PROC  ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         VLTORG                                                                 
         END   .                                                                
