TINT     TITLE 'WYLBUR''s Terminal Internal Commands'                           
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLTINT  CSECT                                                                  
TINT     IDENT 2047                09:48:52 02/16/02  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
         SPACE 2                                                                
         SYSDEFN ,                 Define installation params                   
         GBLB  &PORTINF            (See SYSDEFN)                                
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         POP   DSECTS                                                           
         RECORD 'MIB'                                                           
*                                                                               
RQST     RECORD 'RQST'                                                          
*                                                                               
PDB      RECORD 'PDB'                                                           
*                                                                               
KWR      RECORD 'KWR'                                                           
*                                                                               
         COPY  FSB                                                              
*                                                                               
         COPY  FDB                                                              
*                                                                               
         COPY  CONSTRC                                                          
*                                                                               
         COPY  ASYNTRC                                                          
*                                                                               
         COPY  PDPTRC                                                           
*                                                                               
         COPY  ELFTRC                                                           
*                                                                               
         COPY  VIRTTRC                                                          
*                                                                               
VFTRACE  RECORD BEGIN                                                           
         COPY VFTRACE                                                           
         END                                                                    
*                                                                               
VPTRACE  RECORD BEGIN                                                           
         COPY VPTRACE                                                           
         END                                                                    
         TITLE 'DSECTS'                                                         
         COPY  PSB                                                              
         EJECT                                                                  
         COPY  SSB                                                              
         EJECT                                                                  
         RECORD 'SIB'                                                           
         EJECT                                                                  
         COPY  TRANGE                                                           
         TITLE 'LOGON/LOGOFF Logging Routine'                                   
LGSESSWA RECORD BEGIN                                                           
LGSESSWL DS    CL8                 Save for cmd name                            
LGSESSLL DS    X                   Save for cmd name length                     
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  LOGSESS -- Routine to log session logon/logoff.                              
*                                                                               
*    On entry:                                                                  
*      R1,R0 - msg text ("LOGON:" or "LOGOFF:")                                 
*                                                                               
LOGSESS  XPROC LGSESSWA                                                         
         IF    CVFTEST,LOGSEXIT                                                 
         IF    (CPSACCT,Z),LOGSEXIT                                             
*        LA    R6,LGSESSWA         R6-->WA                                      
*        USING LGSESSWA,R6                                                      
*                                                                               
         LR    R15,R0              Get command length                           
         CEIL  R15,L'LGSESSWL      Make sure of length                          
         STC   R15,LGSESSLL        Save length                                  
         MOVE  R15,LGSESSWL,@R1    Save text                                    
*                                                                               
         TCLEAR                                                                 
         TSEG  LGSESSWL,LC:LGSESSLL,B  Seg LOGON or LOGOFF                      
*                                                                               
         TSEG  CPSGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPSUSER,,B                                                       
*                                                                               
         LA    R1,CPSIB                                                         
         VCALL LINEDISP                                                         
         LCALL LOGSWTO                                                          
*                                                                               
         TCLEAR                                                                 
*                                                                               
         AIF   (NOT &PORTINF).PINF1                                             
         TSEG  LGSESSWL,LC:LGSESSLL,B  Seg LOGON or LOGOFF                      
         TSEG  CPSFE,,B            Seg front end id                             
         TSEG  CPSPORT,,B          Seg port name                                
         SETMSG CPSPINFO            Get port info, if any                       
         IF    (CPSPINFO,Z),'TSEG CVBLANKS,(R0),B'                              
         ELSE  'TSEG (R1),(R0),B'                                               
         SETMSG CPSPLOC             Get port loc, if any                        
         IF    (CPSPLOC,Z),'TSEG CVBLANKS,(R0)'                                 
         ELSE  'TSEG (R1),(R0)'                                                 
         LCALL LOGSWTO                                                          
         TCLEAR                                                                 
.PINF1   ANOP                                                                   
*                                                                               
LOGSEXIT PEND                                                                   
*                                                                               
LOGSWTO  PROC  ,                                                                
         XPUSH ,,260,PTR=R1                                                     
         L     R15,CPSEGLENF                                                    
         LR    R2,R15              R2 holds length                              
         LA    R3,@R1+4            R3 -> text                                   
         LA    R0,@R15+4                                                        
         STH   R0,@R1              Len                                          
         MVC   @R1+2(2),=X'0200'   MCS flags (hardcopy-SYSLOG only)             
         L     R14,CPSEGLOC        Load source address                          
         MOVE  R15,@R1+4,@R14                                                   
         WTO   MF=(E,(1))                                                       
         XPOP  ,,260                                                            
         PEND  ,                   RETURN                                       
*                                                                               
*        DROP  R6                                                               
         TITLE 'SHOW SYSTEMS Command'                                           
*box                                                                            
*                                                                               
*  SHOWSYS -- SHOW SYSTEMS command.                                             
*                                                                               
SHOWSYS  XPROC SSB                                                              
*                                                                               
         VCALL COLLOPTS            Allow collect options                        
*                                                                               
         CLEAR R6                                                               
SSYSLP   STH   R6,CPDOUB                                                        
         TSEG  CPDOUB,2            Subsystem id                                 
         TCNTL CTLSNSS             Sense subsystem info                         
         BP    CVNEXT              All done                                     
         BM    CVMILERR                                                         
*                                                                               
         MVC   SSB,@R1             Save ssb                                     
*                                                                               
         TSEG  SSBNAME,,B                                                       
         SETMSG SSBJOBNM                                                        
         CLEAR R2                                                               
         IF    (SSBSCLCK,NZ),BEGIN                                              
         VCALL LOCALTOD                                                         
         LR    R2,R0                                                            
         LR    R3,R1                                                            
         LM    R0,R1,SSBSCLCK                                                   
         XPUSH ,,30,PTR=R15                                                     
         VCALL FMTRCLCK                                                         
         LR    R2,R0                                                            
         TSEG  (R1),(R0)           "hh:mm" or "hh:mm yy/dd/yyyy"                
         XPOP  ,,30                                                             
         END                                                                    
         LA    R0,17                                                            
         SR    R0,R2               Pad to 17 chars                              
         TSEG  CVBLANKS,(R0)                                                    
*                                                                               
         TNUM  LH:SSBCUR,4                                                      
         TSEG  ' users'                                                         
         TNUM  LH:SSBMAX,5                                                      
         TSEG  ' max'                                                           
         TNUM  L:SSBNIO,9                                                       
         TSEG  ' I/O''s'                                                        
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         LH    R6,SSBID            Current subsystem id                         
         B     SSYSLP                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SYSCHECK -- Routine to determine if the specified subsystem                  
*    is running.                                                                
*                                                                               
*      On entry:                                                                
*        R1 - @ 8 character subsystem name                                      
*                                                                               
*      On exit, R15 (and cc):                                                   
*        0  - yes                                                               
*        nz - no                                                                
*                                                                               
SYSCWA   RECORD BEGIN                                                           
SYSCID   DS    H                   Working subsystem id                         
SYSCNAME DS    CL8                 Subsystem name to check                      
SYSCSSB  DS    XL(L'SSB)           Working subsystem info block                 
         END                                                                    
*-                                                                              
SYSCHECK XPROC SYSCWA                                                           
         MVC   SYSCNAME,@R1        Save subsystem name                          
*-                                                                              
*-       Sense every subsystem and compare the name with the one                
*-         we are looking for.                                                  
*-                                                                              
         CLEAR SYSCID              Start at first subsystem                     
*                                                                               
         LOOP  BEGIN                                                            
         TCLEAR                                                                 
         TSEG  SYSCID              Subsystem id                                 
         TCNTL CTLSNSS             Sense subsystem info                         
         IF    NZ,EXIT             All done, scram                              
*                                                                               
         LA    R6,SYSCSSB                                                       
         WITH  (SSB,R6)                                                         
         MVC   SSB,@R1             Save SSB                                     
         IF    (SSBNAME,EQ,SYSCNAME),BEGIN  We found it...                      
         CLEAR R15                 Set return code                              
         EXIT  SYSCHECK                                                         
         END                                                                    
*                                                                               
         MVC   SYSCID,SSBID        Set next id                                  
         END                                                                    
*-                                                                              
*-       Subsystem not found, give bad return code.                             
*-                                                                              
         LA    R15,4               Bad return code                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SYSGTSSB -- Routine to return subsystem block contents.                      
*                                                                               
*      On entry:                                                                
*        R1 - @ 8 character subsystem name                                      
*        R2 - Pointer to buffer big enough to hold returned                     
*             SSB                                                               
*                                                                               
*      On exit, R15 (and cc):                                                   
*        0  - Subsystem information returned                                    
*        nz - Subsystem not up - no information returned                        
*                                                                               
*-                                                                              
SYSGTSSB XPROC SYSCWA                                                           
         MVC   SYSCNAME,@R1        Save subsystem name                          
*-                                                                              
*-       Sense every subsystem and compare the name with the one                
*-         we are looking for.                                                  
*-                                                                              
         CLEAR SYSCID              Start at first subsystem                     
*                                                                               
         LOOP  BEGIN                                                            
         TCLEAR                                                                 
         TSEG  SYSCID              Subsystem id                                 
         TCNTL CTLSNSS             Sense subsystem info                         
         IF    NZ,EXIT             All done, scram                              
*                                                                               
         LA    R6,SYSCSSB                                                       
         WITH  (SSB,R6)                                                         
         MVC   SSB,@R1             Save SSB                                     
         IF    (SSBNAME,EQ,SYSCNAME),BEGIN  We found it...                      
         MVC   0(L'SSB,R2),SSB     Copy it for the caller                       
         CLEAR R15                 Set return code                              
         EXIT  SYSGTSSB                                                         
         END                                                                    
*                                                                               
         MVC   SYSCID,SSBID        Set next id                                  
         END                                                                    
*-                                                                              
*-       Subsystem not found, give bad return code.                             
*-                                                                              
         LA    R15,4               Bad return code                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'SHOW NETWORK Command'                                           
SHNETWA  RECORD BEGIN                                                           
SHNFLAG  FLAG                                                                   
         FLAG  SHNFADDR            - Address                                    
         FLAG  SHNFTRC             - Trace                                      
         FLAG  SHNFSOME            - Have at least one trace entry              
*                                                                               
SHNTNO   DS    H                   No. of trace table entries                   
SHNTLEN  DS    H                   Length of an individual entry                
*                                                                               
SHNRNG   DS    XL(L'RNGCB)                                                      
SHNCB    SEGCB ,                   Saved CPSEG                                  
SHNINIT  EQU   SHNFLAG,*-SHNFLAG,C'X'                                           
*                                                                               
         DS    0F                                                               
SHNTRC   DS    XL8000              Trace area                                   
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SHOWNET -- SHOW NETWORK command.                                             
*                                                                               
SHOWNET  XPROC                                                                  
         L     R2,=AL4(L'SHNETWA)  SHNETWA MUST BE PUSHED BY HAND               
         XPUSH ,,(R2),PTR=R6       IT IS VERY BBBBIGGGG WORK AREA               
         USING SHNETWA,R6                                                       
         CLEAR SHNINIT                                                          
         ST    R6,CPCWA                                                         
*                                                                               
         IF    (CPCMNM,EQ,'V'),BEGIN  Show virtual...                           
         VCALL COLLOPTS                                                         
         SCINIT 'VIRTUAL'                                                       
         END                                                                    
*                                                                               
         LA    R0,RNGTFE*256+L'RNGFDALL                                         
         SETSCKWS SHNETPRT                                                      
         LA    R15,SHNRNG                                                       
         VCALL GETTRNG                                                          
         BZ    CVABORT                                                          
*                                                                               
         WITH  (RNGCB,R15),'SET RNGFMULT+RNGFLIST; CLEAR RNGFVER'               
         CLEAR R0                  No selection co-routine                      
         LA    R1,SHNETRTN         Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         SPACE 2                                                                
SHNETPRT SCKW  ADDRESS,SHONETA,A                                                
         SCKW  TRACE,SHONETT,A                                                  
         SCKW  ,V(GETRPSH),PUSH                                                 
         SCKW  ,,END                                                            
*                                                                               
SHONETA  PROC  ,                                                                
         SET   SHNFADDR                                                         
         PEND                                                                   
*                                                                               
SHONETT  PROC  ,                                                                
         SET   SHNFTRC                                                          
         PEND                                                                   
         EJECT                                                                  
*                                                                               
* DEFAULT RANGE SCAN TABLE                                                      
*                                                                               
GETRPRT  SCKW  ,V(GETRPSH),PUSH                                                 
         SCKW  ,,END                                                            
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHNETRTN -- SHOW NETWORK range co-routine.                                   
*                                                                               
*    On entry:                                                                  
*      R1 - FSB ptr                                                             
*                                                                               
SHNETRTN PROC  ,                                                                
         L     R6,CPCWA            SHNETWA ptr                                  
         LR    R5,R1                                                            
         USING FSB,R5                                                           
*                                                                               
         IF    SHNFADDR,BEGIN      Give internal address...                     
         BTX   CPDOUB,6,L:FSBFEBP                                               
         TSEG  (R1),(R0)                                                        
         TSEG  CVBLANKS,2                                                       
         END                                                                    
*                                                                               
         LA    R1,FSB                                                           
         VCALL NETPDISP                                                         
         BNZ   CVABORT                                                          
*                                                                               
         IF    SHNFTRC,BEGIN       Display trace table...                       
         MVC   SHNCB,CPSEG         Save current CPSEG                           
         SEGINIT SHNTRC,LH:=AL2(L'SHNTRC),CPSEG  Init new seg buf               
         MVC   CPSEGRTN,SHNCBRTN   Set appropriate routine                      
*                                                                               
         XPUSH ,,L'FDBT1,PTR=R1                                                 
         WITH  (FDBT1,R1),BEGIN                                                 
         CLEAR FDBT1                                                            
         MVC   FDBTFE,FSBID        Front-end id                                 
         MVI   FDBTDNO,FDBCSNSF    Sense front-end                              
         TSEG  FDBT1                                                            
         XPOP  ,,L'FDBT1                                                        
         END                                                                    
*                                                                               
         TCNTL CTLSNSD             Get device-dependent info                    
         MVC   CPSEG,SHNCB         Restore seg buffer                           
         BNZ   CVMILERR                                                         
*                                                                               
         LA    R15,SHNTRC                                                       
         WITH  (FDBF1,R15),BEGIN                                                
         MVC   SHNTLEN,FDBFFLEN    Length of a trace entry                      
         MVC   SHNTNO,FDBFFTNO     No. of trace entries                         
         LA    R4,FDBFFTRC         First trace entry ptr                        
         END                                                                    
*-                                                                              
*-       Display the trace entries from oldest to newest.                       
*-                                                                              
         CLEAR SHNFSOME            Assume nothing in table                      
*                                                                               
         WHILE (SHNTNO,NZ),BEGIN                                                
         LM    R0,R1,@R4           Clock                                        
         IF    (R0,Z),EXIT         End of trace table                           
         SET   SHNFSOME            Indicate not empty                           
*                                                                               
         LA    R15,CPDOUB                                                       
         VCALL FMTTIME             "hh:mm:ss.hh"                                
         TSEG  (R1),(R0),B                                                      
*                                                                               
         LA    R15,RNGTFE                                                       
         SETMSG (R4),LH:SHNTLEN     Trace entry                                 
         LC    RAR,FSBTYPE                                                      
         IF    (RAR,GT,L'FSBTMAX),'CLEAR RAR'                                   
         SLL   RAR,2                                                            
         L     RAR,TRCTBL(RAR)     Trace formatting routine                     
         BASR  RAR,RAR                                                          
*                                                                               
         AH    R4,SHNTLEN          Next trace table entry                       
         DECR  R15,SHNTNO          One less entry now                           
         END                                                                    
*                                                                               
         IF    ^SHNFSOME,'TSEG "Trace table is empty.",,CR'                     
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  FSB,SHNETWA                                                      
         TITLE 'SHOW MDEVICE Command'                                           
MDEVWA   RECORD BEGIN                                                           
MDEVFLAG FLAG                                                                   
         FLAG  MDEFSOME            - We have something                          
*                                                                               
MDEVUNIT DS    F                   Binary unit address                          
*                                                                               
MDEVTNO  DS    H                   No. of trace table entries                   
MDEVTLEN DS    H                   Length of an individual entry                
*                                                                               
MDEVCB   SEGCB ,                   Saved CPSEG                                  
MDEVINIT EQU   MDEVFLAG,*-MDEVFLAG,C'X'                                         
*                                                                               
         DS    0F                                                               
MDEVTRC  DS    XL1024              Trace area                                   
*                                                                               
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SHOWMDEV -- SHOW MDEVICE command.                                            
*                                                                               
SHOWMDEV XPROC MDEVWA                                                           
         CLEAR MDEVINIT                                                         
*-                                                                              
*-       Pick up unit address.                                                  
*-                                                                              
         SCAN                                                                   
         SCANCHK                                                                
         SCUPTOK R1                                                             
         IF    (R0,Z),BEGIN                                                     
         ERROR 'Missing MILTEN unit-address.'                                   
         END                                                                    
*                                                                               
         XTB   (R1),(R0)           Convert to binary                            
         BNZ   CVNVALID            Bad hex                                      
         IF    (R15,GT,X'FFF'),CVNVALID  Out of bounds                          
         ST    R15,MDEVUNIT        Save hex                                     
*                                                                               
         VCALL COLLOPTS            Collect options                              
*-                                                                              
*-       Get device trace from MILTEN.                                          
*-                                                                              
         MVC   MDEVCB,CPSEG        Save current CPSEG                           
         SEGINIT MDEVTRC,,CPSEG    Init new seg buffer                          
         MVC   CPSEGRTN,MDEVCBRTN  Set appropriate routine                      
*                                                                               
         TSEG  MDEVUNIT            Pass unit-address as parm                    
*                                                                               
         TCNTL CTLSNSMD            Get device trace info                        
         MVC   CPSEG,MDEVCB        Restore CPSEG                                
         BNZ   CVMILERR                                                         
*                                                                               
         MVC   MDEVTNO,MDEVTRC     No. of trace entries                         
         MVC   MDEVTLEN,MDEVTRC+2  Length of a trace entry                      
         LA    R4,MDEVTRC+4        First trace entry ptr                        
*-                                                                              
*-       Display the trace entries from oldest to newest.                       
*-                                                                              
         TSEG  'MILTEN I/O trace for unit '                                     
         THEX  L:MDEVUNIT,3                                                     
         TCR                                                                    
         TCR                                                                    
*                                                                               
         CLEAR MDEFSOME            Assume nothing in table                      
*                                                                               
         WHILE (MDEVTNO,NZ),BEGIN                                               
         LM    R0,R1,@R4           Clock                                        
         IF    (R0,Z),EXIT         End of trace table                           
         SET   MDEFSOME            Indicate not empty                           
*                                                                               
         LA    R15,CPDOUB                                                       
         VCALL FMTTIME             "hh:mm:ss.hh"                                
         TSEG  (R1),(R0),B                                                      
*                                                                               
         TSEG  @R4+8,8,T           Trace name                                   
         TCR                                                                    
*                                                                               
         AH    R4,MDEVTLEN         Next trace table                             
         DECR  R15,MDEVTNO         One less entry now                           
         END                                                                    
*                                                                               
         IF    ^MDEFSOME,'TSEG "Trace table is empty.",,CR'                     
         B     CVNEXT                                                           
         PEND                                                                   
         TITLE 'SHOW PORT/PORTS Command'                                        
SHPRTWA  RECORD BEGIN                                                           
SHPFLAG  FLAG                                                                   
         FLAG  SHPFADDR            - Address                                    
         FLAG  SHPFTRC             - Trace                                      
         FLAG  SHPFSOME            - Have at least one trace entry              
*                                                                               
SHPTNO   DS    H                   No. of trace table entries                   
SHPTLEN  DS    H                   Length of an individual entry                
*                                                                               
SHPRTRNG DS    XL(L'RNGCB)                                                      
SHPCB    SEGCB ,                   Saved CPSEG                                  
SHPINIT  EQU   SHPFLAG,*-SHPFLAG,C'X'                                           
*                                                                               
         DS    0F                                                               
SHPTRC   DS    XL1024              Trace area                                   
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  SHOWPORT -- SHOW PORT/PORTS command.                                         
*                                                                               
SHOWPORT XPROC SHPRTWA                                                          
         LA    R6,SHPRTWA                                                       
         USING SHPRTWA,R6                                                       
         CLEAR SHPINIT                                                          
         ST    R6,CPCWA                                                         
*                                                                               
         LA    R0,RNGTPORT*256+L'RNGFDOUR                                       
         IF    (@R15,EQ,'PORTS'),'LA R0,RNGTPORT*256+L"RNGFDALL'                
         SETSCKWS SHPRTPRT                                                      
         LA    R15,SHPRTRNG                                                     
         VCALL GETTRNG                                                          
         BZ    CVABORT                                                          
*                                                                               
         WITH  (RNGCB,R15),'SET RNGFMULT+RNGFLIST; CLEAR RNGFVER'               
         CLEAR R0                  No selection co-routine                      
         LA    R1,SHPRTRTN         Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         DROP  BR                                                               
         SPACE 2                                                                
SHPRTPRT SCKW  ADDRESS,SHOPORTA,A                                               
         SCKW  TRACE,SHOPORTT,A                                                 
         SCKW  ,V(GETRPSH),PUSH                                                 
         SCKW  ,,END                                                            
*                                                                               
SHOPORTA PROC  ,                                                                
         SET   SHPFADDR                                                         
         PEND                                                                   
*                                                                               
SHOPORTT PROC  ,                                                                
         SET   SHPFTRC                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHPRTRTN -- SHOW PORT(S) range co-routine.                                   
*                                                                               
*    On entry:                                                                  
*      R1 - PSB ptr                                                             
*                                                                               
SHPRTRTN PROC  ,                                                                
         L     R6,CPCWA            Shprtwa ptr                                  
         LR    R5,R1                                                            
         USING PSB,R5                                                           
*                                                                               
         IF    SHPFADDR,BEGIN      Give internal address...                     
         BTX   CPDOUB,6,L:PSBPIBP                                               
         TSEG  (R1),(R0)                                                        
         TSEG  CVBLANKS,2                                                       
         END                                                                    
*                                                                               
         LA    R1,PSB                                                           
         VCALL PORTDISP                                                         
         TWRITE                                                                 
         BNZ   CVABORT                                                          
*                                                                               
         IF    SHPFTRC,BEGIN       Display trace table...                       
         MVC   SHPCB,CPSEG         Save current CPSEG                           
         SEGINIT SHPTRC,,CPSEG     Init new seg buffer                          
         MVC   CPSEGRTN,SHPCBRTN   Set appropriate routine                      
*                                                                               
         XPUSH ,,L'FDBT2,PTR=R1                                                 
         WITH  (FDBT2,R1),BEGIN                                                 
         CLEAR FDBT2                                                            
         MVC   FDBTFE,PSBFEID      Front-end id                                 
         MVI   FDBTDNO,FDBCSNSP    Sense port                                   
         MVC   FDBT2P,PSBPNO       Port number                                  
         TSEG  FDBT2                                                            
         XPOP  ,,L'FDBT2                                                        
         END                                                                    
*                                                                               
         TCNTL CTLSNSD             Get device-dependent info                    
         MVC   CPSEG,SHPCB         Restore CPSEG                                
         BNZ   CVMILERR                                                         
*                                                                               
         LA    R15,SHPTRC                                                       
         WITH  (FDBF2,R15),BEGIN                                                
         MVC   SHPTLEN,FDBFPLEN    Length of a trace entry                      
         MVC   SHPTNO,FDBFPTNO     No. of trace entries                         
         LA    R4,FDBFPTRC         First trace entry ptr                        
         END                                                                    
*-                                                                              
*-       Display the trace entries from oldest to newest.                       
*-                                                                              
         CLEAR SHPFSOME            Assume nothing in table                      
*                                                                               
         WHILE (SHPTNO,NZ),BEGIN                                                
         LM    R0,R1,@R4           Clock                                        
         IF    (R0,Z),EXIT         End of trace table                           
         SET   SHPFSOME            Indicate not empty                           
*                                                                               
         LA    R15,CPDOUB                                                       
         VCALL FMTTIME             "hh:mm:ss.hh"                                
         TSEG  (R1),(R0),B                                                      
*                                                                               
         LA    R15,RNGTPORT                                                     
         SETMSG (R4),LH:SHPTLEN     Trace entry                                 
         LC    RAR,PSBTTYPE                                                     
         IF    (RAR,GT,L'PSBTMAX),'CLEAR RAR'                                   
         SLL   RAR,2                                                            
         L     RAR,TRCTBL(RAR)     Trace formatting routine                     
         BASR  RAR,RAR                                                          
*                                                                               
         AH    R4,SHPTLEN          Next trace table                             
         DECR  R15,SHPTNO          One less entry now                           
         END                                                                    
*                                                                               
         IF    ^SHPFSOME,'TSEG "Trace table is empty.",,CR'                     
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  PSB,SHPRTWA                                                      
         SPACE 2                                                                
*-                                                                              
*-       Front-end trace formatting routine vector table.                       
*-                                                                              
TRCTBL   DC    A(DEFTRC)                                                        
         DC    A(DEFTRC)           Cons                                         
         DC    A(ASYNTRC)          Asyn                                         
         DC    A(PDPTRC)           Pdp                                          
         DC    A(VTAMTRC)          Vtam                                         
         DC    A(VIRTTRC)          Virt                                         
         DC    A(NSCTRC)           Nsc                                          
         DC    A(ELFTRC)           Elf                                          
         DC    A(ALFTRC)           Alf                                          
         DC    A(ECUTRC)           ECU                                          
TRCMAX   EQU   (*-TRCTBL-4)/4,,C'N'                                             
*                                                                               
         DS    0S(TRCMAX-L'FSBTMAX,L'FSBTMAX-TRCMAX)                            
         DS    0S(TRCMAX-L'PSBTMAX,L'PSBTMAX-TRCMAX)                            
*                                                                               
         QLTORG                                                                 
         TITLE 'MOPEN Command'                                                  
*box                                                                            
*                                                                               
*  MOPEN -- MOPEN command.                                                      
*                                                                               
MOPNWA   RECORD BEGIN                                                           
MOPNRNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
MOPEN    XPROC MOPNWA                                                           
         IF    ^CPSFOPR,CVNVALID                                                
*                                                                               
         CLEAR MOPNWA                                                           
         LA    R0,RNGTFE*256                                                    
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,MOPNRNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVNEXT                                                           
*                                                                               
         CLEAR R0                  No range selection co-routine                
         LA    R1,OPENRTN          Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OPENRTN -- MOPEN command range co-routine.                                   
*                                                                               
*    On entry:                                                                  
*      R1 - FSB ptr                                                             
*                                                                               
OPENRTN  PROC  ,                                                                
         LR    R6,R1                                                            
         USING FSB,R6                                                           
*                                                                               
         IF    FSBFDET,'SETMSG "is detached--not opened."; B OPENERR'           
         IF    ^FSBFCLS,'SETMSG "is already open."; B OPENERR'                  
*                                                                               
         XPUSH ,,L'FDBT101,PTR=R1                                               
         WITH  (FDBT101,R1),BEGIN                                               
         CLEAR FDBT101                                                          
         MVC   FDBTFE,FSBID        Front-end id                                 
         MVI   FDBTDNO,FDBPSETF    Set front-end                                
         SET   FDBTFOPN            Open                                         
         TSEG  FDBT101                                                          
         END                                                                    
         XPOP  ,,L'FDBT101                                                      
         TCNTL CTLPSETD                                                         
         BZ    OPENX               Aok                                          
         IF    NEG,'VCALL MILERR; B OPENX'                                      
         SETMSG 'not opened.'                                                   
*                                                                               
OPENERR  XPUSH R0,R1                                                            
         TSEG  FSBNAME,,TB                                                      
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         SET   CPFABORT                                                         
OPENX    PEND                                                                   
         DROP  FSB                                                              
         TITLE 'MCLOSE Command'                                                 
*box                                                                            
*                                                                               
*  MCLOSE -- MCLOSE command.                                                    
*                                                                               
MCLSWA   RECORD BEGIN                                                           
MCLSRNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
MCLOSE   XPROC MCLSWA                                                           
         IF    ^CPSFOPR,CVNVALID                                                
*                                                                               
         CLEAR MCLSWA                                                           
         LA    R0,RNGTFE*256                                                    
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,MCLSRNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVNEXT                                                           
*                                                                               
         CLEAR R0                  No range selection co-routine                
         LA    R1,CLOSERTN         Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CLOSERTN -- MCLOSE command range co-routine.                                 
*                                                                               
*    On entry:                                                                  
*      R1 - FSB ptr                                                             
*                                                                               
CLOSERTN PROC  ,                                                                
         LR    R6,R1                                                            
         USING FSB,R6                                                           
*                                                                               
         LTH   R15,FSBSCUR         Current no. of users                         
         IF    POS,BEGIN           Double check with user...                    
         TNUM  (R15)                                                            
         TSEG  ' users still on '                                               
         TSEG  FSBNAME,,CR                                                      
         SETMSG 'Close'                                                         
         VCALL YESREQ              Get permission                               
         END                                                                    
*                                                                               
         XPUSH ,,L'FDBT101,PTR=R1                                               
         WITH  (FDBT101,R1),BEGIN                                               
         CLEAR FDBT101                                                          
         MVC   FDBTFE,FSBID        Front-end id                                 
         MVI   FDBTDNO,FDBPSETF    Set front-end                                
         SET   FDBTFCLS            Close                                        
         TSEG  FDBT101                                                          
         END                                                                    
         XPOP  ,,L'FDBT101                                                      
         TCNTL CTLPSETD                                                         
         BZ    CLOSEX              Aok                                          
         IF    NEG,'VCALL MILERR; B CLOSEX'                                     
         SETMSG 'not closed.'                                                   
*                                                                               
CLOSEERR XPUSH R0,R1                                                            
         TSEG  FSBNAME,,TB                                                      
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         SET   CPFABORT                                                         
CLOSEX   PEND                                                                   
         DROP  FSB                                                              
         TITLE 'START Command'                                                  
*box                                                                            
*                                                                               
*  START -- START command  (priv'd).                                            
*                                                                               
STRTWA   RECORD BEGIN                                                           
STRTRNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
START    XPROC STRTWA                                                           
         IF    ^CPSFOPR,CVNVALID   priv'd                                       
*                                                                               
         CLEAR STRTWA                                                           
         LA    R0,L'RNGFMULT                                                    
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,STRTRNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVNEXT                                                           
*                                                                               
         LA    R0,STARTSEL         Range selection co-routine                   
         LA    R1,STARTRTN         Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  STARTSEL -- START command range selection co-routine.                        
*                                                                               
*    On entry:                                                                  
*      R0 - rngtype                                                             
*      R1 - FSB/PSB ptr                                                         
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - use item                                                             
*      4 - skip item                                                            
*                                                                               
STARTSEL PROC  ,                                                                
         LR    R6,R1                                                            
         LA    R15,4               Assume skip                                  
*                                                                               
         IF    (R0,EQ,RNGTFE),BEGIN  front-end...                               
         WITH  (FSB,R6)                                                         
*temp    if    ^fsbfcls,startsx    already open, skip                           
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN               Port...                                      
         WITH  (PSB,R6)                                                         
*temp    if    ^psbfstop,startsx   already started, skip                        
         CLEAR R15                                                              
         END                                                                    
*                                                                               
STARTSX  PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  STARTRTN -- START command range co-routine.                                  
*                                                                               
*    On entry:                                                                  
*      R0 - rngtype                                                             
*      R1 - FSB/PSB ptr                                                         
*                                                                               
STARTRTN PROC  ,                                                                
         LR    R6,R1                                                            
*                                                                               
         IF    (R0,EQ,RNGTFE),BEGIN  front-end...                               
         WITH  (FSB,R6)                                                         
         IF    FSBFDET,'SETMSG "IS DETACHED--NOT STARTED"; B STARTFR'           
         XPUSH ,,L'FDBT101,PTR=R1                                               
         WITH  (FDBT101,R1),BEGIN                                               
         CLEAR FDBT101                                                          
         MVC   FDBTFE,FSBID        Front-end id                                 
         MVI   FDBTDNO,FDBPSETF    Set front-end                                
         SET   FDBTFSTR            Start                                        
         TSEG  FDBT101                                                          
         END                                                                    
         XPOP  ,,L'FDBT101                                                      
         TCNTL CTLPSETD                                                         
         BZ    STARTX                                                           
         IF    NEG,'VCALL MILERR; B STARTX'                                     
         SETMSG 'NOT STARTED'                                                   
STARTFR  XPUSH R0,R1                                                            
         SETMSG FSBNAME                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         SET   CPFABORT                                                         
         B     STARTX                                                           
         END                                                                    
*                                                                               
         WITH  (PSB,R6),BEGIN      Port...                                      
         IF  PSBFDUM,'SETMSG "IS A DUMMY PORT--NOT STARTED"; B STARTPR'         
         IF    PSBFDET,'SETMSG "IS DETACHED--NOT STARTED"; B STARTPR'           
         XPUSH ,,L'FDBT102,PTR=R1                                               
         WITH  (FDBT102,R1),BEGIN                                               
         CLEAR FDBT102                                                          
         MVC   FDBTFE,PSBFEID      Front-end id                                 
         MVI   FDBTDNO,FDBPSETP    Set port                                     
         MVC   FDBT102P,PSBPNO     Port number                                  
         SET   FDBTPSTR            Start                                        
         TSEG  FDBT102                                                          
         END                                                                    
         XPOP  ,,L'FDBT102                                                      
         TCNTL CTLPSETD                                                         
         BZ    STARTX                                                           
         IF    NEG,'VCALL MILERR; B STARTX'                                     
         SETMSG 'NOT STARTED'                                                   
STARTPR  XPUSH R0,R1                                                            
         SETMSG PSBFENM                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  '-'                                                              
         SETMSG PSBNAME                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         BNZ   CVABORT                                                          
         SET   CPFABORT                                                         
         B     STARTX                                                           
         END                                                                    
*                                                                               
STARTX   PEND                                                                   
         TITLE 'STOP Command'                                                   
*box                                                                            
*                                                                               
*  STOP -- STOP command  (priv'd).                                              
*                                                                               
STOPWA   RECORD BEGIN                                                           
STOPRNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
STOP     XPROC STOPWA                                                           
         IF    ^CPSFOPR,'VCALL CMDFIN'   Pass on to ORVYL ...                   
*                                                                               
         SCPUSH                                                                 
         SCAN  ,                                                                
         SCANCHK                                                                
         SCUPTOK R1                                                             
         IF    (R0,Z),BEGIN                                                     
         ABORT 'Use "STOP MILTEN" command to terminate Milten.'                 
         END                                                                    
         IF    ((R0,NZ),AND,(@R1,EQ,'MIL ')),'ACALL SHUTDOWN'                   
         IF    ((R0,NZ),AND,(@R1,EQ,'MILTEN ')),'ACALL SHUTDOWN'                
         SCPOP                                                                  
*                                                                               
         LA    R0,L'RNGFMULT                                                    
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,STOPRNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVNEXT                                                           
*                                                                               
         LA    R0,STOPSEL          Range selection co-routine                   
         LA    R1,STOPRTN          Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  "stop milten"...                                                             
SHUTDOWN PROC  ,                                                                
         TCNTL CTLPDIE             Down boy!                                    
         BNZ   CVMILERR                                                         
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  STOPSEL -- STOP command range selection co-routine.                          
*                                                                               
*    On entry:                                                                  
*      R0 - rngtype                                                             
*      R1 - FSB/PSB ptr                                                         
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - use item                                                             
*      4 - skip item                                                            
*                                                                               
STOPSEL  PROC  ,                                                                
         LR    R6,R1                                                            
         LA    R15,4               Assume skip                                  
*                                                                               
         IF    (R0,EQ,RNGTFE),BEGIN  front-end...                               
         WITH  (FSB,R6)                                                         
         IF    FSBFCLS,STOPSX      Already closed, skip                         
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN               Port...                                      
         WITH  (PSB,R6)                                                         
         IF    PSBFSTOP,STOPSX     Already stopped, skip                        
         CLEAR R15                                                              
         END                                                                    
*                                                                               
STOPSX   PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  STOPRTN -- STOP command range co-routine.                                    
*                                                                               
*    On entry:                                                                  
*      R0 - rngtype                                                             
*      R1 - FSB/PSB ptr                                                         
*                                                                               
STOPRTN  PROC  ,                                                                
         LR    R6,R1                                                            
*                                                                               
         IF    (R0,EQ,RNGTFE),BEGIN  front-end...                               
         WITH  (FSB,R6)                                                         
         XPUSH ,,L'FDBT101,PTR=R1                                               
         WITH  (FDBT101,R1),BEGIN                                               
         CLEAR FDBT101                                                          
         MVC   FDBTFE,FSBID        Front-end id                                 
         MVI   FDBTDNO,FDBPSETF    Set front-end                                
         SET   FDBTFSTP            Stop                                         
         TSEG  FDBT101                                                          
         END                                                                    
         XPOP  ,,L'FDBT101                                                      
         TCNTL CTLPSETD                                                         
         BZ    STOPX                                                            
         IF    NEG,'VCALL MILERR; B STOPX'                                      
         SETMSG FSBNAME                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         TSEG  'not stopped.',,WR                                               
         BNZ   CVABORT                                                          
         SET   CPFABORT                                                         
         B     STOPX                                                            
         END                                                                    
*                                                                               
         WITH  (PSB,R6),BEGIN      Port...                                      
         XPUSH ,,L'FDBT102,PTR=R1                                               
         WITH  (FDBT102,R1),BEGIN                                               
         CLEAR FDBT102                                                          
         MVC   FDBTFE,PSBFEID      Front-end id                                 
         MVI   FDBTDNO,FDBPSETP    Set port                                     
         MVC   FDBT102P,PSBPNO     Port number                                  
         SET   FDBTPSTP            Stop                                         
         TSEG  FDBT102                                                          
         END                                                                    
         XPOP  ,,L'FDBT102                                                      
         TCNTL CTLPSETD                                                         
         BZ    STOPX                                                            
         IF    NEG,'VCALL MILERR; B STOPX'                                      
         SETMSG PSBFENM                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         TSEG  '-'                                                              
         SETMSG PSBNAME                                                         
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0),B                                                      
         TSEG  'not stopped.',,WR                                               
         SET   CPFABORT                                                         
         B     STOPX                                                            
         END                                                                    
*                                                                               
STOPX    PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'SET OPER Command'                                               
*box                                                                            
*                                                                               
*  SETOPER -- SET OPER command  (priv'd).                                       
*                                                                               
SETOWA   RECORD BEGIN                                                           
SETORNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
SETOPER  XPROC SETOWA                                                           
         IF    (CPSFOPR+CPSFSPR,Z),CVNVALID                                     
*                                                                               
         CLEAR SETOWA                                                           
         LA    R0,RNGTSES*256+L'RNGFDOUR                                        
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,SETORNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVABORT                                                          
*                                                                               
         CLEAR R0                  No selection routine                         
         LA    R1,OPERRTN          Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         DROP  BR                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OPERRTN -- SET OPER command range co-routine.                                
*                                                                               
*    On entry:                                                                  
*      R1 - SIB ptr                                                             
*                                                                               
OPERRTN  PROC  ,                                                                
         LR    R6,R1                                                            
         USING SIB,R6                                                           
*                                                                               
         LOOP  BEGIN                                                            
         SET   SIBSFOP             Operator                                     
         IF    (CPCMNM,EQ,'N'),'CLEAR SIBSFOP'                                  
         INCR  R15,SIBUC           Kick update count                            
         TSEG  SIBID                                                            
         TSEG  SIBNO                                                            
         TSEG  SIBUC                                                            
         TSEG  SIBSESS                                                          
         TCNTL CTLPSETO            Set session options                          
         IF    Z,EXIT              Ok                                           
         IF    (R15,NE,4),CVMILERR                                              
         MVC   SIB,@R1             Must re-update this sib                      
         END                                                                    
         PEND                                                                   
         DROP  SIB                                                              
         TITLE 'BROADCAST Command'                                              
BROADWA  RECORD BEGIN                                                           
BRRNG    DS    XL(L'RNGCB)                                                      
BRSG     SEGCB                                                                  
*                                                                               
BRSGBUF  DS    CL(&LINESZ)                                                      
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  BROADCAS -- BROADCAST command  (priv'd).                                     
*                                                                               
BROADCAS XPROC BROADWA                                                          
         VCALL ORVCPASS            (allow SPIRES to intercept)                  
*                                                                               
         LA    R6,BROADWA                                                       
         ST    R6,CPCWA            Broadwa ptr                                  
         USING BROADWA,R6                                                       
         CLEAR BROADWA                                                          
*                                                                               
         SEGINIT BRSGBUF,,BRSG                                                  
*                                                                               
         IF    ^CPSFOPR,CVNVALID   priv'd                                       
*                                                                               
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,Z),'ERROR "MISSING LINE-RANGE FOR BROADCAST"'                
         XPUSH R0,R1                                                            
*                                                                               
         SCINFO                                                                 
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    ((CPCMNM,EQ,'SEN'),AND,(R0,POS)),BEGIN    ***temp***             
         TSEG  (R1),(R0)                                 ***temp***             
         ABORT ': INVALID'                               ***temp***             
         END   ,                                         ***temp***             
*                                                        ***temp***             
         XPUSH R0,R1               Save 'em again                               
         VCALL LOCALTOD            Get current time/date                        
         LR    R2,R0                                                            
         XPOP  R0,R1               Get 'em back                                 
         IF    (R0,^POS),BEGIN     Use general message...                       
         IF    (CVMSG,Z),'ABORT "NO BROADCAST MESSAGE"'                         
         SETMSG CVMSG                                                           
         L     R2,CVMSGCK          Time/date when msg was set                   
         END                                                                    
*                                                                               
         XPUSH R0,R1                                                            
         LR    R0,R2                                                            
         CLEAR R1                                                               
         CLEAR R2,R3                                                            
         XPUSH ,,30,PTR=R15                                                     
         VCALL FMTRCLCK            "hh:mm" or "hh:mm mm/dd/yyyy"                
         SEG   (R1),(R0)                                                        
         SEG   CVBLANKS,2                                                       
         XPOP  ,,30                                                             
         XPOP  R0,R1                                                            
         SEGT  (R1),(R0)                                                        
*                                                                               
         XPOP  R0,R1                                                            
         SCINIT (R1),(R0)          Line-range                                   
         LA    R0,RNGTSES*256+L'RNGFMULT  range options                         
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,BRRNG                                                        
         VCALL GETTRNG                                                          
         BZ    CVABORT                                                          
*                                                                               
         LA    R0,BROADSEL         Selection co-routine                         
         LA    R1,BROADRTN         Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BROADSEL -- BROADCAST command range selection co-routine.                    
*                                                                               
*    On entry:                                                                  
*      R1 - SIB ptr                                                             
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - use item                                                             
*      4 - skip item                                                            
*                                                                               
BROADSEL PROC                                                                   
         LR    R6,R1                                                            
         USING SIB,R6                                                           
*                                                                               
         BEGIN                                                                  
         LA    R15,4               Assume skip                                  
         IF    SIBFNMSG,EXIT       Nomsg blocks broadcast                       
         IF    SIBSFPS,EXIT        Skip psuedo-sessions                         
         CLEAR R15                                                              
         END                                                                    
         PEND                                                                   
         DROP  SIB                                                              
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BROADRTN -- BROADCAST command range co-routine.                              
*                                                                               
*    On entry:                                                                  
*      R1 - SIB ptr                                                             
*                                                                               
BROADRTN PROC  ,                                                                
         L     R6,CPCWA                                                         
         USING BROADWA,R6                                                       
         LR    R5,R1                                                            
         USING SIB,R5                                                           
*                                                                               
         IF    ^SIBTCONS,'TSEG X"2F15"'                                         
         SETMSG L:BRSGLOC,L:BRSGLENF                                            
         TSEG  (R1),(R0)                                                        
         IF    ^SIBTCONS,'TSEG X"15"'                                           
         L     R1,SIBID            Session id                                   
         VCALL TASWRITE            Async write                                  
         IF    NZ,BEGIN                                                         
         TSEG  'Broadcast skipped for account',,B                               
         TSEG  SIBGRP                                                           
         TSEG  '.'                                                              
         TSEG  SIBUSER                                                          
         L2    R2,SIBANO                                                        
         IF    (R2,GT,1),BEGIN                                                  
         TSEG  '#'                                                              
         BTD   CPDOUB,0,(R2)                                                    
         TSEG  (R1),(R0)                                                        
         END                                                                    
         TSEG  ' -- msg limit exceeded',,WR                                     
         BNZ   CVABORT                                                          
         END                                                                    
         PEND                                                                   
         DROP  SIB,BROADWA                                                      
         TITLE 'KILL Command'                                                   
*box                                                                            
*                                                                               
*  KILL -- KILL command.                                                        
*                                                                               
KILLWA   RECORD BEGIN                                                           
KILLRNG  DS    XL(L'RNGCB)                                                      
         END                                                                    
*                                                                               
*                                                                               
KILL     XPROC KILLWA                                                           
*                                                                               
         CLEAR KILLWA                                                           
         LA    R0,RNGTSES*256+L'RNGFMULT                                        
         SETSCKWS GETRPRT          No special prt                               
         LA    R15,KILLRNG                                                      
         VCALL GETTRNG                                                          
         BZ    CVABORT                                                          
*                                                                               
         LA    R0,KILLSEL          Range selection co-routine                   
         LA    R1,KILLRTN          Range co-routine                             
         VCALL TRANGE                                                           
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  KILLSEL -- KILL command range selection co-routine.                          
*                                                                               
*    On entry:                                                                  
*      R1 - SIB ptr                                                             
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - use item                                                             
*      4 - skip item                                                            
*                                                                               
KSWA     RECORD BEGIN                                                           
KSSPTR   DS    A                   Saved R1 on entry                            
KSSIB    DS    XL(L'SIB)           SIB                                          
KSKWR    DS    XL(L'KWR)           KWR work area                                
         END                                                                    
*-                                                                              
KILLSEL  PROC  KSWA                                                             
         CLEAR KSWA                                                             
*                                                                               
         ST    R1,KSSPTR           Save ptr                                     
         MVC   KSSIB,@R1           Save a copy of the SIB                       
         LA    R6,KSSIB                                                         
         WITH  (SIB,R6)                                                         
*                                                                               
         BEGIN                                                                  
         LA    R15,4               Assume skip                                  
         IF    SIBTVIRT,EXIT       don't kill virtuals                          
         IF    SIBTCONS,EXIT       ..or consoles                                
         IF    (SIBID,EQ,CPSID),EXIT  or ourselves                              
         CLEAR R15                                                              
         END                                                                    
*-                                                                              
*-       Check to make sure user is authorized to kill                          
*-         this session.                                                        
*-                                                                              
         IF    (R15,Z),BEGIN       Check authorization...                       
         IF    CPSFOPR,EXIT        Operator can always do it                    
         TSEG  'This session will be logged off:',,CR                           
         LA    R1,SIB                                                           
         VCALL LINEDISP            Display SHOW LINE info                       
         TCR                                                                    
*                                                                               
         IF    (SIBACCT,EQ,CPSACCT),BEGIN  Same as logged on...                 
         SETMSG 'OK'                                                            
         VCALL YESREQ              We need the OK                               
         END                                                                    
*                                                                               
         ELSE  BEGIN               Not yours...                                 
         TSEG  'Account '                                                       
         TSEG  SIBGRP                                                           
         TSEG  '.'                                                              
         TSEG  SIBUSER                                                          
         TSEG  ' is not yours.',,CR                                             
*                                                                               
         LA    R1,KSKWR            KWR area                                     
         WITH  (KWR,R1),'MVC KWRACCT,SIBACCT'  Their account                    
         CLEAR R0                  0 = normal mode                              
         VCALL CHKKW               Make them supply password                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         END                                                                    
*-                                                                              
*-       Restore SIB (because the caller passed it to us in the                 
*-         terminal read buffer).                                               
*-                                                                              
         L     R2,KSSPTR           Saved SIB ptr                                
         MVC   @R2(L'SIB),SIB      Restore SIB (in CPSEG buffer)                
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  KILLRTN -- KILL command range co-routine.                                    
*                                                                               
*    On entry:                                                                  
*      R1 - SIB ptr                                                             
*                                                                               
KRWA     RECORD BEGIN                                                           
KRSIB    DS    XL(L'SIB)           Working SIB                                  
         END                                                                    
*-                                                                              
KILLRTN  PROC  KRWA                                                             
         MVC   KRSIB,@R1           Make a copy of the SIB                       
         LA    R6,KRSIB                                                         
         WITH  (SIB,R6)                                                         
*                                                                               
         IF    ^CPSTCONS,BEGIN     Only if not a console...                     
*-                                                                              
*-       Write log message to the console.                                      
*-                                                                              
         SEGDEF L:CVWTOSGP                                                      
         SEG   'Line '                                                          
         SEGDC LH:SIBNO                                                         
         SEG   ' '                                                              
         SEG   SIBGRP                                                           
         SEG   '.'                                                              
         SEG   SIBUSER                                                          
         SEG   ' killed by line '                                               
         SEGDC LH:CPSNO                                                         
         SEG   ' '                                                              
         SEG   CPSGRP                                                           
         SEG   '.'                                                              
         SEG   CPSUSER                                                          
         SET   CVWTOFNOPFX         No special WTO prefix                        
         SET   CVWTOFUPFX+CVWTOFWTL  WTO logging flags                          
         XWTO  ,                   Do WTO                                       
         CLEAR CVWTOFUPFX+CVWTOFWTL  Reset                                      
         SEGDEF DUMMY              (Neatness)                                   
*-                                                                              
*-       Notify the session of impending death.                                 
*-                                                                              
         TCR                                                                    
*                                                                               
         VCALL LOCALTOD            Current time/date                            
         XPUSH ,,16,PTR=R15                                                     
         VCALL FMTTIME             Format time                                  
         TSEG  (R1),5,B            Hh:mm                                        
         XPOP  ,,16                                                             
*                                                                               
         TSEG  'You have been logged off by line '                              
*                                                                               
         TNUM  LH:CPSNO                                                         
*                                                                               
         TSEG  ' '                                                              
         TSEG  CPSGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPSUSER                                                          
         IF    (CPSNAME,NZ),'TSEG " "; TSEG CPSNAME,,T'                         
         TCR                                                                    
         TCR                                                                    
*                                                                               
         L     R1,SIBID            Get session ID                               
         VCALL TASWRITE            Asynchronous write                           
*                                                                               
         LA    R15,200             2 second wait                                
         VCALL WAITER              Wait                                         
         END                                                                    
*-                                                                              
*-       Kill the session.                                                      
*-                                                                              
         TSEG  SIBID                                                            
         TSEG  SIBNO                                                            
         TCNTL CTLPKILL            Kill this line                               
         BNZ   CVMILERR                                                         
*-                                                                              
*-       Write out confirmation message that session will                       
*-         be logged off.                                                       
*-                                                                              
         IF    ^CPSTCONS,BEGIN     Don't do message if console...               
         TSEG  'Line '                                                          
         TNUM  LH:SIBNO                                                         
         TSEG  ' '                                                              
         TSEG  SIBGRP                                                           
         TSEG  '.'                                                              
         TSEG  SIBUSER                                                          
         TSEG  ' will be logged off.',,WR                                       
         BNZ   CVABORT                                                          
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Subroutines'                                                    
*box                                                                            
*                                                                               
*  DEFTRC -- Routine to format trace entry.                                     
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc, len                                                   
*                                                                               
DEFTRC   PROC                                                                   
         LA    R6,@R1+8                                                         
         LR    R5,R0                                                            
         SH    R5,=Y(8)                                                         
*                                                                               
         IF    (R15,EQ,RNGTFE),BEGIN                                            
         TSEG  (R6),8,B                                                         
         LA    R6,@R6+8                                                         
         SH    R5,=Y(8)                                                         
         LA    R15,21                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,12              Indent                                       
         END                                                                    
*                                                                               
         SETMSG (R6),(R5)                                                       
         VCALL HEXFMT                                                           
         BNZ   CVABORT                                                          
*                                                                               
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NSCTRC -- Routine to format NSC trace entry.                                 
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc, len                                                   
*                                                                               
NSCTRC   PROC                                                                   
         ABORT 'NSC trace formatting not available.'                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ECUTRC -- Routine to format ECU trace entry.                                 
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc, len                                                   
*                                                                               
ECUTRC   PROC                                                                   
         ABORT 'ECU trace formatting not available.'                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ASYNTRC -- Routine to format async trace entry.                              
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc, len                                                   
*                                                                               
ASYNTRC  PROC  ,                                                                
         ABORT 'ASYNC trace formatting not available.'                          
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PDPTRC -- Routine to format pdp trace entry.                                 
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc,len                                                    
*                                                                               
PDPTRC   PROC  ,                                                                
         ABORT 'PDP trace formatting not available.'                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ELFTRC -- Routine to format telnet trace entry.                              
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc,len                                                    
*                                                                               
ELFTRC   PROC                                                                   
         LR    R6,R1                                                            
         WITH  (TTRC,R6)                                                        
         LR    R5,R0                                                            
*-                                                                              
*-       Port trace entry.                                                      
*-                                                                              
         IF    (R15,EQ,RNGTPORT),BEGIN  Port trace...                           
         SETMSG 'FROMAMY'                                                       
         IF    (TTRCPTYP,EQ,'T'),'SETMSG "TOAMY  "'                             
         TSEG  (R1),(R0)                                                        
*                                                                               
         TSEG  CVBLANKS,2                                                       
         IF    (TTRCPCMD,EQ,TMILWRIT),'TSEG "Write:"'                           
         ELSEIF (TTRCPCMD,EQ,TMILREAD),'TSEG "Read: "'                          
         ELSE  BEGIN                                                            
         TSEG  'Cmd='                                                           
         THEX  LC:TTRCPCMD                                                      
         END                                                                    
*                                                                               
         TSEG  ' CPL='                                                          
         THEX  L2:TTRCPCPL,4                                                    
         TSEG  ' Length='                                                       
         LH    R2,TTRCPLEN                                                      
         SH    R2,=AL2(L'PDBHDR)                                                
         TNUM  (R2)                                                             
         B     TTRCEXIT                                                         
         END                                                                    
*-                                                                              
*-       Front-end trace entry.                                                 
*-                                                                              
         SETMSG TTRCPNAM                                                        
         IF    (TTRCPNAM,Z),'LA R1,CVBLANKS'                                    
         TSEG  (R1),(R0),B                                                      
         TSEG  TTRCNAME                                                         
         TSEG  CVBLANKS,2                                                       
*-                                                                              
*-       TERM trace entries.                                                    
*-                                                                              
         IF    (TTRCNAME,EQ,'TERM'),BEGIN                                       
         IF    (TTRCSACC,Z),'TSEG "------ "'                                    
         ELSE  BEGIN                                                            
         TSEG  TTRCSACC+3,2                                                     
         TSEG  '.'                                                              
         TSEG  TTRCSACC,3,B                                                     
         END                                                                    
         IF    (TTRCCMD,EQ,TMILWRIT),'TSEG "Write: "'                           
         ELSEIF (TTRCCMD,EQ,TMILREAD),'TSEG "Read:  "'                          
         ELSE  'TSEG "Cmd="; THEX LC:TTRCCMD,2; TSEG " "'                       
         LH    R2,TTRCTLEN                                                      
         SH    R2,=Y(L'PDBHDR)                                                  
         IF    ^NEG,BEGIN                                                       
         TSEG  'Length='                                                        
         TNUM  (R2)                                                             
         IF    ^CPSFSPR,EXIT                                                    
         IF    (R2,Z),EXIT                                                      
         TSEG  ', Text='                                                        
         CEIL  R2,L'TTRCTEXT                                                    
         SETMSG TTRCTEXT,(R2)                                                   
         VCALL MIXFMT                                                           
         END                                                                    
         B     TTRCEXIT                                                         
         END                                                                    
*-                                                                              
*-       IO trace entries.                                                      
*-                                                                              
         IF    (TTRCNAME,EQ,'IO'),BEGIN                                         
         TSEG  'Length='                                                        
         LH    R15,TTRCILEN        Total length                                 
         SH    R15,=AL2(16+4)      Deduct header and id                         
         TNUM  (R15)                                                            
         TSEG  ', '                                                             
         TSEG  TTRCIHDR,,T         Header part ("XXX:")                         
         TSEG  '  ID='                                                          
         THEX  L4:TTRCIREM,8                                                    
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,23                                                      
         LA    R1,TTRCIREM+4                                                    
         LH    R0,TTRCILEN                                                      
         SH    R0,=AL2(L'TTRCIHDR+4)                                            
         IF    POS,BEGIN           Display data...                              
         IF    ^CPSFSPR,EXIT       Not sys priv'd, no user data                 
         LA    R15,L'TTRCIREM-4                                                 
         CEIL  R0,R15              Not too much now                             
         LA    R15,23              Indent                                       
         VCALL HEXFMT              Display in hex                               
         BNZ   CVABORT                                                          
         END                                                                    
         B     TTRCEXIT                                                         
         END                                                                    
*-                                                                              
*-       MSG trace entries.                                                     
*-                                                                              
         IF    (TTRCNAME,EQ,'MSG'),BEGIN                                        
         TSEG  TTRCMSG,,T                                                       
         B     TTRCEXIT                                                         
         END                                                                    
*-                                                                              
*-       Any other trace entry.                                                 
*-                                                                              
         SETMSG TTRCDATA,(R5)                                                   
         LA    R15,8               Indent                                       
         VCALL HEXFMT                                                           
         BNZ   CVABORT                                                          
*                                                                               
TTRCEXIT TWRITE                                                                 
         BNZ   CVABORT                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ALFTRC -- Routine to format ALF trace entry.                                 
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc,len                                                    
*                                                                               
ALFTRC   PROC                                                                   
         ABORT 'ALF trace formatting not available.'                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTTRC -- Routine to format virtual trace entry.                            
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc,len                                                    
*                                                                               
VIRTTRC  PROC  ,                                                                
         LR    R6,R1                                                            
         USING VTRC,R6                                                          
         LR    R5,R0                                                            
*                                                                               
         IF    (R15,EQ,RNGTPORT),BEGIN  port trace...                           
         SETMSG 'FROMVIRT'                                                      
         IF    (VTRCPTYP,EQ,'T'),'SETMSG "TOVIRT  "'                            
         TSEG  (R1),(R0)                                                        
         TSEG  CVBLANKS,2                                                       
         SETMSG VTRCPRQS                                                        
         LCALL VIRTHDR                                                          
         B     VTRCEXIT                                                         
         END                                                                    
         EJECT                                                                  
         BTD   CPDOUB,4,LH:VTRCPNO                                              
         TSEG  (R1),(R0),B                                                      
         TSEG  VTRCNAME                                                         
         TSEG  CVBLANKS,2                                                       
*                                                                               
         IF    (VTRCNAME,EQ,'VIRT'),BEGIN                                       
         IF    (VTRCSACC,Z),'TSEG "------",,B'                                  
         ELSE  BEGIN                                                            
         TSEG  VTRCSACC+3,2                                                     
         TSEG  '.'                                                              
         TSEG  VTRCSACC,3,B                                                     
         END                                                                    
         IF    (VTRCCMD,EQ,TMILWRIT),'TSEG "Write:",,B'                         
         ELSEIF (VTRCCMD,EQ,TMILREAD),'TSEG "Read: ",,B'                        
         ELSE  BEGIN                                                            
         TSEG  'Cmd='                                                           
         BTX   CPDOUB,2,LC:VTRCCMD                                              
         TSEG  (R1),(R0),B                                                      
         END                                                                    
         LH    R2,VTRCTLEN                                                      
         SH    R2,=Y(L'PDBHDR)                                                  
         IF    ^NEG,BEGIN                                                       
         TSEG  'Length='                                                        
         BTD   CPDOUB,0,(R2)                                                    
         TSEG  (R1),(R0)                                                        
         IF    ^CPSFSPR,EXIT                                                    
         IF    (R2,Z),EXIT                                                      
         TSEG  ', Text='                                                        
         CEIL  R2,L'VTRCTEXT                                                    
         SETMSG VTRCTEXT,(R2)                                                   
         VCALL CHARFMT                                                          
         END                                                                    
         B     VTRCEXIT                                                         
         END                                                                    
         EJECT                                                                  
         IF    (VTRCNAME,EQ,'RQST'),BEGIN                                       
         SETMSG VTRCRQST                                                        
         LCALL VIRTHDR                                                          
         B     VTRCEXIT                                                         
         END                                                                    
         EJECT                                                                  
         SETMSG VTRCDATA,(R5)                                                   
         LA    R15,26              Indent                                       
         VCALL HEXFMT                                                           
         BNZ   CVABORT                                                          
*                                                                               
VTRCEXIT TWRITE                                                                 
         BNZ   CVABORT                                                          
         PEND                                                                   
         EJECT                                                                  
* virthdr -- local routine.  on entry: r1,r0 - virt hdr loc, len                
VIRTHDR  PROC  ,                                                                
         LR    R3,R1                                                            
         LR    R2,R0                                                            
         TSEG  'Rqst='                                                          
         SETMSG (R3),(R2)                                                       
         CEIL  R0,L'RQSTHDR                                                     
         CLEAR R15                                                              
         VCALL HEXFMT                                                           
*                                                                               
         WITH  (RQST,R3),'LA R1,RQSTTEXT; LH R0,RQSTLEN'                        
         CEIL  R0,R2               Only as much as we have                      
         SH    R0,=Y(L'RQSTHDR)                                                 
         IF    ^CPSFSPR,'CLEAR R0'                                              
         IF    (R0,POS),BEGIN                                                   
         XPUSH R0,R1,*                                                          
         TSEG  CVBLANKS,27                                                      
         TSEG  'TEXT='                                                          
         XPOP  R0,R1,*                                                          
         VCALL CHARFMT                                                          
         END                                                                    
         PEND                                                                   
*                                                                               
         DROP  VTRC                                                             
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VTAMTRC -- Routine to format vtam trace entry.                               
*                                                                               
*    On entry:                                                                  
*      R15 - rngtype                                                            
*      R1,R0 - trace loc, len                                                   
*                                                                               
VTAMTRC  PROC  ,                                                                
         LR    R6,R1                         r6 := source                       
         LR    R5,R0                         r5 := #remaining                   
         SPACE                                                                  
         IF    (R15,EQ,RNGTPORT),BEGIN       if port trace                      
         WITH  (VPTRACE,R6)                                                     
         IF    (R5,NE,=A(L'VPTRACE)),EXIT    exit if mismatch                   
         TSEG  VPTNAME                                                          
         TSEG  ' REQ='                                                          
         BTX   CPDOUB,2,LC:VPTREQ                                               
         TSEG  (R1),(R0)                                                        
         TSEG  ' SRTYP='                                                        
         BTX   CPDOUB,2,LC:VPTSRTYP                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' RTNCD='                                                        
         BTX   CPDOUB,2,LC:VPTRTNCD                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' FDBK2='                                                        
         BTX   CPDOUB,2,LC:VPTFDBK2                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' RH3='                                                          
         BTX   CPDOUB,2,LC:VPTRH3                                               
         TSEG  (R1),(R0)                                                        
         TSEG  ' CHN='                                                          
         BTX   CPDOUB,2,LC:VPTCHN                                               
         TSEG  (R1),(R0)                                                        
         TSEG  ' SNF='                                                          
         BTX   CPDOUB,4,L2:VPTSNF                                               
         TSEG  (R1),(R0)                                                        
         TSEG  ' STATE='                                                        
         BTX   CPDOUB,2,LC:VPTSTATE                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' FLAGS='                                                        
         BTX   CPDOUB,6,L3:VPTFLGS                                              
         TSEG  (R1),(R0)                                                        
         IF    (VPTNAME+1,EQ,C'SIGNAL'),BEGIN  if signal                        
         TSEG  ' SIGDATA='                                                      
         BTX   CPDOUB,8,VPTSIGNL                                                
         TSEG  (R1),(R0)                                                        
         B     VTTREXIT                                                         
         END   ,                               end signal                       
         SPACE                                                                  
         IF    ('LT R3,VPTSENS',Z),VTTREXIT                                     
         TSEG  ' SENSE='                     if sense present                   
         BTX   CPDOUB,8,(R3)                                                    
         TSEG  (R1),(R0)                                                        
         B     VTTREXIT                                                         
         SPACE                                                                  
         END   ,                             end port trace                     
         SPACE 2                                                                
         IF    (R15,EQ,RNGTFE),BEGIN         if fe trace                        
         WITH  (VFTRACE,R6)                                                     
         IF    (R5,NE,=A(L'VPTRACE)),EXIT    exit if mismatch                   
         IF    (VFTTYPE,GT,X'0C'),VTNKNOWN                                      
         TSEG  VFTNAME                                                          
         LC    R15,VFTTYPE                                                      
         L     R15,VTAMTABL(R15)                                                
         BR    R15                                                              
         SPACE                                                                  
VTAMTABL DC    A(VTNKNOWN)                                                      
         DC    A(VFOPCLS)                                                       
         DC    A(VFLTL)                                                         
         DC    A(VFLMODE)                                                       
         SPACE 4                                                                
VFOPCLS  LABEL                     OPEN acb | close acb                         
         TSEG  ' OFLGS='                                                        
         BTX   CPDOUB,2,LC:VFTOFLGS                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' ERFLG='                                                        
         BTX   CPDOUB,2,LC:VFTERFLG                                             
         TSEG  (R1),(R0)                                                        
         B     VTTREXIT                                                         
         SPACE                                                                  
VFLTL    LABEL                     LOSTERM | tpend | logon | err                
         TSEG  ' '                                                              
         IF    (VFTPORTN,Z),'TSEG "--------"'                                   
         ELSE  'TSEG VFTPORTN'                                                  
         TSEG  ' CODE='                                                         
         BTX   CPDOUB,2,LC:VFTCODE                                              
         TSEG  (R1),(R0)                                                        
         B     VTTREXIT                                                         
         SPACE                                                                  
VFLMODE  LABEL                     LOGMODE                                      
         TSEG  ' RTNCD='                                                        
         BTX   CPDOUB,2,LC:VFTRTNCD                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' FDBK2='                                                        
         BTX   CPDOUB,2,LC:VFTFDBK2                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' OPT11='                                                        
         BTD   CPDOUB,2,LC:VFTOPT11                                             
         TSEG  (R1),(R0)                                                        
         IF    ('TM VFTOPT11,X"20"',ON),'TSEG " STOP"'                          
         IF    ('TM VFTOPT11,X"40"',ON),'TSEG " START"'                         
         B     VTTREXIT                                                         
         SPACE                                                                  
         END   ,                             end fe trace                       
         SPACE 2                                                                
VTNKNOWN LA    R15,12                        not known                          
         LA    R1,@R6+8                                                         
         LR    R0,R5                                                            
         S     R0,=A(8)                                                         
         VCALL HEXFMT                                                           
         BNZ   CVABORT                                                          
         SPACE                                                                  
VTTREXIT TWRITE                                                                 
         BNZ   CVABORT                                                          
         PEND                                                                   
         SPACE 3                                                                
*                                                                               
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
*                                                                               
         END   .                                                                
