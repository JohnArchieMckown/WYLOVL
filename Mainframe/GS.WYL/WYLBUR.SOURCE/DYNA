DYNA     TITLE 'DYNAMIC ALLOCATION INTERFACE ROUTINE'                           
         HIBAL ,                                                                
WYLDYNA  CSECT                                                                  
DYNA     IDENT 5169                13:54:19 06/18/85 (JDN)                      
         SYSDEFN ,                                                              
         EJECT ,                                                                
*-                                                                              
*-       REGISTER USAGE                                                         
*-                                                                              
         REGS  ,,,,DYNWAR,PARMLISTR,IOWAR,BR,WAR,,,CPR,CVR,,RAR                 
         EJECT ,                                                                
         MACRO                                                                  
&L       SETTP &D                                                               
         GBLA  &OFFSET                                                          
         ST    R1,DYNTP+&OFFSET                                                 
&OFFSET  SETA  &OFFSET+4                                                        
         MEND  ,                                                                
*                                                                               
         PUSH  DSECTS                                                           
*                                                                               
         COPY  CONTROL             CP, CV                                       
*                                                                               
         COPY  RTNCODES                                                         
         TITLE 'DSECTS'                                                         
TIOT     DSECT                                                                  
         IEFTIOT1 ,                                                             
         EJECT ,                                                                
         IEFZB4D0                  DYNALLOC PARM MAP                            
         EJECT ,                                                                
         IEFZB4D2                  DYNALLOC KEY MAP                             
         EJECT ,                                                                
IOWA     RECORD 'IOWA'                                                          
         EJECT ,                                                                
         POP   DSECTS                                                           
         TITLE 'DYNAMIC ALLOCATION INTERFACE'                                   
WYLDYNA  CSECT                                                                  
DYNWA    RECORD BEGIN                                                           
DYNP     DS    F                   RB POINTER                                   
DYNRB    DS    XL20                RB                                           
DYNTP    DS    5F                  TEXT UNIT POINTERS                           
DYNTDDN  DS    3XL2,CL8            TEXT UNIT FOR DDNAME                         
DYNTUNAL DS    2XL2                TEXT UNIT FOR UNALLOC FORCE                  
DYNTVOL  DS    3XL2,CL6            TEXT UNIT FOR VOLSER                         
DYNTUNIT DS    3XL2,CL8            TEXT UNIT FOR UNIT                           
DYNTSTAT DS    3XL2,XL1            TEXT UNIT FOR STATUS                         
DYNPEND  DS    0X                                                               
         END   ,                                                                
DYNALLOC XENTER R2,R8,*                                                         
         WITH  IOWA,'LA DYNWAR,DBUF'   --> IOWA BUFFER                          
         LR    PARMLISTR,R1        --> PARMLIST                                 
         WITH  DYNWA,PARMLIST,BEGIN                                             
         CLEAR (DYNP,DYNPEND-DYNP)                                              
         LA    R1,DYNRB                                                         
         WITH  (S99RB,R1),BEGIN                                                 
         MVI   S99RBLN,L'DYNRB     SET RB LENGTH                                
         MVI   S99VERB,S99VRBAL    ASSUME ALLOCATE                              
         IF    (PARREQCD,EQ,PARRUNAL),'MVI S99VERB,S99VRBUN'                    
*  IF THIS IS NOT A MOUNT REQUEST, SUPPRESS VOLMOUNT                            
         IF    (PARREQCD,NE,PARRMNT),'MVI S99FLG11,S99NOMNT'                    
         LA    R0,DYNTP            R0-->TEXT UNIT POINTER LIST                  
         ST    R0,S99TXTPP         SET INTO RB                                  
*  SUPPRESS SYSDSN ENQ ON TEMPORARY DSN                                         
         OI    S99FLG21,S99NORES   NO RESERVE                                   
*  ALLOW MOUNTING OF VOLUMES.  NOTE THAT S99NOMNT SET ABOVE WILL                
*  OVERRIDE.                                                                    
         OI    S99FLG21,S99MOUNT   MOUNT PERMITTED                              
         END   ,                                                                
         SPACE 1                                                                
*  SET UP DDNAME TEXT UNIT                                                      
         LA    R1,DYNTDDN          R1-->DDNAME TEXT UNIT                        
         WITH  (S99TUNIT,R1),BEGIN                                              
         MVC   S99TUKEY,=AL2(DALDDNAM)  DDNAME KEY                              
         MVC   S99TUNUM,=XL2'0001' NUMBER OF ENTRIES                            
         MVC   S99TULNG,=XL2'0008' LENGTH OF DDNAME                             
         MVC   S99TUPAR(8),PARDDNAM  DDNAME                                     
         SETTP ,                   SET TU POINTER                               
         SPACE 1                                                                
*  SET UP TEXT UNIT FOR UNALLOC OVERRIDE OF PERM ALLOC                          
         IF    (PARREQCD,EQ,PARRUNAL),BEGIN  ONLY FOR UNALLOC                   
         LA    R1,DYNTUNAL         R1-->OVERRIDE TEXT UNIT                      
         MVC   S99TUKEY,=AL2(DUNUNALC)   OVERRIDE KEY                           
         SETTP ,                   SET TU POINTER                               
         END   ,                                                                
         SPACE 1                                                                
         IF    (PARREQCD,NE,PARRUNAL),BEGIN  THESE ONLY FOR ALLOC               
*  SET UP VOLSER TEXT UNIT                                                      
         LA    R1,DYNTVOL          R1-->VOLSER TEXT UNIT                        
         MVC   S99TUKEY,=AL2(DALVLSER)  VOLSER KEY                              
         MVC   S99TUNUM,=XL2'0001' NUMBER OF ENTRIES                            
         MVC   S99TULNG,=XL2'0006' LENGTH OF VOLSER                             
         MVC   S99TUPAR(6),PARDDVOL  VOLSER                                     
         SETTP ,                   SET TU POINTER                               
         SPACE 1                                                                
*  SET UP UNIT TEXT UNIT                                                        
         LA    R1,DYNTUNIT         R1-->UNIT TEXT UNIT                          
         MVC   S99TUKEY,=AL2(DALUNIT)   UNIT KEY                                
         MVC   S99TUNUM,=XL2'0001' NUMBER OF ENTRIES                            
         MVC   S99TULNG,=XL2'0008' Unit length                                  
         MVC   S99TUPAR(8),=C'SYSALLDA'  Unit                                   
         IF    (PARREQCD,EQ,PARRMNT),BEGIN  IF MOUNT REQUEST...                 
         MVC   S99TULNG,=XL2'0004' LENGTH NOW 4                                 
         MVC   S99TUPAR(4),=CL4'3330'  MOUNT ONLY ON 3330                       
         END   ,                                                                
         SETTP ,                   SET TU POINTER                               
         SPACE 1                                                                
*  SET UP STATUS TEXT UNIT                                                      
         LA    R1,DYNTSTAT         R1-->STATUS TEXT UNIT                        
         MVC   S99TUKEY,=AL2(DALSTATS)  STATS KEY                               
         MVC   S99TUNUM,=XL2'0001' NUMBER OF ENTRIES                            
         MVC   S99TULNG,=XL2'0001' LENGTH                                       
         MVI   S99TUPAR,X'01'      OLD                                          
         SETTP ,                   SET TU POINTER                               
         END   ,                                                                
         END   ,                                                                
         SPACE 2                                                                
         OI    DYNTP+16,X'80'      SET TERMINATOR                               
         SPACE 1                                                                
*  SET UP PARM LIST AND CALL DYNALLOC                                           
         LA    R0,DYNRB            R0-->RB                                      
         ST    R0,DYNP                                                          
         OI    DYNP,X'80'          SET TERMINATOR                               
         LA    R1,DYNP                                                          
         IF    ^CVFINIT,'WAITOFF X'                                             
         DYNALLOC ,                                                             
         IF    ^CVFINIT,'WAITON X'                                              
DYNEND   IF    (R15,Z),BEGIN                                                    
*  CAN'T RETURN UCB ADDRESS ON UNALLOCATE                                       
         IF    (PARREQCD,EQ,PARRUNAL),'CLEAR R0; EXIT DYNEND'                   
         SPACE 2                                                                
*  A UCB ADDR IS TO BE RETURNED SO...                                           
         L     R1,CVTCBP           --> WYLBUR'S TCB                             
         L     R1,@R1+12           --> TIOT                                     
         WITH  (TIOT,R1),BEGIN                                                  
         CLEAR R3                                                               
DYNTIOSR LOOP  BEGIN                                                            
         AR    R1,R3               NEXT DD ENTRY                                
         LC    R3,TIOELNGH         CURRENT DD ENTRY LENGTH                      
         IF    (R3,Z),ERREXIT                                                   
         IF    (TIOEDDNM,EQ,PARDDNAM),BEGIN                                     
         L3    R0,TIOEFSRT         R0-->UCB                                     
         CLEAR R15                                                              
         EXIT  DYNTIOSR                                                         
         END   ,                                                                
         END   ,                                                                
         END   ,                                                                
         END   ELSE,'ERREXIT: CLEAR R0; LA R15,40'                              
         END   ,                                                                
         XEXIT R2,R8                                                            
         SPACE 2                                                                
         LTORG ,                                                                
         END   .                                                                
