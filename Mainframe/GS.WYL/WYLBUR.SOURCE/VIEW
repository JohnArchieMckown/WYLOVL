VIEW     TITLE 'WYLBUR''s VIEW Command'                                         
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLVIEW  CSECT                                                                  
VIEW     IDENT 2025                11:49:37 01/25/02  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
         SPACE 2                                                                
         MACRO                                                                  
&L       FREECURS                                                               
         CLEAR CPVCURS,CPVACURS                                                 
         MVC   CPVLNO,=F'-1000'                                                 
         MVC   CPVALNO,=F'-1'                                                   
         CLEAR CPVCOL,CPVACOL                                                   
         MEND                                                                   
*                                                                               
         SYSDEFN                                                                
         SPACE 2                                                                
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         POP   DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  WIND                                                             
         EJECT                                                                  
         COPY  LINT                                                             
         EJECT                                                                  
         MACRO                                                                  
&L       FREELINT                                                               
&L       IF    (LINTLNO,EQ,LINTOLNO),BEGIN  Save old attributes...              
         OC    LINTOFLG,LINTFLG    Preserve the attributes                      
         END                                                                    
         CLEAR LINTOPT             (Also sets LINFFREE)                         
         MEND                                                                   
         TITLE 'VIEW Command'                                                   
*box                                                                            
*                                                                               
*  SETVIEW -- SET VIEW Command.                                                 
*                                                                               
SETVIEW  XPROC                                                                  
         SCAN  VIEWPRT             View options                                 
         SCANCHK                                                                
         B     CVNEXT                                                           
*                                                                               
         PEND                                                                   
         TITLE 'SHOW VIEW Command'                                              
*box                                                                            
*                                                                               
*  SHOWVIEW -- SHOW VIEW Command.                                               
*                                                                               
SHOWVIEW XPROC                                                                  
         SCAN  SHVWPRT                                                          
         SCANCHK                                                                
*                                                                               
         TSEG  'INDENT='                                                        
         IF    (CPVIND,EQ,-1),'TSEG "AUTO"'                                     
         ELSE  'TNUM LH:CPVIND'                                                 
*                                                                               
         TSEG  ' MARGIN='                                                       
         TNUM  LH:CPVMARG                                                       
*                                                                               
         TSEG  CVBLANKS,1                                                       
         IF    CPVFUNN,'TSEG "UN"'                                              
         TSEG  'NUMBERED '                                                      
*                                                                               
         IF    ^CPVFWRAP,'TSEG "NO"'                                            
         TSEG  'WRAP '                                                          
*                                                                               
         IF    ^CPVFINS,'TSEG "NO"'                                             
         TSEG  'INSERT '                                                        
*                                                                               
         IF    ^CPVFSPEL,'TSEG "NO"'                                            
         TSEG  'SPELL '                                                         
*                                                                               
         IF    CPVFPREP,'TSEG "PREPARE "'                                       
*                                                                               
         IF    CPVFTRC,'TSEG "TRACE "'                                          
*                                                                               
         IF    CPVFBUG,'TSEG "DEBUG "'                                          
*                                                                               
         IF    CPVFFORMAT,'TSEG "FORMATTED "'                                   
*                                                                               
         SETMSG '- View options'                                                
         B     CVNXTMSG                                                         
*                                                                               
         PEND                                                                   
*                                                                               
SHVWPRT  SCKW  DEBUG,SHVWDBUG,A                                                 
         SCKW  TRACE,SHVWTRC,A                                                  
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHVWDBUG -- SHOW VIEW DEBUG Routine.                                         
*                                                                               
SHVWWA   RECORD BEGIN                                                           
SHVWMSG  DS    CL128               Formatting work area                         
         END                                                                    
*-                                                                              
SHVWDBUG XPROC SHVWWA                                                           
         L     R5,CVCURJCB                                                      
*-                                                                              
*-       Allow optional line-number of another user.                            
*-                                                                              
         SCAN                                                                   
         IF    (R0,NZ),BEGIN                                                    
         DTB   (R1),(R0)           Convert to binary                            
         IF    (R0,NZ),'VCALL NOTVALID'                                         
         LR    R5,R15                                                           
         MH    R5,=AL2(L'JCB)                                                   
         AL    R5,CVFJCB           Other user's JCB ptr                         
         IF    (R5,LGE,CVLJCB),BEGIN                                            
         ERROR 'Line is out of bounds.'                                         
         END                                                                    
         END                                                                    
*                                                                               
         WITH  (JCB,R5),BEGIN                                                   
         L     R6,JCBCPX           Get CP ptr                                   
         END                                                                    
*                                                                               
         IF    (R6,Z),BEGIN                                                     
         ERROR 'Illegal line -- no CP.'                                         
         END                                                                    
*-                                                                              
*-       General page information.                                              
*-                                                                              
         TSEG  'Screen image ('                                                 
         TNUM  LH:CPVHEI-CP(R6)                                                 
         TSEG  ' by '                                                           
         TNUM  LH:CPVWID-CP(R6)                                                 
         TSEG  ' by '                                                           
         TNUM  LH:CPVTWID-CP(R6)                                                
         TSEG  '):'                                                             
         TCR                                                                    
         TCR                                                                    
*                                                                               
*CP      TSEG  'Exportable information: '                                       
*CP      SETMSG SHVWMSG                                                         
*CP      VCALL GETVINFO            Get view information                         
*CP      TSEG  (R1),(R0)                                                        
*CP      TCR                                                                    
*CP      TCR                                                                    
*                                                                               
         TSEG  'Marked block:  Active #'                                        
         TNUM  L:CPBLKACT-CP(R6)                                                
         TSEG  ', first='                                                       
         L     R15,CPFBKLNO-CP(R6)                                              
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' (col='                                                         
         TNUM  LH:CPFBKCOL-CP(R6)                                               
         TSEG  '), last='                                                       
         L     R15,CPLBKLNO-CP(R6)                                              
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' (col='                                                         
         TNUM  LH:CPLBKCOL-CP(R6)                                               
         TSEG  ')'                                                              
         TCR                                                                    
         TCR                                                                    
*                                                                               
         TSEG  'Current   cursor: '                                             
         TNUM  L:CPVCURR-CP(R6)                                                 
         TSEG  ','                                                              
         TNUM  L:CPVCURC-CP(R6)                                                 
         TSEG  ' ('                                                             
         L     R15,CPVLNO-CP(R6)                                                
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' lineno, '                                                      
         TNUM  LH:CPVCOL-CP(R6)                                                 
         TSEG  ' column)',,CR                                                   
*                                                                               
         TSEG  'Alternate cursor: '                                             
         TNUM  L:CPVACURR-CP(R6)                                                
         TSEG  ','                                                              
         TNUM  L:CPVACURC-CP(R6)                                                
         TSEG  ' ('                                                             
         L     R15,CPVALNO-CP(R6)                                               
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  ' lineno, '                                                      
         TNUM  LH:CPVACOL-CP(R6)                                                
         TSEG  ' column)',,CR                                                   
         TCR                                                                    
*-                                                                              
*-       Display each window's status.                                          
*-                                                                              
         LA    R4,1                                                             
         L     R5,CPWINDP-CP(R6)                                                
         WHILE ('CH R4,CPVWCNT-CP(R6)',LE),BEGIN                                
         WITH  (WIND,R5)                                                        
*                                                                               
         TSEG  'Window '                                                        
         TNUM  LH:WINDNO                                                        
         TSEG  ':  '                                                            
         IF    ('CLC WINDNO,CPVWCUR-CP(R6)',EQ),'TSEG "** Cur **"'              
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'Active #'                                                       
         TNUM  L:WINDACT                                                        
         IF    ('CLC WINDACT,CPACTNO-CP(R6)',EQ),'TSEG " ** Cur **"'            
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'First/Last='                                                    
         L     R15,WINDFLNO                                                     
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  '/'                                                              
         L     R15,WINDLLNO                                                     
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'Start/End='                                                     
         L     R15,WINDSLNO                                                     
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TSEG  '/'                                                              
         L     R15,WINDELNO                                                     
         ACALL VLNO                                                             
         TSEG  (R1),(R0)                                                        
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'Lines='                                                         
         TNUM  LH:WINDTCNT                                                      
         TSEG  ', text col='                                                    
         TNUM  LH:WINDTCOL                                                      
         TSEG  ', text length='                                                 
         TNUM  LH:WINDTLEN                                                      
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'First column-1='                                                
         TNUM  LH:WINDFCOL                                                      
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'Frow/Lrow='                                                     
         TNUM  LH:WINDFROW                                                      
         TSEG  ','                                                              
         TNUM  LH:WINDLROW                                                      
         TCR                                                                    
*                                                                               
         TSEG  CVBLANKS,11                                                      
         TSEG  'Indent='                                                        
         TNUM  LH:WINDIND                                                       
         TSEG  CVBLANKS,2                                                       
         TSEG  'Margin='                                                        
         TNUM  LH:WINDMARG                                                      
         TSEG  CVBLANKS,2                                                       
*                                                                               
         SETMSG 'Tabs='                                                         
         LA    R3,WINDTABS                                                      
         LA    R2,L'WINDTABS                                                    
         LOOP  BEGIN                                                            
         IF    ('CLI @R3,0',EQ),EXIT  All done, scram                           
         TSEG  (R1),(R0)                                                        
         TNUM  LC:@R3              Display tab stop                             
         SETMSG ', '                                                            
*                                                                               
         LA    R3,@R3+1                                                         
         DECR  R2                                                               
         UNTIL (R2,Z)                                                           
         END                                                                    
         TCR                                                                    
*                                                                               
         IF    WINFREWR,'TSEG "           Flag: Rewrite",,CR'                   
         IF    WINFMOD,' TSEG "           Flag: Mods",,CR'                      
*                                                                               
         LA    R5,WINDNEXT                                                      
         LA    R4,@R4+1                                                         
*                                                                               
         TCR                                                                    
         TCR                                                                    
         END                                                                    
*-                                                                              
*-       Display each line's status.                                            
*-                                                                              
         TSEG  'Wind Row Type   Flags      Lineno  Old-lineno',,CR              
         TSEG  '---- --- ----   -----      ------  ----------',,CR              
*                                                                               
         LA    R4,1                                                             
         L     R5,CPLINTP-CP(R6)                                                
         WHILE ('CH R4,CPVHEI-CP(R6)',LE),BEGIN  Go through lines...            
         WITH  (LINT,R5)                                                        
*                                                                               
         TNUM  LH:LINTWIND,4                                                    
         TNUM  LH:LINTROW,4                                                     
*                                                                               
         SETMSG ' ??????'                                                       
         IF    LINFFREE,'LA R1,=C" Free  "'                                     
         IF    LINFNEW,' LA R1,=C" New   "'                                     
         IF    LINFTEXT,'LA R1,=C" Text  "'                                     
         IF    LINFCOL,' LA R1,=C" Col   "'                                     
         IF    LINFCMD,' LA R1,=C" Cmd   "'                                     
         IF    LINFMSG,' LA R1,=C" Msg   "'                                     
         IF    LINFSTAT,'LA R1,=C" Status"'                                     
         TSEG  (R1),(R0),B                                                      
*                                                                               
         THEX  LC:LINTFLG,2                                                     
         TSEG  ','                                                              
         THEX  LC:LINTOFLG,2                                                    
         TSEG  CVBLANKS,2                                                       
*                                                                               
         L     R15,LINTLNO                                                      
         ACALL VLNO                                                             
         TSEG  CPDOUB,9,B                                                       
*                                                                               
         L     R15,LINTOLNO                                                     
         ACALL VLNO                                                             
         TSEG  CPDOUB,9                                                         
*                                                                               
         TCR                                                                    
*                                                                               
         LA    R5,LINTNEXT                                                      
         LA    R4,@R4+1                                                         
         END                                                                    
*-                                                                              
*-       All done, scram.                                                       
*-                                                                              
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VLNO -- Local routine to format line-number in R15.                          
*                                                                               
VLNO     PROC                                                                   
         IF    (R15,EQ,-1),'LH R15,=H"-1000"'                                   
         IF    (R15,EQ,-2),'LH R15,=H"-2000"'                                   
         IF    (R15,EQ,-3),'LH R15,=H"-3000"'                                   
         BTR   CPDOUB,(9,3),(R15)                                               
         VCALL LRTRIM                                                           
         PRETURN (R0,R1)                                                        
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VTRC -- VIEW Trace Entry                                                     
*                                                                               
VTRC     RECORD BEGIN                                                           
VTRCSTRT DS    CL4'VTRC'           Self identification                          
VTRCLINK DS    A                   Next VTRC ptr (or zero)                      
VTRCCLCK DS    D                   Time stamp                                   
*                                                                               
VTRCSNO  DS    H                   Session number                               
VTRCACCT DS    CL5                 Session account (uuugg)                      
*                                                                               
VTRCFLAG FLAG                                                                   
         FLAG  VTRCFOUT            - Terminal output text                       
         FLAG  VTRCFIN             - Terminal input text                        
*                                                                               
VTRCDLEN DS    H                   Data length                                  
VTRCHDR  EQU   VTRCSTRT,*-VTRCSTRT,C'X'                                         
*                                                                               
VTRCDATA DS    0X                  Data starts here (variable len)              
         END                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIEWTRC -- Routine to queue a trace buffer for debugging (if                 
*    the user has set view tracing).                                            
*                                                                               
*    On entry:                                                                  
*      R1,R0 - data loc, len                                                    
*      R15 - type of trace:                                                     
*              0 - terminal output                                              
*              1 - terminal input                                               
*                                                                               
VIEWTRC  XPROC                                                                  
*-                                                                              
*-       Only save the buffer if view tracing is requested.                     
*-                                                                              
         IF    CPVFTRC,BEGIN       View tracing...                              
         LR    R5,R15                                                           
         LR    R4,R1                                                            
         LR    R3,R0                                                            
*                                                                               
         AH    R0,=AL2(L'VTRCHDR)  Get a new trace buffer                       
*        GETMAIN R,LV=(0)                                                       
         VCALL GETCORE                                                          
         LR    R6,R1                                                            
         WITH  (VTRC,R6)                                                        
         CLEAR VTRCHDR                                                          
         MVC   VTRC(4),=C'VTRC'    Set self-identification                      
         MVC   VTRCLINK,CVVTRCP    Set our queue link ptr                       
         VCALL LOCALTOD            Time stamp                                   
         STM   R0,R1,VTRCCLCK                                                   
*                                                                               
         IF    (R5,EQ,0),'SET VTRCFOUT'  Terminal output buffer                 
         IF    (R5,EQ,1),'SET VTRCFIN'  Terminal input buffer                   
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'MVC VTRCSNO,JCBSEQ'  Save session number              
         MVC   VTRCACCT,CPSACCT    Save account                                 
*                                                                               
         STH   R3,VTRCDLEN         Save data length                             
         MOVEL VTRCDATA,(R4),(R3)  Save data                                    
*-                                                                              
*-       Now link this new view trace entry.                                    
*-                                                                              
         ST    R6,CVVTRCP          Set new view trace queue head                
*                                                                               
         INCR  R15,CVVNTRC         Keep count of number of entries              
*-                                                                              
*-       Free oldest entries.                                                   
*-                                                                              
         WHILE (CVVNTRC,GT,=Y(CVVTRC#)),'CLEAR R1; ACALL FREEVTRC'              
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FREEVTRC -- Routine to free a trace entry.                                   
*                                                                               
*    On entry:                                                                  
*      R1 - VTRC ptr of entry to free;                                          
*             or zero to free last (oldest) entry.                              
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - no entries left in trace queue                                       
*      nz- there are still entries in the trace queue                           
*                                                                               
FREEVTRC XPROC                                                                  
         LR    R6,R1               VTRC ptr                                     
*-                                                                              
*-       Find the previous VTRC entry so we can unlink this entry.              
*-                                                                              
         CLEAR R5                  Previous entry ptr                           
         LT    R15,CVVTRCP         First entry ptr                              
         FAIL  Z,VTRCERR1,'Free VTRC logic error.'                              
*                                                                               
         LOOP  BEGIN               Go through VTRC entries...                   
         WITH  (VTRC,R15)                                                       
*                                                                               
         IF    ((R6,Z),AND,(VTRCLINK,Z)),BEGIN  Free last entry...              
         LR    R6,R15              Set current ptr to last entry                
         B     FVTRCOM                                                          
         END                                                                    
*                                                                               
         IF    (R6,EQ,R15),FVTRCOM  Matched the entry, scram                    
*                                                                               
         LR    R5,R15              Save previous entry ptr                      
         LT    R15,VTRCLINK        Next VTRC entry                              
         FAIL  Z,VTRCERR2,'Free VTRC logic error.'                              
         END                                                                    
*-                                                                              
*-       Dequeue our VTRC entry.                                                
*-                                                                              
FVTRCOM  IF    (R5,Z),BEGIN        We are removing the 1st entry...             
         MVC   CVVTRCP,VTRCLINK-VTRC(R6)  Set new queue head                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               Dequeue...                                   
         WITH  (VTRC,R5)                                                        
         FAIL  (VTRC,NE,'VTRC'),VTRCERR3,'Free VTRC logic error.'               
         MVC   VTRCLINK,VTRCLINK-VTRC(R6)  Dequeue our entry                    
         END                                                                    
*-                                                                              
*-       Free the VTRC entry.                                                   
*-                                                                              
         WITH  (VTRC,R6),BEGIN                                                  
         FAIL  (VTRC,NE,'VTRC'),VTRCERR4,'Free VTRC logic error.'               
         MVC   VTRC(4),=C'OVTR'    Neatness                                     
         MVC   VTRCLINK,=F'-1'     (Ditto)                                      
*                                                                               
         LA    R1,VTRC                                                          
         LA    R0,L'VTRCHDR        VTRC header...                               
         AH    R0,VTRCDLEN         ...plus buffer (variable length)             
*        FREEMAIN R,A=(1),LV=(0)                                                
         VCALL FREECORE                                                         
*                                                                               
         DECR  R15,CVVNTRC         Decrement number of trace entries            
         END                                                                    
*                                                                               
         L     R15,CVVTRCP         Set CC for caller                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHVWTRC -- SHOW VIEW TRACE Routine.                                          
*                                                                               
SHVWTRC  XPROC                                                                  
         IF    ^CPSFSPR,'VCALL NOTVALID'   Priv'd                               
*-                                                                              
*-       Title line.                                                            
*-                                                                              
         IF    (CVVTRCP,Z),BEGIN   No trace entries...                          
         SETMSG 'View buffer trace table is empty.'                             
         B     CVNXTMSG                                                         
         END                                                                    
*                                                                               
         TSEG  'View buffer trace',,CR                                          
*-                                                                              
*-       Step through the trace entry queue.                                    
*-                                                                              
         LA    R6,CVVTRCP-(VTRCLINK-VTRC)  Dummy first link                     
         LOOP  BEGIN                                                            
         WITH  (VTRC,R6)                                                        
         WHILE ('LT R6,VTRCLINK',NZ)  Go through buffers...                     
         TSEG  '-----------------',,CR                                          
         TCR                                                                    
*-                                                                              
*-       Format the trace entry.                                                
*-                                                                              
         IF    (VTRC,NE,'VTRC'),BEGIN  Shucks...                                
         SETMSG 'Bad trace entry queue!'                                        
         B     CVNXTMSG                                                         
         END                                                                    
*                                                                               
         LM    R0,R1,VTRCCLCK      Get time stamp                               
         XPUSH ,,11,PTR=R15                                                     
         VCALL FMTTIME             Get "hh:mm:ss.th"                            
         TSEG  (R1),(R0),B                                                      
         TSEG  'Line '                                                          
         TNUM  LH:VTRCSNO,3        Session number                               
         TSEG  CVBLANKS,1                                                       
         TSEG  VTRCACCT+3,2        gg                                           
         TSEG  '.'                                                              
         TSEG  VTRCACCT,3          uuu                                          
*                                                                               
         SETMSG '  ??? '                                                        
         IF    VTRCFOUT,'SETMSG "  OUTPUT "'                                    
         IF    VTRCFIN,'SETMSG "  INPUT "'                                      
         TSEG  (R1),(R0)                                                        
*                                                                               
         TSEG  '('                                                              
         TNUM  LH:VTRCDLEN                                                      
         TSEG  ' chars)',,CR                                                    
         TCR                                                                    
*                                                                               
         SETMSG VTRCDATA,LH:VTRCDLEN                                            
         LA    R15,2                                                            
         L     R2,=A(X'273B')      Break on ESC, CSI                            
         VCALL ESCFMT              Display buffer text                          
         TCR                                                                    
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'VIEW Command'                                                   
*box                                                                            
*                                                                               
*  VIEW -- VIEW Command.                                                        
*                                                                               
VIEW     XPROC                                                                  
*-                                                                              
*-       Check out terminal type.                                               
*-                                                                              
         VCALL PAGETCHK            Make sure the terminal is ok                 
*                                                                               
         SCAN  VIEWPRTC            Look for options                             
         SCANCHK                                                                
         IF    (R15,EQ,4),VUNREC                                                
*                                                                               
         IF    CPFCLEAR+CPFKEEP,'VCALL CLRTEXT'                                 
*                                                                               
         SET   CPVFPAGE,CPVFCLR                                                 
*                                                                               
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),'SET JCBSFV'  At least one VIEW issued                 
         VCALL PAGECMD             Back to page mode                            
         B     CVNEXT              (No return)                                  
*-                                                                              
*-       Process view range.                                                    
*-                                                                              
VUNREC   IF    CPFCLEAR+CPFKEEP,BEGIN     Invalid option                        
         VCALL NOTVALID                                                         
         END                                                                    
         SCBKUP                                                                 
         CLEAR CPVFFORMAT          Default is UNFORMATTED                       
         SET   CPLFLG1.CPFALL      All is the default range                     
         VCALL NDETRNG             Scan range                                   
         SCAN  VIEWPRT             Scan for command options                     
*                                                                               
         IF    CPFSTRRG,BEGIN      String range...                              
         SET   CPVFREWR            Write all lines (for highlighting)           
         END                                                                    
*                                                                               
         IF    ^CPVFPAGE,BEGIN     Re-write the screen...                       
         VCALL PAGECLR             Forget the previous screen                   
         SET   CPVFCLR             Screen needs to be rewritten                 
         END                                                                    
*                                                                               
         RDRNG VIEWRNG,(DESNRTN+LOCATRTN+UNPRST) NO RETURN                      
         BOMB  ,                   WOW, NO RETURN                               
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
VIEWPRT  SCKW  ,VIEWPSH,PUSH                                                    
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
VIEWPRTC SCKW  ,VIEWPSH,PUSH                                                    
         SCKW  ,V(CLRPSH),PUSH                                                  
         SCKW  ,VWUNREC                                                         
*                                                                               
VWUNREC  PROC                                                                   
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
*                                                                               
VIEWPSH  PUSHRTN VIEWKWS                                                        
*                                                                               
VIEWKWS  SCKW  HEIGHT,VHEIGHT,(P,I,A),80 !!! THIS IS LINTMAX#                   
         SCKW  WIDTH,VWIDTH,(P,I,A),&LINESZ                                     
         SCKW  INSERT,VINS,A                                                    
         SCKW  NOINSERT,VNOINS,A                                                
         SCKW  MARGIN,VMARGIN,(A,P,PI),&LINESZ                                  
         SCKW  INDENT,VINDENT,A                                                 
         SCKW  NOINDENT,VNOIND,A                                                
         SCKW  UNNUMBERED,VUNN,A                                                
         SCKW  NUMBERED,VNUM,A                                                  
         SCKW  FORMATTED,VFORMAT,A                                              
         SCKW  UNFORMATTED,VUNFMT,A                                             
         SCKW  WRAP,VWRAP,A                                                     
         SCKW  NOWRAP,VNOWRAP,A                                                 
         SCKW  SPELL,VSPELL,A                                                   
         SCKW  NOSPELL,VNOSPELL,A                                               
         SCKW  PREP,VPREP                                                       
         SCKW  PREPARE,VPREP                                                    
         SCKW  NOPREPARE,VNOPREP,A                                              
         SCKW  RESET,VRESET,A                                                   
         SCKW  MULTIPLE,VMULT,A                                                 
         SCKW  TRACE,VTRACE,A                                                   
         SCKW  NOTRACE,VNOTRACE,A                                               
         SCKW  TCLEAR,VTCLEAR,A                                                 
         SCKW  DEBUG,VDEBUG,A                                                   
         SCKW  NODEBUG,VNODEBUG,A                                               
         SCKW  ,,END                                                            
*                                                                               
VHEIGHT  PROC                                                                   
         IF    (R0,NE,CPVHEI),BEGIN  Changing heights...                        
         LR    R4,R0               New height                                   
         XPUSH ,,256,PTR=R3                                                     
         SETMSG (R3),256            Working view info area                      
         VCALL GETVINFO            Get current view info                        
         XPUSH R0,R1               Save view info message ptrs                  
*                                                                               
         STH   R4,CPVHEI           Set new height                               
         VCALL PAGEINIT            Re-initialize page                           
*                                                                               
         XPOP  R0,R1               Restore view info ptrs                       
         VCALL SETVINFO            Set view info                                
         XPOP  ,,256               Neatness                                     
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VWIDTH   PROC                                                                   
         IF    CPVFVTAM,'DECR R0'  VTAM width is actual-1                       
*                                                                               
         IF    (R0,NE,CPVWID),'STH R0,CPVWID; VCALL CALCDIM'                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VINS     PROC                                                                   
         SET   CPVFINS                                                          
         PEND                                                                   
*                                                                               
VNOINS   PROC                                                                   
         CLEAR CPVFINS                                                          
         PEND                                                                   
*                                                                               
VMARGIN  PROC                                                                   
         IF    (R0,NE,CPVMARG),'STH R0,CPVMARG; VCALL PAGEINIT'                 
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VINDENT  PROC                                                                   
         CLEAR R3                                                               
         SCAN  INDPRT              Look for options                             
         SCANCHK                                                                
         LR    R0,R3                                                            
         IF    (R0,NE,CPVIND),'STH R0,CPVIND; VCALL PAGEINIT'                   
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
INDPRT   SCKW  ,VINDNULL,NULL                                                   
         SCKW  AUTO,VINDAUTO,A                                                  
         SCKW  ,VINDNUM,I                                                       
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
VINDAUTO PROC                                                                   
         L     R3,=F'-1'                                                        
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
VINDNUM  PROC                                                                   
         LR    R3,R0                                                            
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
VINDNULL PROC                                                                   
         ERROR 'Indent: missing operand.'                                       
         PEND                                                                   
*                                                                               
VNOIND   PROC                                                                   
         IF    (CPVIND,NZ),'CLEAR CPVIND; VCALL PAGEINIT'                       
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VUNN     PROC                                                                   
         IF    ^CPVFUNN,'SET CPVFUNN; VCALL PAGEINIT'                           
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VNUM     PROC                                                                   
         IF    CPVFUNN,'CLEAR CPVFUNN; VCALL PAGEINIT'                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VFORMAT  PROC                                                                   
         SET   CPVFFORMAT                                                       
         PEND                                                                   
*                                                                               
VUNFMT   PROC                                                                   
         CLEAR CPVFFORMAT                                                       
         PEND                                                                   
*                                                                               
VWRAP    PROC                                                                   
         IF    ^CPVFWRAP,'SET CPVFWRAP; VCALL PAGEINIT'                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VNOWRAP  PROC                                                                   
         IF    CPVFWRAP,'CLEAR CPVFWRAP; VCALL PAGEINIT'                        
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VSPELL   PROC                                                                   
         SET   CPVFSPEL                                                         
         SET   CPVFREWR                                                         
         PEND                                                                   
*                                                                               
VNOSPELL PROC                                                                   
         CLEAR CPVFSPEL                                                         
         SET   CPVFREWR                                                         
         PEND                                                                   
*                                                                               
VPREP    PROC                                                                   
         SET   CPVFPREP                                                         
         SET   CPVFREWR                                                         
         PEND                                                                   
*                                                                               
VNOPREP  PROC                                                                   
         CLEAR CPVFPREP                                                         
         SET   CPVFREWR                                                         
         PEND                                                                   
*                                                                               
VRESET   PROC                                                                   
         CLEAR CPVHEI,CPVWID,CPVTWID                                            
         VCALL PAGEINIT                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VMULT    PROC                                                                   
         CLEAR CPVHEI,CPVWID,CPVTWID                                            
         VCALL PAGEINIT                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VTRACE   PROC                                                                   
         SET   CPVFTRC                                                          
         PEND                                                                   
*                                                                               
VNOTRACE PROC                                                                   
         CLEAR CPVFTRC                                                          
         PEND                                                                   
*                                                                               
VTCLEAR  PROC                                                                   
         WHILE ('LT R1,CVVTRCP',NZ),BEGIN  Free all trace entries...            
         ACALL FREEVTRC            Free this entry                              
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
VDEBUG   PROC                                                                   
         SET   CPVFBUG                                                          
         PEND                                                                   
*                                                                               
VNODEBUG PROC                                                                   
         CLEAR CPVFBUG                                                          
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIEWRNG -- Co-routine to process lines from the range processor.             
*                                                                               
VIEWRNG  PROC                                                                   
         SET   CPVFPAGE            Page mode from now on                        
*                                                                               
         L     R5,CPLCNO           Line-number                                  
*                                                                               
VRNGLOOP LR    R15,R5                                                           
         VCALL PAGELINE            Add line to page                             
         IF    NZ,BEGIN            Won't fit...                                 
         MVC   CPVMSG,CVBLANKS                                                  
         MVC   CPVMSG(L'VIEWMSG),VIEWMSG  ESC RETURN to continue.               
         VCALL PAGEMOD             Display the current page                     
         IF    NZ,'VCALL RNGABORT; B CVABORT'                                   
         B     VRNGLOOP                                                         
         END                                                                    
         PEND                                                                   
*                                                                               
VIEWMSGX DC    C'Press CONTINUE [ESC RETURN] '                                  
         DC    C'for the next page in the range.'                               
VIEWMSG  EQU   VIEWMSGX,*-VIEWMSGX,C'X'                                         
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIEWCMD -- Routine to process VIEW Subcommands.                              
*                                                                               
*    On entry:                                                                  
*      R6 - CPCMD contains the command                                          
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - local command executed                                               
*      4 - not a local command                                                  
*                                                                               
VWA      RECORD BEGIN                                                           
VWAFLAG  FLAG                                                                   
         FLAG  VWAFNLOC            - Not a local command                        
         FLAG  VWAFNCMD            - New command text is set                    
         FLAG  VWAFPOS             - Cursor position is set                     
         FLAG  VWAFLBOT            - Lineno can't be close to bottom            
*                                                                               
VWACHKL  DS    F                   Verify this lineno is on the page            
*                                                                               
VWASG    SEGCB                                                                  
*                                                                               
VWAGCOL  DS    H                   BLKGET current column                        
VWAGLEN  DS    H                   BLKGET current line length                   
VWAGLNO  DS    F                   BLKGET current lineno                        
*                                                                               
VWACMD   DS    CL64                Command to execute                           
VWAINIT  EQU   VWAFLAG,*-VWAFLAG,C'X'                                           
*                                                                               
*                                                                               
VWAPTRS  DS    2F                  Working ptrs                                 
VWAVAR   DS    CL32                Working exec variable area                   
*                                                                               
VWASGBUF DS    CL(L'CPVMSG)        Working message buffer                       
*                                                                               
VWAGLINE DS    CL(&LINESZ)         Get line buffer                              
         DS    XL(&PRESSZ-&LINESZ)                                              
*                                                                               
VWAPLINE DS    CL(&LINESZ)         Put line buffer                              
         DS    XL(&PRESSZ-&LINESZ)                                              
         END                                                                    
*-                                                                              
VIEWCMD  XPROC VWA                                                              
         LA    R6,VWA                                                           
         WITH  (VWA,R6)                                                         
*                                                                               
         CLEAR VWAINIT                                                          
         MVC   VWACHKL,=F'-1'                                                   
*                                                                               
         SEGINIT VWASGBUF,,VWASG   Initialize message seg buffer                
*-                                                                              
*-     Commands don't change the cursor position if the cursor isn't            
*-         on a text line.                                                      
*-                                                                              
         LT    R15,CPVCURR                                                      
         IF    NZ,BEGIN                                                         
         MH    R15,=AL2(L'LINT)                                                 
         A     R15,CPXLINT                                                      
         WITH  (LINT,R15)                                                       
         IF    ^LINFTEXT,'SET VWAFPOS'  Keep the current position               
         END                                                                    
*-                                                                              
*-       Process VIEW Subcommands (which must begin with a ".").                
*-                                                                              
         SETMSG CPCMD,LH:CPCMDLEN   Command text                                
         VCALL LRTRIM                                                           
*                                                                               
         IF    ((R0,NP),OR,(@R1,NE,'.')),BEGIN  Not our command...              
         LA    R15,4               Not our command                              
         B     VCMDEXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Check for user defined function key.                                   
*-                                                                              
         IF    (R0,EQ,2),BEGIN     Could be re-defined...                       
         STM   R0,R1,VWAPTRS                                                    
         MVC   VWAVAR,=CL(L'VWAVAR)'SES.SYSVIEW_x'                              
         MVC   VWAVAR+12(1),@R1+1  Function code                                
         SETMSG VWAVAR              Variable name                               
         VCALL GBLCMD              Execute command if possible                  
*                                                                               
         MVC   VWAVAR(3),=C'PUB'   Now try PUB.SYSVIEW_x                        
         SETMSG VWAVAR              Variable name                               
         VCALL GBLCMD              Execute command if possible                  
         LM    R0,R1,VWAPTRS                                                    
         END                                                                    
*                                                                               
         SCINIT (R1),(R0)                                                       
         SCAN  CMDPRT              VIEW Subcommands                             
         SCANCHK                                                                
         CLEAR R15                                                              
*-                                                                              
*-       Make sure we are still displaying enough lines.                        
*-                                                                              
         IF    (VWACHKL,NE,-1),BEGIN  We need to check...                       
         VCALL PAGEFILL            Update page image                            
*                                                                               
         L     R1,VWACHKL                                                       
         CLEAR R0                  Find in current window                       
         VCALL PAGEFIND            Find the new line                            
         IF    NZ,BEGIN            It's on the page...                          
         IF    ^VWAFLBOT,EXIT      We don't need a bottom check                 
         IF    (R1,LT,2),'CLEAR R15'  Too close to bottom                       
         END                                                                    
*                                                                               
         IF    (R15,Z),BEGIN       Can't find line...                           
         L     R15,CPVLNO          Cursor line-number                           
         LA    R0,2                Allow two lines after cursor                 
         VCALL PAGEBOT                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Finish up.                                                             
*-                                                                              
         IF    (VWACMD,NZ),BEGIN   We have a command to execute...              
         MVC   CPCMDLEN,=AL2(L'VWACMD)                                          
         MVC   CPCMD(L'VWACMD),VWACMD  Set command                              
         SET   VWAFNLOC            It's not a local command                     
         END                                                                    
*                                                                               
         LA    R15,4               Assume it wasen't our command                
         IF    ^VWAFNLOC,BEGIN     It's ours...                                 
         MVC   CPVMSG,CVBLANKS                                                  
         L     R15,VWASGLENF                                                    
         MOVE  R15,CPVMSG,VWASGBUF  Set any message                             
*                                                                               
         IF    ^VWAFNCMD,'CLEAR CPCMDLEN'  Clear old command                    
*                                                                               
         CLEAR R15                 Set our command rc                           
         END                                                                    
*                                                                               
VCMDEXIT PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         USING VWA,R6                                                           
*box                                                                            
*                                                                               
*   VIEW Sub-commands.                                                          
*                                                                               
CMDPRT   SCKW  ,CMDNULL,NULL                                                    
         SCKW  ..A,CMDERR,S                                                     
         SCKW  ..B,BOTTOM,S        .BOTTOM                                      
         SCKW  ..C,CMDERR,S                                                     
         SCKW  ..D,DOWN,S          .DOWN                                        
         SCKW  ..E,CMDERR,S                                                     
         SCKW  ..F,CMDERR,S                                                     
         SCKW  ..G,CMDERR,S                                                     
         SCKW  ..H,CMDERR,S                                                     
         SCKW  ..I,CMDERR,S                                                     
         SCKW  ..J,CMDERR,S                                                     
         SCKW  ..K,CMDERR,S                                                     
         SCKW  ..L,CMDERR,S                                                     
         SCKW  ..M,CONTINUE,S      .CONTINUE                                    
         SCKW  ..N,CMDERR,S                                                     
         SCKW  ..O,CMDERR,S                                                     
         SCKW  ..P,CMDERR,S                                                     
         SCKW  ..Q,CMDERR,S                                                     
         SCKW  ..R,CMDERR,S                                                     
         SCKW  ..S,CMDERR,S                                                     
         SCKW  ..T,TOP,S           .TOP                                         
         SCKW  ..U,UP,S            .UP                                          
         SCKW  ..V,CMDERR,S                                                     
         SCKW  ..W,CMDERR,S                                                     
         SCKW  ..X,CMDERR,S                                                     
         SCKW  ..Y,CMDERR,S                                                     
         SCKW  ..Z,CMDERR,S                                                     
*                                                                               
         SCKW  .A,ALIGNV,S         .ALIGN                                       
         SCKW  .B,BOTTOM,S         .BOTTOM                                      
         SCKW  .C,COPY,S           .COPY                                        
         SCKW  .D,DOWN,S           .DOWN                                        
         SCKW  .E,END,S            .END                                         
         SCKW  .F,FIRST,S          .FIRST                                       
         SCKW  .G,GUESS,S          .GUESS                                       
         SCKW  .H,HELPESC,S         .HELP                                       
         SCKW  .I,V(SCNNOOP),S    .IMODE                                        
         SCKW  .J,JOIN,S           .JOIN                                        
         SCKW  .K,CMDERR,S                                                      
         SCKW  .L,LEARN,S          .LEARN                                       
         SCKW  .M,COPY,S           .MOVE                                        
         SCKW  .N,NEXT,S           .NEXT                                        
         SCKW  .O,OOPS,S           .OOPS                                        
         SCKW  .P,PREVIOUS,S       .PREVIOUS                                    
         SCKW  .Q,CMDERR,S                                                      
         SCKW  .R,CMDERR,S                                                      
         SCKW  .S,SPLIT,S          .SPLIT                                       
         SCKW  .T,TOP,S            .TOP                                         
         SCKW  .U,UP,S             .UP                                          
         SCKW  .V,REVIEW,S         .REVIEW                                      
         SCKW  .W,WERROR,S         .WINDOW                                      
         SCKW  .X,CMDERR,S                                                      
         SCKW  .Y,CMDERR,S                                                      
         SCKW  .Z,ALIGNV,S         .JUSTIFY                                     
*                                                                               
         SCKW  .WA,WERROR,S                                                     
         SCKW  .WB,WERROR,S                                                     
         SCKW  .WC,WCOLUMN,S       .WINDOW COLUMN                               
         SCKW  .WD,WERROR,S                                                     
         SCKW  .WF,WERROR,S                                                     
         SCKW  .WG,WERROR,S                                                     
         SCKW  .WH,WERROR,S                                                     
         SCKW  .WI,WERROR,S                                                     
         SCKW  .WJ,WJOIN,S         .WINDOW JOIN                                 
         SCKW  .WK,WERROR,S                                                     
         SCKW  .WL,WLEFT,S         .WINDOW LEFT                                 
         SCKW  .WM,WERROR,S                                                     
         SCKW  .WN,WACTNEXT,S      .WINDOW NEXT                                 
         SCKW  .WO,WERROR,S                                                     
         SCKW  .WP,WACTPREV,S      .WINDOW PREVIOUS                             
         SCKW  .WQ,WERROR,S                                                     
         SCKW  .WR,WRIGHT,S        .WINDOW RIGHT                                
         SCKW  .WS,WSPLIT,S        .WINDOW SPLIT                                
         SCKW  .WT,WTAB,S          .WINDOW TAB                                  
         SCKW  .WU,WNUM,S          .WINDOW UNNUMBERED                           
         SCKW  .WV,WERROR,S                                                     
         SCKW  .WW,WWIDTH,S        .WINDOW WIDTH                                
         SCKW  .WY,WERROR,S                                                     
         SCKW  .WZ,WERROR,S                                                     
*                                                                               
         SCKW  .W<,WLMARGIN,S      .W<                                          
         SCKW  .W>,WRMARGIN,S      .W>                                          
         SCKW  .WPLUS,WWRAP,S      .W+                                          
         SCKW  .WMINUS,WWRAP,S     .W-                                          
         SCKW  .WSLASH,WLAUTO,S    .W/                                          
*                                                                               
         SCKW  .W0,WSELECT,S                                                    
         SCKW  .W1,WSELECT,S                                                    
         SCKW  .W2,WSELECT,S                                                    
         SCKW  .W3,WSELECT,S                                                    
         SCKW  .W4,WSELECT,S                                                    
         SCKW  .W5,WSELECT,S                                                    
         SCKW  .W6,WSELECT,S                                                    
         SCKW  .W7,WSELECT,S                                                    
         SCKW  .W8,WSELECT,S                                                    
         SCKW  .W9,WSELECT,S                                                    
*                                                                               
         SCKW  .PF0,CONTINUE,S           ESC RETURN                             
         SCKW  .PF1,COLLECT,S            ESC SPACE                              
         SCKW  .PF2,SPLIT,S              ESC S                                  
         SCKW  .PF3,JOIN,S               ESC J                                  
         SCKW  .PF4,PREVIOUS,S           ESC P                                  
         SCKW  .PF5,NEXT,S               ESC N                                  
         SCKW  .PF6,DELL,S               DEL L                                  
         SCKW  .PF7,DOWN,S               ESC D                                  
         SCKW  .PF8,UP,S                 ESC U                                  
         SCKW  .PF9,OOPS,S               ESC O                                  
         SCKW  .PF10,CONTINUE,S          .CONTINUE                              
         SCKW  .PF11,ESCAPE,S            .ESCAPE                                
         SCKW  .PF12,PREPARE,S           .PREPARE                               
         SCKW  .PF13,FBLOCK,S            ESC (                                  
         SCKW  .PF14,LBLOCK,S            ESC )                                  
         SCKW  .PF15,COPY,S              ESC C                                  
         SCKW  .PF16,MOVE,S              ESC M                                  
         SCKW  .PF17,DELETE,S            DEL B                                  
         SCKW  .PF18,REMARK,S            ESC +                                  
         SCKW  .PF19,UNMARK,S            ESC -                                  
         SCKW  .PF20,WSPLIT,S            WINDOW S                               
         SCKW  .PF21,WJOIN,S             WINDOW J                               
         SCKW  .PF22,PF22,S              WINDOW W or WINDOW L                   
         SCKW  .PF23,FIRST,S             ESC F                                  
         SCKW  .PF24,END,S               ESC E                                  
*                                                                               
         SCKW  .CONTINUE,CONTINUE,(A,S)  .CONTINUE                              
         SCKW  .RESET,RESET,(A,S)        .RESET                                 
         SCKW  .BREAK,BREAK,(A,S)        .BREAK                                 
         SCKW  .BOTTOM,BOTTOM,(A,S)      .BOTTOM                                
         SCKW  .DOWN,DOWN,(A,S)          .DOWN                                  
         SCKW  .END,END,(A,S)            .END                                   
         SCKW  .FIRST,FIRST,(A,S)        .FIRST                                 
         SCKW  .NEXT,NEXT,(A,S)          .NEXT                                  
         SCKW  .PREVIOUS,PREVIOUS,(A,S)  .PREVIOUS                              
         SCKW  .TOP,TOP,S                .TOP                                   
         SCKW  .UP,UP,S                  .UP                                    
         SCKW  .ALIGN,ALIGNV,(A,S)       .ALIGN                                 
         SCKW  .COPY,COPY,(A,S)          .COPY                                  
         SCKW  .MOVE,COPY,(A,S)          .MOVE                                  
         SCKW  .DELETE,DELETE,(A,S)      .DELETE                                
         SCKW  .FBLOCK,FBLOCK,(A,S)      .(                                     
         SCKW  .LBLOCK,LBLOCK,(A,S)      .)                                     
         SCKW  .<,FBLOCK,S               .<                                     
         SCKW  .>,LBLOCK,S               .>                                     
         SCKW  .UNMARK,UNMARK,(A,S)      .-                                     
         SCKW  .REMARK,REMARK,(A,S)      .+                                     
         SCKW  .HELP,HELPESC,(A,S)  .HELP                                       
         SCKW  .JOIN,JOIN,(A,S)          .JOIN                                  
         SCKW  .INSERT,INSERT,(A,S)      .INSERT                                
         SCKW  .COLLECT,COLLECT,(A,S)    .COLLECT                               
         SCKW  .COLMORE,COLMORE,S        .COLMORE                               
         SCKW  .CLOSE,CLOSE,(A,S)        .CLOSE                                 
         SCKW  .REVIEW,REVIEW,(A,S)      .REVIEW                                
         SCKW  .SPLIT,SPLIT,(A,S)        .SPLIT                                 
         SCKW  .OOPS,OOPS,(A,S)          .OOPS                                  
         SCKW  .COLUMN,WCOLUMN,(A,S)     .COLUMN                                
         SCKW  .PREPARE,PREPARE,(A,S)    .PREPARE                               
*                                                                               
         SCKW  .#130,BREAK,S                                                    
         SCKW  .#131,WRAP,S                                                     
         SCKW  .#132,V(SCNNOOP),S [Moved into/out of trigger row]               
         SCKW  .#133,V(SCNNOOP),S [--Unused--]                                  
         SCKW  .#134,V(SCNNOOP),S [--Unused--]                                  
         SCKW  .#135,V(SCNNOOP),S [Home pressed and no home point]              
         SCKW  .#136,V(SCNNOOP),S [--Unused--]                                  
         SCKW  .#137,V(SCNNOOP),S [--Unused--]                                  
         SCKW  .#138,V(SCNNOOP),S [Cursor in non-displayed area]                
         SCKW  .#139,V(SCNNOOP),S [Reverse break]                               
*                                                                               
         SCKW  .#140,DELL,S        [DEL L--delete line]                         
         SCKW  .#141,CLOSE,S       [DEL SPACE--delete blank lines]              
         SCKW  .#142,DELETE,S      [DEL B--delete block]                        
*                                                                               
         SCKW  .#161,REVIEW        [CLEAR key]                                  
         SCKW  ,CMDERR                                                          
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CONTINUE -- ESC RETURN Subcommand.                                           
*                                                                               
CONTINUE PROC                                                                   
         SET   CPVFEXIT            All done                                     
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BREAK -- BREAK Subcommand.                                                   
*                                                                               
BREAK    PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,^NEG),BEGIN    On a text line...                            
         IF    VWAFPOS,EXIT        (Not on a text line)                         
*-                                                                              
*-       Check to see if the user is just finishing a collect.                  
*-                                                                              
         IF    (R15,EQ,CPACNLN),BEGIN  Finishing a collect...                   
         IF    ^CPACFPG,EXIT       Not page mode collect, scram                 
*                                                                               
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get the last collected line                  
         IF    NZ,'SET CPVFBELL; B BRKEXIT'                                     
*                                                                               
         CLEAR CPACINFO            Collect has completed                        
*                                                                               
         IF    (R0,Z),BEGIN        Delete the blank line...                     
         L     R0,CPVLNO                                                        
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete line                                  
         END                                                                    
         B     BRKEXIT             All done, scram                              
         END                                                                    
         END                                                                    
*                                                                               
         SET   CPVFEXIT+CPVFBRK    Get out                                      
BRKEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  HELP -- ESC H Subcommand.                                                    
*                                                                               
HELPESC  PROC                                                                   
         MVC   CPCMDLEN,=AL2(L'HELPCMD)                                         
         MVC   CPCMD(L'HELPCMD),HELPCMD  Move in HELP command                   
         MVC   CPCMNM,=C'HEL'      Set scanner id                               
*                                                                               
         SCINIT CPCMD,LH:CPCMDLEN  Set scanner                                  
         OSCINIT CPCMD,LH:CPCMDLEN  OLD/NEW Scanner compatability               
         VCALL HELP                Go do help                                   
         B     CVNEXT              (Shouldn't happen)                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
HELPCMD  DC    C'PAGE INTRO'       (Command is "HELP PAGE INTRO")               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  UP -- ESC U Subcommand.                                                      
*                                                                               
UP       PROC                                                                   
*-                                                                              
*-       Scroll by 1/5th the number of text lines on the screen.                
*-       On a standard 24 line display this ends up being 4 lines.              
*-                                                                              
         CLEAR R0,R1                                                            
         VCALL PAGECNT             Count number of text lines                   
         LA    R15,@R15+4          (lines+4)/5                                  
         CLEAR R14                                                              
         D     R14,=F'5'                                                        
         FLOOR R15,1                                                            
         LNR   R15,R15             Negative means scroll up                     
*                                                                               
         VCALL PAGESCRL            Do it                                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DOWN -- ESC D Subcommand.                                                    
*                                                                               
DOWN     PROC                                                                   
*-                                                                              
*-       Scroll by 1/5th the number of text lines on the screen.                
*-       On a standard 24 line display this ends up being 4 lines.              
*-                                                                              
         CLEAR R0,R1                                                            
         VCALL PAGECNT             Count number of text lines                   
         LA    R15,@R15+4          (lines+4)/5                                  
         CLEAR R14                                                              
         D     R14,=F'5'                                                        
         FLOOR R15,1                                                            
*                                                                               
         VCALL PAGESCRL            Scroll down                                  
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TOP -- ESC T Subcommand.                                                     
*                                                                               
TOP      PROC                                                                   
         LT    R15,CPVLNO                                                       
         IF    NEG,BEGIN           Not on a text line...                        
         SEG   'Position the cursor to a text line '                            
         SEG   'and then press TOP [ESC T].'                                    
         EXIT  TOP                                                              
         END                                                                    
*-                                                                              
*-       Limit the request to existing lines.                                   
*-                                                                              
         L     R15,CPVLNO                                                       
         FLOOR R15,CPAFLNO         Not before first lineno...                   
         CEIL  R15,CPALLNO         ...or after last lineno                      
*                                                                               
         CLEAR R0                  We don't want lines before this              
         VCALL PAGETOP             Put the line at the top                      
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BOTTOM -- ESC B Subcommand.                                                  
*                                                                               
BOTTOM   PROC                                                                   
         LT    R15,CPVLNO                                                       
         IF    NEG,BEGIN           Not on a text line...                        
         SEG   'Position the cursor to a text line '                            
         SEG   'and then press BOTTOM [ESC B].'                                 
         EXIT  BOTTOM                                                           
         END                                                                    
*-                                                                              
*-       Limit the request to existing lines.                                   
*-                                                                              
         L     R15,CPVLNO                                                       
         FLOOR R15,CPAFLNO         Not before first lineno...                   
         CEIL  R15,CPALLNO         ...or after last lineno                      
*                                                                               
         CLEAR R0                  We don't want lines after this               
         VCALL PAGEBOT             Put the line at the bottom                   
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FIRST -- ESC F Subcommand.                                                   
*                                                                               
FIRST    PROC                                                                   
         VCALL PAGECLR             Clear the current window                     
*                                                                               
         MVC   CPVLNO,CPAFLNO      Set cursor lineno                            
         CLEAR CPVCOL              Calculate column later                       
*                                                                               
         IF    (R15,NZ),BEGIN      There is an ACTIVE file...                   
         LR    R15,R0                                                           
         VCALL PAGELINE            Put the first line on the page               
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  END -- ESC E Subcommand.                                                     
*                                                                               
END      PROC                                                                   
         L     R4,CPALLNO          Save LAST lineno for below                   
*                                                                               
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         VCALL PAGECLR             Clear the current window                     
         LR    R2,R15                                                           
         SH    R2,=H'4'            No. of lines to backup                       
*                                                                               
         ST    R4,CPLCNO           Last line in file                            
         WHILE (R2,POS),'VCALL PREVLNO; DECR R2'  Backup                        
*                                                                               
         L     R15,CPLCNO          First line in window                         
         VCALL PAGEMAKE            Build a new page                             
         END                                                                    
*                                                                               
         LA    R15,1                                                            
         AR    R15,R4              Last lineno plus one                         
         ST    R15,CPVLNO          Set cursor lineno                            
         CLEAR CPVCOL              Calculate column later                       
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PREVIOUS -- ESC P Subcommand.                                                
*                                                                               
PREVIOUS PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         L     R3,WINDFLNO         First line in window                         
         VCALL PAGECLR             Clear the current window                     
         LR    R2,R15              No. of lines in window                       
*                                                                               
         ST    R3,CPLCNO           First lineno for the new page                
         SH    R2,=H'2'            Allow two lines slop                         
         WHILE (R2,POS),BEGIN      Back up...                                   
         VCALL PREVLNO                                                          
         DECR  R2                                                               
         END                                                                    
*                                                                               
         SET   WINFREWR            Rewrite the window                           
         END                                                                    
*                                                                               
         L     R15,CPLCNO                                                       
         VCALL PAGEMAKE            Build a new page                             
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NEXT -- ESC N Subcommand.                                                    
*                                                                               
NEXT     PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         L     R2,WINDLLNO         Last line on current page                    
*                                                                               
         ST    R2,CPLCNO                                                        
         VCALL PREVLNO             Keep the last two lines                      
*                                                                               
         SET   WINFREWR            Rewrite the window                           
*                                                                               
         L     R15,CPLCNO                                                       
         VCALL PAGEMAKE            Build a new page                             
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COLLECT -- ESC SPACE Subcommand.                                             
*                                                                               
COLLECT  PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SET   CPVFBELL                                                         
         B     COLEXIT                                                          
         END                                                                    
*-                                                                              
*-       Figure out where to start the collect.                                 
*-                                                                              
         IF    (CPVCOL,GT,1),BEGIN  Is cursor past eol...                       
         L     R15,CPVLNO                                                       
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get the current line                         
         IF    NZ,BEGIN            Line doesn't exist (odd)...                  
         SET   CPVFBELL                                                         
         B     COLEXIT                                                          
         END                                                                    
*                                                                               
         IF    ((R0,NZ),AND,(CPVCOL,GT,R0)),BEGIN  Past eol...                  
         L     R15,CPVLNO                                                       
         A     R15,CVONE                                                        
         VCALL GETLNO              Get the next line                            
         IF    NEG,EXIT            Trouble, scram                               
*                                                                               
         MVC   CPVLNO,CPLCNO       Start at next line                           
         CLEAR CPVCOL              Calculate column later                       
         END                                                                    
         END                                                                    
*-                                                                              
*-       Split the current line if neccessary.                                  
*-                                                                              
         L     R15,CPVLNO          Current lineno                               
*                                                                               
         LH    R0,CPVCOL                                                        
         IF    (R0,GT,1),BEGIN     Need to split line...                        
         VCALL CHOPLINE            Split line                                   
         IF    NZ,BEGIN            Problems (shouldn't happen)...               
         SET   CPVFBELL                                                         
         B     COLEXIT                                                          
         END                                                                    
         LR    R15,R1              Current lineno (perhaps renum)               
         END                                                                    
*-                                                                              
*-       Calculate starting collect line-number and delta.                      
*-                                                                              
         CLEAR CPACINFO            Reset collect info                           
         SET   CPACFPG             Collecting in page mode                      
*                                                                               
         ST    R15,CPLCNO          We want to collect before this               
         VCALL PREVLNO             Get previous lineno                          
         IF    NZ,BEGIN            No previous (first in file)...               
         LA    R0,1                                                             
         L     R15,CPLCNO                                                       
         VCALL CHOPLINE            Split existing line at col 1                 
         IF    NZ,BEGIN            Problems (shouldn't happen)...               
         SET   CPVFBELL                                                         
         B     COLEXIT                                                          
         END                                                                    
         ST    R0,CPACFLN          First line                                   
         ST    R0,CPACNLN          Current collect line                         
         MVC   CPACDEL,CPDELTA     Delta                                        
         END                                                                    
*-                                                                              
*-       Make a new line.                                                       
*-                                                                              
         ELSE  BEGIN                                                            
         L     R15,CPLCNO                                                       
         LA    R0,5                Choose delta for 5 new lines                 
         VCALL CHKLINES                                                         
         ST    R15,CPACFLN         First existing lineno                        
         ST    R1,CPACDEL          Delta                                        
         AR    R15,R1                                                           
         ST    R15,CPACNLN         Current collect line                         
*                                                                               
         SETMSG VWAPLINE,0                                                      
         VCALL VPUTLINE            Add a blank line                             
         END                                                                    
*                                                                               
         MVC   CPVLNO,CPACNLN      Set cursor lineno                            
         CLEAR CPVCOL              Calculate column later                       
*                                                                               
         MVC   VWACHKL,CPVLNO      Make sure it's on the page                   
         SET   VWAFLBOT            Can't be too close to bottom                 
         SET   CPVFCH              File changes have occurred                   
COLEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COLMORE -- Automatic Collect.                                                
*                                                                               
COLMORE  PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SET   CPVFBELL                                                         
         B     COLMEXIT                                                         
         END                                                                    
*-                                                                              
*-       Find the next line-number.                                             
*-                                                                              
         A     R15,CVONE           Next line-number                             
         VCALL GETLNO                                                           
         IF    NEG,BEGIN           Past end of file...                          
         CLEAR CPACINFO            Turn off collect                             
         VCALL ENDLNO              End line-number                              
         IF    NEG,'SET CPVFBELL; B COLMEXIT'                                   
         LR    R15,R0                                                           
         END                                                                    
*                                                                               
         ELSE  BEGIN               Add a new line...                            
         L     R15,CPACNLN                                                      
         A     R15,CPACDEL                                                      
         IF    (R15,LT,CPLCNO),'ST R15,CPLCNO'  Old delta is fine               
         ELSE  BEGIN               Pick a new delta...                          
         LA    R0,5                Allow room for 5 lines                       
         L     R15,CPACNLN                                                      
         VCALL CHKLINES            Find an acceptable delta                     
         ST    R15,CPACFLN         New first line collected                     
         AR    R15,R1                                                           
         END                                                                    
         ST    R15,CPACNLN         Set current collect line                     
         END                                                                    
*-                                                                              
*-       Add a blank line as the next line to be collected.                     
*-                                                                              
         ST    R15,CPVLNO          Set new cursor line                          
         CLEAR CPVCOL              Calculate column later                       
*                                                                               
         SETMSG VWAPLINE,0                                                      
         VCALL VPUTLINE            Add the blank line                           
*                                                                               
         MVC   VWACHKL,CPVLNO      Make sure line is on the page                
         SET   VWAFLBOT            Can't be too close to bottom                 
         SET   CPVFCH              File changes have occurred                   
COLMEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CLOSE -- DEL SPACE Subcommand.                                               
*                                                                               
CLOSE    PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SET   CPVFBELL                                                         
         B     CLOSEXIT                                                         
         END                                                                    
*                                                                               
         LA    R1,VWAGLINE         Put line text here                           
         VCALL GETLINE             Current line                                 
         IF    Z,BEGIN             Delete this too if it's blank...             
         IF    (R0,Z),CLOSECOM     It's blank, go delete it                     
         END                                                                    
*-                                                                              
*-   Delete all following blank lines until the next non-blank line.            
*-                                                                              
         CLEAR R2                  Flag: nothing deleted                        
         LOOP  BEGIN                                                            
         L     R15,CPLCNO                                                       
         A     R15,CVONE                                                        
         LA    R1,VWAGLINE         Put line text here                           
         VCALL GETLINE             Find next line                               
         IF    NEG,EXIT            End of file, scram                           
         IF    (R0,NZ),EXIT        Stop if line is non-blank                    
*                                                                               
CLOSECOM L     R0,CPLCNO           Line-number to be deleted                    
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete line                                  
         LA    R2,1                Flag: lines have been deleted                
         END                                                                    
*                                                                               
         IF    (R2,NZ),'SET CPVFCH'  File changes occurred                      
CLOSEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DELL -- DEL L Subcommand.                                                    
*                                                                               
DELL     PROC                                                                   
         L     R0,CPVLNO                                                        
         IF    (R0,NEG),'SET CPVFBELL; B DELLEXIT'                              
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete the cursor line                       
         IF    NZ,'SET CPVFBELL; B DELLEXIT'                                    
*                                                                               
         SET   CPVFCH              File changes have occurred                   
DELLEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OOPS -- ESC O Subcommand.                                                    
*                                                                               
OOPSWA   RECORD BEGIN                                                           
OOPSKEY  DS    XL16                Previous lineno key in CPRG16                
         END                                                                    
*-                                                                              
OOPS     PROC  OOPSWA                                                           
*-                                                                              
*-       OOPS means BREAK for VTAM terminals.                                   
*-                                                                              
         IF    CPVFVTAM,BEGIN   Vtam's PF9 key is different...                  
         ACALL BREAK                                                            
         EXIT  OOPS                                                             
         END                                                                    
*-                                                                              
*-       Regular OOPS command.                                                  
*-                                                                              
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SEG   'Position the cursor to the text line you '                      
         SEG   'want to undo first for OOPS [ESC O] function.'                  
         SET   CPVFBELL                                                         
         EXIT  OOPS                                                             
         END                                                                    
*-                                                                              
*-       Now get previous copy of line text.                                    
*-                                                                              
         LA    R0,CP#OOPS          Old lines are kept here                      
         LA    R15,CPRG16          Misc record group                            
         VCALL XSETSET                                                          
*                                                                               
         CLEAR OOPSKEY                                                          
         MVC   OOPSKEY(4),CPACTNO                                               
         MVC   OOPSKEY+4(4),CPVLNO                                              
         LA    R2,OOPSKEY                                                       
         SETMSG VWAPLINE            Working text area                           
         LA    R15,CPRG16                                                       
         VCALL XGET                Get previous line text                       
         IF    (R15,NZ),BEGIN      No previous text...                          
         SEG   'No previous copy of this line.'                                 
         SET   CPVFBELL                                                         
         EXIT  OOPS                                                             
         END                                                                    
*-                                                                              
*-   Save old text by using the standard page-mode VPUTLINE routine.            
*-                                                                              
         L     R15,CPVLNO          Line-number                                  
         SETMSG (R1),(R0)           New line text                               
         VCALL VPUTLINE            Save line text                               
*                                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FBLOCK -- ESC ( and ESC < Subcommands.                                       
*                                                                               
FBLOCK   PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SET   CPVFBELL                                                         
         B     FBKEXIT                                                          
         END                                                                    
*-                                                                              
*-       Reset block if it's in a different Active file.                        
*-                                                                              
         IF    (CPACTNO,NE,CPBLKACT),BEGIN  Different Active file...            
         IF    (CPBLKACT,Z),EXIT                                                
         CLEAR CPVFHIDE,CPBLKACT   Reset block                                  
         SET   CPVFMARK            Undo displayed marks                         
         END                                                                    
*-                                                                              
*-       Reset block if it's hidden.                                            
*-                                                                              
         IF    CPVFHIDE,BEGIN      Clear previous hidden block...               
         CLEAR CPVFHIDE,CPBLKACT   Reset block                                  
         END                                                                    
*                                                                               
*                                                                               
         ST    R15,CPFBKLNO        Save FBLOCK lineno                           
         LH    R0,CPVCOL                                                        
         FLOOR R0,1                                                             
         IF    (CPCMNM,EQ,'.<'),'LA R0,1'                                       
         STH   R0,CPFBKCOL         Save FBLOCK column                           
*-                                                                              
*-       Set LBLOCK if appropriate.                                             
*-                                                                              
         IF    (CPBLKACT,Z),FBKSET  New block defn, go set LBLOCK               
*                                                                               
         IF    (R15,GE,CPLBKLNO),BEGIN  Better set LBLOCK...                    
         IF    ((R15,EQ,CPLBKLNO),AND,                                 *        
               ((CPLBKCOL,Z),OR,(R0,LE,CPLBKCOL))),EXIT                         
FBKSET   ST    R15,CPLBKLNO        Set LBLOCK lineno                            
         CLEAR CPLBKCOL            Set column too                               
         END                                                                    
*                                                                               
         MVC   CPBLKACT,CPACTNO    Save Active file number                      
*                                                                               
         SET   CPVFMARK                                                         
FBKEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LBLOCK -- ESC ) and ESC > Subcommands.                                       
*                                                                               
LBLOCK   PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SET   CPVFBELL                                                         
         B     LBKEXIT                                                          
         END                                                                    
*-                                                                              
*-       Reset block if it's in a different Active file.                        
*-                                                                              
         IF    (CPACTNO,NE,CPBLKACT),BEGIN  Different Active file...            
         IF    (CPBLKACT,Z),EXIT                                                
         CLEAR CPVFHIDE,CPBLKACT   Reset block                                  
         SET   CPVFMARK            Undo displayed marks                         
         END                                                                    
*-                                                                              
*-       Reset block if it's hidden.                                            
*-                                                                              
         IF    CPVFHIDE,BEGIN      Clear previous hidden block...               
         CLEAR CPVFHIDE,CPBLKACT   Reset block                                  
         END                                                                    
*                                                                               
*                                                                               
         MVC   CPBLKACT,CPACTNO    Save Active file number                      
         L     R15,CPVLNO                                                       
         ST    R15,CPLBKLNO        Save LBLOCK lineno                           
         LH    R0,CPVCOL                                                        
         FLOOR R0,1                                                             
         IF    (CPCMNM,EQ,'.>'),'CLEAR R0'                                      
         STH   R0,CPLBKCOL         Save LBLOCK column                           
*-                                                                              
*-       Set FBLOCK if appropriate.                                             
*-                                                                              
         IF    (CPBLKACT,Z),LBKSET  New block defn, go set FBLOCK               
*                                                                               
         IF    (R15,LE,CPFBKLNO),BEGIN  Better set FBLOCK...                    
         IF    (R0,Z),'LA R0,&LINESZ'  For last column check                    
         IF    ((R15,EQ,CPFBKLNO),AND,(R0,GE,CPFBKCOL)),EXIT                    
LBKSET   ST    R15,CPFBKLNO        Set FBLOCK lineno                            
         MVC   CPFBKCOL,=H'1'      Set column too                               
         END                                                                    
*                                                                               
         MVC   CPBLKACT,CPACTNO    Save Active file number                      
*                                                                               
         SET   CPVFMARK                                                         
LBKEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COPY -- ESC C and ESC M Subcommands.                                         
*                                                                               
COPYWA   RECORD BEGIN                                                           
COPYFLAG FLAG                                                                   
         FLAG  COPYFSPL            - We split the target line                   
*                                                                               
COPYLEN  DS    H                   Working block length                         
*                                                                               
COPYCNT  DS    F                   Number of lines in block                     
COPYTLNO DS    F                   Original target line-number                  
COPYWLNO DS    F                   Working line-number                          
COPYDEL  DS    F                   Working delta                                
         END                                                                    
*-                                                                              
MOVE     PROC                                                                   
         MVC   CPCMNM,=C'.M '      Indicate move operation                      
         ACALL COPY                                                             
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
COPY     PROC  COPYWA                                                           
         CLEAR COPYWA                                                           
*-                                                                              
*-       First make sure there is a block defined.                              
*-                                                                              
         IF    (CPVFHIDE,OR,(CPBLKACT,Z)),BEGIN                                 
COPYNULL SEG   'No block defined to '                                           
         SETMSG 'copy.'                                                         
         IF    (CPCMNM,EQ,'.M'),'SETMSG "move."'                                
         SEG   (R1),(R0)                                                        
         SET   CPVFBELL                                                         
         B     COPYRES                                                          
         END                                                                    
*                                                                               
         IF    ('LT R15,CPVLNO',NEG),BEGIN  Not on a text line...               
         SEG   'First position the cursor and the press '                       
         SETMSG 'COPY [ESC C] to copy the block.'                               
         IF    (CPCMNM,EQ,'.M'),BEGIN                                           
         SETMSG 'MOVE [ESC M] to move the block.'                               
         END                                                                    
         SEG   (R1),(R0)                                                        
         SET   CPVFBELL                                                         
         B     COPYEXIT                                                         
         END                                                                    
*-                                                                              
*-       Set up the block range.                                                
*-                                                                              
         MVC   VWAGCOL,CPFBKCOL                                                 
         MVC   VWAGLNO,CPFBKLNO                                                 
*                                                                               
         IF    (CPLBKCOL,Z),BEGIN  Calculate ending column now...               
         L     R15,CPLBKLNO                                                     
         LA    R1,VWAGLINE                                                      
         ACALL BLKGLINE            Get LBLOCK line length                       
         IF    (R0,LT,&LINESZ),'A R0,CVONE'  One trailing space                 
         STH   R0,CPLBKCOL                                                      
         END                                                                    
*-                                                                              
*-       Make sure the copy/move doesn't overlap.                               
*-                                                                              
         IF    (CPBLKACT,EQ,CPACTNO),BEGIN  Same Active file...                 
         L     R15,CPVLNO                                                       
         IF    ((R15,LT,CPFBKLNO),OR,(R15,GT,CPLBKLNO)),EXIT                    
         IF    ((R15,EQ,CPFBKLNO),AND,(CPVCOL,LE,CPFBKCOL)),EXIT                
         IF    ((R15,EQ,CPLBKLNO),AND,(CPVCOL,GT,CPLBKCOL)),EXIT                
         SEG   'You can''t '                                                    
         SETMSG 'copy'                                                          
         IF    (CPCMNM,EQ,'.M'),'SETMSG "move"'                                 
         SEGB  (R1),(R0)                                                        
         SEG   'a block into itself!'                                           
         SET   CPVFBELL                                                         
         B     COPYEXIT                                                         
         END                                                                    
*-                                                                              
*-       Process a one line block.                                              
*-                                                                              
         IF    (CPFBKLNO,EQ,CPLBKLNO),BEGIN  Simple...                          
*                                                                               
         IF    (CPVCOL,LE,1),BEGIN  Check for entire line copy...               
         IF    (CPFBKCOL,NE,1),EXIT  Nope, forget it                            
         L     R15,CPFBKLNO                                                     
         LA    R1,VWAGLINE                                                      
         ACALL BLKGLINE            Get the block line                           
         BNZ   COPYNULL            Trouble, scram                               
         IF    (CPLBKCOL,GE,R0),COPYMULT  Entire line, do mult copy             
         END                                                                    
*                                                                               
         ACALL BLKGET              Get the block                                
         BNZ   COPYNULL            Trouble, scram                               
*                                                                               
         STH   R0,COPYLEN                                                       
         LR    R5,R1                                                            
         LR    R4,R0                                                            
*                                                                               
         L     R15,CPVLNO                                                       
         LA    R1,VWAPLINE                                                      
         VCALL GETLINE             Get the target line                          
         BNZ   COPYNULL                                                         
         FLOOR R0,CPVCOL           Extend line to cursor                        
         LR    R2,R0                                                            
         AR    R0,R4               New total line length                        
         IF    (R0,GT,&LINESZ),EXIT  Handle as a complex block                  
*                                                                               
         LR    R3,R1                                                            
         LH    R15,CPVCOL                                                       
         DECR  R15                                                              
         AR    R3,R15              Text after block ptr                         
         SR    R2,R15                                                           
*                                                                               
         MOVE  R2,@R13,@R3         Save text after insertion point              
         LA    R15,@R3(R4)                                                      
         MOVE  R4,@R3,@R5          Add block text                               
         TEX   R2,'MVC @R15(0),@R13'  Restore remainder of line                 
*                                                                               
         L     R15,CPVLNO                                                       
         VCALL PUTLINE             Replace the line                             
*                                                                               
         MVC   COPYWLNO,CPFBKLNO   Save FBLOCK lineno (before del)              
*-                                                                              
*-       Keep block position accurate.                                          
*-                                                                              
         IF    (CPVLNO,EQ,CPFBKLNO),BEGIN  On the same line...                  
         IF    (CPBLKACT,NE,CPACTNO),EXIT  Different Active, scram              
         IF    (CPVCOL,GT,CPLBKCOL),EXIT  New text is past block                
         LH    R15,CPFBKCOL                                                     
         AH    R15,COPYLEN                                                      
         STH   R15,CPFBKCOL        New starting column                          
*                                                                               
         LH    R15,CPLBKCOL                                                     
         AH    R15,COPYLEN                                                      
         STH   R15,CPLBKCOL        New ending column                            
         END                                                                    
*-                                                                              
*-       Delete the old block (if move).                                        
*-                                                                              
         IF    (CPCMNM,EQ,'.M'),BEGIN  Move...                                  
         ACALL DELETE              First delete the block                       
*                                                                               
         MVC   CPBLKACT,CPACTNO    Block is now the destination                 
         MVC   CPFBKLNO,CPVLNO                                                  
         MVC   CPLBKLNO,CPVLNO                                                  
         LH    R15,CPVCOL                                                       
         FLOOR R15,1                                                            
         STH   R15,CPFBKCOL        Set appropriate columns                      
         AH    R15,COPYLEN                                                      
         DECR  R15                                                              
         STH   R15,CPLBKCOL                                                     
         END                                                                    
*                                                                               
         MVC   VWACHKL,CPVLNO      Make sure line is on page                    
*                                                                               
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         IF    WINFWRAP,BEGIN      Do any wordwrapping...                       
         IF    ((CPCMNM,EQ,'.M'),AND,(CPVLNO,EQ,COPYWLNO)),EXIT                 
         ACALL WRAP                Do wordwrapping if needed                    
         END                                                                    
         END                                                                    
         B     COPYRES                                                          
         END                                                                    
*-                                                                              
*-       Process a multi-line block.                                            
*-                                                                              
COPYMULT L     R15,CPVLNO                                                       
         LH    R0,CPVCOL                                                        
         IF    (R0,GT,1),BEGIN     First split the line...                      
         VCALL CHOPLINE                                                         
         LR    R15,R0              Original line-number                         
         SET   COPYFSPL            Remember that we split it                    
         END                                                                    
         ELSE  BEGIN                                                            
         ST    R15,CPLCNO                                                       
         VCALL PREVLNO             Get line before cursor                       
         IF    NZ,BEGIN            No previous line...                          
         IF    (R15,EQ,-8),'SET CPVFBELL; B COPYEXIT'  Line zero!               
         L     R1,CPLCNO                                                        
         VCALL SELDELTA            Amount to backup                             
         L     R15,CPLCNO          First lineno                                 
         SR    R15,R1              Calclated target lineno                      
         END                                                                    
         ELSE  'L R15,CPLCNO'      Target lineno                                
         END                                                                    
*                                                                               
         ST    R15,COPYTLNO        Save target line-number                      
         ST    R15,COPYWLNO        Save working (first) line-number             
         MVC   CPVLNO,=F'-1000'    Cursor pos will be set later                 
*-                                                                              
*-       Find out how many lines there are in the block.                        
*-                                                                              
         L     R15,CPBLKACT                                                     
         VCALL ACTPICK             Destination Active file                      
         LR    R2,R15                                                           
*                                                                               
         L     R0,CPFBKLNO                                                      
         L     R1,CPLBKLNO                                                      
         VCALL CNTRANGE            Get the line count                           
         ST    R15,COPYCNT                                                      
*                                                                               
         LR    R15,R2                                                           
         VCALL ACTPICK             Switch back                                  
*                                                                               
         IF    (COPYCNT,Z),'SET CPVFBELL; B COPYEXIT'  (Odd)                    
*-                                                                              
*-       Get a starting line-number and appropriate delta.                      
*-                                                                              
         L     R15,COPYWLNO                                                     
         L     R0,COPYCNT                                                       
         VCALL CHKLINES            Get starting lineno and delta                
         ST    R15,COPYWLNO                                                     
         ST    R1,COPYDEL                                                       
*                                                                               
         MVC   VWAGLNO,CPFBKLNO    Reset (could have been renum)                
*-                                                                              
*-       Copy the block.                                                        
*-                                                                              
         WHILE ('ACALL BLKGET',Z),BEGIN  Copy the block...                      
         L     R15,COPYWLNO                                                     
         A     R15,COPYDEL         Next lineno to use                           
         ST    R15,COPYWLNO                                                     
*                                                                               
         IF    (CPVLNO,EQ,-1000),BEGIN  Assign cursor line...                   
         ST    R15,CPVLNO          Set cursor lineno                            
         CLEAR CPVCOL              Calculate cursor column                      
         END                                                                    
*                                                                               
         VCALL PUTLINE             Add the line                                 
         END                                                                    
*-                                                                              
*-       If it's a move, finish up by deleting the block.                       
*-                                                                              
         IF    (CPCMNM,EQ,'.M'),BEGIN  Move...                                  
         ACALL DELETE              Delete the old block                         
*                                                                               
         MVC   CPBLKACT,CPACTNO    Block is now the destination                 
         MVC   CPFBKLNO,CPVLNO                                                  
         MVC   CPFBKCOL,=H'1'                                                   
         MVC   CPLBKLNO,COPYWLNO                                                
         CLEAR CPLBKCOL                                                         
         END                                                                    
*-                                                                              
*-       Try to join the cursor line to the first line copied.                  
*-                                                                              
         IF    COPYFSPL,BEGIN      Only if we split the line...                 
         CLEAR R0                                                               
         LA    R1,&LINESZ                                                       
         L     R15,COPYTLNO                                                     
         VCALL JOINLINE            Join lines                                   
         IF    NZ,EXIT             Problems, scram                              
*                                                                               
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         IF    WINFWRAP,BEGIN      Do any wordwrapping...                       
         ACALL WRAP                Wrap line if needed                          
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Finally, undefine the block.                                           
*-                                                                              
COPYRES  SET   CPVFCH              File changes have occurred                   
         ACALL UNMARK              Undefine the block                           
COPYEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DELETE -- DEL B Subcommand.                                                  
*                                                                               
DELWA    RECORD BEGIN                                                           
DELOLDAN DS    F                   Previous Active file number                  
         END                                                                    
*-                                                                              
DELETE   PROC  DELWA                                                            
*-                                                                              
*-       First make sure there is a block defined.                              
*-                                                                              
         IF    (CPVFHIDE,OR,(CPBLKACT,Z)),BEGIN                                 
DELNULL  SEG   'No block defined to delete.'                                    
         SET   CPVFBELL                                                         
         B     DELRESET                                                         
         END                                                                    
*-                                                                              
*-       Switch into the marked block Active file.                              
*-                                                                              
         MVC   DELOLDAN,CPACTNO    Save previous Active file number             
         IF    (CPACTNO,NE,CPBLKACT),BEGIN  Switch Actives...                   
         L     R15,CPBLKACT        New Active file number                       
         VCALL ACTPICK                                                          
         ST    R15,DELOLDAN        Save previous Active file number             
         END                                                                    
*                                                                               
*                                                                               
         IF    (CPLBKCOL,Z),'MVC CPLBKCOL,=AL2(&LINESZ)'                        
*-                                                                              
*-      Move the cursor position if it is in the area to be deleted.            
*-                                                                              
         L     R15,CPVLNO          Cursor lineno                                
         IF    (CPACTNO,EQ,CPBLKACT),BEGIN  Same Active file...                 
         IF    ((R15,GE,CPFBKLNO),AND,(R15,LE,CPLBKLNO)),BEGIN                  
         IF    ((R15,EQ,CPFBKLNO),AND,(CPVCOL,LE,CPFBKCOL)),EXIT                
         IF    ((R15,EQ,CPLBKLNO),AND,(CPVCOL,GT,CPLBKCOL)),BEGIN               
         LH    R0,CPVCOL                                                        
         LH    R15,CPLBKCOL                                                     
         IF    (CPFBKLNO,EQ,CPLBKLNO),'SH R15,CPFBKCOL; LA R15,@R15+1'          
         SR    R0,R15              Adjust cursor column position                
         STH   R0,CPVCOL                                                        
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         MVC   CPVLNO,CPFBKLNO     New cursor line                              
         MVC   CPVCOL,CPFBKCOL     ...and column                                
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Process a one line block.                                              
*-                                                                              
         IF    (CPFBKLNO,EQ,CPLBKLNO),BEGIN  Simple...                          
         L     R15,CPFBKLNO                                                     
         LA    R1,VWAPLINE                                                      
         VCALL GETLINE             Get target line                              
         BNZ   DELNULL                                                          
         FLOOR R0,CPLBKCOL         Extend line to end of block                  
*                                                                               
         LH    R4,CPLBKCOL                                                      
         LH    R3,CPFBKCOL                                                      
*                                                                               
         LR    R5,R0                                                            
         SR    R5,R4               Size of line after block                     
*                                                                               
         LR    R2,R4                                                            
         SR    R2,R3                                                            
         LA    R2,@R2+1            Size of block                                
         IF    ((R3,EQ,1),AND,(R2,GE,R0)),EXIT  Delete line                     
*                                                                               
         SR    R0,R2               Reduce line length appropriately             
*                                                                               
         LA    R3,@R1(R3)                                                       
         DECR  R3                  Start of block ptr                           
         LA    R4,@R1(R4)          Text after block ptr                         
         MOVE  R5,@R3,@R4          Delete the text                              
*                                                                               
         L     R15,CPLCNO          Line-number                                  
         VCALL VPUTLINE            Replace the line                             
         B     DELDONE                                                          
         END                                                                    
*-                                                                              
*-       Process a multi-line block.                                            
*-                                                                              
         L     R5,CPFBKLNO                                                      
         IF    (CPFBKCOL,NE,1),BEGIN  Need to delete partial line...            
         LR    R15,R5                                                           
         LA    R1,VWAPLINE                                                      
         VCALL GETLINE             Get FBLOCK line                              
         IF    NZ,EXIT             Not found, scram                             
         IF    (R0,GT,CPFBKCOL),BEGIN  Need to truncate line...                 
         LH    R0,CPFBKCOL                                                      
         DECR  R0                                                               
         L     R15,CPLCNO          Line-number                                  
         VCALL VPUTLINE            Replace the line                             
         END                                                                    
         A     R5,CVONE            Next line                                    
         END                                                                    
*                                                                               
         LR    R0,R5               R0 = first line to delete                    
         L     R1,CPLBKLNO                                                      
         DECR  R1                  R1 = last line-1 to delete                   
         VCALL DELRANGE            Delete range of lines                        
*                                                                               
         L     R15,CPLBKLNO                                                     
         LA    R1,VWAPLINE                                                      
         VCALL GETLINE             Get the last line                            
         IF    Z,BEGIN             We got it...                                 
         LH    R2,CPLBKCOL                                                      
         SR    R0,R2               New line length                              
         IF    ((R2,Z),OR,(R0,NP)),BEGIN  Delete the entire line...             
         L     R0,CPLBKLNO         Line-number to delete                        
         LR    R1,R0                                                            
         VCALL DELRANGE            Delete line                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN               Partial delete of last line...               
         LA    R3,@R1(R2)          Text after block                             
         LR    R15,R0                                                           
         MOVE  R15,@R1,@R3                                                      
         L     R15,CPLCNO                                                       
         VCALL VPUTLINE            Replace the line                             
         END                                                                    
         END                                                                    
*-                                                                              
*-       Switch into the marked block Active file.                              
*-                                                                              
DELDONE  L     R15,DELOLDAN        Previous Active file number                  
         VCALL ACTPICK                                                          
*                                                                               
         CLEAR CPBLKACT            Reset block                                  
         SET   CPVFMARK            Block marks have changed                     
         SET   CPVFCH              File changes have occurred                   
*                                                                               
DELRESET ACALL UNMARK              Remove block definition                      
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REMARK -- ESC + Subcommand.                                                  
*                                                                               
REMARK   PROC                                                                   
         IF    (CPBLKACT,Z),BEGIN  No block defined...                          
         SEG   'No block is defined.'                                           
         SET   CPVFBELL                                                         
         B     REBLEXIT                                                         
         END                                                                    
*                                                                               
         IF    ^CPVFHIDE,BEGIN     Block is not hidden...                       
         SEG   'Block is not hidden.'                                           
         SET   CPVFBELL                                                         
         B     REBLEXIT                                                         
         END                                                                    
*                                                                               
         CLEAR CPVFHIDE                                                         
         SET   CPVFMARK            Block needs to be shown                      
REBLEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  UNMARK -- ESC - Subcommand.                                                  
*                                                                               
UNMARK   PROC                                                                   
         IF    (^CPVFHIDE,AND,(CPBLKACT,NZ)),BEGIN                              
         SET   CPVFHIDE,CPVFMARK   Hide the block                               
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BLKGET -- Local routine to get the next line in a block.                     
*                                                                               
*    On entry:                                                                  
*      VWA contains block range information                                     
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok; R1,R0 - text loc, len                                            
*      4 - no more text                                                         
*                                                                               
BLKGET   PROC  (R2,LSR)                                                         
         L     R15,VWAGLNO                                                      
         IF    (R15,GT,CPLBKLNO),GETBLEOF  Already done, scram                  
         LA    R1,VWAGLINE                                                      
         ACALL BLKGLINE            Get the next line                            
         STH   R0,VWAGLEN          Save total line length                       
         IF    NZ,'MVC VWAGCOL,=H"1"'                                           
         MVC   VWAGLNO,CPLCNO      Current lineno                               
         IF    (VWAGLNO,GE,CPLBKLNO),BEGIN  Last line...                        
         IF    (VWAGLNO,GT,CPLBKLNO),GETBLEOF  All done                         
         IF    (CPLBKCOL,Z),BEGIN  Entire line plus a space...                  
         IF    (R0,LT,&LINESZ),'A R0,CVONE'  A trailing space                   
         END                                                                    
         ELSE  'LH R0,CPLBKCOL'    Specific length                              
         END                                                                    
*                                                                               
         LH    R15,VWAGCOL         First text column                            
         DECR  R15                                                              
         IF    (R15,NZ),BEGIN      Select appropriate cols...                   
         LA    R2,VWAGLINE(R15)                                                 
         SR    R0,R15                                                           
         IF    NEG,'CLEAR R0'                                                   
         LR    R15,R0                                                           
         MOVE  R15,VWAGLINE,@R2    Move text over                               
         END                                                                    
*                                                                               
         INCR  R15,VWAGLNO         Kick range ptr                               
         MVC   VWAGCOL,=H'1'       Reset to first column                        
*                                                                               
         CLEAR R15                 Set rc                                       
         B     GETBLXIT                                                         
*                                                                               
GETBLEOF CLEAR R0,R1                                                            
         LA    R15,4                                                            
GETBLXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BLKGLINE -- Local routine to get a line of text from the                     
*    marked block Active file.                                                  
*                                                                               
*    Entry/exit paramaters are the same as the GETLINE routine.                 
*                                                                               
BLKGLINE PROC                                                                   
*-                                                                              
*-       Get the line text.                                                     
*-                                                                              
         L     R2,CPBLKACT         Active number of marked lines                
         VCALL GETLINEX            Get line text                                
         PRETURN (R0,R1)           Return                                       
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REVIEW -- ESC V Subcommand.                                                  
*                                                                               
REVIEW   PROC                                                                   
         SET   CPVFCLR             Clear and rewrite page                       
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RESET -- ESC ??? Subcommand.                                                 
*                                                                               
RESET    PROC                                                                   
         VCALL PAGEINIT            Re-initialize page                           
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GUESS -- ESC G Subcommand.                                                   
*                                                                               
GSWA     RECORD BEGIN                                                           
GSRESP   DS    XL128               Guess response words                         
         END                                                                    
*-                                                                              
GUESS    PROC  GSWA                                                             
*                                                                               
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SEG   'Press GUESS [ESC G] for the possible spelling '                 
         SEG   'of a word.'                                                     
         SET   CPVFBELL                                                         
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get line text                                
         IF    NZ,BEGIN                                                         
GSERR    SEG   'Position cursor to the start of a word '                        
         SEG   'for GUESS [ESC G].'                                             
         SET   CPVFBELL                                                         
         B     GSEXIT                                                           
         END                                                                    
         LH    R15,CPVCOL                                                       
         VCALL GETWD               Isolate word                                 
         IF    (R0,NP),GSERR       Can't isolate word, forget it                
*                                                                               
         LR    R3,R1               Save word loc...                             
         LR    R2,R0               ...and length                                
*-                                                                              
*-       Guess the correct spelling of the word in R1,R0.                       
*-                                                                              
         LA    R15,GSRESP          Put response words here                      
         VCALL GUESSWD             Guess spelling                               
*                                                                               
         IF    (R15,NZ),BEGIN      Some sort of error...                        
         IF    (R15,EQ,4),BEGIN    Spelled correctly...                         
         SEGB  (R3),(R2)                                                        
         SEG   'is spelled correctly.'                                          
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,8),BEGIN    No similar words...                          
         SEG   'No similar words found.'                                        
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,12),BEGIN   Too many similar words...                    
         SEG   'Too many similar words, be more specific.'                      
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,16),BEGIN   No spelling dictionary...                    
         SEG   'Can''t guess -- there is no spelling dictionary.'               
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,20),BEGIN   Invalid guess...                             
         SEGB  (R3),(R2)                                                        
         SEG   'can''t be used as a word for guess.'                            
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         SEG   'Can''t guess.'     (Shouldn't happen)                           
         B     GSEXIT                                                           
         END                                                                    
*                                                                               
         SEG   'Similar words: '                                                
         LA    R4,GSRESP                                                        
         LOOP  BEGIN                                                            
         SETMSG @R4+1,LC:@R4                                                    
         WHILE (R0,NZ)             While len byte <> 0                          
         AR    R4,R0                                                            
         LA    R4,@R4+1            Next word ptr                                
         SEGB  (R1),(R0)           Seg current word                             
         END                                                                    
GSEXIT   LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LEARN -- ESC L Subcommand.                                                   
*                                                                               
LEARN    PROC                                                                   
         L     R15,CPVLNO                                                       
         IF    (R15,NEG),BEGIN     Not on a text line...                        
         SEG   'Press LEARN [ESC L] to add a word to your '                     
         SEG   'spelling dictionary.'                                           
         SET   CPVFBELL                                                         
         B     LRNEXIT                                                          
         END                                                                    
*                                                                               
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get line text                                
         IF    NZ,BEGIN                                                         
LRNERR   SEG   'Position cursor to the start of a word '                        
         SEG   'to LEARN [ESC L].'                                              
         SET   CPVFBELL                                                         
         B     LRNEXIT                                                          
         END                                                                    
         LH    R15,CPVCOL                                                       
         VCALL GETWD               Isolate word                                 
         IF    (R0,NP),LRNERR      Can't isolate word, forget it                
*                                                                               
         LR    R3,R1               Save word loc...                             
         LR    R2,R0               ...and length                                
*-                                                                              
*-       Learn the word in R1,R0.                                               
*-                                                                              
         VCALL LEARNWD             Learn this word                              
         IF    NZ,BEGIN            Trouble...                                   
         SEGB  (R3),(R2)                                                        
         SEG   'is already in the system dictionary.'                           
         SET   CPVFBELL                                                         
         END                                                                    
LRNEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INSERT -- Automatic Insert.                                                  
*                                                                               
INSERT   PROC                                                                   
         LT    R15,CPVLNO          Cursor line-number                           
         IF    NEG,'SET CPVFBELL; B INSEXIT'  Not on a line                     
*                                                                               
         LH    R0,CPVCOL           Split at cursor                              
         VCALL CHOPLINE            Split the line                               
         IF    NZ,'SET CPVFBELL; B INSEXIT'                                     
*                                                                               
         ST    R1,VWACHKL          New line needs to be on the page             
         SET   CPVFCH              File changes have occurred                   
*                                                                               
INSEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PREPARE -- Prepare for insert (for 3270s).                                   
*                                                                               
PREPARE  PROC                                                                   
         LT    R15,CPVLNO          Cursor line-number                           
         IF    NEG,'SET CPVFBELL; B PREPEXIT'  Not on a line                    
*                                                                               
         L     R4,CPVCURR          Cursor row                                   
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4),BEGIN                                                  
         FLIP  LINFPREP            Invert current prepare setting               
         SET   LINFREWR            Need to rewrite this line                    
         END                                                                    
*                                                                               
PREPEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ESCAPE -- Move cursor to/from home point (for 3270s).                        
*                                                                               
ESCAPE   PROC                                                                   
         L     R4,CPVCURR          Cursor row                                   
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4)                                                        
*-                                                                              
*-       Save current cursor location.                                          
*-                                                                              
         MVC   CPVALNO,CPVLNO      Save current cursor...                       
         MVC   CPVACOL,CPVCOL      ...as alternate                              
         MVC   CPVACURS,CPVCURS    ...cursor                                    
*                                                                               
         MVC   CPVLNO,=F'-1000'    Set cursor...                                
         CLEAR CPVCOL              ...to command                                
         LH    R15,CPVCMDR         ...row                                       
         ST    R15,CPVCURR                                                      
         MVC   CPVCURC,=F'28'      Specially adjust cursor column               
*                                                                               
         MVI   CPCMD,C'.'          Preset command prefix of "dot"               
         MVC   CPCMDLEN,=H'1'                                                   
         SET   VWAFNCMD            New command text is set                      
*                                                                               
         SET   CPVFESC             Go into ESC emulation mode                   
*                                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SPLIT -- ESC S Subcommand.                                                   
*                                                                               
SPLIT    PROC                                                                   
         IF    ('LT R15,CPVLNO',NEG),'SET CPVFBELL; B SPLITXIT'                 
*                                                                               
         LH    R2,CPVCOL           Current cursor column                        
*                                                                               
         LR    R0,R2               Split at cursor                              
         L     R15,CPVLNO          Cursor line-number                           
         VCALL CHOPLINE            Split the line                               
         IF    NZ,'SET CPVFBELL; B SPLITXIT'                                    
*-                                                                              
*-       Restore the cursor position.                                           
*-                                                                              
         ST    R0,CPVLNO           Set cursor line-number                       
         STH   R2,CPVCOL           ...and cursor column                         
*                                                                               
         ST    R1,VWACHKL          New line needs to be on the page             
         SET   CPVFCH              File changes have occurred                   
*                                                                               
SPLITXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  JOIN -- ESC J Subcommand.                                                    
*                                                                               
JOIN     PROC                                                                   
         IF    ('LT R15,CPVLNO',NEG),'SET CPVFBELL'  Not a text line            
*                                                                               
         ELSE  BEGIN                                                            
         LA    R0,1                Trim leading blanks                          
         CLEAR R1                  Allow max length                             
         L     R15,CPVLNO          Cursor line-number                           
         VCALL JOINLINE            Join the next line to this one               
         IF    Z,'SET CPVFCH'      File changes have occurred                   
         ELSE  BEGIN               Trouble...                                   
         IF    (R15,EQ,20),BEGIN                                                
         SEG   'Joined line would be too long.  No action taken.'               
         END                                                                    
         SET   CPVFBELL                                                         
         END                                                                    
         END                                                                    
*                                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ALIGNV -- ESC A and ESC Z Subcommands.                                       
*                                                                               
ALIGNV   PROC                                                                   
         IF    ('LT R15,CPVLNO',^NEG),BEGIN   On a text line...                 
*                                                                               
         COMMENT                   MOVE IN ALIGN COMMAND                        
         MVC   CPCMDLEN,=AL2(L'ALICMD)                                          
         MVC   CPCMD(L'ALICMD),ALICMD  Move in "command"                        
*                                                                               
         COMMENT                   FIX ALIGN LENGTH, INDENT VALUES              
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         BTD   CPCMD+(ALILEN-ALICMD),L'ALILEN,LH:WINDMARG                       
*                                                                               
         LTH   R15,WINDIND         Default indentation                          
         IF    NEG,'CLEAR R15'     Floating indent, use indent=0                
         BTD   CPCMD+(ALIIND-ALICMD),L'ALIIND,(R15)                             
         END                                                                    
*                                                                               
         COMMENT                   IF JUSTIFY, FIX COMMAND                      
         IF    (CPCMNM,EQ,'.Z'),BEGIN                                           
         MVC   CPCMD(3),=CL3'JUS'                                               
         END                                                                    
*                                                                               
         COMMENT                   Go execute command, (non-typed)              
         CLEAR CPFTYPED                                                         
         SETMSG CPCMD,LH:CPCMDLEN                                               
         VCALL EDTCOM              Go do Align/Justify command.                 
         B     CVNEXT              (Shouldn't happen)                           
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
ALIWORK  DC    C'ALI */LAST SPACE EVEN PARA MARKER=.'                           
         DC    C' LENGTH='                                                      
ALILEN   DC    C'???'                                                           
         DC    C' INDENT='                                                      
ALIIND   DC    C'???'                                                           
ALICMD   EQU   ALIWORK,*-ALIWORK,C'X'                                           
         DC    CL(L'VWACMD-L'ALICMD)' '                                         
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRAP -- Automatic Wordwrap.                                                  
*                                                                               
WRAPWA   RECORD BEGIN                                                           
WRAPFLAG FLAG                                                                   
         FLAG  WRAPFCOL            - Continuing collect                         
*                                                                               
WRAPOLNO DS    F                   Old line-number                              
WRAPNLNO DS    F                   New line-number                              
WRAPLEN  DS    H                   Original's new line length                   
WRAPMAXL DS    H                   Maximum line length                          
         END                                                                    
*-                                                                              
WRAP     PROC  WRAPWA                                                           
         CLEAR WRAPWA                                                           
*                                                                               
         IF    ('LT R15,CPVLNO',NEG),BEGIN  Not on a text line...               
         SET   CPVFBELL                                                         
         B     WRAPEXIT                                                         
         END                                                                    
*                                                                               
         IF    CPACFPG,'SET WRAPFCOL'                                           
*                                                                               
         IF    (CPCMNM,EQ,'.#'),'INCR R15,CPVCOL'  Kick cursor                  
*                                                                               
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5)                                                        
*-                                                                              
*-       Get the line and figure out where to split it.                         
*-                                                                              
         L     R15,CPVLNO                                                       
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get the line                                 
*                                                                               
         LH    R2,WINDMARG         Don't go beyond margin                       
*                                                                               
         IF    ((CPCMNM,NE,'.#'),AND,(R0,LT,R2)),WRAPEXIT  No wrap              
*                                                                               
         STH   R2,WRAPMAXL                                                      
         LA    R3,@R1(R2)                                                       
         DECR  R3                  Last position in line                        
*-                                                                              
*-       Try 1: The character causing the wrap is a blank.                      
*-                                                                              
         IF    (R0,EQ,R2),BEGIN    Ended exactly at length...                   
         SETMSG (R3),@R2+1          Use entire line (plus the space)            
         B     WRAPDO                                                           
         END                                                                    
*-                                                                              
*-       Try 2: Split at the closest blank.                                     
*-                                                                              
         SETMSG (R3),(R2)                                                       
         WHILE (R0,POS),BEGIN      Find a blank...                              
         IF    (@R1,EQ,' '),WRAPDO                                              
         DECR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*-                                                                              
*-       Try 3: Split at the closest punctuation character.                     
*-                                                                              
         SETMSG (R3),(R2)                                                       
         WHILE (R0,POS),BEGIN                                                   
         IF    ((@R1,EQ,'.'),OR,(@R1,EQ,':'),OR,(@R1,EQ,';')),WRAPDO            
         IF    (@R1,EQ,','),WRAPDO                                              
         IF    (@R1,EQ,'-'),WRAPDO                                              
*                                                                               
         DECR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*-                                                                              
*-       If all else fails split at the current cursor position.                
*-                                                                              
         IF    (R0,Z),'LH R0,CPVCOL; DECR R0; DECR R0'  Cursor-1                
         LH    R0,CPVCOL           Current cursor position+1                    
         DECR  R0                  Last cursor position                         
         DECR  R0                  Maximum line length                          
*-                                                                              
*-       Split the line.                                                        
*-                                                                              
WRAPDO   STH   R0,WRAPLEN          Save last column for later                   
         A     R0,CVONE            New line starts here                         
         L     R15,CPVLNO                                                       
         VCALL CHOPLINE            Split the line                               
         ST    R0,WRAPOLNO         Old line-number                              
         ST    R1,WRAPNLNO         New line-number                              
*-                                                                              
*-       Handle left margin indentation.                                        
*-                                                                              
         LTH   R2,WINDIND          Indentation needed                           
         IF    NZ,BEGIN            Add blanks...                                
         IF    NEG,BEGIN           Automatic indentation...                     
         L     R15,WRAPOLNO                                                     
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get previous line text                       
         BNZ   WRAPNI              Odd, forget it                               
*                                                                               
         CLEAR R2                  R2 = calculated indentation                  
         WHILE (R0,POS),BEGIN      Look for a non-blank...                      
         IF    (@R1,NE,' '),EXIT   Stop at non-blank                            
         LA    R2,@R2+1            Indentation                                  
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
         IF    (R2,Z),WRAPNI       No indentation, scram                        
         END                                                                    
*                                                                               
         FLOOR R2,0                                                             
         CEIL  R2,&LINESZ                                                       
         XPUSH ,,&LINESZ*2,PTR=R3                                               
         MVC   @R3(&LINESZ),CVBLANKS                                            
         LA    R1,@R3(R2)                                                       
         L     R15,WRAPNLNO                                                     
         VCALL GETLINE             Get line text                                
         BNZ   WRAPNI                                                           
*                                                                               
         LR    R1,R3                                                            
         AR    R0,R2                                                            
         IF    (R0,GT,&LINESZ),WRAPNI  Too long, forget it                      
         L     R15,WRAPNLNO                                                     
         VCALL PUTLINE             Replace line with indentation                
*                                                                               
         IF    (WRAPNLNO,EQ,CPVLNO),BEGIN  Adjust cursor pos...                 
         LH    R15,CPVCOL          Adjust...                                    
         AR    R15,R2              ... cursor                                   
         STH   R15,CPVCOL          ... position                                 
         END                                                                    
         END                                                                    
*-                                                                              
*-      Try to join the newly created line with the line after that.            
*-                                                                              
WRAPNI   IF    (WRAPOLNO,EQ,CPVLNO),BEGIN  Try it...                            
         LA    R15,1                                                            
         A     R15,WRAPNLNO                                                     
         LA    R1,VWAGLINE                                                      
         VCALL GETLINE             Get line after the new one                   
         IF    NEG,EXIT            Nothing, scram                               
         IF    (R0,Z),EXIT         Null line, scram                             
         IF    (@R1,EQ,' '),EXIT   Can't join if leading blanks                 
         IF    (@R1,EQ,'.'),EXIT   Can't join if leading dot                    
*                                                                               
         L     R15,WRAPNLNO        Newly created line                           
         LA    R0,3                Trim and use next's lineno                   
         LH    R1,WRAPMAXL         Maximum line length                          
         VCALL JOINLINE                                                         
         IF    Z,'ST R1,WRAPNLNO'                                               
         END                                                                    
*-                                                                              
*-       Figure out if this is a continuing collect.                            
*-                                                                              
         IF    WRAPFCOL,BEGIN      Continuing collect...                        
         IF    (CPVLNO,EQ,WRAPNLNO),'MVC CPACNLN,CPVLNO'                        
         ELSE  'CLEAR CPACINFO'                                                 
         END                                                                    
*                                                                               
         MVC   VWACHKL,WRAPNLNO    Line still needs to be on page               
         SET   CPVFCH              File changes have occurred                   
*                                                                               
WRAPEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WSELECT -- WINDOW <number> Subcommand.                                       
*                                                                               
WSELECT  PROC                                                                   
         IF    (CPVWCNT,EQ,1),BEGIN  Only one window...                         
         SET   CPVFBELL                                                         
         SEG   'There is only one window being displayed.'                      
         B     WSELEXIT                                                         
         END                                                                    
*                                                                               
         IC    R5,CPCMNM+2         Window number                                
         N     R5,=A(X'F')         ...in binary                                 
         IF    Z,'LA R5,1'         (zero=one, for now)                          
         IF    (R5,GT,CPVWCNT),BEGIN  Window number too high...                 
         SET   CPVFBELL                                                         
         SEG   'There are only '                                                
         SEGDC LH:CPVWCNT                                                       
         SEG   ' windows being displayed.'                                      
         B     WSELEXIT                                                         
         END                                                                    
*-                                                                              
*-       Change windows.                                                        
*-                                                                              
         STH   R5,CPVWCUR          Set new window                               
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         IF    (CPVLNO,NE,-1000),BEGIN  Move cursor to new window...            
         MVC   CPVLNO,WINDFLNO     First line...                                
         MVC   CPVCOL,=H'1'        ...column 1                                  
         END                                                                    
*                                                                               
         L     R15,WINDACT                                                      
         VCALL ACTPICK             Select the new Active file                   
         END                                                                    
*                                                                               
WSELEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WACTNEXT -- WINDOW Next Subcommand.                                          
*                                                                               
WACTNEXT PROC                                                                   
         LA    R1,CPATYPE                                                       
         L     R15,CPACTNO                                                      
         VCALL ACTNEXT             Next Active file                             
         IF    Z,BEGIN             Can't...                                     
         SET   CPVFBELL                                                         
         SEG   'There is no next '                                              
         SEGT  CPATYPE                                                          
         SEG   ' Active file.'                                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         VCALL ACTPICK             Select next Active file                      
         VCALL ACTVREST            Restore view position                        
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WACTPREV -- WINDOW Previous Subcommand.                                      
*                                                                               
WACTPREV PROC                                                                   
         LA    R1,CPATYPE                                                       
         L     R15,CPACTNO                                                      
         VCALL ACTPREV             Previous Active file                         
         IF    Z,BEGIN             Can't...                                     
         SET   CPVFBELL                                                         
         SEG   'There is no previous '                                          
         SEGT  CPATYPE                                                          
         SEG   ' Active file.'                                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         VCALL ACTPICK             Select next Active file                      
         VCALL ACTVREST            Restore view position                        
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WSPLIT -- WINDOW S Subcommand.                                               
*                                                                               
WSPLIT   PROC                                                                   
*                                                                               
         L     R4,CPVCURR                                                       
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4)                                                        
*                                                                               
         IF    (CPVWCNT,GE,WINDMAX#),BEGIN  Too many windows...                 
         SET   CPVFBELL                                                         
         SEG   'Can''t split this window, '                                     
         SEG   'too many windows.'                                              
         B     WSPLEXIT                                                         
         END                                                                    
*-                                                                              
*-       Split the window at the row specified.                                 
*-                                                                              
         LH    R5,LINTWIND                                                      
         IF    (R5,NZ),BEGIN       Regular text line...                         
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5)                                                        
*                                                                               
         IF    (^LINFNEW,AND,^LINFTEXT),BEGIN  Can't do it...                   
         SET   CPVFBELL                                                         
         SEG   'Press WINDOW SPLIT [ESC W S] on a text line '                   
         SEG   'to divide the page.'                                            
         B     WSPLEXIT                                                         
         END                                                                    
*                                                                               
         L     R2,CPVCURR          Current (cursor) row                         
         IF    (R2,EQ,WINDLROW),BEGIN                                           
         SET   CPVFBELL                                                         
         SEG   'Can''t split this window, '                                     
         SEG   'This is the last line of the window.'                           
         B     WSPLEXIT                                                         
         END                                                                    
*                                                                               
         LH    R0,WINDNO           Our window                                   
         LH    R1,LINTROW          Split the window here                        
         END                                                                    
*-                                                                              
*-       Split the window in half.                                              
*-                                                                              
         ELSE  BEGIN               Split the current window...                  
         CLEAR R0                  Current window                               
         CLEAR R1                  Split it in half                             
         END                                                                    
*-                                                                              
*-       Split the window.                                                      
*-                                                                              
         VCALL PAGENEWW            Do it                                        
         IF    NZ,BEGIN            Couldn't split window...                     
         SET   CPVFBELL                                                         
         XPUSH R0,R1                                                            
         SEG   'Can''t split this window ('                                     
         XPOP  R0,R1                                                            
         SEG   (R1),(R0)                                                        
         SEG   ').'                                                             
         B     WSPLEXIT                                                         
         END                                                                    
*                                                                               
WSPLEXIT LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WJOIN -- WINDOW J Subcommand.                                                
*                                                                               
WJOIN    PROC                                                                   
*-                                                                              
*-       Make sure it's a valid join request.                                   
*-                                                                              
         IF    (CPVWCNT,EQ,1),BEGIN  Trouble...                                 
         SEG   'Can''t join windows; '                                          
         SEG   'there is only one window.'                                      
         SET   CPVFBELL                                                         
         B     WJEXIT                                                           
         END                                                                    
*                                                                               
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5)                                                        
*-                                                                              
*-       Free the text lines in the window to be deleted.                       
*-                                                                              
         LA    R15,WINDNEXT        Window to be deleted                         
         IF    (CPVWCUR,EQ,CPVWCNT),'SH R15,=AL2(2*L"WIND)'                     
         WITH  (WIND,R15),BEGIN                                                 
         LH    R3,WINDFROW                                                      
         LR    R4,R3                                                            
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WHILE (R3,LE,WINDLROW),BEGIN  Fix up lines...                          
         WITH  (LINT,R4)                                                        
         IF    (LINFSTAT,OR,LINFNEW,OR,LINFTEXT),'FREELINT'                     
         SET   LINFREWR            Must rewrite line                            
*                                                                               
         LA    R4,LINTNEXT                                                      
         LA    R3,@R3+1                                                         
         END                                                                    
         END                                                                    
*-                                                                              
*-       Join last and previous windows.                                        
*-                                                                              
         LH    R2,CPVWCUR                                                       
         IF    (R2,EQ,CPVWCNT),BEGIN  Current is last window...                 
         LH    R4,WINDFROW                                                      
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4),BEGIN                                                  
         IF    LINFSTAT,'FREELINT'  Remove status line                          
         END                                                                    
*                                                                               
         SH    R5,=AL2(L'WIND)     Back up to previous window                   
*                                                                               
         LH    R3,WINDFROW                                                      
         LR    R4,R3                                                            
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WHILE (R3,LE,WINDLROW),BEGIN  Invalidate all old linenos...            
         WITH  (LINT,R4)                                                        
         MVC   LINTOLNO,=F'-2'     Invalidate old                               
*                                                                               
         LA    R4,LINTNEXT                                                      
         LA    R3,@R3+1                                                         
         END                                                                    
*                                                                               
         LH    R4,WINDFROW         Current window's first row                   
         MVC   WIND,WINDNEXT       Set new window information                   
         STH   R4,WINDFROW         Set new first row                            
*                                                                               
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4),BEGIN                                                  
         FREELINT                                                               
         SET   LINFSTAT,LINFREWR   Status line                                  
         END                                                                    
*                                                                               
         L     R15,=F'-1'          Must recalc starting lineno, etc.            
         ST    R15,WINDSLNO                                                     
         ST    R15,WINDELNO                                                     
         ST    R15,WINDFLNO                                                     
         ST    R15,WINDLLNO                                                     
*                                                                               
         SET   WINFMOD             Recalc first,last linenos                    
*                                                                               
         DECR  R2                                                               
         STH   R2,CPVWCUR          Save new current window number               
         STH   R2,CPVWCNT          Save new total number of windows             
         END                                                                    
*-                                                                              
*-       Join current and next windows.                                         
*-                                                                              
         ELSE  BEGIN               Join current and next windows...             
         LA    R5,WINDNEXT         Next window                                  
         LH    R3,WINDFROW                                                      
         LR    R4,R3                                                            
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WHILE (R3,LE,WINDLROW),BEGIN  Invalidate all old linenos...            
         WITH  (LINT,R4)                                                        
         MVC   LINTOLNO,=F'-2'     Invalidate old                               
*                                                                               
         LA    R4,LINTNEXT                                                      
         LA    R3,@R3+1                                                         
         END                                                                    
*                                                                               
         SH    R5,=AL2(L'WIND)     Current window                               
         LH    R4,WINDNEXT+(WINDLROW-WIND)  New last row                        
         STH   R4,WINDLROW                                                      
*                                                                               
         SET   WINFMOD             Recalc first,last linenos                    
*                                                                               
         LH    R2,CPVWCNT                                                       
         SH    R2,CPVWCUR                                                       
         DECR  R2                  No. of windows after next                    
         IF    (R2,POS),BEGIN      Move them up...                              
         MH    R2,=AL2(L'WIND)                                                  
         MOVEL WINDNEXT,WINDNEXT+L'WIND,(R2)  Delete "next" window              
         END                                                                    
*                                                                               
         DECR  R15,CPVWCNT         One less window                              
         END                                                                    
*-                                                                              
*-       Fix up window numbering.                                               
*-                                                                              
         VCALL WINDFIX             Fix up window numbering                      
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
WJEXIT   LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PF22 -- Smart "WINDOW L"; if an alternate width is defined then              
*    this PF key becomes "WINDOW W".                                            
*                                                                               
PF22     PROC                                                                   
         IF    CPVFVTAM,BEGIN      VTAM only...                                 
         IF    (CPSALTHEI,NZ),'ACALL WWIDTH; EXIT PF22'  Change wid             
         END                                                                    
*                                                                               
         ACALL WLEFT               Otherwise, it's "WINDOW L"                   
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WLEFT -- WINDOW L Subcommand.                                                
*                                                                               
WLEFT    PROC                                                                   
         L     R4,CPVCURR          Cursor row                                   
         LR    R5,R4                                                            
         MH    R5,=AL2(L'LINT)                                                  
         A     R5,CPXLINT                                                       
         WITH  (LINT,R5),BEGIN                                                  
         IF    ('LTH R6,LINTWIND',Z),BEGIN  Not a text line...                  
         SET   CPVFBELL                                                         
         B     LEFTEXIT                                                         
         END                                                                    
*                                                                               
         MH    R6,=AL2(L'WIND)                                                  
         A     R6,CPXWIND                                                       
         WITH  (WIND,R6)                                                        
*                                                                               
         L     R15,CPVCURC         Cursor column                                
         SH    R15,WINDTCOL        Starting text column                         
         IF    POS,'AH R15,WINDFCOL'  Adjust for cols displayed                 
         ELSE  'CLEAR R15'                                                      
*                                                                               
         LA    R0,@R15+1           Actual starting column                       
         LA    R15,WIND                                                         
         VCALL PAGECOLS            Display these columns                        
         END                                                                    
LEFTEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRIGHT -- WINDOW R Subcommand.                                               
*                                                                               
WRIGHT   PROC                                                                   
         L     R4,CPVCURR          Cursor row                                   
         LR    R5,R4                                                            
         MH    R5,=AL2(L'LINT)                                                  
         A     R5,CPXLINT                                                       
         WITH  (LINT,R5),BEGIN                                                  
         IF    ('LTH R6,LINTWIND',Z),BEGIN  Not a text line...                  
         SET   CPVFBELL                                                         
         B     RIGHTXIT                                                         
         END                                                                    
*                                                                               
         MH    R6,=AL2(L'WIND)                                                  
         A     R6,CPXWIND                                                       
         WITH  (WIND,R6)                                                        
*                                                                               
         L     R15,CPVCURC         Cursor column                                
         AH    R15,WINDFCOL        Adjust for cols being displayed              
         SH    R15,CPVWID          Calc left col from right col                 
         IF    NEG,'CLEAR R15'                                                  
*                                                                               
         LA    R0,@R15+1           Actual starting column                       
         LA    R15,WIND                                                         
         VCALL PAGECOLS            Display these columns                        
         END                                                                    
RIGHTXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WNUM -- WINDOW U Subcommands.                                                
*                                                                               
WNUM     PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
*                                                                               
         FLIP  WINFUNN             Switch numbered/unnumbered                   
         SET   WINFREWR            Rewrite entire window                        
*-                                                                              
*-       Recalculate window lengths.                                            
*-                                                                              
         LA    R15,12              First text column                            
         IF    WINFUNN,BEGIN       Unnumbered...                                
         LA    R15,1               Starting text column                         
         END                                                                    
*                                                                               
         STH   R15,WINDTCOL        First text column                            
         DECR  R15                                                              
         LH    R2,CPVTWID                                                       
         IF    WINFUNN,'DECR R2'   Adjust for one char prot field               
         SR    R2,R15              Maximum text length                          
         LH    R0,CPFCOL                                                        
         STH   R0,WINDFCOL         Column-1 for lines                           
         LA    R15,&LINESZ                                                      
         SR    R15,R0                                                           
         CEIL  R2,R15              Don't go past end of line                    
         STH   R2,WINDTLEN         Save it                                      
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WCOLUMN -- WINDOW C Subcommand.                                              
*                                                                               
WCOLUMN  PROC                                                                   
         L     R4,CPVCURR          Cursor row                                   
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4),BEGIN                                                  
*-                                                                              
*-       Don't allow getting rid of the global column line.                     
*-                                                                              
         IF    (LINTWIND,Z),BEGIN  Global line...                               
         SET   CPVFBELL            Complain                                     
         B     WCOLEXIT                                                         
         END                                                                    
*-                                                                              
*-       Replace the current text line with a column line.                      
*-                                                                              
         IF    (LINFNEW,OR,LINFTEXT),BEGIN  Replace text line...                
         FREELINT                                                               
         SET   LINFCOL,LINFREWR                                                 
         END                                                                    
*-                                                                              
*-       Replace the current column line with a text line.                      
*-                                                                              
         ELSEIF LINFCOL,BEGIN      Replace column line...                       
         FREELINT                                                               
         END                                                                    
*-                                                                              
*-       Cursor is on a funny type of line, complain.                           
*-                                                                              
         ELSE  BEGIN               Complain...                                  
         SET   CPVFBELL                                                         
         SEG   'Position cursor to text line to be replaced '                   
         SEG   'with a column line.'                                            
         B     WCOLEXIT                                                         
         END                                                                    
*-                                                                              
*-       Tidy up window.                                                        
*-                                                                              
         LH    R5,LINTWIND                                                      
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         LH    R0,WINDFROW                                                      
         LH    R1,WINDLROW                                                      
         VCALL PAGECNT             Count number of text lines                   
         STH   R15,WINDTCNT        Keep text line count accurate                
*                                                                               
         L     R15,=F'-1'          Must recalc starting lineno, etc.            
         ST    R15,WINDSLNO                                                     
         ST    R15,WINDELNO                                                     
         ST    R15,WINDFLNO                                                     
         ST    R15,WINDLLNO                                                     
*                                                                               
         SET   WINFMOD             Recalc first,last linenos                    
         END                                                                    
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
WCOLEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WWRAP -- WINDOW + and - Subcommands.                                         
*                                                                               
WWRAP    PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         SET   WINFWRAP            Turn on wordwrap                             
         IF    (CPCMNM,EQ,'.WM'),'CLEAR WINFWRAP'  Turn it off                  
         END                                                                    
*                                                                               
         ACALL WCOLREWR            Rewrite column lines                         
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WLMARGIN -- WINDOW < Subcommand.                                             
*                                                                               
WLMARGIN PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         L     R15,CPVCURC         Cursor column                                
         SH    R15,WINDTCOL        Starting text column                         
         IF    POS,'AH R15,WINDFCOL'  Adjust for cols displayed                 
         LA    R15,@R15+1          Relative to one                              
         FLOOR R15,1                                                            
         CEIL  R15,&LINESZ         Better safe than sorry                       
         DECR  R15                                                              
         STH   R15,WINDIND         Set indentation (left margin)                
*                                                                               
         ACALL WCOLREWR            Make sure col lines get rewritten            
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WLAUTO -- WINDOW / Subcommand.                                               
*                                                                               
WLAUTO   PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         IF    (WINDIND,NE,-1),'MVC WINDIND,=H"-1"'                             
         ELSE  'CLEAR WINDIND'     Toggle auto indentation                      
*                                                                               
         ACALL WCOLREWR            Make sure col lines get rewritten            
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WRMARGIN -- WINDOW > Subcommand.                                             
*                                                                               
WRMARGIN PROC                                                                   
         LH    R5,CPVWCUR                                                       
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5),BEGIN                                                  
         SET   WINFWRAP            Make sure wordwrap is on                     
*                                                                               
         L     R15,CPVCURC         Cursor column                                
         SH    R15,WINDTCOL        Starting text column                         
         IF    POS,'AH R15,WINDFCOL'  Adjust for cols displayed                 
         LA    R15,@R15+1          Relative to one                              
         FLOOR R15,1                                                            
         CEIL  R15,&LINESZ         Better safe than sorry                       
         STH   R15,WINDMARG        Set new right margin                         
*                                                                               
         ACALL WCOLREWR            Make sure col lines get rewritten            
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WTAB -- WINDOW T Subcommand.                                                 
*                                                                               
WTAB     PROC                                                                   
         L     R4,CPVCURR                                                       
         MH    R4,=AL2(L'LINT)                                                  
         A     R4,CPXLINT                                                       
         WITH  (LINT,R4),BEGIN                                                  
         IF    ('LTH R5,LINTWIND',Z),BEGIN  Not a text line...                  
         SET   CPVFBELL                                                         
         B     WTABEXIT                                                         
         END                                                                    
*                                                                               
         MH    R5,=AL2(L'WIND)                                                  
         A     R5,CPXWIND                                                       
         WITH  (WIND,R5)                                                        
*                                                                               
         L     R3,CPVCURC          Cursor column                                
         SH    R3,WINDTCOL         Starting text column                         
         IF    POS,'AH R3,WINDFCOL'  Adjust for cols displayed                  
         LA    R3,@R3+1            Relative to one                              
         FLOOR R3,1                                                             
         CEIL  R3,&LINESZ          Better safe than sorry                       
*-                                                                              
*-       Find tab stop insertion point.                                         
*-                                                                              
         LA    R1,WINDTABS                                                      
         LA    R15,L'WINDTABS                                                   
         LOOP  BEGIN               Find tab stop insertion point...             
         LC    R2,@R1              Tab stop                                     
         IF    (R3,EQ,R2),BEGIN    Remove existing tab stop...                  
         DECR  R15                                                              
         MOVE  R15,@R1,@R1+1        Shift tabs over                             
         MVI   WINDTABS+L'WINDTABS-1,0  Zot last tab stop                       
         B     WTABREWR                                                         
         END                                                                    
*                                                                               
         IF    ((R2,Z),OR,(R2,GT,R3)),EXIT  Found insertion point               
*                                                                               
         LA    R1,@R1+1            Next tab stop                                
         DECR  R15                                                              
         UNTIL (R15,Z)                                                          
         END                                                                    
*-                                                                              
*-       Check for tabs overflow.                                               
*-                                                                              
         IF    ('CLI WINDTABS+L"WINDTABS-1,0',NE),BEGIN  No room...             
         SEG   'Too many tabs.  Can''t add new tab stop.'                       
         SET   CPVFBELL                                                         
         B     WTABEXIT                                                         
         END                                                                    
*                                                                               
         DECR  R15                                                              
         DECR  R15                                                              
         IF    (R15,^NEG),BEGIN     Open up space for new tabs...               
         EX    R15,'MVC @R13(0),@R1'                                            
         EX    R15,'MVC @R1+1(0),@R13' Shift everything right 1 space           
         END                                                                    
*                                                                               
         STC   R3,@R1              Set new tab stop                             
*                                                                               
WTABREWR ACALL WCOLREWR            Make sure col lines get rewritten            
         END                                                                    
*                                                                               
         SET   CPVFFMT             Redo field formatting                        
WTABEXIT LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WWIDTH -- WINDOW W Subcommand.                                               
*                                                                               
WWIDTH   PROC                                                                   
*-                                                                              
*-       VTAM style screen dimension switching.                                 
*-                                                                              
         IF    CPVFVTAM,BEGIN      VTAM only...                                 
         IF    (CPSALTHEI,Z),WIDERR  No alternate dimension                     
*-                                                                              
*-       First switch from primary to alternate (or v.v.) and then              
*-         make sure Milten has the updated SIB.                                
*-                                                                              
         LA    R2,100                                                           
         WHILE (R2,POS),BEGIN      Maximum loop count...                        
         FLIP  CPSFALTDIM          Switch from primary to alternate             
         VCALL UPDCPSIB            Update SIB                                   
         IF    Z,EXIT              Done, scram                                  
         DECR  R2                                                               
         END                                                                    
*                                                                               
         LH    R0,CPSWID           Screen width                                 
         LH    R1,CPSHEI           Screen height                                
*                                                                               
         IF    CPSFALTDIM,BEGIN    Alternate dimensions...                      
         LH    R0,CPSALTWID        Alt. screen width                            
         LH    R1,CPSALTHEI        Alt. screen height                           
         END                                                                    
*                                                                               
         IF    CPVFVTAM,'DECR R0'  VTAM width is actual-1                       
         STH   R0,CPVWID           Save view width                              
         STH   R0,CPVTWID          Save view text width                         
         STH   R1,CPVHEI           Save view height                             
         VCALL CALCDIM             Set new dimensions                           
*-                                                                              
*-       Rebuild the screen image if height is changing.                        
*-                                                                              
         IF    (CPSHEI,NE,CPSALTHEI),BEGIN  Height is changing...               
         XPUSH ,,256,PTR=R3                                                     
         SETMSG (R3),256            Working view info area                      
         VCALL GETVINFO            Get current view info                        
         XPUSH R0,R1               Save view info message ptrs                  
*                                                                               
         VCALL PAGEINIT            Re-initialize page                           
*                                                                               
         XPOP  R0,R1               Restore view info ptrs                       
         VCALL SETVINFO            Set view info                                
         XPOP  ,,256               Neatness                                     
         END                                                                    
*                                                                               
         B     WIDEXIT             All done                                     
         END                                                                    
*-                                                                              
*-       Terminal type based screen width switching.                            
*-                                                                              
         LT    R5,CPTTYPEP                                                      
         IF    NZ,BEGIN            Terminal type...                             
         WITH  (TTYPE,R5)                                                       
         IF    (TTYAWID,Z),EXIT    No alternate width, scram                    
         CLEAR R15                                                              
         IF    (CPVWID,EQ,TTYWID),'LH R15,TTYAWID'  Toggle                      
         IF    (CPVWID,EQ,TTYAWID),'LH R15,TTYWID'                              
         IF    (R15,Z),'LH R15,TTYWID'                                          
         STH   R15,CPVWID          Set new width                                
         VCALL CALCDIM             Recalculate screen dimensions                
         B     WIDEXIT                                                          
         END                                                                    
*                                                                               
WIDERR   SET   CPVFBELL            Complain                                     
         SEG   'You can''t change screen width on this terminal.'               
WIDEXIT  LABEL ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WCOLREWR -- Local routine to make sure all the column lines                  
*    get rewritten.                                                             
*                                                                               
WCOLREWR PROC                                                                   
         LA    R2,1                                                             
         L     R3,CPLINTP                                                       
         WHILE (R2,LE,CPVHEI),BEGIN  Go through lines...                        
         WITH  (LINT,R3)                                                        
*                                                                               
         IF    ((LINTWIND,Z),OR,(LINTWIND,EQ,CPVWCUR)),BEGIN                    
         IF    LINFCOL,'SET LINFREWR'  Rewrite column line                      
         END                                                                    
*                                                                               
         LA    R3,LINTNEXT                                                      
         LA    R2,@R2+1                                                         
         END                                                                    
*                                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WERROR -- Unrecognized WINDOW Subcommand.                                    
*                                                                               
WERROR   PROC                                                                   
         SET   CPVFBELL                                                         
         SEG   'Unrecognized WINDOW command '                                   
         SEG   '(type HELP PAGE WINDOW for information).'                       
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CMDNULL -- Unrecognized Command.                                             
*                                                                               
CMDNULL  PROC                                                                   
         B    CVABSENT                                                          
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CMDERR -- Unrecognized Command.                                              
*                                                                               
CMDERR   PROC                                                                   
         IF    (@R1,EQ,'.'),BEGIN  Unrecognized subcommand...                   
         SEG   'Unknown VIEW function.'                                         
         SET   CPVFBELL                                                         
         END                                                                    
*                                                                               
         ELSE  'SET VWAFNLOC'      Not our command                              
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         DROP  VWA                                                              
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIEWPF -- Routine to process "immediate" function codes.  This               
*    routine is primarily needed for VTAM (3270ish) terminals which             
*    have immediate function codes which return the function code               
*    but not all the modified fields (such as ATTN, PA1, PA2, PA3).             
*                                                                               
*    On entry:                                                                  
*      R15 - function code                                                      
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - OK; we processed it                                                  
*      4 - not an immediate function code                                       
*                                                                               
VIEWPF   XPROC                                                                  
         LR    R2,R15              Save function code                           
*                                                                               
         LA    R15,4               Preset not immediate function rc             
*-                                                                              
*-       Special VTAM immediate function code processing.                       
*-                                                                              
         IF    CPVFVTAM,BEGIN      VTAM only...                                 
         IF    ((R2,EQ,130),OR,                                        *        
               ((R2,GE,171),AND,(R2,LE,173))),BEGIN                             
         ACALL VTAMATTN            ATTN or PA1/PA2/PA3                          
         CLEAR R15                                                              
         END                                                                    
         ELSEIF (R2,EQ,161),'ACALL VTAMCLR; CLEAR R15'  CLEAR                   
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VTAMATTN -- VTAM's ATTN Immediate Subcommand.                                
*                                                                               
VTAMATTN PROC                                                                   
         SET   CPVFBELL            Complain                                     
         MVC   CPVMSG,CVBLANKS     Error message                                
         MVC   CPVMSG(L'VATTNMSG),VATTNMSG                                      
         FREECURS ,                Move to the command line                     
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
VATTNST  DC    C'Press PF9 (or type .BREAK on the command line) '               
         DC    C'to leave page mode.'                                           
VATTNMSG EQU   VATTNST,*-VATTNST,C'X'                                           
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VTAMCLR -- VTAM's CLEAR Immediate Subcommand.                                
*                                                                               
VTAMCLR  PROC                                                                   
         MVC   CPVMSG,CVBLANKS                                                  
         MVC   CPVMSG(L'VCLRMSG),VCLRMSG                                        
         SET   CPVFCLR             Clear and rewrite page                       
         FREECURS ,                Move to the command line                     
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
VCLRMSG  DC    C'Your last changes (if any) have been undone!'                  
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIEWXCMD -- Routine to execute the specified view operation.                 
*    This is called from the SYSCALL VIEW_CMD procedure.                        
*                                                                               
*    On entry:                                                                  
*      R1,R0 - return string area (loc,len)                                     
*      R3,R2 - options string loc, len                                          
*                                                                               
*    On exit:                                                                   
*      R1,R0 - return message loc, len                                          
*                                                                               
VXWA     RECORD BEGIN                                                           
VXWNO    DS    F                   Window number to split                       
VXFROW   DS    F                   First row of new window                      
*                                                                               
VXSEG    SEGCB                                                                  
         END                                                                    
*-                                                                              
VIEWXCMD XPROC VXWA                                                             
         LA    R6,VXWA                                                          
         WITH  (VXWA,R6)                                                        
         CLEAR VXWA                                                             
         SEGINIT (R1),(R0),VXSEG                                                
         SCPUSH                                                                 
*                                                                               
         SCINIT (R3),(R2)                                                       
         SCAN  XCMDPRT             VIEW_CMD verbs                               
         IF    NEG,'SEG "SCAN ERROR"'                                           
         SETMSG L:VXSEGLOC,L:VXSEGLENF  Return string                           
         IF    (R0,Z),'SETMSG "OK"'                                             
         PRETURN (R0,R1)                                                        
*                                                                               
         SCPOP ,                   Restore scanner                              
*                                                                               
         CLEAR R15                 (Tidy)                                       
         PEND                                                                   
*-                                                                              
*-       VIEW_CMD Commands.                                                     
*-                                                                              
XCMDPRT  SCKW  NEWWINDOW,XCMDNEW                                                
         SCKW  RESET,XCMDRES                                                    
         SCKW  GETBLOCK,XCMDGETB                                                
         SCKW  UNMARK,XCMDUNMK                                                  
         SCKW  REMARK,XCMDREMK                                                  
         SCKW  ,XCMDERR                                                         
*-                                                                              
*-       Unrecognized command.                                                  
*-                                                                              
XCMDERR  PROC                                                                   
         WITH  (VXWA,R6)                                                        
         SEG   'UNRECOGNIZED COMMAND'                                           
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XCMDNEW -- NEWWINDOW Command.                                                
*                                                                               
XCMDNEW  PROC                                                                   
         WITH  (VXWA,R6)                                                        
         SCPUSH ,                  Save scanner info                            
*                                                                               
         SCAN  XNEWPRT             NEWWINDOW options                            
         IF    NEG,'SEG "SCAN ERROR"'                                           
*                                                                               
         L     R0,VXWNO            Window number (0=current)                    
         L     R1,VXFROW           First row of window (0=default)              
         VCALL PAGENEWW            Make a new window                            
         IF    NZ,'SEG (R1),(R0)'  Error message                                
         ELSE  BEGIN               Normal return code...                        
         LR    R2,R0                                                            
         SEG   'OK WINDOW='                                                     
         SEGDC (R2)                                                             
         END                                                                    
*                                                                               
         SCPOP ,                   Restore scanner info                         
*                                                                               
         LA    R15,4               Stop main scan                               
         PEND                                                                   
*-                                                                              
*-       NEWWINDOW options.                                                     
*-                                                                              
XNEWPRT  SCKW  WINDOW,XNEWWIND,(A,P,I)                                          
         SCKW  FROW,XNEWROW,(A,P,I)                                             
         SCKW  ,XNEWERR                                                         
*-                                                                              
XNEWWIND PROC                                                                   
         WITH  (VXWA,R6)                                                        
         ST    R0,VXWNO            Save window number                           
         PEND                                                                   
*                                                                               
XNEWROW  PROC                                                                   
         WITH  (VXWA,R6)                                                        
         ST    R0,VXFROW           Save first row of window                     
         PEND                                                                   
*                                                                               
XNEWERR  PROC                                                                   
         WITH  (VXWA,R6)                                                        
         SEG   'UNRECOGNIZED OPTION'                                            
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XCMDRES -- RESET Command.                                                    
*                                                                               
XCMDRES  PROC                                                                   
         VCALL PAGEINIT            Reset page                                   
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XCMDGETB -- GETBLOCK Command.                                                
*                                                                               
XCMDGETB PROC                                                                   
         WITH  (VXWA,R6)                                                        
*-                                                                              
*-       Return marked block information.                                       
*-                                                                              
         IF    (^CPVFHIDE,AND,(CPBLKACT,NZ)),BEGIN                              
         SEG   'ACTIVE='                                                        
         SEGDC L:CPBLKACT                                                       
*                                                                               
         SEG   ' FBLOCK='                                                       
         L     R0,CPFBKLNO                                                      
         VCALL CVEXNO                                                           
         SEG   (R1),(R0)                                                        
         IF    (CPFBKCOL,GT,1),'SEG " FCOL="; SEGDC LH:CPFBKCOL'                
*                                                                               
         SEG   ' LBLOCK='                                                       
         L     R0,CPLBKLNO                                                      
         VCALL CVEXNO                                                           
         SEG   (R1),(R0)                                                        
         IF    (CPLBKCOL,NZ),'SEG " LCOL="; SEGDC LH:CPLBKCOL'                  
         END                                                                    
*-                                                                              
*-       No block is defined.                                                   
*-                                                                              
         ELSE  BEGIN                                                            
         SEG   'NO BLOCK'                                                       
         END                                                                    
*                                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XCMDUNMK -- UNMARK Command.                                                  
*                                                                               
XCMDUNMK PROC                                                                   
         IF    (^CPVFHIDE,AND,(CPBLKACT,NZ)),BEGIN                              
         SET   CPVFHIDE,CPVFMARK   Hide the block                               
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
*box                                                                            
*                                                                               
*  XCMDREMK -- REMARK Command.                                                  
*                                                                               
XCMDREMK PROC                                                                   
         IF    ((CPBLKACT,NZ),AND,CPVFHIDE),BEGIN                               
         CLEAR CPVFHIDE                                                         
         SET   CPVFMARK            Block needs to be shown                      
         END                                                                    
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         VLTORG                                                                 
         END   .                                                                
