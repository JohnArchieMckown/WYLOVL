OPT      TITLE 'WYLBUR''s Common Scan Tables'                                   
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
         GBLC  &PUBUSER,&PUBGRP                                                 
*                                                                               
WYLOPT   CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
OPT      IDENT 2025                11:49:28 01/25/02  (SLP)                     
*                                                                               
         PUSH  DSECTS                                                           
         EJECT                                                                  
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
         COPY  CONTROL             Copy cv/cp                                   
*                                                                               
         POP   DSECTS                                                           
         TITLE 'Some Common Scan Routines'                                      
*                                                                               
*  SCAN, NO OPTIONS ALLOWED                                                     
*                                                                               
*                                                                               
NOOPTS   XPROC ,                                                                
         SCAN  NOOPTSKW                                                         
         SCANCHK                                                                
         PEND  ,                                                                
NOOPTSKW SCKW  ,V(NOTVALID)                                                     
         SPACE 1                                                                
*                                                                               
*  SCAN COLLECT OPTIONS                                                         
*                                                                               
*                                                                               
COLLOPTS XPROC ,                                                                
         SCAN  COPTSKWS                                                         
         SCANCHK                                                                
         PEND  ,                                                                
COPTSKWS SCKW  ,COLLPSH,PUSH                                                    
         SCKW  ,V(SCNINVAL)                                                     
         EJECT                                                                  
*                                                                               
*  SCAN DSNAME OPTS         ** DEBUG **                                         
*                                                                               
*                                                                               
DSNOPTS  XPROC ,                                                                
         SCAN  DOPTSKWS                                                         
         SCANCHK                                                                
         PEND  ,                                                                
DOPTSKWS SCKW  ,DSNPSH,PUSH                                                     
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 1                                                                
*                                                                               
*  SCAN DSNAME OPTS  W/FORMAT OPTIONS INCLUDED                                  
*                                                                               
*                                                                               
DSNFOPTS XPROC ,                                                                
         SCAN  DFOPTSKW                                                         
         SCANCHK                                                                
         PEND  ,                                                                
DFOPTSKW SCKW  ,DSNPSHF,PUSH                                                    
         SCKW  ,V(SCNINVAL)                                                     
         SPACE 1                                                                
         EJECT                                                                  
*                                                                               
*  SCAN FILE NAME OPTIONS                                                       
*                                                                               
*                                                                               
FNAMOPTS XPROC ,                                                                
         SCAN  FOPTSKWS                                                         
         SCANCHK                                                                
         PEND  ,                                                                
FOPTSKWS SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,V(SCNINVAL)                                                     
         QLTORG                                                                 
         TITLE 'Account Scan Table'                                             
*                                                                               
*  ACCOUNT SCAN TABLES, ROUTINES                                                
*                                                                               
ACCTPSHL XPUSHRTN ACCTKWSL         Gg.uuu[.project]                             
*                                                                               
ACCTPSHN XPUSHRTN ACCTKWSN         Gg.uuu or uuu$gg                             
*                                                                               
ACCTPSHC XPUSHRTN ACCTKWSC         Gg.uuu, acc, user, group,                    
*                                                                               
ACCTPSH  XPUSHRTN ACCTKWS          Acc, user, group                             
*                                                                               
ACCTKWSL SCKW  ,V(SCNNOOP),A(MACCTPRJ)       Gg.uuu[.project]                   
         SCKW  ,,END                                                            
         SPACE 2                                                                
ACCTKWSN SCKW  ,V(SCNNOOP),A(MACCT)          Gg.uuu or uuu$gg                   
         SCKW  ,,END                                                            
         SPACE 2                                                                
ACCTKWSC SCKW  ,V(SCNNOOP),A(MACCT)          Gg.uuu, acc, user, gro             
         SCKW  ,ACCTPSH,PUSH                                                    
         SCKW  ,,END                                                            
*                                                                               
ACCTKWS  SCKW  ACCOUNT,SCNACCT,(A,P) Acc, user, group                           
         SCKW  ACCT,SCNACCT,P                                                   
         SCKW  USER,SCNUSER,(A,P)                                               
         SCKW  USR,SCNUSER,P                                                    
         SCKW  GROUP,SCNGRP,(A,P)                                               
         SCKW  GRP,SCNGRP,P                                                     
         SCKW  USERID,SCNUID,(P,END)                                            
         EJECT                                                                  
*                                                                               
*   MATCH GG.UUU[.PROJECT]                                                      
*                                                                               
*   gg.uuu[.project]                                                            
*                                                                               
MACCTPRJ PROC  ,                                                                
*                                                                               
         COMMENT                   GET 6 CHAR ACCT, UPPER CASE                  
         SCTOK                                                                  
         SCUPTOK R1                                                             
         IF ((R0,GT,7),AND,(@R1+2,EQ,'.'),AND,(@R1+6,EQ,'.')),BEGIN             
         CEIL  R0,6                                                             
         END                                                                    
*                                                                               
         COMMENT                   CHECK ACCOUNT PART                           
         ACALL TRYACCT                                                          
         IF    NZ,BEGIN                                                         
         B     MPRJDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   NOW CHECK PROJECT                            
         SCTOK                                                                  
         SCUPTOK R1                                                             
         IF    (R0,GE,7),BEGIN                                                  
         IF    (@R1+6,NE,'.'),BEGIN                                             
         LA    R15,4                                                            
         B     MPRJDONE                                                         
         END                                                                    
         LA    R1,@R1+7                                                         
         SH    R0,=H'7'                                                         
         IF    (R0,GT,L'CPSPROJ),BEGIN                                          
         SCSEG (R1),(R0)                                                        
         SCSEG ': project-id is too long (max is 8 characters).'                
         L     R15,=F'-4'                                                       
         B     MPRJDONE                                                         
         END                                                                    
         MVC   CPSPROJ,CVBLANKS    All ok, set project                          
         LR    R4,R0                                                            
         MOVE  R4,CPSPROJ,@R1                                                   
         END                                                                    
*                                                                               
         COMMENT                   All ok, set account                          
         SCUPTOK R1                                                             
*-                                                                              
*-       gg.uuu or gg$uuu.                                                      
*-                                                                              
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),BEGIN                         
         MVC   CPCGRP,@R1                                                       
         MVC   CPCUSER,@R1+3                                                    
         END                                                                    
*-                                                                              
*-       Allow "uuu$gg" for compatibility.                                      
*-                                                                              
         ELSEIF (@R1+3,EQ,'$'),BEGIN                                            
         MVC   CPCGRP,@R1+4                                                     
         MVC   CPCUSER,@R1                                                      
         END                                                                    
*                                                                               
         ELSE  ' BOMB '                                                         
         CLEAR R15                 all ok match found                           
*                                                                               
MPRJDONE LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  MATCH GG.UUU  or  GG$UUU  or  UUU$GG                                         
*                                                                               
MACCT    PROC                                                                   
*                                                                               
         COMMENT                   GET 6 CHAR ACCT, UPPER CASE                  
         SCTOK                                                                  
         SCUPTOK R1                                                             
         COMMENT                   CHECK ACCOUNT                                
         ACALL TRYACCT                                                          
         IF    (R15,Z),BEGIN       Got one...                                   
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),BEGIN                         
         MVC   CPCGRP,@R1                                                       
         MVC   CPCUSER,@R1+3                                                    
         END                                                                    
*                                                                               
         ELSEIF (@R1+3,EQ,'$'),BEGIN                                            
         MVC   CPCGRP,@R1+4                                                     
         MVC   CPCUSER,@R1                                                      
         END                                                                    
*                                                                               
         ELSE  ' BOMB '                                                         
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  gg.uuu, gg$uuu or uuu$gg                                                     
*                                                                               
SCNACCT  PROC                                                                   
         SCUPTOK R1                                                             
         ACALL CKACCT                                                           
         IF    NZ,BEGIN                                                         
         END                                                                    
*                                                                               
         ELSEIF ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$')),BEGIN                        
         MVC   CPCGRP,@R1                                                       
         MVC   CPCUSER,@R1+3                                                    
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF (@R1+3,EQ,'$'),BEGIN                                            
         MVC   CPCGRP,@R1+4                                                     
         MVC   CPCUSER,@R1                                                      
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  MATCH <user id> OR <account>                                                 
*                                                                               
*  if match, CPCACCT is set to correct account                                  
*  user routine assumed SCNNOOP, (similar to MACCT usage)                       
*                                                                               
MATUID   XPROC                                                                  
*                                                                               
         COMMENT                   CHECK IF USER ID/ACCT                        
         SCTOK ,                   R1,R0 TOKEN SCANNED                          
         LA    R15,CPCACCT         R15 - ACCOUNT RETURNED                       
         COMMENT                   CHECK FOR UID                                
         VCALL GETACCT                                                          
         IF    (R15,Z),BEGIN       FOUND USER ID                                
         SCTOK ,                   RETURN KEYWORD                               
         PRETURN (R0,R1)                                                        
         END                                                                    
         ELSE  BEGIN               USERID NOT FOUND                             
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*        userid                                                                 
*                                                                               
SCNUID   PROC                                                                   
         LA    R15,CPCACCT                                                      
         VCALL GETACCT             Convert userid to account                    
         IF    NZ,BEGIN            Unknown userid...                            
         TSEG  (R1),(R0)                                                        
         ERROR ' is an unknown user-ID.'                                        
         END                                                                    
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  TRY ACCOUNT,  ON ENTRY: R1,R0 - ACCOUNT FIELD, R2 - NSCNCB                   
*    R15 = 4, NOT ACCOUNT                                                       
*    R15 = 0, ACCOUNT                                                           
*    R15 = -4, INVALID ACCOUNT, APPROPRIATE MESSAGE PLACED IN SCAN CB           
*                                                                               
TRYACCT  PROC  ,                                                                
         IF    (R0,NE,6),TNOTACCT                                               
         IF    (@R1+3,EQ,'.'),BEGIN  Close, but wrong...                        
*        SCTOK                                                                  
*        SCSEG (R1),(R0)                                                        
*        SCSEG ': invalid; '                                                    
*        SCSEG 'account must be specified in "gg.uuu" format.'                  
*        B     TBADACCT                                                         
         B     TNOTACCT            less stringent, may be userid                
         END                                                                    
*                                                                               
         IF    ((@R1+2,EQ,'.'),OR,(@R1+2,EQ,'$'),OR,                   *        
               (@R1+3,EQ,'$')),BEGIN  gg.uuu, gg$uuu, or uuu$gg...              
         ACALL CKACCT                                                           
         BNZ   TBADACCT                                                         
         CLEAR R15                 ACCOUNT OK                                   
         B     TACCDONE                                                         
         END                                                                    
*                                                                               
         B     TNOTACCT            NOT EVEN CLOSE                               
*                                                                               
TNOTACCT LABEL ,                                                                
         LA    R15,4                                                            
         B     TACCDONE                                                         
*                                                                               
TBADACCT LABEL ,                                                                
         L     R15,=F'-4'                                                       
         B     TACCDONE                                                         
*                                                                               
TACCDONE PEND                                                                   
         EJECT                                                                  
*                                                                               
*  CHECK ACCOUNT,  ON ENTRY: R1,R0 - ACCOUNT FIELD, R2 - NSCNCB                 
*    R15 = 0, ACCOUNT                                                           
*    R15 = -4, INVALID ACCOUNT, APPROPRIATE MESSAGE PLACED IN SCAN CB           
*                                                                               
CKACCT   PROC ,                                                                 
         IF    (R0,NE,6),BEGIN                                                  
         B     CBADACCT                                                         
         END                                                                    
         LR    R6,R1                                                            
*-                                                                              
*-       Allow "gg.uuu" or "gg$uuu".                                            
*-                                                                              
         IF    ((@R6+2,EQ,'.'),OR,(@R6+2,EQ,'$')),BEGIN                         
         SETMSG @R6,2                                                           
         VCALL CHARCHKA                                                         
         BNZ   CBADCHAR                                                         
         SETMSG @R6+3,3                                                         
         VCALL CHARCHKA                                                         
         BNZ   CBADCHAR                                                         
         END                                                                    
*-                                                                              
*-       Allow "uuu$gg" for compatibility.                                      
*-                                                                              
         ELSEIF (@R6+3,EQ,'$'),BEGIN  uuu$gg...                                 
         SETMSG @R6,3                                                           
         VCALL CHARCHKA                                                         
         BNZ   CBADCHAR                                                         
         SETMSG @R6+4,2                                                         
         VCALL CHARCHKA                                                         
         BNZ   CBADCHAR                                                         
         END                                                                    
*                                                                               
         ELSEIF (@R6+3,EQ,'.'),BEGIN  Close, but wrong...                       
         B     CBADFRMT                                                         
         END                                                                    
*                                                                               
         ELSE BEGIN                Totally wrong...                             
         B     CBADACCT                                                         
         END                                                                    
*                                                                               
         COMMENT                   OK ACCOUNTS FALL THRU HERE                   
         CLEAR R15                 ACCOUNT OK                                   
         B CACCDONE                                                             
*                                                                               
CBADACCT LABEL ,                                                                
         SCTOK                                                                  
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid account.  '                                           
         L     R15,=F'-4'                                                       
         B     CACCDONE                                                         
*                                                                               
CBADCHAR LABEL ,                                                                
         SCTOK                                                                  
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid characters in account.  '                             
         L R15,=F'-4'                                                           
         B CACCDONE                                                             
*                                                                               
CBADFRMT LABEL ,                                                                
         SCTOK                                                                  
         SCSEG 'Account must be specified in "gg.uuu" format.  '                
         L     R15,=F'-4'                                                       
         B     CACCDONE                                                         
*                                                                               
CACCDONE LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  scan user                                                                    
*                                                                               
SCNUSER  PROC  ,                                                                
         SCUPTOK R1                                                             
         IF    ((R0,EQ,1),AND,(@R1,EQ,'&&')),'SETMSG CPSUSER'                   
         IF    (R0,NE,3),BEGIN                                                  
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid user. '                                               
         L     R15,=F'-4'                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL CHARCHKA                                                         
         IF    NZ,BEGIN                                                         
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid characters in user-id.'                               
         L     R15,=F'-4'                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   CPCUSER,@R1                                                      
         CLEAR R15                                                              
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
*   scan group                                                                  
*                                                                               
SCNGRP   PROC  ,                                                                
         SCUPTOK R1                                                             
         IF    ((R0,EQ,1),AND,(@R1,EQ,'@')),'SETMSG CPSGRP'                     
         IF    (R0,NE,2),BEGIN                                                  
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid group. '                                              
         L     R15,=F'-4'                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL CHARCHKA                                                         
         IF    NZ,BEGIN                                                         
         SCSEG (R1),(R0)                                                        
         SCSEG ': invalid characters in group. '                                
         L     R15,=F'-4'                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   CPCGRP,@R1                                                       
         CLEAR R15                                                              
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Editing Command Scan Tables'                                    
*-                                                                              
*-       list/number/etc options                                                
*-                                                                              
LTNPSH   XPUSHRTN LTNKWS           LIST OPTIONS                                 
*                                                                               
LISTPSH  XPUSHRTN LISTKWS          LIST/NOLIST OPTIONS                          
*                                                                               
TEXTPSH  XPUSHRTN TEXTKWS          NOTEXT OPTIONS                               
*                                                                               
RNUMPSH  XPUSHRTN RNUMKWS          NUMBER OPTIONS                               
*                                                                               
NUMPSH   XPUSHRTN NUMKWS           NUMBER OPTIONS                               
*                                                                               
CASEPSH  XPUSHRTN CASEKWS          CASE (UPPER/LOWER) OPTIONS                   
*                                                                               
LENPSH   XPUSHRTN LENKWS           LENGTH OPTIONS                               
*                                                                               
VERPSH   XPUSHRTN VERKWS           VERIFY OPTIONS                               
*                                                                               
         EJECT                                                                  
*                                                                               
*  pushed tables                                                                
*                                                                               
LTNKWS   SCKW  ,LISTPSH,PUSH                                                    
         SCKW  ,TEXTPSH,PUSH                                                    
         SCKW  ,NUMPSH,PUSH                                                     
         SCKW  ,,END                                                            
*-                                                                              
*-       list/nolist prt                                                        
*-                                                                              
LISTKWS  SCKW  LIST,SCNLIST,A                                                   
         SCKW  NOLIST,SCNNLST,A                                                 
         SCKW  N,SCNNLST                                                        
         SCKW  ,,END                                                            
*-                                                                              
*-       notext prt                                                             
*-                                                                              
TEXTKWS  SCKW  NOTEXT,SCNNTEX,(A,END)                                           
*-                                                                              
*-       numbered prt                                                           
*-                                                                              
RNUMKWS  SCKW  RNUMBERED,SCNRNUM,A                                              
NUMKWS   SCKW  NUMBERED,SCNNUM,A                                                
         SCKW  UNNUMBERED,SCNUNUM,A                                             
         SCKW  NONUMBERED,SCNONUM,A                                             
         SCKW  INTEGER,SCNINT,A                                                 
         SCKW  ,,END                                                            
*-                                                                              
*-       case (upper/uplow) prt                                                 
*-                                                                              
CASEKWS  SCKW  UPPER,SCNUPP,A                                                   
         SCKW  UPLOW,SCNUPL,A                                                   
         SCKW  ,,END                                                            
*-                                                                              
*-       length prt                                                             
*-                                                                              
LENKWS   SCKW  NOLENGTH,SCNNOLEN,A                                              
         SCKW  LENGTH,SCNLEN,(A,P,PI),&LINESZ                                   
         SCKW  ,,END                                                            
*-                                                                              
*-       verify prt                                                             
*-                                                                              
VERKWS   SCKW  VERIFY,SCNVER,A                                                  
         SCKW  NOVERIFY,SCNNVER,A                                               
         SCKW  ,,END                                                            
         EJECT                                                                  
SCNLIST  DPROC ,                                                                
         CLEAR CPLFLG5.CPFNLST                                                  
         PEND  ,                                                                
*                                                                               
SCNNLST  DPROC ,                                                                
         SET   CPLFLG5.CPFNLST                                                  
         PEND  ,                                                                
*                                                                               
SCNNTEX  DPROC ,                                                                
         NI    CPLFLG5,255-CPFNONUM-CPFNLST  reset options & list               
         OI    CPLFLG5,CPFNTEX+CPFFOPT  turn on notext flag                     
         PEND  ,                                                                
*                                                                               
SCNONUM  DPROC ,                                                                
         SET   CPLFLG5.CPFNONUM*CPFFOPT                                         
         PEND  ,                                                                
*                                                                               
SCNUNUM  DPROC ,                                                                
         CLEAR CPLFLG5.CPFNONUM                                                 
         SET   CPLFLG5.CPFUNUM+CPFFOPT                                          
         PEND  ,                                                                
*                                                                               
SCNRNUM  DPROC ,                                                                
         CLEAR CPLFLG5.CPFNONUM                                                 
         SET   CPNFRNUM,CPLFLG5.CPFFOPT                                         
         PEND  ,                                                                
*                                                                               
SCNNUM   DPROC ,                                                                
         CLEAR CPLFLG5.CPFNONUM,CPNFRNUM                                        
         SET   CPLFLG5.CPFFOPT                                                  
         PEND  ,                                                                
*                                                                               
SCNINT   PROC  ,                                                                
         SET   CPLFLG5.CPNFINT                                                  
         IF    ^CPLFLG5.CPFFOPT,BEGIN If no option                              
         NI    CPLFLG5,255-CPFNONUM  no, turn on numbered                       
         SET   CPLFLG5.CPFFOPT     Set option given                             
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SCNUPP   DPROC ,                                                                
         SET   CPFCASE+CPFUPPER                                                 
         PEND  ,                                                                
*                                                                               
SCNUPL   DPROC ,                                                                
         SET   CPFCASE,(CPFUPPER,OFF)                                           
         PEND  ,                                                                
*                                                                               
SCNNOLEN DPROC ,                                                                
         LA    R15,&LINESZ                                                      
         STH   R15,CPLLEN                                                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SCNLEN   DPROC ,                                                                
         STH   R0,CPLLEN                                                        
         PEND  ,                                                                
*                                                                               
SCNVER   DPROC ,                                                                
         SET   CPFVER                                                           
         PEND  ,                                                                
*                                                                               
SCNNVER  DPROC ,                                                                
         CLEAR CPFVER                                                           
         PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-       columns stuff                                                          
*-                                                                              
COLSPSH  XPUSHRTN COLSKWS                                                       
*                                                                               
*  columns=x(/y)                                                                
*                                                                               
*  CHECK OUT COLS SCAN PRIMITIVE,,, !                                           
*                                                                               
COLSKWS  SCKW  COLUMNS,SCNCOLS,(A,V(MPCOLS))                                    
         SCKW  COLS,SCNCOLS,(A,V(MPCOLS))                                       
         SCKW  ,,END                                                            
         SPACE 2                                                                
*                                                                               
*   SAVE COL1 AND COL2 ,,, AS FOLLOWS                                           
*                                                                               
SCNCOLS  PROC  ,                                                                
         DECR  R0                  ZERO BIAS FIRST COLUMN                       
         STH   R0,CPFCOL                                                        
         IF    (R1,Z),BEGIN        DEFAULT SECOND COLUMN TO &LINESZ             
         LA    R1,&LINESZ                                                       
         END                                                                    
         STH   R1,CPLCOL                                                        
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
**                                                                              
**  SCAN COLLECT OPTIONS                                                        
**                                                                              
**                                                                              
*                                                                               
COLLPSH  XPUSHRTN COLLKWS                                                       
*                                                                               
COLLKWS  SCKW  COLLECT,SCNCOL,A                                                 
         SCKW  ,V(SCNNOOP),A(MCOLOPTS)                                          
         SCKW  ,,END                                                            
*                                                                               
SCNCOL   PROC  ,                                                                
*                                                                               
         COMMENT                   ENTRANCE EXAMS                               
         IF    (CPSACCT,Z),BEGIN                                                
         SCSEG 'Collect option not allowed before logon.'                       
         L     R15,=F'-4'                                                       
         B     SCOLDONE                                                         
         END                                                                    
         IF    CPOFNCHG,BEGIN      ACTIVE file mods blocked...                  
         SCSEG 'ACTIVE file changes blocked by ORVYL program.'                  
         L     R15,=F'-4'                                                       
         B     SCOLDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN FOR OPTIONAL COLLECT LINENO             
         SET   CPFDUMP             Route output to active file                  
*        VCALL BLNKTOK   DEBUG     Blank token if wanted   ???                  
         SCPUSH                                                                 
         SCAN  COLLN                                                            
         IF    (R15,NE,4),BEGIN    IF NOT FOUND, RESTORE SCAN                   
         SCPOP                                                                  
         END                                                                    
         CLEAR R15                                                              
SCOLDONE LABEL ,                                                                
         PEND                                                                   
*                                                                               
COLLN    SCKW  ,CNOLINE,NULL                                                    
         SCKW  ,COLLINE,SYMLN                                                   
         SCKW  ,CNOLINE,(END,DONTBLANK)       DEBUG DONTBLANK                   
*                                                                               
CNOLINE  PROC  ,                                                                
         MVI   CPDMPLST,X'80'      No lineno yet                                
         MVC   CPACLNO,=F'-1000'   No current lineno                            
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
COLLINE  PROC  ,                                                                
         SET   CPLCOLXP                                                         
         ST    R0,CPDMPLST                                                      
         ST    R1,CPDMPDLT                                                      
*        VCALL BLNKTOK            ??? HELP FIX  DEBUG                           
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
*  MCOLOPTS ,,,, MATCH IF COLLECT OPTION                                        
*                                                                               
*  NOTE: THIS ROUTINE WILL NOT ONLY MATCH BUT WILL DO                           
*  PROCESSING.  THUS PROCESSING ROUTINE IS ONLY A NOOP.                         
*                                                                               
MCOLOPTS PROC ,                                                                 
*                                                                               
         IF    CPFDUMP,BEGIN                                                    
         SCPUSH ,                                                               
         SCBKUP                                                                 
         COMMENT                   DEBUG,,, NOBLANK WHAT HOW WHERE              
         SCAN  CHKKWS                                                           
         IF    (R15,NEG),BEGIN     IF ERROR, ,,                                 
         END                                                                    
         ELSEIF (R15,EQ,4),BEGIN   IF COLLECT OPTION MATCH, ,,                  
         CLEAR R15                                                              
         END                                                                    
         ELSEIF (R15,NE,4),BEGIN   IF NOT A MATCH, ,,                           
         SCPOP                                                                  
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN ,             NOT CPFDUMP                                  
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
CHKKWS   SCKW  ,NOTCOPT,NULL                                                    
         SCKW  BY,COLBY,(P,LN)        ???                                       
         SCKW  CLEAR,COLCLR,A                                                   
         SCKW  CLR,COLCLR,A                                                     
         SCKW  KEEP,COLKEEP,A                                                   
         SCKW  NOCLEAR,COLKEEP,A                                                
         SCKW  NOCLR,COLKEEP,A                                                  
         SCKW  WARN,COLWARN,A                                                   
         SCKW  NOWARN,COLOWRN,A                                                 
         SCKW  ,NOTCOPT                                                         
*                                                                               
COLBY    PROC  ,                                                                
         IF    (R0,Z),BEGIN                                                     
         SCSEG '0: invalid. '                                                   
         L      R15,=F'-4'                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         ST    R0,CPDMPDLT        Collect delta                                 
         IF    CPDMPLST.X'80',BEGIN  No collect lineno yet...                   
         VCALL ENDLNO              Get END line-number                          
         ST    R0,CPDMPLST         Set starting lineno                          
         END                                                                    
         LA    R15,4                                                            
         END                                                                    
         PEND                                                                   
*                                                                               
COLCLR   DPROC ,                                                                
         SET   CPFCLEAR                                                         
         CLEAR CPFKEEP                                                          
         SCPUSH ,                                                               
         VCALL CLRTEXT                                                          
         SCPOP ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
COLKEEP  DPROC ,                                                                
         SET   CPFKEEP                                                          
         CLEAR CPFCLEAR                                                         
         SCPUSH ,                                                               
         VCALL CLRTEXT                                                          
         SCPOP ,                                                                
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
COLOWRN  DPROC ,                                                                
         SET   CPFNWARN                                                         
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
COLWARN  DPROC ,                                                                
         CLEAR CPFNWARN                                                         
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
NOTCOPT  DPROC ,                                                                
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*-                                                                              
*-       quiet,, other stuff                                                    
*-                                                                              
NOOPTPSH XPUSHRTN NOOPTKWS                                                      
*                                                                               
INTPSH   XPUSHRTN INTKWS                                                        
*                                                                               
QUIETPSH XPUSHRTN QUIETKWS                                                      
*                                                                               
*                                                                               
*  push sckws                                                                   
*                                                                               
NOOPTKWS SCKW  ,V(SCNINVAL)                                                     
*                                                                               
INTKWS   SCKW  INTERNAL,SCNINTRL,A                                              
         SCKW  ,,END                                                            
*                                                                               
QUIETKWS SCKW  QUIET,SCNQUIET,A                                                 
         SCKW  ,,END                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
*  routines                                                                     
*                                                                               
SCNINTRL DPROC ,                                                                
         SET   CPFINT                                                           
         PEND                                                                   
*                                                                               
SCNQUIET DPROC ,                                                                
         SET   CPFQUIET            don't write msgs                             
         PEND  ,                                                                
         TITLE 'SCRATCH/CLEAR PRTs'                                             
*-                                                                              
*-       scratch/clear stuff                                                    
*-                                                                              
SCRPSH   XPUSHRTN SCRKWS                                                        
*                                                                               
CLSCRPSH XPUSHRTN CLSCRKWS                                                      
*                                                                               
CLRPSH   XPUSHRTN CLRKWS                                                        
*                                                                               
*                                                                               
* sckws                                                                         
*                                                                               
SCRKWS   SCKW  SCRATCH,SCNSCR,A                                                 
         SCKW  REPLACE,SCNSCR,A                                                 
         SCKW  ,,END                                                            
*                                                                               
* clear/noclear and scratch/noscratch                                           
*                                                                               
CLSCRKWS SCKW  ,SCRPSH,PUSH                                                     
CLRKWS   SCKW  CLEAR,SCNCLR,A                                                   
         SCKW  CLR,SCNCLR,A                                                     
         SCKW  KEEP,SCNKEEP,A                                                   
         SCKW  NOCLEAR,SCNKEEP,A                                                
         SCKW  NOCLR,SCNKEEP,A                                                  
         SCKW  WARN,SCNWARN,A                                                   
         SCKW  NOWARN,SCNOWRN,A                                                 
         SCKW  ,,END                                                            
*                                                                               
*  routines                                                                     
*                                                                               
SCNSCR   DPROC ,                                                                
         SET   CPLFLG2.CPFSCRTC                                                 
         PEND  ,                                                                
*                                                                               
SCNCLR   DPROC ,                                                                
         SET   CPFCLEAR                                                         
         CLEAR CPFKEEP                                                          
         PEND  ,                                                                
*                                                                               
SCNKEEP  DPROC ,                                                                
         SET   CPFKEEP                                                          
         CLEAR CPFCLEAR                                                         
         PEND  ,                                                                
*                                                                               
SCNOWRN  DPROC ,                                                                
         SET   CPFNWARN                                                         
         PEND  ,                                                                
*                                                                               
SCNWARN  DPROC ,                                                                
         CLEAR CPFNWARN                                                         
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'Line Number Recognition Routine'                                
*                                                                               
*  LINE NUMBER RECOGNITION ROUTINE DELETED !!,                                  
*  LINE NUMBERS NOW RECOGNIZED BY SCANNER !!                                    
*                                                                               
*  line numbers (symbolic ones too) are now recognized by                       
*  by the scanner.  use the SYMLN flag.  (eg SCKW ,ROUTINE,SYMLN ).             
*  the processing routine will be entered with R0 - line number,                
*  R1 - delta.  go for it.                                                      
*                                                                               
*  what follows are the old LNENOPRT comments.  Remove in 1991.                 
*                                                                               
* ----------------------------------------------------------                    
*                                                                               
* to use this prt, register r6 must be set to the desired                       
* return address if a line number is recognized                                 
*                                                                               
* R0 contains the specified line number on exit                                 
* R1 has a delta that can be used by collect, move and copy                     
* R15 has return codes to show which way the line no was                        
* specified.                                                                    
* R15 -  0 regular line number specified                                        
*        4 first specified                                                      
*        8 current specified                                                    
*       12 last specified                                                       
*       16 end specified                                                        
*       20 next specified                                                       
*       24 previous specified                                                   
*       28 return specified                                                     
*       32 ATTN specified                                                       
*       36 ABORT specified                                                      
*                                                                               
* after the line number specified has been processed,                           
* an increment is scanned for and added in if found                             
* unless cpfnoinc bit is on in cplflg4.                                         
*                                                                               
*         ENTRY LNENOPRT,LNENOPRN                                               
*LNENOPRT SCKW  F,SCNFRST                                                       
*         SCKW  L,SCNLAST                                                       
*         SCKW  RETURN,SCNRETN,A                                                
*LNENOPRN SCKW  ,SCNLNENO,LN                                                    
*         SCKW  FIRST,SCNFRST,A                                                 
*         SCKW  CURRENT,SCNCURR,A                                               
*         SCKW  LAST,SCNLAST,A                                                  
*         SCKW  END,SCNEND                                                      
*         SCKW  NEXT,SCNNEXT,A                                                  
*         SCKW  PREVIOUS,SCNPREV,A                                              
*         SCKW  FBLOCK,SCNFBLK,A                                                
*         SCKW  LBLOCK,SCNLBLK,A                                                
*         SCKW  *,SCNCURR                                                       
**IOWA*   SCKW  ATTN,SCNATTN,A                                                  
**IOWA*   SCKW  ATTENTION,SCNATTN,A                                             
**IOWA*   SCKW  ABORT,SCNABORT,A                                                
*         SCKW  ,,POP                                                           
*         SCKW  ,V(INVALID)                                                     
*                                                                               
*  ROUTINES DELETED                                                             
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ATTNLINE - GET ATTENTION LINE NUMBER                                         
*                                                                               
*  ON ENTRY:                                                                    
*       SCANCB - POINTS JUST PAST 'ATTN' KEYWORD                                
*                                                                               
*  ON EXIT:                                                                     
*       SCANCB - UPDATED                                                        
*       R0 - ATTENTION LINE NUMBER                                              
*                                                                               
*                                                                               
*  WE TREAT TWO CASES: 1. IN OLD EXECS A LINE NUMBER                            
*     MUST BE SPECIFIED.  2. IN EXTENDED EXECS A LABEL                          
*     MUST BE SPECIFIED.                                                        
*                                                                               
*                                                                               
ATTNLINE XPROC ,                                                                
*                                                                               
         IF    ^CPFEXEC,BEGIN                                                   
         TSEG  'ATTN: invalid. '                                                
         ERROR 'ATTN option may only be used in EXEC files. '                   
         END                                                                    
*                                                                               
         ELSEIF  CPFXOLD,BEGIN     IF OLD,                                      
         IC    R2,CPFRDSRC         GET LINENO IS STANDARD (OLD)                 
         CLEAR CPFRDSRC                FASION                                   
         SET   CPFRDEXE                                                         
         SCAN  ATTNLNKW                                                         
         SCANCHK                                                                
         LR    R0,R3                                                            
         STC   R2,CPFRDSRC                                                      
         END                                                                    
*                                                                               
         COMMENT                   IF EXTENDED MODE, LABELS ONLY                
         COMMENT                                                                
         ELSEIF CPFXTEND,BEGIN     PROCSSES ATTN = LABEL                        
         SCINFO                                                                 
         AR    R0,R1                                                            
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN    SKIP BLANKS               
         LA    R1,1(R1)                                                         
         END                                                                    
         IF    ((@R1,EQ,'='),AND,(R1,LT,R0)),BEGIN    SKIP '='                  
         LA    R1,1(R1)                                                         
         END                                                                    
         WHILE ((@R1,EQ,' '),AND,(R1,LT,R0)),BEGIN    SKIP BLANKS               
         LA    R1,1(R1)                                                         
         END                                                                    
         SR    R0,R1                                                            
         SCINIT (R1),(R0)                                                       
*                                                                               
         VCALL LABELLNO            PROCESS LABEL                                
         COMMENT                   RETURNS R0=LABEL LINENO                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               MUST HAVE BEEN ONE OF ABOVE                  
         BOMB                                                                   
         END                                                                    
*                                                                               
         PRETURN (R0)                                                           
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*  OLD EXEC ATTN LINE SCANNING, SCKWS ROUTINES                                  
*                                                                               
ATTNLNKW SCKW  ,NOATTNLN,NULL                                                   
         SCKW  ,GOTALINE,SYMLN                                                  
         SCKW  ,V(SCNINVAL)                                                     
*                                                                               
NOATTNLN PROC  ,                                                                
         SCSEG 'Missing attention line number. '                                
         L     R15,=F'-4'                                                       
         PEND                                                                   
*                                                                               
GOTALINE PROC  ,                                                                
         LR    R3,R0                                                            
         PRETURN (R3)                                                           
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Scan Line for EXECUTE and/or FROM'                              
*                                                                               
* scan line to see if select from active, execute or external                   
*                                                                               
* NOTE:                                                                         
* If nothing set on entry we default to active.                                 
*                                                                               
SCNEX    XPROC ,                   Allow active/exec                            
         IF    (CPFRDSRC,EQ,0),BEGIN  Default to active                         
         SET   CPFRDACT                                                         
         END                                                                    
         SCPUSH                                                                 
         SCAN EXKWS                                                             
         SCPOP ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SCNEXFR  XPROC ,                   Allow active/exec/from                       
         IF    (CPFRDSRC,EQ,0),BEGIN  Default to active                         
         SET   CPFRDACT                                                         
         END                                                                    
         SCPUSH                                                                 
         SCAN EXFRKWS                                                           
         SCPOP ,                                                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
EXKWS    SCKW  EXECUTE,GOTEXEC,A                                                
         SCKW  ACTIVE,GOTACT,A                                                  
         SCKW  ,SKIPIT                                                          
*                                                                               
EXFRKWS  SCKW  FROM,GOTFROM,A                                                   
         SCKW  EXECUTE,GOTEXEC,A                                                
         SCKW  ACTIVE,GOTACT,A                                                  
         SCKW  ,SKIPIT                                                          
*                                                                               
GOTFROM  PROC  ,                                                                
         SET   CPFRDEXT                                                         
         MVC   CPSEQFLD(4),=H'-1,8'  initialize seqflds                         
         MVI   CPLRCL,X'80'        Set file type to undefined                   
         CLEAR (CPDSNWA,DSSIZ)     Clear dsname work area                       
         MVI   DSNWADSN,C' '       Now blank first part                         
         MVC   DSNWADSN+1(DSNWACLR-1),DSNWADSN  of the area                     
         CLEAR DSNON                                                            
         OI    CPLFLG1,CPFDSN1     Set flag for dodsnm init.                    
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
GOTACT   PROC  ,                                                                
         SET   CPFRDACT                                                         
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
GOTEXEC  PROC  ,                                                                
         SET   CPFRDEXE                                                         
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SKIPIT   DPROC ,                                                                
         PEND                                                                   
         QLTORG                                                                 
         TITLE 'DSNSCAN/DODSNM Routines'                                        
*box                                                                            
*                                                                               
*  DSNSCAN -- Entered as SCKW routine to process dsname.                        
*                                                                               
DSNSCAN  XPROC ,                                                                
         ACALL DODSNM                                                           
         CLEAR R15                 KEEP SCANNING                                
         PEND  ,                                                                
* dodsnm takes 1st token found as dsname and handles it.                        
*                                                                               
* errors are resolved internally                                                
DODWA    RECORD BEGIN                                                           
DODSNAME DS    CL256               DS NAME WORK AREA                            
DODSKIPT DS    CL256                                                            
DODSTOPT DS    CL256                                                            
         END                                                                    
*                                                                               
*                                                                               
DODSNM   XPROC DODWA                                                            
         IF    CPLFLG1.CPFDSN1,BEGIN  This is a re-entry...                     
         MVI   DSNWADSN,X'40'      Blank part of work area                      
         MVC   DSNWADSN+1(DSNWACL2-1),DSNWADSN                                  
         CLEAR DSNWLEN,DSNWFILE                                                 
         MVI   DSNWAF1,0           Reset attribute types                        
         END                                                                    
*                                                                               
         ELSE  BEGIN               First time initialization...                 
         SET   CPLFLG1.CPFDSN1     Set first entry done                         
         MVC   CPSEQFLD(4),=H'-1,8'  Initialize seqflds                         
         MVI   CPLRCL,X'80'        Set file type to undefined                   
         CLEAR (CPDSNWA,DSSIZ)     Clear dsname work area                       
         MVI   DSNWADSN,X'40'                                                   
         MVC   DSNWADSN+1(DSNWACLR-1),DSNWADSN                                  
         CLEAR DSNON                                                            
         END                                                                    
*                                                                               
         SR    R6,R6               Zero length counter                          
         LA    R5,DSNWADSN         Addr of dsname work area                     
*-                                                                              
*-      Scan next operand for a dsname.  We save and reset the                  
*-         scan skip/stop chars because we want to allow special                
*-         characters in the dsname that would ordinarily stop the              
*-         scan (such as / and -).  Network file names have these               
*-         characters in them.                                                  
*-                                                                              
*                                                                               
         COMMENT                   SCAN DATA SET NAME                           
*                                                                               
         SCPUSH TABLES                                                          
         SETSKIP DODSKIPT,' ,='    SET NEW SKIP/STOP CHARS                      
         SETSTOP DODSTOPT,' ,;('                                                
*                                                                               
         COMMENT                   SCAN NAME                                    
         SCAN  ,TABLES=NOPUSH      SCAN NAME                                    
         SCANCHK                                                                
         SCPOP TABLES                                                           
         IF    (R0,Z),BEGIN        IF MISSING NAME, ERROR                       
         IF    CPFDSNNE,BEGIN                                                   
         MVC   CPERRID,=CL8'MISSDSN'                                            
         B     DOERREX                                                          
         END                                                                    
         ERROR 'Missing data set name.',,MISSDSN                                
         END                                                                    
         IF    (@R1,EQ,'('),BEGIN  IF '(' IS DSN NAME, ERROR                    
         IF    CPFDSNNE,BEGIN                                                   
         MVC   CPERRID,=CL8'BADDSN'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR '(: invalid data set name.'                                      
         END                                                                    
*                                                                               
         COMMENT                   MOVE NAME TO WORK AREA                       
         L     R14,R0                                                           
         MOVE  R14,DODSNAME,@R1                                                 
         LA    R1,DODSNAME                                                      
*                                                                               
         STM   R0,R1,CPSAVE        Save scan token ptrs (for later)             
*                                                                               
         COMMENT                   DEQUOTE APPROPRIATELY                        
         VCALL DEQUOTE             Dequote if quoted                            
         IF    NM,'SET DSNWAF1.DSNFQDSN'  Quoted dsname                         
*-                                                                              
*-       Save the file name as the user typed it (for network                   
*-         files).  This entire data set name scanning code needs               
*-         to be re-coded.                                                      
*-                                                                              
         LR    R3,R0                                                            
         CEIL  R3,L'DSNWFILE                                                    
         STH   R3,DSNWLEN          Save file name length                        
         MVC   DSNWFILE,CVBLANKS   Pre-blank                                    
         MOVE  R3,DSNWFILE,@R1     Save file name (in uplow)                    
*                                                                               
         IF    ^DSNWAF1.DSNFQDSN,BEGIN  Not-quoted dsname...                    
         LR    R15,R0                                                           
         L     R14,CVUPPTBL                                                     
         DTEX  R15,'TR @R1(0),@R14'   Convert to upper case                     
         END                                                                    
*                                                                               
         COMMENT                   R4,R3 - LOC LEN OF DSN NAME                  
         LR    R4,R1                                                            
         LR    R3,R0                                                            
*-                                                                              
*-       If not quoted, check for "<dev>" and "volume:" prefixes.               
*-                                                                              
         IF    ^DSNWAF1.DSNFQDSN,BEGIN  Not quoted dsname...                    
*-                                                                              
*-       Check for <device-type> prefix.                                        
*-                                                                              
         IF    ((R3,POS),AND,                                          *        
               (@R4,EQ,'<')),BEGIN  We have <>...                               
         LA    R1,@R1+1            Skip past "<"                                
         DECR  R0                                                               
*                                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,'>'),EXIT   End quote, scram                             
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         IF    (R0,Z),BEGIN                                                     
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  'Missing ">" in '                                                
         ABORT (R4),(R3)                                                        
         END                                                                    
*                                                                               
         SCPUSH                                                                 
*                                                                               
         XPUSH R0,R1                                                            
*                                                                               
         LR    R0,R1                                                            
         SR    R0,R4                                                            
         DECR  R0                  Length of text inside brackets               
         SCINIT @R4+1,(R0)                                                      
*                                                                               
         XPOP  R0,R1                                                            
         LA    R4,@R1+1            Dsname ptr past [dev]                        
         LR    R3,R0               Dsname length                                
         DECR  R3                                                               
*                                                                               
         SCAN  DEVPRT                                                           
         SCANCHK                                                                
         IF    (R15,NZ),BEGIN                                                   
         IF    CPFDSNNE,BEGIN                                                   
         B     DOERREX                                                          
         END                                                                    
         ELSE  ' BOMB '                                                         
         END                                                                    
         SCPOP                                                                  
         END                                                                    
*-                                                                              
*-       Check for volume:dsname notation.                                      
*-                                                                              
         COMMENT                   RESET LOC,LEN                                
         LR    R1,R4                                                            
         LR    R0,R3                                                            
*                                                                               
         WHILE (R0,POS),BEGIN      Scan for a :...                              
         IF    ((@R1,EQ,'#'),OR,(@R1,EQ,'(')),EXIT  Scram if member             
*                                                                               
         IF    (@R1,EQ,':'),BEGIN                                               
         LR    R15,R1                                                           
         SR    R15,R4                                                           
         IF    POS,BEGIN                                                        
         IF    (@R4,EQ,'CAT'),'CLEAR R15'  Allow "CAT:"                         
         IF    (@R4,EQ,'TEM'),BEGIN  Allow "TEMP:"...                           
         XPUSH R0,R1               Exit will wipe R0,R1                         
         VCALL EXIT2               Get scratch volser                           
         LR    R4,R1               Copy volser ptr                              
         LR    R15,R0              Copy volser length                           
         XPOP  R0,R1               Restore R0,R1                                
         END                                                                    
         CEIL  R15,L'DSNON         Not too long now                             
*                                                                               
         MVC   DSNON,CVBLANKS                                                   
         MOVE  R15,DSNON,@R4       Set volume                                   
         IF    (DSNON,EQ,CVBLANKS),'CLEAR DSNON'                                
         END                                                                    
*                                                                               
         LA    R4,@R1+1            Update dsname...                             
         LR    R3,R0               ...ptrs                                      
         DECR  R3                                                               
*                                                                               
         IF    (R3,NP),BEGIN       Null dsname...                               
         CLEAR DSNWANL             No dsname                                    
         MVC   DSNWADSN,CVBLANKS   Nada                                         
         CLEAR DSNWAMBR            No member either                             
         CLEAR DSNWAF1.DSNFSTD     Not a standard file name                     
         B     DSCNEXIT            Scam                                         
         END                                                                    
*                                                                               
         B     DMORE                                                            
         END                                                                    
*                                                                               
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
DMORE    LABEL ,                                                                
         COMMENT                   R4,R3 - LOC, END OF NAME                     
         AR    R3,R4               End of operand +1                            
*  If 'WYL.' specified treat as full dsname.                                    
         CLC   =C'WYL.',0(R4)                                                   
         BE    MAINDSN                                                          
*  Check for "SYSn." and treat as full dsname.                                  
         IF    (@R4,EQ,'SYS'),BEGIN                                             
         IF    (@R4+4,NE,'.'),EXIT                                              
         IF    ((@R4+3,LT,'0'),OR,(@R4+3,GT,'9')),EXIT                          
         B     MAINDSN             Ok, treat as full dsname                     
         END                                                                    
*-                                                                              
*-       For network file names:  If file name begins with % then               
*-         it is a network file.                                                
*-                                                                              
         IF    (@R4,EQ,'%'),BEGIN  Also a network file name...                  
         IF    (DSNON,Z),BEGIN     No volume...                                 
         IF    (CPHOST,Z),BEGIN    No default host...                           
         TSEG  'Can''t access remote file, no default host set.',,CR            
         ABORT 'Type HELP SET HOST for more information.',,REMFILE              
         END                                                                    
*                                                                               
         MVC   DSNON,CVBLANKS                                                   
         MVC   DSNON(L'CPHOST),CPHOST  Set default host                         
*                                                                               
         IF    (DSNWFILE,EQ,'%'),BEGIN  Tidy up other dsname...                 
         MVC   DSNWFILE(L'DSNWFILE-1),DSNWFILE+1  Remove %                      
         MVI   DSNWFILE+L'DSNWFILE-1,C' '                                       
         DECR  R15,DSNWLEN         Adjust file name length                      
         IF    (R15,NEG),'CLEAR DSNWLEN'                                        
         END                                                                    
         END                                                                    
*                                                                               
         LA    R4,@R4+1            Skip over %                                  
         B     MAINDSN                                                          
         END                                                                    
*-                                                                              
*-       If '$' specified its full dsname.                                      
*-                                                                              
         IF    (@R4,EQ,'$'),BEGIN                                               
         LA    R4,1(R4)            Bump past $                                  
         MVI   DSNWANL,C'$'        Flag $ dsname for later                      
         B     MAINDSN                                                          
         END                                                                    
*  full dsn not spec - construct first part of dsn as default                   
         MVC   0(4,R5),=C'WYL.'                                                 
         MVC   DSNXG(DSNXGL-1,R5),CPGRPSV                                       
         MVI   DSNXG+DSNXGL-1(R5),C'.'                                          
         MVC   DSNXU(DSNXUL-1,R5),CPUSERSV                                      
         MVI   DSNXU+DSNXUL-1(R5),C'.'                                          
*                                                                               
         IF    (CPPACCT,NZ),BEGIN  PACCOUNT is set...                           
         IF    ^CPFEXEC,EXIT       Only do if in exec mode                      
         MVC   DSNXG(DSNXGL-1,R5),CPPGRP                                        
         MVC   DSNXU(DSNXUL-1,R5),CPPUSER                                       
         END                                                                    
         LA    R6,DSNXN                                                         
         SPACE 2                                                                
*-                                                                              
*- SCAN &USER OR @GROUP                                                         
*-                                                                              
         COMMENT                   PROCESS & OR @                               
         IF    (R4,LT,R3),BEGIN                                                 
         WHILE ((@R4,EQ,'@'),OR,(@R4,EQ,'&&')),BEGIN                            
*                                                                               
         IF    (@R4,EQ,'@'),BEGIN                                               
         INCR  R4                                                               
         LR    R1,R4                                                            
         WHILE ((R4,LT,R3),AND,(@R4,NE,'.'),AND,(@R4,NE,'#')),BEGIN             
         INCR  R4                                                               
         END                                                                    
         LR    R0,R4                                                            
         SR    R0,R1                                                            
         IF    (@R4,EQ,'.'),BEGIN                                               
         INCR  R4                                                               
         END                                                                    
         COMMENT                   R1,R0 - @GROUP LOC, LEN                      
         IF    (R0,Z),BEGIN                                                     
         MVC   CPCGRP,CPSGRP                                                    
         END                                                                    
         ELSEIF (R0,EQ,2),BEGIN                                                 
         MVC   CPCGRP,@R1                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'INVGRP'                                             
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid group.',,INVGRP                                       
         END                                                                    
         END   , OF GROUP STUFF                                                 
*                                                                               
         ELSEIF (@R4,EQ,'&&'),BEGIN                                             
         INCR  R4                                                               
         LR    R1,R4                                                            
         WHILE ((R4,LT,R3),AND,(@R4,NE,'.'),AND,(@R4,NE,'#')),BEGIN             
         INCR  R4                                                               
         END                                                                    
         LR    R0,R4                                                            
         SR    R0,R1                                                            
         IF    (@R4,EQ,'.'),BEGIN                                               
         INCR  R4                                                               
         END                                                                    
         COMMENT                   R1,R0 - @GROUP LOC, LEN                      
         IF    (R0,Z),BEGIN                                                     
         MVC   CPCUSER,CPSUSER                                                  
         END                                                                    
         ELSEIF (R0,EQ,3),BEGIN                                                 
         MVC   CPCUSER,@R1                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'INVGRP'                                             
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid user.',,INVGRP                                        
         END                                                                    
         END   , OF USER STUFF                                                  
         END                                                                    
         END                                                                    
         EJECT                                                                  
*                                                                               
**  process dsn after overrides or entire dsname                                
*                                                                               
MAINDSN  AR    R5,R6               Update dsn pointer                           
         LR    R2,R3               End of operand                               
         SR    R2,R4               Length remaining                             
         BNP   ENDSNOP             Br if nothing left                           
*  check whether use of prefix was specified                                    
         CLI   0(R4),C'*'          Prefix char?                                 
         BNE   NOPFX                                                            
*-                                                                              
*-       Don't allow "*XYZ" as a dsname; it's "*" or nothing.                   
*-       (But all the code to handle "*xxx" still remains below).               
*-                                                                              
         IF    (R2,NE,1),BEGIN     Only allow "*" as dsname...                  
         IF    ((@R4+1,EQ,'#'),OR,(@R4+1,EQ,'(')),EXIT  "*#member"              
         IF    CPFDSNNE,BEGIN      Error suppression...                         
         MVC   CPERRID,=CL8'STARBAD'                                            
         B     DOERREX                                                          
         END                                                                    
         TSEG  (R4),(R2)                                                        
         ERROR ': invalid characters in dsname.',,STARBAD                       
         END                                                                    
**  prefix to be used                                                           
         SET   DSNWAF1.DSNFQDSN    Avoid OS dsname check                        
         IF    (CPAFNAMEL,EQ,0),BEGIN                                           
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'NOSTAR'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR 'The current dsname is not set.',,NOSTAR                         
         END                                                                    
         IF    (DSNON,Z),BEGIN     DO HOST                                      
         L     R2,CPAHOSTL                                                      
         IF    (R2,POS),BEGIN                                                   
         CEIL  R2,L'DSNON                                                       
         MVC   DSNON,CVBLANKS                                                   
         MOVE  R2,DSNON,CPAHOST                                                 
         END                                                                    
         END                                                                    
         XPUSH R14,R1              DO MEMBER IF ANY                             
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         IF    (R0,POS),BEGIN                                                   
         MVC   DSNWAMBR,CVBLANKS                                                
         LR    R2,R0                                                            
         MOVE  R2,DSNWAMBR,@R1                                                  
         END                                                                    
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         LR    R2,R0               R2 prefix length                             
         XPOP  R14,R1                                                           
DOPFX    LA    R4,1(R4)            Past *                                       
**  insert prefix                                                               
         LA    R15,CPAFNAME                                                     
         IF    (@R15,EQ,'WYL.'),BEGIN  Wylbur dsn...                            
         IF    (R2,LE,11),EXIT  Nope                                            
         IF    ((@R15+3,NE,'.'),OR,(@R15+6,NE,'.')),EXIT                        
         IF    (@R15+10,NE,'.'),EXIT                                            
         SET   DSNWAF1.DSNFSTD     Standard dsn                                 
         B     PFXCOMA                                                          
         END                                                                    
*                                                                               
         CLI   @R15,C'$'           Is prefix entire dsn spec                    
*        BNE   INSPFX              Br if no                                     
         BNE   PFXCOMA             Br if no  CH - 05/91                         
         LA    R15,@R15+1          Past $                                       
         DECR  R2                  Decr len                                     
PFXCOMA  SR    R5,R6               Start over again                             
         LTR   R6,R6               Test if nothing generated yet                
         BZ    INSPFX              Bypass re-clear                              
         LA    R0,X'40'            Set up blank                                 
PFXLOOP  STC   R0,0(R6,R5)         Blank orig dsname                            
         BCT   R6,PFXLOOP          doesn't clear first char - tough             
         IF    (R2,NP),NOPFX       Br if $ was alone in pfx                     
INSPFX   AR    R6,R2               Compute total dsn length                     
         CH    R6,=H'44'           Is there room for pfx                        
         BH    DSNLONG             No                                           
         DEX   R2,' MVC 0(0,R5),@R15 '  Add prefix to dsn                       
         LA    R5,1(R2,R5)         Update dsn pointer                           
         SPACE 2                                                                
** continue processing operand                                                  
NOPFX    CLR   R4,R3               Is scan at end of operand                    
         BNL   ENDSNOP             Br if yes                                    
         CLI   0(R4),C'('          Test paren specified alone                   
         BE    ENDSNOP             Br if yes                                    
*  scan for member spec (#) - scanner takes care of parens                      
         LR    R0,R3                                                            
         SR    R0,R4               Length remaining                             
         LR    R2,R4               Current pointer                              
MEMSRCH  CLI   @R2,C'#'                                                         
         BE    HVDSNSTR            Found end of string                          
         LA    R2,@R2+1                                                         
         BCT   R0,MEMSRCH                                                       
**  have end of dsname string                                                   
HVDSNSTR SR    R2,R4               Length of string                             
         BZ    ENDSNOP             Br if null string                            
HVDSTR2  AR    R6,R2               Compute total length                         
         CH    R6,=H'44'           Is it too long?                              
         BH    DSNLONG             Br if yes                                    
         LR    R15,R4              For ex mvc                                   
         DEX   R2,' MVC 0(0,R5),@R15 '  Move string                             
         LA    R2,@R2+1            Restore length                               
         AR    R4,R2               Update scan pointer                          
         AR    R5,R2               Update dsn pointer                           
         SPACE 2                                                                
***  end of dsn operand except member spec                                      
ENDSNOP  LR    R15,R5              Current point                                
         SR    R5,R6               Start of name                                
         SPACE 2                                                                
**  now process member name present                                             
         CLR   R4,R3               Is operand at the end?                       
         BNL   PARENSCN            Br if yes                                    
         CLI   0(R4),C'#'          Is it #?                                     
         BE    HAVEMEM                                                          
*  only other possibility is '('                                                
         B     PARENMEM                                                         
*                                                                               
**  end of scanned stuff - check member in parens or # after ''                 
*                                                                               
PARENSCN LM    R0,R1,CPSAVE        Get pointers to dsname                       
         AR    R1,R0               Point to character after                     
         CLI   @R1,C'#'            Member after quoted dsn?                     
         BE    MBRSCN              Br if yes                                    
         CLI   @R1,C'('                                                         
         BNE   DSNDONE             No member specified                          
MBRSCN   SCINFO ,                 Find out if any left to scan                  
         LTR   R0,R0               Was there anything left?                     
         BNP   DSNDONE             Br if finished                               
         SCAN  ,                   Go scan for member name                      
         SCANCHK                                                                
         IF    (R0,Z),DSNDONE      Done if nothing there                        
**  have member in parens or # following quoted dsname                          
         LR    R4,R1               Loc of lparen                                
         LR    R3,R1                                                            
         AR    R3,R0               Loc of rparen+1                              
         CLI   0(R4),C'#'                                                       
         BE    HAVEMEM                                                          
PARENMEM DECR  R3                  Loc of rparen                                
*  process member name in parens or by #                                        
HAVEMEM  LA    R4,1(R4)            Past # or (                                  
         LR    R2,R3                                                            
         SR    R2,R4               Length of member name                        
         BZ    NULLMBR             Br if null override                          
         SPACE 1                                                                
         IF    ((@R4,EQ,'0)'),OR,(@R4,EQ,'-'),OR,(@R4,EQ,'+')),BEGIN            
         LR    R15,R5              R15 -> Start of dsn                          
         AR    R15,R6              R15 -> End of dsn + 1                        
         BCTR  R4,0                Fall back to start of gen#                   
         LA    R2,2(R2)            Compute the gen# length                      
         AR    R6,R2               Add into total dsn length                    
         DEX   R2,'MVC @R15(0),@R4' Move in generation number                   
         SET   CPFGEN              Flag as generation data set                  
         B     DSNDONE             Finish up                                    
         END                                                                    
         SPACE 1                                                                
         CH    R2,=H'8'            Check length                                 
         BNH   REPLMEM             Br if ok                                     
*  member name is too long                                                      
MBRLONG  IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  (R4),(R2)                                                        
         ERROR ': member name too long.'                                        
*  dsname is too long                                                           
DSNLONG  LR    R0,R6                                                            
         SR    R6,R2               Back up to prev length                       
         SR    R5,R6               Find start of dsname                         
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         TSEG  (R5),(R0)                                                        
         ERROR ': dsname too long.'                                             
         SPACE 2                                                                
**  insert specified member name                                                
REPLMEM  MVC   DSNWAMBR,CVBLANKS                                                
         DEX   R2,' MVC DSNWAMBR(0),@R4 '  Move member name                     
         IF    (DSNWAMBR,EQ,=CL8'*'),BEGIN                                      
         XPUSH R14,R2                                                           
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         LR    R2,R0                                                            
         MVC   DSNWAMBR,CVBLANKS                                                
         MOVE  R2,DSNWAMBR,@R1                                                  
         XPOP  R14,R2                                                           
         IF    (DSNWAMBR,NE,' '),EXIT                                           
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         MVC   CPERRID,=CL8'NOSTAR'                                             
         B     DOERREX                                                          
         END                                                                    
         ERROR 'The current member is not set.',,NOSTAR                         
         END                                                                    
         OC    DSNWAMBR,CVBLANKS   Convert to upper case                        
         B     DSNDONE                                                          
*                                                                               
***  done with dsname                                                           
*                                                                               
NULLMBR  CLEAR DSNWAMBR            Flag explicit null member                    
DSNDONE  TM    CPLFLG2,CPFDSNMS    Test null ok                                 
         BO    DOLGO               Br if so                                     
DSNDONE2 LTR   R6,R6                                                            
         BP    DOLGO                                                            
         IF    CPFDSNNE,DOERREX    Error suppression request?                   
         ERROR 'Dsname is null.'                                                
DOERREX  SET   CPFDSNBD            Set error                                    
         B     DSCNEXIT                                                         
DOLGO    STH   R6,DSNWANL          Save length for dsnstddo                     
         ACALL DSNSTDDO            Go set dsname flags                          
**  if std wyl dsname check null after userid                                   
         TM    DSNWAF1,DSNFSTD     Is it std wyl dsn?                           
         BZ    DOLGO2              Br if no                                     
         CH    R6,=AL2(DSNXN)      Is wyl name just std length?                 
         BNE   DOLGO2              Br if no                                     
**  default dsname = 'lib'                                                      
         IF    (CPCMNM,EQ,'PFX'),PFXLIB  IT'S ok, mom                           
         TM    CPLFLG2,CPFDSNMS    Is default permitted?                        
         BO    DOLGO2              Br if no                                     
PFXLIB   MVC   DSNWADSN+DSNXN(L'CPLIB),CPLIB  move in def lib                   
         OI    DSNWAF1,DSNFWLIB    Flag wylbur library                          
         LA    R15,DSNWADSN+DSNXN                                               
BLKLOOP  CLI   @R15,C' '           At end of dsn                                
         BE    DOLGO2              Yes, have correct length                     
         LA    R6,1(,R6)           No, increment length                         
         LA    R15,@R15+1          Pt to next slot                              
         B     BLKLOOP             And look for end                             
DOLGO2   LR    R0,R6                                                            
         STH   R0,DSNWANL          Save dsname length                           
DSCNEXIT PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
DEVPRT   SCKW  DISK,DEVDISK                                                     
         SCKW  MICRO,DEVMICRO                                                   
         SCKW  SAMSON,DEVMICRO     (Compat--Remove me after 8/85)               
         SCKW  NETWORK,DEVNET,A                                                 
         SCKW  ,NULDEV,NULL                                                     
         SCKW  ,INVDEV                                                          
*                                                                               
DEVDISK  DPROC ,                                                                
         MVI   DSNDTYPE,DSNDDISK                                                
         PEND  ,                                                                
*                                                                               
DEVMICRO DPROC ,                                                                
         MVI   DSNDTYPE,DSNDSAM                                                 
         PEND  ,                                                                
*                                                                               
DEVNET   DPROC ,                                                                
         MVI   DSNDTYPE,DSNDNET                                                 
         PEND  ,                                                                
*                                                                               
NULDEV   PROC  ,                                                                
         IF    ^CPFDSNNE,BEGIN     IF NO Error suppression request?             
         ABORT 'Missing device-type name.'  give error                          
         END                                                                    
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
INVDEV   PROC  ,                                                                
         IF    ^CPFDSNNE,BEGIN     IF NO Error suppression request?             
         TSEG  (R1),(R0)                    give error                          
         ABORT ' is an unkown device type.'                                     
         END                                                                    
         LA    R15,8                                                            
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Dsname Finish Routine -- Do Overrides'                          
* this routine processes any group or user overrides found.                     
* it must be called after scanning has been completed.                          
*                                                                               
DSNFIN   XPROC                                                                  
*-                                                                              
*-       Validity checking.                                                     
*-                                                                              
         IF    DSNFASCII+DSNFEBCDIC,BEGIN  ASCII/EBCDIC explicit...             
         IF    DSNFTRAN,EXIT       It's OK, scram                               
         TSEG  'TRANSPARENT must be specified '                                 
         ERROR 'if ASCII/EBCDIC is specified.'                                  
         END                                                                    
*                                                                               
         IF    (DSNDTYPE,Z),BEGIN  Default device type...                       
*-                                                                              
*-       Check the device type based on the volume.                             
*-                                                                              
         MVI   DSNDTYPE,DSNDDISK   Assume disk                                  
*                                                                               
         IF    (DSNON,NZ),BEGIN    Have a volume...                             
*-                                                                              
*-       Check for Samson looking devices.                                      
*-                                                                              
         IF    CPSFSAM,BEGIN       Samson terminal...                           
         IF    ((DSNON,GE,'A'),AND,(DSNON,LE,'Z')),BEGIN                        
         IF    (DSNON+1,NE,' '),EXIT  Looking for one char device               
         MVI   DSNDTYPE,DSNDSAM    It's a samson device                         
         B     DFINEXT                                                          
         END                                                                    
*                                                                               
         IF    ((DSNON,EQ,'CLIP '),OR,(DSNON,EQ,'CLIPBOARD ')),BEGIN            
         MVI   DSNDTYPE,DSNDSAM    It's a samson device                         
         B     DFINEXT                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Check for volume name which is a host name.                            
*-                                                                              
         ACALL HOSTCHK             Check DSNON for host name                    
         IF    Z,'MVI DSNDTYPE,DSNDNET'  It's a network file                    
*-                                                                              
*-       Check for user defined device names.                                   
*-                                                                              
DFINEXT  SETMSG DSNON                                                           
*-                                                                              
*-       This is for an explicit device type set by the                         
*-         SET DEVICE command (it's not really used).                           
*-                                                                              
         VCALL FINDDEV             Is it an external device?                    
         IF    Z,BEGIN             Yes...                                       
         WITH  (DEV,R1)                                                         
         IF    DEVTSAM,'MVI DSNDTYPE,DSNDSAM'                                   
         IF    DEVTNET,'MVI DSNDTYPE,DSNDNET'                                   
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Process a disk device the same as before.                              
*-                                                                              
         IF    (DSNDTYPE,EQ,DSNDDISK),BEGIN  Disk...                            
         IF    (DSNON,NZ),BEGIN    Check volume name...                         
         CLC   DSNON+6(L'DSNON-6),CVBLANKS  Extra chars in vol name?            
         IF    EQ,EXIT             Yes, scram                                   
         TSEG  DSNON,,T                                                         
         ERROR ': volume/host not found.'                                       
         END                                                                    
         END                                                                    
*                                                                               
         IF    DSNWAF1.DSNFSTD,BEGIN  Process user/group override...            
         IF    (CPCGRP,NZ),'MVC DSNWADSN+DSNXG(DSNXGL-1),CPCGRP'                
         IF    (CPCUSER,NZ),'MVC DSNWADSN+DSNXU(DSNXUL-1),CPCUSER'              
         END                                                                    
*                                                                               
         ACALL DSNUDSDO            Go set user data set flags                   
*                                                                               
         IF    (DSNDTYPE,EQ,DSNDDISK),BEGIN  Check dsname...                    
         IF    (DSNWADSN,EQ,'NULLFILE '),XIBADNM  Never, never                  
*                                                                               
         IF    (DSNFBADN,AND,^DSNWAF1.DSNFQDSN),BEGIN  Bad name...              
         IF    CPFGEN,EXIT         Allow funny chars if GDG dsname              
         ACALL DSNCHK              Is dsn ok?                                   
         IF    Z,EXIT              (Shouldn't happen)                           
         LR    R2,R15                                                           
XIBADNM  IF    CPFDSNNE,BEGIN      Error suppression request?                   
         MVC   CPERRID,=CL8'INVDSN'                                             
         SET   CPFDSNBD            Set error                                    
         EXIT  DSNFIN                                                           
         END                                                                    
         TSEG  DSNWADSN,LH:DSNWANL                                              
         SETMSG ': invalid dsname.'                                             
         IF    (R2,EQ,8),'SETMSG ": invalid characters in dsname."'             
         ABORT (R1),(R0),INVDSN                                                 
         END                                                                    
         END                                                                    
*-                                                                              
*-       Check the exact copy of the file name the user specifed                
*-         for "*".  If it is an asterisk, then use the saved                   
*-         dsname.                                                              
*-                                                                              
         IF    ((DSNWLEN,EQ,1),AND,(DSNWFILE,EQ,'*')),BEGIN                     
         SETMSG DSNWADSN,LH:DSNWANL  Complete file name                         
         IF    (DSNDTYPE,NE,DSNDDISK),BEGIN  Non-disk...                        
         IF    DSNWAF1.DSNFSTD,'LA R1,@R1+11; SH R0,=H"11"'  No pfx             
         END                                                                    
*                                                                               
         CEIL  R0,L'DSNWFILE       (Safety)                                     
         IF    (R15,NP),'CLEAR R0'                                              
         STH   R0,DSNWLEN                                                       
         LR    R15,R0              File length                                  
         MVC   DSNWFILE,CVBLANKS   Blank out file name                          
         MOVE  R15,DSNWFILE,@R1    Save file name                               
         END                                                                    
*                                                                               
         PEND                                                                   
         TITLE 'Dsname Attribute and Possession Routines'                       
***  routine to set dsname attribute flags                                      
*                                                                               
DSNSTDDO XPROC ,                                                                
         CLEAR DSNWAF1.DSNFSTD+DSNFWLIB  reset flags                            
         CLC   =C'WYL.',DSNWADSN   First level ok?                              
         BNE   DODSNNS             Br if not std                                
         CLI   DSNWADSN+DSNXG+DSNXGL-1,C'.'  Group delim?                       
         BNE   DODSNNS             Br if not                                    
         CLI   DSNWADSN+DSNXU+DSNXUL-1,C'.'  User delim?                        
         BNE   DODSNNS             Br if not                                    
         LH    R0,DSNWANL          Get dsname length                            
         CH    R0,=Y(DSNXN)        Is it long enough?                           
         BL    DODSNNS             Br if not                                    
         OI    DSNWAF1,DSNFSTD     Set std wyl dsn flag                         
         LA    R2,DSNXN+8          Length of std pfx +8                         
         CR    R0,R2               Is that the length?                          
         BH    DODSNNS             Br if not                                    
         CLC   CPLIB,DSNWADSN+DSNXN  Is a wylbur lib?                           
         BNE   DODSNNS             Br if not                                    
         OI    DSNWAF1,DSNFWLIB    Say so                                       
DODSNNS  PEND  ,                                                                
         DROP  BR                                                               
         EJECT                                                                  
***  routine to set dsname possession flags                                     
*                                                                               
DSNUDSDO XPROC ,                                                                
         XPUSH R15                                                              
         CLEAR DSNFBADN            Assume a-ok dsname                           
         IF    ^CPFGEN,BEGIN       Not a GDG...                                 
         ACALL DSNCHK              Is it a valid os dsname?                     
         IF    NZ,'SET DSNFBADN'   Flag as invalid                              
         END                                                                    
*                                                                               
         NI    DSNWAF1,255-DSNFMYDS  reset                                      
         TM    DSNWAF1,DSNFSTD     Is it std data set?                          
         BZ    DODSNSX             Br if not                                    
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPSGRP  his group?                      
         BNE   DODSNGSV            Br no to test saved group                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPSUSER  his id?                        
         BE    DODSNSMY            Br if yes                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BNE   DODSNGSV            Br no to test saved group                    
         B     DODSNSMY                                                         
*                                                                               
DODSNGSV TM    CPGFLG3,CPFKWENT    Did user give kw for save grp                
         BZ    DODSNSA             no, don't test saved group                   
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPGRPSV  saved group?                   
         BNE   DODSNSA             Br if not                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPUSERSV  saved user?                   
         BE    DODSNSMY            Br yes to set my data set                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BE    DODSNSMY            Br yes to set my data set                    
         B     DODSNSX             Br to set not mine                           
*                                                                               
DODSNSA  IF    (CPPACCT,NZ),BEGIN  Check PACCOUNT...                            
         IF    ^CPFEXEC,EXIT       Only in exec mode                            
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPPGRP  PACCT group?                    
         IF    NE,EXIT             No                                           
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPPUSER  PACCT user?                    
         BE    DODSNSMY            Br if yes                                    
         END                                                                    
*                                                                               
         TM    CPGFLG4,CPFAUSET    Authority field set?                         
         BZ    DODSNSX             no, don't test auth group                    
         CLC   DSNWADSN+DSNXG(DSNXGL-1),CPAUTHGP  auth group?                   
         BNE   DODSNSX             Br if not                                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),CPAUTHUS  auth user?                    
         BE    DODSNSMY            Br yes to set my data set                    
         CLC   DSNWADSN+DSNXU(DSNXUL-1),=C'&PUBUSER'  pub?                      
         BE    DODSNSMY            Br yes to set my data set                    
         B     DODSNSX             Exit                                         
*                                                                               
DODSNSMY OI    DSNWAF1,DSNFMYDS    Set my data set flag                         
DODSNSX  XPOP  R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DSNCHK -- Routine to check that dsname conforms to OS                        
*    data set name conventions.                                                 
*                                                                               
*      On entry:                                                                
*        DSNWA - contains dsname, etc.                                          
*                                                                               
*      On exit, R15 (and CC):                                                   
*        0 - ok                                                                 
*        4 - bad os dsname                                                      
*        8 - bad chars in dsname                                                
*                                                                               
DSNCHK   PROC  ,                                                                
         CLEAR R15                 Assume ok                                    
*                                                                               
         LA    R6,DSNWADSN                                                      
         LTH   R5,DSNWANL                                                       
         BNP   DSNRC4              Null dsname is n.g.                          
*                                                                               
         LA    R4,8                Max no. of chars per level                   
         LA    R3,2                Allow dot, alpha, or national                
         WHILE (R5,POS),BEGIN                                                   
         LC    R2,@R6              Get char                                     
         LC    R0,DSNCHR(R2)       Get char type                                
         IF    (R0,Z),BEGIN        It's a dot...                                
         IF    (R4,GE,8),DSNRC4    Dot can't be first char                      
         LA    R4,8                Allow eight more chars                       
         LA    R3,2                Allow dot, alpha, or national                
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (R0,GT,R3),'LA R15,8; B DSNCHKX'  bad chars                      
         DECR  R4                                                               
         IF    (R4,NEG),DSNRC4     Over 8 chars w/o a dot                       
         LA    R3,3                Allow numbers too now                        
         END                                                                    
         LA    R6,@R6+1                                                         
         DECR  R5                                                               
         END                                                                    
         B     DSNCHKX             All done, aok -- scram                       
*                                                                               
DSNRC4   LA    R15,4                                                            
DSNCHKX  PEND  ,                                                                
         EJECT                                                                  
*-                                                                              
*-        Dsname character type table.                                          
*-                                                                              
*-       0 - Dot (.)                                                            
*-       1 - Alphabetic                                                         
*-       2 - National ($,@,#)                                                   
*-       3 - Numeric                                                            
*-           255 - Everything else                                              
*-                                                                              
DSNCHR   DC    256X'FF'                                                         
         ORG   DSNCHR+C'.'                                                      
         DC    X'00'                                                            
         ORG   DSNCHR+C'A'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'J'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'S'                                                      
         DC    X'0101010101010101'                                              
         ORG   DSNCHR+C'$'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'@'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'#'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'0'                                                      
         DC    X'03030303030303030303'                                          
         ORG                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  HOSTCHK -- Routine to check to see if name passed is                         
*    the name of another host computer on the network.                          
*                                                                               
*    On entry:                                                                  
*      DSNON = possible host name                                               
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - yes, this is a host name                                             
*      4 - no, it is not a host name                                            
*                                                                               
HOSTCHK  PROC                                                                   
         SETMSG DSNON               Possible host name                          
         VCALL RTRIM               Get length                                   
         IF    (R0,LT,2),HOSTNO    Too short, can't be host name                
         IF    (R0,GT,6),HOSTYES   Too long for OS volume                       
*-                                                                              
*-       Check for "aaannn" (a=alpha,n=numeric) style name.                     
*-         If we find a name like this then assume it is a                      
*-         disk volume name.                                                    
*-                                                                              
         IF    (R0,EQ,6),BEGIN     Exactly 6 characters...                      
         IF    ((DSNON,GE,'A'),AND,(DSNON,LE,'Z'),AND,                 *        
               (DSNON+1,GE,'A'),AND,(DSNON+1,LE,'Z'),AND,              *        
               (DSNON+2,GE,'A'),AND,(DSNON+2,LE,'Z'),AND,              *        
               (DSNON+3,GE,'0'),AND,(DSNON+3,LE,'9'),AND,              *        
               (DSNON+4,GE,'0'),AND,(DSNON+4,LE,'9'),AND,              *        
               (DSNON+5,GE,'0'),AND,(DSNON+5,LE,'9')),HOSTNO                    
         END                                                                    
*-                                                                              
*-       Check the volume table to see if this is a disk volume.                
*-                                                                              
         LA    R1,DSNON                                                         
         VCALL VOLCHK              Is this a disk volume?                       
         BNZ   HOSTYES             No, then maybe it's a host                   
*                                                                               
HOSTNO   LA    R15,4               It's not a host name                         
         B     HOSTEXIT                                                         
*                                                                               
HOSTYES  CLEAR R15                 It is a host name                            
HOSTEXIT PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Dsname Parameter Scan Routines'                                 
* the dsname parameter scan routines are used by including                      
* a push pointer to dsnprt in the scan table for a command.                     
* these routines recognize group and user overrides. at the                     
* end of the scan of a line, the routine dsnfin completes                       
* processing of these parameters and must be called.                            
*                                                                               
*                                                                               
*                                                                               
* dsnprtf is the scanner prt used by use, save, exec from,                      
* and copy from to recognize format dsname keywords as well                     
* as the normal dsname keywords.                                                
*                                                                               
DSNPSH   XPUSHRTN DSNPRT                                                        
DSNPSHF  XPUSHRTN DSNPRTF                                                       
DSNPSHON XPUSHRTN DSNPRTON                                                      
*                                                                               
DSNPRTF  SCKW  EDIT,EDITA,A                                                     
         SCKW  NEWEDIT,NEWEDIT                                                  
         SCKW  OLDEDIT,OLDEDIT                                                  
         SCKW  CARD,CARDA,A                                                     
         SCKW  PRINT,PRNTA,A                                                    
         SCKW  TABS,TABS,A                                                      
         SCKW  NOTABS,NOTABS,A                                                  
         SCKW  UNFORMATTED,UNFMT,A                                              
         SCKW  FORMATTED,FMT,A                                                  
         SCKW  TEXTONLY,TEXTONLY,A                                              
         SCKW  TRANSPARENT,TRANS,A                                              
         SCKW  DTRANSPARENT,DTRANS,A                                            
         SCKW  ASCII,ASCII,A                                                    
         SCKW  EBCDIC,EBCDIC,A                                                  
         SCKW  WRAP,WRAP,(A,P,PI),&LINESZ                                       
         SCKW  SPLIT,SPLIT,(A,P,PI),&LINESZ                                     
         SCKW  LRECL,LRECLA,(P,PI,A),256                                        
         SCKW  CODE,CNVCODE,(P,A)                                               
         SCKW  ,NUMPSH,PUSH                                                     
         SCKW  SEQFLD,SETSEQ,(A,P)                                              
*                                                                               
* dsnprt is the scanner prt used by all of wylbur to                            
* recognize dsname keywords.                                                    
*                                                                               
DSNPRT   SCKW  ON,HAVOLM                                                        
         SCKW  VOLUME,HAVOLM,A                                                  
         SCKW  WINGS,V(SCNNOOP)    ALLOW, IGNORE TYPES IN                       
         SCKW  UNIX,V(SCNNOOP)     OLD FILE OPTIONS SCANNING                    
         SCKW  SAMSON,V(SCNNOOP),A                                              
         SCKW  NET,V(SCNNOOP)                                                   
         SCKW  OLDIBM,V(SCNNOOP),A                                              
         SCKW  TEMPORARY,HAVTEMP,A                                              
         SCKW  SET,SETSET                                                       
         SCKW  NOSET,SETNOSET,A                                                 
         SCKW  PUBLIC,HAVPUB,A                                                  
         SCKW  GUEST,HAVGUEST,A                                                 
         SCKW  NOENQUEUE,NOENQ,A                                                
         SCKW  ,ACCTPSH,PUSH                                                    
         SCKW  ,,END                                                            
*                                                                               
DSNPRTON SCKW  ON,HAVOLM                                                        
         SCKW  VOLUME,HAVOLM,A                                                  
         SCKW  ,,END                                                            
*                                                                               
SEQPRT1  SCKW  ,SEQF1NUL,NULL                                                   
         SCKW  ,SEQF1NUM,I                                                      
         SCKW  END,SEQF1END                                                     
         SCKW  ,SEQFBAD                                                         
*                                                                               
SEQPRT2  SCKW  ,SEQF2NUL,NULL                                                   
         SCKW  ,SEQF2NUM,I                                                      
         SCKW  ,SEQFBAD                                                         
*                                                                               
SEQPRT3  SCKW  ,SEQFBAD                                                         
*                                                                               
         EJECT                                                                  
*                                                                               
*        DSN PARAMETER SCANNING ROUTINES                                        
*                                                                               
HAVTEMP  PROC                                                                   
         VCALL EXIT2               Get scratch volser                           
         CEIL  R0,L'DSNON                                                       
         MVC   DSNON,CVBLANKS                                                   
         LR    R15,R0                                                           
         MOVE  R15,DSNON,@R1       Set volume name                              
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
HAVONWA  RECORD BEGIN                                                           
HAVOSKIP DS    CL256                                                            
HAVOSTOP DS    CL256                                                            
         END                                                                    
*                                                                               
*                                                                               
HAVOLM   PROC  HAVONWA                                                          
*                                                                               
         COMMENT                   SIMPLE SCAN                                  
         SCANSTR ,                 SCAN STRING INCLUDING ODD CHARS              
         IF    (R15,NEG),' EXIT HAVOLM '                                        
         IF    (R0,NP),BEGIN       IF NOTHING MORE ,,,                          
         IF    CPFDSNNE,BEGIN      Error suppression requested?                 
         SET   CPFDSNBD                                                         
         EXIT  HAVOLM                                                           
         END                                                                    
         ERROR 'Missing disk/host name after "ON".'                             
         END                                                                    
*                                                                               
         CEIL  R0,L'DSNON                                                       
         MVC   DSNON,CVBLANKS      Pre-blank                                    
         LR    R15,R0                                                           
         MOVE  R15,DSNON,@R1       Save volume/host name                        
         L     R15,CVUPPTBL                                                     
         TR    DSNON,@R15          Convert to upper case                        
*                                                                               
         IF    ((DSNON,EQ,'CATALOG '),OR,(DSNON,EQ,'CATLG '),          *        
               OR,(DSNON,EQ,'CAT ')),'CLEAR DSNON'  Reset volume                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETSET   DPROC ,                                                                
         OI    CPLFLG3,CPFSET      Turn on set operand                          
         PEND  ,                                                                
*                                                                               
SETNOSET DPROC ,                                                                
         NI    CPLFLG3,255-CPFSET  Turn off set flag                            
         PEND  ,                                                                
*                                                                               
HAVGUEST PROC  ,                                                                
         MVC   DSNNUSER,=CL(L'DSNNUSER)'guest'                                  
         MVC   DSNNPASS,CVBLANKS                                                
         PEND  ,                                                                
*                                                                               
HAVPUB   PROC  ,                                                                
         MVC   CPCACCT,=C'&PUBUSER&PUBGRP'  Public account                      
         PEND  ,                                                                
*                                                                               
CNVCODE  XPROC ,                                                                
         LR    R15,R0              Load length of code string                   
         IF    ((R15,LT,8),OR,(R15,GT,16)),BEGIN                                
         ERROR 'CODE must be 8-16 characters in length.'                        
         END                                                                    
         CLEAR (@R13,16)           Clear work area                              
         SET   DSNFCODE            Set encode/decode flag                       
         DEX   R15,'MVC @R13(0),@R1'  Move code str to work area                
         L     R15,@R13            Load 1st four bytes                          
         AL    R15,@R13+4          Add 2nd four bytes                           
         AL    R15,@R13+8          Add 3rd four bytes                           
         AL    R15,@R13+12         Add 4th four bytes                           
         LPR   R15,R15             Make code value positive                     
         ST    R15,DSNCODE         Store code value                             
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
LRECLA   DPROC ,                                                                
         ST    R0,CPLRCL          Set LRECL                                     
         PEND  ,                                                                
*                                                                               
NEWEDIT  DPROC ,                                                                
         SET   CPFNEWED            NEWEDIT                                      
         CLEAR CPLRCL              Set Edit format                              
         PEND  ,                                                                
*                                                                               
OLDEDIT  DPROC ,                                                                
         CLEAR CPFNEWED            OLDEDIT                                      
         CLEAR CPLRCL              Set Edit format                              
         PEND  ,                                                                
*                                                                               
EDITA    DPROC ,                                                                
         CLEAR CPLRCL              Set Edit format                              
         PEND  ,                                                                
*                                                                               
CARDA    DPROC ,                                                                
         LA    R0,80                                                            
         ST    R0,CPLRCL           Set CARD format                              
         PEND  ,                                                                
*                                                                               
PRNTA    DPROC ,                                                                
         LA    R0,133                                                           
         ST    R0,CPLRCL           Set PRINT format                             
         PEND  ,                                                                
*                                                                               
TABS     DPROC ,                                                                
         SET   DSNFTABS            Keep TAB characters in data                  
         PEND  ,                                                                
*                                                                               
NOTABS   DPROC ,                                                                
         CLEAR DSNFTABS            Expand TAB characters in data                
         PEND  ,                                                                
*                                                                               
FMT      DPROC ,                                                                
         CLEAR DSNFUFMT+DSNFTABS   FORMATTED                                    
         PEND  ,                                                                
*                                                                               
UNFMT    DPROC ,                                                                
         SET   DSNFUFMT+DSNFTABS   UNFORMATTED                                  
         PEND  ,                                                                
*                                                                               
TEXTONLY DPROC ,                                                                
         SET   DSNFTEXTONLY        TEXTONLY                                     
         PEND  ,                                                                
*                                                                               
TRANS    DPROC                                                                  
         SET   DSNFTRAN            Set TRANSPARENT format                       
         PEND                                                                   
*                                                                               
DTRANS   DPROC                                                                  
         SET   DSNFDTRAN+DSNFTRAN  Set DTRANSPARENT format                      
         PEND                                                                   
*                                                                               
ASCII    DPROC                                                                  
         SET   DSNFASCII           ASCII                                        
         CLEAR DSNFEBCDIC          Not EBCDIC                                   
         PEND                                                                   
*                                                                               
EBCDIC   DPROC                                                                  
         SET   DSNFEBCDIC          EBCDIC                                       
         CLEAR DSNFASCII           Not ASCII                                    
         PEND                                                                   
*                                                                               
WRAP     PROC                                                                   
         IF    (CPCMNM,EQ,'SAV'),BEGIN    WRAP invalid on SAVE                  
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR DSNFSPLT            WRAP, not SPLIT                              
         STH   R0,DSNWRAPL         Save WRAP length                             
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SPLIT    PROC  ,                                                                
         IF    (CPCMNM,EQ,'SAV'),BEGIN    SPLIT invalid on SAVE                 
         VCALL NOTVALID                                                         
         END                                                                    
         SET   DSNFSPLT            SPLIT, not WRAP                              
         STH   R0,DSNWRAPL         Save WRAP length                             
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
NOENQ    PROC  ,                                                                
         IF    ^CPSFSPR,BEGIN      Only the strong survive ...                  
         VCALL NOTVALID                                                         
         END                                                                    
         SET   DSNFNONQ            No enqueue on dataset                        
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
* SEQUENCE FIELD SCANNING                                                       
*                                                                               
SETSEQ   PROC  ,                   TYPE HELP FORMAT-OPTIONS                     
*                                                                               
         COMMENT                   SET SEQ FLAGS                                
         NI    CPLFLG5,255-CPFNONUM  SET NUMBERED DEFAULT                       
         OI    CPLFLG5,CPFFOPT     SET NUMBER OPTION GIVEN                      
*                                                                               
         COMMENT                   CHECK PARENTHESIS, ETC                       
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         DECR  R3                                                               
         IF    ((R0,LT,3),OR,(@R1,NE,'('),OR,(@R3,NE,')')),BEGIN                
         VCALL NOTVALID                                                         
         END                                                                    
         INCR  R1                                                               
         S     R0,=F'2'                                                         
*                                                                               
         COMMENT                   SCAN SEQ FIELDS                              
         SCPUSH                                                                 
         SCINIT (R1),(R0)                                                       
         SCAN  SEQPRT1                                                          
         SCANCHK                                                                
         SCAN  SEQPRT2                                                          
         SCANCHK                                                                
         SCAN  SEQPRT3                                                          
         SCANCHK                                                                
         SCPOP                                                                  
*                                                                               
         PEND                                                                   
*                                                                               
SEQF1NUL PROC  ,                   SEQ FIELD OPTIONS                            
         ERROR 'Missing sequence field specification. '                         
         PEND                                                                   
*                                                                               
SEQF1NUM PROC  ,                                                                
         IF    (R0,GE,&LINESZ),BEGIN                                            
         ERROR 'Invalid sequece field specification. '                          
         END                                                                    
         STH   R0,CPSEQFLD                                                      
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SEQF1END PROC  ,                                                                
         L     R0,=F'-1'                                                        
         STH   R0,CPSEQFLD                                                      
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SEQF2NUL PROC  ,                                                                
         L     R0,=F'8'                                                         
         STH   R0,CPSEQFLD                                                      
         PEND                                                                   
*                                                                               
SEQF2NUM PROC  ,                                                                
         IF    (R0,GE,9),BEGIN                                                  
         ERROR 'Invalid sequece field specification. '                          
         END                                                                    
         STH   R0,CPSEQLN                                                       
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
SEQFBAD  PROC  ,                                                                
         ERROR 'Invalid sequence field specification. '                         
         PEND                                                                   
*                                                                               
RTNAD    EQU   32                                                               
PRESTPT  EQU   32+4                                                             
LINAREA  EQU   32+8                                                             
WRKAREA  EQU   32+16                                                            
         TITLE 'Literals'                                                       
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
