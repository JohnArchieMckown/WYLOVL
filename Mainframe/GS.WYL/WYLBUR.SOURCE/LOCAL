LOCAL    TITLE 'WYLBUR Local Exits'                                             
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*box                                                                            
*                                                                               
*  LOCAL is a set of installation dependent subroutines.                        
*  This is the Stanford version.                                                
*                                                                               
*  Note for ISU:  I replaced the LOCALRTN macro with direct                     
*     VCALLs to the routine.  I left the names "EXITxx", but I                  
*     expect that future routines will have more symbolic values.               
*     When you find a routine in some other module that should                  
*     really be in LOCAL let me know and I'll move it here (keeping             
*     the same subroutine name).                                                
*                                                                               
*                                               8/23/88  Niz                    
*                                                                               
*                                                                               
*  Another note for ISU:                                                        
*     The old ACCT module was split in to ACMD (accounting related              
*     commands) and ACCT (internal accounting routines).  The ACCT              
*     module is an "installation dependent" module, so the                      
*     accounting routines that used to be in LOCAL have been moved              
*     back to ACCT.  I think ISU should just have it's own copy of              
*     ACCT...  let me know how well it works.                                   
*                                                                               
*                                                                               
*                                               9/20/88  Niz                    
*                                                                               
WYLLOCAL CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
LOCL     IDENT 2193                15:11:45 07/12/02  (SLP)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
VOLDSECT VOLUME DSECT=YES                                                       
VOLENTSZ EQU   *-VOLDSECT                                                       
         EJECT                                                                  
KWR      RECORD 'KWR'                                                           
         POP   DSECTS                                                           
WYLLOCL  CSECT                                                                  
*-                                                                              
*-       LOCAL MACROS                                                           
*-                                                                              
         MACRO                                                                  
&L       SYSDSNID &DSN                                                          
         LCLA  &LEN                                                             
         LCLC  &DSNX                                                            
&LEN     SETA  K'&DSN                                                           
&DSNX    SETC  '&DSN'                                                           
         AIF   ('&DSN'(1,1) NE '''').NOTQ                                       
.*                                                                              
         AIF   ('&DSN'(&LEN,1) NE '''').BADQ                                    
&DSNX    SETC  '&DSN'(2,&LEN-2)                                                 
&LEN     SETA  &LEN-2                                                           
&L       DC    AL2(&LEN)                                                        
         DC    C'&DSNX'                                                         
         MEXIT                                                                  
.*                                                                              
.NOTQ    ANOP                                                                   
&L       DC    AL2(&LEN)                                                        
         DC    C'&DSN'                                                          
         MEXIT                                                                  
.*                                                                              
.BADQ    MNOTE 12,'Bad quotes'                                                  
.EXIT    MEND                                                                   
*                                                                               
         TITLE 'Local Exit #0 - Generate System Global DSN'                     
*box                                                                            
*                                                                               
* EXIT #0 -- Generate system global DSN                                         
*                                                                               
*       On Entry:  R0 = DSN ID to generate                                      
*                                                                               
*       On Exit:   R1,R0 point to DSNAME                                        
*                  R15 = return code                                            
*                        (zero = OK)                                            
*                        (non-zero = bad SYSDSNID)                              
*                                                                               
         COPY  SYSDSNS                                                          
*-                                                                              
EXIT0    XPROC ,                                                                
         LR    R2,R0               Copy SYSDSNID value                          
         IF    ((R2,LT,0),OR,(R2,GT,SYSDSNMX)),BEGIN                            
         LA    R15,4               Set bad return code                          
         B     EXIT0X              Return                                       
         END                                                                    
         SLL   R2,2                Convert to table offset                      
         L     R2,SYSDSNTB(R2)     Load pointer to string                       
         LH    R0,@R2              Pick up length                               
         LA    R1,@R2+2            Point to DSN                                 
         CLEAR R15                 Set return code                              
EXIT0X   LABEL ,                                                                
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
*                                                                               
SYSDSNTB DC    A(HELPDIR)           0: Help file directory                      
         DC    A(HELPFILE)          1: Help file                                
         DC    A(ERRFILE)           2: ERRORS file                              
         DC    A(TERMFILE)          3: Terminal file                            
         DC    A(WORDFILE)          4: Spelling dictionary                      
         DC    A(*-*)               5: -- Not used (use me!) --                 
         DC    A(PRINFILE)          6: Printer file                             
         DC    A(NEWSFILE)          7: News file                                
         DC    A(HOSTSYSH)          8: SYSH host file                           
         DC    A(BATMFILE)          9: BATMAIL exec lib                         
         DC    A(HOSTSYSA)         10: SYSA host file                           
         DC    A(HOSTRLG)          11: RLG  host file                           
         DC    A(HOSTSYSC)         12: SUH(SYSC) host file                      
         DC    A(HOSTSYSD)         13: SYSD host file                           
SYSDSNMX EQU   (*-SYSDSNTB)/4                                                   
*                                                                               
HELPDIR  SYSDSNID $WYL.GA.PUB.HELPDIR                                           
HELPFILE SYSDSNID $WYL.GA.PUB.HELP                                              
ERRFILE  SYSDSNID $WYL.GA.PUB.ERRORS                                            
TERMFILE SYSDSNID $WYL.GS.WYL.TERMINAL                                          
WORDFILE SYSDSNID $WYL.GS.WYL.WORDS                                             
HOSTSYSA SYSDSNID $WYL.GS.WYL.SYSA.HOSTS                                        
HOSTRLG  SYSDSNID $WYL.GS.WYL.RLG.HOSTS                                         
HOSTSYSC SYSDSNID $WYL.GS.WYL.SYSC.HOSTS                                        
HOSTSYSD SYSDSNID $WYL.GS.WYL.SYSD.HOSTS                                        
HOSTSYSH SYSDSNID $WYL.GS.WYL.SYSH.HOSTS                                        
PRINFILE SYSDSNID $WYL.GS.WYL.PRINTERS                                          
NEWSFILE SYSDSNID $WYL.GA.PUB.NEWS                                              
BATMFILE SYSDSNID $WYL.GS.WYL.LIB                                               
         TITLE 'Local Exit #1 - Output for SHOW VOLS Command'                   
*box                                                                            
*                                                                               
* EXIT #1 -- Generate SHOW VOLS Output                                          
*                                                                               
*       On Entry:  (Standard global entry conventions)                          
*                                                                               
*       On Exit:   (No special exit conditions)                                 
*                                                                               
EXIT1    XPROC ,                                                                
         VCALL COLLOPTS                                                         
*                                                                               
         SETMSG 'Public volumes:'                                               
         LA    R6,CVSTYPE          Use & save volumes                           
         LA    R5,1                Public                                       
         ACALL VOLSDISP                                                         
*                                                                               
         MVC   CPDOUB(2),CPSUSER                                                
         MVC   CPDOUB+3(2),CPSGRP                                               
         HASPSERV USERTYPE,CPDOUB                                               
         IF    (R15,Z),BEGIN                                                    
         STC   R0,CPDOUB           User type                                    
         IF    (CPDOUB,EQ,X'01'),VN  skip internal vols if open shop            
         END                                                                    
         SETMSG 'Internal volumes:'                                             
         LA    R6,CVSTYPE          Use & save volumes                           
         LA    R5,2                Internal                                     
         ACALL VOLSDISP                                                         
*                                                                               
VN       IF    (CPSGRP,EQ,'G'),BEGIN                                            
         SETMSG 'Use only volumes:'                                             
         LA    R6,CVUTYPE          Use only volumes                             
         CLEAR R5                                                               
         ACALL VOLSDISP                                                         
         END                                                                    
*                                                                               
         SETMSG 'Temporary volumes:'                                            
         LA    R6,CVTTYPE          Temporary volumes                            
         CLEAR R5                                                               
         ACALL VOLSDISP                                                         
         TCLEAR                                                                 
         IF    (CVBKUPCT,NZ),BEGIN                                              
         TSEG  ' ',,CR                                                          
         VCALL SHOWBKUP            Display backup message                       
         END                                                                    
EXIT1X   PEND  ,                                                                
         SPACE 3                                                                
*box                                                                            
*                                                                               
*  VOLSDISP -- Local routine to display volume names.                           
*                                                                               
VOLSDISP PROC  ,                                                                
         CLEAR R2                                                               
         L     R3,CVVOLIST         Volume list ptr                              
*                                                                               
         LOOP  BEGIN                                                            
         WITH  (VOLDSECT,R3)                                                    
         WHILE (VOLDSECT,NE,X'FF')                                              
         IF    (VOLUCBAD,Z),VOLNX                                               
         CLC   VTYPE(2),@R6        Right type?                                  
         BNZ   VOLNX               No                                           
         IF    (R5,EQ,1),BEGIN     Public...                                    
         IF    (VNAME,NE,'PUB'),VOLNX                                           
         END                                                                    
         IF    (R5,EQ,2),BEGIN     Internal...                                  
         IF    (VNAME,EQ,'PUB'),VOLNX                                           
         END                                                                    
         IF    (VFLG1.VF1PRIV,OR,^VFLG3.VF3SHOW),BEGIN  Priv'd...               
         IF    (CPSFSPR+CPSFOPR,Z),VOLNX  But user isn't                        
         END                                                                    
         IF    ^VFLG1.VF1AVAIL,VOLNX                                            
         IF    VFLG3.VF3BKUP,VOLNX                                              
         IF    (R2,Z),BEGIN                                                     
         TSEG  (R1),(R0),WR                                                     
         BNZ   CVABORT                                                          
         SETMSG CVBLANKS,2                                                      
         END   ,                                                                
         LA    R2,@R2+1                                                         
         L     R15,CPSEGLENF                                                    
         LA    R15,@R15+7                                                       
         IF    (R15,GT,CPSWID),BEGIN                                            
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         SETMSG CVBLANKS,2                                                      
         END                                                                    
         TSEG  (R1),(R0)                                                        
         SETMSG VNAME                                                           
         VCALL LRTRIM                                                           
         TSEG  (R1),(R0)                                                        
         IF    ^VFLG3.VF3SHOW,'TSEG "#"'                                        
         IF    VFLG1.VF1PRIV,'TSEG "*"'                                         
         SETMSG CVBLANKS,1                                                      
VOLNX    LA    R3,VOLENTSZ(R3)     Next entry                                   
         END                                                                    
*                                                                               
         IF    (R2,NZ),BEGIN                                                    
         TWRITE                                                                 
         BNZ   CVABORT                                                          
         TSEG  X'15'                                                            
         END                                                                    
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'Local Exit #2 - Supply scratch volser'                          
*box                                                                            
*                                                                               
* EXIT #2 -- Supply scratch volser                                              
*                                                                               
*       On Entry:  (No special entry conventions)                               
*                                                                               
*       On Exit:   R1,R0 -> Volser                                              
*                                                                               
EXIT2    XPROC (R2,LSR)                                                         
         SETMSG 'SCR002'                                                        
         IF    CVFRLG,'SETMSG "SCR001"'                                         
         IF    (CVMACHID,EQ,'SYSC '),'SETMSG "SCR102"'                          
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         TITLE 'Local Exit #3 - Show Space'                                     
*box                                                                            
*                                                                               
* EXIT #3 -- SHOW SPACE                                                         
*                                                                               
*       On Entry:  R0 = zero -> Unconditional                                   
*                       non-zero -> Conditional                                 
*                  R1 -> KWR                                                    
*                                                                               
*       On Exit:   (no special exit conditions)                                 
*                                                                               
EXIT3    XPROC ,                                                                
         LR    R5,R1               Save KWR ptr                                 
         USING KWR,R5                                                           
         IF    (R0,NZ),BEGIN       Conditional SHOW SPACE ...                   
         IF    CPFRCVY,EXIT3X      Skip for recovery                            
         IF    ^KWRIFL.KWRIFVAL,EXIT3X   Record not valid                       
         LTH   R6,KWRSPMAX         Max space allowed                            
         IF    NEG,EXIT3X          No limits, scram                             
         IF    (KWRSPUSD,LE,R6),EXIT3X    No problem - skip it                  
         END                                                                    
         TSEG  KWRGRP                                                           
         TSEG  '.'                                                              
         TSEG  KWRUSER,,B                                                       
         TSEG  '-',,B                                                           
         LH    R15,KWRSPUSD        Get space used                               
         COMMENT                   Add to buffer                                
         BTD   CPDOUB,0,(R15)      Convert to decimal                           
         TSEG  (R1),(R0),B         Add to buffer                                
         TSEG  'tracks accounted'                                               
         IF    (KWRSPMAX,EQ,-1),SHOSPAC1  no limits                             
         TSEG  ',',,B                                                           
         LH    R15,KWRSPMAX        Get max space count                          
         COMMENT                   Add to buffer                                
         BTD   CPDOUB,0,(R15)      Convert to decimal                           
         TSEG  (R1),(R0),B         Add to buffer                                
         TSEG  'tracks allowed.'                                                
*                                                                               
         CLC   KWRSPMAX,KWRSPUSD   Spmax>spusd                                  
         BH    SHOSPAC1            Yes, save okay                               
         TSEG  '  Sequential saves && recovery are blocked.'                    
SHOSPAC1 TCR                                                                    
EXIT3X   PEND  ,                                                                
*                                                                               
*                                                                               
         DROP  KWR                                                              
         TITLE 'Local Exit #4 - Check Space'                                    
*box                                                                            
*                                                                               
* EXIT #4 -- CHECK SPACE                                                        
*                                                                               
*       On Entry:  R1 = KWR                                                     
*                                                                               
*       On Exit:   R15 = Zero (Space not exceeded)                              
*                        Non-zero (Space exceeded)                              
*                                                                               
EXIT4    XPROC ,                                                                
         LR    R5,R1               Copy KWR ptr                                 
         USING KWR,R5                                                           
         CLEAR R15                 Preset good return code                      
         IF    ^KWRIFL.KWRIFVAL,EXIT4X  Not valid KWR                           
         IF    (KWRSPMAX,EQ,-1),EXIT4X   No limits                              
         IF    (KWRSPUSD,GE,KWRSPMAX),'LA R15,4'                                
EXIT4X   PEND  ,                                                                
         DROP  KWR                                                              
         TITLE 'Local Exit #5 - Update Space'                                   
*box                                                                            
*                                                                               
* EXIT #5 -- UPDATE SPACE                                                       
*                                                                               
*       On Entry:  R0 = Change in space                                         
*                  R1 = KWR                                                     
*                                                                               
*       On Exit:   (No special exit conditions)                                 
*                                                                               
EXIT5    XPROC ,                                                                
         LR    R3,R0               Copy space change                            
         LR    R5,R1               Copy KWR ptr                                 
         USING KWR,R5                                                           
UPDTKWR  IF    ^KWRIFL.KWRIFVAL,EXIT5X   Record not valid                       
         LH    R2,KWRSPUSD         Space used                                   
         AR    R2,R3               Delta                                        
         IF    (R2,LT,0),'CLEAR R2'                                             
         STH   R2,KWRSPUSD                                                      
         KWRITE KWR                Go rewrite record                            
         IF    ^KWRHFL.KWRHFUOK,UPDTKWR  Loop until updated                     
EXIT5X   PEND  ,                                                                
         DROP  KWR                                                              
         TITLE 'Local Exit #12 - Set local account'                             
*box                                                                            
*                                                                               
* EXIT #12 -- Set local account                                                 
*                                                                               
*       On Entry:  (Global entry conventions)                                   
*                                                                               
*       On Exit:   (Aborts if local account not set)                            
*                                                                               
         SPACE 2                                                                
X12WA    RECORD BEGIN                                                           
XACCTIN  DS    CL6                                                              
XACCTOUT DS    CL5                                                              
         END                                                                    
*                                                                               
*                                                                               
EXIT12   XPROC X12WA                                                            
*                                                                               
         SCPUSH ,                                                               
         SCAN  ,                                                                
         SCANCHK                                                                
         IF    (R0,Z),BEGIN             No options given...                     
         IF    (CPCMNM,EQ,'U'),'MVC CPUSERSV,CPSUSER'                           
         IF    (CPCMNM,EQ,'G'),'MVC CPGRPSV,CPSGRP'                             
         IF    (CPCMNM,EQ,'A'),'MVC CPACCTSV,CPSACCT'                           
         CLEAR CPGFLG3.CPFKWENT    Reset key too                                
         B     EXIT12X                                                          
         END                                                                    
         SCPOP ,                                                                
*                                                                               
         IF    (CPCMNM,NE,'A'),' SCBKUP '    Back up                            
*                                                                               
         MVC   CPCACCT,CPACCTSV    Use saved account for default                
         CLEAR R6                  Flag: "password" option not spec.            
         SCAN  SETAPRT                                                          
         SCANCHK                                                                
*                                                                               
         COMMENT                   If no password, we still check               
         COMMENT                     to see that account exists                 
         IF    (R6,Z),BEGIN                                                     
         MVC   XACCTIN(2),CPCGRP                                                
         MVI   XACCTIN+2,C'.'                                                   
         MVC   XACCTIN+3(3),CPCUSER                                             
         LA    R1,XACCTIN                                                       
         LA    R0,L'XACCTIN                                                     
         LA    R15,XACCTOUT                                                     
         VCALL CHKACCT                                                          
         IF    NZ,BEGIN                                                         
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER                                                          
         ERROR ' does not exist.'                                               
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   If password, go get password                 
         CLEAR CPGFLG3.CPFKWENT    Assume no kw                                 
         IF    (R6,NZ),BEGIN       Password option given...                     
         TSEG  'Account '                                                       
         TSEG  CPCGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPCUSER,,WR                                                      
         CLEAR R0                  Getkwr options                               
         XPUSH ,,L'KWR,PTR=R1                                                   
         WITH  (KWR,R1)                                                         
         CLEAR KWR                                                              
         MVC   KWRUSER(5),CPCACCT                                               
         VCALL GETKWR              Check password                               
         XPOP  ,,L'KWR                                                          
         SET   CPGFLG3.CPFKWENT    Password has been checked                    
         END                                                                    
*                                                                               
         MVC   CPACCTSV,CPCACCT    Set new account                              
*                                                                               
EXIT12X  PEND  ,                                                                
*                                                                               
SETAPRT  SCKW  KEYWORD,SETAKEY,A                                                
         SCKW  PASSWORD,SETAKEY,A                                               
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SETAKEY  PROC  ,                                                                
         LA    R6,1                                                             
         PRETURN (R6)                                                           
         PEND  ,                                                                
*                                                                               
         TITLE 'Local Exit #13 - Show local account'                            
*box                                                                            
*                                                                               
* EXIT #13 -- Show local account                                                
*                                                                               
*       On Entry:  (Global entry conventions)                                   
*                                                                               
*       On Exit:   (No special exit conventions)                                
*                                                                               
EXIT13   XPROC                                                                  
*                                                                               
         VCALL COLLOPTS                                                         
*                                                                               
         TSEG  CPSGRP                                                           
         TSEG  '.'                                                              
         TSEG  CPSUSER                                                          
         TSEG  ' - logged on account'                                           
         IF    CPSFPERM,'TSEG " (permanent)"'                                   
*                                                                               
         IF    ((CPUSERSV,NE,CPSUSER),OR,(CPGRPSV,NE,CPSGRP)),BEGIN             
         TSEG  ', '                                                             
         TSEG  CPGRPSV                                                          
         TSEG  '.'                                                              
         TSEG  CPUSERSV                                                         
         TSEG  ' - set account'                                                 
         IF    CPGFLG3.CPFKWENT,'TSEG " (password)"'                            
         END                                                                    
*                                                                               
         TSEG  '.',,WR                                                          
EXIT13X  PEND                                                                   
*                                                                               
         QLTORG                                                                 
         VLTORG                                                                 
*                                                                               
         END   .                                                                
