FUNC     TITLE 'WYLBUR Functions'                                               
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
* Modification history:                                                         
*                                                                               
*   3-July-1997 - M. Durket                                                     
*      Added SUBSYS_START function for Roberta.                                 
*                                                                               
*   7-July-1997 - M. Durket                                                     
*      Removed all TOEVENP calls. Generating parity for terminal                
*      output is the responsibility of the front ends, not                      
*      WYLBUR.                                                                  
*                                                                               
         SPACE 10                                                               
*******************************************************************             
*                                                                 *             
*                **** ATTENTION !!!! ****                         *             
*                **** ATTENTION !!!! ****                         *             
*                **** ATTENTION !!!! ****                         *             
*                                                                 *             
*       SEE COMMENTS ON NEXT PAGE BEFORE ADDING SYSTEM            *             
*       VARIABLES OR FUNCTIONS !!!!                               *             
*                                                                 *             
*******************************************************************             
*                                                                               
*  NOTE: SYSTEM PROCEDURES ARE LOCATED IN PROC MODULE.                          
*  NOTE: SYSTEM VARIABLES ARE LOCATED IN SYSV MODULE.                           
         EJECT                                                                  
* LAST UPDATE 7/89                                                              
*******************************************************************             
**                                                             ****             
**                                                              ***             
**      HOW TO ADD A SYSTEM VARIABLE OR FUNCTION !               **             
***     ------------------------------------------               **             
*****                                                            **             
*******************************************************************             
*                                                                               
*  PROCEDURE TO ADD VARIABLE OR FUNCTION:                                       
*                                                                               
*  1. ADD A ENTRY INTO THE SYSTEM VARIABLE/FUNCTION TABLE.                      
*                                                                               
*       THIS TABLE (SYSFNS) IS SEARCHED TO FIND THE ADDRESS OF                  
*       THE ROUTINE FOR THE SPECIFIED FUNCTION, VARIABLE, OR                    
*       PROC.  THE ENTRIES MUST APPEAR IN ALPHABETICAL ORDER.                   
*       USE THE 'SYSFN'    AND  'SYSVAR'   MACRO TO CREATE A                    
*       PROPERLY FORMATTED ENTRY.                                               
*                                                                               
*  2. ADD A SYSTEM VARIABLE/FUNCTION ROUTINE.                                   
*                                                                               
*       SYSTEM VARIABLE/FUNCTION ROUTINES ARE FOUND AFTER THE                   
*       SYSTEM VARIABLE/FUNCTION TABLE. THE ROUTINES SHOULD BE                  
*       PLACED IN ALPHABETICAL ORDER BASED ON THE VAR/FUNC                      
*       KEYWORD.  WORK AND COMMON ROUTINES SHOULD BE PLACED                     
*       IN ALPHABETICAL ORDER IN THE COMMON/WORK ROUTINES AREA.                 
*       THE BASIC ENTRY AND EXIT CONVENTIONS ARE:                               
*                                                                               
*       ON ENTRY:                                                               
*            R0 - VARIABLE COUNT                                                
*            R1 - ARGUMENT LIST                                                 
*            R6 - FUNCTION/VARIABLE RETURN AREA                                 
*                 (AREA WILL INITIALLY CONTAIN VAR/FUNC/PROC NAME)              
*                                                                               
*       ON EXIT:                                                                
*            R6 - RETURNED VALUE (SAME AS ON ENTRY)                             
*           R15 - CC=ZERO, ALL OK                                               
*                 CC=NZ, IF ERROR,,, ERROR MSG TSEG 'D                          
*                                                                               
*       THE FORMAT OF THE ARGUMENTS PASSED AND RETURNED IS                      
*       DESCRIBED BY THE VNENTRY DSECT.  EACH ARGUMENT OCCUPIES                 
*       A FIXED LENGTH (L'VPARM) ENTRY. THUS EACH INPUT                         
*       ARGUMENT (IF ANY) CAN BE EASILY FOUND AT A FIXED OFFSET                 
*       BASED OFF OF R1.                                                        
*                                                                               
*       SEVERAL MACROS EXIST TO MAKE THE ADDITION OF ROUTINES                   
*       EASIER.  TO USE THESE MACROS CERTAIN ADDITIONAL                         
*       CONVENTIONS MUST BE ADHERED TO.  THESE CONVENTIONS AND                  
*       THE MACROS ARE DESCRIBED NOW:                                           
*                                                                               
*   *** AN 'PROC' AND A 'PEND' MUST                                             
*       BE USED TO ENTER AND EXIT THE ROUTINE.                                  
*                                                                               
*   *** IF THE ROUTINE IS SUCCESSFUL R15 MUST BE RETURNED                       
*       EQUAL TO ZERO.  IF NOT R15 MUST BE RETURNED AS NON-ZERO                 
*       (4).  ALSO AN APPROPRIATE ERROR MESSAGE MUST BE TSEG'D.                 
*       ** DO NOT EXIT A ROUTINE VIA CVNEXT OR CVERROR !! **                    
*                                                                               
*   *** IF YOU INTEND TO USE THE "SCANNER", YOU MUST SAVE AND                   
*       RESTORE THE CURRENT SCANNER STATE.  FOR AN EXAMPLE SEE                  
*       'YESNO' ROUTINE.                                                        
*                                                                               
*   *** THE 'SETARGS' MACRO.                                                    
*                                                                               
*       TO CHECK ENTRY ARGUMENTS, THE 'SETARGS' MACRO IS                        
*       PROVIDED.  IT TAKES A SINGLE STRING AS A PARAMETER.                     
*       THE FIRST TWO CHARACTERS OF THIS STRING DEFINE THE                      
*       MAXIMUM AND MINIMUM NUMBER OF ARGUMENTS THAT THE                        
*       ROUTINE WILL TAKE.  THE REMAINING CHARACTERS DESCRIBE                   
*       THE FORMAT THAT THESE ARGUMENTS SHOULD BE CONVERTED TO:                 
*       S=STRING, I=INTEGER, W=LINE NUMBER, X=DON'T CARE.  FOR                  
*       AN EXAMPLE SEE THE 'FNSUBSTR' ROUTINE.  NOTE THAT THE                   
*       'SETARGS' MACRO CALLS A SETARGS ROUTINE.  THIS ROUTINE                  
*       ASSUMES THAT R0,R1 AND R6 ARE THE SAME AS WHEN THE                      
*       SYSTEM VAR/FUNC ROUTINE WAS CALLED.  'SETARGS' ALSO                     
*       WILL GIVE A NON-ZERO RETURN CODE AND TSEG A ERROR                       
*       MESSAGE IF THE ARGUMENTS CANNOT BE CONVERTED TO THE                     
*       REQUIRED FORMAT. THE 'SETARGS' MACRO DOES NOT HANDLE                    
*       'ARRAYS' OR 'PACKED' NUMBERS.                                           
*                                                                               
*   *** THE 'BADARG' MACRO                                                      
*                                                                               
*       ERROR MESSAGES CAN BE GENERATED                                         
*       WITH THE 'BADARG' MACRO.  THE MACRO TAKES A SINGLE                      
*       NUMBER AS A PARAMETER.  THIS NUMBER IS THE NUMBER OF                    
*       THE INVALID ARGUMENT.  THIS MACRO REQUIRES THAT R6 AND                  
*       THE AREA POINTED TO BY R6 BE THE SAME AS WHEN THE                       
*       SYSTEM VAR/FUNC ROUTINE WAS ENTERED.  FOR AN EXAMPLE                    
*       SEE THE 'FNSUBSTR' ROUTINE.                                             
*                                                                               
*   *** THE 'VRETURN' MACRO                                                     
*                                                                               
*       THE 'VRETURN' MACRO IS USED TO RETURN A VARIABLE OR                     
*       FUNCTION VALUE.  IT TAKES 2-3 PARAMETERS. THE FIRST IS                  
*       THE TYPE OF VALUE TO RETURN.  THIS MAY BE STRING,                       
*       INTEGER, LINENO, OR BOOLEAN.  ARRAYS AND PACKED NUMBERS                 
*       MAY NOT BE RETURNED WITH THIS MACRO.  ARGUMENTS 2-3 ARE                 
*       THE VALUE OF THE VAR/FUNC.  IF THE TYPE IS A STRING:                    
*       ARGS 2-3 ARE THE STRING LOC, LENGTH; A QUOTED STRING                    
*       MAY BE RETURNED, THE CONVENTIONS FOR RETURNING A STRING                 
*       ARE THE SAME AS FOR THE 'MSG' MACRO.  IF THE TYPE IS                    
*       INTEGER OR LINENO: THE SECOND PARAMETER IS A FULLWORD                   
*       VALUE.  IF THE TYPE IS BOOLEAN: THE SECOND PARAMETER IS                 
*       THE WORD 'TRUE' OR THE WORD 'FALSE'.  THIS MACRO                        
*       REQUIRES THAT R6 BE THE SAME AS WHEN THE SYSTEM                         
*       VAR/FUNC. ROUTINE WAS ENTERED.  FOR EXAMPLES, SEE THE                   
*       'FNCASE' 'FNLENGTH' 'FNLILB' 'FNLAST' AND 'FNTRUE'.                     
*       SOME ISOLATED EXAMPLES ARE:                                             
*             VRETURN STRING,' THIS IS IT '                                     
*             VRETURN STRING,(R1),(R0)                                          
*             VRETURN BOOLEAN,TRUE                                              
*             VRETURN LINENO,L:XXLINENO                                         
*             VRETURN INTEGER,(R4)                                              
*       HEY DUDE !! THE VRETURN MACRO CLOBBERS R15,R0,R1,R2 SO                  
*       BE CAREFUL.  R15 IS SET TO ZERO BY THE VRETURN MACRO.                   
*                                                                               
*   *** THE 'VTRIM' MACRO                                                       
*                                                                               
*       THE 'VTRIM' MACRO IS USED TO TRIM TRAILING BLANKS OFF                   
*       THE END OF RETURNED STINGS.  IT WILL ALSO SET BLANK OR                  
*       ZEROED STRINGS TO NULL STRINGS.  IT REQUIRES THAT R6 BE                 
*       THE SAVE AS WHEN THE SYSTEM FUNC/VAR ROUTINE WAS                        
*       ENTERED.  IT IS GENERALLY CODED DIRECTLY FOLLOWING THE                  
*       'VRETURN STRING,..' MACRO.  FOR AN EXAMPLE SEE 'FNLIB'                  
*       ROUTINE.                                                                
*                                                                               
*   *** GENERAL ARGUMENT LOADING/CHECKING PROCEDURES                            
*                                                                               
*       ARGUMENTS PASSED TO A ROUTINE ARE GENERALLY CHECKED FOR                 
*       VALIDITY AT THE VERY BEGINNING OF THE ROUTINE WITH THE                  
*       'SETARGS' MACRO.  THE ARGUMENTS ARE THEN LOADED AND                     
*       CHECKED (WHILE THE ENTRY REGS ARE STILL SET UP)                         
*       GENERALLY AS FOLLOWS:  (THIS IS AN EXAMPLE ONLY, OH                     
*       YEAH:)                                                                  
*                                                                               
*       FNX      PROC  ,                                                        
*                SETARGS '22SI'            2 ARGS: STRING, INTEGER              
*                IF    Z,BEGIN             IF ARGS OK,...                       
*                USING VNENTRY,R5                                               
*                LA    R5,ARG1(R1)         R5 - 1ST ARG POINTER                 
*                LA    R2,VNSTRLOC         R2 - LOC OF STRING                   
*                LH    R3,VNSTRLEN         R3 - LEN OF STRING                   
*                IF    (R3,GT,120),BEGIN                                        
*                BADARG 1                  PRETEND CHECK FOR STR LEN            
*                EXIT  FNX                                                      
*                END                                                            
*                LA    R5,ARG2(R1)         R5 - 2ND ARG POINTER                 
*                L     R4,VNVALUE          R4 - INTEGER VALUE                   
*                IF    ((R4,NEG),OR,(R4,GT,120)),BEGIN                          
*                BADARG 2                  PRETEND CHECK FOR ARG 2              
*                EXIT  FNX                                                      
*                END                                                            
*                ..... AND SO ON                                                
*                                                                               
*                                                                               
*                ... END OF ROUTINE, XEXIT ...                                  
*                                                                               
*                                                                               
*   *** RETURNING VAR/FUNCTION VALUE,,,                                         
*                                                                               
*       THE EASY WAY IS WITH THE 'VRETURN MACRO', BUT THERE ARE                 
*       OTHER WAYS.  YOU MAY SET UP YOUR OWN RETURN VALUE @R6.                  
*       SEE VNENTRY DSECT. THE AREA MUST BE CLEARED.  FLAGS                     
*       MUST BE SET FOR THE VARIABLE TYPE:  STRING, BOOLEAN,                    
*       NUMBER.  THE VARIABLE VALUE MUST BE SET.  FOR EXAMPLES,                 
*       SEE 'FNASCII' 'FNPI' OR 'FNSUBSTR'.  IF YOU WANT TO                     
*       DEAL WITH ARRAYS (IF THEY EXIST),,, GOOD LUCK HASBRO !!                 
*                                                                               
*   *** FINAL ADVICE                                                            
*                                                                               
*       THIS MODULE ADHERES TO SEVERAL CONVENTIONS.  IF YOU                     
*       ADHERE TO THESE CONVENTIONS, IT WILL MAKE IT EASIER FOR                 
*       YOU AND ALL THE PEOPLE WHO ALSO WANT TO ADD VARS/FUNCS                  
*       TO ADD THEM. IT IS WORTH IT TO TAKE A BIT OF TIME AND                   
*       LOOK AT HOW SEVERAL EXITING SYSTEM VARIABLE/FUNCTION                    
*       ROUTINES ARE CODED.                                                     
*                                                                               
*   *** SOME DOCUMENTATION                                                      
*                                                                               
*        ON ENTRY:                                                              
*             R0 - VARIABLE COUNT                                               
*             R1 - ARGUMENT LIST                                                
*             R6 - FUNCTION/VARIABLE RETURN AREA                                
*                  (AREA WILL INITIALLY CONTAIN VAR/FUNC/PROC NAME)             
*                                                                               
*                                                                               
*        ON EXIT:                                                               
*             R6 - RETURNED VALUE (SAME AS ON ENTRY)                            
*            R15 - CC=ZERO, ALL OK                                              
*                  CC=NZ, IF ERROR,,, ERROR MSG TSEG 'D                         
*                                                                               
*        ARG1     EQU   0*L'VPARM           USUALLY @R1                         
*        ARG2     EQU   1*L'VPARM                                               
*        ARG3     EQU   2*L'VPARM                                               
*        ARG4     EQU   3*L'VPARM                                               
*        ARG5     EQU   4*L'VPARM                                               
*        ARG6     EQU   5*L'VPARM                                               
*                                                                               
*        FNRET    EQU   0*L'VPARM           USUALLY @R6                         
*                                                                               
*                                                                               
*        FNWA     RECORD BEGIN ,            GENERALIZED WORK AREA !             
*        FNWORK   DS    XL(L'VPARM)         256+ BYTE WORK AREA                 
*                 END                                                           
*        ******************** OH YEAH !!                                        
         EJECT                                                                  
WYLFUNC  CSECT                                                                  
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
FUNC     IDENT 2193                15:11:44 07/12/02  (SLP)                     
*                                                                               
         SYSDEFN ,                 Define installation values                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
         COPY  CONTROL             Copy CV/CP                                   
         COPY  SSB                                                              
         TITLE 'DSECTS'                                                         
*        CVT   DSECT=YES                                                        
*                                                                               
*SERCVT  RECORD 'USERCVT'                                                       
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
KWR      RECORD BEGIN                                                           
         KWR                                                                    
         END                                                                    
*                                                                               
SIN      RECORD BEGIN                                                           
         COPY  SIN                                                              
         END                                                                    
*                                                                               
         POP   DSECTS                                                           
WYLFUNC  CSECT                                                                  
         EJECT                                                                  
         COPY  BSINFO                                                           
         EJECT                                                                  
         COPY  VNENTRY                                                          
         COPY  VPARM                                                            
         EJECT                                                                  
         COPY  XSENTRY                                                          
         EJECT                                                                  
         COPY  SHDSARGS                                                         
         COPY  FFINFO                                                           
         COPY  DSNINFO                                                          
         EJECT                                                                  
         TITLE 'Local Macros'                                                   
*                                                                               
*                                                                               
*   SYSTEM ROUTINE TABLE ENTRY MACRO                                            
*                                                                               
*                                                                               
RTNENTRY RECORD BEGIN                                                           
RTNNAME  DS    CL16                                                             
RTNPTR   DEFRTNP                                                                
         END                                                                    
*                                                                               
*C-DATA  struct sysrtn {                                                        
*C-DATA    unsigned char name[16];                                              
*C-DATA    void (* routine)();                                                  
*C-DATA  };                                                                     
*                                                                               
*                                                                               
         DS    0S(16-L'VNNAME)     VNNAME MUST BE 16 OR FIXED ABOVE             
         DS    0S(L'VNNAME-16)       AND FIXED IN SYSRTN MACRO                  
         DS    0S(16-L'RTNNAME)    RTNNAME MUST BE 16 OR FIXED ABOVE            
         DS    0S(L'RTNNAME-16)      AND FIXED IN SYSRTN MACRO                  
*                                                                               
         MACRO                                                                  
&L       SYSFN     &NAME,&ROUTINE                                               
&L       SYSRTN    &NAME,&ROUTINE                                               
         MEND                                                                   
*                                                                               
         MACRO                                                                  
&L       LA$TABLE &RX,&LOC                                                      
         GBLB  &$PCODE                                                          
         AIF   (&$PCODE).CGENLA                                                 
         L     &RX,=A(&LOC)                                                     
         MEXIT                                                                  
.CGENLA  ANOP                                                                   
         MNOTE *,'C-CODE &RX = (long int)((char *)( && &LOC));'                 
         MEND                                                                   
*                                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*   LOCAL MACROS                                                                
*                                                                               
*        SETARGS - SET ARG TYPES                                                
*        RETARGS - RETURN PROCEDURE ARGS                                        
*        VRETURN - RETURN VARIABLE VALUE, TYPE                                  
*        BADARG - CREATE BAD ARGUMENT MESSAGE                                   
*                                                                               
*.... ..   -... ..- -..                                                         
         SPACE 2                                                                
*                                                                               
*                               SET ARGUMENT TYPES                              
         MACRO                                                                  
&L       SETARGS  &LIST                                                         
&L       LA    R2,=C&LIST          LIST OF ARGUMENT TYPES TO SET                
         ACALL SETARGS             R1 - ARG LIST, R0 - ARG COUNT                
         MEND                                                                   
         SPACE 2                                                                
*                                                                               
*                             RETURN VARIABLE @R6                               
         MACRO                                                                  
&L       VRETURN &TYPE,&VALUE,&LEN                                              
&L       DS    0H                                                               
         AIF   ('&TYPE' EQ 'STRING').TYPESTR                                    
         AIF   ('&TYPE' EQ 'INTEGER').TYPEINT                                   
         AIF   ('&TYPE' EQ 'LINENO').TYPELNO                                    
         AIF   ('&TYPE' EQ 'BOOLEAN').TYPEBOOL                                  
         MNOTE 8,'INVALID TYPE SPECIFIED'                                       
.*                                                                              
.*                                 RETURN STRING VALUE                          
.TYPESTR ANOP                                                                   
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                 CLEAR ENTRY                              
         SET   VNSTRING                SET STRING TYPE                          
         SETMSG &VALUE,&LEN                                                     
         STH   R0,VNSTRLEN             SAVE STRING LEN                          
         LTR   R3,R0                   SAVE STRING VALUE                        
         IF    POS,BEGIN                                                        
         DEX   R3,' MVC VNSTRLOC(0),@R1 '                                       
         END                                                                    
         SR    R15,R15                                                          
         END   , WITH (VNENTRY,R6)                                              
         MEXIT                                                                  
.*                                 RETURN INTEGER VALUE                         
.TYPEINT ANOP                                                                   
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                 CLEAR ENTRY                              
         SET   VNNUMB                  SET NUMBER TYPE                          
         $L    R1,&VALUE,OP=LA         SET VALUE                                
         ST    R1,VNVALUE                                                       
         LA    R2,VNENTRY                                                       
         VCALL MAKEITOD                ALL #'S RETURNED AS PKD DEC              
         END   , WITH (VNENTRY,R6)                                              
         MEXIT                                                                  
.*                                 RETURN LINE NUMBER VALUE                     
.TYPELNO ANOP                                                                   
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                 CLEAR ENTRY                              
         SET   VNNUMB                  SET NUMBER TYPE                          
         $L    R1,&VALUE,OP=LA         SET VALUE                                
         ST    R1,VNVALUE                                                       
         LA    R2,VNENTRY                                                       
         VCALL MAKELTOD                ALL #'S RETURNED AS PKD DEC              
         END   , WITH (VNENTRY,R6)                                              
         MEXIT                                                                  
.*                                 RETURN BOOLEAN                               
.TYPEBOOL ANOP                                                                  
         AIF   ('&VALUE' EQ 'TRUE').TRUEVAL                                     
         AIF   ('&VALUE' EQ 'FALSE').FALSEVAL                                   
         MNOTE 8,'INVALID BOOLEAN VALUE'                                        
.TRUEVAL ANOP                                                                   
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                 CLEAR ENTRY                              
         SET   VNBOOL                  SET BOOLEAN TYPE                         
         MVC   VNVALUE(12),=X'00000000000001000000000C'   TRUE                  
         SR    R15,R15                                                          
         END   , WITH (VNENTRY,R6)                                              
         MEXIT                                                                  
.FALSEVAL ANOP                                                                  
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                 CLEAR ENTRY                              
         SET   VNBOOL                  SET BOOLEAN TYPE                         
         MVC   VNVALUE(12),=X'00000000000000000000000C'   FALSE                 
         SR    R15,R15                                                          
         END   , WITH (VNENTRY,R6)                                              
         MEXIT                                                                  
.*                                 DECIMAL/ARRAY RETURNS MUST                   
.*                                 BE CODED BY HAND !!!!                        
         MEND                                                                   
         SPACE 2                                                                
*                                                                               
*                               TRIM BLANKS FROM END OF STRING                  
         MACRO                                                                  
&L       VTRIM                                                                  
&L       ACALL VTRIM               TRIM BLANKS, R6 - VAR ENTRY                  
         MEND                                                                   
         SPACE 2                                                                
*                                                                               
*                               BAD ARGUMENT                                    
         MACRO                                                                  
&L       BADARG &ARGNO                                                          
&L       $L    R2,&ARGNO,OP=LA     NUMBER OF BAD ARGUMENT                       
         ACALL BADARG                                                           
         MEND                                                                   
         TITLE 'Binary Search Info for Procedure List'                          
*                                                                               
**                                                                              
**  BINARY SEARCH INFO FOR TABLE OF SYSTEM FUNCTIONS                            
**                                                                              
**  INITIALIZED BY INITFNS ROUTINE IN THIS MODULE.                              
*$  SHOULD GETMAIN THIS AREA TO MAKE REENTRANT !                                
**                                                                              
*                                                                               
         TITLE 'Initialize System Variables/Functions'                          
*box                                                                            
*                                                                               
*                                                                               
*  INITFUNC - FUNC MODULE INITIALIZATION                                        
*                                                                               
*  SET UP BINARY SEARCH INFO FOR SYSTEM VARIABLES/FUNCTIONS.                    
*                                                                               
*                                                                               
*                                                                               
INITFUNC XPROC ,                                                                
*                                                                               
         COMMENT                   GET MEMORY FOR FNS BSEARCH INFO              
         LA    R0,L'BSINFO                                                      
         VCALL GETCORE                                                          
         ST    R1,CVFBINFO                                                      
*                                                                               
         COMMENT                   FUNCTIONS BSEARCH INFO                       
         L     R0,CVFBINFO         R0 - BSEARCH INFO                            
         LA$TABLE R1,SYSFEND       R1 - TOTAL POSSIBLE TABLE LEN                
         LA$TABLE R2,SYSFNS                                                     
         SR    R1,R2                                                            
         LA    R2,L'RTNENTRY       R2 - ENTRY LENGTH                            
         LA    R3,L'RTNNAME        R3 - SEARCH LENGTH (NAME LEN)                
         LR    R4,R1               R4 - END OF LAST ENTRY = TAB LEN             
         VCALL BSINIT                                                           
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  GTFNRTN - GET SYSTEM FUNCTION ROUTINE ADDRESS                                
*                                                                               
*  ON ENTRY:                                                                    
*    @R0 - PLACE TO RETURN ROUTINE ADDRESS                                      
*    @R1 - FUNCTION NAME, LEFT JUSTIFIED BLANK PADDED                           
*                                                                               
*  ON EXIT:                                                                     
*    R15=Z, ROUTINE FOUND;  R15=NZ, ROUTINE NOT FOUND                           
*    R1 - POINTS TO FULL NAME OF FUNCTION (NOT SAME AS ENTRY VALUE)             
*                                                                               
*                                                                               
*  NOTE:                                                                        
*  IF ROUTINE POINTER IS ZERO, ROUTINE IS NOT DEFINED. THIS IS                  
*  SO THAT WE CAN CONTROL WHICH ABBREVAIATIONS FIND MATCHES.                    
*                                                                               
*                                                                               
GTFNRTN  XPROC ,                                                                
*                                                                               
         LR    R4,R0                                                            
         LR    R5,R1                                                            
*                                                                               
         COMMENT                   SEARCH FOR SYSTEM ROUTINE                    
         L     R0,CVFBINFO                                                      
         LA$TABLE R1,SYSFNS                                                     
         LR    R2,R5 IE. NAME                                                   
         VCALL ABSEARCH                                                         
*                                                                               
         COMMENT                   IF FOUND RETURN VALUES                       
         IF    (R15,Z),BEGIN                                                    
         LR    R6,R1                                                            
         USING RTNENTRY,R6                                                      
         IF    (RTNPTR,NZ),BEGIN                                                
         MVC   @R4(L'RTNPTR),RTNPTR                                             
         LA    R1,RTNNAME                                                       
         DROP  R6                                                               
         END                                                                    
         ELSE  BEGIN               IF NO RTNPTR, RETURN R15=NZ                  
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
         PRETURN (R1)                                                           
         PEND                                                                   
         TITLE 'ENTRY/EXIT Routines for System Vars/Functions'                  
*box                                                                            
*                                                                               
*                                                                               
*  SETARGS - CHECK FUNCTION ARGUMENTS                                           
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - NUMBER OF ARGUMENTS                                                
*       R1 - ADDRESS OF ARGUMENTS                                               
*       R2 - TYPE LIST, AS FOLLOWS                                              
*            1 BYTE: MINIMUM NUMBER OF ARGS, 0-9                                
*            1 BYTE: MAXIMUM NUMBER OF ARGS, 0-9                                
*            N BYTES: ARG TYPES                                                 
*               S - STRING                                                      
*               N - DECIMAL NUMBER                                              
*               I - INTEGER FULLWORD (INTERNAL ONLY)                            
*               W - LINE NUMBER FULLWORD (INTERNAL ONLY)                        
*               B - BOOLEAN                                                     
*               X - DON'T CARE                                                  
*       R6 - VARIABLE ENTRY (CONTAINS FUNCTION/PROC NAME)                       
*                                                                               
*  ON EXIT:                                                                     
*       R15 - CC=ZERO, IF ALL ARG TYPES SET OK                                  
*             CC=NZ, IF ERROR... ERROR MSG IS TSEG'D                            
*                                                                               
*                                                                               
*                                                                               
SETARGS  XPROC ,                                                                
         LR    R5,R2               R5 - ARG TYPE LIST                           
* IF TOO FEW ARGS                                                               
         IC    R3,0(R5)                                                         
         N     R3,=X'0000000F'                                                  
         IF    (R0,LT,R3),BEGIN    IF TOO FEW ARGS,                             
         TSEG  'Too few arguments specified for "'                              
         TSEG  (R6),L'VNNAME,T                                                  
         TSEG  '". '                                                            
         LA    R15,4                                                            
         B     SETADONE                                                         
         END                                                                    
* IF TOO MANY ARGS                                                              
         IC    R3,1(R5)                                                         
         N     R3,=X'0000000F'                                                  
         IF    (R0,GT,R3),BEGIN    IF TOO MANY ARGS,                            
         TSEG  'Too many arguments specified for "'                             
         TSEG  (R6),L'VNNAME,T                                                  
         TSEG  '". '                                                            
         LA    R15,4                                                            
         B     SETADONE                                                         
         END                                                                    
* LOOP CONVERTING ARGS                                                          
         LA    R5,2(R5)            R5 - ARGUMENT LIST POINTER                   
         LA    R4,1                R4 - ARG NUMBER                              
         WHILE (R4,LE,R0),BEGIN                                                 
         IF    (@R5,EQ,C'S'),BEGIN      STRING                                  
         LR    R2,R1                                                            
         VCALL MAKESTR                                                          
         IF    NZ,SETABAD                                                       
         END                                                                    
         ELSEIF (@R5,EQ,C'I'),BEGIN     INTEGER                                 
         LR    R2,R1                                                            
         VCALL MAKEINT                                                          
         IF    NZ,SETABAD                                                       
         END                                                                    
         ELSEIF (@R5,EQ,C'W'),BEGIN      LINE NUMBER                            
         LR    R2,R1                                                            
         VCALL MAKELNO                                                          
         IF    NZ,SETABAD                                                       
         END                                                                    
         ELSEIF (@R5,EQ,C'N'),BEGIN      PACKED DECIMAL                         
         LR    R2,R1                                                            
         VCALL MAKENUMB                                                         
         IF    NZ,SETABAD                                                       
         END                                                                    
         ELSEIF (@R5,EQ,C'B'),BEGIN      BOOLEAN                                
         LR    R2,R1                                                            
         VCALL MAKEBOOL                                                         
         IF    NZ,SETABAD                                                       
         END                                                                    
         ELSEIF (@R5,EQ,C'X'),BEGIN      DON'T CARE                             
         END                                                                    
         ELSEIF (R0,EQ,R0),BEGIN         ALL OTHERS INVALID                     
         TSEG   'Invalid specification of argument type. '                      
         B     SETABAD                                                          
         END                                                                    
         LA    R5,1(R5)            BUMP TYPE LIST POINTER                       
         LA    R4,1(R4)            BUMP ARG COUNT                               
         LA    R1,L'VPARM(R1)      POINT AT NEXT ARGUMENT                       
         END                                                                    
SETAGOOD SR    R15,R15             SUCCESS !!!                                  
         B     SETADONE                                                         
*                                                                               
SETABAD  LABEL ,                                                                
         BADARG (R4)                                                            
         B     SETADONE                                                         
*                                                                               
SETADONE LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  VTRIM - TRIM TRAILING BLANKS FROM A VAR ENTRY                                
*                                                                               
*  ON ENTRY - R6 VARIABLE ENTRY                                                 
*                                                                               
*  ROUTINE CALLED VIA VTRIM MACRO, TO TRIM SYSTEM VARIABLES                     
*  THAT ARE LEFT JUSTIFIED AND PADDED WITH BLANKS, OR ALL                       
*  ZEROS.                                                                       
*                                                                               
*                                                                               
VTRIM    XPROC ,                                                                
         USING VNENTRY,R6                                                       
         LA    R2,VNSTRLOC         R2 - STRING LOCATION                         
         LH    R3,VNSTRLEN         R3 - STRING LEN                              
         SR    R5,R5                                                            
         CLCL  R2,R4                                                            
         IF    E,BEGIN             IF FIELD IS ALL ZERO,                        
         SR    R0,R0                  RETURN NULL STRING                        
         STH   R0,VNSTRLEN                                                      
         B     TRIMDONE                                                         
         END                                                                    
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         L     R5,=X'40000000'                                                  
         CLCL  R2,R4                                                            
         IF    E,BEGIN             IF FIELD IS ALL BLANKS,                      
         SR    R0,R0                  RETURN NULL STRING                        
         STH   R0,VNSTRLEN                                                      
         B     TRIMDONE                                                         
         END                                                                    
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         AR    R3,R2               R2 - START, R3 - END OF STRING               
         BCTR  R3,0                                                             
         COMMENT                   TRIM TRAILING BLANKS                         
         WHILE ((R3,GE,R2),AND,(@R3,EQ,' ')),' BCTR R3,0 '                      
         LA    R3,1(R3)                                                         
         SR    R3,R2               R3 - LENGTH AFTER TRIMMING                   
         STH   R3,VNSTRLEN                                                      
         B     TRIMDONE                                                         
         DROP  R6                                                               
*                                                                               
TRIMDONE LABEL ,                                                                
         SR    R15,R15                                                          
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BADARG - TSEG BAD ARGUMENT MESSAGE                                           
*                                                                               
*  ON ENTRY:                                                                    
*       R2 - ARGUMENT NUMBER                                                    
*       R6 - VAR ENTRY, CONTAINS FUNCTION/PROCEDURE NAME                        
*                                                                               
*  ON EXIT:                                                                     
*       ERROR MESSAGE IS TSEG'D                                                 
*                                                                               
*                                                                               
*                                                                               
BADARG   XPROC ,                                                                
         TCCR                                                                   
         TSEG  'Argument #'        ARGUMENT #n OF XYZ IS INVALID.               
         TNUM  (R2)                                                             
         TSEG  ' of "'                                                          
         TSEG  (R6),L'VNNAME,T                                                  
         TSEG  '" is invalid. '                                                 
         MVC   CPERRID,=CL8'BADARG  '    ERRID = 'BADARG'                       
         LA    R15,4                                                            
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'Table of System Functions'                                      
         DS    0D                                                               
*box                                                                            
*                                                                               
*                                                                               
*  SORTED TABLE OF SYSTEM FUNCTIONS                                             
*                                                                               
*  FOR TABLE ENTRIES USE THE FOLLOWING MACRO:                                   
*       SYSFN   <FUNCTION NAME>,<ROUTINE>                                       
*                                                                               
*                                                                               
*       **** CAUTION !!! ****                                                   
*  ENTRIES MUST BE SORTED ALPHABETICALLY BY NAME !!                             
*                                                                               
*                                                                               
*                                                                               
*                                                                               
         COMMENT                   SYSTEM FUNCTIONS                             
SYSFNS   SYSFN   ABBREV,FNABBREV       ALPHABETIZE !                            
         SYSFN   ABS,FNABS                                                      
         SYSFN   ACCOUNT_INFO,FNACCTNF                                          
         SYSFN   ARRAY_SEARCH,FNASRCH                                           
         SYSFN   ASCII,FNASCII         ALPHABETIZE !                            
         SYSFN   BIT,FNBITTST                                                   
         SYSFN   BITAND,FNBITAND                                                
         SYSFN   BITOR,FNBITOR                                                  
         SYSFN   BITTEST,FNBITTST                                               
         SYSFN   BITXOR,FNBITXOR                                                
         SYSFN   BOUNDS,FNBOUNDS                                                
         SYSFN   CENTER,FNCENTER                                                
         SYSFN   CENTRE,FNCENTER                                                
         SYSFN   CHECK_ACCOUNT,FNCHKACC                                         
         SYSFN   CHSTR,FNCHSTR                                                  
         SYSFN   COMPARE,FNCOMP                                                 
         SYSFN   COPIES,FNCOPIES                                                
         SYSFN   C2D,FNC2D                                                      
         SYSFN   C2X,FNC2X                                                      
         SYSFN   DATATYPE,FNDTYPE                                               
         SYSFN   DECRYPT,FNDECRYP                                               
         SYSFN   DELSTR,FNDELSTR                                                
         SYSFN   DELWORD,FNDELWRD                                               
         SYSFN   DEST_VERIFY,FNDESTV   ALPHABETIZE !                            
         SYSFN   DISPLAY,FNDISPLY                                               
         SYSFN   DSNDEQ,FNDSNDEQ                                                
         SYSFN   DSNENQ,FNDSNENQ                                                
         SYSFN   DSNFMT,FNDSNFMT                                                
         SYSFN   DSNFORMAT,FNDSNFMT                                             
         SYSFN   DSNINFO,FNDSNINF                                               
         SYSFN   DSNPROT,FNDPROT       ALPHABETIZE !                            
         SYSFN   DSNVERIFY,FNDSNVER    ALPHABETIZE !                            
         SYSFN   D2C,FND2C                                                      
         SYSFN   D2X,FND2X                                                      
         SYSFN   EBCDIC,FNEBCDIC                                                
         SYSFN   ENCRYPT,FNENCRYP                                               
         SYSFN   FIND,FNFIND                                                    
         SYSFN   FORMAT,FNFORMAT                                                
         SYSFN   GET_ACCOUNT,FNGETACC                                           
         SYSFN   GET_USERID,FNGETID                                             
         SYSFN   GET_USERID_LIST,FNGETIDL                                       
         SYSFN   GET_USERID_TYPE,FNGETIDT                                       
         SYSFN   GMT,FNGETGMT                                                   
         SYSFN   HOST_INFO,FNHOSTI                                              
         SYSFN   HOST_USER,FNHOSTU                                              
         SYSFN   INCHAR,FNINCHAR                                                
         SYSFN   INDEX,FNINDEX                                                  
         SYSFN   INPUT,FNINPUT                                                  
         SYSFN   INSERT,FNINSERT                                                
         SYSFN   INT,FNINT                                                      
         SYSFN   JUSTIFY,FNJUST                                                 
         SYSFN   LASTPOS,FNLPOS                                                 
         SYSFN   LEFT,FNLEFT                                                    
         SYSFN   LINE,FNLINE                                                    
         SYSFN   LINE_APPEND,FNLNAPND  ALPHABETIZE !                            
         SYSFN   LINE_DELETE,FNLNDEL                                            
         SYSFN   LINE_PUT,FNLNPUT                                               
         SYSFN   LNO,FNLNO                                                      
         SYSFN   LNO_CHECK,FNLNCHK     ALPHABETIZE !                            
         SYSFN   LNO_END,FNLNEND                                                
         SYSFN   LNO_FIELD,FNLNFLD                                              
         SYSFN   LNO_FIRST,FNLNFRST                                             
         SYSFN   LNO_LAST,FNLNLAST                                              
         SYSFN   LNO_NEXT,FNLNNEXT                                              
         SYSFN   LNO_PREV,FNLNPREV                                              
         SYSFN   LOWER,FNLOWER                                                  
         SYSFN   MATCH,FNMATCH                                                  
         SYSFN   MAX,FNMAX                                                      
         SYSFN   MIN,FNMIN                                                      
         SYSFN   NCONVERT,FNNCONV                                               
         SYSFN   NDUMP,FNNDUMP                                                  
         SYSFN   NHEX,FNNHEX                                                    
         SYSFN   NOPARITY,FNNOPAR      ALPHABETIZE !                            
         SYSFN   ORVYL_CPU_,FNGORVC                                             
         SYSFN   ORVYL_IO_,FNGORVI                                              
         SYSFN   OVERLAY,FNOLAY                                                 
         SYSFN   PARITY,FNPARITY                                                
         SYSFN   POS,FNPOS                                                      
         SYSFN   QSTATE,FNQSTATE                                                
         SYSFN   QUERY,FNQUERY                                                  
         SYSFN   REC_CLOSE,FNRECCLS                                             
         SYSFN   REC_COUNT,FNRECCNT                                             
         SYSFN   REC_DELETE,FNRECDEL                                            
         SYSFN   REC_FIRSTKEY,FNRECFRK                                          
         SYSFN   REC_GET,FNRECGET                                               
         SYSFN   REC_LASTKEY,FNRECLAK                                           
         SYSFN   REC_NEXTKEY,FNRECNEK                                           
         SYSFN   REC_OPEN,FNRECOPN                                              
         SYSFN   REC_PREVKEY,FNRECPRK                                           
         SYSFN   REC_PUT,FNRECPUT                                               
         SYSFN   REC_TEST,FNRECTST                                              
         SYSFN   REVERSE,FNREV         ALPHABETIZE !                            
         SYSFN   RF_DECRYPT_V8B,FNV8BD                                          
         SYSFN   RF_DEST_CHECK,FNDESTV                                          
         SYSFN   RF_ENCRYPT_V8B,FNV8BE                                          
         SYSFN   RF_PW_CHECK,FNPASSV                                            
         SYSFN   RF_PW_PROMPT,FNPASSP                                           
         SYSFN   RIGHT,FNRIGHT         ALPHABETIZE !                            
         SYSFN   RMATCH,FNRMATCH                                                
         SYSFN   SCAN_SKIP,FNSCANSK                                             
         SYSFN   SCAN_TOKEN,FNSCANTK                                            
         SYSFN   SCONVERT,FNSCONV                                               
         SYSFN   SDUMP,FNSDUMP                                                  
         SYSFN   SHEX,FNSHEX                                                    
         SYSFN   SIGN,FNSIGN                                                    
         SYSFN   SIZE,FNSIZE           ALPHABETIZE !                            
         SYSFN   SPACE,FNSPACE                                                  
         SYSFN   SQUASH,FNSQUASH                                                
         SYSFN   STRIP,FNSTRIP                                                  
         SYSFN   SUB,FNSUBSTR          ALPHABETIZE !                            
         SYSFN   SUBSTR,FNSUBSTR       ALPHABETIZE !                            
         SYSFN   SUBSYS_START,FNSUBCLK                                          
         SYSFN   SUBWORD,FNSUBWRD                                               
         SYSFN   SYSTEM_UP,FNSYSUP                                              
         SYSFN   TODTIME,FNTODTIM                                               
         SYSFN   TRANSLATE,FNXLATE                                              
         SYSFN   TRUNC,FNTRUNC                                                  
         SYSFN   UPPER,FNUPPER                                                  
         SYSFN   URL_DECODE,FNURLDCD                                            
         SYSFN   VARTYPE,FNVARTYP                                               
         SYSFN   VERIFY,FNVERIFY                                                
         SYSFN   WCONVERT,FNWCONV                                               
         SYSFN   WCOUNT,FNWCOUNT                                                
         SYSFN   WHEX,FNWHEX                                                    
         SYSFN   WORD,FNWORD           ALPHABETIZE !                            
         SYSFN   WORDINDEX,FNWORDIX                                             
         SYSFN   WORDLENGTH,FNWORDLN                                            
         SYSFN   WORDS,FNWORDS                                                  
         SYSFN   WYLBUR_CPU_,FNGWYLC                                            
         SYSFN   WYLBUR_IO_,FNGWYLI                                             
         SYSFN   XRANGE,FNXRANGE                                                
         SYSFN   X2C,FNX2C                                                      
         SYSFN   X2D,FNX2D                                                      
         SYSFN   YESNO,FNYESNO         ALPHABETIZE !                            
SYSFEND  SYSFN   ZZZDUMMY,NULL     END OF SYSTEM TABLE                          
*                                  END OF SYSTEM TABLE                          
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'System Routines for Functions'                                  
*box                                                                            
*                                                                               
*                                                                               
*  SYSTEM FUNCTION, PROCEDURE, AND VARIABLE ROUTINES                            
*                                                                               
*  ****NOTE !!                                                                  
*  SEE COMMENTS AT START OF THIS MODULE.                                        
*                                                                               
*                                                                               
*  ON ENTRY:                                                                    
*       R0 - VARIABLE COUNT                                                     
*       R1 - ARGUMENT LIST                                                      
*       R6 - FUNCTION/VARIABLE RETURN AREA                                      
*            (AREA WILL INITIALLY CONTAIN FUNCTION/PROC/VAR NAME)               
*                                                                               
*                                                                               
*  ON EXIT:                                                                     
*       R6 - RETURNED VALUE (SAME AS ON ENTRY)                                  
*      R15 - CC=ZERO, ALL OK                                                    
*            CC=NZ, IF ERROR,,, ERROR MSG TSEG 'D                               
*                                                                               
*                                                                               
*                                                                               
ARG1     EQU   0*L'VPARM           USUALLY @R1                                  
ARG2     EQU   1*L'VPARM                                                        
ARG3     EQU   2*L'VPARM                                                        
ARG4     EQU   3*L'VPARM                                                        
ARG5     EQU   4*L'VPARM                                                        
ARG6     EQU   5*L'VPARM                                                        
ARG7     EQU   6*L'VPARM                                                        
ARG8     EQU   7*L'VPARM                                                        
*                                                                               
FNRET    EQU   0*L'VPARM           USUALLY @R6                                  
*                                                                               
*                                                                               
FNWA     RECORD BEGIN ,            GENERALIZED WORK AREA !                      
FNWORK   DS    XL(L'VPARM)         256+ BYTE WORK AREA                          
         END                                                                    
*                                                                               
*                                                                               
**************** ROUTINES FOLLOW BELOW !!                                       
*box                                                                            
*                                                                               
*  FNINV -- Generic error message.                                              
*                                                                               
FNINV    PROC                                                                   
         TSEG  'Invalid variable or function name.',,CR                         
*                                                                               
         LA    R15,4               Error                                        
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ABBREV -- ABBREV Function.                                                   
*                                                                               
*    ABBREV(info-string,match-string[,length])                                  
*                                                                               
FNABBREV PROC  ,                                                                
         SETARGS '23SSI'           2-3 args: string, string, integer            
         IF    Z,BEGIN             Ok...                                        
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg#1:  information string.                                            
*-                                                                              
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - information str len, loc             
         LH    R3,VNSTRLEN                                                      
*-                                                                              
*-       Arg#2:  possibly abbreviated string.                                   
*-                                                                              
         LA    R5,ARG2(R1)                                                      
         LA    R14,VNSTRLOC        R14,R15 - token string                       
         LH    R15,VNSTRLEN                                                     
*-                                                                              
*-       Arg#3: minimum length.                                                 
*-                                                                              
         IF    (R0,GE,3),BEGIN     Minimum length...                            
         LA    R5,ARG3(R1)                                                      
         L     R4,VNVALUE                                                       
         IF    ((R4,NEG),OR,(R4,GT,MAXSTRLN)),BEGIN                             
         BADARG 3                                                               
         EXIT  FNABBREV                                                         
         END                                                                    
         IF    (R15,LT,R4),BEGIN   Token is too small...                        
         VRETURN BOOLEAN,FALSE     No match                                     
         CLEAR R15                 Set RC                                       
         EXIT  FNABBREV            Scram                                        
         END                                                                    
         END                                                                    
*-                                                                              
*-       Compare to see if valid abbreviation.                                  
*-                                                                              
         LR    R3,R15                                                           
         CLCL  R14,R2              Valid abbreviation?                          
         IF    EQ,'VRETURN BOOLEAN,TRUE'                                        
         ELSE  'VRETURN BOOLEAN,FALSE'                                          
*                                                                               
         CLEAR R15                 A-Ok                                         
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ABS Function.                                                                
*                                                                               
*    ABS(number)                                                                
*                                                                               
FNABS    PROC  ,                                                                
         SETARGS '11N'             1 arg: number                                
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG1(R1)                                                      
*-                                                                              
*-       Return absolute value.                                                 
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNNUMB                                                           
         MVC   VNVALUE,VNVALUE-VNENTRY(R5)  Copy number                         
         NI    VNVALUE+L'VNVALUE-1,X'F0'                                        
         OI    VNVALUE+L'VNVALUE-1,X'0C'  Set pos                               
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
* ACCOUNT_INFO -- RETURN KEYWORD FILE INFO                                      
*                                                                               
* ACCOUNT_INFO('gg.uuu','FIELD_NAME')                                           
*                                                                               
*                                                                               
* SEE COMMENTS AT START OF MODULE.                                              
*                                                                               
ANFWA    RECORD BEGIN                                                           
ANFKEYLC DS    F                                                                
ANFKEYLN DS    F                                                                
ANFFLDLC DS    F                                                                
ANFFLDLN DS    F                                                                
ANFKWR   DS    XL(L'KWR)                                                        
         END                                                                    
*                                                                               
*                                                                               
FNACCTNF PROC  ANFWA                                                            
         LA    R4,ANFKWR                                                        
         USING KWR,R4                                                           
*                                                                               
         SETARGS '22SS'                                                         
*                                                                               
         COMMENT                   GET ARGS                                     
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC                                                      
         ST    R2,ANFKEYLC                                                      
         LH    R3,VNSTRLEN                                                      
         ST    R3,ANFKEYLN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R2,VNSTRLOC                                                      
         ST    R2,ANFFLDLC                                                      
         LH    R3,VNSTRLEN                                                      
         ST    R3,ANFFLDLN                                                      
         DROP  R5                                                               
*                                                                               
         COMMENT                   CONVERT ARGS TO UPPER CASE                   
         L     R2,CVUPPTBL                                                      
         L     R1,ANFKEYLC                                                      
         L     R3,ANFKEYLN                                                      
         DEX   R3,' TR @R1(0),@R2 '                                             
         L     R1,ANFFLDLC                                                      
         L     R3,ANFFLDLN                                                      
         DEX   R3,' TR @R1(0),@R2 '                                             
*                                                                               
         COMMENT                   GET AND VERIFY KEY,,,                        
         L     R1,ANFKEYLC                                                      
         L     R0,ANFKEYLN                                                      
         IF    ((R0,NE,6),OR,(@R1+2,NE,'.')),BEGIN                              
         TSEG  'Invalid account specified. ',,CR                                
         BADARG 1                                                               
         CLEAR R0                                                               
         EXIT  FNACCTNF                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR KWR                                                              
         MVC   KWRGRP,@R1                                                       
         MVC   KWRUSER,@R1+3                                                    
         KREAD KWR                                                              
         IF    ^KWRIFL.KWRIFVAL,BEGIN  IF NO KWD RECORD,,,                      
         TSEG  'Account does not exist. ',,CR                                   
         MVC   CPERRID,=CL8'NOINFO  '                                           
         LA    R15,4                                                            
         CLEAR R0                                                               
         END                                                                    
         ELSE  BEGIN               GOT VALID KWD RECORD                         
         L     R1,ANFFLDLC                                                      
         L     R0,ANFFLDLN                                                      
*                                                                               
         IF    ((R0,EQ,9),AND,(@R1,EQ,'USER_NAME')),BEGIN                       
         SETMSG KWRNAME             GET NAME, TRIM TRAILING BLANKS,,            
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         DECR  R3                                                               
         WHILE ((R3,GE,R1),AND,((@R3,EQ,' '),OR,(@R3,EQ,X'00'))),BEGIN          
         DECR  R3                                                               
         END                                                                    
         INCR  R3                                                               
         LR    R0,R3                                                            
         SR    R0,R1                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSEIF ((R0,EQ,12),AND,(@R1,EQ,'MAIL_MESSAGE')),BEGIN                  
         SETMSG KWRMTXT             Mail message text                           
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         DECR  R3                                                               
         WHILE ((R3,GE,R1),AND,((@R3,EQ,' '),OR,(@R3,EQ,X'00'))),BEGIN          
         DECR  R3                                                               
         END                                                                    
         INCR  R3                                                               
         LR    R0,R3                                                            
         SR    R0,R1                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  'Invalid request. ',,CR                                          
         BADARG 2                                                               
         CLEAR R0                                                               
         EXIT FNACCTNF                                                          
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   GOT INFO, RETURN IT                          
         XPUSH R15                                                              
         VRETURN STRING,(R1),(R0)                                               
         XPOP  R15                                                              
         CLEAR KWR                 DON'T CLR UNTIL INFO IS RETURNED             
         PEND                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  ASCII                                                                        
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNASCII  PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVASCTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BITAND                                                                       
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNBITAND PROC  ,                                                                
         SETARGS '22II'            2 ARGS: INTEGER, INTEGER                     
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST NUMBER                              
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - 2ND NUMBER                              
         DROP  R5                                                               
         NR    R2,R3               AND BITS                                     
         VRETURN INTEGER,(R2)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BITOR                                                                        
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNBITOR  PROC  ,                                                                
         SETARGS '22II'            2 ARGS: INTEGER, INTEGER                     
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST NUMBER                              
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - 2ND NUMBER                              
         DROP  R5                                                               
         OR    R2,R3               OR BITS                                      
         VRETURN INTEGER,(R2)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BITTEST                                                                      
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNBITTST PROC  ,                                                                
         SETARGS '22II'                                                         
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST NUMBER                              
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - 2ND NUMBER (MASK)                       
         DROP  R5                                                               
         NR    R2,R3               TEST BITS                                    
         CR    R2,R3               ARE ALL BITS IN MASK ON ??                   
         IF    E,BEGIN                                                          
         VRETURN BOOLEAN,TRUE                                                   
         END                                                                    
         ELSE  BEGIN                                                            
         VRETURN BOOLEAN,FALSE                                                  
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  BITXOR                                                                       
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNBITXOR PROC  ,                                                                
         SETARGS '22II'            2 ARGS: INTEGER, INTEGER                     
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST NUMBER                              
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - 2ND NUMBER                              
         DROP  R5                                                               
         XR    R2,R3               XOR BITS                                     
         VRETURN INTEGER,(R2)                                                   
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
CENWA  RECORD BEGIN                                                             
CENPAD   DS    C                   Pad character                                
CENLEADL DS    H                   Number of leading pad chars                  
CENREML  DS    H                   Length of text after leader                  
CENTLEN  DS    H                   Total string length                          
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  CENTER -- CENTER Function.                                                   
*                                                                               
*    CENTER(string,length[,pad-char])                                           
*                                                                               
FNCENTER PROC  CENWA                                                            
         CLEAR CENWA                                                            
         MVI   CENPAD,C' '         Default pad character                        
*                                                                               
         SETARGS '23SIS'           2-3 args: string,integer,string              
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - string loc, len                      
         LH    R3,VNSTRLEN                                                      
*-                                                                              
*-       Arg#2:  Length.                                                        
*-                                                                              
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          R4 - Length                                  
         IF    ((R4,NEG),OR,(R4,GT,MAXSTRLN)),BEGIN                             
         BADARG 2                                                               
         EXIT  FNCENTER                                                         
         END                                                                    
*                                                                               
         IF    (R3,GT,R4),BEGIN    Take right most chars...                     
         SR    R3,R4                                                            
         SRL   R3,1                Divided by two for centering                 
         LA    R2,@R2(R3)          R2,R3 - right most string loc,len            
         LR    R3,R4                                                            
         END                                                                    
*                                                                               
         LR    R14,R4                                                           
         SR    R14,R3              Number of pad chars                          
         SRL   R14,1                                                            
         STH   R14,CENLEADL        Number of leading pads                       
         SR    R4,R14                                                           
         STH   R4,CENREML          Length of remaining text                     
*-                                                                              
*-       Arg#3:  Pad char string.                                               
*-                                                                              
         IF    (R0,GE,3),BEGIN     Process arg#3...                             
         LA    R5,ARG3(R1)                                                      
         IF    (VNSTRLEN,GT,1),BEGIN                                            
         TSEG  'Expecting one character pad string.  '                          
         BADARG 3                                                               
         EXIT  FNCENTER                                                         
         END                                                                    
*                                                                               
         IF    (VNSTRLEN,EQ,1),BEGIN                                            
         MVC   CENPAD,VNSTRLOC     Save pad character                           
         END                                                                    
         END                                                                    
*-                                                                              
*-       Move in pad and then centered text.                                    
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
*-                                                                              
*-       First add leading pad characters (if any).                             
*-                                                                              
         LA    R14,VNSTRLOC                                                     
         LH    R15,CENLEADL        Length to pad                                
         CLEAR R1                                                               
         ICM   R1,8,CENPAD         Pad character                                
         MVCL  R14,R0              First add any pad characters                 
*-                                                                              
*-       Now add centered text.                                                 
*-                                                                              
         LH    R15,CENREML         Remaining string length                      
         ICM   R3,8,CENPAD         Pad character                                
         MVCL  R14,R2              Move string and trailing pad                 
*                                                                               
         LH    R15,CENLEADL                                                     
         AH    R15,CENREML                                                      
         STH   R15,VNSTRLEN        Save string length                           
*                                                                               
         CLEAR R15                 A-ok                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  CHK_ACCOUNT function.                                                        
*                                                                               
*  NOTE:                                                                        
*  same as get account except checks that acct exists.                          
*                                                                               
FNCAWA   RECORD BEGIN                                                           
FNCAACCT DS    CL5                 Working account (uuugg form)                 
FNCADATA DS    CL256               Work area                                    
         END                                                                    
*                                                                               
*                                                                               
FNCHKACC PROC  FNCAWA                                                           
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account/id                                
         VCALL LRTRIM                                                           
         LA    R15,FNCAACCT        Put account in "uuugg" form here             
         VCALL CHKACCT             Get account                                  
         IF    NZ,'CLEAR R1,R0'    ID not found/ACCT does not exist             
         ELSE  BEGIN                                                            
         MVC   FNCADATA(2),FNCAACCT+3  gg                                       
         MVI   FNCADATA+2,C'.'                                                  
         MVC   FNCADATA+3(3),FNCAACCT  uuu                                      
         SETMSG FNCADATA,6          "gg.uuu"                                    
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  CHSTR - CHANGE STRING                                                        
*                                                                               
*  CHSTR(STRING, MATCH_STRING, SUBSTITUTION_STRING)                             
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
*                                                                               
FNCHWA   RECORD BEGIN                                                           
CHSTRLOC DS    F                   STRING                                       
CHSTRLEN DS    F                                                                
CHMATLOC DS    F                   MATCH STRING                                 
CHMATLEN DS    F                                                                
CHSUBLOC DS    F                   SUBSTITUTION STRING                          
CHSUBLEN DS    F                                                                
CHOUTLOC DS    F                                                                
CHOUTLEN DS    F                                                                
CHOUTSTR DS    CL(3*MAXSTRLN)      OUTPUT STRING (W/OVERFLOW)                   
CHMAXOUT DS    F                   MAX VALID OUTPUT INDEX VALUE                 
         END                                                                    
*                                                                               
FNCHSTR  PROC  FNCHWA                                                           
         SETARGS '33SSS'           3 ARGS: STRING, STRING, STRING               
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
*                                                                               
         COMMENT                   SET UP WORK AREA, GET ARGS                   
         CLEAR FNCHWA                                                           
         LA    R5,ARG1(R1)         ARG 1                                        
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         ST    R2,CHSTRLOC                                                      
         ST    R3,CHSTRLEN                                                      
         LA    R5,ARG2(R1)         ARG 2                                        
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         ST    R2,CHMATLOC                                                      
         ST    R3,CHMATLEN                                                      
         LA    R5,ARG3(R1)         ARG 3                                        
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         ST    R2,CHSUBLOC                                                      
         ST    R3,CHSUBLEN                                                      
         DROP  R5                                                               
         LA    R2,CHOUTSTR                                                      
         LA    R2,MAXSTRLN(R2)                                                  
         ST    R2,CHMAXOUT                                                      
*                                                                               
         COMMENT                   SET UP LOOP REGISTERS                        
         L     R2,CHSTRLOC         R2 - STRING                                  
         L     R3,CHMATLOC         R3 - MATCH STRING                            
         L     R4,CHSUBLOC         R4 - SUBSTITUTE STRING                       
         LA    R5,CHOUTSTR         R5 - NEW STRING                              
         L     R14,CHSTRLOC        R14 - TEST LIMIT                             
         A     R14,CHSTRLEN                                                     
         S     R14,CHMATLEN                                                     
         INCR  R14                                                              
         L     R15,CHMATLEN        R15 - MATCH COMPARE LEN                      
         DECR  R15                                                              
         COMMENT                   IF NULL STRINGS                              
         IF    ((CHSTRLEN,EQ,0),OR,(CHMATLEN,EQ,0)),BEGIN                       
         L     R1,CHSTRLOC                                                      
         L     R0,CHSTRLEN                                                      
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         ELSE  BEGIN               ,, ELSE NON NULL STRINGS                     
*                                                                               
         COMMENT                   MAIN LOOP                                    
         WHILE (R2,LT,R14),BEGIN                                                
         EX    R15,' CLC @R2(0),@R3 '                                           
         IF    E,BEGIN             IF MATCH, DO SUBSTITUTION                    
         L     R1,CHSUBLEN                                                      
         MOVE  R1,@R5,@R4                                                       
         A     R2,CHMATLEN                                                      
         A     R5,CHSUBLEN                                                      
         IF    (R5,GT,CHMAXOUT),' L R5,CHMAXOUT '                               
         END                                                                    
         ELSE  BEGIN               IF NO MATCH, JUST OUTPUT                     
         MVC   @R5(1),@R2                                                       
         INCR  R2                                                               
         INCR  R5                                                               
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   DO LAST BIT OF STRING, IF ANY                
         L     R14,CHSTRLOC                                                     
         A     R14,CHSTRLEN                                                     
         WHILE (R2,LT,R14),BEGIN                                                
         MVC   @R5(1),@R2                                                       
         INCR  R2                                                               
         INCR  R5                                                               
         END                                                                    
*                                                                               
         COMMENT RETURN STRING                                                  
         IF    (R5,GT,CHMAXOUT),' L R5,CHMAXOUT '                               
         LA    R1,CHOUTSTR                                                      
         LR    R0,R5                                                            
         SR    R0,R1                                                            
         VRETURN STRING,(R1),(R0)                                               
         END   , OF NON NULL STRINGS                                            
         END   , OF GOOD ARGS                                                   
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COMPARE Function.                                                            
*                                                                               
*    COMPARE(??????????)                                                        
*                                                                               
FNCOMP   PROC  ,                                                                
         TSEG  'Not Yet!  ',,CR                                                 
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  COPIES Function.                                                             
*                                                                               
*    COPIES(??????????)                                                         
*                                                                               
FNCOPIES PROC  ,                                                                
         SETARGS '22SI'            1-2 args: string, integer                    
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - string loc, len                      
         LH    R3,VNSTRLEN                                                      
*-                                                                              
*-       Arg#2: Repition count.                                                 
*-                                                                              
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          Repition count                               
*                                                                               
         LR    R15,R3                                                           
         MR    R14,R4                                                           
         LR    R4,R15              R4 = total string length                     
         IF    ((R4,NEG),OR,(R4,GT,MAXSTRLN)),BEGIN                             
         BADARG 2                                                               
         EXIT  FNCOPIES                                                         
         END                                                                    
*-                                                                              
*-       Duplicate string.                                                      
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
         STH   R4,VNSTRLEN         Set string length                            
*                                                                               
         LA    R14,VNSTRLOC        Start of string                              
         WHILE (R4,POS),BEGIN      Keep duplicating...                          
         XPUSH R2,R3               Save string ptrs                             
         LR    R15,R3                                                           
         MVCL  R14,R2              Copy string                                  
         XPOP  R2,R3               Restore orig string ptrs                     
*                                                                               
         SR    R4,R3               Decrement remaining length                   
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  C2D Function.                                                                
*                                                                               
*    C2D(??????????)                                                            
*                                                                               
FNC2D    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  C2X Function.                                                                
*                                                                               
*    C2X(??????????)                                                            
*                                                                               
FNC2X    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DATATYPE Function.                                                           
*                                                                               
*    DATATYPE(??????????)                                                       
*                                                                               
FNDTYPE PROC  ,                                                                 
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
DECRYWA  RECORD BEGIN                                                           
DECARGP  DS    F                   ARG ptr                                      
DECNARG  DS    F                   Number of arguments                          
*                                                                               
DECTEXT  DS    CL(MAXSTRLN)        Working text area                            
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  DECRYPT -- DECRYPT Function.                                                 
*                                                                               
*    DECRYPT(text-string[, encryption-key-string])                              
*                                                                               
FNDECRYP PROC  DECRYWA                                                          
         CLEAR DECRYWA                                                          
*                                                                               
         SETARGS '12SS'            1-2 args: string, string                     
         IF    Z,BEGIN             OK...                                        
         ST    R0,DECNARG          Number of arguments                          
         ST    R1,DECARGP          Start of args ptr                            
*                                                                               
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1: text to be decrypted.                                           
*-                                                                              
         LA    R5,ARG1                                                          
         AL    R5,DECARGP                                                       
*                                                                               
         LH    R4,VNSTRLEN                                                      
         MOVEL DECTEXT,VNSTRLOC,(R4)  Copy text string                          
*-                                                                              
*-       Arg 2: encryption key.                                                 
*-                                                                              
         CLEAR R2,R3               Assume no key                                
*                                                                               
         IF    (DECNARG,GE,2),BEGIN  Encryption key specified...                
         LA    R5,ARG2                                                          
         AL    R5,DECARGP                                                       
*                                                                               
         LS    R2,R3,VNSTRLOC,LH:VNSTRLEN  Encryption key                       
         END                                                                    
*-                                                                              
*-       Do the encryption.                                                     
*-                                                                              
         SETMSG DECTEXT,(R4)        Text to encrypt                             
         VCALL DECRYPT             Decrypt the text                             
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DELSTR Function.                                                             
*                                                                               
*    DELSTR(??????????)                                                         
*                                                                               
FNDELSTR PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DELWORD Function.                                                            
*                                                                               
*    DELWORD(??????????)                                                        
*                                                                               
FNDELWRD PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RF_DEST_CHECK -- CHECK IF DEST EXISTS                                        
*                                                                               
*    RF_DEST_CHECK('DEST')                                                      
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF MODULE.                                             
*                                                                               
DCHECKWA RECORD BEGIN                                                           
DESTNAME DS    CL8                                                              
         END                                                                    
*-                                                                              
FNDESTV  PROC  DCHECKWA                                                         
         MVC   DESTNAME,CVBLANKS                                                
*                                                                               
         SETARGS '11SS'                                                         
*                                                                               
         COMMENT                   GET ARG                                      
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - STRING LOC,LEN                       
         LH    R3,VNSTRLEN                                                      
         DROP  R5                                                               
*                                                                               
         COMMENT                   PRESUME DEST IS INVALID                      
         LA    R15,4                                                            
*                                                                               
         COMMENT                   CHECK ARG                                    
         IF    ((R3,GE,1),AND,(R3,LE,8)),BEGIN                                  
*                                                                               
         COMMENT                   MOVE NAME (IN UPPER CASE)                    
         COMMENT                   TO 8 CHAR HOLDING AREA                       
         MVC   DESTNAME,CVBLANKS                                                
         L     R1,CVUPPTBL                                                      
         DEX   R3,' MVC DESTNAME(0),@R2 '                                       
         EX    R3,' TR DESTNAME(0),@R1 '                                        
*                                                                               
         COMMENT                   CHECK DEST                                   
         LA    R0,L'DESTNAME                                                    
         LA    R1,DESTNAME                                                      
         VCALL DESTCHK                                                          
         END                                                                    
         IF    (R15,Z),BEGIN                                                    
         VRETURN BOOLEAN,TRUE                                                   
         END                                                                    
         ELSE  BEGIN                                                            
         VRETURN BOOLEAN,FALSE                                                  
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   RETURN                                       
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RF_PW_CHECK -- Routine to verify a user password.                            
*                                                                               
*      RF_PW_CHECK('password', 'account')                                       
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF MODULE.                                             
*                                                                               
DPASSWA  RECORD BEGIN                                                           
DPASKWR  DS    XL(L'KWR)           Working KWR area                             
*                                                                               
DPASNARG DS    F                   Number of arguments                          
DPASARGP DS    F                   First ARG ptr                                
DPASKEY  DS    CL8                 Password                                     
         END                                                                    
*-                                                                              
FNPASSV  PROC  DPASSWA                                                          
*                                                                               
         LA    R4,DPASKWR                                                       
         WITH  (KWR,R4)                                                         
         CLEAR KWR                                                              
         MVC   KWRUSER,CPSUSER     Set logged...                                
         MVC   KWRGRP,CPSGRP       ...on account                                
*                                                                               
         ST    R0,DPASNARG         Number of arguments                          
         ST    R1,DPASARGP         Start of args ptr                            
*                                                                               
         SETARGS '12SS'            1-2 args: str, str                           
         BNZ   FNPSEXIT                                                         
*                                                                               
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1:  Password to check.                                             
*-                                                                              
         LA    R5,ARG1                                                          
         AL    R5,DPASARGP                                                      
*                                                                               
         MVC   DPASKEY,CVBLANKS    Pre-blank                                    
         LH    R2,VNSTRLEN                                                      
         CEIL  R2,L'DPASKEY        Not too much                                 
         DEX   R2,'MVC DPASKEY(0),VNSTRLOC'  Copy string                        
         L     R1,CVUPPTBL                                                      
         EX    R2,'TR DPASKEY(0),@R1'   Convert to caps                         
*-                                                                              
*-       Arg 2:  Account (use logged on account if not specified).              
*-                                                                              
         IF    (DPASNARG,GE,2),BEGIN  Account specified...                      
         LA    R5,ARG2                                                          
         AL    R5,DPASARGP                                                      
*                                                                               
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account                                   
         VCALL LRTRIM              Strip blanks                                 
         IF    ((R0,NE,6),OR,(@R1+2,NE,'.')),BEGIN  Must be "gg.uuu"            
         VRETURN BOOLEAN,FALSE     Bad account format                           
         CLEAR KWR,DPASKEY         (Tidy)                                       
         B     FNPSEXIT            Scram                                        
         END                                                                    
*                                                                               
         MVC   KWRGRP,@R1          Save gg                                      
         MVC   KWRUSER,@R1+3       Save uuu                                     
         L     R1,CVUPPTBL                                                      
         TR    KWRACCT,@R1         Convert KWRUSER/KWRGRP to caps               
         END                                                                    
*-                                                                              
*-       Read the keyword record and check for correct password.                
*-                                                                              
         KREAD KWR                                                              
*                                                                               
         LA    R15,4               Assume no match                              
         IF    KWRIFL.KWRIFVAL,BEGIN  Valid KWR record...                       
         IF    (KWRPASS,EQ,DPASKEY),'CLEAR R15'  It matches                     
         ELSE  'VCALL WATCHKW; LA R15,4'  No match                              
         END                                                                    
*                                                                               
         CLEAR KWR,DPASKEY         (Tidy)                                       
*                                                                               
         IF    (R15,Z),'VRETURN BOOLEAN,TRUE'                                   
         ELSE  'VRETURN BOOLEAN,FALSE'                                          
*                                                                               
         CLEAR R15                 A-ok                                         
FNPSEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RF_PW_PROMPT -- Routine to prompt for a user password.                       
*                                                                               
*      RF_PW_PROMPT('prompt string', 'account')                                 
*                                                                               
*  Returns:                                                                     
*      "OK"                                                                     
*      "WRONG"                                                                  
*      "ATTN"                                                                   
*                                                                               
DPPWA    RECORD BEGIN                                                           
DPPKWR   DS    XL(L'KWR)           Working KWR area                             
*                                                                               
DPPNARG  DS    F                   Number of arguments                          
DPPARGP  DS    F                   First ARG ptr                                
         END                                                                    
*-                                                                              
FNPASSP  PROC  DPPWA                                                            
*                                                                               
         LA    R4,DPPKWR                                                        
         WITH  (KWR,R4)                                                         
         CLEAR KWR                                                              
         MVC   KWRUSER,CPSUSER     Set logged...                                
         MVC   KWRGRP,CPSGRP       ...on account                                
*                                                                               
         ST    R0,DPPNARG          Number of arguments                          
         ST    R1,DPPARGP          Start of args ptr                            
*                                                                               
         SETARGS '12SS'            1-2 args: str, str                           
         BNZ   FNPPEXIT                                                         
*                                                                               
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1:  Password prompt text.                                          
*-                                                                              
         LA    R5,ARG1                                                          
         AL    R5,DPPARGP                                                       
*                                                                               
         IF    (VNSTRLEN,NZ),BEGIN                                              
         TSEG  'Non-standard password prompt not implemented.'                  
         BADARG 1                                                               
         B     FNPPEXIT                                                         
         END                                                                    
*-                                                                              
*-       Arg 2:  Account (use logged on account if not specified).              
*-                                                                              
         IF    (DPPNARG,GE,2),BEGIN  Account specified...                       
         LA    R5,ARG2                                                          
         AL    R5,DPPARGP                                                       
*                                                                               
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account                                   
         VCALL LRTRIM              Strip blanks                                 
         IF    ((R0,NE,6),OR,(@R1+2,NE,'.')),BEGIN  Must be "gg.uuu"            
         VRETURN STRING,'BAD ACCOUNT FORMAT'                                    
         B     FNPPEXIT            Scram                                        
         END                                                                    
*                                                                               
         MVC   KWRGRP,@R1          Save gg                                      
         MVC   KWRUSER,@R1+3       Save uuu                                     
         L     R1,CVUPPTBL                                                      
         TR    KWRACCT,@R1         Convert KWRUSER/KWRGRP to caps               
         END                                                                    
*-                                                                              
*-       Read the keyword record and check for correct password.                
*-                                                                              
         LH    R0,=H'-1'           GETKWR option                                
         LA    R1,KWR                                                           
         VCALL GETKWR              Get keyword record                           
         CLEAR R15                 Only returns if OK (shortcoming)             
*                                                                               
         CLEAR KWR                 Tidy                                         
*                                                                               
         VRETURN STRING,'OK'                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
FNPPEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DISPLAY - DISPLAY VARIABLE VALUE IN HEX                                      
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNDSWA   RECORD BEGIN                                                           
FNDSTEMP DS    CL(&LINESZ)         TEMP WORK AREA                               
         END                                                                    
*                                                                               
*                                                                               
FNDISPLY PROC  FNDSWA                                                           
         USING VNENTRY,R5                                                       
         SETARGS '11X'             DO NOT CHANGE ARG TYPE                       
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         IF    VNSTRING,BEGIN      IF STRING,                                   
         LH    R0,VNSTRLEN                                                      
         IF    (R0,GT,116),BEGIN                                                
         TSEG  'String overflow. '                                              
         BADARG 1                                                               
         EXIT  FNDISPLY                                                         
         END                                                                    
         LA    R1,VNSTRLOC                                                      
         LA    R2,FNDSTEMP                                                      
         VCALL MAKECHAR               CONVERT TO HEX CHARS IN TEMP              
         VRETURN STRING,(R2),(R0)     RETURN STRING                             
         END                                                                    
*$                                                                              
*$ note: display number !@!                                                     
*$ hex display of integer part only !! oh yeah !! delete line no                
*$ part,, do it to it bud,, ax it jack !                                        
*$                                                                              
         IF    VNNUMB,BEGIN        IF NUMBER                                    
         CLC   VNVALUE+7(4),=X'00000000'                                        
         IF    NE,BEGIN            IF LINENO (IE. FRACTIONAL PART)              
         LR    R2,R5                                                            
         VCALL MAKELNO                                                          
         BNZ   FNDSDONE                                                         
         END                                                                    
         ELSE  BEGIN ,,            IF INTEGER,, DECIMAL                         
         LR    R2,R5                                                            
         VCALL MAKEINT                                                          
         BNZ   FNDSDONE               *** NOTE DECIMAL ONLY DISPLAYS            
         END   ,                      WHOLE PART IN HEX,, JUST LIKE             
         LA    R0,4                   AN INTEGER                                
         LA    R1,VNVALUE                                                       
         LA    R2,FNDSTEMP                                                      
         VCALL MAKECHAR               CONVERT TO HEX CHARS IN TEMP              
         VRETURN STRING,(R2),(R0)     RETURN STRING                             
         END                                                                    
         IF    VNBOOL,BEGIN        IF BOOLEAN, NO DISPLAY                       
         TSEG  'Boolean values may not be displayed in hex. '                   
         LA    R15,4                                                            
         END                                                                    
         END                                                                    
*                                                                               
FNDSDONE LABEL ,                                                                
         PEND                                                                   
         DROP  R5                                                               
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DSNDEQ(string) -- Dequeue on the data set.                                   
*                                                                               
*  See comments at start of this module                                         
*                                                                               
FNDSNDEQ PROC                                                                   
         SCPUSH ,                                                               
         SETARGS '11S'             Get string argument                          
         IF    Z,BEGIN             If ok, process                               
         IF    (CPSFSPR+CPSFOPR,Z),FNDSNDQZ                                     
         SET   CPFDSNNE            Suppress error exits in dsn proc             
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1         Address argument                             
         SCINIT VNSTRLOC,LH:VNSTRLEN                                            
         END                                                                    
         VCALL DODSNM              Process dsname                               
         IF    CPFDSNBD,FNDSNDQE                                                
         SCAN  FNDEQPRT            Find any overrides                           
         IF    (R15,NZ),FNDSNDQE                                                
         IF    CPFDSNBD,FNDSNDQE                                                
         VCALL DSNFIN              Finish dsname processing                     
         IF    CPFDSNBD,FNDSNDQE                                                
         VCALL DDEQ                Dequeue dsname                               
         SETMSG 'OK'                Assume "OK"                                 
         IF    (R15,NZ),'SETMSG "NOT ENQUEUED"'                                 
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         B     FNDSNDQX                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
FNDSNDQZ LABEL ,                                                                
         VRETURN STRING,'INVALID FUNCTION'                                      
         B     FNDSNDQX                                                         
*                                                                               
FNDSNDQE LABEL ,                                                                
         VRETURN STRING,'BAD DSN'                                               
         B     FNDSNDQX                                                         
*                                                                               
FNDSNDQX SCPOP ,                   Restore scanner                              
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNDEQPRT SCKW  ,V(DSNPSH),PUSH                                                  
         SCKW  ,FNDSNDQB                                                        
*                                                                               
FNDSNDQB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  DSNENQ(string) -- Enqueue on the data set.                                   
*                                                                               
*  See comments at start of this module                                         
*                                                                               
FNDSNENQ PROC                                                                   
         SCPUSH ,                                                               
         SETARGS '11S'             Get string argument                          
         IF    Z,BEGIN             If ok, process                               
         IF    (CPSFSPR+CPSFOPR,Z),FNDSNEQZ                                     
         SET   CPFDSNNE            Suppress error exits in dsn proc             
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1         Address argument                             
         SCINIT VNSTRLOC,LH:VNSTRLEN                                            
         END                                                                    
         VCALL DODSNM              Process dsname                               
         IF    CPFDSNBD,FNDSNEQE                                                
         SCAN  FNENQPRT            Find any overrides                           
         IF    (R15,NZ),FNDSNEQE                                                
         IF    CPFDSNBD,FNDSNEQE                                                
         VCALL DSNFIN              Finish dsname processing                     
         IF    CPFDSNBD,FNDSNEQE                                                
*                                                                               
         CLEAR R15                 Flag: permanent ENQ                          
         VCALL DENQ                Enqueue on data set                          
         IF    (R15,Z),'SETMSG "OK"'                                            
         ELSEIF (R15,EQ,4),'SETMSG "NOT YOURS"'                                 
         ELSEIF (R15,EQ,8),'SETMSG "IN USE"'                                    
         ELSE  'SETMSG "ERROR"'                                                 
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         B     FNDSNEQX                                                         
*                                                                               
* EXITS                                                                         
*                                                                               
FNDSNEQZ LABEL ,                                                                
         VRETURN STRING,'INVALID FUNCTION'                                      
         B     FNDSNEQX                                                         
*                                                                               
FNDSNEQE LABEL ,                                                                
         VRETURN STRING,'BAD DSN'                                               
         B     FNDSNEQX                                                         
*                                                                               
FNDSNEQX LABEL ,                   Restore scanner                              
         SCPOP ,                                                                
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNENQPRT SCKW  ,V(DSNPSH),PUSH                                                  
         SCKW  ,FNDSNEQB                                                        
*                                                                               
FNDSNEQB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  Expand String to Full Dsname                                                 
*                                                                               
*  See comments at start of this module                                         
*                                                                               
FNDSNFMT PROC                                                                   
         SCPUSH ,                                                               
         SETARGS '11S'             Get string argument                          
         IF    Z,BEGIN             If ok, process                               
         SET   CPFDSNNE            Suppress error exits in dsn proc             
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1         Address argument                             
         SCINIT VNSTRLOC,LH:VNSTRLEN                                            
         END                                                                    
         VCALL FNAMESCN            Get the filename                             
         IF    (R15,NZ),BEGIN                                                   
         SCSEGCLR                                                               
         B     FNDSNFME                                                         
         END                                                                    
         SCAN  FNDSNPRT            Find any overrides                           
         IF    (R15,NZ),FNDSNFME                                                
         COMMENT                   FINISH FILE NAME PROCESSING                  
         VCALL FNAMEFIN                                                         
         L     R4,CPFINFOP                                                      
         USING FFINFO,R4                                                        
         IF    (^FFFWINGS),FNDSNFME                                             
         VCALL NRESOLVE                                                         
         IF    (R15,NZ),BEGIN                                                   
         SCSEGCLR                                                               
         TCLEAR                                                                 
         B     FNDSNFME                                                         
         END                                                                    
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returned value is a string                   
         L     R2,FFRNAMEL                                                      
         STH   R2,VNSTRLEN                                                      
         LR    R15,R2                                                           
         MOVE  R15,VNSTRLOC,FFRNAME                                             
         END                                                                    
         CLEAR R15                                                              
         END                                                                    
         B     FNDSNFMX                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
FNDSNFME VRETURN STRING,'BAD DSN'                                               
         B     FNDSNFMX                                                         
*                                                                               
FNDSNFMX SCPOP ,                   Restore scanner                              
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNDSNPRT SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,FNDSNFMB                                                        
*                                                                               
FNDSNFMB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  DSNPROT(dsname) -- Routine to return the type of data set                    
*    protection being used for the data set specified.  The                     
*    function will return one of the following:                                 
*                                                                               
*         NONE - not protected by any security system                           
*         STANFORD - protected by the Stanford security system                  
*         RACF - protected by the RACF security system                          
*                                                                               
FNDPROT  PROC  ,                                                                
         SCPUSH ,                                                               
         SETARGS '11S'             Args: 1 string                               
         IF    Z,BEGIN                                                          
*                                                                               
         SET   CPFDSNNE            Suppress error exits in dsn proc             
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1                                                      
         SCINIT VNSTRLOC,LH:VNSTRLEN  First argument to function                
         END                                                                    
*-                                                                              
*-       Scan 1st argument as dsname.                                           
*-                                                                              
         VCALL DODSNM              Scan dsname                                  
         IF    CPFDSNBD,FNDSNPRE                                                
         SCAN  FNDSPPRT            Scan overrides                               
         IF    (R15,NZ),FNDSNPRE                                                
         IF    CPFDSNBD,FNDSNPRE                                                
         VCALL DSNFIN              Finish dsname processing                     
         IF    CPFDSNBD,FNDSNPRE                                                
*                                                                               
         VCALL SECSYS              Check for security system                    
         IF    Z,'SETMSG "RACF"'                Protected by RACF               
         ELSEIF (R15,EQ,4),'SETMSG "Stanford"' Protected by Stanford            
         ELSE  'SETMSG "Unknown"'            Unknown protection                 
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         B     FNDSNPRX                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
FNDSNPRE LABEL ,                                                                
         VRETURN STRING,'BAD DSN'                                               
         B     FNDSNPRX                                                         
*                                                                               
FNDSNPRX SCPOP ,                                                                
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNDSPPRT SCKW  ,V(DSNPSH),PUSH                                                  
         SCKW  ,FNDSNPRB                                                        
*                                                                               
FNDSNPRB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  Get DSCB info for a Dataset on the System                                    
*                                                                               
*  See comments at start of this module                                         
*                                                                               
SDSNBUF  RECORD 'SDSNBUF'                                                       
         SPACE 2                                                                
FNDSNINF PROC  FNWA                                                             
         SCPUSH ,                                                               
         SETARGS '11S'             Get string argument                          
         IF    Z,BEGIN             If ok, process                               
         SET   CPFDSNNE            Suppress error exits in dsn proc             
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1         Address argument                             
         SCINIT VNSTRLOC,LH:VNSTRLEN                                            
         END                                                                    
         VCALL FNAMESCN            Get the filename                             
         IF    (R15,NZ),BEGIN                                                   
         SCSEGCLR                                                               
         B     FNDSNINE                                                         
         END                                                                    
         SCAN  FNDSIPRT            Find any overrides                           
         IF    (R15,NZ),FNDSNINE                                                
         COMMENT                   FINISH FILE NAME PROCESSING                  
         VCALL FNAMEFIN                                                         
         L     R4,CPFINFOP                                                      
         USING FFINFO,R4                                                        
         IF    (^FFFWINGS),FNDSNINE                                             
*                                                                               
         XPUSH ,,L'SHDSARGS,PTR=R3                                              
         WITH  (SHDSARGS,R3),BEGIN                                              
         XPUSH R3,R5                                                            
         LR    R2,R3                                                            
         LA    R3,L'SHDSARGS                                                    
         SR    R5,R5                                                            
         MVCL  R2,R4                                                            
         XPOP  R3,R5                                                            
         VCALL NRESOLVE                                                         
         CLEAR CPFDSNNE                                                         
         IF    (R15,NZ),FNDSNINE                                                
         L     R0,FFRNAMEL                                                      
         LA    R1,FFRNAME                                                       
         VCALL RETFNAME                                                         
         ST    R0,SHDSLIKL                                                      
         LR    R1,R0                                                            
         MOVE  R1,SHDSLIKE,FFRNAME                                              
         SET   SHDSONLY                                                         
         LA    R1,SHDSARGS                                                      
         VCALL WGSHODSN                                                         
         DROP  R4                                                               
*                                                                               
         CLEAR R2                  Init to null string                          
         IF    (R15,NZ),FNDSNIX    Can't get the info ...                       
*                                                                               
         IF    (SHDSPGCT,EQ,0),FNDSNIX  No pages????                            
         CLEAR R0                  Assume no page number                        
         IF    (PAR,NZ),'PNUM PAR' Get number of page already in                
         IF    (R0,NE,SHDSPAGS),'PGET PAR,L:SHDSPAGS'  Get proper page          
*                                                                               
         LA    R2,2                Number of bytes processed on page            
         CH    R2,@PAR             More on page?                                
         IF    NL,FNDSNIX          No - all done                                
         LC    R4,@R2(PAR)         Field character count                        
         IF    (R4,Z),FNDSNIX      All done                                     
         LA    R2,3(,PAR)          Point at DSNINFO                             
         WITH  (DSNINFO,R2),BEGIN                                               
         IF    (DSNERRCD,NE,' '),BEGIN                                          
         CLEAR R2                                                               
         B     FNDSNIX                                                          
         END                                                                    
         END                                                                    
         XPUSH ,,512,PTR=R5                                                     
         LA    R1,L'DSNINFO(,R5)                                                
         MVC   0(L'DSNINFO,R5),CVBLANKS                                         
         DEX   R4,'MVC 0(0,R5),0(R2)'                                           
         LR    R2,R5                                                            
         LA    R0,256                                                           
         VCALL DSPDSNIN                                                         
         LR    R2,R0                                                            
         CEIL  R2,L'FNWORK                                                      
         MOVE  R2,FNWORK,@R1                                                    
         INCR  R2                                                               
         XPOP  ,,512                                                            
*                                                                               
         PFREE PAR,EMPTY           Don't need this anymore                      
*                                                                               
FNDSNIX  XPOP  ,,L'SHDSARGS                                                     
         VRETURN STRING,FNWORK,(R2)                                             
         END                                                                    
         END                                                                    
         B     FNDSNINX                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
FNDSNINE LABEL ,                                                                
         VRETURN STRING,'BAD DSN'                                               
         B     FNDSNINX                                                         
*                                                                               
FNDSNINX SCPOP ,                                                                
         PEND                                                                   
*                                                                               
*  WHERE IS THIS ROUTINE USED IN DIR ???                                        
*  MAY NEED TO RESTORE TO ORIGINAL ?                                            
*                                                                               
GETTPAG  XPROC FNWA                                                             
         XPUSH R15                                                              
         FAIL  (CPMDLN,NZ),DSNINFO,'DSNINFO error #1.'                          
         VCALL PGETNEW             Get a temp page in PAR                       
         IF    Z,BEGIN             Got the page ...                             
         LA    R2,1                One page                                     
         ST    R2,CPMDLN           Store page count                             
         ST    R0,CPMDAD           Store page no                                
         END                                                                    
         XPOP  R15                                                              
         LTR   PAR,PAR             NEED TO RETURN PAR AS CC ??                  
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNDSIPRT SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,FNDSNINB                                                        
*                                                                               
FNDSNINB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  Verify Existence of a Dataset on the System                                  
*                                                                               
*  See comments at start of this module                                         
*                                                                               
FNDSNVER PROC  FNWA                                                             
         SCPUSH ,                                                               
         SETARGS '12SS'            Get string argument(s)                       
         IF    Z,BEGIN             If ok, process                               
         LR    R2,R0               Save number of arguments                     
         LR    R3,R1                 and argument pointer                       
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,@R1+ARG1         Address argument                             
         SCINIT VNSTRLOC,LH:VNSTRLEN                                            
         END                                                                    
         SET   CPFDSNNE                                                         
         VCALL FNAMESCN            Get the filename                             
         IF    (R15,NZ),BEGIN                                                   
         SCSEGCLR                                                               
         B     FNDSNVEE                                                         
         END                                                                    
         SCAN  FNDSVPRT            Find any overrides                           
         IF    (R15,NZ),FNDSNVEE                                                
         COMMENT                   FINISH FILE NAME PROCESSING                  
         VCALL FNAMEFIN                                                         
         L     R4,CPFINFOP                                                      
         USING FFINFO,R4                                                        
         SET   FFFWINGS                                                         
         SET   FFFREAD                                                          
         VCALL NRESOLVE                                                         
         IF    (R15,NZ),FNDSNVEE                                                
         CLEAR CPFDSNNE                                                         
*                                                                               
         IF    (R2,LT,2),FNDSNVDO                                               
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,ARG2(R3)                                                      
         SCINIT VNSTRLOC,LH:VNSTRLEN  Options                                   
         END                                                                    
         SCAN  DSNVPRT                                                          
         IF    (R15,NZ),BEGIN                                                   
         BADARG 2                                                               
         B     FNDSNVEX                                                         
         END                                                                    
*                                                                               
FNDSNVDO VCALL WGSACCES                                                         
         IF    (R15,Z),'SETMSG "OK"; B FNDSNVX'                                 
         IF    (CPERRID,EQ,'BADOSDSN'),'B FNDSNVEE'                             
         IF    (CPERRID,EQ,'NOTFOUND'),'SETMSG "NOT THERE"; B FNDSNVX'          
         IF    (CPERRID,EQ,'INUSE   '),'SETMSG "IN USE"; B FNDSNVX'             
         IF    ((CPERRID,EQ,'NOTAUTH '),OR,                            X        
               (CPERRID,EQ,'NOACCESS')),BEGIN                                   
         SETMSG 'NO ACCESS'                                                     
         B     FNDSNVX                                                          
         END                                                                    
         IF    ((CPERRID,EQ,'NOTMNTD '),OR,                            X        
               (CPERRID,EQ,'NOVOL   '),OR,                             X        
               (CPERRID,EQ,'INVVOL  ')),BEGIN                                   
         SETMSG 'OFFLINE'                                                       
         B     FNDSNVX                                                          
         END                                                                    
         IF    ((CPERRID,EQ,'NOMEMBER'),OR,                            X        
               (CPERRID,EQ,'NOTINPDS')),BEGIN                                   
         SETMSG 'BAD MEMBER'                                                    
         B     FNDSNVX                                                          
         END                                                                    
         SETMSG 'UNUSABLE'                                                      
FNDSNVX  VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         B     FNDSNVEX                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
FNDSNVEE LABEL ,                                                                
         VRETURN STRING,'BAD DSN'                                               
         B     FNDSNVEX                                                         
*                                                                               
FNDSNVEX SCPOP ,                                                                
         PEND                                                                   
*                                                                               
*  SCAN DSN NAME                                                                
*                                                                               
FNDSVPRT SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,FNDSNVEB                                                        
*-                                                                              
*-       DSNVERIFY function options.                                            
*-                                                                              
DSNVPRT  SCKW  READ,FNDSNVRD,A                                                  
         SCKW  WRITE,FNDSNVWR,A                                                 
         SCKW  ,FNDSNVEB                                                        
*                                                                               
FNDSNVRD PROC  ,                                                                
         SET   FFFREAD                                                          
         PEND                                                                   
*                                                                               
FNDSNVWR PROC  ,                                                                
         SET   FFFWRITE                                                         
         PEND                                                                   
*                                                                               
FNDSNVEB DPROC ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
         DROP  R4                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  D2C Function.                                                                
*                                                                               
*    D2C(??????????)                                                            
*                                                                               
FND2C    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  D2X Function.                                                                
*                                                                               
*    D2X(??????????)                                                            
*                                                                               
FND2X    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  EBCDIC                                                                       
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNEBCDIC PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVEBCTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
ENCRYWA  RECORD BEGIN                                                           
ENCARGP  DS    F                   ARG ptr                                      
ENCNARG  DS    F                   Number of arguments                          
*                                                                               
ENCTEXT  DS    CL(MAXSTRLN)        Encrypted text area                          
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  ENCRYPT -- ENCRYPT Function.                                                 
*                                                                               
*    ENCRYPT(text-string[, encryption-key-string])                              
*                                                                               
FNENCRYP PROC  ENCRYWA                                                          
         CLEAR ENCRYWA                                                          
*                                                                               
         SETARGS '12SS'            1-2 args: string, string                     
         IF    Z,BEGIN             OK...                                        
         ST    R0,ENCNARG          Number of arguments                          
         ST    R1,ENCARGP          Start of args ptr                            
*                                                                               
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1: text to be encrypted.                                           
*-                                                                              
         LA    R5,ARG1                                                          
         AL    R5,ENCARGP                                                       
*                                                                               
         LH    R4,VNSTRLEN                                                      
         MOVEL ENCTEXT,VNSTRLOC,(R4)  Copy text string                          
*-                                                                              
*-       Arg 2: encryption key.                                                 
*-                                                                              
         CLEAR R2,R3               Assume no key                                
*                                                                               
         IF    (ENCNARG,GE,2),BEGIN  Encryption key specified...                
         LA    R5,ARG2                                                          
         AL    R5,ENCARGP                                                       
*                                                                               
         LS    R2,R3,VNSTRLOC,LH:VNSTRLEN  Encryption key                       
         END                                                                    
*-                                                                              
*-       Do the encryption.                                                     
*-                                                                              
         SETMSG ENCTEXT,(R4)        Text to encrypt                             
         VCALL ENCRYPT             Encrypt the text                             
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FIND                                                                         
*                                                                               
*  FIND(STRING,CHARS_TO_FIND),,,                                                
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNFIND   PROC  FNWA                                                             
         SETARGS '22SS'            2 ARGS: STRING, STRING                       
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - 1ST STRING LOC, LEN                  
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R4,VNSTRLOC         R4,R5 - 2ND STRING LOC, END                  
         LH    R5,VNSTRLEN                                                      
         AR    R5,R4                                                            
         DROP  R5                                                               
         CLEAR FNWORK              SET UP TRT TABLE                             
         SR    R1,R1               MARK CHARS IN STRING #2                      
         LA    R0,1                                                             
         WHILE (R4,LT,R5),BEGIN    MARK CHARS IN STRING #2                      
         IC    R1,0(R4)                                                         
         STC   R0,FNWORK(R1)                                                    
         LA    R4,1(R4)                                                         
         END                                                                    
         LR    R1,R2               R1,R4 - START OF STRING #1                   
         LR    R4,R2                                                            
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' TRT @R1(0),FNWORK '   ** TRT **                             
         IF    NZ,BEGIN                                                         
         LA    R1,1(R1)            OFFSET+1 = CHAR NUMBER                       
         END                                                                    
         END                                                                    
         COMMENT                   R1 - LOC OF 1ST CHAR FOUND                   
         SR    R1,R4               R1 - INDEX OF 1ST CHAR FOUND                 
         VRETURN INTEGER,(R1)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  FORMAT                                                                       
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FMTWA    RECORD BEGIN                                                           
FMTARG2  DS    F                   LEADING LENGTH  (BLANK PADDED)               
FMTARG3  DS    F                   TRAILING LENGTH (ZERO PADDED)                
FMTARG1  DS    CL(64+1+1)          NUMBER IN STRING FORMAT  (-1,-1)             
FMTARG1L DS    F                   LENGTH OF STRING                             
FMTAREA  DS    CL(64+1+1)          FORMATING AREA                               
         END                                                                    
*-                                                                              
FNFORMAT PROC  FMTWA                                                            
*                                                                               
         SETARGS '33NII'           3 ARGS: NUMBER, INTEGER, INTEGER             
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
*                                                                               
         COMMENT                   CONVERT ARG #1 TO STRING                     
         LA    R5,ARG1(R1)         AND SAVE                                     
         LA    R2,VNENTRY                                                       
         VCALL MAKESTR                                                          
         LH    R3,VNSTRLEN                                                      
         ST    R3,FMTARG1L                                                      
         MOVE  R3,FMTARG1,VNSTRLOC                                              
*                                                                               
         COMMENT                   CHECK ARG #2 AND #3 AND SAVE                 
         COMMENT                                                                
         LA    R5,ARG2(R1)                                                      
         L     R2,VNVALUE                                                       
         ST    R2,FMTARG2                                                       
         IF    ((R2,GT,64),OR,(R2,LT,-1)),BEGIN                                 
         BADARG 2                                                               
         EXIT  FNFORMAT                                                         
         END                                                                    
         LA    R5,ARG3(R1)                                                      
         L     R2,VNVALUE                                                       
         ST    R2,FMTARG3                                                       
         IF    ((R2,GT,32),OR,(R2,LT,-1)),BEGIN                                 
         BADARG 3                                                               
         EXIT  FNFORMAT                                                         
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   LOCATE DECIMAL POINT @R3, IF ANY             
         LA    R3,FMTARG1                                                       
         LA    R4,FMTARG1                                                       
         A     R4,FMTARG1L                                                      
         WHILE ((R3,LT,R4),AND,(@R3,NE,'.')),BEGIN                              
         INCR  R3                                                               
         END                                                                    
         IF    (R3,EQ,R4),BEGIN    IF NO DECIMAL POINT, PUT ONE                 
         MVI   0(R3),C'.'          R3 - ADDR OF DECIMAL POINT                   
         INCR  R4                  R4 - END OF STRING                           
         END                                                                    
*                                                                               
         COMMENT                   CALCULATE                                    
         COMMENT                   R4 - UNFORMATTED LEADING LEN                 
         COMMENT                   R5 - UNFORMATTED TRAILING LEN                
         LR    R5,R4                                                            
         SR    R5,R3                                                            
         DECR  R5                                                               
         LR    R4,R3                                                            
         LA    R2,FMTARG1                                                       
         SR    R4,R2                                                            
*                                                                               
*                                                                               
         COMMENT                   DO FORMATTING   (WHOLE PART)                 
         LA    R1,FMTAREA          R1 - FORMAT AREA                             
         LA    R2,FMTARG1          R2 - UNFORMATTED NUMBER                      
         L     R3,FMTARG2          R3 - COUNTER (LEADING COUNTER)               
         IF    (FMTARG2,EQ,-1),BEGIN                                            
         LR    R3,R4                                                            
         END                                                                    
         WHILE (R3,GT,R4),BEGIN    PUT OUT LEADING BLANKS                       
         MVI   0(R1),C' '                                                       
         INCR  R1                                                               
         DECR  R3                                                               
         END                                                                    
         IF    (R3,LT,R4),BEGIN    ADJUST IF TRUNCATING WHOLE PART              
         AR    R2,R4                                                            
         SR    R2,R3                                                            
         END                                                                    
         WHILE (R3,POS),BEGIN      MOVE WHOLE PART OF NUMBER                    
         MVC   0(1,R1),0(R2)                                                    
         INCR  R1                                                               
         INCR  R2                                                               
         DECR  R3                                                               
         END                                                                    
*                                                                               
         COMMENT                   DO FORMATTING (FRACTIONAL PART)              
         COMMENT                                                                
         LA    R3,1                R3 - COUNTER (TRAILING COUNT)                
         IF    (FMTARG3,EQ,-1),BEGIN                                            
         ST    R5,FMTARG3                                                       
         END                                                                    
         IF    (FMTARG3,GT,0),BEGIN                                             
         MVI   0(R1),C'.'          PUT OUT DECIMAL POINT                        
         INCR  R1                                                               
         INCR  R2                                                               
         WHILE (R3,LE,FMTARG3),BEGIN  PUT OUT FRACTIONAL PART                   
         IF    (R3,LE,R5),BEGIN       WITH DIGITS                               
         MVC   0(1,R1),0(R2)                                                    
         INCR  R1                                                               
         INCR  R2                                                               
         INCR  R3                                                               
         END                                                                    
         ELSE  BEGIN ,                WITH TRAILING ZEROS                       
         MVI   0(R1),C'0'                                                       
         INCR  R1                                                               
         INCR  R3                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   GET FORMATTED STRING LOC, LENGTH             
         LR    R2,R1                                                            
         LA    R1,FMTAREA          R1 - LOCATION                                
         SR    R2,R1               R2 - LENGTH                                  
*                                                                               
         COMMENT                   RETURN FORMATTED NUMBER                      
         VRETURN STRING,(R1),(R2)                                               
*                                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  Common work area for GET_ACCOUNT, GET_USERID,                                
*     GET_USERID_LIST, GET_USERID_TYPE.                                         
*                                                                               
FNGIDWA  RECORD BEGIN                                                           
FNGIACCT DS    CL5                 Working account (uuugg form)                 
FNGIDATA DS    CL256               Work area                                    
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  GET_ACCOUNT function.                                                        
*                                                                               
FNGETACC PROC  FNGIDWA                                                          
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account/id                                
         VCALL LRTRIM                                                           
         LA    R15,FNGIACCT        Put account in "uuugg" form here             
         VCALL GETACCT             Get account                                  
         IF    NZ,'CLEAR R1,R0'    ID not found                                 
         ELSE  BEGIN                                                            
         MVC   FNGIDATA(2),FNGIACCT+3  gg                                       
         MVI   FNGIDATA+2,C'.'                                                  
         MVC   FNGIDATA+3(3),FNGIACCT  uuu                                      
         SETMSG FNGIDATA,6          "gg.uuu"                                    
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GET_USERID function.                                                         
*                                                                               
FNGETID  PROC  FNGIDWA                                                          
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account                                   
         VCALL LRTRIM                                                           
         LA    R15,FNGIACCT        Put account in "uuugg" form here             
         VCALL GETACCT             Get account                                  
         IF    NZ,'CLEAR R1,R0'    ID not found                                 
         ELSE  BEGIN                                                            
         LA    R15,FNGIACCT        "uuugg" ptr                                  
         SETMSG FNGIDATA            Work area                                   
         VCALL GETUID              Get userid for this account                  
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GET_USERID_LIST function.                                                    
*                                                                               
FNGETIDL PROC  FNGIDWA                                                          
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Account                                   
         VCALL LRTRIM                                                           
         LA    R15,FNGIACCT        Put account in "uuugg" form here             
         VCALL GETACCT             Get account                                  
*                                                                               
         LA    R15,FNGIACCT        "uuugg" ptr                                  
         SETMSG FNGIDATA            Work area                                   
         VCALL GETUIDL             Get IDLIST for this account                  
         IF    NZ,'CLEAR R0'       No IDLIST                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GET_USERID_TYPE function.                                                    
*                                                                               
FNGETIDT PROC  FNGIDWA                                                          
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG '???'               !!!! TEMPORARY !!!!                         
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  GMT function.                                                                
*                                                                               
*    Convert a local time string                                                
*    of the form YYYY/MM/DD HH:MM[:SS] to                                       
*    ARPA standard format in GMT                                                
*                                                                               
FNGMTWA  RECORD BEGIN                                                           
FNGMTCLK DS    D                                                                
FNGMTOFF DS    F                                                                
FNGMTCNV DS    CL16                                                             
FNGMTXDT DS    CL32                                                             
         END                                                                    
         SPACE 2                                                                
FNGETGMT PROC  FNGMTWA                                                          
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         IF    (R3,GE,16),BEGIN                                                 
         CLEAR FNGMTCNV                                                         
         LC    R0,11(,R2)          Do HH                                        
         SLL   R0,28                                                            
         LC    R1,12(,R2)                                                       
         SLL   R1,28                                                            
         SRL   R1,4                                                             
         OR    R0,R1                                                            
         LC    R1,14(,R2)          Do MM                                        
         SLL   R1,28                                                            
         SRL   R1,8                                                             
         OR    R0,R1                                                            
         LC    R1,15(,R2)                                                       
         SLL   R1,28                                                            
         SRL   R1,12                                                            
         OR    R0,R1                                                            
         IF    (R3,GE,19),BEGIN    Do SS                                        
         LC    R1,17(,R2)                                                       
         SLL   R1,28                                                            
         SRL   R1,16                                                            
         OR    R0,R1                                                            
         LC    R1,18(,R2)                                                       
         SLL   R1,28                                                            
         SRL   R1,20                                                            
         OR    R0,R1                                                            
         END                                                                    
         ST    R0,FNGMTCNV         Time goes in 1st word                        
         LC    R0,0(,R2)           Do YYYY                                      
         SLL   R0,28                                                            
         LC    R1,1(,R2)                                                        
         SLL   R1,28                                                            
         SRL   R1,4                                                             
         OR    R0,R1                                                            
         LC    R1,2(,R2)                                                        
         SLL   R1,28                                                            
         SRL   R1,8                                                             
         OR    R0,R1                                                            
         LC    R1,3(,R2)                                                        
         SLL   R1,28                                                            
         SRL   R1,12                                                            
         OR    R0,R1                                                            
         LC    R1,5(,R2)           Do MM                                        
         SLL   R1,28                                                            
         SRL   R1,16                                                            
         OR    R0,R1                                                            
         LC    R1,6(,R2)                                                        
         SLL   R1,28                                                            
         SRL   R1,20                                                            
         OR    R0,R1                                                            
         LC    R1,8(,R2)           And DD                                       
         SLL   R1,28                                                            
         SRL   R1,24                                                            
         OR    R0,R1                                                            
         LC    R1,9(,R2)                                                        
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         OR    R0,R1                                                            
         ST    R0,FNGMTCNV+8       Date goes in 3rd word                        
         CONVTOD CONVVAL=FNGMTCNV,TODVAL=FNGMTCLK,DATETYPE=YYYYMMDD             
         IF    (R15,Z),BEGIN                                                    
         LM    R0,R1,FNGMTCLK                                                   
         LA    R15,FNGMTXDT                                                     
         VCALL FMTXDATE                                                         
         MVC   FNGMTOFF,=X'0000800F'   Assume standard time                     
         IF    (@R1+27,EQ,'D'),BEGIN   Daylight/Standard time?                  
         MVC   FNGMTOFF,=X'0000700F'                                            
         END                                                                    
         CONVTOD CONVVAL=FNGMTCNV,TODVAL=FNGMTCLK,DATETYPE=YYYYMMDD,   X        
               OFFSET=FNGMTOFF                                                  
         IF    (R15,Z),BEGIN                                                    
         LM    R0,R1,FNGMTCLK                                                   
         LA    R15,FNGMTXDT                                                     
         VCALL FMTXDATE                                                         
         IF    (@R1+5,EQ,' '),'MVI @R1+5,C"0"'  Restore leading zero            
         MVC   26(3,R1),=CL3'GMT'                                               
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0,R1                                                            
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0,R1                                                            
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0,R1                                                            
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         QLTORG ,                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*   HOST_INFO( what [,host] )                                                   
*                                                                               
*   Return information about either the specified host or (if host is           
*   omitted) the current host.  Currently "what" can be used to return          
*   one of the following:                                                       
*      'HOST'   - the hostname (only useful if "host" is omitted)               
*      'USER'   - the username                                                  
*      'PASS'   - "*" if a password has been set, "" if not                     
*      'ACCT'   - the account                                                   
*                                                                               
HIWA     RECORD  BEGIN                                                          
HIFLAG1  FLAG    HIFHOST           wants the host name                          
         FLAG    HIFPASS           wants the password                           
         DS      0F                                                             
HIKEY    DS      0CL64             a place for the key                          
HIKTYPE  DS      CL5                 type_                                      
HIKHOST  DS      CL59                     hostname                              
HIDATA   DS      CL32              a place for the record                       
         END                                                                    
         SPACE   2                                                              
*                                                                               
FNHOSTI  PROC    HIWA                                                           
         LA      R3,HIWA                                                        
         USING   HIWA,R3                                                        
         CLEAR   HIFLAG1                                                        
         MVC     HIKHOST,CVBLANKS                                               
         SETARGS '12SS'            check arguments                              
         IF      Z,BEGIN                                                        
*                                                                               
* Create a search key of the form "what_host"  (i.e., USER_CCVAX)               
*                                                                               
         LR      R4,R1             move argument pointer to R4                  
         WITH    (VNENTRY,R5)                                                   
         IF      (R0,GE,2),BEGIN   if (a host specified) begin                  
         LA      R5,ARG2(R4)                                                    
         SETMSG   VNSTRLOC,LH:VNSTRLEN  get address & length                    
         VCALL   LRTRIM                trim leading/trailing blanks             
         END                                                                    
         ELSE    BEGIN             otherwise                                    
         SETMSG   CPHOST                use default host                        
         END                                                                    
         LR      R15,R0                                                         
         CEIL    R15,L'HIKHOST     insure a reasonable length                   
         MOVE    R15,HIKHOST,@R1   copy into key area                           
         L       R2,CVUPPTBL                                                    
         TR      HIKHOST,@R2       make it uppercase                            
*                                                                               
*   Scan "type" to finish building the key                                      
*                                                                               
         SCPUSH  ,                                                              
         LA      R5,ARG1(R4)                                                    
         SCINIT  VNSTRLOC,LH:VNSTRLEN  get address & length                     
         SCAN    HIPRT                                                          
         SCPOP   ,                                                              
         IF    (R15,NZ),HIXIT                                                   
*                                                                               
* If they just want the name of the (current) host, give it to them             
*                                                                               
         IF      (HIFHOST),BEGIN                                                
         SETMSG   HIKHOST                                                       
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         ELSE    BEGIN                                                          
*                                                                               
* Setup access to the record group                                              
*                                                                               
         LA      R0,CP#HOSTI                                                    
         LA      R15,CPRG64                                                     
         VCALL   XSETSET                                                        
*                                                                               
* Find the record                                                               
*                                                                               
         LA      R2,HIKEY                                                       
         SETMSG   HIDATA                                                        
         LA      R15,CPRG64                                                     
         VCALL   XGET                                                           
         IF      NZ,'CLEAR R0'                                                  
         ELSEIF  (HIFPASS),'SETMSG "*"'                                         
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         END                                                                    
HIXIT    LABEL ,                                                                
         PEND  ,                                                                
*                                                                               
* Scan table for "type"                                                         
*                                                                               
HIPRT    SCKW    ACCT,HIACCT                                                    
         SCKW    HOST,HIHOST                                                    
         SCKW    PASS,HIPASS                                                    
         SCKW    USER,HIUSER                                                    
         SCKW    ,HIERR                                                         
*                                                                               
* Modify the search key to reflect what they are asking for                     
*                                                                               
HIACCT   PROC                                                                   
         MVC     HIKEY(5),=C'ACCT_'                                             
         PEND                                                                   
*                                                                               
HIPASS   PROC                                                                   
         MVC     HIKEY(5),=C'PASS_'                                             
         SET     HIFPASS                                                        
         PEND                                                                   
*                                                                               
HIUSER   PROC                                                                   
         MVC     HIKEY(5),=C'USER_'                                             
         PEND                                                                   
*                                                                               
HIHOST   PROC                                                                   
         SET     HIFHOST           Type=HOST is special                         
         PEND                                                                   
*                                                                               
HIERR    PROC                                                                   
         TSEG    (R1),(R0)                                                      
         TSEG    ' is an unknown HOST_INFO option.'                             
         BADARG  1                                                              
         LA      R15,4                                                          
         PEND                                                                   
         DROP    R3                                                             
         EJECT                                                                  
*box                                                                            
*                                                                               
*  HOST_USER('host') -- Return userid for the host specified.                   
*                                                                               
*  See comments at the start of this module.                                    
*                                                                               
HWA      RECORD BEGIN                                                           
HWAREA   DS    CL64                Assemble record key here                     
HWDATA   DS    CL64                Data goes here                               
         END                                                                    
*-                                                                              
FNHOSTU  PROC  HWA                                                              
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         MVC   HWAREA,CVBLANKS                                                  
         MVC   HWAREA(5),=C'USER_'                                              
*                                                                               
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Host name specified                       
         VCALL LRTRIM              Trim leading/trailing blanks                 
         IF    (R0,Z),'SETMSG CPHOST'  Use default host name                    
         LR    R15,R0                                                           
         CEIL  R15,L'HWAREA-5      Not too long now                             
         MOVE  R15,HWAREA+5,@R1    Make "USER_host" as key                      
         L     R2,CVUPPTBL                                                      
         TR    HWAREA,@R2          Make sure it's all upper case                
*                                                                               
         LA    R0,CP#HOSTI                                                      
         LA    R15,CPRG64          Misc keylen 64 record group                  
         VCALL XSETSET             Set host info RG                             
*                                                                               
         LA    R2,HWAREA                                                        
         SETMSG HWDATA                                                          
         LA    R15,CPRG64                                                       
         VCALL XGET                Get user-id for this host                    
         IF    NZ,'CLEAR R0'                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)  Return network userid                        
         VTRIM                                                                  
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INCHAR -- Routine to get a single input character from the                   
*    terminal.                                                                  
*                                                                               
*                                                                               
INCHRWA  RECORD BEGIN                                                           
INCHRFLG FLAG                                                                   
         FLAG  INCHRFATTN          - ATTNs are allowed                          
         FLAG  INCHRFTRAN          - TRANSPARENT mode                           
*                                                                               
INCHRPTR DS    2A                  Prompt loc, len                              
INCHRRC  DS    F                   TREAD return code                            
*                                                                               
INCHRTVL DS    F                   Timeout in .01 sec (0=no timeout)            
INCHRTRM DS    CL(L'CPTRM)         Saved terminal options                       
         END                                                                    
*-                                                                              
FNINCHAR PROC  INCHRWA                                                          
         LA    R4,INCHRWA                                                       
         USING INCHRWA,R4                                                       
         CLEAR INCHRWA                                                          
*                                                                               
         SCPUSH ,                                                               
         MVC   INCHRTRM,CPTRM      Save terminal options                        
         SET   CPFTRAN,CPFNMOD,CPFUFMT                                          
         SET   CPFRUND             Count rundown mode                           
         MVC   CPTRMIN,=H'1'       Maximum number of chars                      
*                                                                               
         SETARGS '12SS'            1-2 args: str, str                           
         BNZ   INCHRXIT                                                         
*                                                                               
         IF    CPFZAPIF,BEGIN      Expression rescan...                         
         VRETURN STRING,'DUMMY'                                                 
         B     INCHRXIT                                                         
         END                                                                    
*                                                                               
         LR    R2,R0               Save number of arguments                     
         LR    R3,R1               Argument list ptr                            
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Save prompt string pointers.                                           
*-                                                                              
         LA    R5,ARG1(R3)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN  Prompt text                               
*                                                                               
         IF    ^INCHRFTRAN,BEGIN   Translate prompt text...                     
         VCALL TOASCII             Convert to ASCII                             
         END                                                                    
*                                                                               
         STM   R0,R1,INCHRPTR      Save ptrs for later                          
*-                                                                              
*-       Scan second parameter for inchar options.                              
*-                                                                              
         IF    (R2,LT,2),INCHRDO                                                
         LA    R5,ARG2(R3)                                                      
         SCINIT VNSTRLOC,LH:VNSTRLEN  Options                                   
         SCAN  INCPRT                                                           
         IF    (R15,NZ),INCERR                                                  
         B     INCHRDO             Now go do it                                 
*                                                                               
INCERR   TSEG  (R1),(R0)                                                        
         TSEG  ' is an unknown INCHAR option.',,CR                              
         BADARG 2                                                               
         LA    R15,4                                                            
         B     INCHRXIT                                                         
*-                                                                              
*-       Do the terminal read for one character.                                
*-                                                                              
INCHRDO  LABEL ,                                                                
*                                                                               
         TPUSH                                                                  
*                                                                               
         LM    R0,R1,INCHRPTR      Prompt                                       
         L     R15,INCHRTVL        Timeout value in .01 secs                    
         TREAD (R1),(R0),TIMEOUT=(R15)  Get input from user                     
         ST    R15,INCHRRC                                                      
*                                                                               
         IF    (R0,GE,1),BEGIN     We got the character...                      
         IF    INCHRFTRAN,EXIT     Transparent, don't xl, scram                 
         LC    R15,@R1             Get ASCII character                          
         N     R15,=A(X'7F')                                                    
         A     R15,CVEBCTBL                                                     
         SETMSG (R15),1             EBCDIC character                            
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)  Return input string                          
         TPOP  JUNK                                                             
*-                                                                              
*-       Set IORESULT variable to input return code.                            
*-                                                                              
         SETMSG 'OK'                                                            
*                                                                               
         IF    (INCHRRC,EQ,1),BEGIN  Attn...                                    
         IF    INCHRFATTN,BEGIN    Attn's are allowed...                        
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'CLEAR JCBFATTN,JCBFPATN'                               
         END                                                                    
*                                                                               
         SETMSG 'ATTN'                                                          
         END                                                                    
*                                                                               
         IF    (INCHRRC,EQ,2),CVABORT  ?-Attn...  force EXEC BREAK              
*                                                                               
         IF    (INCHRRC,EQ,101),BEGIN  Reverse break...                         
         SETMSG 'TIMEOUT'           (Can only happen because TIMEOUT)           
         END                                                                    
*                                                                               
         LA    R2,=CL16'SYS.IORESULT'                                           
         VCALL SAVEDATA            Save I/O return code in IORESULT             
*                                                                               
         CLEAR R15                                                              
*                                                                               
INCHRXIT MVC   CPTRM,INCHRTRM      Restore terminal options                     
         SCPOP ,                                                                
         PEND                                                                   
*-                                                                              
*-       INCHAR function options.                                               
*-                                                                              
INCPRT   SCKW  ATTN,INCATTN,A                                                   
         SCKW  INTERRUPT,INCINT,A                                               
         SCKW  CLEAN,INCCLEAN,A                                                 
         SCKW  ECHO,INCECHO,A                                                   
         SCKW  NOECHO,INCNECHO,A                                                
         SCKW  TYPEAHEAD,INCTYP,A                                               
         SCKW  NOTYPEAHEAD,INCNTYP,A                                            
         SCKW  FORCE,INCFORCE,A                                                 
         SCKW  TRANSPARENT,INCTRAN,A                                            
         SCKW  TIMEOUT,INCTMOUT,(A,P,PI),100000                                 
         SCKW  ,INCHRERR                                                        
*                                                                               
INCATTN  PROC  ,                                                                
         SET   INCHRFATTN                                                       
         PEND                                                                   
*                                                                               
INCTRAN  PROC  ,                                                                
         SET   INCHRFTRAN                                                       
         PEND                                                                   
*                                                                               
INCINT   PROC  ,                                                                
         SET   CPFTINT                                                          
         PEND                                                                   
*                                                                               
INCCLEAN PROC  ,                                                                
         SET   CPFCLEAN                                                         
         PEND                                                                   
*                                                                               
INCECHO  PROC  ,                                                                
         CLEAR CPFNECHO                                                         
         PEND                                                                   
*                                                                               
INCNECHO PROC  ,                                                                
         SET   CPFNECHO                                                         
         PEND                                                                   
*                                                                               
INCTYP   PROC  ,                                                                
         CLEAR CPFNTYPE                                                         
         PEND                                                                   
*                                                                               
INCNTYP  PROC  ,                                                                
         SET   CPFNTYPE                                                         
         PEND                                                                   
*                                                                               
INCFORCE PROC  ,                                                                
         SET   CPFFORCE                                                         
         PEND                                                                   
*                                                                               
INCTMOUT PROC  ,                                                                
         MH    R0,=H'100'          Times 100 for value in .01 sec               
         ST    R0,INCHRTVL                                                      
         PEND                                                                   
*                                                                               
INCHRERR PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  INDEX                                                                        
*                                                                               
*  INDEX(STRING,STRING_TO_FIND),,,                                              
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNINDEX  PROC  ,                                                                
         SETARGS '22SS'            2 ARGS: STRING, STRING                       
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - 1ST STRING LOC, LEN                  
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R4,VNSTRLOC         R4,R5 - 2ND STRING LOC, LEN                  
         LH    R5,VNSTRLEN                                                      
         DROP  R5                                                               
         LR    R1,R2               R1 - INDEX INTO 1ST STRING                   
         COMMENT                                                                
         IF    ((R3,POS),AND,(R5,POS)),BEGIN     NON NULL STRINGS               
         IF    (R3,GE,R5),BEGIN                  1ST STR LEN >= 2ND             
         AR    R3,R2                                                            
         SR    R3,R5               R3 - LOC TO STOP CLC TEST                    
         BCTR  R5,0                R5 - LEN TO TEST (-1 FOR EX)                 
         LR    R1,R2               R1 - LOC TO TEST                             
         SR    R0,R0               R0 - ASSUME NOT FOUND (R0=0)                 
         COMMENT                                                                
         WHILE ((R0,Z),AND,(R1,LE,R3)),BEGIN                                    
         EX    R5,' CLC @R1(0),@R4 '                                            
         IF    E,'LA R0,1'         IF INDEX FOUND, SET R0=1                     
         LA    R1,1(R1)                                                         
         END                                                                    
         COMMENT                   R1 - OFFSET OF MATCH+1 = CHAR #              
         IF    (R0,Z),'LR R1,R2'   IF NO MATCH RESET R1=R2                      
         END                                                                    
         END                                                                    
         SR    R1,R2               R1 - INDEX OF STRING                         
         VRETURN INTEGER,(R1)                                                   
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
INSTRWA  RECORD BEGIN                                                           
INSFLAG  FLAG                                                                   
         FLAG  INSFATTN            - ATTNs are allowed                          
         FLAG  INSFCASE            - Saved copy of CPFCASE flag                 
         FLAG  INSFUPPER           - Saved copy of CPFUPPER flag                
*                                                                               
INSPTRS  DS    2A                  Prompt loc, len                              
INSRC    DS    F                   TREAD return code                            
*                                                                               
INSTMVAL DS    F                   Timeout in .01 sec (0=no timeout)            
*                                                                               
INSTRTRM DS    XL(L'CPTRM)         Saved terminal options                       
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*                                                                               
*  INPUT -- Routine to get terminal input.                                      
*                                                                               
*    See comments at the start of this module.                                  
*                                                                               
FNINPUT  PROC  INSTRWA                                                          
         LA    R4,INSTRWA                                                       
         USING INSTRWA,R4                                                       
         CLEAR INSTRWA                                                          
*                                                                               
         SCPUSH ,                                                               
         MVC   INSTRTRM,CPTRM      Save terminal options                        
         IF    CPFCASE,'SET INSFCASE'  Save flags                               
         IF    CPFUPPER,'SET INSFUPPER'                                         
         SET   CPFCASE                                                          
         CLEAR CPFUPPER            Force UPLOW                                  
*                                                                               
         MVC   CPTRMIN,=AL2(MAXSTRLN)  Maximum read length                      
*                                                                               
         SETARGS '12SS'            1-2 args: str, str                           
         BNZ   INPEXIT                                                          
*                                                                               
         IF    CPFZAPIF,BEGIN      Expression rescan...                         
         VRETURN STRING,'DUMMY'                                                 
         B     INPEXIT             Scram                                        
         END                                                                    
*                                                                               
         LR    R2,R0               Save number of arguments                     
         LR    R3,R1               Argument list ptr                            
         WITH  (VNENTRY,R5)        (Will be accurate later)                     
*-                                                                              
*-       Save prompt string pointers.                                           
*-                                                                              
         LA    R5,ARG1(R3)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN  Prompt text                               
         STM   R0,R1,INSPTRS       Save ptrs for later                          
*-                                                                              
*-       Scan second parameter for input options.                               
*-                                                                              
         IF    (R2,LT,2),INPCONT                                                
         LA    R5,ARG2(R3)                                                      
         SCINIT VNSTRLOC,LH:VNSTRLEN  Options                                   
         SCAN  INPPRT                                                           
         IF    (R15,NZ),INPERR                                                  
         B     INPCONT             Now go do it                                 
*                                                                               
INPERR   TSEG  (R1),(R0)                                                        
         TSEG  ' is an unknown INPUT option.'                                   
         BADARG 2                                                               
         LA    R15,4                                                            
         B     INPEXIT                                                          
*                                                                               
*                                                                               
INPCONT  LABEL ,                                                                
*                                                                               
         TPUSH                                                                  
*                                                                               
         LM    R0,R1,INSPTRS       Prompt                                       
         L     R15,INSTMVAL        Timeout value in .01 secs                    
         TREAD (R1),(R0),TIMEOUT=(R15)  Get input from user                     
         ST    R15,INSRC                                                        
         VRETURN STRING,(R1),(R0)  Return input string                          
         TPOP  JUNK                                                             
*-                                                                              
*-       Set IORESULT variable to input return code.                            
*-                                                                              
         SETMSG 'OK'                                                            
*                                                                               
         IF    (INSRC,EQ,1),BEGIN  Attn...                                      
         IF    INSFATTN,BEGIN      Attn's are allowed...                        
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),'CLEAR JCBFATTN,JCBFPATN'                               
         END                                                                    
*                                                                               
         SETMSG 'ATTN'                                                          
         END                                                                    
*                                                                               
         IF    (INSRC,EQ,2),CVABORT  ?-Attn...  force EXEC BREAK                
*                                                                               
         IF    (INSRC,EQ,101),BEGIN  Reverse break...                           
         SETMSG 'TIMEOUT'           (Can only happen because TIMEOUT)           
         END                                                                    
*                                                                               
         LA    R2,=CL16'SYS.IORESULT'                                           
         VCALL SAVEDATA            Save I/O return code in IORESULT             
*                                                                               
         CLEAR R15                                                              
*                                                                               
*                                                                               
INPEXIT  CLEAR CPFCASE,CPFUPPER                                                 
         IF    INSFCASE,'SET CPFCASE'                                           
         IF    INSFUPPER,'SET CPFUPPER'                                         
         MVC   CPTRM,INSTRTRM      Restore terminal options                     
         SCPOP ,                                                                
         PEND                                                                   
***                                                                             
***  INPUT function options.                                                    
***                                                                             
INPPRT   SCKW  UPPER,INPUPPER,A                                                 
         SCKW  UPLOW,INPUPLOW,A                                                 
         SCKW  ATTN,INPATTN,A                                                   
         SCKW  INTERRUPT,INPINT,A                                               
         SCKW  CLEAN,INPCLEAN,A                                                 
         SCKW  ECHO,INPECHO,A                                                   
         SCKW  NOECHO,INPNECHO,A                                                
         SCKW  BUFFERED,INPBUF,A                                                
         SCKW  NOBUFFERED,INPNBUF,A                                             
         SCKW  DATTN,INPDATN,A                                                  
         SCKW  NODATTN,INPNDATN,A                                               
         SCKW  STARS,INPSTAR,A                                                  
         SCKW  NOSTARS,INPNSTAR,A                                               
         SCKW  LF,INPLF,A                                                       
         SCKW  NOLF,INPNOLF,A                                                   
         SCKW  TYPEAHEAD,INPTYP,A                                               
         SCKW  NOTYPEAHEAD,INPNTYP,A                                            
         SCKW  SLOWLIST,INPSLOW,A                                               
         SCKW  FORCE,INPFORCE,A                                                 
         SCKW  TRANSPARENT,INPTRAN,A                                            
         SCKW  NOMODEL,INPNMOD,A                                                
         SCKW  UNFORMATTED,INPUFMT,A                                            
         SCKW  TIMEOUT,INPTMOUT,(A,P,PI),100000                                 
         SCKW  MAXIMUM,INPMAX,(A,P,PI),256  *MAXSTRLN                           
         SCKW  RUNDOWN,INPRUND,(A,P,PI),256 *MAXSTRLN                           
         SCKW  ,INPSTERR                                                        
*                                                                               
INPUPPER PROC  ,                                                                
         SET   CPFCASE,CPFUPPER                                                 
         PEND                                                                   
*                                                                               
INPUPLOW PROC  ,                                                                
         SET   CPFCASE                                                          
         CLEAR CPFUPPER                                                         
         PEND                                                                   
*                                                                               
INPATTN  PROC  ,                                                                
         SET   INSFATTN                                                         
         PEND                                                                   
*                                                                               
INPINT   PROC  ,                                                                
         SET   CPFTINT                                                          
         PEND                                                                   
*                                                                               
INPCLEAN PROC  ,                                                                
         SET   CPFCLEAN                                                         
         PEND                                                                   
*                                                                               
INPECHO  PROC  ,                                                                
         CLEAR CPFNECHO                                                         
         PEND                                                                   
*                                                                               
INPNECHO PROC  ,                                                                
         SET   CPFNECHO                                                         
         PEND                                                                   
*                                                                               
INPBUF   PROC  ,                                                                
         SET   CPFBUFIO                                                         
         PEND                                                                   
*                                                                               
INPNBUF  PROC  ,                                                                
         CLEAR CPFBUFIO                                                         
         PEND                                                                   
*                                                                               
INPTYP   PROC  ,                                                                
         CLEAR CPFNTYPE                                                         
         PEND                                                                   
*                                                                               
INPNTYP  PROC  ,                                                                
         SET   CPFNTYPE                                                         
         PEND                                                                   
*                                                                               
INPSTAR  PROC  ,                                                                
         CLEAR CPFNSTAR                                                         
         PEND                                                                   
*                                                                               
INPNSTAR PROC  ,                                                                
         SET   CPFNSTAR                                                         
         PEND                                                                   
*                                                                               
INPDATN  PROC  ,                                                                
         SET   CPFDATTN                                                         
         PEND                                                                   
*                                                                               
INPNDATN PROC  ,                                                                
         CLEAR CPFDATTN                                                         
         PEND                                                                   
*                                                                               
INPLF    PROC  ,                                                                
         CLEAR CPFNLF                                                           
         PEND                                                                   
*                                                                               
INPNOLF  PROC  ,                                                                
         SET   CPFNLF                                                           
         PEND                                                                   
*                                                                               
INPTRAN  PROC  ,                                                                
         SET   CPFTRAN                                                          
         PEND                                                                   
*                                                                               
INPNMOD  PROC  ,                                                                
         SET   CPFNMOD                                                          
         PEND                                                                   
*                                                                               
INPUFMT  PROC  ,                                                                
         SET   CPFUFMT                                                          
         PEND                                                                   
*                                                                               
INPFORCE PROC  ,                                                                
         SET   CPFFORCE                                                         
         PEND                                                                   
*                                                                               
INPTMOUT PROC  ,                                                                
         MH    R0,=H'100'          Times 100 for value in .01 sec               
         ST    R0,INSTMVAL                                                      
         PEND                                                                   
*                                                                               
INPMAX   PROC  ,                                                                
         STH   R0,CPTRMIN                                                       
         PEND                                                                   
*                                                                               
INPSLOW  PROC  ,                                                                
         SET   CPFSLOW                                                          
         PEND                                                                   
*                                                                               
INPRUND  PROC  ,                                                                
         SET   CPFRUND                                                          
         CEIL  R0,256                                                           
         STH   R0,CPTRMIN                                                       
         PEND                                                                   
*                                                                               
INPSTERR PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  INSERT Function.                                                             
*                                                                               
*    INSERT(??????????)                                                         
*                                                                               
FNINSERT PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  INT - TRUNCATE NUMBER TO INTEGER                                             
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNINT    PROC  ,                                                                
         SETARGS '11I'             1 ARG: NUMBER, INTEGER                       
         COMMENT                   THE SETARG ROUTINE ABOVE DOES THE            
         COMMENT                   WORK, CONVERTING THE INPUT ARG TO            
         COMMENT                   A INTEGER,,, WHICH WE RETURN                 
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          R3 - INTGER VALUE                            
         DROP  R5                                                               
         VRETURN INTEGER,(R3)                                                   
         VCALL ZAPINTR6            ** ZAP INTEGER (**OLD N0-N9)                 
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  JUSTIFY Function.                                                            
*                                                                               
*    JUSTIFY(??????????)                                                        
*                                                                               
FNJUST   PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LEFT -- LEFT Function.                                                       
*                                                                               
*    LEFT(string,length,pad-char)                                               
*                                                                               
FNLEFT   PROC  ,                                                                
         SETARGS '23SIS'           2-3 args: string,integer,string              
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - string loc, len                      
         LH    R3,VNSTRLEN                                                      
         ICM   R3,8,=C' '          Default pad character                        
*-                                                                              
*-       Arg#2:  Length.                                                        
*-                                                                              
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          R4 - Length                                  
         IF    ((R4,NEG),OR,(R4,GT,MAXSTRLN)),BEGIN                             
         BADARG 2                                                               
         EXIT  FNLEFT                                                           
         END                                                                    
*-                                                                              
*-       Arg#3:  Pad char string.                                               
*-                                                                              
         IF    (R0,GE,3),BEGIN     Process arg#3...                             
         LA    R5,ARG3(R1)                                                      
         IF    (VNSTRLEN,GT,1),BEGIN                                            
         TSEG  'Expecting one character pad string.  '                          
         BADARG 3                                                               
         EXIT  FNLEFT                                                           
         END                                                                    
*                                                                               
         IF    (VNSTRLEN,EQ,1),BEGIN                                            
         ICM   R3,8,VNSTRLOC       Pick up pad character                        
         END                                                                    
         END                                                                    
*-                                                                              
*-       Move in LEFT most characters.                                          
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
         STH   R4,VNSTRLEN         Set string length                            
         LA    R14,VNSTRLOC                                                     
         LR    R15,R4                                                           
         MVCL  R14,R2              Move string (with pad)                       
         CLEAR R15                 A-ok                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LINE Function.                                                               
*                                                                               
*    LINE(line-number [,active-number])                                         
*                                                                               
FNLINE   PROC  ,                                                                
         SETARGS '12WI'            1-2 args: lineno, integer                    
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1:  Line-number.                                                   
*-                                                                              
         LA    R5,ARG1(R1)                                                      
         L     R15,VNVALUE         Get lineno                                   
         IF    ((R15,NEG),OR,(R15,GT,CVMAXLNO)),BEGIN                           
         TSEG  'Line-number out of bounds.'                                     
         BADARG 1                                                               
         EXIT  FNLINE                                                           
         END                                                                    
*-                                                                              
*-       Arg 2:  Active file number (0=default).                                
*-                                                                              
         CLEAR R2                  Assume default Active                        
         IF    (R0,GE,2),BEGIN     Actno specified...                           
         LA    R5,ARG2(R1)                                                      
         L     R2,VNVALUE          Get active number                            
         END                                                                    
*-                                                                              
*-       Now get the line contents.                                             
*-                                                                              
         WITH  (VNENTRY,R6)                                                     
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
*                                                                               
         SETMSG VNSTRLOC,&LINESZ    Put line here                               
         VCALL GETLINEX            Get line contents                            
         IF    Z,BEGIN             Got the line...                              
         IF    (R0,Z),BEGIN        Null line...                                 
         MVI   @R1,C' '            Change to one blank                          
         LA    R0,1                                                             
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  'CLEAR R0'                                                       
         STH   R0,VNSTRLEN         Save line length                             
         CLEAR R15                 A-ok                                         
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LINE_APPEND function.                                                        
*                                                                               
*  LINE_APPEND ( text-string [, activeno])                                      
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF MODULE                                              
*                                                                               
FNLNAPND PROC  ,                                                                
         SETARGS '12SI'            1-2 args: string, integer                    
         IF    Z,BEGIN             OK...                                        
         USING VNENTRY,R5                                                       
*                                                                               
         COMMENT                   GET ARG2: ACTIVE FILE NUMBER                 
         COMMENT ,                       (DEFAULT IS ZERO)                      
         CLEAR R3                                                               
         IF    (R0,GE,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE                                                       
         END                                                                    
*                                                                               
         COMMENT                   GET ARG1: TEXT                               
         LA    R5,ARG1(R1)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN                                            
         DROP  R5                                                               
*                                                                               
         COMMENT                   ADD THE LINE                                 
         COMMENT                                                                
         COMMENT                   R0,R1 - TEXT LOC, LENGTH                     
         COMMENT                   R3 - ACTIVE FILE NUMBER                      
         VCALL PTAPPEND  ,                                                      
         IF    Z,BEGIN                                                          
         VRETURN STRING,'OK'                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LINE_DELETE Function.                                                        
*                                                                               
*    LINE_DELETE(first-lineno [,last-lineno] [,activeno])                       
*                                                                               
FNLNDEL  PROC  ,                                                                
         SETARGS '13WWI'           1-3 args: lineno, lineno, integer            
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1:  First line-number.                                             
*-                                                                              
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          Get lineno                                   
         IF    ((R3,NEG),OR,(R3,GT,CVMAXLNO)),BEGIN                             
         TSEG  'Line-number out of bounds.'                                     
         BADARG 1                                                               
         EXIT  FNLNDEL                                                          
         END                                                                    
*-                                                                              
*-       Arg 2:  Last line-number.                                              
*-                                                                              
         LR    R4,R3                                                            
         IF    (R0,GE,2),BEGIN     Second Arg specified...                      
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          Get lineno                                   
         IF    ((R4,NEG),OR,(R4,GT,CVMAXLNO)),BEGIN                             
         TSEG  'Line-number out of bounds.'                                     
         BADARG 2                                                               
         EXIT  FNLNDEL                                                          
         END                                                                    
*                                                                               
         IF    (R3,GT,R4),BEGIN    Bad range...                                 
         TSEG  'First line-number is greater than last line-number.'            
         BADARG 2                                                               
         EXIT  FNLNDEL                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Arg 3:  Active file number (0=default).                                
*-                                                                              
         CLEAR R2                  Assume default Active                        
         IF    (R0,GE,3),BEGIN     Actno specified...                           
         LA    R5,ARG3(R1)                                                      
         L     R2,VNVALUE          Get active number                            
         END                                                                    
*-                                                                              
*-       Now delete the lines.                                                  
*-                                                                              
         IF    ('LTR R15,R2',NZ),BEGIN  Switch Actives...                       
         VCALL ACTPICK                                                          
         IF    Z,'SETMSG "NO SUCH ACTIVE"; B FNLNDM'                            
         LR    R2,R15                                                           
         END                                                                    
*                                                                               
         LR    R0,R3               First lineno                                 
         LR    R1,R4               Last lineno                                  
         VCALL DELRANGE            Delete lines                                 
         IF    Z,'SETMSG "OK"'                                                  
         ELSE  'SETMSG "NO LINES"'                                              
*                                                                               
         XPUSH R0,R1                                                            
         LTR   R15,R2                                                           
         IF    NZ,'VCALL ACTPICK'  Switch back                                  
         XPOP  R0,R1                                                            
*                                                                               
FNLNDM   VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LINE_PUT Function.                                                           
*                                                                               
*  LINE_PUT(lineno, text-string [, activeno])                                   
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNPUT  PROC  ,                                                                
         SETARGS '23WSI'           2-3 args: lineno, string, integer            
         IF    Z,BEGIN             OK...                                        
         USING VNENTRY,R5                                                       
*                                                                               
         COMMENT                   ARG1: LINE NUMBER                            
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNVALUE                                                       
*                                                                               
         COMMENT                   GET ARG3: ACTIVE FILE NUMBER                 
         COMMENT ,                       (DEFAULT IS ZERO)                      
         CLEAR R3                                                               
         IF    (R0,GE,3),BEGIN                                                  
         LA    R5,ARG3(R1)                                                      
         L     R3,VNVALUE                                                       
         END                                                                    
*                                                                               
         COMMENT                   GET ARG2: TEXT                               
         LA    R5,ARG2(R1)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN                                            
         DROP  R5                                                               
*                                                                               
         COMMENT                   ADD THE LINE                                 
         COMMENT                                                                
         COMMENT                   R0,R1 - TEXT LOC, LENGTH                     
         COMMENT                   @R2 - LINE NUMBER                            
         COMMENT                   R3 - ACTIVE FILE NUMBER                      
         VCALL PTLINE                                                           
         IF    Z,BEGIN                                                          
         VRETURN STRING,'OK'                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO - TRUNCATE NUMBER TO LINE NUMBER                                         
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNO    PROC  ,                                                                
         SETARGS '11W'             1 ARG: NUMBER, WYLBUR LINENO                 
         COMMENT                   THE SETARG ROUTINE ABOVE DOES THE            
         COMMENT                   WORK, CONVERTING THE INPUT ARG TO            
         COMMENT                   A LINENO,,, WHICH WE RETURN                  
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          R3 - LINENO VALUE                            
         DROP  R5                                                               
         VRETURN LINENO,(R3)                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LINE_CHECK ( LINENO [, ACTIVENO] )                                           
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNCHK  PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '12WI'                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - LINE NUMBER                             
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT  FNLNCHK                                                          
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   CHECK FOR LINE NUMBER                        
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL CHECKLN                                                          
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT FOUND, RETURN -1.000                  
         VRETURN LINENO,L:=F'-1000'                                             
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_END ( [ACTIVENO] )                                                       
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNEND  PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '01I'                                                          
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         IF    (R0,EQ,1),BEGIN                                                  
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT   FNLNEND                                                         
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   GET FIRST LINENO IN RANGE                    
         LR    R1,R3                                                            
         VCALL GTENDLN                                                          
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_FIELD - FORMAT LINE NUMBER IN FIXED FIELD                                
*                                                                               
*  LNO_FIELD(LINENO)                                                            
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
*                                                                               
FNLNFLD  PROC  ,                                                                
         SETARGS '11W'             1 ARG: WYLBUR LINE NUMBER                    
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   Check line number                            
         IF    ((R2,NEG),OR,(R2,GT,CVMAXLNO)),BEGIN                             
         TSEG  'Invalid line number (negative or too large)'                    
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   GET ARG 1,                                   
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R0,VNVALUE          R0 - LINE NUMBER                             
         DROP  R5                                                               
*                                                                               
         COMMENT                   CHECK LINE NUMBER                            
         IF    ((R0,NEG),OR,(R0,GT,CVMAXLNO)),BEGIN                             
         TSEG  'Invalid line number (negative or too large)'                    
         VRETURN STRING,'?????.???'                                             
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         COMMENT                   IF VALID LINE NO, FORMAT IT                  
         ELSE  BEGIN                                                            
         VCALL CVEXNO              FORMAT LINE NUMBER                           
         VRETURN STRING,CPDOUB,9   RETURN FORMATTED LINE NUMBER                 
         END , OF VALID LINE NO                                                 
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_FIRST ( LINENO [, ACTIVENO] )                                            
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNFRST PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '12WI'                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - LINE NUMBER                             
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT  FNLNFRST                                                         
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   GET FIRST LINENO IN RANGE                    
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL GTFRSTLN                                                         
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT FOUND, RETURN -1.000                  
         VRETURN LINENO,L:=F'-1000'                                             
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_LAST  ( LINENO [, ACTIVENO] )                                            
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNLAST PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '12WI'                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - LINE NUMBER                             
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT  FNLNLAST                                                         
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   GET LAST LINENO IN RANGE                     
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL GTLASTLN                                                         
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT FOUND, RETURN -1.000                  
         VRETURN LINENO,L:=F'-1000'                                             
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_NEXT ( LINENO [, ACTIVENO] )                                             
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNNEXT PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '12WI'                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - LINE NUMBER                             
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT  FNLNNEXT                                                         
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   GET NEXT LINE NUMBER IN RANGE                
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL GTNEXTLN                                                         
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT FOUND, RETURN -1.000                  
         VRETURN LINENO,L:=F'-1000'                                             
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  LNO_PREV ( LINENO [, ACTIVENO] )                                             
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLNPREV PROC  ,                                                                
*                                                                               
         COMMENT                   CHECK ARGS                                   
         SETARGS '12WI'                                                         
         IF    Z,BEGIN                                                          
*                                                                               
         COMMENT                   GET ARGS                                     
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - LINE NUMBER                             
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - ACTIVE FILE NUMBER, IF ANY              
         IF    (R3,NP),BEGIN                                                    
         BADARG 2                                                               
         EXIT  FNLNPREV                                                         
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R3                                                               
         END                                                                    
         DROP  R5                                                               
*                                                                               
         COMMENT                   GET PREVIOUS LINENO IN RANGE                 
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL GTPREVLN                                                         
         IF    Z,BEGIN             IF LINENO EXISTS, RETURN IT                  
         VRETURN LINENO,(R0)                                                    
         END                                                                    
         ELSE  BEGIN               IF NOT FOUND, RETURN -1.000                  
         VRETURN LINENO,L:=F'-1000'                                             
         END                                                                    
*                                                                               
         END , OF GOOD ARGS                                                     
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LOWER                                                                        
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNLOWER  PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVLOWTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  LASTPOS Function.                                                            
*                                                                               
*    LASTPOS(??????????)                                                        
*                                                                               
FNLPOS   PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MATCH -- MATCH function.                                                     
*                                                                               
*    Return true if string in arg2 matches the first part                       
*    of the string in arg1.                                                     
*                                                                               
MATWA    RECORD BEGIN                                                           
MATFLAG  FLAG                                                                   
         FLAG  MATFCASE            - Do case independent match                  
         FLAG  MATFSTRIP           - Strip any trailing blanks                  
         END                                                                    
*-                                                                              
FNMATCH  PROC  MATWA                                                            
         CLEAR MATWA                                                            
*                                                                               
         SETARGS '23SSS'           2-3 string args                              
         IF    NZ,EXIT             Return if error                              
*-                                                                              
*-       Scan arg3 for special options.                                         
*-                                                                              
         IF    (R0,GT,2),BEGIN     Check options...                             
         LA    R5,ARG3(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         LA    R3,VNSTRLOC                                                      
         LH    R2,VNSTRLEN                                                      
         WHILE (R2,POS),BEGIN      Scan options...                              
         IF    ((@R3,EQ,'C'),OR,(@R3,EQ,'c')),'SET MATFCASE'                    
         ELSEIF ((@R3,EQ,'B'),OR,(@R3,EQ,'b')),'SET MATFSTRIP'                  
         ELSE  BEGIN                                                            
         TSEG  @R3,1                                                            
         TSEG  ': bad MATCH option character.',,CR                              
         BADARG 3                                                               
         EXIT  FNMATCH                                                          
         END                                                                    
*                                                                               
         LA    R3,@R3+1                                                         
         DECR  R2                                                               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Set up for comparison.                                                 
*-                                                                              
         LA    R2,ARG1+VNSTRLOC-VNENTRY(R1)  Arg 1 address                      
         LH    R3,ARG1+VNSTRLEN-VNENTRY(R1)  Arg 1 length                       
         IF    MATFSTRIP,BEGIN     Strip leading blanks...                      
         WHILE ((R3,POS),AND,(@R2,EQ,' ')),'LA R2,@R2+1; DECR R3'               
         END                                                                    
*                                                                               
         LA    R4,ARG2+VNSTRLOC-VNENTRY(R1)  Arg 2 address                      
         LH    R5,ARG2+VNSTRLEN-VNENTRY(R1)  Arg 2 length                       
         IF    MATFSTRIP,BEGIN     Strip leading blanks...                      
         WHILE ((R5,POS),AND,(@R4,EQ,' ')),'LA R4,@R4+1; DECR R5'               
         END                                                                    
*                                                                               
         CLEAR R15                 Init result to false                         
*                                                                               
         IF    ((R5,P),AND,(R5,LE,R3)),BEGIN                                    
         DECR  R5                  Get length-1                                 
*                                                                               
         IF    MATFCASE,BEGIN      We don't care about case...                  
         L     R1,CVLOWTBL         Translate table                              
         EX    R5,'TR @R2(0),@R1'     Lower case arg 1                          
         EX    R5,'TR @R4(0),@R1'     Lower case arg 2                          
         END                                                                    
*-                                                                              
*-       Do the comparison.                                                     
*-                                                                              
         EX    R5,'CLC @R2(0),@R4'    Compare strings                           
         IF    EQ,'INCR R15'       If equal, set true return                    
         END                                                                    
*                                                                               
         IF    (R15,Z),'VRETURN BOOLEAN,FALSE'                                  
         ELSE  'VRETURN BOOLEAN,TRUE'                                           
*                                                                               
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MAX -- MAX Function.                                                         
*                                                                               
*    MAX(number[,number]...)                                                    
*                                                                               
FNMAX    PROC  ,                                                                
         SETARGS '19NNNNNNNNN'     1-9 args: numbers                            
         IF    Z,BEGIN                                                          
         WITH  (VNENTRY,R6)                                                     
         CLEAR VNENTRY                                                          
         SET   VNNUMB                                                           
         MVC   VNVALUE(12),=X'99999999999999999999999D'  Pre-set                
*-                                                                              
*-       Find highest number and return that.                                   
*-                                                                              
         LA    R5,ARG1(R1)                                                      
         WHILE (R0,POS),BEGIN      Find highest value...                        
         CP    VNVALUE-VNENTRY(12,R5),VNVALUE  Highest so far?                  
         IF    GT,BEGIN            Yes, save it...                              
         MVC   VNVALUE,VNVALUE-VNENTRY(R5)                                      
         END                                                                    
*                                                                               
         LA    R5,@R5+L'VPARM      Next arg                                     
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  MIN -- MIN Function.                                                         
*                                                                               
*    MIN(number[,number]...)                                                    
*                                                                               
FNMIN    PROC  ,                                                                
         SETARGS '19NNNNNNNNN'     1-9 args: numbers                            
         IF    Z,BEGIN                                                          
         WITH  (VNENTRY,R6)                                                     
         CLEAR VNENTRY                                                          
         SET   VNNUMB                                                           
         MVC   VNVALUE(12),=X'99999999999999999999999C'  Preset                 
*-                                                                              
*-       Find lowest number and return that.                                    
*-                                                                              
         LA    R5,ARG1(R1)                                                      
         WHILE (R0,POS),BEGIN      Find lowest value...                         
         CP    VNVALUE-VNENTRY(12,R5),VNVALUE  Lowest so far?                   
         IF    LT,BEGIN            Yes, save it...                              
         MVC   VNVALUE,VNVALUE-VNENTRY(R5)                                      
         END                                                                    
*                                                                               
         LA    R5,@R5+L'VPARM      Next arg                                     
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  NCONVERT - CONVERT TO NUMBER, WITH INTEGER FORMAT                            
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNNCONV  PROC  ,                                                                
         SETARGS '11I'             1 ARG: NUMBER, INTEGER                       
         COMMENT                   THE SETARG ROUTINE ABOVE DOES THE            
         COMMENT                   WORK, CONVERTING THE INPUT ARG TO            
         COMMENT                   A INTEGER,,, WHICH WE RETURN                 
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          R3 - INTGER VALUE                            
         DROP  R5                                                               
         VRETURN INTEGER,(R3)                                                   
         VCALL ZAPINTR6            ** ZAP INTEGER (**OLD N0-N9)                 
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  NDUMP - DUMP NUMBER AT MEMORY LOC, LEN                                       
*                                                                               
*  BOTH ARGUMENTS ARE INTEGER NUMBERS, 2ND ARG IS OPTIONAL (DFT=4)              
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNNDUMP  PROC  ,                                                                
         SETARGS '12II'            1-2 ARGS: INT, INT                           
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST ARG = MEMORY LOCATION               
         XPUSH R0,R1                                                            
         LR    R1,R2                                                            
         LA    R0,4                CHECK ADDRESS VALIDITY FOR READ              
         VCALL CHKRADDR            ROUTINE IS IN DEBUG MODULE                   
         XPOP  R0,R1                                                            
         IF    (R15,NZ),BEGIN                                                   
         BADARG 1                                                               
         EXIT  FNNDUMP                                                          
         END                                                                    
         COMMENT                   R3 - 2ND ARG = MEMORY LENGTH                 
         COMMENT                                                                
         COMMENT                   MEMORY LEN = 1,2,3 OR 4                      
         COMMENT                   1 RETURNS 1 BYTE NUM VAL AT ADDR             
         COMMENT                   2 RETURNS 2 BYTE NUM VAL AT ADDR             
         COMMENT                   ETC, ETC.                                    
         IF    (R0,EQ,1),BEGIN                                                  
         LA    R3,4                DEFAULT IS 4  (R0 IS ARG COUNT)              
         END                                                                    
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - MEM LEN                                 
         IF    ((R3,NEG),OR,(R3,GT,4)),BEGIN                                    
         BADARG 2                                                               
         EXIT  FNNDUMP                                                          
         END                                                                    
         END                                                                    
         L     R2,0(R2)            LOAD VALUE, AND SHIFT IT                     
         WHILE (R3,LT,4),BEGIN                                                  
         SRL   R2,8                                                             
         LA    R3,1(R3)                                                         
         END                                                                    
         VRETURN INTEGER,(R2)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NHEX - CONVERT HEX STRING TO INTEGER FORMAT NUMBER                           
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNNHWA   RECORD BEGIN                                                           
         XSA   R0,R8                                                            
FNNHTEMP DS    CL8                                                              
         END                                                                    
*-                                                                              
FNNHEX   PROC  FNNHWA                                                           
         USING VNENTRY,R5                                                       
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         LH    R0,VNSTRLEN                                                      
         IF    (R0,GT,8),BEGIN                                                  
         TSEG  'Arithmetic overflow.  '                                         
         BADARG 1                                                               
         EXIT  FNNHEX                                                           
         END                                                                    
         LA    R1,VNSTRLOC                                                      
         LA    R2,FNNHTEMP                                                      
         VCALL MAKEHEX             CONVERT HEX STRING TO HEX                    
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid hex string.  '                                          
         BADARG 1                                                               
         EXIT  FNNHEX                                                           
         END                                                                    
         L     R1,FNNHTEMP         R1 - HEX VALUE                               
         WHILE (R0,LT,4),BEGIN     PROPERLY SHIFTED                             
         SRL   R1,8                                                             
         A     R0,=F'1'                                                         
         END                                                                    
         VRETURN INTEGER,(R1) ,    RETURN INTEGER                               
         VCALL ZAPINTR6            ** ZAP INTEGER (**OLD N0-N9)                 
         END                                                                    
         PEND                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  NOPARITY                                                                     
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNNOPAR  PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVNPRTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVYL_CPU_ function.                                                         
*                                                                               
FNGORVC  PROC  ,                                                                
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Component name specified                  
         LA    R15,=CL16'ORVYL_CPU'                                             
         VCALL GETCVAR                                                          
         VRETURN LINENO,(R15)      Return ORVYL cpu in .001 sec                 
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ORVYL_IO_ function.                                                          
*                                                                               
FNGORVI  PROC  ,                                                                
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Component name specified                  
         LA    R15,=CL16'ORVYL_IO'                                              
         VCALL GETCVAR                                                          
         VRETURN INTEGER,(R15)     Return ORVYL I/Os                            
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  OVERLAY Function.                                                            
*                                                                               
*    OVERLAY(??????????)                                                        
*                                                                               
FNOLAY   PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  PARITY                                                                       
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNPARITY PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVPARTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  POS Function.                                                                
*                                                                               
*    POS(??????????)                                                            
*                                                                               
FNPOS    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QSTATE Function.                                                             
*                                                                               
*  QSTATE(server-number) -- returns state of query server.                      
*                                                                               
FNQSTATE PROC                                                                   
         SETARGS '11I'             1 arg: integer                               
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         L     R15,VNVALUE         Get server number                            
         VCALL GETQSTAT            Get server's state                           
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  QUERY Function.                                                              
*                                                                               
*  QUERY(server, command) -- returns the first row of results or the            
*                            null string if not available.                      
*                                                                               
QFWA     RECORD BEGIN                                                           
QFRETSTR DS    CL(MAXSTRLN)        Returned variable buffer                     
         END                                                                    
*-                                                                              
FNQUERY  PROC  QFWA                                                             
*                                                                               
         SETARGS '22SS'            2 args: string, string                       
         IF    Z,BEGIN             OK...                                        
*                                                                               
         WITH  (VNENTRY,R5),BEGIN                                               
         LA    R5,ARG1(R1)                                                      
         LA    R3,VNSTRLOC         R3,R2 - 1st string loc,len                   
         LH    R2,VNSTRLEN                                                      
*                                                                               
         LA    R5,ARG2(R1)                                                      
         LA    R1,VNSTRLOC         R1,R0 - 2nd string loc,len                   
         LH    R0,VNSTRLEN                                                      
         VCALL LRTRIM                                                           
         END                                                                    
*                                                                               
         LA    R5,QFRETSTR         R5,R4 - Response buffer                      
         LA    R4,L'QFRETSTR                                                    
*-                                                                              
*-       Send query to server and get the first response line.                  
*-                                                                              
         VCALL QUERYFC             Query function call                          
         IF    Z,BEGIN             Normal completion...                         
         IF    ((R0,POS),AND,(@R1,EQ,X'05')),'LA R1,@R1+1; DECR R0'             
         CLEAR R2,R3               No error message                             
         END                                                                    
         ELSE  'LR R2,R0; LR R3,R1; CLEAR R0,R1'  Error completion              
*-                                                                              
*-       Return first response line (or null if error).                         
*-                                                                              
         VRETURN STRING,(R1),(R0)  Return line of data                          
*-                                                                              
*-       Save any error information in the QUERY_ERRINFO variable.              
*-                                                                              
         SETMSG (R3),(R2)           Error info (or null string)                 
         LA    R2,=CL16'SYS.QUERY_ERRINFO'                                      
         VCALL SAVEDATA            Set variable                                 
*                                                                               
         CLEAR R15                 A-ok                                         
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
RECWA    RECORD BEGIN                                                           
RECFLAG  FLAG                                                                   
         FLAG  RECFPUB             - Public record group wanted                 
*                                                                               
RECNARGS DS    F                   Number of arguments given                    
RECDRG   DS    A                   Record group ptr                             
*                                                                               
RECKEY   DS    CL(CPDATANL)        Maximum key length                           
RECKPFX  EQU   RECKEY,1,C'X'               .our prefix                          
RECKDATA EQU   RECKEY+1,L'RECKEY-1,C'X'    .real key                            
*                                                                               
RECKEY2  DS    CL(CPDATANL)        Maximum key length                           
RECKPFX2 EQU   RECKEY2,1,C'X'              .our prefix                          
RECKDAT2 EQU   RECKEY2+1,L'RECKEY2-1,C'X'  .real key                            
*                                                                               
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  REC_CLOSE -- REC_CLOSE Function.                                             
*                                                                               
*    REC_CLOSE(setno)                                                           
*                                                                               
FNRECCLS PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '11I'             1 arg: number                                
         IF    Z,BEGIN                                                          
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          Record set number                            
         IF    (R3,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECCLS                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R3,NEG),'LCR R3,R3; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         LR    R0,R3                                                            
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET                                                          
*                                                                               
         L     R15,RECDRG          Data record group                            
         ACALL RECOPCHK            Make sure record set is open                 
         IF    Z,BEGIN                                                          
         LR    R0,R3                                                            
         L     R15,RECDRG          Data record group                            
         VCALL XDELSET             Delete all entries in this set               
         SETMSG 'OK'                                                            
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)  A-ok                                         
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_COUNT -- REC_COUNT Function.                                             
*                                                                               
*    REC_COUNT(setno, [startkey], [endkey])                                     
*                                                                               
FNRECCNT PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '13ISS'           1-3 args: num, str, str                      
         IF    Z,BEGIN             OK...                                        
         ST    R0,RECNARGS         Save number of args                          
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg 1: record set number.                                              
*-                                                                              
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECCNT                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*-                                                                              
*-       Arg 2: starting key.                                                   
*-                                                                              
         MVI   RECKPFX,C' '                                                     
         CLEAR RECKDATA            First possible key                           
*                                                                               
         IF    (RECNARGS,GE,2),BEGIN                                            
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECCNT                                                         
         END                                                                    
         MVC   RECKDATA,CVBLANKS   Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
         END                                                                    
*-                                                                              
*-       Arg 3: ending key.                                                     
*-                                                                              
         MVI   RECKPFX2,C' '                                                    
         MVC   RECKDAT2,=(L'RECKDAT2)X'FF'  Last possible key                   
*                                                                               
         IF    (RECNARGS,GE,3),BEGIN                                            
         LA    R5,ARG3(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDAT2),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 3                                                               
         EXIT  FNRECCNT                                                         
         END                                                                    
         MVC   RECKDAT2,CVBLANKS   Pre-blank                                    
         MOVE  R15,RECKDAT2,VNSTRLOC  Move in key data                          
         END                                                                    
*-                                                                              
*-        Count the number of records in the range.                             
*-                                                                              
         LA    R0,RECKEY           First key                                    
         LA    R1,RECKEY2          Last key                                     
         CLEAR R2                  (Not crossing rec set boundry)               
         L     R15,RECDRG          Data record group                            
         VCALL XCNTRECS            Count records                                
         VRETURN INTEGER,(R15)     Return number of records                     
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_DELETE -- REC_DELETE Function.                                           
*                                                                               
*    REC_DELETE(setno,key)                                                      
*                                                                               
FNRECDEL PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECDEL                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         L     R15,RECDRG          Data record group                            
         ACALL RECOPCHK            Make sure record set is open                 
         IF    Z,BEGIN                                                          
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECDEL                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         L     R15,RECDRG          Data record group                            
         LA    R2,RECKEY           Key data (blank padded)                      
         VCALL XDELETE             Delete the record                            
         IF    Z,'SETMSG "OK"'                                                  
         ELSE  'SETMSG "NOT FOUND"'                                             
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_FIRSTKEY -- REC_FIRSTKEY Function.                                       
*                                                                               
*    REC_FIRSTKEY(setno,key)                                                    
*                                                                               
FNRECFRK PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECFRK                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECFRK                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         LA    R2,RECKEY           Key data (blank padded)                      
         L     R15,RECDRG          Data record group                            
         VCALL XLOCFRST            Get record                                   
         IF    Z,BEGIN             We found it...                               
         SETMSG RECKDATA            Return "key"                                
         IF    (RECKEY,Z),'CLEAR R0'  That's internal                           
         END                                                                    
         ELSE  'CLEAR R0'                                                       
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_GET -- REC_GET Function.                                                 
*                                                                               
*    REC_GET(setno,key)                                                         
*                                                                               
FNRECGET PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECGET                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECGET                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*-                                                                              
*-  Get the record data given it's key.  If the record set is record            
*-     doesn't exist then return a null string.  A null string would            
*-    also be returned if a unopened record set number was supplied.            
*-                                                                              
         LA    R2,RECKEY           Key data (blank padded)                      
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
         SETMSG VNSTRLOC,MAXSTRLN   Put string here                             
         L     R15,RECDRG          Data record group                            
         VCALL XGET                Get record data                              
         IF    NZ,'CLEAR R0'       Record not found                             
         STH   R0,VNSTRLEN         Save string length                           
         CLEAR R15                 Function return code                         
         END                                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_LASTKEY -- REC_LASTKEY Function.                                         
*                                                                               
*    REC_LASTKEY(setno,key)                                                     
*                                                                               
FNRECLAK PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECLAK                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECLAK                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         LA    R2,RECKEY           Key data (blank padded)                      
         L     R15,RECDRG          Data record group                            
         VCALL XLOCLAST            Get record                                   
         IF    Z,BEGIN             We found it...                               
         SETMSG RECKDATA            Return "key"                                
         IF    (RECKEY,Z),'CLEAR R0'  That's internal                           
         END                                                                    
         ELSE  'CLEAR R0'                                                       
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_NEXTKEY -- REC_NEXTKEY Function.                                         
*                                                                               
*    REC_NEXTKEY(setno,key)                                                     
*                                                                               
FNRECNEK PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECNEK                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECNEK                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         LA    R2,RECKEY           Key data (blank padded)                      
         L     R15,RECDRG          Data record group                            
         VCALL XLOCNEXT            Get record                                   
         IF    Z,BEGIN             We found it...                               
         SETMSG RECKDATA            Return "key"                                
         IF    (RECKEY,Z),'CLEAR R0'  That's internal                           
         END                                                                    
         ELSE  'CLEAR R0'                                                       
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_OPEN -- REC_OPEN Function.                                               
*                                                                               
*    REC_OPEN(option-string)                                                    
*                                                                               
FNRECOPN PROC  RECWA                                                            
         LA    R4,RECWA                                                         
         USING RECWA,R4                                                         
         CLEAR RECWA                                                            
         SCPUSH ,                                                               
*                                                                               
         SETARGS '11SS'            1 arg: string                                
         IF    Z,BEGIN                                                          
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
*                                                                               
         SCINIT VNSTRLOC,LH:VNSTRLEN  Option string                             
         SCAN  ROPNPRT                                                          
         IF    (R15,NZ),ROPNERR                                                 
*                                                                               
         LA    R15,CPDATA                                                       
         IF    RECFPUB,'LA R15,CVDATA'  Public record group                     
         ST    R15,RECDRG          Assume local record group                    
*-                                                                              
*-       Assign a new set number.                                               
*-                                                                              
         L     R0,=A(X'7FFFFFFF')                                               
         L     R15,RECDRG          Data record group                            
         VCALL XLASTSET            Get highest set number so far                
         IF    NZ,'CLEAR R0'                                                    
         LR    R3,R0                                                            
         A     R3,CVONE            Next set number                              
*                                                                               
         LR    R0,R3               Set number                                   
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET                                                          
*-                                                                              
*-       Add one dummy record which indicates that the record group             
*-          is open.                                                            
*-                                                                              
         CLEAR RECKEY              Key is all binary zeros                      
         SETMSG 'OPENED'                                                        
         LA    R2,RECKEY                                                        
         L     R15,RECDRG          Data record group                            
         VCALL XPUT                Add definition record                        
         IF    (R15,NZ),ROPNERR                                                 
*                                                                               
         IF    RECFPUB,'LNR R3,R3'  Public rg is neg number                     
         VRETURN INTEGER,(R3)      Return the set number                        
         END                                                                    
         B     ROPNEXIT                                                         
*                                                                               
*  EXITS                                                                        
*                                                                               
ROPNERR  CLEAR R3                  Error: return zero                           
         VRETURN INTEGER,(R3)                                                   
         B     ROPNEXIT                                                         
*                                                                               
*                                                                               
ROPNEXIT SCPOP ,                                                                
         PEND                                                                   
*                                                                               
*  SCKW STUFF                                                                   
*                                                                               
ROPNPRT  SCKW  PUBLIC,ROPNPUB,A                                                 
         SCKW  ,ROPNBAD                                                         
*                                                                               
*                                                                               
ROPNPUB  PROC  ,                                                                
         SET   RECFPUB             User wants public record group               
         PEND                                                                   
*                                                                               
ROPNBAD  PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_PREVKEY -- REC_PREVKEY Function.                                         
*                                                                               
*    REC_PREVKEY(setno,key)                                                     
*                                                                               
FNRECPRK PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECPRK                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECPRK                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS     Pre-blank                                    
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         LA    R2,RECKEY           Key data (blank padded)                      
         L     R15,RECDRG          Data record group                            
         VCALL XLOCPREV            Get record                                   
         IF    Z,BEGIN             We found it...                               
         SETMSG RECKDATA            Return "key"                                
         IF    (RECKEY,Z),'CLEAR R0'  That's internal                           
         END                                                                    
         ELSE  'CLEAR R0'                                                       
         VRETURN STRING,(R1),(R0)                                               
         VTRIM                                                                  
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_PUT -- REC_PUT Function.                                                 
*                                                                               
*    REC_PUT(setno,key,data)                                                    
*                                                                               
FNRECPUT PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '33ISS'           3 args: number, string, string               
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECPUT                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECPUT                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS                                                  
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         L     R15,RECDRG          Data record group                            
         ACALL RECOPCHK            Make sure record set is opened               
         IF    NZ,'SETMSG "NOT OPENED"'                                         
         ELSE  BEGIN                                                            
         LA    R5,ARG3(R4)                                                      
         LA    R2,RECKEY           Key data is here                             
         SETMSG VNSTRLOC,LH:VNSTRLEN  Value data is here                        
         L     R15,RECDRG          Data record group                            
         VCALL XPUT                Add record                                   
         IF    Z,'SETMSG "OK"'                                                  
         ELSEIF (R15,NEG),'SETMSG "NO ROOM"'                                    
         ELSE 'SETMSG "ERROR"'                                                  
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REC_TEST -- REC_TEST Function.                                               
*                                                                               
*    REC_TEST(setno,key)                                                        
*                                                                               
FNRECTST PROC  RECWA                                                            
         CLEAR RECWA                                                            
*                                                                               
         SETARGS '22IS'            2 args: number, string                       
         IF    Z,BEGIN             OK...                                        
         LR    R4,R1                                                            
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R4)                                                      
         L     R0,VNVALUE          Record set number                            
         IF    (R0,Z),BEGIN                                                     
         BADARG 1                                                               
         EXIT  FNRECTST                                                         
         END                                                                    
*                                                                               
         LA    R15,CPDATA                                                       
         IF    (R0,NEG),'LCR R0,R0; LA R15,CVDATA'                              
         ST    R15,RECDRG                                                       
*                                                                               
         L     R15,RECDRG          Data record group                            
         VCALL XSETSET             Specified record set                         
*                                                                               
         L     R15,RECDRG                                                       
         ACALL RECOPCHK            Make sure record group is open               
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG2(R4)                                                      
         LH    R15,VNSTRLEN        Key data string length                       
         IF    (R15,GT,L'RECKDATA),BEGIN  Too long...                           
         TSEG  'Key data string is too long.  '                                 
         BADARG 2                                                               
         EXIT  FNRECTST                                                         
         END                                                                    
         MVC   RECKEY,CVBLANKS                                                  
         MOVE  R15,RECKDATA,VNSTRLOC  Move in key data                          
*                                                                               
         LA    R2,RECKEY           Key data (blank padded)                      
         XPUSH ,,4,PTR=R1                                                       
         SETMSG (R1),4              Put a little data here                      
         L     R15,RECDRG          Data record group                            
         VCALL XGET                Get record entry (if it exists)              
         IF    Z,'SETMSG "OK"'                                                  
         ELSE  'SETMSG "NOT FOUND"'                                             
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RECOPCHK -- Local routine to check to see if the current                     
*    record set has been "opened".  If it has been opened then                  
*    it will have a record with a key of all binary zeros in it.                
*                                                                               
*    On entry:                                                                  
*      R15 - Record group ptr (CPDATA or CVDATA)                                
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - it's open; R1,R0 = ""                                                
*      4 - not open; R1,R0 = "NOT OPEN"                                         
*                                                                               
RECOPCHK PROC  ,                                                                
         XPUSH ,,L'RECKEY,PTR=R2                                                
         CLEAR (@R2,L'RECKEY)      Set binary zero record                       
         VCALL XLOCATE             Does our zero record exist?                  
         IF    Z,'CLEAR R1,R0'     -Yes                                         
         ELSE  'SETMSG "NOT OPEN"'    -No                                       
         PRETURN (R0,R1)           Return string too                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  REVERSE -- REVERSE Function.                                                 
*                                                                               
*    REVERSE(string)                                                            
*                                                                               
FNREV    PROC  ,                                                                
         SETARGS '11S'             1 arg: string                                
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - string loc, len                      
         LH    R3,VNSTRLEN                                                      
*-                                                                              
*-       Reverse the string character by character.                             
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
*                                                                               
         STH   R3,VNSTRLEN         Save string length                           
*                                                                               
         AR    R2,R3                                                            
         DECR  R2                  R2 - Last char of arg string                 
*                                                                               
         LA    R4,VNSTRLOC         First char of our string                     
         WHILE (R3,POS),BEGIN                                                   
         MVC   @R4(1),@R2          Move char                                    
         LA    R4,@R4+1                                                         
         DECR  R2                                                               
         DECR  R3                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RIGHT -- RIGHT Function.                                                     
*                                                                               
*    RIGHT(string,length,pad-char)                                              
*                                                                               
RIGHTWA  RECORD BEGIN                                                           
RIGHTPAD DS    C                   Pad character                                
RIGHTPDL DS    H                   Length of area to pad                        
RIGHTLEN DS    H                   Total string length                          
         END                                                                    
*-                                                                              
FNRIGHT  PROC  RIGHTWA                                                          
         CLEAR RIGHTWA                                                          
         MVI   RIGHTPAD,C' '       Default pad character                        
*                                                                               
         SETARGS '23SIS'           2-3 args: string,integer,string              
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - string loc, len                      
         LH    R3,VNSTRLEN                                                      
*-                                                                              
*-       Arg#2:  Length.                                                        
*-                                                                              
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          R4 - Length                                  
         IF    ((R4,NEG),OR,(R4,GT,MAXSTRLN)),BEGIN                             
         BADARG 2                                                               
         EXIT  FNRIGHT                                                          
         END                                                                    
*                                                                               
         IF    (R3,GT,R4),BEGIN    Take right most chars...                     
         SR    R3,R4                                                            
         LA    R2,@R2(R3)          R2,R3 - right most string loc,len            
         LR    R3,R4                                                            
         END                                                                    
*                                                                               
         LR    R14,R4                                                           
         SR    R14,R3              Number of pad chars                          
         STH   R14,RIGHTPDL        Save pad length                              
*-                                                                              
*-       Arg#3:  Pad char string.                                               
*-                                                                              
         IF    (R0,GE,3),BEGIN     Process arg#3...                             
         LA    R5,ARG3(R1)                                                      
         IF    (VNSTRLEN,GT,1),BEGIN                                            
         TSEG  'Expecting one character pad string.  '                          
         BADARG 3                                                               
         EXIT  FNRIGHT                                                          
         END                                                                    
*                                                                               
         IF    (VNSTRLEN,EQ,1),BEGIN                                            
         MVC   RIGHTPAD,VNSTRLOC   Save pad character                           
         END                                                                    
         END                                                                    
*-                                                                              
*-       Move in RIGHT most characters.                                         
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING            Returning a string                           
*                                                                               
         LA    R14,VNSTRLOC                                                     
         LH    R15,RIGHTPDL        Length to pad                                
         CLEAR R1                                                               
         ICM   R1,8,RIGHTPAD       Pad character                                
         MVCL  R14,R0              First add any pad characters                 
*                                                                               
         LR    R15,R3              String length                                
         MVCL  R14,R2              Move string                                  
*                                                                               
         STH   R4,VNSTRLEN         Save string length                           
*                                                                               
         CLEAR R15                 A-ok                                         
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RMATCH -- RMATCH function.                                                   
*                                                                               
*    Return true if string in arg2 matches the right hand part                  
*    of the string in arg1.                                                     
*                                                                               
RMATWA   RECORD BEGIN                                                           
RMATFLAG FLAG                                                                   
         FLAG  RMATFCASE           - Do case independent match                  
         FLAG  RMATFSTRIP          - Strip any trailing blanks                  
         END                                                                    
*-                                                                              
FNRMATCH PROC  RMATWA                                                           
         CLEAR RMATWA                                                           
*                                                                               
         SETARGS '23SSS'           2-3 string args                              
         IF    NZ,EXIT             Return if error                              
*-                                                                              
*-       Scan arg3 for special options.                                         
*-                                                                              
         IF    (R0,GT,2),BEGIN     Check options...                             
         LA    R5,ARG3(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         LA    R3,VNSTRLOC                                                      
         LH    R2,VNSTRLEN                                                      
         WHILE (R2,POS),BEGIN      Scan options...                              
         IF    ((@R3,EQ,'C'),OR,(@R3,EQ,'c')),'SET RMATFCASE'                   
         ELSEIF ((@R3,EQ,'B'),OR,(@R3,EQ,'b')),'SET RMATFSTRIP'                 
         ELSE  BEGIN                                                            
         TSEG  @R3,1                                                            
         TSEG  ': bad RMATCH option character.',,CR                             
         BADARG 3                                                               
         EXIT  FNRMATCH                                                         
         END                                                                    
*                                                                               
         LA    R3,@R3+1                                                         
         DECR  R2                                                               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Set up for comparison.                                                 
*-                                                                              
         LA    R2,ARG1+VNSTRLOC-VNENTRY(R1)  Arg 1 address                      
         LH    R3,ARG1+VNSTRLEN-VNENTRY(R1)  Arg 1 length                       
         IF    RMATFSTRIP,BEGIN    Strip trailing blanks...                     
         LA    R4,@R2(R3)                                                       
         DECR  R4                                                               
         WHILE ((R3,POS),AND,(@R4,EQ,' ')),'DECR R3; DECR R4'                   
         END                                                                    
*                                                                               
         LA    R4,ARG2+VNSTRLOC-VNENTRY(R1)  Arg 2 address                      
         LH    R5,ARG2+VNSTRLEN-VNENTRY(R1)  Arg 2 length                       
         IF    RMATFSTRIP,BEGIN    Strip trailing blanks...                     
         LA    R1,@R4(R5)                                                       
         DECR  R1                                                               
         WHILE ((R5,POS),AND,(@R1,EQ,' ')),'DECR R5; DECR R1'                   
         END                                                                    
*                                                                               
         CLEAR R15                 Init result to false                         
*                                                                               
         IF    ((R5,P),AND,(R5,LE,R3)),BEGIN                                    
         AR    R2,R3               Point to right hand                          
         SR    R2,R5                substring of arg1                           
         DECR  R5                  Get length-1                                 
*                                                                               
         IF    RMATFCASE,BEGIN     We don't care about case...                  
         L     R1,CVLOWTBL         Translate table                              
         EX    R5,'TR @R2(0),@R1'     Lower case arg 1                          
         EX    R5,'TR @R4(0),@R1'     Lower case arg 2                          
         END                                                                    
*-                                                                              
*-       Do the comparison.                                                     
*-                                                                              
         EX    R5,'CLC @R2(0),@R4'    Compare strings                           
         IF    EQ,'INCR R15'       If equal, set true return                    
         END                                                                    
*                                                                               
         IF    (R15,Z),'VRETURN BOOLEAN,FALSE'                                  
         ELSE  'VRETURN BOOLEAN,TRUE'                                           
*                                                                               
         CLEAR R15                 A-ok                                         
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCAN_SKIP                                                                    
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSCANSK PROC  FNWA                                                             
         SCPUSH ,                                                               
         SETARGS '11SS'            1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2, R3 - LOC, LEN OF STRING                  
         LH    R3,VNSTRLEN                                                      
         SCINIT (R2),(R3)                                                       
         SCAN  ,                   SCAN FOR 1ST TOKEN                           
         IF    (R15,NZ),BEGIN                                                   
         CLEAR R1                                                               
         CLEAR R0                                                               
         END                                                                    
         SCINFO                                                                 
         VRETURN STRING,(R1),(R0)                                               
         CLEAR R15                                                              
         END                                                                    
         SCPOP                                                                  
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SCAN_TOKEN                                                                   
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSCANTK PROC  FNWA                                                             
         SCPUSH                                                                 
         SETARGS '11SS'            1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2, R3 - LOC, LEN OF STRING                  
         LH    R3,VNSTRLEN                                                      
         SCINIT (R2),(R3)                                                       
         SCAN  ,                   SCAN FOR 1ST TOKEN                           
         IF    (R15,NZ),BEGIN      Scan errors...                               
         SETMSG (R2),(R3)           Give the remainder of the text              
         END                                                                    
         VRETURN STRING,(R1),(R0)                                               
         CLEAR R15                                                              
         END                                                                    
         SCPOP                                                                  
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SCONVERT - CONVERT TO STRING                                                 
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSCONV  PROC                                                                   
         SETARGS '11S'             1 ARG: STRING                                
         COMMENT                   THE SETARG ROUTINE ABOVE DOES THE            
         COMMENT                   WORK, CONVERTING THE INPUT ARG TO            
         COMMENT                   A STRING,,, WHICH WE RETURN                  
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2 - STRING LOCATION                         
         LH    R3,VNSTRLEN         R3 - STRING LENGTH                           
         DROP  R5                                                               
         VRETURN STRING,(R2),(R3)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SDUMP(MEMORY ADDR, MEMORY LENGTH)                                            
*                                                                               
*  BOTH ARGUMENTS ARE INTEGER NUMBERS, 2ND ARG IS OPTIONAL (DFT=4)              
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSDUMP  PROC  ,                                                                
         SETARGS '12II'            1-2 ARGS: INT, INT                           
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST ARG = MEMORY LOCATION               
         XPUSH R0,R1                                                            
         LR    R1,R2                                                            
         LA    R0,4                CHECK ADDRESS VALIDITY FOR READ              
*        LA    R0,MAXSTRLN         CHECK ADDRESS VALIDITY FOR READ              
         VCALL CHKRADDR            ROUTINE IS IN DEBUG MODULE                   
         XPOP  R0,R1                                                            
         IF    (R15,NZ),BEGIN                                                   
         BADARG 1                                                               
         EXIT  FNSDUMP                                                          
         END                                                                    
         COMMENT                   R3 - 2ND ARG = MEMORY LENGTH                 
         IF    (R0,EQ,1),BEGIN                                                  
         LA    R3,4                DEFAULT IS 4  (R0 IS ARG COUNT)              
         END                                                                    
         IF    (R0,EQ,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - MEM LEN                                 
         IF    ((R3,NEG),OR,(R3,GT,MAXSTRLN)),BEGIN                             
         BADARG 2                                                               
         EXIT  FNSDUMP                                                          
         END                                                                    
         END                                                                    
         VRETURN STRING,(R2),(R3)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SHEX - CONVERT HEX STRING TO STRING                                          
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSHWA   RECORD BEGIN                                                           
FNSHTEMP DS    CL(&LINESZ)                                                      
         END                                                                    
*-                                                                              
FNSHEX   PROC  FNSHWA                                                           
         USING VNENTRY,R5                                                       
         SETARGS '11X'                                                          
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         IF    VNNUMB,BEGIN        IF INTEGER FORMAT NUMBER,,,                  
         CLC   VNVALUE+7(4),=X'00000000'                                        
         IF    NE,BEGIN            IF NOT INTGER, ERROR                         
         TSEG  'Invalid number format. '                                        
         BADARG 1                                                               
         EXIT  FNSHEX                                                           
         END                                                                    
         LR    R2,R5                                                            
         VCALL MAKEINT                CONVERT TO INTEGER,,,                     
         IF    NZ,BEGIN                                                         
         BADARG 1                                                               
         EXIT  FNSHEX                                                           
         END                                                                    
         LA    R1,VNVALUE             PRUNE OFF HIGH ORDER ZEROS                
         LA    R0,4                                                             
         WHILE ((@R1,EQ,X'0'),AND,(R0,GT,1)),BEGIN                              
         LA    R1,1(R1)                                                         
         DECR  R0                                                               
         END                                                                    
         VRETURN STRING,(R1),(R0)     RETURN AS STRING                          
         END                                                                    
         IF    ^VNNUMB,BEGIN       OTHER WISE,  GET HEX STRING ARG              
         LR    R2,R5                                                            
         VCALL MAKESTR                                                          
         IF    NZ,BEGIN                                                         
         BADARG 1                                                               
         EXIT  FNSHEX                                                           
         END                                                                    
         LH    R0,VNSTRLEN                                                      
         LA    R1,VNSTRLOC                                                      
         LA    R2,FNSHTEMP                                                      
         VCALL MAKEHEX                CONVERT TO HEX,, AND SAVE AS              
         COMMENT                      AS STRING                                 
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid hex string.  '                                          
         BADARG 1                                                               
         EXIT  FNSHEX                                                           
         END                                                                    
         VRETURN STRING,(R2),(R0)                                               
         END                                                                    
         END                                                                    
         PEND                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SIGN Function.                                                               
*                                                                               
*    SIGN(??????????)                                                           
*                                                                               
FNSIGN   PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SIZE - GET STRING SIZE                                                       
*                                                                               
*  SIZE(STRING)                                                                 
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNSIZE   PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LH    R3,VNSTRLEN         R3 - STRING LENGTH                           
         DROP  R5                                                               
         VRETURN INTEGER,(R3)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SPACE Function.                                                              
*                                                                               
*    SPACE(??????????)                                                          
*                                                                               
FNSPACE  PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SQUASH -- SQUASH Function.  Compresses multiple blanks to a                  
*    a single blank.                                                            
*                                                                               
*    SQUASH(string)                                                             
*                                                                               
FNSQUASH PROC  ,                                                                
         SETARGS '11SS'            1 arg: string                                
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN                                            
*                                                                               
         VCALL LRTRIM              Zot leading/trailing blanks                  
         VCALL SQSHRTN             Squash multiple blanks                       
*                                                                               
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
STRIPWA  RECORD BEGIN                                                           
STRIPFLG FLAG                                                                   
         FLAG  STRIPFL             - Strip leading chars                        
         FLAG  STRIPFT             - Strip trailing chars                       
*                                                                               
STRIPCHR DS    C                   Char to strip (usually a blank)              
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  STRIP -- STRIP Function.                                                     
*                                                                               
*    STRIP(string,option-string,char)                                           
*                                                                               
FNSTRIP  PROC  STRIPWA                                                          
         CLEAR STRIPWA                                                          
         SET   STRIPFL+STRIPFT     Strip leading and trailing                   
         MVI   STRIPCHR,C' '       ...blanks                                    
*                                                                               
         SETARGS '13SSS'           1-3 args: string,string,string               
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
*-                                                                              
*-       Arg#2:  Option string.                                                 
*-                                                                              
         IF    (R0,GE,2),BEGIN     Check options...                             
         LA    R5,ARG2(R1)                                                      
         IF    (VNSTRLEN,GT,1),BEGIN                                            
         TSEG  'Expecting one character options string.  '                      
         BADARG 2                                                               
         EXIT  FNSTRIP                                                          
         END                                                                    
*                                                                               
         IF    (VNSTRLEN,EQ,1),BEGIN  Check option char...                      
         CLEAR STRIPFL+STRIPFT                                                  
         LA    R3,VNSTRLOC                                                      
         IF     ((@R3,EQ,'L'),OR,(@R3,EQ,'l')),'SET STRIPFL'                    
         ELSEIF ((@R3,EQ,'T'),OR,(@R3,EQ,'t')),'SET STRIPFT'                    
         ELSEIF ((@R3,EQ,'B'),OR,(@R3,EQ,'b')),BEGIN                            
         SET   STRIPFL+STRIPFT                                                  
         END                                                                    
         ELSE  BEGIN               Bad option char                              
         TSEG  'Expecting string of "L"=leading, "T"=trailing, '                
         TSEG  '"B"=both.',,CR                                                  
         BADARG 2                                                               
         EXIT  FNSTRIP                                                          
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Arg#3:  Char to strip.                                                 
*-                                                                              
         IF    (R0,GE,3),BEGIN     Check char...                                
         LA    R5,ARG3(R1)                                                      
         IF    (VNSTRLEN,GT,1),BEGIN                                            
         TSEG  'Expecting one character string.  '                              
         BADARG 3                                                               
         EXIT  FNSTRIP                                                          
         END                                                                    
*                                                                               
         IF    (VNSTRLEN,EQ,1),BEGIN  Save strip char...                        
         MVC   STRIPCHR,VNSTRLOC   Save char                                    
         END                                                                    
         END                                                                    
*                                                                               
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         Source string loc                            
         LH    R0,VNSTRLEN         ...and len                                   
*-                                                                              
*-       Strip leading characters.                                              
*-                                                                              
         IF    STRIPFL,BEGIN       Strip leading chars...                       
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,NE,STRIPCHR),EXIT  Stop                                     
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*-                                                                              
*-       Strip trailing characters.                                             
*-                                                                              
         IF    STRIPFT,BEGIN       Strip trailing chars...                      
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         DECR  R3                                                               
*                                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R3,NE,STRIPCHR),EXIT  Stop                                     
         DECR  R3                                                               
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
         VRETURN STRING,(R1),(R0)  Return stripped string                       
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  SUBSTR - GET SUBSTRING                                                       
*                                                                               
*  SUBSTR(STRING,FIRST_CHAR,LENGTH),,,                                          
*                                                                               
*                                                                               
*  SEE ENTRY/EXIT CONVENTIONS IN COMMENT AT START                               
*  OF THIS MODULE.                                                              
*                                                                               
FNSUBSTR PROC  ,                                                                
         SETARGS '23SII'           2-3 ARGS: STRING, INT, INT                   
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - STRING LOC, LEN                      
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         L     R4,VNVALUE          R4 - SUBSTRING OFFSET                        
         IF    ((R4,NP),OR,(R4,GT,MAXSTRLN)),BEGIN                              
         BADARG 2                                                               
         EXIT  FNSUBSTR                                                         
         END                                                                    
         BCTR  R4,0                                                             
         IF    (R0,EQ,3),BEGIN                                                  
         LA    R5,ARG3(R1)                                                      
         L     R5,VNVALUE          R5 - SUBSTRING LENGTH                        
         END                                                                    
         DROP  R5                                                               
         IF    (R0,EQ,2),BEGIN                                                  
         LR    R5,R3                                                            
         SR    R5,R4                                                            
         IF    (R5,LT,0),' SR R5,R5 '                                           
         END                                                                    
         IF    ((R5,NEG),OR,(R5,GT,MAXSTRLN)),BEGIN                             
         BADARG 3                                                               
         EXIT  FNSUBSTR                                                         
         END                                                                    
         AR    R2,R4                                                            
         SR    R3,R4                                                            
         IF    (R3,NEG),' SR R3,R3 '                                            
         O     R3,=X'40000000'                                                  
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         LA    R4,VNSTRLOC         R4,R5 - SUBSTRING LOC,LEN                    
         STH   R5,VNSTRLEN                                                      
         MVCL  R4,R2               MOVE SUBSTRING                               
         DROP  R6                                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SUBWORD Function.                                                            
*                                                                               
*    SUBWORD(??????????)                                                        
*                                                                               
FNSUBWRD PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SYSTEM_UP boolean variable.                                                  
*                                                                               
SYSUPWA  RECORD BEGIN                                                           
SYSUPNM  DS    CL8                 Subsystem name                               
         END                                                                    
*-                                                                              
FNSYSUP  PROC  SYSUPWA                                                          
         SETARGS '11S'             1 arg: string                                
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
*                                                                               
         SETMSG VNSTRLOC,LH:VNSTRLEN  Subsystem name                            
         CEIL  R0,L'SYSUPNM        Not too long                                 
         MVC   SYSUPNM,CVBLANKS                                                 
         LR    R15,R0                                                           
         MOVE  R15,SYSUPNM,@R1                                                  
         L     R15,CVUPPTBL                                                     
         TR    SYSUPNM,@R15        Convert to caps                              
*                                                                               
         SETMSG SYSUPNM             Subsystem name                              
         VCALL SYSCHECK            Is it up?                                    
         IF    Z,'VRETURN BOOLEAN,TRUE'                                         
         ELSE  'VRETURN BOOLEAN,FALSE'                                          
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SUBSYS_START string variable.                                                
*                                                                               
*  This function returns the time the given subsystem                           
*  was started. It is intended to allow the user to                             
*  determine if the subsystem has been restarted at                             
*  any time during the user's WYLBUR session (this                              
*  can be done by saving the SUBSYS_START value and                             
*  comparing it against the value returned by subsequent                        
*  SUBSYS_START calls).                                                         
*                                                                               
*  This function was specifically added to solve Roberta                        
*  Lucier's DISPLAY CIRC PSOC server restart problem, since                     
*  the system variable PROGRAM (which indicates the name                        
*  of the current ORVYL program) doesn't get reset when                         
*  ORVYL dies.                                                                  
*                                                                               
SYSCKWA  RECORD BEGIN                                                           
SYSCKNM  DS    CL8                 Subsystem name                               
SYSCKSSB DS    XL(L'SSB)           Subsystem info block                         
         END                                                                    
*-                                                                              
FNSUBCLK PROC  SYSCKWA                                                          
         SETARGS '11S'             1 arg: string                                
         IF    Z,BEGIN             OK...                                        
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
*                                                                               
         SETMSG VNSTRLOC,LH:VNSTRLEN  Subsystem name                            
         CEIL  R0,L'SYSCKNM        Not too long                                 
         MVC   SYSCKNM,CVBLANKS                                                 
         LR    R15,R0                                                           
         MOVE  R15,SYSCKNM,@R1                                                  
         L     R15,CVUPPTBL                                                     
         TR    SYSCKNM,@R15        Convert to caps                              
*                                                                               
         SETMSG SYSCKNM             Subsystem name                              
         LA    R2,SYSCKSSB                                                      
         VCALL SYSGTSSB            Did we get it?                               
         IF    Z,BEGIN                                                          
         XPUSH  ,,MAXSTRLN,PTR=R15                                              
         WITH  (SSB,R2)                                                         
         LM    R0,R1,SSBSCLCK                                                   
         VCALL FMTCLCK              IN CR MODULE                                
         XPOP  ,,MAXSTRLN                                                       
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R0                 Return null string                            
         CLEAR R1                                                               
         END                                                                    
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
         EJECT                                                                  
XLWA     RECORD BEGIN                                                           
XLSTPTRS DS    2F                  Arg1 string len, loc                         
XLFRPTRS DS    2F                  xl-from string len, loc                      
XLTOPTRS DS    2F                  xl-to string len, loc                        
*                                                                               
XLPAD    DS    C                   Pad character                                
*                                                                               
XLTBL    DS    XL256               Translation table                            
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  TODTIME Function.                                                            
*                                                                               
*    TODTIME(Hi-order STCK, Low-order STCK)                                     
*                                                                               
FNTODTIM PROC  ,                                                                
         SETARGS '22II'            2 ARGS: INTEGER, INTEGER                     
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R2,VNVALUE          R2 - 1ST NUMBER                              
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          R3 - 2ND NUMBER                              
         DROP  R5                                                               
         XPUSH ,,11,PTR=R15                                                     
         LR    R0,R2                                                            
         LR    R1,R3                                                            
         VCALL FMTTIME                                                          
         VRETURN STRING,(R1),(R0)                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TRANSLATE Function.                                                          
*                                                                               
*    TRANSLATE(string[,[tableo][,[tablei][,pad]]])                              
*                                                                               
FNXLATE  PROC  XLWA                                                             
         SETARGS '14SSSS'          1-4 args: str,str,str,str                    
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
*                                                                               
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         STM   R2,R3,XLSTPTRS      Save orig string loc, len                    
*-                                                                              
*-       Only one argument means translate to upper case.                       
*-                                                                              
         IF    (R0,EQ,1),BEGIN                                                  
         L     R4,CVUPPTBL                                                      
         MVC   XLTBL,@R4           Move in UPPER xl table                       
         END                                                                    
*-                                                                              
*-       Build translate table from user's arguments.                           
*-                                                                              
         ELSE  BEGIN                                                            
*-                                                                              
*-       Arg#2:  Translate-to string.                                           
*-                                                                              
         CLEAR R2,R3                                                            
         IF    (R0,GE,2),BEGIN     Arg#2 specified...                           
         LA    R5,ARG2(R1)                                                      
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         END                                                                    
         STM   R2,R3,XLTOPTRS      R2,R3 - translate-to string ptrs             
*-                                                                              
*-       Arg#3:  Translate-from string.                                         
*-                                                                              
         CLEAR R2,R3                                                            
         IF    (R0,GE,3),BEGIN     Arg#3 specified...                           
         LA    R5,ARG3(R1)                                                      
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         END                                                                    
         STM   R2,R3,XLFRPTRS      R2,R3 - translate-from str ptrs              
*-                                                                              
*-       Arg#4:  Pad character.                                                 
*-                                                                              
         MVI   XLPAD,C' '          Default pad character                        
*                                                                               
         IF    (R0,GE,4),BEGIN     Pad character specified...                   
         LA    R5,ARG4(R1)                                                      
         LTH   R4,VNSTRLEN                                                      
         IF    Z,EXIT              Ignore null string                           
         IF    (R4,GT,1),BEGIN                                                  
         BADARG 4  Too long                                                     
         EXIT  FNXLATE                                                          
         END                                                                    
         MVC   XLPAD,VNSTRLOC      Save pad character                           
         END                                                                    
*-                                                                              
*-       Build translate table.                                                 
*-                                                                              
         MVC   XLTBL,XLMDLTBL      Default translate table                      
*                                                                               
         LM    R14,R15,XLFRPTRS    R14,R15 - xl-from loc, len                   
         LM    R2,R3,XLTOPTRS      R2,R3 - xl-to loc, len                       
         WHILE (R15,POS),BEGIN     Go through from string...                    
         IF    (R3,NP),'LC R0,XLPAD'                                            
         ELSE  'LC R0,@R2; LA R2,@R2+1; DECR R3'                                
         LC    R1,@R14                                                          
         LA    R14,@R14+1                                                       
         DECR  R15                                                              
*                                                                               
         LA    R1,XLTBL(R1)        Loc in xl table                              
         STC   R0,@R1              Set byte in translate table                  
         END                                                                    
         END                                                                    
*-                                                                              
*-       Copy the string and do the translation.                                
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         LM    R2,R3,XLSTPTRS      Orig string loc, len                         
         STH   R3,VNSTRLEN         Set output string length                     
         LA    R14,VNSTRLOC                                                     
         LR    R15,R3                                                           
         MVCL  R14,R2              Copy orig string                             
*                                                                               
         LA    R2,VNSTRLOC                                                      
         LH    R3,VNSTRLEN                                                      
         WHILE (R3,POS),BEGIN      Translate string...                          
         LR    R15,R3                                                           
         CEIL  R15,256                                                          
         DEX   R15,'TR @R2(0),XLTBL'  TRANSLATE!                                
*                                                                               
         LA    R2,@R2+256                                                       
         SH    R3,=H'256'                                                       
         END                                                                    
         CLEAR R15                 Set RC                                       
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
XLMDLTBL DC    256AL1(*-XLMDLTBL)  Virgin translate table                       
         EJECT                                                                  
*box                                                                            
*                                                                               
*  TRUNC Function.                                                              
*                                                                               
*    TRUNC(??????????)                                                          
*                                                                               
FNTRUNC  PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  UPPER                                                                        
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNUPPER  PROC  ,                                                                
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R1,VNSTRLOC         R1 - STRING LOC                              
         LH    R2,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R2,VNSTRLEN                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC VNSTRLOC(0),0(R1) '  MOVE STRING TO RET AREA            
         L     R4,CVUPPTBL                                                      
         EX    R2,' TR VNSTRLOC(0),0(R4) '   CONVERT IT                         
         END                                                                    
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
         QLTORG                                                                 
*box                                                                            
*                                                                               
*  URL_DECODE                                                                   
*                                                                               
*  Decodes a string according to the escaping                                   
*  convention described in RFC 2396                                             
*                                                                               
FNURLWA  RECORD BEGIN                                                           
FNURLHEX DS    CL2         Uppercase form for XTB                               
         END                                                                    
         SPACE 2                                                                
FNURLDCD PROC  FNURLWA                                                          
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R1 - STRING LOC                              
         LH    R3,VNSTRLEN         R2 - STRING LEN                              
         DROP  R5                                                               
         USING VNENTRY,R6                                                       
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         STH   R3,VNSTRLEN                                                      
         LA    R4,VNSTRLOC                                                      
         WHILE (R3,POS),BEGIN                                                   
         IF    ((@R2,EQ,'%'),AND,(R3,GE,3)),BEGIN                               
         MVC   FNURLHEX(L'FNURLHEX),@R2+1                                       
         L     R1,CVUPPTBL                                                      
         TR    FNURLHEX(L'FNURLHEX),0(R1)                                       
         XTB   FNURLHEX,L'FNURLHEX                                              
         IF    Z,BEGIN                                                          
         A     R15,CVEBCTBL                                                     
         MVC   @R4(1),@R15                                                      
         LA    R2,2(,R2)                                                        
         DECR  R3                                                               
         DECR  R3                                                               
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   @R4(1),@R2                                                       
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   @R4(1),@R2                                                       
         END                                                                    
         DECR  R3                                                               
         INCR  R2                                                               
         INCR  R4                                                               
         END                                                                    
         LA    R1,VNSTRLOC                                                      
         SR    R4,R1                                                            
         STH   R4,VNSTRLEN                                                      
         CLEAR R15                                                              
         DROP  R6                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
FNVARWA  RECORD BEGIN                                                           
FNVARRET DS    CL16                Variable type text                           
*                                                                               
FNVARPRM DS    XL(L'VPARM)         Working VPARM                                
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  VARTYPE                                                                      
*                                                                               
*    VARTYPE('variable-name')                                                   
*                                                                               
*    See comments at the start of this module.                                  
*                                                                               
FNVARTYP PROC  FNVARWA                                                          
         SCPUSH ,                                                               
         TPUSH                                                                  
*                                                                               
*                                                                               
         SETARGS '11SS'            1 Arg: string                                
         IF    Z,BEGIN                                                          
         MVC   FNVARRET,=CL16'UNDEFINED'  Assume the worst                      
*                                                                               
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN                                            
         LA    R2,FNVARPRM                                                      
         VCALL SCNVDESC            Scan variable name                           
         IF    Z,BEGIN             Get variable value...                        
         LA    R5,FNVARPRM         Working VPARM area                           
*                                                                               
         IF    (VNASUB1,Z),BEGIN   Regular variable...                          
         LA    R1,VNENTRY                                                       
         LR    R2,R1                                                            
         VCALL GETVAR              Get variable entry value                     
         IF    Z,BEGIN             OK...                                        
         IF    VNSTRING,'MVC FNVARRET,=CL16"STRING"'                            
         IF    VNNUMB,'MVC FNVARRET,=CL16"NUMBER"'                              
         IF    VNBOOL,'MVC FNVARRET,=CL16"BOOLEAN"'                             
         IF    VNARRAY,'MVC FNVARRET,=CL16"ARRAY"'                              
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN               Array...                                     
         CLEAR R0                  No array descriptor                          
         LA    R1,VNENTRY                                                       
         LR    R2,R1                                                            
         VCALL GETARRAY            Get array entry value                        
         IF    Z,BEGIN             OK...                                        
         IF    VNSTRING,'MVC FNVARRET,=CL16"STRING"'                            
         IF    VNNUMB,'MVC FNVARRET,=CL16"NUMBER"'                              
         IF    VNBOOL,'MVC FNVARRET,=CL16"BOOLEAN"'                             
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         VRETURN STRING,FNVARRET   Return variable type text                    
         VTRIM                                                                  
         END                                                                    
*                                                                               
*                                                                               
         TPOP  JUNK                                                             
         SCPOP ,                                                                
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  VERIFY                                                                       
*                                                                               
*  VERIFY(STRING,CHARS_TO_VERIFY),,,                                            
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNVERIFY PROC  FNWA                                                             
         SETARGS '22SS'            2 ARGS: STRING, STRING                       
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - 1ST STRING LOC, LEN                  
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R4,VNSTRLOC         R4,R5 - 2ND STRING LOC, END                  
         LH    R5,VNSTRLEN                                                      
         AR    R5,R4                                                            
         DROP  R5                                                               
         MVI   FNWORK,X'04'         SET UP TRT TABLE                            
         MVC   FNWORK+1(255),FNWORK MARK ALL CHARS,                             
         SR    R1,R1                UNMARK ALL CHARS IN STR #2                  
         SR    R0,R0                                                            
         WHILE (R4,LT,R5),BEGIN     UNMARK ALL CHARS IN STR #2                  
         IC    R1,0(R4)                                                         
         STC   R0,FNWORK(R1)                                                    
         LA    R4,1(R4)                                                         
         END                                                                    
         LR    R1,R2               R1,R4 - START OF STRING #1                   
         LR    R4,R2                                                            
         IF    (R3,POS),BEGIN                                                   
         DEX   R3,' TRT @R1(0),FNWORK '   ** TRT **                             
         IF    NZ,BEGIN                                                         
         LA    R1,1(R1)            OFFSET+1 = CHAR NUMBER                       
         END                                                                    
         END                                                                    
         COMMENT                   R1 - LOC OF 1ST CHAR NOT FOUND               
         SR    R1,R4               R1 - INDEX OF 1ST CHAR NOT FOUND             
         VRETURN INTEGER,(R1)                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RF_DECRYPT_V8B(KEY,CIPHER)                                                   
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNV8BD   PROC  FNWA                                                             
*                                                                               
         SETARGS '22SS'            2 ARGS: STRING, STRING                       
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - 1ST STRING LOC, LEN                  
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R4,VNSTRLOC         R4,R5 - 2ND STRING LOC, LEN                  
         LH    R5,VNSTRLEN                                                      
         DROP  R5                                                               
         IF    (R3,NE,8),BEGIN                                                  
         TSEG  'Key must be exactly 8 bytes long.'                              
         BADARG 1                                                               
         EXIT  FNV8BD                                                           
         END                                                                    
         IF    (R5,NE,8),BEGIN                                                  
         TSEG  'Cipher must be exactly 8 bytes long.'                           
         BADARG 2                                                               
         EXIT  FNV8BD                                                           
         END                                                                    
*                                                                               
         COMMENT                   DECODE 8 BYTES                               
         LR    R0,R2               8 BYTE KEY                                   
         LA    R1,FNWA             8 BYTE TEXT (RETURNED)                       
         LR    R2,R4               8 BYTE CIPHER                                
         VCALL FEALBLKD                                                         
         VRETURN STRING,(R1),8                                                  
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RF_ENCRYPT_V8B(KEY,TEXT)                                                     
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNV8BE   PROC  FNWA                                                             
*                                                                               
         SETARGS '22SS'            2 ARGS: STRING, STRING                       
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         LA    R2,VNSTRLOC         R2,R3 - 1ST STRING LOC, LEN                  
         LH    R3,VNSTRLEN                                                      
         LA    R5,ARG2(R1)                                                      
         LA    R4,VNSTRLOC         R4,R5 - 2ND STRING LOC, LEN                  
         LH    R5,VNSTRLEN                                                      
         DROP  R5                                                               
         IF    (R3,NE,8),BEGIN                                                  
         TSEG  'Key must be exactly 8 bytes long.'                              
         BADARG 1                                                               
         EXIT  FNV8BE                                                           
         END                                                                    
         IF    (R5,NE,8),BEGIN                                                  
         TSEG  'Text must be exactly 8 bytes long.'                             
         BADARG 2                                                               
         EXIT  FNV8BE                                                           
         END                                                                    
*                                                                               
         COMMENT                   DECODE 8 BYTES                               
         LR    R0,R2               8 BYTE KEY                                   
         LR    R1,R4               8 BYTE TEXT                                  
         LA    R2,FNWA             8 BYTE CIPHER (RETURNED)                     
         VCALL FEALBLKE                                                         
         VRETURN STRING,(R2),8                                                  
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  WCONVERT - CONVERT TO NUMBER, WITH LINE NUMBER FORMAT                        
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNWCONV  PROC  ,                                                                
         SETARGS '11W'             1 ARG: NUMBER, WYLBUR LINENO                 
         COMMENT                   THE SETARG ROUTINE ABOVE DOES THE            
         COMMENT                   WORK, CONVERTING THE INPUT ARG TO            
         COMMENT                   A LINENO,,, WHICH WE RETURN                  
         IF    Z,BEGIN                                                          
         USING VNENTRY,R5                                                       
         LA    R5,ARG1(R1)                                                      
         L     R3,VNVALUE          R3 - LINENO VALUE                            
         DROP  R5                                                               
         VRETURN LINENO,(R3)                                                    
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WCOUNT -- Get the value of a "counter".                                      
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNWCOUNT PROC  ,                                                                
         SETARGS '11S'             1 arg: string                                
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Counter name                              
         VCALL GETWCNT             Get WCOUNTER value in R15                    
         VRETURN INTEGER,(R15)                                                  
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*                                                                               
*  WHEX - CONVERT HEX STRING TO LINENO FORMATTED NUMBER                         
*                                                                               
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNWHWA   RECORD BEGIN                                                           
         XSA   R0,R8                                                            
FNWHTEMP DS    CL8                                                              
         END                                                                    
*                                                                               
*                                                                               
FNWHEX   PROC  FNWHWA                                                           
         USING VNENTRY,R5                                                       
         SETARGS '11S'             1 ARG: STRING                                
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         LH    R0,VNSTRLEN                                                      
         IF    (R0,GT,8),BEGIN                                                  
         TSEG  'Arithmetic overflow.  '                                         
         BADARG 1                                                               
         EXIT  FNWHEX                                                           
         END                                                                    
         LA    R1,VNSTRLOC                                                      
         LA    R2,FNWHTEMP                                                      
         VCALL MAKEHEX             CONVERT HEX STRING TO HEX                    
         IF    NZ,BEGIN                                                         
         TSEG  'Invalid hex string.  '                                          
         BADARG 1                                                               
         EXIT  FNWHEX                                                           
         END                                                                    
         L     R1,FNWHTEMP         R1 - HEX VALUE                               
         WHILE (R0,LT,4),BEGIN     PROPERLY SHIFTED                             
         SRL   R1,8                                                             
         A     R0,=F'1'                                                         
         END                                                                    
         VRETURN LINENO,(R1)       RETURN LINE NUMBER                           
         END                                                                    
         PEND                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WORD Function.                                                               
*                                                                               
*    WORD(??????????)                                                           
*                                                                               
FNWORD   PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         LA    R15,4                                                            
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WORDINDEX Function.                                                          
*                                                                               
*    WORDINDEX(??????????)                                                      
*                                                                               
FNWORDIX PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WORDLENGTH Function.                                                         
*                                                                               
*    WORDLENGTH(??????????)                                                     
*                                                                               
FNWORDLN PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WORDS Function.                                                              
*                                                                               
*    WORDS(??????????)                                                          
*                                                                               
FNWORDS  PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WYLBUR_CPU_ function.                                                        
*                                                                               
FNGWYLC  PROC  ,                                                                
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Component name specified                  
         LA    R15,=CL16'WYLBUR_CPU'                                            
         VCALL GETCVAR                                                          
         VRETURN LINENO,(R15)      Return WYLBUR cpu in .001 sec                
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  WYLBUR_IO_ function.                                                         
*                                                                               
FNGWYLI  PROC  ,                                                                
*                                                                               
         SETARGS '11S'             1 arg: str                                   
         IF    Z,BEGIN                                                          
         LA    R5,ARG1(R1)                                                      
         WITH  (VNENTRY,R5)                                                     
         SETMSG VNSTRLOC,LH:VNSTRLEN  Component name specified                  
         LA    R15,=CL16'WYLBUR_IO'                                             
         VCALL GETCVAR                                                          
         VRETURN INTEGER,(R15)     Return WYLBUR I/Os                           
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  XRANGE Function.                                                             
*                                                                               
*    XRANGE([start-char][,end-char])                                            
*                                                                               
FNXRANGE PROC  ,                                                                
         SETARGS '02SS'            0-2 args: string, string                     
         IF    Z,BEGIN             OK...                                        
         WITH  (VNENTRY,R5)                                                     
         CLEAR R2                  R2 - lower bound                             
         LA    R3,X'FF'            R3 - upper bound                             
*-                                                                              
*-       Arg#1: lower bound                                                     
*-                                                                              
         IF    (R0,GE,1),BEGIN                                                  
         LA    R5,ARG1(R1)                                                      
         LTH   R4,VNSTRLEN                                                      
         IF    Z,EXIT                                                           
         IF    (R4,GT,1),BEGIN     String too long                              
         BADARG 1                                                               
         EXIT  FNXRANGE                                                         
         END                                                                    
         LC    R2,VNSTRLOC         Get starting byte                            
         END                                                                    
*-                                                                              
*-       Arg#2: upper bound                                                     
*-                                                                              
         IF    (R0,GE,2),BEGIN                                                  
         LA    R5,ARG2(R1)                                                      
         LTH   R4,VNSTRLEN                                                      
         IF    Z,EXIT                                                           
         IF    (R4,GT,1),BEGIN     String too long                              
         BADARG 2                                                               
         EXIT  FNXRANGE                                                         
         END                                                                    
         LC    R3,VNSTRLOC         Get ending byte                              
         END                                                                    
*-                                                                              
*-       Build range string.                                                    
*-                                                                              
         WITH  (VNENTRY,R6),BEGIN                                               
         CLEAR VNENTRY                                                          
         SET   VNSTRING                                                         
         CLEAR R0                                                               
         LA    R1,VNSTRLOC                                                      
         LOOP  BEGIN               Build range string...                        
         STC   R2,@R1              Set char                                     
         LA    R1,@R1+1                                                         
         INCR  R0                                                               
         IF    (R2,EQ,R3),EXIT     Time to stop, scram                          
         LA    R2,@R2+1                                                         
         N     R2,=A(X'FF')        Modulo 256                                   
         END                                                                    
         STH   R0,VNSTRLEN         Set string length                            
         END                                                                    
*                                                                               
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  X2C Function.                                                                
*                                                                               
*    X2C(??????????)                                                            
*                                                                               
FNX2C    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  X2D Function.                                                                
*                                                                               
*    X2D(??????????)                                                            
*                                                                               
FNX2D    PROC  ,                                                                
         TSEG  'Not Yet!  '                                                     
         BADARG 1                                                               
         PEND                                                                   
         EJECT                                                                  
YNWA     RECORD BEGIN                                                           
YNFLAG   FLAG  YNFYES              - Allow YES                                  
         FLAG  YNFNO               - Allow NO                                   
         FLAG  YNFATTN             - Allow ATTN                                 
         FLAG  YNFNULL             - Allow NULL (just return)                   
         FLAG  YNFHELP             - Allow HELP or ?                            
*                                                                               
YNPTRS   DS    2A                  Prompt loc, len                              
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*                                                                               
*  YESNO - Prompt with "Yes or No" question and set return string.              
*                                                                               
*  SEE COMMENTS AT START OF THIS MODULE                                         
*                                                                               
FNYESNO  PROC  YNWA                                                             
         LA    R4,YNWA                                                          
         USING YNWA,R4                                                          
         CLEAR YNWA                                                             
*                                                                               
         SCPUSH ,                                                               
*                                                                               
         SETARGS '12SS'            1-2 args: str, str                           
         BNZ   YNEXIT                                                           
*                                                                               
         IF    CPFZAPIF,BEGIN      Expression rescan...                         
         VRETURN STRING,'DUMMY'                                                 
         B     YNEXIT              Scram                                        
         END                                                                    
*                                                                               
         LR    R2,R0               Save number of arguments                     
         LR    R3,R1               Argument list ptr                            
         WITH  (VNENTRY,R5)        (Will be accurate later)                     
*-                                                                              
*-       Save prompt string pointers.                                           
*-                                                                              
         LA    R5,ARG1(R3)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN  Prompt text                               
         STM   R0,R1,YNPTRS        Save ptrs for later                          
*-                                                                              
*-       Scan second parameter for yesno options.                               
*-                                                                              
         IF    (R2,LT,2),YNCONT                                                 
         LA    R5,ARG2(R3)                                                      
         SCINIT VNSTRLOC,LH:VNSTRLEN  Options                                   
         SCAN  YNPRT                                                            
         IF    (R15,NZ),YNERR                                                   
         B     YNCONT              Now go do it                                 
*                                                                               
YNERR    TSEG  (R1),(R0)                                                        
         TSEG  ' is an unknown YESNO option.'                                   
         BADARG 2                                                               
         B     YNEXIT                                                           
*                                                                               
*                                                                               
YNCONT   LABEL ,                                                                
*                                                                               
         IF    (YNFYES+YNFNO+YNFNULL+YNFATTN,Z),BEGIN  Default...               
         SET   YNFYES+YNFNO        Default options                              
         END                                                                    
*                                                                               
         IF    (YNFYES+YNFNO,Z),BEGIN                                           
         TSEG  'YES or NO must be specified as a YESNO option.'                 
         BADARG 2                                                               
         B     YNEXIT                                                           
         END                                                                    
*                                                                               
         B     YNASK                                                            
*-                                                                              
*-       Pop the question.                                                      
*-                                                                              
YNLOOP   SETMSG 'YES or NO.'                                                    
         IF    (YNFYES+YNFNO,MIXED),BEGIN                                       
         IF    YNFYES,'SETMSG "YES when you are ready."'                        
         IF    YNFNO,'SETMSG "NO when you are ready."'                          
         END                                                                    
         TSEG  (R1),(R0),CR                                                     
*                                                                               
YNASK    LM    R0,R1,YNPTRS        Prompt                                       
         TSEG  (R1),(R0)                                                        
         SETMSG ' (Yes/No)'                                                     
         IF    (YNFYES+YNFNO,MIXED),BEGIN                                       
         IF    YNFYES,'SETMSG " (Yes)"'                                         
         IF    YNFNO,'SETMSG " (No)"'                                           
         END                                                                    
         LCR   R0,R0               (Don't add anything to prompt)               
         IF    YNFHELP,'VCALL YESRTNH'  Allow yes/no/help                       
         ELSE  'VCALL YESRTN'      Otherwise just yes/no                        
*                                                                               
         IF    (R15,LT,0),BEGIN    Attn ...                                     
         SETMSG 'ATTN'                                                          
         IF    YNFATTN,BEGIN       Attn's are allowed...                        
         L     R2,CVCURJCB         Ptr to current JCB                           
         WITH  (JCB,R2),'CLEAR JCBFATTN'  Reset attn                            
         END                                                                    
         END                                                                    
*-                                                                              
*-       "HELP" or "?"                                                          
*-                                                                              
         ELSEIF (R15,EQ,8),BEGIN   Help...                                      
         SETMSG 'HELP'                                                          
         END                                                                    
*-                                                                              
*-       Unrecognized response (could be NO, NULL).                             
*-                                                                              
         ELSEIF (R15,GT,0),BEGIN   No (or null)...                              
*-                                                                              
*-       "NULL"                                                                 
*-                                                                              
         IF    (R0,Z),BEGIN        NULL...                                      
         SETMSG 'NULL'                                                          
         IF    ^YNFNULL,BEGIN                                                   
         TSEG  'Answer '                                                        
         B     YNLOOP                                                           
         END                                                                    
         END                                                                    
*-                                                                              
*-       "NO"                                                                   
*-                                                                              
         ELSE  BEGIN               NO...                                        
         SETMSG 'NO'                                                            
         IF    ^YNFNO,BEGIN                                                     
         TSEG  'You can not answer NO here, answer '                            
         B     YNLOOP                                                           
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       "YES"                                                                  
*-                                                                              
         ELSE  BEGIN                                                            
         SETMSG 'YES'                                                           
         IF    ^YNFYES,BEGIN                                                    
         TSEG  'You can not answer YES here, answer '                           
         B     YNLOOP                                                           
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
YNSETRC  VRETURN STRING,(R1),(R0)                                               
*                                                                               
YNEXIT   SCPOP                                                                  
         PEND                                                                   
*                                                                               
*  SCKW STUFF                                                                   
*                                                                               
YNPRT    SCKW  YES,YNYES                                                        
         SCKW  NO,YNNO                                                          
         SCKW  HELP,YNHELP                                                      
         SCKW  NULL,YNNULL                                                      
         SCKW  ATTN,YNATTN                                                      
         SCKW  ,YNBAD                                                           
*                                                                               
YNYES    PROC  ,                                                                
         SET   YNFYES                                                           
         PEND                                                                   
*                                                                               
YNNO     PROC  ,                                                                
         SET   YNFNO                                                            
         PEND                                                                   
*                                                                               
YNHELP   PROC  ,                                                                
         SET   YNFHELP                                                          
         PEND                                                                   
*                                                                               
YNATTN   PROC  ,                                                                
         SET   YNFATTN                                                          
         PEND                                                                   
*                                                                               
YNNULL   PROC  ,                                                                
         SET   YNFNULL                                                          
         PEND                                                                   
*                                                                               
YNBAD    PROC  ,                                                                
         LA    R15,4                                                            
         PEND                                                                   
         DROP  R4                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  ARRAY_SEARCH( array, string [,cnt] [,flags] [,min] [,pos] [,size] )I         
*                                                                               
*   search "array[1] to array[cnt]" for "string", if "cnt" is 0 or              
*     omitted search the whole array.  Returns the index into the               
*     array or 0 if a match is not found. Flags are as follows:                 
*        01  Case insensitive compare                                           
*        02  Abbreviation is allowed                                            
*        04  Use a linear search (array is not sorted)                          
*        08  Perform a stem-search (array must be sorted)                       
*     If abbreviation is allowed then a positive "min" indicates the            
*     minimum abbreviation allowed.  "Pos" and "size" delimit the               
*     portion of each string of array to be compared, they default to           
*     1 and "to the end" respectively.                                          
*     If a stem-search is being performed "min" is the last element ofI         
*     array searched (i.e., start searching at array[min+1]).                   
*                                                                               
*                                                                               
ASWA     RECORD  BEGIN                                                          
ASFLAG   FLAG    ASFLG80                                                        
         FLAG    ASFLG40                                                        
         FLAG    ASFLG20                                                        
         FLAG    ASFLG10                                                        
         FLAG    ASSTEM                                                         
         FLAG    ASLINEAR                                                       
         FLAG    ASABBREV                                                       
         FLAG    ASCASEIN                                                       
         DS      0F                                                             
ASMIN    DS      F                                                              
ASMAX    DS      F                                                              
ASPOS    DS      F                                                              
ASSIZ    DS      F                                                              
ASARRAY  DS      F                                                              
ASSTRLEN DS      F                 length of string                             
ASSTRING DS      CL256             comparison string                            
         END                                                                    
         SPACE   2                                                              
*                                                                               
FNASRCH  PROC    ASWA                                                           
         CLEAR   ASWA                                                           
         SETARGS '27XSIIIII'       check arguments                              
         IF      Z,BEGIN                                                        
         WITH    (VNENTRY,R5)                                                   
* check out the array                                                           
         LA      R5,ARG1(R1)                                                    
         ST      R5,ASARRAY                                                     
         IF      VNARRAY,BEGIN                                                  
         IF      VNASTR,BEGIN                                                   
         LH      R3,VNADIM1        save the array bounds as MAX                 
         ST      R3,ASMAX                                                       
         END                                                                    
         ELSE    BEGIN             arg 1 array base type not string             
         BADARG  1                                                              
         EXIT    FNASRCH                                                        
         END                                                                    
         END                                                                    
         ELSE    BEGIN             arg 1 not an array                           
         BADARG  1                                                              
         EXIT    FNASRCH                                                        
         END                                                                    
*- see which optional arguments are present                                     
         MVC     ASSIZ,=F'256'     maximum allowable size                       
         IF      (R0,GE,3),BEGIN   cnt specified                                
         LA      R5,ARG3(R1)                                                    
         L       R3,VNVALUE                                                     
         IF      ((R3,POS),AND,(R3,LT,ASMAX)),'ST R3,ASMAX'                     
*                                                                               
         IF      (R0,GE,4),BEGIN   flags specified                              
         LA      R5,ARG4(R1)                                                    
         L       R3,VNVALUE        set flags                                    
         STC     R3,ASFLAG                                                      
*                                                                               
         IF      (R0,GE,5),BEGIN   min specified                                
         LA      R5,ARG5(R1)                                                    
         L       R3,VNVALUE                                                     
         IF      (R3,POS),'ST R3,ASMIN'                                         
*                                                                               
         IF      (R0,GE,6),BEGIN   pos specified                                
         LA      R5,ARG6(R1)                                                    
         L       R3,VNVALUE                                                     
         DECR    R3                                                             
         IF      (R3,POS),'ST R3,ASPOS'                                         
*                                                                               
         IF      (R0,GE,7),BEGIN   size specified                               
         LA      R5,ARG7(R1)                                                    
         L       R3,VNVALUE                                                     
         IF      (R3,POS),'ST R3,ASSIZ'                                         
*                                                                               
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         LA      R5,ARG2(R1)                                                    
         LA      R2,VNSTRLOC                                                    
         LH      R3,VNSTRLEN                                                    
         ST      R3,ASSTRLEN       save string length away                      
         DEX     R3,'MVC ASSTRING(0),0(R2)'  move string                        
         IF      ASCASEIN,BEGIN                                                 
         L       R4,CVUPPTBL       uppercase translate table                    
         EX      R3,'TR ASSTRING(0),0(R4)'  uppercase the string                
         END                                                                    
* do appropriate search                                                         
         IF      ASSTEM,ASRCHS     stem search?                                 
         IF      ASLINEAR,ASRCHL   linear search?                               
*- binary search                                                                
ASRCHB   L       R2,=F'1'          LO=1                                         
         L       R4,ASMAX          HI=MAX                                       
         WHILE   (R2,LE,R4),BEGIN  ; else return 0                              
         LR      R3,R4             MID=HIGH+LOW/2                               
         AR      R3,R2                                                          
         SRL     R3,1                                                           
         ACALL   ASCMPC           compare string & array[mid]<p:p+s-1>I         
         IF      EQ,ASFOUND                                                     
         IF      LT,'LR R4,R3;DECR R4'   HI=MID-1                               
         IF      GT,'LR R2,R3;INCR R2'   LO=MOD+1                               
         END                                                                    
         B       ASNOTFND          didn't find it, oh well                      
*- linear search                                                                
ASRCHL   L       R3,=F'1'                                                       
         L       R4,ASMAX                                                       
         WHILE   (R3,LE,R4),BEGIN                                               
         ACALL   ASCMPC                                                         
         IF      EQ,ASFOUND                                                     
         INCR    R3                look at the next one                         
         END                                                                    
         B       ASNOTFND          didn't find it                               
*- stem search                                                                  
ASRCHS   CLEAR   R3                index of previous stem-match                 
         L       R4,ASMIN          index to start matching                      
         CLEAR   R2                number of chars in prev s-match              
         IF      (R4,NZ),BEGIN     had a previous stem-match                    
         ACALL   ASCMPC            match characters                             
         LR      R2,R15                                                         
         END                                                                    
         INCR    R4                                                             
         WHILE   (R4,LE,ASMAX),BEGIN                                            
         ACALL   ASCMPC            R15=matchc(array[r4])                        
         IF      (R15,LT,R2),ASFOUND  found a shorter match (done)              
         IF      ((R15,GT,R2),OR,((R3,Z),AND,(R15,NZ))),BEGIN  match            
         LR      R2,R15            longest match so far                         
         LR      R3,R4             index of the element                         
         END                                                                    
         INCR    R4                                                             
         END                                                                    
         B       ASFOUND           if (R3 > 0) found something                  
*                                                                               
ASNOTFND CLEAR   R3                                                             
ASFOUND  VRETURN INTEGER,(R3)                                                   
         END                                                                    
         PEND                                                                   
         SPACE   2                                                              
*box                                                                            
* compare subroutine                                                            
*   on input:  R3  index of array element to compare (^ASSTEM)                  
*              R4  index of array element to match (ASSTEM)                     
*              R8  address of ASWA                                              
*   on output: R15 count of matched characters (ASSTEM)                         
ASCMPC   PROC    ASWA,TRACE=NO                                                  
         LR      R6,R8             save our WAR                                 
         L       R8,36(R8)         point WAR back at callers work areaI         
         CLEAR   R0                we have no array descriptor                  
         WITH    (VNENTRY,R2),BEGIN                                             
         L       R2,ASARRAY                                                     
         LR      R1,R2             R1,R2 point to VNENTRY area                  
         IF      ASSTEM,'LR R3,R4'                                              
         STH     R3,VNASUB1                                                     
         VCALL   GETARRAY                                                       
         IF      NZ,'BOMB'         We are screwed                               
         END                                                                    
         WITH    (VNENTRY,R5),BEGIN                                             
         LR      R5,R2                                                          
         LA      R0,ASSTRING                                                    
         L       R1,ASSTRLEN                                                    
         LA      R2,VNSTRLOC                                                    
         LH      R3,VNSTRLEN                                                    
         IF      (ASABBREV,AND,(R1,GE,R3)),'LR R1,R3'                           
         A       R2,ASPOS          Take care of POS & SIZE                      
         S       R3,ASPOS                                                       
         IF      (ASSIZ,LT,R3),'L R3,ASSIZ'                                     
         IF      (R3,NEG),'CLEAR R3'                                            
         IF      ASCASEIN,BEGIN                                                 
         L       R4,CVUPPTBL                                                    
         DEX     R3,'TR @R2(0),0(R4)'  upcase the array element                 
         INCR    R3                                                             
         END                                                                    
         IF      ASSTEM,BEGIN                                                   
         CLEAR   R15               count of characters matched                  
         LR      R4,R0             since we can't use R0 in a CLC               
         WHILE   ((R15,LT,R1),AND,(R15,LT,R3),AND,                     X        
               ('CLC @R4(1),@R2',EQ)),BEGIN                                     
         INCR    R15               one more matched                             
         INCR    R4                bump input string pointer                    
         INCR    R2                bump array element pointer                   
         END                                                                    
         END                                                                    
         ELSE    'CLCL R0,R2'                                                   
         END                                                                    
         LR      R8,R6             put WAR back where it belongs                
         PEND    *                                                              
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*   BOUNDS                                                                      
*                                                                               
*     BOUNDS('array-name' [,dimension])                                         
*                                                                               
*     Returns the declared size of an array dimension.  Returns -1 if           
*     array-name is not declared.  Returns 0 if array-name is a scalarI         
*     or if the array is of less dimensions than specified.                     
FNBOUNDS PROC  FNVARWA                                                          
         SCPUSH ,                                                               
         TPUSH                                                                  
*                                                                               
*                                                                               
         SETARGS '12SI'            2 Args: a string, an optional int            
         IF    Z,BEGIN                                                          
         WITH  (VNENTRY,R5)                                                     
         IF    (R0,LT,2),'LA R3,1' Dimension omitted, default it to 1           
         ELSE  BEGIN                                                            
         LA    R5,ARG2(R1)                                                      
         L     R3,VNVALUE          Get the dimension specified                  
         END                                                                    
         IF    ((R3,LT,1),OR,(R3,GT,3)),BEGIN                                   
         BADARG 2                                                               
         EXIT  FNBOUNDS                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         L     R4,=F'-1'           Assume variable is undefined                 
*                                                                               
         LA    R5,ARG1(R1)                                                      
         SETMSG VNSTRLOC,LH:VNSTRLEN  Loads R0,R1                               
         LA    R2,FNVARPRM                                                      
         VCALL SCNVDESC            Scan variable name                           
         IF    Z,BEGIN             Is it a valid variable name?                 
         LA    R5,FNVARPRM         Working VPARM area                           
         CLEAR R0                                                               
         LA    R1,VNENTRY                                                       
         LR    R2,R1                                                            
         IF    (VNASUB1,Z),'VCALL GETVAR'                                       
         ELSE  'VCALL GETARRAY'                                                 
         IF    Z,BEGIN             Is it declared?                              
         CLEAR R4                  Assume it's a scalar                         
         IF    VNARRAY,BEGIN       Array?  Get the requested dimensionI         
         IF    (R3,EQ,1),'LH R4,VNADIM1'                                        
         IF    (R3,EQ,2),'LH R4,VNADIM2'                                        
         IF    (R3,EQ,3),'LH R4,VNADIM3'                                        
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         VRETURN INTEGER,(R4)      Return array bounds                          
         END                                                                    
*                                                                               
*                                                                               
         TPOP  JUNK                                                             
         SCPOP ,                                                                
         PEND                                                                   
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
