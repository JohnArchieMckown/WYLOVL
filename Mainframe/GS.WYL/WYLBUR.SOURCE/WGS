WGS      TITLE 'Wylbur''s WINGS Interface'                                      
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
         GBLC  &PUBUSER,&PUBGRP                                                 
*                                                                               
WYLWGS   CSECT                                                                  
*                                                                               
WGS      IDENT 2193                15:11:57 07/12/02  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
KWR      RECORD 'KWR'                                                           
*                                                                               
UPATH    RECORD BEGIN                                                           
UPAT     UPATH                                                                  
         END                                                                    
*                                                                               
SEGCB    RECORD 'SEGCB'                                                         
*                                                                               
         COPY  SHDRARGS                                                         
         COPY  SHDSARGS                                                         
         COPY  SHCTARGS                                                         
         COPY  DSNINFO                                                          
         POP   DSECTS                                                           
WYLWGS   CSECT                                                                  
         TITLE 'Wings File Interface (Client)'                                  
*                                                                               
*  DEFINES NEEDED FOR WINGS                                                     
*                                                                               
*                                                                               
*                                                                               
*  WINGS LINE OFFSETS                                                           
*                                                                               
WGSLNHDR EQU   0,12                                                             
WGSLNLEN EQU   0,4                                                              
WGSLNLNO EQU   4,8                                                              
WGSLNTXT EQU   12                                                               
*                                                                               
WGSBKHDR EQU   0,16                                                             
WGSBKLEN EQU   4096                MAKE BIGGER !! (after testing)               
*                                                                               
*                                                                               
*  WGSWA - WINGS WORK AREA FOR WGS ROUTINES                                     
*                                                                               
*  THIS CONTROL BLOCK IS POINTED TO BY CPDEVP.                                  
*                                                                               
*                                                                               
WGSWA    RECORD BEGIN                                                           
*                                                                               
         COMMENT                   OPEN STUFF, MISC                             
WGSOHEAP DS    A                   Old heap mark                                
WGSNHEAP DS    A                   New heap mark                                
WGSPATH  DS    A                   PATH CONTROL BLOCK                           
WGSLRECL DS    F                   FILE LRECL                                   
WGSBLKSI DS    F                   FILE BLOCKSIZE                               
WGSNREC  DS    F                   NREC '(nnn)' OPTION VALUE                    
WGSPRQTY DS    F                   PRIQTY for allocate                          
WGSSCQTY DS    F                   SECQTY for allocate                          
WGSDRBLK DS    F                   DIRBLKS for allocate                         
WGSDRARG DS    A                   Pointer to SHOW DIR args                     
WGSALOFG FLAG  ,                   Allocation flags                             
         FLAG  WGSFCYL             Cylinders                                    
         FLAG  WGSFTRK             Tracks                                       
         FLAG  WGSFKB              Kilobytes                                    
         FLAG  WGSFMB              Megabytes                                    
         FLAG  WGSFMEMB            Members specified not blocks                 
WGSRECFM FLAG  ,                   FILE RECFM                                   
         FLAG  WGSFEDIT            EDIT                                         
         FLAG  WGSFF               FIXED                                        
         FLAG  WGSFV               VARIABLE                                     
WGSOPTS  FLAG  ,                   OPTIONS                                      
         FLAG  WGSFNUM             NUMBERED                                     
         FLAG  WGSFNNUM            NONUMBERED                                   
         FLAG  WGSFINT             INTEGER                                      
         FLAG  WGSFUNUM            UNNUMBERED                                   
         FLAG  WGSFSEQ             SEQUENCE FIELDV 912                          
WGSSTAT  FLAG  ,                                                                
         FLAG  WGSFEOF             EOF RECIEVED                                 
         FLAG  WGSFLUSH            FLUSHING TO EOF                              
WGSFLG0  FLAG  ,                                                                
WGSFLG1  FLAG  ,                                                                
         FLAG  WGSFWRAP            LINES NEED TO WRAP                           
WGSFLG2  FLAG  ,                                                                
WGSFLG3  FLAG  ,                                                                
WGSFLG4  FLAG  ,                                                                
WGSSEQF  DS    F                   SEQUENCE FIELD                               
WGSSEQFL DS    F                   SEQUENCE FIELD LENGTH                        
*                                                                               
         COMMENT                   READ STUFF                                   
WGRDBLK  DS    A                   READ BUFFER POINTER                          
WGRDNDX  DS    A                   INDEX INTO READ BUFFER                       
WGRDLIM  DS    A                   READ BUFFER END                              
WGRDOFF  DS    A                   OFFSET INTO READ LINE                        
WGRDLNO  DS    A                   READ LINE NUMBER, IF NOT SUPPLIED            
*                                                                               
         COMMENT                   WRITE STUFF                                  
WGWTBLK  DS    F                   WRITE SEG BUFFER (INCL. HEADER)              
WGWTBLKL DS    F                   WRITE SEG BUFFER LEN                         
WGSSG    SEGCB ,                   WINGS PATH SEG BUFFER                        
         END                                                                    
*                                                                               
         EJECT                                                                  
         COPY  FFINFO                                                           
*                                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*   WINGS FILE INTERFACE ...                                                    
*                                                                               
*                                                                               
*   WINGS IS A 'HOMEBREW' MVS FILE SERVER BUILT FOR WYLBUR.                     
*   THIS MODULE CONTAINS THE WYLBUR INTERFACE TO THE WINGS                      
*   FILE SERVER.                                                                
*                                                                               
*                                                                               
         EJECT                                                                  
         TITLE  'Get Wings work area'                                           
*                                                                               
*                                                                               
*  Create WINGS work area and initialize it.                                    
*                                                                               
*  ON EXIT:                                                                     
*  R1 Points to initialized work area.                                          
*                                                                               
*                                                                               
GETWA    PROC  ,                                                                
         VCALL MARKHEAP            Get current heap ptr                         
         LR    R2,R15                                                           
         L     R0,=AL4(L'WGSWA)                                                 
         VCALL GETHEAP                                                          
         ST    R1,CPDEVP                                                        
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
         CLEAR WGSWA                                                            
         ST    R2,WGSOHEAP         Save old heap ptr                            
*                                                                               
         COMMENT                   GET A BUFFER FOR PROCESSING                  
         COMMENT                   FILE NAME OPTIONS AND LATER                  
         COMMENT                   FOR DOING WINGS PATH WRITES.                 
         L     R0,=AL4(WGSBKLEN)                                                
         VCALL GETHEAP                                                          
         ST    R1,WGWTBLK                                                       
         ST    R0,WGWTBLKL                                                      
         A     R1,=AL4(L'WGSBKHDR)                                              
         S     R0,=AL4(L'WGSBKHDR)                                              
         SEGINIT (R1),(R0),WGSSG,RTNTYPE=A,RTN=WPSEG                            
*                                                                               
         VCALL MARKHEAP            Get current heap ptr                         
         ST    R15,WGSNHEAP        Save new heap ptr                            
*                                                                               
         LR    R1,R5               RETURN WORK AREA ADDRESS                     
         PRETURN R1                                                             
         PEND  ,                                                                
*                                                                               
         DROP  R5                                                               
         QLTORG                                                                 
         EJECT                                                                  
         TITLE  'Release Wings work area'                                       
*                                                                               
*  Release the Wings work area (if the heap hasn't                              
*  changed on us in the meantime).                                              
*                                                                               
*  On entry:                                                                    
*    CPDEVP - Points to the work area to be released.                           
*                                                                               
*  ON EXIT:                                                                     
*     If the heap hasn't changed, the work area will be                         
*     released and CPDEVP will be cleared. Otherwise,                           
*     the work area will not be freed (because we can't                         
*     shrink the heap).                                                         
*                                                                               
*                                                                               
RLSWA    PROC  ,                                                                
         L     R5,CPDEVP                                                        
         WITH  (WGSWA,R5),BEGIN                                                 
         VCALL MARKHEAP            Get current heap ptr                         
         IF    (R15,EQ,WGSNHEAP),BEGIN  We can reduce the heap...               
         L     R15,WGSOHEAP                                                     
         VCALL RELHEAP             Release WGSWA                                
         CLEAR CPDEVP                                                           
         END                                                                    
         END                                                                    
         PEND  ,                                                                
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSOPEN - WINGS OPEN                                                         
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=NZ, CPERRID CONTAINS ERROR, MSG TSEG'D                                   
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*  THIS ROUTINE MUST PROCESS FILE NAME AND OPTIONS.                             
*  THIS ROUTINE MUST GATHER INFO REQUIRED TO OPEN FILE.                         
*  THIS ROUTINE MUST OPEN A PATH TO WINGS FILE SERVER.                          
*  THIS ROUTINE MUST START UP WINGS SERVER PROTOCOL.                            
*  THIS ROUTINE MUST PROCESS FILE SERVER RETURNS.                               
*                                                                               
*  NOTE:                                                                        
*  MOST OPTIONS ARE PROCESSED BY SEG'ING THEM INTO THE WINGS                    
*  WRITE BUFFER.   THE BUFFER IS NOT (MUST NOT BE!) WRITTEN                     
*  TO WINGS UNTIL THE PATH IS OPENED.  WE DO NOT OPEN THE                       
*  PATH FIRST BECAUSE WE WANT TO CATCH ANY SYNTAX OR OPTION                     
*  ERRORS BEFORE WE GO TO THE OVERHEAD OF OPENING THE PATH.                     
*                                                                               
*  CAUTION!!                                                                    
*  FILE SYSTEM OPTIONS MAY BE SAME AS FILE OPTIONS. (EG. VOLUME=                
*  MAY BE SPECIFIED TWICE.)  WE PROCESS THE FILE SYSTEM FIRST                   
*  AND THE SPECIFIC FILE OPTIONS NEXT.  THUS, IF THERE ARE                      
*  DUPLICATE OPTIONS, THE SECOND OCCURANCE SHOULD OVERRIDE THE                  
*  FIRST.  WINGS SERVER BEWARE !                                                
*                                                                               
*                                                                               
WGSOPEN  XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    FFFREAD,'INCR R15,CVWGRDOP'                                      
         ELSE  'INCR R15,CVWGWROP'                                              
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS OPEN INFO TO WINGS                      
         COMMENT                                                                
*                                                                               
         COMMENT                   PASS NAME                                    
         IF    (FFNAMEL,EQ,0),BEGIN                                             
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WPDONE                                                           
         END                                                                    
         COMMENT                   SEG FILE_OPEN:  NAME=<FILE NAME>             
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_OPEN:      '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFNAME,L:FFNAMEL,WGSSG                                           
*                                                                               
         COMMENT                   ACCESS                                       
         SEG   ' REQTYPE=',,WGSSG                                               
         IF    FFFWRITE,BEGIN                                                   
         SEG   'WRITE',,WGSSG                                                   
         END                                                                    
         ELSEIF FFFREAD,BEGIN                                                   
         SEG   'READ',,WGSSG                                                    
         END                                                                    
         ELSEIF FFFAPPD,BEGIN                                                   
         SEG   'APPEND',,WGSSG                                                  
         END                                                                    
         ELSEIF FFFREPL,BEGIN                                                   
         SEG   'REPLACE',,WGSSG                                                 
         END                                                                    
         ELSE  BEGIN  DEFAULT IS READ                                           
         SET   FFFREAD                                                          
         SEG   'READ',,WGSSG                                                    
         END                                                                    
*                                                                               
         COMMENT                   BYTE COUNT                                   
         SEG   ' BYTECNT=',,WGSSG                                               
         L     R1,CPABYCT                                                       
         IF    CPFXMULT,'L R1,CPATBYTS'                                         
         IF    CPFRDEXE,'L R3,=A(300000)' IF EXEC, WE GUESS                     
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
*                                                                               
         COMMENT                   LINE COUNT                                   
         SEG   ' LINECNT=',,WGSSG                                               
         L     R1,CPALNCT                                                       
         IF    CPFXMULT,'L R1,CPATLINS'                                         
         IF    CPFRDEXE,'L R3,=A(2000)' IF EXEC, WE GUESS                       
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
*                                                                               
         COMMENT                   CURRENT NAME                                 
*                                                                               
*  USE PREVIOUS NAME ONLY IF WINGS OR OLDIBM TYPE                               
*  (IE. SAVE XX ON LINDY, WILL NOT PASS WINGS WITH CURRENT NAME)                
*                                                                               
         IF    ((FFPTYPE,EQ,L'FFFWINGS),OR,                            X        
               (FFPTYPE,EQ,L'FFFOLD)),BEGIN                                     
         IF    (FFPNAMEL,GT,0),BEGIN   SEND CURRENT NAME                        
         SEG   ' CNAME=',,WGSSG                                                 
         SEG   FFPNAME,L:FFPNAMEL,WGSSG                                         
         END                                                                    
         END                                                                    
* we do not check volume name here other than 6 char test.                      
* we let wings check it.  also we have a hack for adding                        
* the current volume (for 'sav *') when needed, yuck.                           
*                                                                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         ACALL CVOLHACK            CHECK FOR 'SAVE *' CUR VOL HACK              
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WPDONE                                                           
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
         COMMENT                   SET USER GROUP                               
         IF    (CPACCTSV,NZ),BEGIN                                              
         SEG   ' SETUSER=',,WGSSG                                               
         SEG   CPUSERSV,3,WGSSG                                                 
         SEG   ' SETGROUP=',,WGSSG                                              
         SEG   CPGRPSV,2,WGSSG                                                  
         END                                                                    
*                                                                               
*  ** DEBUG ** SET ACCOUNT REMOVE 7/93                                          
*         COMMENT                   SET VOL                                     
*         IF    (CPVOLUME,NE,0),BEGIN                                           
*         SEG   ' SETVOL=',,WGSSG                                               
*         SEG   CPVOLUME,L'CPVOLUME,WGSSG                                       
*         END                                                                   
*                                                                               
         COMMENT                   SET LIB                                      
         IF    (CPLIB,NE,0),BEGIN                                               
         SEG   ' SETLIB=',,WGSSG                                                
         SEGT  CPLIB,L'CPLIB,WGSSG                                              
         END                                                                    
* note:                                                                         
* we check and allow NORACF for 'set account .. pass ..' data sets.             
* Pitfall is if data set is set to not update for owner. Ahhh!                  
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES'                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF DIFFVOL OK,                               
         IF    FFFDIFFV,BEGIN                                                   
         SEG   ' VOLCHK=YES'                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF CHANGE DSN FORMAT OK                      
         IF    FFFCHFMT,BEGIN                                                   
         SEG   ' CHANGFMT=YES'                                                  
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO'                                                       
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  FILEOPTS                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WPDONE                                                           
         END                                                                    
*                                                                               
* ** DEBUG ** (nnn) for BLKSIZE=xxx is UGLY !!!                                 
* this must come after we scan the opts because we need (nnn) and               
* lrecl to calcualte the blksize=lrecl*nnn.                                     
*                                                                               
         COMMENT                   BLOCKSIZE                                    
         IF    ((WGSNREC,NE,0),AND,(WGSLRECL,NE,0)),BEGIN                       
         CLEAR R0                                                               
         L     R1,WGSLRECL                                                      
         M     R0,WGSNREC                                                       
         ST    R1,WGSBLKSI                                                      
         SEG   ' BLKSIZE=',,WGSSG                                               
         CLEAR R0                                                               
         L     R1,WGSBLKSI                                                      
         SEGDC (R1),(R0),WGSSG                                                  
         END                                                                    
*                                                                               
*  ** DEBUG ** lrecl=nn for recfm=f lrecl=nn is UGLY !!                         
*  not as ugly as (nnn) for BLKSIZE (see above) but still                       
*  ugly.  if we have lrecl and no recfm, we set recfm=f                         
         COMMENT                   DO IT                                        
         IF    ((WGSLRECL,NE,0),AND,(WGSRECFM,EQ,0)),BEGIN                      
         SET   WGSFF                                                            
         SEG   ' RECFM=F',,WGSSG                                                
         END                                                                    
*                                                                               
         COMMENT                   VALIDITY CHECK OPTIONS                       
         IF    ((WGSFINT,OR,WGSFNUM,OR,WGSFSEQ),AND,                   X        
               (WGSFV,OR,WGSFEDIT)),BEGIN                                       
         ERROR 'NUMBER/INTEGER must be used with LRECL option.'                 
         END                                                                    
         IF    ((WGSFINT,OR,WGSFNUM,OR,WGSFSEQ),AND,                   X        
               (WGSLRECL,EQ,0)),BEGIN                                           
         SET   WGSFF                                                            
         LA    R0,80                                                            
         ST    R0,WGSLRECL                                                      
         IF    FFFWRITE,BEGIN                                                   
         SEG   ' RECFM=F LRECL=80 ',,WGSSG                                      
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WPDONE                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF WRITE, WE DO NOT ALLOW ATTNS              
         COMMENT                     AFTER PATH HAS OPENED                      
         IF    (FFFWRITE,OR,FFFREPL,OR,FFFAPPD),BEGIN                           
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON WRITES             
         END                                                                    
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   WRITE FILE_OPEN: INFO                        
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS OPEN RETURN                          
         ACALL WRDOPEN                                                          
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WPDONE                                                           
         END                                                                    
*                                                                               
         CLEAR R15                                                              
*                                                                               
WPDONE   LABEL ,                                                                
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSACCES - Test file access for USE                                          
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK - File can be accessed for use                                 
*  R15=NZ, CPERRID CONTAINS ERROR                                               
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSACCES XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS OPEN INFO TO WINGS                      
         COMMENT                                                                
*                                                                               
         COMMENT                   PASS NAME                                    
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
         COMMENT                   SEG FILE_OPEN:  NAME=<FILE NAME>             
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_OPEN:      '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         SEG   ' REQTYPE=VERIFY NTYPE=EXACT',,WGSSG                             
*                                                                               
         SEG   ' VTYPE=',,WGSSG                                                 
         IF    FFFWRITE,BEGIN                                                   
         SEG   'WRITE',,WGSSG                                                   
         END                                                                    
         ELSEIF FFFREAD,BEGIN                                                   
         SEG   'READ',,WGSSG                                                    
         END                                                                    
         ELSEIF FFFAPPD,BEGIN                                                   
         SEG   'WRITE',,WGSSG                                                   
         END                                                                    
         ELSEIF FFFREPL,BEGIN                                                   
         SEG   'WRITE',,WGSSG                                                   
         END                                                                    
         ELSE  BEGIN  DEFAULT IS READ                                           
         SET   FFFREAD                                                          
         SEG   'READ',,WGSSG                                                    
         END                                                                    
*                                                                               
*                                                                               
* we do not check volume name here other than 6 char test.                      
* we let wings check it.  also we have a hack for adding                        
* the current volume (for 'sav *') when needed, yuck.                           
*                                                                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         ACALL CVOLHACK            CHECK FOR 'SAVE *' CUR VOL HACK              
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         MVC   CPERRID,=CL8'INVVOL'                                             
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
* note:                                                                         
* we check and allow NORACF for 'set account .. pass ..' data sets.             
* Pitfall is if data set is set to not update for owner. Ahhh!                  
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES'                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF DIFFVOL OK,                               
         IF    FFFDIFFV,BEGIN                                                   
         SEG   ' VOLCHK=YES'                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO'                                                       
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  FILEOPTS                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         SEGCLR .CPSEG                                                          
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON WRITES             
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   WRITE FILE_OPEN: INFO                        
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         SEGCLR .CPSEG                                                          
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_VERIFY:   ')),BEGIN                                
         MVC   CPERRID,=CL8'PATHERR'                                            
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         SEGCLR .CPSEG                                                          
         LA    R15,4                                                            
         B     WACSDONE                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WACSDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         XPOP  R15                                                              
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSTRKS - Show space on volume                                               
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK - File can be accessed for use                                 
*  R15=NZ, CPERRID CONTAINS ERROR                                               
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSTRKS  XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   SEG FILE_LSPACE:  VOL=<VOLUME>               
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_LSPACE:    '                          
*                                                                               
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
*                                                                               
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WTRKDONE                                                         
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON WRITES             
*                                                                               
         COMMENT                   WRITE FILE_LSPACE: INFO                      
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WTRKDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WTRKDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'DATA:          ')),BEGIN                                
         MVC   CPERRID,=CL8'PATHERR'                                            
         LA    R15,4                                                            
         B     WTRKDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSTPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WTRKDONE                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WTRKDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         ACALL RLSWA                                                            
         XPOP  R15                                                              
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
*                                                                               
*  We specially process the return message here                                 
*  rather than using WGSEPRT because we need to                                 
*  remove the volume name from the reply.                                       
*                                                                               
WGSTPRT  SCKW  MSG,DOTRKMSG,P                                                   
         SCKW  ERRDATA,WRTERRDT,P                                               
         SCKW  INFOMSG,WRTIMSG,P                                                
         SCKW  ,WRTOTHER                                                        
*                                                                               
*                                                                               
TRKMSGWA RECORD BEGIN                                                           
TRKMSGL  DS    F                                                                
TRKMSG   DS    CL(&LINESZ)                                                      
         END                                                                    
*                                                                               
*                                                                               
DOTRKMSG PROC  TRKMSGWA                                                         
         COMMENT                   WRITE MESSAGE TO TERMINAL                    
         SCDQUOTE TRKMSG                                                        
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         BCTR  R15,0                                                            
         CLI   0(R15),C'+'         ADDITIONAL INFORMATION?                      
         BNE   TRKMSGNP            NOPE                                         
         S     R0,=F'1'            TAKE OUT THE '+' SYMBOL                      
TRKMSGNP LABEL ,                                                                
         ST    R0,TRKMSGL                                                       
         TCCR  ,                                                                
         LA    R1,TRKMSG                                                        
         TSEG  (R1),6,CR                                                        
         L     R0,TRKMSGL                                                       
         LA    R1,TRKMSG                                                        
         S     R0,=F'9'            Remove 'VOLNNN - '                           
         LA    R1,9(,R1)                                                        
         TSEG  (R1),(R0)                                                        
         CLEAR R15                                                              
         PEND  ,                                                                
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSCRTCH - WINGS SCRATCH                                                     
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=NZ, CPERRID CONTAINS ERROR, MSG TSEG'D                                   
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSCRTCH XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WSCRDON1                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS SCRATCH INFO TO WINGS                   
         COMMENT                   PASS NAME                                    
         COMMENT              SEG FILE_SCRATCH:  NAME=<FILE NAME>               
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_SCRATCH:   '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         COMMENT                   SET NTYPE=EXACT                              
         SEG   ' NTYPE=EXACT',,WGSSG                                            
*                                                                               
*        COMMENT                   CURRENT NAME                                 
*                                                                               
*  USE PREVIOUS NAME ONLY IF WINGS OR OLDIBM TYPE                               
*  (IE. SAVE XX ON LINDY, WILL NOT PASS WINGS WITH CURRENT NAME)                
*                                                                               
*        IF    ((FFPTYPE,EQ,L'FFFWINGS),OR,                                     
*              (FFPTYPE,EQ,L'FFFOLD)),BEGIN                                     
*        IF    (FFPNAMEL,GT,0),BEGIN   SEND CURRENT NAME                        
*        SEG   ' CNAME=',,WGSSG                                                 
*        SEG   FFPNAME,L:FFPNAMEL,WGSSG                                         
*        END                                                                    
*        END                                                                    
* we do not check volume name here other than 6 char test.                      
* we let wings check it.  also we have a hack for adding                        
* the current volume (for 'sav *') when needed, yuck.                           
*                                                                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         ACALL CVOLHACK            CHECK FOR 'SAVE *' CUR VOL HACK              
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
         SEG   ' VOL=',,WGSSG                                                   
         SEG   FFHOST,L:FFHOSTL,WGSSG                                           
         END                                                                    
*                                                                               
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES',,WGSSG                                            
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO',,WGSSG                                                
         END                                                                    
*                                                                               
         COMMENT                   IF PDS SPECIFIED,                            
         IF    FFFPDS,BEGIN                                                     
         SEG   ' WHOLEPDS=YES',,WGSSG                                           
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  SCRTOPTS                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON SCRATCH            
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE SCRATCH COMMAND                     
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_SCRATCH:  ')),BEGIN                                
         VCALL LOGCMD                                                           
         CLEAR R15                                                              
         VCALL LOGEVENT                                                         
         TSEG  'WINGS path error.  Unable to scratch file.'                     
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON SCRATCH                        
         LA    R15,4                                                            
         B     WSCRDONE                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WSCRDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         ACALL RLSWA                                                            
         XPOP  R15                                                              
WSCRDON1 L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
*                                                                               
SCRTOPTS SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,CATPSH,PUSH                                                      
         SCKW RETAIN,WSNRTAIN,A                                                 
         SCKW NORETAIN,WSNNRTN,A                                                
         SCKW EXPDT,WSNEXPDT,A                                                  
         SCKW ERASE,WSNERASE,A                                                  
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSRENAM - WINGS RENAME                                                      
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION. The current file name is                            
*  in the FFNAME field and the new name is in the FFRNAME                       
*  field. Both names are fully-qulaified.                                       
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=NZ, CPERRID CONTAINS ERROR, MSG TSEG'D                                   
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSRENAM XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WRENDON1                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS RENAME INFO TO WINGS                    
         COMMENT                                                                
*                                                                               
         COMMENT                   PASS NAME                                    
         COMMENT              SEG FILE_RENAME:  NAME=<FILE NAME>                
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_RENAME:    '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFNAME,L:FFNAMEL,WGSSG                                           
*                                                                               
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
         SEG   ' NEWNAME=',,WGSSG                                               
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         COMMENT                   SET NTYPE=EXACT                              
         SEG   ' NTYPE=EXACT',,WGSSG                                            
*                                                                               
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES',,WGSSG                                            
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO',,WGSSG                                                
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  RENOPTS                                                          
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED                       
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE COMMAND                             
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_RENAME:   ')),BEGIN                                
         TSEG  'WINGS path error.  Unable to rename file.'                      
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION                                   
         LA    R15,4                                                            
         B     WRENDONE                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WRENDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         ACALL RLSWA                                                            
         XPOP  R15                                                              
WRENDON1 L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
*                                                                               
RENOPTS  SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  PGAPPEND - Append indicated line to current Page A                           
*                                                                               
*  This routine is used by the various SHOW routines to                         
*  append the indicated display line coming back from the                       
*  Wings server to the current page in the Page A register.                     
*                                                                               
*  On entry:                                                                    
*     R1,R0 hold the location and length of the line                            
*     PAR   points to the page                                                  
*                                                                               
*     Note: Since a 0 length line cannot be stored on                           
*           the page, this routine will store a line                            
*           consisting of 1 blank if R0 is 0 on input.                          
*                                                                               
*                                                                               
*  On exit:                                                                     
*     R15=0 Line appended successfully                                          
*     R15=NZ Not enough space on page                                           
*                                                                               
PGAPPEND PROC ,                                                                 
         XPUSH R0,R1                                                            
         IF    (R0,Z),BEGIN                                                     
         LA    R0,1                                                             
         LA    R1,CVBLANKS                                                      
         END                                                                    
         LH    R3,@PAR             Get count on page now                        
         AR    R3,R0                                                            
         C     R3,=A(CVPSZ-3)      That's how much there is                     
         IF    LE,BEGIN                                                         
         LH    R3,@PAR             Amount currently on page                     
         LA    R2,@R3+1(PAR)       Convert offset to address                    
         AR    R3,R0                                                            
         LA    R3,@R3+1            Store the new count                          
         STH   R3,@PAR                                                          
         LR    R3,R0                                                            
         IF    (R0,P),BEGIN                                                     
         DEX   R3,'MVC @R2+1(0),@R1'  Move the message in                       
         END                                                                    
         LA    R3,@R3+1            Prefix message with                          
         STC   R3,@R2                 a count field.                            
         LA    R3,@R3+1(R2)                                                     
         MVI   @R3,0               Store an end flag                            
         PMARK PAR                  and mark the page as changed                
         CLEAR R15                 All ok                                       
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4               Insufficient space                           
         END                                                                    
         XPOP  R0,R1                                                            
         PEND ,                                                                 
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  PGRELS   - Release all pages in a page array                                 
*                                                                               
*  This routine is called to release all the pages in an array                  
*  of pages. It's called by the SHOW commands.                                  
*                                                                               
*  On entry:                                                                    
*   R0 - Contains the number of items in the array                              
*   R1 - Points to the array of page identifiers                                
*                                                                               
*  On output:                                                                   
*   Pages released.                                                             
*                                                                               
PGRELS   PROC  ,                                                                
         IF    (R0,P),BEGIN                                                     
         LR    R4,R0                                                            
         LR    R5,R1                                                            
         CLEAR R3                                                               
         WHILE (R3,LT,R4),BEGIN                                                 
         LR    R1,R3                                                            
         SLL   R1,2                                                             
         L     R2,0(R1,R5)                                                      
         PGET  PAR,(R2)                                                         
         PFREE PAR,EMPTY                                                        
         LA    R3,1(,R3)                                                        
         END                                                                    
         END                                                                    
         PEND  ,                                                                
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'Show directory'                                                 
*                                                                               
*  WGSHODIR - WINGS SHO DIRECTORY                                               
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/OPTIONS AND                                  
*  FILE SYSTEM INFORMATION.                                                     
*  R1  Point to show directory argument and                                     
*      return block.                                                            
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=4 CPERRID CONTAINS ERROR, MSG TSEG'D                                     
*  R15 = 8 ALL OK, BUT WE RAN OUT OF PAGES TO HOLD                              
*        THE OUTPUT SO THE DIRECTORY LISTING IS                                 
*        NOT COMPLETE.                                                          
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSHODIR XPROC ,                                                                
         LR    R4,R1               Save pointer to args                         
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WDIRDON1                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         ST    R4,WGSDRARG         Save pointer to args.                        
         USING SHDRARGS,R4                                                      
*                                                                               
         COMMENT                   PASS NAME                                    
         COMMENT              SEG SHOW_DIR:  NAME=<FILE NAME>                   
         L     R3,WGWTBLK                                                       
         MVC   @R3(L'WGSBKHDR),=CL16'SHOW_DIR:       '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         COMMENT                   SET NTYPE=EXACT                              
         SEG   ' NTYPE=EXACT'                                                   
*                                                                               
* we do not check volume name here other than 6 char test.                      
* we let wings check it.  also we have a hack for adding                        
* the current volume (for 'sav *') when needed, yuck.                           
*                                                                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         ACALL CVOLHACK            CHECK FOR '*' CUR VOL HACK                   
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
         IF    (SHDRLOWL,GT,0),BEGIN                                            
         SEG   ' FROM=',,WGSSG                                                  
         SEG   SHDRLOWB,L:SHDRLOWL,WGSSG                                        
         END                                                                    
         IF    (SHDRUPL,GT,0),BEGIN                                             
         SEG   ' TO=',,WGSSG                                                    
         SEG   SHDRUPB,L:SHDRUPL,WGSSG                                          
         END                                                                    
         IF    (SHDRLIKL,GT,0),BEGIN                                            
         SEG   ' LIKE=',,WGSSG                                                  
         SEG   SHDRLIKE,L:SHDRLIKL,WGSSG                                        
         END                                                                    
*                                                                               
         IF    SHDRHEX,BEGIN                                                    
         SEG   ' FORMAT=HEX',,WGSSG                                             
         END                                                                    
         ELSEIF SHDRTAB,BEGIN                                                   
         SEG   ' FORMAT=MEMBERS',,WGSSG                                         
         END                                                                    
         ELSEIF SHDRALL,BEGIN                                                   
         IF    SHDROLD,BEGIN                                                    
         SEG   ' FORMAT=OLD',,WGSSG                                             
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   ' FORMAT=ALL',,WGSSG                                             
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   ' FORMAT=TERSE',,WGSSG                                           
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  DIROPTS                                                          
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
*                                                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' SET JCBFNATN '  NO ATTN ALLOWED ON DIR                
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE COMMAND                             
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
*                                                                               
*  Loop, processing the directory lines and storing them into                   
*  pages.                                                                       
*                                                                               
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         VCALL PGETNEW                                                          
         IF    Z,BEGIN                                                          
         ST    R0,SHDRPAGS                                                      
         MVC   SHDRPGCT,=H'1'                                                   
         MVC   0(2,PAR),=H'1'          Initialize it for PGAPPEND               
         XPOP  R0,R1                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSDRFLS                 Flush the rest                          
         END                                                                    
         END                                                                    
*                                                                               
         WHILE ((SHDRPGCT,LT,SHDRMXPG),AND,                            X        
               (R0,GE,L'WGSBKHDR),AND,                                 X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Now append the line                          
         IF    NZ,BEGIN            Oops - no room on this page                  
         LH    R3,@PAR             Adjust the page count                        
         DECR  R3                   to remove the additional                    
         STH   R3,@PAR               end-of-page marker                         
         PMARK PAR                 Mark page changed                            
         VCALL PGETNEW               and get a new one                          
         IF    Z,BEGIN                                                          
         LH    R1,SHDRPGCT                                                      
         LA    R1,1(,R1)                                                        
         STH   R1,SHDRPGCT                                                      
         SLL   R1,2                                                             
         ST    R0,SHDRPAGS-4(R1)   Save the page id                             
         MVC   0(2,PAR),=H'1'      Initialize it for PGAPPEND                   
         XPOP  R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Now append the line                          
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSDRFLS            Flush the rest                               
         END                                                                    
         END                                                                    
         ACALL WPREAD              Read the next line                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
* This loop is entered when we've allocated the maximum pages                   
* and we're adding entries to the last page.                                    
*                                                                               
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Append the line                              
         IF    NZ,BEGIN            Oops - no room on this page                  
         XPOP  R0,R1                                                            
         B     WGSDRFLS             so we'll have to flush the rest.            
         END                                                                    
         XPOP  R0,R1                                                            
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         B     WDIRLAST            Handle last message                          
*                                                                               
* We come here to flush excess directory entries.                               
*                                                                               
*                                                                               
WGSDRFLS SET   WGSFLUSH                                                         
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
WDIRLAST LABEL                                                                  
         IF    (R0,LT,L'WGSBKHDR),BEGIN                                         
         TSEG  'WINGS path error.  Unable to display directory.'                
         ACALL WPCLOSE                                                          
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHDRPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHDRPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHDRPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
*                                                                               
*   Look for Error.                                                             
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GT,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHDRPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHDRPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHDRPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
*                                                                               
* Command completed successfully, process the final message                     
* (if any) on the FILE_EOF: message.                                            
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON DIRECTORY                      
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     WDIRDONE                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL WPCLOSE                                                          
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         IF    (WGSFLUSH),BEGIN                                                 
         LA    R15,8                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
WDIRDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL RLSWA                                                            
         XPOP  R15                                                              
WDIRDON1 L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
         DROP  R4                                                               
*                                                                               
DIROPTS  SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'Show dsnames'                                                   
*                                                                               
*  WGSHODSN - WINGS SHOW DSNAMES                                                
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/OPTIONS AND                                  
*  FILE SYSTEM INFORMATION.                                                     
*  R1  Point to show dsnames  argument and                                      
*      return block.                                                            
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=4 CPERRID CONTAINS ERROR, MSG TSEG'D                                     
*  R15 = 8 ALL OK, BUT WE RAN OUT OF PAGES TO HOLD                              
*        THE OUTPUT SO THE LISTING IS                                           
*        NOT COMPLETE.                                                          
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSHODSN XPROC ,                                                                
         LR    R4,R1               Save pointer to args                         
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         ST    R4,WGSDRARG         Save pointer to args.                        
         USING SHDSARGS,R4                                                      
*                                                                               
         COMMENT                   PASS NAME                                    
         COMMENT              SEG FILE_SHOW:                                    
         L     R3,WGWTBLK                                                       
         MVC   @R3(L'WGSBKHDR),=CL16'FILE_SHOW:      '                          
*                                                                               
* we do not check volume name here other than 6 char test.                      
* we let wings check it.  also we have a hack for adding                        
* the current volume (for 'sav *') when needed, yuck.                           
*                                                                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
*        ACALL CVOLHACK            CHECK FOR 'SAVE *' CUR VOL HACK              
         IF    (FFHOSTL,NZ),BEGIN                                               
*        IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
*        TSEG  FFHOST,L:FFHOSTL                                                 
*        TSEG  ': invalid volume name.'                                         
*        LA    R15,4                                                            
*        B     WDSNDONE                                                         
*        END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
         IF    (SHDSLOWL,GT,0),BEGIN                                            
         SEG   ' FROM=',,WGSSG                                                  
         SEG   SHDSLOWB,L:SHDSLOWL,WGSSG                                        
         END                                                                    
*                                                                               
         IF    (SHDSUPL,GT,0),BEGIN                                             
         SEG   ' TO=',,WGSSG                                                    
         SEG   SHDSUPB,L:SHDSUPL,WGSSG                                          
         END                                                                    
*                                                                               
         IF    (SHDSLIKL,GT,0),BEGIN                                            
         SEG   ' LIKE=',,WGSSG                                                  
         SEG   SHDSLIKE,L:SHDSLIKL,WGSSG                                        
         IF    SHDSONLY,BEGIN                                                   
         SEG   ' ONLY=YES',,WGSSG                                               
         END                                                                    
         END                                                                    
*                                                                               
         SEG   ' NTYPE=EXACT',,WGSSG                                            
*                                                                               
         IF    (SHDSTRK,OR,SHDSTYP,OR,SHDSDAT,OR,SHDSINT),BEGIN                 
         SEG   ' CATREPT=DSCB',,WGSSG                                           
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   ' CATREPT=DSN',,WGSSG                                            
         END                                                                    
*                                                                               
*  If an exact volume name was specified, tell Wings to just look               
*  at the volume and not search the catalog for now until we've                 
*  fully converted to SMS.                                                      
*                                                                               
         IF    (FFHOSTL,EQ,6),BEGIN                                             
         SEG   ' CATTYPE=VTOC',,WGSSG                                           
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   ' CATTYPE=DISK',,WGSSG                                           
         END                                                                    
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  DSNOPTS                                                          
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
*                                                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' SET JCBFNATN '  NO ATTN ALLOWED ON DIR                
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE COMMAND                             
*                                                                               
* For debugging                                                                 
*                                                                               
*        XPUSH R15,R1                                                           
*        L     R0,WGSSGLENF                                                     
*        A     R0,=F'16'                                                        
*        L     R1,WGWTBLK                                                       
*        TSEG  (R1),(R0)                                                        
*        TWRITE                                                                 
*        XPOP  R15,R1                                                           
*                                                                               
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
*                                                                               
*  Loop, processing the lines and storing them into                             
*  pages.                                                                       
*                                                                               
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         VCALL PGETNEW                                                          
         IF    Z,BEGIN                                                          
         ST    R0,SHDSPAGS                                                      
         MVC   SHDSPGCT,=H'1'                                                   
         MVC   0(2,PAR),=H'1'          Initialize it for PGAPPEND               
         XPOP  R0,R1                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSDSFLS                 Flush the rest                          
         END                                                                    
         END                                                                    
*                                                                               
         WHILE ((SHDSPGCT,LT,SHDSMXPG),AND,                            X        
               (R0,GE,L'WGSBKHDR),AND,                                 X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Now append the line                          
         IF    NZ,BEGIN            Oops - no room on this page                  
         LH    R3,@PAR             Adjust the page count                        
         DECR  R3                   to remove the additional                    
         STH   R3,@PAR               end-of-page marker                         
         PMARK PAR                 Mark page changed                            
         VCALL PGETNEW               and get a new one                          
         IF    Z,BEGIN                                                          
         LH    R1,SHDSPGCT                                                      
         LA    R1,1(,R1)                                                        
         STH   R1,SHDSPGCT                                                      
         SLL   R1,2                                                             
         ST    R0,SHDSPAGS-4(R1)   Save the page id                             
         MVC   0(2,PAR),=H'1'      Initialize it for PGAPPEND                   
         XPOP  R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Now append the line                          
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSDSFLS            Flush the rest                               
         END                                                                    
         END                                                                    
         ACALL WPREAD              Read the next line                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
* This loop is entered when we've allocated the maximum pages                   
* and we're adding entries to the last page.                                    
*                                                                               
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Append the line                              
         IF    NZ,BEGIN            Oops - no room on this page                  
         XPOP  R0,R1                                                            
         B     WGSDSFLS             so we'll have to flush the rest.            
         END                                                                    
         XPOP  R0,R1                                                            
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         B     WDSNLAST            Handle last message                          
*                                                                               
* We come here to flush excess directory entries.                               
*                                                                               
*                                                                               
WGSDSFLS SET   WGSFLUSH                                                         
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
WDSNLAST LABEL                                                                  
         IF    (R0,LT,L'WGSBKHDR),BEGIN                                         
         TSEG  'WINGS path error.  Unable to display files.'                    
         ACALL WPCLOSE                                                          
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHDSPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHDSPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHDSPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
*                                                                               
*   Look for Error.                                                             
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GT,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHDSPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHDSPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHDSPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
*                                                                               
* Command completed successfully, process the final message                     
* (if any) on the FILE_EOF: message.                                            
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON DIRECTORY                      
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     WDSNDONE                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL WPCLOSE                                                          
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         IF    (WGSFLUSH),BEGIN                                                 
         LA    R15,8                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
WDSNDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL RLSWA                                                            
         XPOP  R15                                                              
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
         DROP  R4                                                               
*                                                                               
DSNOPTS  SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'Catalog filter routine'                                         
*                                                                               
* This routine determines whether or not the indicated catalog                  
* entry should be accepted and stored, based on criteria specified              
* in the SHOW CAT arguments.                                                    
*                                                                               
* Inputs:                                                                       
*  R0,R1 - Describe the length and location of the entry to be                  
*          checked.                                                             
*  R2    - Points to the SHOW CATALOG argument list                             
*                                                                               
* Outputs:                                                                      
*  R0,R1 - Same as on input                                                     
*  R15   - Zero if the line is accepted                                         
*          Nonzero if the line doesn't meet the criteria                        
*                                                                               
CATLINOK PROC  ,                                                                
         WITH  (DSNINFO,R1),BEGIN                                               
*                                                                               
         IF    (DSNCATTP,NE,'+'),BEGIN                                          
         WITH  (SHCTARGS,R2),BEGIN                                              
         IF    (SHCTDSK,OR,SHCTTAP),BEGIN                                       
         IF    (SHCTDSK,AND,(DSNDEVTP,EQ,'D')),'CLEAR R15'                      
         ELSEIF (SHCTTAP,AND,(DSNDEVTP,EQ,'T')),'CLEAR R15'                     
         ELSE  'LA R15,4'                                                       
         END                                                                    
         ELSE  'CLEAR R15'                                                      
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         END                                                                    
         PEND  ,                                                                
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'Show catalog'                                                   
*                                                                               
*  WGSHOCAT - WINGS SHOW CATALOG                                                
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/OPTIONS AND                                  
*  FILE SYSTEM INFORMATION.                                                     
*  R1  Point to show catalog argument and                                       
*      return block.                                                            
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=4 CPERRID CONTAINS ERROR, MSG TSEG'D                                     
*  R15 = 8 ALL OK, BUT WE RAN OUT OF PAGES TO HOLD                              
*        THE OUTPUT SO THE LISTING IS                                           
*        NOT COMPLETE.                                                          
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSHOCAT XPROC ,                                                                
         LR    R4,R1               Save pointer to args                         
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         ST    R4,WGSDRARG         Save pointer to args.                        
         USING SHCTARGS,R4                                                      
*                                                                               
         COMMENT                   PASS NAME                                    
         COMMENT              SEG FILE_SHOW:                                    
         L     R3,WGWTBLK                                                       
         MVC   @R3(L'WGSBKHDR),=CL16'FILE_SHOW:      '                          
*                                                                               
         IF    (SHCTLOWL,GT,0),BEGIN                                            
         SEG   ' FROM=',,WGSSG                                                  
         SEG   SHCTLOWB,L:SHCTLOWL,WGSSG                                        
         END                                                                    
*                                                                               
         IF    (SHCTUPL,GT,0),BEGIN                                             
         SEG   ' TO=',,WGSSG                                                    
         SEG   SHCTUPB,L:SHCTUPL,WGSSG                                          
         END                                                                    
*                                                                               
         IF    (SHCTLIKL,GT,0),BEGIN                                            
         SEG   ' LIKE=',,WGSSG                                                  
         SEG   SHCTLIKE,L:SHCTLIKL,WGSSG                                        
         END                                                                    
*                                                                               
         IF    ((SHCTUPL,GT,0),OR,                                     X        
               (SHCTLOWL,GT,0),OR,                                     X        
               (SHCTLIKL,GT,0)),BEGIN                                           
         SEG   ' NTYPE=EXACT',,WGSSG                                            
         END                                                                    
*                                                                               
*   Remove performance change. We have to ask for volume information            
*   on every request, because WYLBUR (in the DIR module) TSEGs the              
*   filenames from SHOW CAT differently, depending on whether or not            
*   the dataset is on disk, and is a standard WYLBUR dsname. Thus,              
*   we need the information about the device type.                              
*                                                                               
*         IF    (SHCTVOL,OR,SHCTDSK,OR,SHCTTAP,                         X       
*              OR,SHCTUNIT,OR,SHCTINT),BEGIN                                    
         SEG   ' CATREPT=VOL',,WGSSG                                            
*        END                                                                    
*        ELSE  BEGIN                                                            
*        SEG   ' CATREPT=DSN',,WGSSG                                            
*        END                                                                    
*                                                                               
*        IF    (SHCTDSK,AND,SHCTTAP),BEGIN                                      
*        SEG   ' CATTYPE=DATA',,WGSSG                                           
*        END                                                                    
*        ELSEIF SHCTTAP,BEGIN                                                   
*        SEG   ' CATTYPE=TAPE',,WGSSG                                           
*        END                                                                    
*        ELSE  BEGIN                                                            
*        SEG   ' CATTYPE=DISK',,WGSSG                                           
*        END                                                                    
         SEG   ' CATTYPE=ALL',,WGSSG                                            
*                                                                               
         COMMENT                   PROCESS OPTIONS                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  SHOCTOPT                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
*                                                                               
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' SET JCBFNATN '  NO ATTN ALLOWED ON CAT                
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE COMMAND                             
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
*                                                                               
*  Loop, processing the lines and storing them into                             
*  pages.                                                                       
*                                                                               
         IF    ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         VCALL PGETNEW                                                          
         IF    Z,BEGIN                                                          
         ST    R0,SHCTPAGS                                                      
         MVC   SHCTPGCT,=H'1'                                                   
         MVC   0(2,PAR),=H'1'          Initialize it for PGAPPEND               
         XPOP  R0,R1                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSCTFLS                 Flush the rest                          
         END                                                                    
         END                                                                    
*                                                                               
         WHILE ((SHCTPGCT,LT,SHCTMXPG),AND,                            X        
               (R0,GE,L'WGSBKHDR),AND,                                 X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         LA    R2,SHCTARGS                                                      
         ACALL CATLINOK                                                         
         IF    (R15,Z),BEGIN                                                    
         ACALL PGAPPEND            Now append the line                          
         IF    NZ,BEGIN            Oops - no room on this page                  
         LH    R3,@PAR             Adjust the page count                        
         DECR  R3                   to remove the additional                    
         STH   R3,@PAR               end-of-page marker                         
         PMARK PAR                 Mark page changed                            
         VCALL PGETNEW               and get a new one                          
         IF    Z,BEGIN                                                          
         LH    R1,SHCTPGCT                                                      
         LA    R1,1(,R1)                                                        
         STH   R1,SHCTPGCT                                                      
         SLL   R1,2                                                             
         ST    R0,SHCTPAGS-4(R1)   Save the page id                             
         MVC   0(2,PAR),=H'1'      Initialize it for PGAPPEND                   
         XPOP  R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         ACALL PGAPPEND            Now append the line                          
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0,R1                                                            
         B     WGSCTFLS            Flush the rest                               
         END                                                                    
         END                                                                    
         END                                                                    
         ACALL WPREAD              Read the next line                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
* This loop is entered when we've allocated the maximum pages                   
* and we're adding entries to the last page.                                    
*                                                                               
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         XPUSH R0,R1                                                            
         A     R1,=AL4(L'WGSBKHDR)     Skip over block header                   
         S     R0,=AL4(L'WGSBKHDR)                                              
         LA    R2,SHCTARGS                                                      
         ACALL CATLINOK                                                         
         IF    (R15,Z),BEGIN                                                    
         ACALL PGAPPEND            Append the line                              
         IF    NZ,BEGIN            Oops - no room on this page                  
         XPOP  R0,R1                                                            
         B     WGSCTFLS             so we'll have to flush the rest.            
         END                                                                    
         END                                                                    
         XPOP  R0,R1                                                            
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         B     WCATLAST            Handle last message                          
*                                                                               
* We come here to flush excess catalog entries.                                 
*                                                                               
*                                                                               
WGSCTFLS SET   WGSFLUSH                                                         
         WHILE ((R0,GE,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         ACALL WPREAD              Read another line                            
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
*                                                                               
WCATLAST LABEL                                                                  
         IF    (R0,LT,L'WGSBKHDR),BEGIN                                         
         TSEG  'WINGS path error.  Unable to display catalog.'                  
         ACALL WPCLOSE                                                          
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHCTPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHCTPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHCTPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
*                                                                               
*   Look for Error.                                                             
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GT,L'WGSBKHDR),AND,                                X        
               (@R1,EQ,'FILE_ERROR:     ')),BEGIN                               
         ACALL WRDERROR                                                         
*                                                                               
* Release the page array                                                        
*                                                                               
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         LH    R0,SHCTPGCT                                                      
         DECR  R0                  Adjust for page we just freed                
         LA    R1,SHCTPAGS                                                      
         ACALL PGRELS                                                           
         CLEAR SHCTPGCT                                                         
         END                                                                    
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
*                                                                               
* Command completed successfully, process the final message                     
* (if any) on the FILE_EOF: message.                                            
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON DIRECTORY                      
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     WCATDONE                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL WPCLOSE                                                          
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         IF    (WGSFLUSH),BEGIN                                                 
         LA    R15,8                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
WCATDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL RLSWA                                                            
         XPOP  R15                                                              
         L     R2,CVCURJCB                                                      
         WITH  (JCB,R2),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
         DROP  R4                                                               
*                                                                               
SHOCTOPT SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSALLOC - WINGS ALLOCATE                                                    
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=NZ, CPERRID CONTAINS ERROR, MSG TSEG'D                                   
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSALLOC XPROC ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WALLDON1                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS ALLOCATE INFO TO WINGS                  
         COMMENT                                                                
*                                                                               
         COMMENT                   PASS NAME                                    
*-                                                                              
*-  Send FILE_ALLOCATE: NAME=<filename> NTYPE=EXACT                             
*-                                                                              
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_ALLOCATE:  '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         COMMENT                   SET NTYPE=EXACT                              
         SEG   ' NTYPE=EXACT'                                                   
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         ACALL CVOLHACK            CHECK FOR 'SAVE *' CUR VOL HACK              
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
*                                                                               
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES'                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO'                                                       
         END                                                                    
*                                                                               
         COMMENT                   IF DIFFVOL OK,                               
         IF    FFFDIFFV,BEGIN                                                   
         SEG   ' VOLCHK=YES'                                                    
         END                                                                    
         COMMENT                   PROCESS OPTIONS                              
*                                                                               
*  Indicate nothing specified yet                                               
*                                                                               
         MVC   WGSPRQTY,=F'-1'                                                  
         MVC   WGSSCQTY,=F'-1'                                                  
         MVC   WGSDRBLK,=F'-1'                                                  
         SET   WGSFTRK                                                          
*                                                                               
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  ALLOOPTS                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
*                                                                               
*                                                                               
*   Check out specified allocations or set defaults, which are:                 
*                                                                               
*   For a PDS:                                                                  
*                                                                               
*        Primary quantity = 10 tracks                                           
*                                                                               
*   For a sequential dataset:                                                   
*                                                                               
*        Primary quantity = 1 track                                             
*                                                                               
*   For both, secondary quantity = 1/2 primary.                                 
*                                                                               
         LT    R15,WGSPRQTY                                                     
         IF    (NEG),BEGIN                                                      
         LT    R0,WGSDRBLK                                                      
         IF    (Z),'LA R15,1'                                                   
         ELSE  'LA R15,10'                                                      
         ST    R15,WGSPRQTY                                                     
         END                                                                    
*                                                                               
         LT    R0,WGSSCQTY                                                      
         IF    (NEG),BEGIN                                                      
         IF    WGSFTRK,BEGIN                                                    
         SRL   R15,1                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                 Can't make a good guess                      
         END                                                                    
         ST    R15,WGSSCQTY                                                     
         END                                                                    
*                                                                               
         IF    CPFVER,BEGIN        Verify (default)...                          
         IF    WGSFMB,BEGIN                                                     
         L     R15,WGSPRQTY        No. of megabytes to allocate                 
         IF    (R15,GT,50),BEGIN  Be reasonable...                              
         TNUM  (R15)                                                            
         TSEG  ' megabytes is a lot of space.',,CR                              
         SETMSG 'Are you sure'                                                  
         VCALL YESREQ                                                           
         END                                                                    
         END                                                                    
         ELSEIF WGSFKB,BEGIN                                                    
         END                                                                    
         ELSEIF WGSFCYL,BEGIN                                                   
         L     R15,WGSPRQTY        No. of cylinders to allocate                 
         IF    (R15,GT,20),BEGIN  Be reasonable...                              
         TNUM  (R15)                                                            
         TSEG  ' cylinders is a lot of space.',,CR                              
         SETMSG 'Are you sure'                                                  
         VCALL YESREQ                                                           
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         L     R15,WGSPRQTY        No. of tracks to allocate                    
         IF    (R15,GT,300),BEGIN  Be reasonable...                             
         TNUM  (R15)                                                            
         TSEG  ' tracks is a lot of space.',,CR                                 
         SETMSG 'Are you sure'                                                  
         VCALL YESREQ                                                           
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
*  Final option checking and processing                                         
*                                                                               
*                                                                               
*  ** DEBUG ** lrecl=nn for recfm=f lrecl=nn is UGLY !!                         
*  not as ugly as (nnn) for BLKSIZE (see above) but still                       
*  ugly.  if we have lrecl and no recfm, we set recfm=f                         
         COMMENT                   DO IT                                        
         IF    ((WGSLRECL,NE,0),AND,(WGSRECFM,EQ,0)),BEGIN                      
         SET   WGSFF                                                            
         SEG   ' RECFM=F',,WGSSG                                                
         END                                                                    
*                                                                               
         COMMENT                   VALIDITY CHECK OPTIONS                       
         IF    ((WGSFINT,OR,WGSFNUM,OR,WGSFSEQ),AND,                   X        
               (WGSFV,OR,WGSFEDIT)),BEGIN                                       
         ERROR 'NUMBER/INTEGER must be used with LRECL option.'                 
         END                                                                    
         IF    ((WGSFINT,OR,WGSFNUM,OR,WGSFSEQ),AND,                   X        
               (WGSLRECL,EQ,0)),BEGIN                                           
         SET   WGSFF                                                            
         LA    R0,80                                                            
         ST    R0,WGSLRECL                                                      
         SEG   ' RECFM=F LRECL=80 ',,WGSSG                                      
         END                                                                    
*                                                                               
         SEG   ' ALOCUNIT=',,WGSSG                                              
         IF    WGSFMB,BEGIN                                                     
         SEG   'MB',,WGSSG                                                      
         END                                                                    
         ELSEIF WGSFCYL,BEGIN                                                   
         SEG   'CYL',,WGSSG                                                     
         END                                                                    
         ELSEIF WGSFKB,BEGIN                                                    
         SEG   'KB',,WGSSG                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   'TRK',,WGSSG                                                     
         END                                                                    
*                                                                               
         SEG   ' PRIQTY=',,WGSSG                                                
         L     R1,WGSPRQTY                                                      
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
*                                                                               
         LT    R3,WGSSCQTY                                                      
         IF    (R3,POS),BEGIN                                                   
         SEG   ' SECQTY=',,WGSSG                                                
         CLEAR R0                                                               
         SEGDC (R3),(R0),WGSSG                                                  
         END                                                                    
*                                                                               
*  If directory blocks not specified for PDS,                                   
*  then check the allocation type. If it's tracks,                              
*  set the number of blocks to min(320,max(32,WGSPRQTY)).                       
*  If allocation is some other type, just pick 32 for                           
*  now. The user should really set it.                                          
*                                                                               
         LT    R1,WGSDRBLK                                                      
         IF    NEG,BEGIN                                                        
         IF    WGSFTRK,BEGIN                                                    
         L     R1,WGSPRQTY                                                      
         FLOOR R1,32               At least 32 dir blocks                       
         CEIL  R1,320              Be reasonable                                
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R1,32                                                            
         END                                                                    
         END                                                                    
*                                                                               
         IF    (R1,GT,0),BEGIN                                                  
         XPUSH R1                                                               
         IF    WGSFMEMB,BEGIN                                                   
         SEG   ' DIRMEMS=',,WGSSG                                               
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   ' DIRBLKS=',,WGSSG                                               
         END                                                                    
         XPOP  R1                                                               
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
         END                                                                    
         COMMENT                                                                
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON ALLOC              
*                                                                               
         COMMENT                   NOW OPEN WINGS CONNECTION                    
*                                                                               
         COMMENT                   SEND THE COMMAND                             
*                                                                               
         SEGWR ,,WGSSG                                                          
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_ALLOCATED:')),BEGIN                                
         VCALL LOGCMD                                                           
         CLEAR R15                                                              
         VCALL LOGEVENT                                                         
         TSEG  'WINGS path error.  Unable to allocate file.'                    
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSAPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON ALLOCATE                       
         LA    R15,4                                                            
         B     WALLDONE                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WALLDONE LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         ACALL RLSWA                                                            
         XPOP  R15                                                              
WALLDON1 L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
         QLTORG                                                                 
*                                                                               
ALLOOPTS SCKW TRACKS,ALLOTRK,(A,P,I),32767                                      
         SCKW SEQUENTIAL,ALLOSEQ,A                                              
         SCKW BLOCKS,ALLOBLK,(A,P,I),32767                                      
         SCKW DIRECTORY,ALLOBLK,(A,P,I),32767                                   
         SCKW MEMBERS,ALLOMEM,(A,P,I),32767                                     
         SCKW KILOBYTES,ALLOKB,(A,P,I)                                          
         SCKW KBYTES,ALLOKB,(A,P,I)                                             
         SCKW KB,ALLOKB,(A,P,I)                                                 
         SCKW MEGABYTES,ALLOMB,(A,P,I),32767                                    
         SCKW MBYTES,ALLOMB,(A,P,I),32767                                       
         SCKW MB,ALLOMB,(A,P,I),32767                                           
         SCKW CYLINDERS,ALLOCYL,(A,P,I),32767                                   
         SCKW SECONDARY,ALLOSQTY,(A,P,I)                                        
         SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,CATPSH,PUSH                                                      
         SCKW ,FORMPSH,PUSH                                                     
         SCKW ,LINEPSH,PUSH                                                     
         SCKW ,WSMSPSH,PUSH                                                     
         SCKW ,V(NOTVALID)                                                      
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSCATLG - WINGS CATALOG                                                     
*                                                                               
*  ON ENTRY:                                                                    
*  FFINFO DSECT CONTAINS FILE NAME/FORMAT/OPTIONS AND                           
*  FILE SYSTEM INFORMATION.                                                     
*  R0 = <, = or > 0 depending on whether or not the                             
*       operation is RECATLG, CATLG or UNCATLG.                                 
*                                                                               
*  ON EXIT:                                                                     
*  R15=0, ALL OK                                                                
*  R15=NZ, CPERRID CONTAINS ERROR, MSG TSEG'D                                   
*                                                                               
*  CAUTION!:                                                                    
*  SOME SCANNING ERRORS EXIT VIA CVERROR                                        
*  (THIS COULD BE CHANGED AND FIXED.)                                           
*                                                                               
*                                                                               
WGSCATLG XPROC ,                                                                
         LR    R3,R0               Save operation type                          
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFRNAMEL,EQ,0),BEGIN                                            
         MVC   CPERRID,=CL8'NOFNAME'                                            
         TSEG  'Missing file name.'                                             
         LA    R15,4                                                            
         B     WCATDON1                                                         
         END                                                                    
*                                                                               
         COMMENT                   GET CLEAR WINGS WORKAREA                     
         ACALL GETWA                                                            
         LR    R5,R1                                                            
         USING WGSWA,R5                                                         
*                                                                               
         COMMENT                   PASS NAME                                    
*-                                                                              
*-  Send FILE_CATLG: NAME=<filename> NTYPE=EXACT                                
*-                                                                              
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_CATLG:     '                          
         SEG   'NAME=',,WGSSG                                                   
         SEG   FFRNAME,L:FFRNAMEL,WGSSG                                         
*                                                                               
         COMMENT                   SET NTYPE=EXACT                              
         SEG   ' NTYPE=EXACT'                                                   
*                                                                               
         IF    (R3,LE),BEGIN       Process volume                               
         COMMENT                   PROCESS 'ON VOLUME'                          
         COMMENT                                                                
         ACALL CATHACK             IF VOL=CATLG, CLEAR IT                       
         IF    (FFHOSTL,NZ),BEGIN                                               
         IF    (FFHOSTL,NE,6),BEGIN   ALLOW no host names                       
         TSEG  FFHOST,L:FFHOSTL                                                 
         TSEG  ': invalid volume name.'                                         
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
         SEG   ' VOL='                                                          
         SEG   FFHOST,L:FFHOSTL                                                 
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK ACCESS, IF OK, SET RACF=NO             
         ACALL CKACCESS                                                         
         IF    Z,BEGIN                                                          
         SET   FFFNRACF                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF PASSWORD GIVEN,                           
         IF    FFFPASSW,BEGIN                                                   
         SEG   ' PASSCHK=YES'                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NORACF, SKIP RACF CHECK                   
         IF    FFFNRACF,BEGIN                                                   
         SEG   ' RACF=NO'                                                       
         END                                                                    
*                                                                               
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  CTLGOPTS                                                         
         SCANCHK                   * ERR EXIT TO CVNEXT INDIRECTLY              
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,=CL8'SYNTAX'                                             
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
*                                                                               
         ACALL WPOPEN              OPEN PHYSICAL PATH                           
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
*                                                                               
         SEG   ' CATLG='                                                        
         IF    (R3,Z),BEGIN                                                     
         SEG   'CAT'                                                            
         END                                                                    
         ELSEIF (R3,LT),BEGIN                                                   
         SEG   'RECAT'                                                          
         END                                                                    
         ELSE   BEGIN                                                           
         SEG   'UNCAT'                                                          
         END                                                                    
*                                                                               
         L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' SET JCBFNATN '  NO ATTN ALLOWED ON CATALOG            
*                                                                               
         SEGWR ,,WGSSG             Send the catalog command                     
*                                                                               
         COMMENT                   PROCESS THE RETURN                           
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_CATLG:    ')),BEGIN                                
         VCALL LOGCMD                                                           
         CLEAR R15                                                              
         VCALL LOGEVENT                                                         
         TSEG  'WINGS path error.  Unable to CATLG/UNCATLG file.'               
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN                                                   
         LA    R15,4                                                            
         B     WCTLGDON                                                         
         END                                                                    
*                                                                               
*  Everything OK if we get here.                                                
*                                                                               
         CLEAR R15                                                              
*                                                                               
WCTLGDON LABEL ,                                                                
         XPUSH R15                                                              
         ACALL WPCLOSE             CLOSE THE PATH                               
         ACALL RLSWA                                                            
         XPOP  R15                                                              
WCATDON1 L     R4,CVCURJCB                                                      
         WITH  (JCB,R4),' CLEAR JCBFNATN '                                      
         PEND  ,                                                                
         QLTORG                                                                 
*                                                                               
CTLGOPTS SCKW UNIT,CATUNIT,(A,P)                                                
         SCKW FILESEQ,FILESEQ,(A,P)                                             
         SCKW ,VOLPSH,PUSH                                                      
         SCKW ,NACCTPSH,PUSH                                                    
         SCKW ,V(NOTVALID)                                                      
*                                                                               
CATUNIT  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' UNIT=',,WGSSG                                                   
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
FILESEQ  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' FILESEQ=',,WGSSG                                                
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  FILE OPTION SCANNING                                                         
*                                                                               
*  WE JUST PASS MOST OPTIONS TO WINGS... SOME FORMAT OPTIONS                    
*  SET STUFF IN THE CP.                                                         
*                                                                               
*                                                                               
VOLPSH   XPUSHRTN VOLOPTS                                                       
*                                                                               
ACCTPUSH XPUSHRTN ACCTOPTS                                                      
*                                                                               
NACCTPSH XPUSHRTN NACCTOPT                                                      
*                                                                               
FORMPSH  XPUSHRTN FORMOPTS                                                      
*                                                                               
LINEPSH  XPUSHRTN LINEOPTS                                                      
*                                                                               
XFERPSH  XPUSHRTN XFEROPTS                                                      
*                                                                               
CATPSH   XPUSHRTN CATOPTS                                                       
*                                                                               
PDSPSH   XPUSHRTN PDSOPTS                                                       
*                                                                               
WSMSPSH  XPUSHRTN SMSOPTS                                                       
*                                                                               
         QLTORG                                                                 
*                                                                               
* The following table recognizes the account options                            
* but tosses them. They should have been handled by                             
* name resolution and should not be passed on to                                
* WINGS.                                                                        
*                                                                               
NACCTOPT SCKW ACCOUNT,V(SCNNOOP),(A,P)                                          
         SCKW ACCT,V(SCNNOOP),(A,P)                                             
         SCKW USER,V(SCNNOOP),(A,P)                                             
         SCKW GROUP,V(SCNNOOP),(A,P)                                            
         SCKW GRP,V(SCNNOOP),(A,P)                                              
         SCKW PUBLIC,V(SCNNOOP),A                                               
         SCKW USERID,V(SCNNOOP),P                                               
         SCKW GUEST,V(SCNNOOP),A                                                
         SCKW ,,END                                                             
*                                                                               
FORMOPTS SCKW EDIT,WSNEDIT,A                                                    
         SCKW CARD,WSNCARD,A                                                    
         SCKW PRINT,WSNPRINT,A                                                  
         SCKW LRECL,WSNLRECL,(A,P,PI),256                                       
         SCKW RECFM,WSNRECFM,P                                                  
         SCKW BLKSIZE,WSNBLKSI,(A,P,PI)                                         
         SCKW ,WSNBCALC,V(MPAREN) RECORD COUNT BLOCK SIZE CALC                  
         SCKW ,,END                                                             
*                                                                               
LINEOPTS SCKW INTEGER,WSNINT,A                                                  
         SCKW NUMBERED,WSNNUM,A                                                 
         SCKW NONUMBERED,WSNNONUM,A                                             
         SCKW UNNUMBERED,WSNUNNUM,A                                             
         SCKW SEQFLD,WSNSEQF,(A,P)                                              
         SCKW ,,END                                                             
*                                                                               
ACCTOPTS SCKW ACCOUNT,WSNACCT,(A,P)                                             
         SCKW ACCT,WSNACCT,(A,P)                                                
         SCKW USER,WSNUSER,(A,P)                                                
         SCKW GROUP,WSNGROUP,(A,P)                                              
         SCKW GRP,WSNGROUP,(A,P)                                                
         SCKW PUBLIC,WSNPUB,A                                                   
         SCKW ,,END                                                             
*                                                                               
SMSOPTS  SCKW DATACLAS,SMSDATA,(A,P)                                            
         SCKW MGMTCLAS,SMSMGMT,(A,P)                                            
         SCKW STORCLAS,SMSSTOR,(A,P)                                            
         SCKW ,,END                                                             
*                                                                               
CATOPTS  SCKW CATLG,V(SCNNOOP),A                                                
         SCKW CATALOG,V(SCNNOOP),A                                              
         SCKW NOCATLG,WSNNOCAT,A                                                
         SCKW NOCATALOG,WSNNOCAT,A                                              
         SCKW RECATLG,WSNRECAT,A                                                
         SCKW RECATALOG,WSNRECAT,A                                              
         SCKW UNCATLG,V(SCNNOOP),A                                              
         SCKW UNCATALOG,V(SCNNOOP),A                                            
         SCKW ,,END                                                             
*                                                                               
VOLOPTS  SCKW VOLUME,WSNVOL,(A,P)                                               
         SCKW TEMPORARY,WSNTEMP,A                                               
         SCKW ,,END                                                             
*                                                                               
PDSOPTS  SCKW TITLE,WSNTITLE,(A,P)                                              
         SCKW ,,END                                                             
*                                                                               
XFEROPTS SCKW TRANSPARENT,V(SCNNOOP),A                                          
         SCKW ,,END                                                             
*                                                                               
FILEOPTS SCKW ,VOLPSH,PUSH                                                      
         SCKW ,ACCTPUSH,PUSH                                                    
         SCKW ,FORMPSH,PUSH                                                     
         SCKW ,LINEPSH,PUSH                                                     
         SCKW ,PDSPSH,PUSH                                                      
         SCKW ,XFERPSH,PUSH                                                     
         SCKW ,CATPSH,PUSH                                                      
         SCKW ,WSMSPSH,PUSH                                                     
         SCKW NOENQUE,WSNOENQ,A                                                 
         SCKW ,V(NOTVALID)                                                      
         EJECT                                                                  
*                                                                               
*                                                                               
WSNOENQ  PROC  ,                                                                
         SEG  ' SHARE=YES',,WGSSG                                               
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNVOL   PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' VOL=',,WGSSG                                                    
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNACCT  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' ACCOUNT=',,WGSSG                                                
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNUSER  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' USER=',,WGSSG                                                   
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNGROUP PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' GROUP=',,WGSSG                                                  
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNTEMP  PROC  ,                                                                
         SEG   ' TEMP=YES',,WGSSG                                               
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNPUB   PROC  ,                                                                
*        SEG   ' ACCOUNT=&PUBUSER..&PUBGRP',,WGSSG                              
         SEG   ' ACCOUNT=GA.PUB',,WGSSG ** DEBUG **                             
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNEDIT  PROC  ,                   NO,NO,NO !!                                  
         SET   WGSFEDIT                                                         
         SEG   ' RECFM=EDIT',,WGSSG                                             
         PEND  ,                                                                
*                                                                               
WSNCARD  PROC  ,                                                                
         SET   WGSFF                                                            
         LA    R0,80                                                            
         ST    R0,WGSLRECL                                                      
         SEG   ' RECFM=F LRECL=80',,WGSSG                                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNPRINT PROC  ,                                                                
         SET   WGSFF                                                            
         LA    R0,133                                                           
         ST    R0,WGSLRECL                                                      
         SEG   ' RECFM=F LRECL=133',,WGSSG                                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNLRECL PROC  ,                                                                
         ST    R0,WGSLRECL                                                      
         SEG   ' LRECL=',,WGSSG                                                 
         L     R1,WGSLRECL                                                      
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
         PEND  ,                                                                
*                                                                               
WSNRECFM PROC  ,                                                                
         SCUPTOK R1                                                             
         IF    ((@R1,EQ,'F '),OR,(@R1,EQ,'FB ')),BEGIN                          
         SET   WGSFF                                                            
         SEG   ' RECFM=F',,WGSSG                                                
         END                                                                    
         ELSEIF ((@R1,EQ,'FA '),OR,(@R1,EQ,'FBA ')),BEGIN                       
         SET   WGSFF                                                            
         SEG   ' RECFM=FA',,WGSSG                                               
         END                                                                    
         ELSEIF ((@R1,EQ,'V '),OR,(@R1,EQ,'VB ')),BEGIN                         
         SET   WGSFV                                                            
         SEG   ' RECFM=V',,WGSSG                                                
         END                                                                    
         ELSEIF ((@R1,EQ,'VA '),OR,(@R1,EQ,'VBA ')),BEGIN                       
         SET   WGSFV                                                            
         SEG   ' RECFM=VA',,WGSSG                                               
         END                                                                    
         ELSEIF (@R1,EQ,'EDIT '),BEGIN                                          
         SET   WGSFEDIT                                                         
         SEG   ' RECFM=EDIT'                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  (R1),(R0)                                                        
         ERROR ': invalid RECFM.'                                               
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNBLKSI PROC  ,                                                                
*** NO   SET   WGSFF               *** 5.1.92                                   
         ST    R0,WGSBLKSI                                                      
         SEG   ' BLKSIZE=',,WGSSG                                               
         L     R1,WGSBLKSI                                                      
         CLEAR R0                                                               
         SEGDC (R1),(R0),WGSSG                                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNBCALC PROC  ,                   GET RECORD COUNT '(nnn)'                     
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         DECR  R3                                                               
         IF    ((R0,LE,2),OR,(@R1,NE,'('),OR,(@R3,NE,')')),BEGIN                
         VCALL NOTVALID                                                         
         END                                                                    
         INCR  R1                                                               
         DECR  R0                                                               
         DECR  R0                                                               
         VCALL LRTRIM                                                           
         VCALL DTB                                                              
         IF    Z,BEGIN                                                          
         ST    R15,WGSNREC                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         VCALL NOTVALID                                                         
         END                                                                    
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNTITLE PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG   ' TITLE=',,WGSSG                                                 
         XPOP  R0,R1                                                            
         SEG   (R1),(R0),WGSSG                                                  
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
ALLOBLK  PROC  ,                                                                
         CLEAR WGSFMEMB                                                         
         ST    R0,WGSDRBLK                                                      
         PEND  ,                                                                
*                                                                               
ALLOMEM  PROC  ,                                                                
         SET   WGSFMEMB                                                         
         ST    R0,WGSDRBLK                                                      
         PEND  ,                                                                
*                                                                               
ALLOTRK  PROC  ,                                                                
         CLEAR WGSFCYL+WGSFKB+WGSFMB                                            
         SET   WGSFTRK                                                          
         ST    R0,WGSPRQTY                                                      
         PEND  ,                                                                
*                                                                               
ALLOCYL  PROC  ,                                                                
         CLEAR WGSFTRK+WGSFKB+WGSFMB                                            
         SET   WGSFCYL                                                          
         ST    R0,WGSPRQTY                                                      
         PEND  ,                                                                
*                                                                               
ALLOKB   PROC  ,                                                                
         CLEAR WGSFCYL+WGSFMB+WGSFTRK                                           
         SET   WGSFKB                                                           
         ST    R0,WGSPRQTY                                                      
         PEND  ,                                                                
*                                                                               
ALLOMB   PROC  ,                                                                
         CLEAR WGSFCYL+WGSFKB+WGSFTRK                                           
         SET   WGSFMB                                                           
         ST    R0,WGSPRQTY                                                      
         PEND  ,                                                                
*                                                                               
ALLOSQTY PROC  ,                                                                
         ST    R0,WGSSCQTY                                                      
         PEND  ,                                                                
*                                                                               
ALLOSEQ  PROC  ,                                                                
         CLEAR WGSDRBLK                                                         
         PEND  ,                                                                
*                                                                               
SMSDATA  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' DATACLAS=',,WGSSG                                               
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SMSMGMT  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' MGMTCLAS=',,WGSSG                                               
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SMSSTOR  PROC  ,                                                                
         XPUSH R0,R1                                                            
         SEG  ' STORCLAS=',,WGSSG                                               
         XPOP R0,R1                                                             
         SEG  (R1),(R0),WGSSG                                                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNRTAIN PROC  ,                                                                
         SEG   ' RETAIN=YES',,WGSSG                                             
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNNRTN  PROC  ,                                                                
         SEG   ' RETAIN=NO',,WGSSG                                              
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNEXPDT PROC  ,                                                                
         SEG   ' OVRD=YES',,WGSSG                                               
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNERASE PROC  ,                                                                
         SEG   ' ERASE=YES',,WGSSG                                              
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WSNINT   PROC  ,                                                                
         SET   WGSFINT                                                          
         PEND  ,                                                                
*                                                                               
WSNNUM   PROC  ,                                                                
         SET   WGSFNUM                                                          
         PEND  ,                                                                
*                                                                               
WSNNONUM PROC  ,                                                                
         SET   WGSFNNUM                                                         
         PEND  ,                                                                
*                                                                               
WSNUNNUM PROC  ,                                                                
         SET   WGSFUNUM                                                         
         PEND  ,                                                                
*                                                                               
WSNNOCAT PROC  ,                                                                
*        IF    CPSFSPR,BEGIN                                                    
         SEG   ' CAT=NO',,WGSSG                                                 
         CLEAR R15                                                              
*        END                                                                    
*        ELSE  BEGIN                                                            
*        VCALL NOTVALID   Does not return                                       
*        END                                                                    
         PEND  ,                                                                
*                                                                               
WSNRECAT PROC  ,                                                                
* Don't log the use of RECAT. You'll fill up                                    
* the WLOG every time WYLBUR goes down and                                      
* zillions of active files get recovered,                                       
* because the SAVE command specified RECAT                                      
* for now...                                                                    
*         VCALL LOGCMD                                                          
         SEG   ' CAT=RECAT',,WGSSG                                              
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*  SCAN SEQFLD=                                                                 
*                                                                               
WSNSEQF  PROC  ,                                                                
         USING NSCNCB,R2                                                        
*                                                                               
         COMMENT                   SEQUENCE FIELD GIVEN                         
         SET   WGSFSEQ                                                          
*                                                                               
         COMMENT                   SAVE CURRENT SCAN                            
         SCPUSH ,                                                               
*                                                                               
         COMMENT                   SET UP NEW SCAN                              
         IF    (@R1,EQ,'('),BEGIN                                               
         INCR  R1                                                               
         DECR  R0                                                               
         DECR  R0                                                               
         END                                                                    
         SCINIT (R1),(R0)                                                       
*                                                                               
         COMMENT                   SCAN 1ST SEQFLD ARG                          
         SCAN  ,                                                                
         IF    ((R15,NZ),OR,(R0,NP)),SSEQBAD                                    
         SCUPTOK R1                                                             
         IF    (@R1,EQ,'END '),BEGIN                                            
         L     R3,=F'-1'                                                        
         ST    R3,WGSSEQF                                                       
         LA    R3,8                                                             
         ST    R3,WGSSEQFL                                                      
         END                                                                    
         ELSEIF NSCNFINT,BEGIN                                                  
         L     R3,NSCNTVAL                                                      
         IF    ((R3,NP),OR,(R3,GT,&LINESZ)),SSEQBAD                             
         ST    R3,WGSSEQF                                                       
         LA    R3,8                                                             
         ST    R3,WGSSEQFL                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         B     SSEQBAD                                                          
         END                                                                    
*                                                                               
         COMMENT                   SCAN 2ND SEQFLD ARG                          
         SCAN  ,                                                                
         IF    (R15,NZ),SSEQBAD                                                 
         IF    (R0,NZ),BEGIN       IF 2ND ARG                                   
         IF    NSCNFINT,BEGIN                                                   
         L     R3,NSCNTVAL                                                      
         IF    ((R3,NP),OR,(R3,GT,9)),SSEQBAD                                   
         ST    R3,WGSSEQFL                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         B     SSEQBAD                                                          
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   ONLY 2 ARGS ALLOWED                          
         SCAN  ,                                                                
         IF    (R15,NZ),SSEQBAD                                                 
         IF    (R0,POS),SSEQBAD                                                 
*                                                                               
         CLEAR R15                                                              
         SCPOP ,                                                                
         B     SSEQDONE                                                         
*                                                                               
SSEQBAD  LABEL ,                                                                
         SCPOP ,                                                                
         SCSEGCLR                                                               
         SCSEG 'Invalid sequence field specification.'                          
         L     R15,=F'-4'                                                       
         B     SSEQDONE                                                         
*                                                                               
SSEQDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R2                                                               
         DROP  R6                                                               
         DROP  R5                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  CATHACK, IF VOL=CATLG,,,                                                     
*                                                                               
*  WE CLEAR VOLUME AND PREFIX VOLUME.  THE REASON WE                            
*  CLEAR THE PREFIX VOLUME IS SO THAT THE PREFIX VOLUME                         
*  IS NOT USED AS A DEFAULT WHEN THE USER WANTS VOL=CAT                         
*                                                                               
CATHACK  PROC  ,                                                                
*                                                                               
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         IF    (FFHOSTL,NZ),BEGIN                                               
         SCPUSH                                                                 
         SCCLR                                                                  
         SCINIT FFHOST,L:FFHOSTL                                                
         SCAN  CATHPRT                                                          
         COMMENT                   IF VOL=CATLG                                 
         IF    (R15,EQ,4),BEGIN                                                 
         CLEAR FFHOSTL                                                          
         CLEAR FFPHOSTL                                                         
         END                                                                    
         SCPOP                                                                  
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
*                                                                               
CATHPRT  SCKW  CATLG,CTFOUND,A                                                  
         SCKW  CATALOG,CTFOUND,A                                                
         SCKW  ,CTOTHER,NULL                                                    
         SCKW  ,CTOTHER                                                         
*                                                                               
CTFOUND  PROC                                                                   
         LA    R15,4                                                            
         PEND                                                                   
*                                                                               
CTOTHER  PROC                                                                   
         LA    R15,8                                                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  CVOLHACK - CURRENT VOLUME HACK                                               
*                                                                               
*  IF WE HAD 'USE X ON SYSXXX' FOLLOWED BY A                                    
*  'SAVE *' OR A 'USE *' ETC.  WE MUST USE THE                                  
*  PREFIX VOLUME SYSXXX.  THAT IS WHAT THIS HACK                                
*  DOES.                                                                        
*                                                                               
*  APPLY HACK IF, 1. NAME IS *, 2. NO VOLUME IS SPECIFIED,                      
*  AND 3. IF PREFIX VOLUME EXISTS.                                              
*                                                                               
*  HACK IS TO MOVE PREFIX VOLUME IN AS A SPECIFIED VOLUME.                      
*  UGG.  EFFECTIVE THOUGH.                                                      
*                                                                               
*  YUCK2,,, THIS ROUTINE MAY EXIT DIRECTLY IF PROMPT IS                         
*  NOT ANSWERED PROPERLY FOR 'SAV XXX#*', VERY UNUSUAL CASE                     
*  BUT STILL A YUCK2                                                            
*                                                                               
CVOLHACK PROC  ,                                                                
*                                                                               
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   CHECK FOR '*' IN FILE NAME                   
         L     R0,FFNAMEL                                                       
         LA    R1,FFNAME                                                        
         CLEAR R2                                                               
         WHILE (R0,POS),BEGIN                                                   
         IF    (@R1,EQ,'*'),' LA R2,1 '                                         
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
*                                                                               
         IF    (R2,POS),BEGIN      IF '*' IN FILE NAME                          
         IF    (FFHOSTL,Z),BEGIN   IF NO VOL SPECIFIED                          
* *** DEBUG IF VOL=CATALOG, WE HOSE OURSELVES !                                 
* BECAUSE CAT IS TREATED AS NO VOLUME, CAT SHOULD CLEAR PREFIX VOL              
         IF    (FFPHOSTL,NZ),BEGIN IF PREFIX VOLUME,                            
         LA    R1,FFNAME                                                        
         IF    (@R1,EQ,'*'),BEGIN                                               
         MVC   FFHOSTL,FFPHOSTL                                                 
         MVC   FFHOST,FFPHOST                                                   
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  'OK to use volume '                                              
         TSEG  FFPHOST,L:FFPHOSTL                                               
         CLEAR R0                                                               
         VCALL YESREQ                                                           
         MVC   FFHOSTL,FFPHOSTL                                                 
         MVC   FFHOST,FFPHOST                                                   
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  WGSREAD - WINGS READ                                                         
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - BUFFER LOC,LEN TO READ LINE INTO                                   
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - LINE TEXT                                                          
*    R2 - LINE NUMBER                                                           
*    R15=0 IF ALL OK,  R15=NZ IF END OF FILE                                    
*                                                                               
*  NOTE:                                                                        
*  THE BUFFER LENGTH (PASSED IN R0) IS CURRENTLY                                
*  IGNORED.  BUFFER MUST BE &LINESZ+16 OR BIGGER.                               
*                                                                               
*                                                                               
*                                                                               
WGSREAD  XPROC ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         IF    WGSFEOF,'B WREADEOF'                                             
*                                                                               
         LR    R4,R1               R4,R5 - RETURN BUFFER LOC, LEN               
         LR    R5,R0                                                            
*                                                                               
         COMMENT                   READ A NEW BLOCK IF NEEDED                   
         WHILE ((WGRDBLK,EQ,0),OR,(WGRDNDX,GE,WGRDLIM)),BEGIN                   
         ACALL WRDREAD                                                          
         IF    NZ,BEGIN                                                         
         SET   WGSFEOF                                                          
         B     WREADEOF                                                         
         END                                                                    
         AR    R0,R1                                                            
         ST    R0,WGRDLIM                                                       
         LA    R1,L'WGSBKHDR(R1)                                                
         ST    R1,WGRDBLK                                                       
         ST    R1,WGRDNDX                                                       
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR LINE WRAP PROCESSING               
         L     R1,WGRDNDX          R0 - LINE LEN                                
         L     R0,WGSLNLEN(R1)                                                  
         S     R0,=AL4(L'WGSLNHDR)                                              
         IF    (R0,NEG),BEGIN                                                   
         ERROR 'WINGS format error.  Unable to read file.',,ERROR               
         END                                                                    
         LA    R2,&LINESZ                                                       
         A     R2,WGSSEQFL                                                      
         IF    (R0,GT,R2),BEGIN  IF LONG LINE, SET WRAP                         
         SET   WGSFWRAP                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF WRAPPED LINES, GO PROCESS                 
         IF    WGSFWRAP,BEGIN                                                   
         LR    R1,R4                                                            
         LA    R0,R5                                                            
         ACALL WRWRAP                                                           
         END                                                                    
*                                                                               
         COMMENT                   IF INT, NUM, OR SEQFLD,                      
         COMMENT                     UNFORMAT LINE                              
         ELSEIF    (WGSFINT,OR,WGSFNUM,OR,WGSFSEQ),BEGIN                        
         LR    R1,R4                                                            
         LR    R0,R5                                                            
         ACALL WRFORMAT                                                         
         END                                                                    
*                                                                               
         COMMENT                   OTHERWISE DO STANDARD READ                   
         ELSE  BEGIN                                                            
         LR    R1,R4                                                            
         LR    R0,R5                                                            
         ACALL WRNORMAL                                                         
         END                                                                    
*                                                                               
         PRETURN (R0,R2)                                                        
         CLEAR R15                                                              
         B     WGSRDONE                                                         
*                                                                               
*                                                                               
WREADEOF LABEL ,                                                                
         LA    R15,4                                                            
         B     WGSRDONE                                                         
*                                                                               
WGSRDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSWRITE - WINGS WRITE ROUTINE                                               
*                                                                               
*  WE USE SEG WRITE TO WRITE TO WINGS OUTPUT BUFFER.                            
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - LINE TO WRITE                                                      
*    R2 - LINE NUMBER TO WRITE                                                  
*                                                                               
*  ON EXIT:                                                                     
*    IF ERROR, WE EXIT DIRECTLY                                                 
*                                                                               
*  NOTE:                                                                        
*  THIS ROUTINE WRITES ONLY LINES (IE. FILE TEXT).                              
*  TO WRITE STRAIGHT WINGS STUFF, YOU CAN SEG IT                                
*  TO THE WINGS BUFFER. OR DIRECTLY WRITE IT IF                                 
*  YOU CALL WPWRITE (WINGS PATH WRITE). IF YOU WRITE                            
*  DIRECTLY, THE BUFFER STUFF IS BYPASSED AND MAY BE                            
*  OUT OF ORDER.                                                                
*                                                                               
*  NOTE:                                                                        
*  EACH LINE IS WRITTEN TO WINGS AS 4 BYTES TOTAL LENGTH,                       
*  8 BYTES LINE NUMBER IN CHAR FORMAT, N BYTES TEXT.                            
*                                                                               
WGSWWA   RECORD BEGIN                                                           
WGSWLLEN DS    F                                                                
WGSWLINE DS    XL(2*&LINESZ+12)                                                 
         END                                                                    
*                                                                               
*                                                                               
WGSWRITE XPROC WGSWWA                                                           
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   PROCCESS FILE FORMATS                        
         COMMENT ,                 (EG. CARD NUMBERED)                          
         LA    R15,WGSWLINE+L'WGSLNHDR                                          
         ACALL WWFORMAT                                                         
         A     R0,=AL4(L'WGSLNHDR)                                              
         ST    R0,WGSWLLEN         CALC ACTUAL TOTAL LEGTH                      
*                                                                               
         COMMENT                   SAVE LENGTH AT START OF RECORD               
         ST    R0,WGSWLINE+WGSLNLEN                                             
*                                                                               
         COMMENT                   LINE NUMBER IN 8 BYTE FIELD                  
         LA    R1,WGSWLINE+WGSLNLNO                                             
         LA    R0,8                                                             
         LR    R15,R2                                                           
         VCALL BTDZEROS                                                         
*                                                                               
         COMMENT                   WRITE LINE RECORD                            
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_WRITE:     '                          
         LA    R1,WGSWLINE                                                      
         L     R0,WGSWLLEN                                                      
         SEG   (R1),(R0),WGSSG                                                  
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WGSCLOSE - WINGS CLOSE ...                                                   
*                                                                               
*                                                                               
*                                                                               
*  WE FINISH UP END OF WINGS PROTOCOL.  THE PROTOCOL IS DIFFERENT               
*  FOR READ VS. WRITE.  SO THERE ARE TWO VERSIONS OF CODE BELOW.                
*                                                                               
*                                                                               
*  NOTE:                                                                        
*  THIS ROUTINE WILL INDIRECTLY TSEG ANY MSG PASSED TO IT ON THE                
*  'FILE_EOF:  ' MESSAGE.   SEE WRDEOF ROUTINE.                                 
*                                                                               
*  NOTE:                                                                        
*  IF FILE IS ALREADY CLOSED.  THEN WE DO NOT CLOSE IT AGAIN.                   
*                                                                               
*                                                                               
WGSCLOSE XPROC ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
         L     R5,CPFINFOP                                                      
         USING FFINFO,R5                                                        
*                                                                               
         COMMENT                   IF PATH ALREADY CLOSED, DONE                 
         ACALL WPCHECK                                                          
         IF    NZ,WGSCDONE                                                      
*                                                                               
         COMMENT                   READ PROTOCOL                                
         COMMENT                   W/SUCCESSFUL EOF RECEIVED                    
         IF    (FFFREAD,AND,WGSFEOF),BEGIN                                      
         COMMENT SEND EOF MSG                                                   
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_EOF:       '                          
         SEGWR ,,WGSSG                                                          
         COMMENT SEND CLOSE MSG                                                 
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_CLOSE:     '                          
         SEGWR ,,WGSSG                                                          
         COMMENT READ CLOSE MSG                                                 
         ACALL WRDCLOSE                                                         
         END                                                                    
*                                                                               
         COMMENT                   WRITE (APPEND/REPLACE) PROTOCOL              
         IF    (FFFWRITE,OR,FFFAPPD,OR,FFFREPL),BEGIN                           
         COMMENT FORCE OUT PREV 'FILE_WRITE:     ' SEGS                         
         SEGWR ,,WGSSG                                                          
         COMMENT SEND EOF MSG                                                   
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_EOF:       '                          
         SEGWR ,,WGSSG                                                          
         COMMENT READ EOF MSG                                                   
         ACALL WRDEOF                                                           
         COMMENT SEND CLOSE MSG                                                 
         L     R4,WGWTBLK                                                       
         MVC   @R4(L'WGSBKHDR),=CL16'FILE_CLOSE:     '                          
         SEGWR ,,WGSSG                                                          
         COMMENT READ CLOSE MSG                                                 
         ACALL WRDCLOSE                                                         
         END                                                                    
*                                                                               
         COMMENT                   CLOSE PATH                                   
         ACALL WPCLOSE                                                          
*                                                                               
WGSCDONE LABEL ,                                                                
         CLEAR R15                                                              
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  CKACCESS - CHECK FOR DSN ACCESS                                              
*                                                                               
*  WE CHECK IF USER REALLY HAS ACCESS TO THIS DATA                              
*  SET BASEED ON 'SET ACCT ... PASS ..' AND 'SET AUTH'                          
*  ETC.                                                                         
*                                                                               
*  NOTE:                                                                        
*  IF NO ACCOUNT ASSOCIATED WITH THE DATA SET, THEN                             
*  NO ACCESS IS GRANTED.                                                        
*                                                                               
*  ON EXIT:                                                                     
*    R15 - ZERO, ACCESS ALLOWED                                                 
*    R15 - NZ, NO ACCESS ALLOWED                                                
*                                                                               
*                                                                               
*                                                                               
CKACWA   RECORD BEGIN                                                           
CKACGRP  DS    CL2                                                              
CKACUSER DS    CL3                                                              
         END                                                                    
*                                                                               
*                                                                               
CKACCESS PROC  CKACWA                                                           
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   IF NOT WINGS, ERR                            
         COMMENT ,                 (IE. NO 'NOTYOURS' FOR NET,                  
         IF    ^FFFWINGS,BEGIN      SAMSON, ETC.)                               
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   GET ACCOUNT FOR DATA SET WE                  
         COMMENT                   ARE TRYING TO ACCESS.                        
         VCALL NRESOLVE            GET FULL NAME                                
         SETMSG FFRNAME,L:FFRNAMEL                                              
         IF    ((R0,GT,11),AND,(@R1,EQ,'WYL.'),AND,                    X        
               (@R1+6,EQ,'.'),AND,(@R1+10,EQ,'.')),BEGIN                        
         MVC   CKACGRP,@R1+4                                                    
         MVC   CKACUSER,@R1+7                                                   
         END                                                                    
*                                                                               
         COMMENT                   CHECK 'SET ACC .. PASS ..'                   
         IF ((CKACGRP,EQ,CPGRPSV),AND,(CKACUSER,EQ,CPUSERSV)),BEGIN             
         IF    CPGFLG3.CPFKWENT,BEGIN    PASS SPECIFIED                         
         CLEAR R15                                                              
         B     CKACEXIT                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR SET AUTH                           
         IF ((CKACGRP,EQ,CPAUTHGP),AND,(CKACUSER,EQ,CPAUTHUS)),BEGIN            
         CLEAR R15                                                              
         B     CKACEXIT                                                         
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR PACCT (SOMEDAY ??)                 
*        IF    ((CKACGRP,EQ,CPPGRP),AND,(CKACUSER,EQ,CPPUSER)),BEGIN            
*        IF    CPGFLG5.CPFKWENT,BEGIN    PASS SPECIFIED                         
*        CLEAR R15                                                              
*        B     CKACEXIT                                                         
*        END                                                                    
*        END                                                                    
*                                                                               
         COMMENT                   FOR ALL OTHERS, WE MUST PROMPT               
         LA    R15,4                                                            
*                                                                               
CKACEXIT LABEL ,                                                                
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRNORMAL - REGULAR WINGS READ                                                
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - BUFFER LOC,LEN TO READ LINE INTO                                   
*    WGSNDX - PTR TO WINGS FORMAT LINE                                          
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - LINE TEXT                                                          
*    R2 - LINE NUMBER                                                           
*    R15 = ALWAYS RETURN ZERO                                                   
*                                                                               
*  NOTE:                                                                        
*  THE BUFFER LENGTH (PASSED IN R0) IS CURRENTLY                                
*  IGNORED.  BUFFER MUST BE &LINESZ+16 OR BIGGER.                               
*                                                                               
*                                                                               
*                                                                               
WRNORMAL PROC ,                                                                 
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   MOVE TEXT                                    
         LR    R2,R1               R2,R3 - OUTPUT LOC LEN                       
         L     R4,WGRDNDX          R4,R5 - INPUT LINE LOC, LEN                  
         L     R5,WGSLNLEN(R4)                                                  
         LA    R4,WGSLNTXT(R4)     ADJUST FOR START WGS LINE HDR                
         S     R5,=AL4(L'WGSLNHDR)                                              
         IF    (R5,NEG),BEGIN                                                   
         ERROR 'WINGS format error.  Unable to read file.',,ERROR               
         END                                                                    
         LR    R0,R5               R0 - LENGTH OF TEXT MOVED                    
         LR    R3,R5                                                            
         MVCL  R2,R4               MOVE TEXT                                    
*                                                                               
         COMMENT                   CALC LINE NUMBER IN R2                       
         XPUSH R0,R1                                                            
         L     R1,WGRDNDX                                                       
         CLC   WGSLNLNO(L'WGSLNLNO,R1),=XL(L'WGSLNLNO)'00'                      
         IF    NE,BEGIN            IF LINENO, GET IT                            
         LA    R1,WGSLNLNO(R1)                                                  
         LA    R0,L'WGSLNLNO                                                    
         VCALL GTINTVAL                                                         
         IF    (R15,NEG),BEGIN                                                  
         ERROR 'WINGS format error.  Unable to read file.',,ERROR               
         END                                                                    
         LR    R2,R15                                                           
         END                                                                    
         ELSE  BEGIN               ELSE IF NO LINENO, WE SUPPLY ONE             
         L     R2,WGRDLNO                                                       
         A     R2,CPDELTA                                                       
         ST    R2,WGRDLNO                                                       
         END                                                                    
         XPOP  R0,R1                                                            
*                                                                               
         COMMENT                   BUMP BLOCK POINTERS                          
         L     R4,WGRDNDX                                                       
         L     R3,WGSLNLEN(R4)     BUMP NDX                                     
         A     R3,WGRDNDX                                                       
         ST    R3,WGRDNDX                                                       
*                                                                               
         PRETURN (R0,R2)                                                        
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRWRAP - WINGS READ, PROCESS WRAPPED READS                                   
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - BUFFER LOC,LEN TO READ LINE INTO                                   
*    WGSNDX - PTR TO WINGS FORMAT LINE                                          
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - LINE TEXT                                                          
*    R2 - LINE NUMBER                                                           
*    R15 = ALWAYS RETURN ZERO                                                   
*                                                                               
*  NOTE:                                                                        
*  THE BUFFER LENGTH (PASSED IN R0) IS CURRENTLY                                
*  IGNORED.  BUFFER MUST BE &LINESZ+16 OR BIGGER.                               
*                                                                               
*  HEY: !!                                                                      
*  FOR NOW ** WE DO NOT WRAP ** WE TRUNCATE **                                  
*  THIS IS WHAT OLDIBM DOES FOR VBS FILES.  TO                                  
*  START WRAPPING AT 235 INSTEAD OF TRUNCATING                                  
*  SEE ** DEBUG ** COMMENTS BELOW.                                              
*                                                                               
*                                                                               
WRWRAP   PROC ,                                                                 
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   MOVE TEXT                                    
         LR    R2,R1               R2,R3 - OUTPUT LOC LEN                       
         L     R4,WGRDNDX          R4,R5 - INPUT LINE LOC, LEN                  
         L     R5,WGSLNLEN(R4)                                                  
         LA    R4,WGSLNTXT(R4)     ADJUST FOR START WGS LINE HDR                
         S     R5,=AL4(L'WGSLNHDR)                                              
         A     R4,WGRDOFF           ADJUST FOR LINE OFFSET                      
         S     R5,WGRDOFF                                                       
         IF    (R5,NEG),BEGIN                                                   
         ERROR 'WINGS format error.  Unable to read file.',,ERROR               
         END                                                                    
         IF    (R5,GT,&LINESZ),BEGIN   USE A MAXIMUM OF &LINESZ TEXT            
         LA    R5,&LINESZ                                                       
         END                                                                    
         LR    R0,R5               R0 - LENGTH OF TEXT MOVED                    
         LR    R3,R5                                                            
         MVCL  R2,R4               MOVE TEXT                                    
*                                                                               
         COMMENT                   BUMP BLOCK POINTERS                          
         L     R2,WGRDOFF          GET, BUMP OFFSET                             
         AR    R2,R0                                                            
         ST    R2,WGRDOFF                                                       
         L     R2,WGRDNDX          GET TOTAL TEXT LEN IN R3                     
         L     R3,WGSLNLEN(R2)                                                  
         S     R3,=AL4(L'WGSLNHDR)                                              
* ** DEBUG **    TO WRAP AND NOT TRUNCATE DO THE FOLLOWING:                     
* ** DEBUG **    DELETE (COMMENT OUT) FOLLOWING LINE:                           
         IF    (R0,EQ,R0),BEGIN    ALWAYS BUMP TO NEXT RECORD                   
* ** DEBUG **    TO WRAP AND NOT TRUNCATE DO THE FOLLOWING:                     
* ** DEBUG **    ADD (UNCOMMENT OUT) FOLLOWING LINE:                            
* ***    IF    (WGRDOFF,GE,R3),BEGIN  IF AT END OF RECORD                       
         CLEAR WGRDOFF             CLEAR OFFSET                                 
         L     R3,WGSLNLEN(R2)     BUMP NDX                                     
         A     R3,WGRDNDX                                                       
         ST    R3,WGRDNDX                                                       
         END                                                                    
*                                                                               
         COMMENT                   CALCULATE LINE NUMBER                        
         L     R2,WGRDLNO            WRAPPED LINES GET AUTOMATIC                
         A     R2,CPDELTA            NUMBERING                                  
         ST    R2,WGRDLNO                                                       
*                                                                               
         PRETURN (R0,R2)                                                        
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRFORMAT - WINGS READ FORMATTED LINE ROUTINE                                 
*                                                                               
*  THIS ROUTINE DOES ANY INTEGER, NUMBERED, LRECL PROCESSING                    
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - BUFFER LOC,LEN TO READ LINE INTO                                   
*    WGSNDX - PTR TO WINGS FORMAT LINE                                          
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - LINE TEXT                                                          
*    R2 - LINE NUMBER                                                           
*    R15 = ALWAYS RETURN ZERO                                                   
*                                                                               
*  NOTE:                                                                        
*  THE BUFFER LENGTH (PASSED IN R0) IS CURRENTLY                                
*  IGNORED.  BUFFER MUST BE &LINESZ+16 OR BIGGER.                               
*                                                                               
*  NOTE:                                                                        
*  IF LINES WRAP, INTEGER, NUMBER, SEQFLD STUFF NOT PROCESSED.                  
*                                                                               
*                                                                               
WRFWA    RECORD BEGIN                                                           
WRFLOC   DS    F                                                                
WRFLEN   DS    F                                                                
WRFDEST  DS    F                                                                
WRFLNO   DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
WRFORMAT PROC  WRFWA                                                            
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   GET/SAVE INPUT/OUTPUT BUF PTRS               
         ST    R1,WRFDEST          R1,R0 - OUTPUT BUFFER LOC, LEN               
         L     R4,WGRDNDX          R4,R5 - INPUT LINE LOC, LEN                  
         L     R5,WGSLNLEN(R4)                                                  
         S     R5,=AL4(L'WGSLNHDR) ADJUST FOR START WGS LINE HDR                
         A     R4,=AL4(L'WGSLNHDR)                                              
         IF    (R5,NEG),BEGIN                                                   
         ERROR 'WINGS format error.  Unable to read file.',,ERROR               
         END                                                                    
         ST    R4,WRFLOC                                                        
         ST    R5,WRFLEN                                                        
*                                                                               
         COMMENT                   MOVE LINE TO BUFFER                          
         L     R2,WRFDEST                                                       
         L     R3,WRFLEN                                                        
         L     R4,WRFLOC                                                        
         L     R5,WRFLEN                                                        
         MVCL  R2,R4                                                            
*                                                                               
         COMMENT                   ** DEBUG **  EXTEND LINES TO LRECL           
         IF    (WGSLRECL,NE,0),BEGIN                                            
         L     R3,WRFDEST                                                       
         A     R3,WRFLEN                                                        
         L     R4,WRFDEST                                                       
         A     R4,WGSLRECL                                                      
         WHILE (R3,LT,R4),BEGIN                                                 
         MVI   @R3,C' '                                                         
         INCR  R3                                                               
         END                                                                    
         L     R3,WGSLRECL                                                      
         ST    R3,WRFLEN                                                        
         END                                                                    
*                                                                               
         COMMENT                   IF INTEGER/NUMBER DO IT                      
         IF    ((WGSFNUM,OR,WGSFINT),AND,^WGSFSEQ),BEGIN                        
         L     R1,WRFDEST                                                       
         A     R1,WRFLEN                                                        
         S     R1,=F'8'                                                         
         LA    R0,8                                                             
         ACALL GTINTVAL                                                         
         IF    (R15,NEG),BEGIN                                                  
         ACALL GTLNOVAL                                                         
         IF    (R15,NEG),BEGIN                                                  
         ERROR 'Invalid NUMBER/INTEGER option.  Command incomplete.'            
         END                                                                    
         END                                                                    
         ST    R15,WRFLNO                                                       
         MVC   @R1(8),=CL8'        '  BLANK LINE NUMBER                         
         END                                                                    
*                                                                               
         COMMENT                   IF SEQFLD= ..                                
         ELSEIF WGSFSEQ,BEGIN                                                   
         COMMENT                   GET R3 - SEQFLD OFFSET                       
         L     R3,WGSSEQF                                                       
         DECR  R3                                                               
         IF    (R3,NEG),BEGIN                                                   
         L     R3,WRFLEN                                                        
         S     R3,WGSSEQFL                                                      
         END                                                                    
         COMMENT                   GET SEQFLD VALUE                             
         L     R1,WRFDEST                                                       
         AR    R1,R3                                                            
         L     R0,WGSSEQFL                                                      
         ACALL GTINTVAL                                                         
         IF    (R15,NEG),BEGIN                                                  
         ACALL GTLNOVAL                                                         
         IF    (R15,NEG),BEGIN                                                  
         ERROR 'Invalid NUMBER/INTEGER option.  Command incomplete.'            
         END                                                                    
         END                                                                    
         ST    R15,WRFLNO          GOT NUMBER                                   
         COMMENT                   BLANK SEQ NUMBER                             
         L     R4,WRFDEST                                                       
         AR    R4,R3 SEQF                                                       
         L     R5,WGSSEQFL                                                      
         MOVE  R5,@R4,=CL10'         '                                          
         COMMENT                   SHRINK LINE                                  
         L     R4,WRFDEST                                                       
         AR    R4,R3 SEQF                                                       
         A     R4,WGSSEQFL                                                      
         L     R2,WRFDEST                                                       
         AR    R2,R3 SEQF                                                       
         L     R5,WRFLEN                                                        
         SR    R5,R3 SEQF                                                       
         S     R5,WGSSEQFL                                                      
         IF    (R5,POS),BEGIN                                                   
         DEX   R5,' MVC @R2(0),@R4 '                                            
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   BUMP BLOCK POINTERS                          
         L     R4,WGRDNDX                                                       
         L     R3,WGSLNLEN(R4)     BUMP NDX                                     
         A     R3,WGRDNDX                                                       
         ST    R3,WGRDNDX                                                       
*                                                                               
         COMMENT                   SET RETURNS                                  
         L     R1,WRFDEST                                                       
         L     R0,WRFLEN                                                        
         VCALL RTRIM                                                            
         L     R2,WRFLNO                                                        
         PRETURN (R0,R2)                                                        
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  WWFORMAT - WINGS WRITE LINE FORMAT ROUTINE                                   
*                                                                               
*  THIS ROUTINE DOES ANY INTEGER, NUMBERED, LRECL PROCESSING                    
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - LINE LOCATION LENGTH                                               
*    R2 - LINE NUMBER                                                           
*    R15 - PLACE TO PUT TEXT (MAY EXPAND!)                                      
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - NEW LINE LOCATION LENGTH                                           
*    R2 - NOT CHANGED                                                           
*                                                                               
*                                                                               
*                                                                               
WWFWA    RECORD BEGIN                                                           
WWFLOC   DS    F                                                                
WWFLEN   DS    F                                                                
WWFDEST  DS    F                                                                
WWFLNO   DS    F                                                                
WWFLNUSD DS    F       Actual record length                                     
         END                                                                    
*                                                                               
*                                                                               
WWFORMAT PROC  WWFWA                                                            
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         ST    R0,WWFLEN                                                        
         ST    R1,WWFLOC                                                        
         ST    R2,WWFLNO                                                        
         ST    R15,WWFDEST                                                      
         CLEAR WWFLNUSD                                                         
*                                                                               
         COMMENT                   IF NO FORMATTING, JUST MOVE                  
         IF    (WGSLRECL,EQ,0),BEGIN                                            
         LR    R2,R15                                                           
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         MVCL  R2,R4                                                            
         ST    R0,WWFLNUSD                                                      
         END                                                                    
*                                                                               
         ELSE  BEGIN               FORMAT RECORD                                
         LR    R2,R15                                                           
         L     R3,WGSLRECL                                                      
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         IF    (R5,GT,R3),BEGIN                                                 
         SET   CPFTRUNC                                                         
         LR    R5,R3                                                            
         END                                                                    
*                                                                               
* For RECFM=V or RECFM=VB, use the source line length                           
* (possibly truncated above), as the destination length.                        
*                                                                               
         IF    WGSFV,BEGIN                                                      
         LR    R3,R5                                                            
         END                                                                    
         ST    R3,WWFLNUSD                                                      
         O     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
*                                                                               
         COMMENT                   IF INTEGER                                   
         IF    (WGSFINT,AND,^WGSFSEQ),BEGIN                                     
         L     R1,WWFDEST                                                       
         A     R1,WGSLRECL                                                      
         S     R1,=F'8'                                                         
         LA    R0,8                                                             
         L     R15,WWFLNO                                                       
         VCALL BTDZEROS                                                         
         END                                                                    
*                                                                               
         COMMENT                   IF NUMBER                                    
         ELSEIF (WGSFNUM,AND,^WGSFSEQ),BEGIN                                    
         L     R0,WWFLNO                                                        
         VCALL CVEXNO                                                           
         L     R1,WWFDEST                                                       
         A     R1,WGSLRECL                                                      
*                                                                               
* If numbered specified, and CARD specified, default the                        
* sequence field to (73,8) if it wasn't specified.                              
*                                                                               
         IF    (WGSLRECL,EQ,80),BEGIN                                           
         LA    R2,CPDOUB+9                                                      
         LA    R3,8                                                             
         SR    R2,R3                                                            
         SR    R1,R3                                                            
         DEX   R3,'MVC @R1(0),@R2'                                              
         END                                                                    
         ELSE  BEGIN                                                            
         S     R1,=F'9'                                                         
         MVC   @R1(9),CPDOUB                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF SEQFLD= ..                                
         ELSEIF WGSFSEQ,BEGIN                                                   
         COMMENT                   GET R4,R5 SEQFLD OFFSET LEN                  
         L     R4,WGSSEQF                                                       
         DECR  R4                                                               
         IF    (R4,NEG),BEGIN                                                   
         L     R4,WGSLRECL                                                      
         S     R4,WGSSEQFL                                                      
         END                                                                    
         L     R5,WGSSEQFL                                                      
         COMMENT                   MOVE LINE TEXT                               
         L     R0,WWFDEST          LAST PART, THEN FIRST PART                   
         A     R0,WGSSEQFL                                                      
         L     R1,WGSLRECL                                                      
         S     R1,WGSSEQFL                                                      
         L     R2,WWFLOC                                                        
         L     R3,WWFLEN                                                        
         O     R3,=X'40000000'                                                  
         MVCL  R0,R2                                                            
         L     R0,WWFDEST                                                       
         LR    R1,R4                                                            
         L     R2,WWFLOC                                                        
         L     R3,WWFLEN                                                        
         O     R3,=X'40000000'                                                  
         MVCL  R0,R2                                                            
         COMMENT                   MOVE IN INTEGER LINE NUMBER                  
         IF    WGSFINT,BEGIN                                                    
         L     R1,WWFDEST                                                       
         AR    R1,R4                                                            
         LR    R0,R5                                                            
         L     R15,WWFLNO                                                       
         VCALL BTDZEROS                                                         
         END                                                                    
         COMMENT                   MOVE IN LINE NUMBER W/DECIMAL                
         ELSE  BEGIN                                                            
         L     R0,WWFLNO                                                        
         VCALL CVEXNO                                                           
         L     R1,WWFDEST                                                       
         AR    R1,R4                                                            
         LA    R2,CPDOUB+9                                                      
         SR    R2,R5                                                            
         LR    R3,R5                                                            
         DEX   R3,'MVC @R1(0),@R2'                                              
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   SET RETURNS                                  
         L     R1,WWFDEST                                                       
         L     R0,WWFLEN                                                        
         IF    (WWFLNUSD,NE,0),BEGIN                                            
         L     R0,WWFLNUSD                                                      
         END                                                                    
         PRETURN (R0,R1)                                                        
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRDOPEN - READ/PROCESS OPEN FILE RESPONSE                                    
*                                                                               
*  ON EXIT:                                                                     
*    R15=0, ALL OK                                                              
*    R15=NZ, ERROR, ERR MSG TSEG'D, PATH CLOSED.                                
*                                                                               
*                                                                               
WRDOPEN  PROC  ,                                                                
*                                                                               
         COMMENT                   READ FROM WINGS PATH                         
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         LA    R15,4                                                            
         B     WRDODONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     WRDODONE                                                         
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_OPEN:     ')),BEGIN                                
         TSEG  'WINGS path error.  Unable to open file.'                        
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     WRDODONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSOPRT                                                          
         SCPOP ,                                                                
         IF    (R15,NZ),BEGIN      BAD OPTION ON FILE_OPEN,                     
         ACALL WPCLOSE             SHOULD NEVER HAPPEN                          
         LA    R15,4                                                            
         B     WRDODONE                                                         
         END                                                                    
*                                                                               
WRDODONE LABEL ,                                                                
         PEND  ,                                                                
*                                                                               
*  OPEN RETURNS SCKWS                                                           
*                                                                               
WGSOPRT  SCKW  NAME,WRTNAME                                                     
         SCKW  CATALOG,WRTCAT                                                   
         SCKW  VOLUME,WRTVOL                                                    
         SCKW  ,WRTOTHER                                                        
*                                                                               
*  FULLY RESOLVED, LONG FORMAT (CANONICAL) FILE NAME RETURNED                   
*                                                                               
*  NOTE:                                                                        
*  WE SAVE THIS NAME IN THE FFINFO DSECT, THE CPFSET FLAG WILL                  
*  DETERMINE WHETHER THIS BECOMES THE CURRENT FILE NAME. THIS                   
*  CODE IS FOUND IN MODE AND ACT, ETC.                                          
*                                                                               
*  TAKE THE ROUND-A-BOUT.  --   K-9 TO DR. WHO                                  
*                                                                               
*                                                                               
WRTNAME  PROC  ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   SCAN NAME                                    
         SCANSTR ,                 SCAN W/SKIP=' ' STOP=' ,;='                  
*                                                                               
         IF    ((R15,Z),AND,(R0,POS)),BEGIN  GOT NAME                           
         COMMENT                   SAVE CURRENT NAME IN FFINFO                  
         COMMENT                   SAVE CANONICAL NAME IN FFINFO                
         CEIL  R0,L'FFRNAME                                                     
         ST    R0,FFRNAMEL                                                      
         LR    R3,R0                                                            
         MOVE  R3,FFRNAME,@R1                                                   
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN               NO NAME                                      
         LA    R15,4                                                            
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
*  WINGS saved the file on another volume. Update that information              
*  in the necessary areas.                                                      
*                                                                               
*  NOTE:                                                                        
*  WE SAVE THIS NAME IN THE FFINFO DSECT, THE CPFSET FLAG WILL                  
*  DETERMINE WHETHER THIS BECOMES THE CURRENT FILE NAME. THIS                   
*  CODE IS FOUND IN MODE AND ACT, ETC.                                          
*                                                                               
*                                                                               
WRTVOL   PROC  ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   SCAN NAME                                    
         SCANSTR ,                 SCAN W/SKIP=' ' STOP=' ,;='                  
*                                                                               
         IF    ((R15,Z),AND,(R0,POS)),BEGIN  GOT NAME                           
         CEIL  R0,L'FFHOST                                                      
         ST    R0,FFHOSTL                                                       
         ST    R0,FFPHOSTL                                                      
         LR    R3,R0                                                            
         MOVE  R3,FFHOST,@R1                                                    
         LR    R3,R0                                                            
         MOVE  R3,FFPHOST,@R1                                                   
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         ELSE  BEGIN               Forget it                                    
         CLEAR R15                 Don't treat it as an error                   
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
*  WINGS cataloged the file so delete any volume that the user                  
*  might have specified so that volume name doesn't become part                 
*  of '*'.                                                                      
*                                                                               
WRTCAT   PROC  ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
         CLEAR FFHOSTL                                                          
         CLEAR FFPHOSTL                                                         
         CLEAR R15                 Don't treat it as an error                   
         PEND  ,                                                                
         DROP  R6                                                               
*                                                                               
*                                                                               
WRTOTHER PROC  ,                                                                
         COMMENT                   IGNORE UNREC KEYWORD                         
         SCAN  ,                   IGNORE UNREC VALUE                           
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRDREAD - READ/PROCESS FILE READS                                            
*                                                                               
*  ON EXIT:                                                                     
*    R1,R0 - LOCATION, LENGTH OF STUFF READ (HEADER INCLUDED)                   
*    R15=0, ALL OK                                                              
*    R15=NZ, END OF FILE                                                        
*                                                                               
*                                                                               
*  ERRORS EXIT VIA CVERROR                                                      
*                                                                               
*                                                                               
WRDREAD  PROC  ,                                                                
*                                                                               
         COMMENT                   READ FROM WINGS PATH                         
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    (R0,LT,L'WGSBKHDR),BEGIN                                         
         ACALL WPCLOSE                                                          
         ERROR 'WINGS path error (server file read).'                           
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    (@R1,EQ,'FILE_ERROR:     '),BEGIN                                
         ACALL WRDERROR                                                         
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         IF    ((@R1,NE,'FILE_READ:      '),AND,                       X        
               (@R1,NE,'FILE_EOF:       ')),BEGIN                               
         ACALL WPCLOSE                                                          
         ERROR 'WINGS path error (server file read).'                           
         END                                                                    
*                                                                               
         COMMENT                   IF FILE BLOCK READ ,,                        
         IF    (@R1,EQ,'FILE_READ:      '),BEGIN                                
         CLEAR R15                                                              
         END                                                                    
*                                                                               
         COMMENT                   IF END OF FILE ,,                            
         IF    (@R1,EQ,'FILE_EOF:       '),BEGIN                                
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
*        SCANCHK                   BAD OPTIONS OF FILE_EOF: IGNORED             
         SCPOP ,                                                                
         LA    R15,4               END OF FILE                                  
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*                                                                               
*  WRDEOF - READ/PROCESS EOF                                                    
*                                                                               
*  ERRORS EXIT VIA CVERROR                                                      
*                                                                               
*                                                                               
WRDEOF   PROC  ,                                                                
*                                                                               
         COMMENT                   READ FROM WINGS PATH                         
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_EOF:      ')),BEGIN                                
         ACALL WPCLOSE                                                          
         ERROR 'WINGS path error during close. '                                
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
* IF ERROR WHO WILL CLOSE WINGS PATH ??                                         
*        SCANCHK                                                                
         SCPOP ,                                                                
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
*                                                                               
*  XWRDEOF - READ/PROCESS EOF: (not FILE_EOF:)                                  
*                                                                               
*  ERRORS EXIT VIA CVERROR                                                      
*                                                                               
*                                                                               
XWRDEOF   PROC  ,                                                               
*                                                                               
         COMMENT                   READ FROM WINGS PATH                         
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'EOF:           ')),BEGIN                                
         ACALL WPCLOSE                                                          
         ERROR 'WINGS path error during close. '                                
         END                                                                    
*                                                                               
         COMMENT                   SCAN/PROCESS RETURNS                         
         LA    R1,L'WGSBKHDR(R1)                                                
         S     R0,=AL4(L'WGSBKHDR)                                              
         SCPUSH ,                                                               
         SCCLR                                                                  
         SCINIT (R1),(R0)                                                       
         SCAN  WGSEPRT                                                          
* IF ERROR WHO WILL CLOSE WINGS PATH ??                                         
*        SCANCHK                                                                
         SCPOP ,                                                                
*                                                                               
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
* Returns from the ALLOCATE command                                             
*                                                                               
WGSAPRT  SCKW  MSG,WRTMSG,P                                                     
         SCKW  ERRDATA,WRTERRDT,P                                               
         SCKW  INFOMSG,WRTIMSG,P                                                
         SCKW  CATALOG,WRTCAT                                                   
         SCKW  VOLUME,WRTVOL                                                    
         SCKW  ,WRTOTHER                                                        
                                                                                
*                                                                               
*  EOF RETURNS SCKWS                                                            
*                                                                               
WGSEPRT  SCKW  MSG,WRTMSG,P                                                     
         SCKW  ERRDATA,WRTERRDT,P                                               
         SCKW  INFOMSG,WRTIMSG,P                                                
         SCKW  ,WRTOTHER                                                        
*                                                                               
*                                                                               
WRTMWA   RECORD BEGIN                                                           
WRTMMSGL DS    F                                                                
WRTMMSG  DS    CL(&LINESZ)                                                      
         END                                                                    
*                                                                               
*                                                                               
WRTMSG   PROC  WRTMWA                                                           
         COMMENT                   WRITE MESSAGE TO TERMINAL                    
         SCDQUOTE WRTMMSG                                                       
         LR    R15,R1                                                           
         AR    R15,R0                                                           
         BCTR  R15,0                                                            
         CLI   0(R15),C'+'         ADDITIONAL INFORMATION?                      
         BNE   WRTMSGNP            NOPE                                         
         S     R0,=F'1'            TAKE OUT THE '+' SYMBOL                      
WRTMSGNP LABEL ,                                                                
         ST    R0,WRTMMSGL                                                      
         TCCR  ,                                                                
         L     R0,WRTMMSGL                                                      
         LA    R1,WRTMMSG                                                       
         TSEG  (R1),(R0)                                                        
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WRTERRDT PROC  ,                                                                
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
WRTIMSG  PROC  ,                                                                
         CLEAR R15                                                              
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
*  WRDCLOSE -  READ/PROCESS CLOSE                                               
*                                                                               
*  ERRORS EXIT VIA CVERROR                                                      
*                                                                               
*                                                                               
WRDCLOSE PROC  ,                                                                
*                                                                               
         COMMENT                   READ FROM WINGS PATH                         
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                   CHECK FOR WINGS ERROR RETURN                 
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         ACALL WRDERROR                                                         
         ACALL WPCLOSE                                                          
         LA    R15,4                                                            
         B     CVERROR                                                          
         END                                                                    
*                                                                               
         COMMENT                                                                
         IF    ((R0,LT,L'WGSBKHDR),OR,                                 X        
               (@R1,NE,'FILE_CLOSE:    ')),BEGIN                                
         ACALL WPCLOSE                                                          
         ERROR 'WINGS path error during close. '                                
         END                                                                    
*                                                                               
         COMMENT                   ALL RETURNS IGNORED                          
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*  WRDERROR                                                                     
*                                                                               
*  GET ERRID AND ERROR MSG FROM PATH ERROR PACKET                               
*                                                                               
*  ON ENTRY:                                                                    
*    R1,R0 - ERROR PACKET LOCATION LENGTH                                       
*                                                                               
*  ON EXIT:                                                                     
*    CPERRID SET.  ERROR MESSAGE IS TSEG'ED                                     
*    R15 SET TO ZERO.                                                           
*                                                                               
*  NOTE:                                                                        
*  ANYTHING THAT IS NOT RECOGNIZED IS IGNORED.                                  
*                                                                               
*  NOTE:                                                                        
*  WE PROCESS LIBFULL CONDENSE ERROR HERE.  IT                                  
*  WOULD BE BEST IF THIS WAS PROCESSED IN HIGHER                                
*  LEVEL ROUTINE, (EG. SAVE ROUTINE),  BUT WE                                   
*  MAY GET ERROR RETURN DURING WRITE, AND                                       
*  AS OF NOW WYLBUR DOES NOT RETURN ERRORS ON                                   
*  WRITE.  SO WE DO IT HERE.                                                    
*                                                                               
*                                                                               
WRDERROR PROC  ,                                                                
*                                                                               
         IF    ((R0,GE,16),AND,(@R1,EQ,'FILE_ERROR:     ')),BEGIN               
         A     R1,=F'16'                                                        
         S     R0,=F'16'                                                        
         VCALL LOGCMD                                                           
         VCALL LOGEVENT                                                         
         SCPUSH ,                                                               
         SCCLR ,                                                                
         SCINIT (R1),(R0)                                                       
         SCAN  WGERRPRT                                                         
         CLEAR R15                                                              
         SCPOP ,                                                                
         END                                                                    
*                                                                               
         COMMENT                   CLOSE FILE                                   
         ACALL WPCLOSE                                                          
*                                                                               
         COMMENT                   IF LIB FULL, TRY CONDENSE                    
         IF    ((CPERRID,EQ,'LIBFULL '),AND,^CPFNCOND),BEGIN                    
         SETMSG 'Condense'                                                      
         VCALL YESREQ              Go check if ok                               
         ACALL WCONDSE             Condense library                             
         BOMB  ,                   No return                                    
         END                                                                    
*                                                                               
*                                                                               
         PEND  ,                                                                
*                                                                               
*                                                                               
WGERRPRT SCKW  ERRID,WGERRID,P                                                  
         SCKW  ERRMSG,WGERRMSG,P                                                
         SCKW  ,WGERRXXX                                                        
*                                                                               
*                                                                               
WGERRID  PROC  ,                                                                
         SCUPCASE CPERRID                                                       
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
*                                                                               
WGEMSGWA RECORD BEGIN                                                           
WGEMSG   DS    CL(&LINESZ)                                                      
         END                                                                    
*                                                                               
WGERRMSG PROC  WGEMSGWA                                                         
         SCDQUOTE WGEMSG                                                        
         TSEG  (R1),(R0),CR                                                     
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
WGERRXXX PROC  ,                                                                
         COMMENT                   IGNORE UNRECOGNIZED KEYWORD                  
         SCAN  ,                   INGNORE UNRECOGNIZED VALUE                   
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*  WPOPEN - WINGS PATH OPEN                                                     
*                                                                               
*  THIS ROUTINES OPEN A SUZAN PATH TO WINGS SERVER.                             
*  IT *DOES NOT* DO ANY OF THE WINGS PROTOCOL START UP.                         
*                                                                               
*  ON ENTRY:                                                                    
*    NOTHING.                                                                   
*                                                                               
*  ON EXIT:                                                                     
*    R15=0, ALL OK. PATH CONTROL BLOCK POINTER SET IN WGSPATH                   
*    R15=NZ, ERROR, ERROR MSG TSEG'ED                                           
*                                                                               
*                                                                               
WPOPEN   PROC  ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   CHECK FOR MULTIPLE OPENS                     
         IF    (WGSPATH,NE,0),BEGIN                                             
         KAPUT ERROR,'Multiple WINGS connections not supported.'                
         END                                                                    
*                                                                               
         COMMENT                   ALLOCATE PATH                                
         CLEAR R0                                                               
         VCALL PATHALLO                                                         
         IF    NZ,BEGIN                                                         
         MVC   CPERRID,=CL8'ERROR'                                              
         TSEG  (R1),(R0)                                                        
         LA    R15,4                                                            
         B     WPOPDONE                                                         
         END                                                                    
         ST    R1,WGSPATH                                                       
*                                                                               
         COMMENT                   SET UP PATH CHARACTERISTCS                   
         LR    R5,R1                                                            
         USING UPATH,R5                                                         
         IF    (CPWPATH,NE,0),BEGIN  SET PATH NAME                              
         MVC   UPATYOU,CPWPATH                                                  
         END                                                                    
         ELSEIF (CVWPATH,NE,0),BEGIN                                            
         MVC   UPATYOU,CVWPATH                                                  
         END                                                                    
         ELSE  BEGIN                                                            
         MVC   UPATYOU,=CL8'WINGSV00' PARTNER IS WINGS SERVER                   
         END                                                                    
         SET   UPATMFL.UPATMNAM                                                 
         CLEAR UPATFHDR            DON'T ADD LENGTH HEADERS                     
*        CLEAR UPATFNOWAIT         WAIT 10 SECONDS TO OPEN                      
*        LA    R0,5*60             5 minute timeout                             
*        IF    CPFNTMO,'L R0,=A(1*60*60)'  1 hour timeout (debug)               
*        IF    CPSTVIRT,'L R0,=A(4*60*60)'  4 hour timeout                      
*        ST    R0,UPATWAIT         Save timeout value                           
*                                                                               
         COMMENT                   OPEN PATH                                    
         LA    R1,=C'AUTH'         Caller ID                                    
         LA    R15,UPATH                                                        
         VCALL PATHOPEN                                                         
         IF    NZ,BEGIN            IF ERROR ,,,                                 
         IF    ((R0,EQ,4),AND,(@R1,EQ,'ATTN')),BEGIN                            
         SETMSG 'No action taken (stopped before path open).'                   
         END                                                                    
         IF    ((R0,EQ,7),AND,(@R1,EQ,'TIMEOUT')),BEGIN                         
         SETMSG 'Wings file server not responding.  Path timeout.'              
         END                                                                    
         MVC   CPERRID,=CL8'ERROR'                                              
         TSEG  (R1),(R0)                                                        
         ACALL WPCLOSE DEALLOCATE PATH W/CLOSE                                  
         LA    R15,4                                                            
         B     WPOPDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   ALL OK !                                     
         CLEAR R15                                                              
*                                                                               
WPOPDONE LABEL ,                                                                
         PEND  ,                                                                
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  WPREAD - WINGS PATH READ                                                     
*                                                                               
*  DO IT TO IT !!                                                               
*                                                                               
*  THIS ROUTINES OPEN A SUZAN PATH TO WINGS SERVER.                             
*  IT *DOES NOT* DO ANY OF THE WINGS PROTOCOL START UP.                         
*                                                                               
*  ON ENTRY:                                                                    
*    NOTHING.                                                                   
*                                                                               
*  ON EXIT:                                                                     
*    R15=0, ALL OK.  R1,R0 - BUFFER READ                                        
*    R15=NZ, ERROR, ERROR MSG TSEG'ED                                           
*                                                                               
*                                                                               
WPREAD   PROC  ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   READ ONLY IF PATH OPEN                       
         IF    (WGSPATH,EQ,0),BEGIN                                             
         MVC   CPERRID,=CL8'ERROR'                                              
         TSEG  'Wings path not open.'                                           
         CLEAR R0                                                               
         LA    R15,4                                                            
         B     WPRDDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   READ FROM PATH                               
WPRDLOOP L     R15,WGSPATH                                                      
         VCALL PATHREAD                                                         
**                                                                              
** For debugging                                                                
**                                                                              
         IF    (CVLGPATH),BEGIN                                                 
         XPUSH R15,R2                                                           
         BTX   CPDOUB,8,(R15)                                                   
         LA    R15,=CL8'READ R15'                                               
         VCALL LOGEVENT                                                         
         XPOP  R15,R2                                                           
         SPACE ,                                                                
         IF    (R15,Z),BEGIN                                                    
         XPUSH R15,R2                                                           
         IF    ((@R1,EQ,'FILE_READ:      '),OR,                        X        
               (@R1,EQ,'DATA:           ')),BEGIN                               
         LA    R0,L'WGSBKHDR       Log only the header                          
         END   ,                                                                
         LA    R15,=CL8'<-Wings '                                               
         VCALL LOGEVENT                                                         
         L     R2,WGSPATH                                                       
         WITH  (UPATH,R2),'L R2,UPATMGC'                                        
         BTX   CPDOUB,8,(R2)       Log the identifier                           
         LA    R15,=CL8'ON PATH#'                                               
         VCALL LOGEVENT                                                         
         XPOP  R15,R2                                                           
         END   ,                                                                
         END   ,                                                                
                                                                                
**                                                                              
** End of debugging code                                                        
**                                                                              
         IF    (R15,NZ),BEGIN      IF READ ERROR, CLOSE PATH                    
*                                                                               
* On an attention from the terminal, the path is only closed                    
* if the line is logging off. This usually occurred because                     
* of a WINGS server hang. If the user has just signalled attention              
* because they don't want to wait for the read to complete, the                 
* path was never closed, despite the original comment because                   
* the attention was signalled on the subsequent read.                           
*                                                                               
         IF    ((R0,EQ,4),AND,(@R1,EQ,'ATTN')),BEGIN                            
         L     R15,CVCURJCB                                                     
         WITH  (JCB,R15),BEGIN                                                  
         IF    JCBAFALO,BEGIN                                                   
         SETMSG 'DISCONNECTED'                                                  
         B     WPRDCLOS                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         SET   JCBFATTN                                                         
         B     WPRDLOOP                                                         
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         IF    ((R0,EQ,7),AND,(@R1,EQ,'TIMEOUT')),BEGIN                         
         SETMSG 'No response from Wings file server.'                           
         END                                                                    
*                                                                               
         IF    (R15,EQ,=F'20'),BEGIN  PATH CLOSED                               
         VCALL LOGCMD                                                           
         LA    R15,=CL8'WPREADER'                                               
         VCALL LOGEVENT                                                         
         END                                                                    
*                                                                               
WPRDCLOS LABEL ,                                                                
         MVC   CPERRID,=CL8'ERROR'                                              
         TSEG  (R1),(R0)                                                        
         ACALL WPCLOSE                                                          
         CLEAR R0                                                               
         LA    R15,4                                                            
         B     WPRDDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   ALL OK !                                     
         CLEAR R15                                                              
*                                                                               
WPRDDONE LABEL ,                                                                
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  WPCLOSE - WINGS PATH CLOSE                                                   
*                                                                               
*  THIS ROUTINES CLOSES A SUZAN PATH TO WINGS SERVER.                           
*  IT *DOES NOT* DO ANY OF THE WINGS PROTOCOL SHUT DOWN.                        
*                                                                               
*  ON ENTRY:                                                                    
*    WGSPATH - WINGS UPATH PTR.                                                 
*                                                                               
*  ON EXIT:                                                                     
*    PATH CLOSED.   WGSPATH SET TO ZERO.                                        
*                                                                               
*                                                                               
*                                                                               
WPCLOSE  PROC  ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         COMMENT                   CLOSE PATH ONLY IF OPEN                      
         IF    (WGSPATH,NE,0),BEGIN                                             
         L     R15,WGSPATH                                                      
         VCALL PATHCLS                                                          
         CLEAR WGSPATH                                                          
         END                                                                    
*                                                                               
         CLEAR R15                 NO RETURN CODE RETURNED                      
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*  WPCHECK - SEE IF PATH IS OPEN                                                
*                                                                               
*  ON EXIT:                                                                     
*    R15=0 - PATH OPEN                                                          
*    R15=NZ - PATH CLOSED                                                       
*                                                                               
*                                                                               
WPCHECK  PROC  ,                                                                
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         IF    (WGSPATH,EQ,0),' LA R15,4'                                       
         ELSE  'CLEAR R15'                                                      
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
*  WPSEG -- SEG PROCESSING ROUTINE TO HANDLE WRITING                            
*    DATE ACROSS A SUZAN PATH TO WINGS FILE SERVER.                             
*                                                                               
*    ON ENTRY:                                                                  
*      R15 - SEGCB PTR                                                          
*                                                                               
*    ON EXIT:                                                                   
*      ERRORS EXIT VIA CVERROR.                                                 
*                                                                               
*                                                                               
WPSEG    XPROC                                                                  
*                                                                               
         L     R6,CPDEVP                                                        
         USING WGSWA,R6                                                         
*                                                                               
         LR    R5,R15                                                           
         WITH  (SEGCB,R5)                                                       
*                                                                               
         COMMENT                   DO SEG WRITE OR OVERFLOW ...                 
         IF    (SEGCBWR,OR,SEGCBOVF),BEGIN                                      
         LTR   R2,R0               CURRENT BUFFER LENGTH                        
*                                                                               
         COMMENT                   GET PATH POINTER                             
         L     R15,WGSPATH                                                      
         IF    (R15,Z),BEGIN                                                    
         ERROR 'WINGS path non-existant.  Unable to write file.'                
         END                                                                    
*                                                                               
         COMMENT                   WRITE TO PATH                                
         S     R1,=AL4(L'WGSBKHDR) INCLUDE 'FILE_XXXX: ' HEADER                 
         A     R0,=AL4(L'WGSBKHDR)                                              
*                                                                               
** For debugging                                                                
**                                                                              
*         XPUSH R15,R2                                                          
*         IF    ((@R1,EQ,'FILE_READ:      '),OR,                        X       
*               (@R1,EQ,'DATA:           ')),BEGIN                              
*         LA    R0,L'WGSBKHDR       Log only the header                         
*         END                                                                   
*         LA    R15,=CL8'->Wings '                                              
*         VCALL LOGEVENT                                                        
*         L     R2,WGSPATH                                                      
*         WITH  (UPATH,R2),'L R2,UPATMGC'                                       
*         BTX   CPDOUB,8,(R2)       Log the identifier                          
*         LA    R15,=CL8'ON PATH#'                                              
*         VCALL LOGEVENT                                                        
*         XPOP  R15,R2                                                          
**                                                                              
** End of debugging code                                                        
**                                                                              
         VCALL PATHWRIT                                                         
         IF    NZ,BEGIN            Log some information                         
         LR    R2,R15              Save the return code                         
         VCALL LOGCMD              Log the command we were doing                
*         IF    ((@R1,EQ,'FILE_READ:      '),OR,                        X       
*              (@R1,EQ,'DATA:           ')),BEGIN                               
*        LA    R0,L'WGSBKHDR       Log only the header                          
*        END                                                                    
*        LA    R15,=CL8'TO WINGS'                                               
*        VCALL LOGEVENT                                                         
         BTX   CPDOUB,8,(R2)       Now log the return code                      
         LA    R15,=CL8'PATHERR '                                               
         VCALL LOGEVENT                                                         
         ACALL WPSEGERR            Does not return (tacky!)                     
         END                                                                    
*                                                                               
         LCR   R0,R2               FOR OVERFLOW: NBYTES PROCESSED               
         PRETURN (R0)                                                           
         END                                                                    
*                                                                               
         COMMENT                   DO SEG POST PROCESSING ...                   
         IF    SEGCBSPP,BEGIN                                                   
         CLEAR R0                  ASSUME A-OK                                  
*        IF    (UPATRCMP,NZ),'LA R0,4'  TROUBLE                                 
         PRETURN (R0)              RETURN THE RC                                
         END                                                                    
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
*  WPSEGERR - PROCESS WINGS PATH SEG WRITE ERROR                                
*                                                                               
*  WE GOT AN ERROR WHILE TRYING TO WRITE A WINGS                                
*  FILE.   WE TRY TO  READ A 'FILE_ERROR:' FROM                                 
*  OUR PARTNER TO SEE WHAT THE ERROR IS.                                        
*                                                                               
*  IF ERROR IS 'LIBFULL' WE ASK TO CONDENSE.                                    
*  IF ERROR IS '....' (NO OTHERS YET)                                           
*  IF WE GET UNRECOGNIZED 'FILE_ERROR:' WE SEND                                 
*     ERROR ID, ERROR MSG BACK TO USER                                          
*  IF WE GET ERROR TRYING TO READ FROM PARTNER,                                 
*     WE GIVE UP WITH GENERIC                                                   
*                                                                               
*  FINALLY WE CLOSE FILE                                                        
*                                                                               
*                                                                               
WSEWA    RECORD BEGIN                                                           
WSEBUF   DS    CL512                                                            
         END                                                                    
*                                                                               
*                                                                               
WPSEGERR PROC  WSEWA                                                            
*                                                                               
         COMMENT                   TRY TO READ FROM WINGS PATH                  
         LA    R1,WSEWA                                                         
         LA    R0,L'WSEWA                                                       
         ACALL WPREAD                                                           
         IF    NZ,BEGIN                                                         
         ERROR 'WINGS path error.  Unable to write file.'                       
         END                                                                    
*                                                                               
         COMMENT                   IF READ UNEXPECTED LINE, ERROR               
         IF    ((R0,LT,16),OR,(@R1,NE,'FILE_ERROR:     ')),BEGIN                
         ERROR 'WINGS path error.  Unable to write file.'                       
         END                                                                    
*                                                                               
         COMMENT                   PROCESSS 'FILE_ERROR: ' MSG                  
         ACALL WRDERROR                                                         
         ACALL WPCLOSE             BE SURE WE ARE CLOSED                        
         COMMENT                   OTHER ERRS, JUST EXIT VIA CVERROR            
         B     CVERROR                                                          
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  WCONDENSE,  WINGS LIB FULL, CONDENSE LIB                                     
*                                                                               
*                                                                               
WCONDSE  PROC  ,                                                                
         L     R6,CPFINFOP                                                      
         USING FFINFO,R6                                                        
*                                                                               
         COMMENT                   FORMAT COMMAND IN CPCMD                      
         LA    R1,CPCMD                                                         
         MVC   CPCMD(10),=CL10'Condense  '                                      
         L     R2,FFRNAMEL                                                      
         IF    (R2,POS),BEGIN                                                   
         DEX   R2,' MVC CPCMD+10,FFRNAME'                                       
         L     R2,FFRNAMEL                                                      
         END                                                                    
         ELSE  BEGIN                                                            
         L     R2,FFNAMEL                                                       
         DEX   R2,' MVC CPCMD+10,FFNAME'                                        
         L     R2,FFNAMEL                                                       
         END                                                                    
         A     R2,=A(10)                                                        
         STH   R2,CPCMDLEN                                                      
*                                                                               
         LA    R1,CPCMD            ADD 'NOSAVE' WARNING ON COND                 
         AH    R1,CPCMDLEN                                                      
         MVC   @R1(12),=CL12' SAVEFAILED '                                      
         LH    R1,CPCMDLEN                                                      
         A     R1,=A(12)                                                        
         STH   R1,CPCMDLEN                                                      
*                                                                               
         COMMENT                   GO EXECUTE COMMAND                           
         LA    R1,CPCMD                                                         
         LH    R0,CPCMDLEN                                                      
         VCALL EDTCOMNH                                                         
         BOMB  ,                   NO RETURN                                    
*                                                                               
         PEND                                                                   
         DROP  R6                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  GTINTVAL - GET INTEGER VALUE                                                 
*                                                                               
*  ON ENTRY: R1,R0 - INTEGER LOCATION LENGTH                                    
*                                                                               
*  ON EXIT:                                                                     
*    R15=VALUE  (>=0)                                                           
*    R15=NEGATIVE, STRING IS NOT INTEGER                                        
*                                                                               
*                                                                               
*   NOTE:                                                                       
*   INTEGER MUST BE ALL DIGITS (NO BLANKS).                                     
*   MAY BE 9 DIGITS MAX.                                                        
*                                                                               
*   CAUTION:                                                                    
*   VALUE RETURNED IS ACTUAL INTEGER VALUE.                                     
*   VALUE RETURNED IS **NOT** NUMBERx1000.                                      
*                                                                               
GTINTVAL XPROC ,                                                                
*                                                                               
         IF    ((R0,Z),OR,(R0,GT,9)),BEGIN                                      
         L     R15,=F'-4'                                                       
         B     GTIVDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   R1,R0 - INTEGER STRING LOC, LEN              
         LR    R2,R0               R2 - DIGIT INDEX                             
         AR    R2,R1                                                            
         DECR  R2                                                               
         LA    R3,X'0F'            R3 - MASK                                    
         CLEAR R5                  R5 - DIGIT VALUE                             
         LA    R6,1                R6 - MULTIPLIER                              
         CLEAR R15                 R15 - TOTAL                                  
*                                                                               
         COMMENT                   LOOP THRU INTEGER, R. TO LEFT.               
         WHILE (R2,GE,R1),BEGIN                                                 
         IF    ((@R2,LT,'0'),OR,(@R2,GT,'9')),BEGIN IF NOT DIGIT,,              
         L     R15,=F'-4'                                                       
         B     GTIVDONE                                                         
         END                                                                    
         CLEAR R4                                                               
         CLEAR R5                                                               
         IC    R5,@R2              GET DIGIT                                    
         NR    R5,R3                                                            
         MR    R4,R6               DIGIT*MULTIPLIER                             
         AR    R15,R5              ACCUMULATE TOTAL                             
         MH    R6,=H'10'                                                        
         DECR  R2                                                               
         END                                                                    
*                                                                               
GTIVDONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*  GTLNOVAL - GET LINE NUMBER VALUE                                             
*                                                                               
*  ON ENTRY: R1,R0 - LINE NUMBER LOCATION LENGTH                                
*                                                                               
*  ON EXIT:                                                                     
*    R15=VALUE  (>=0)                                                           
*    R15=NEGATIVE, STRING IS NOT INTEGER                                        
*                                                                               
*                                                                               
*   NOTE:                                                                       
*   LINE NUMBER MAY HAVE LEADING AND/OR TRAILING BLANKS.                        
*   LINE NUMBER MAY CONTAIN ONE DECIMAL POINT.  OTHER THAN                      
*   THE ABOVE LINE NUMBER MUST BE DIGITS.                                       
*   LINE NUMBER MAY BE 9 DIGITS MAX, (3 DIGITS MAX AFTER A                      
*   DECIMAL POINT.)                                                             
*                                                                               
*   CAUTION:                                                                    
*   LINE NUMBER VALUE **EQUALS** VALUEx1000.  !!                                
*   IF LINE NUMBER IS INTEGER, VALUE **EQUALS** INTEGERx1000.                   
*                                                                               
*                                                                               
GTLVWA   RECORD BEGIN                                                           
GTLVINT  DS    CL10                                                             
         END                                                                    
*                                                                               
*                                                                               
GTLNOVAL PROC  GTLVWA                                                           
*                                                                               
         IF    ((R0,NP),OR,(R0,GT,9)),BEGIN                                     
         L     R15,=F'-4'                                                       
         B     GTLVDONE                                                         
         END                                                                    
*                                                                               
         COMMENT                   STRIP LEADING/TRAILING BLANKS                
         VCALL LRTRIM                                                           
*                                                                               
         COMMENT                   CONVERT STRING TO INTEGER STRING             
         MVC   GTLVINT,=CL10'0000000000'                                        
         CLEAR R3                                                               
         LA    R2,GTLVINT                                                       
         WHILE (R0,POS),BEGIN      LOOP THRU LINE NUMBER                        
         IF    ((@R1,GE,'0'),AND,(@R1,LE,'9')),BEGIN COPY NUMBERS               
         MVC   @R2(1),@R1                                                       
         INCR  R1                                                               
         INCR  R2                                                               
         DECR  R0                                                               
         END                                                                    
         ELSEIF ((@R1,EQ,'.'),AND,(R3,Z)),BEGIN  MARK DEC. POINT                
         LR    R3,R2                                                            
         INCR  R1                                                               
         DECR  R0                                                               
         END                                                                    
         ELSE  BEGIN               OTHER IS ERR                                 
         L     R15,=F'-4'                                                       
         B     GTLVDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   NOW CONVERT INTEGER                          
         LA    R1,GTLVINT                                                       
         IF    (R1,EQ,R2),BEGIN                                                 
         L     R15,=F'-4'                                                       
         B     GTLVDONE                                                         
         END                                                                    
         IF    (R3,Z),' LR R3,R2 ' IF NO DEC PNT, MARK END OF NUM               
         ELSE  'LA  R3,3(R3)'      ELSE, USE 3 DIGITS PAST DEC PNT              
         SR    R3,R1                                                            
         LR    R0,R3                                                            
         ACALL GTINTVAL                                                         
*                                                                               
GTLVDONE LABEL ,                                                                
         PEND  ,                                                                
         EJECT                                                                  
*                                                                               
*  CHECKVOL - CHECK VOLUME NAME                                                 
*                                                                               
*  ON ENTRY:  R1,R0 VOLUME LOC, LENGTH                                          
*                                                                               
*  ON EXIT:                                                                     
*    R15=ZERO, VALID VOLUME NAME                                                
*    R15=NZ, NOT VALID                                                          
*                                                                               
CHVWA    RECORD BEGIN                                                           
CHVVOL   DS    CL8                                                              
         END                                                                    
*                                                                               
*                                                                               
CHECKVOL XPROC CHVWA                                                            
*                                                                               
         COMMENT                   BLANK PAD, UPPER CASE NAME                   
         LA    R2,CHVVOL                                                        
         LA    R3,8                                                             
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         O     R5,=X'40000000'                                                  
         MVCL  R2,R4                                                            
         L     R4,CVUPPTBL                                                      
         TR    CHVVOL,@R4                                                       
*                                                                               
         COMMENT                   DO CHECKS                                    
         IF    (R0,GT,8),BADVOL                                                 
         IF    (CHVVOL,EQ,'CAT     '),GOODVOL                                   
         IF    (CHVVOL,EQ,'CATLG   '),GOODVOL                                   
         IF    (R0,NE,6),BADVOL                                                 
         LA    R2,CHVVOL                                                        
         IF    ((@R2,LT,'A'),OR,(@R2,GT,'Z')),CHKWVOL                           
         INCR  R2                                                               
         IF    ((@R2,LT,'A'),OR,(@R2,GT,'Z')),CHKWVOL                           
         INCR  R2                                                               
         IF    ((@R2,LT,'A'),OR,(@R2,GT,'Z')),CHKWVOL                           
         INCR  R2                                                               
         IF    ((@R2,LT,'A'),OR,(@R2,GT,'Z')),BEGIN                             
         IF    ((@R2,LT,'0'),OR,(@R2,GT,'9')),CHKWVOL                           
         END                                                                    
         INCR  R2                                                               
         IF    ((@R2,LT,'A'),OR,(@R2,GT,'Z')),BEGIN                             
         IF    ((@R2,LT,'0'),OR,(@R2,GT,'9')),CHKWVOL                           
         END                                                                    
         INCR  R2                                                               
         IF    ((@R2,LT,'0'),OR,(@R2,GT,'9')),CHKWVOL                           
         B     GOODVOL                                                          
*                                                                               
GOODVOL  LABEL ,                                                                
         CLEAR R15                                                              
         B     VOLDONE                                                          
*                                                                               
CHKWVOL  LABEL ,                                                                
         LA    R1,CHVVOL                                                        
         VCALL VOLCHK                                                           
         B     VOLDONE                                                          
*                                                                               
BADVOL   LABEL ,                                                                
         LA    R15,4                                                            
         B     VOLDONE                                                          
*                                                                               
VOLDONE  LABEL ,                                                                
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*   RETFNAME - RETURN FILE NAME                                                 
*                                                                               
*   STRIP '(MEMBER)' FROM 'FILE_NAME(MEMBER)', IF ANY                           
*                                                                               
*   ON ENTRY:                                                                   
*      R1,R0 - FILE NAME LOC, LEN                                               
*                                                                               
*   ON EXIT:                                                                    
*      R1,R0 - FILE NAME LOC, LEN WITHOUT '(MEMBER)'                            
*                                                                               
*                                                                               
*   NOTES: IF NOT TYPE WINGS/OLDIBM WE SHOULD NOT                               
*     RETURN MEMBER. '(' OR ')' MAY BE PART OF THE FILE                         
*     NAME.   WHAT DO YOU SAY !                                                 
*                                                                               
RETFNAME XPROC ,                                                                
*                                                                               
         LR    R2,R1               R2,R3,R4 - START,END,INDEX                   
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         LR    R4,R3                                                            
         DECR  R4                                                               
         IF    ((R0,POS),AND,(@R4,EQ,')')),BEGIN  IF NAME, MEMBER               
         WHILE ((R4,GT,R2),AND,(@R4,NE,'(')),BEGIN                              
         DECR  R4                                                               
         END                                                                    
         LR    R5,R4               R5,R4 - AT '('                               
         SR    R5,R1                                                            
         SR    R3,R4                                                            
         IF    ((R5,POS),AND,(R3,LE,10)),BEGIN                                  
         LR    R0,R4                                                            
         SR    R0,R1                                                            
         PRETURN (R0)                                                           
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*   RETFNGDG - RETURN FILE NAME                                                 
*                                                                               
*   STRIP '(MEMBER)' FROM 'FILE_NAME(MEMBER)', IF ANY                           
*   UNLESS IT'S A GDG.                                                          
*                                                                               
*   ON ENTRY:                                                                   
*      R1,R0 - FILE NAME LOC, LEN                                               
*                                                                               
*   ON EXIT:                                                                    
*      R1,R0 - FILE NAME LOC, LEN WITHOUT '(MEMBER)'                            
*                                                                               
*                                                                               
*   NOTES: IF NOT TYPE WINGS/OLDIBM WE SHOULD NOT                               
*     RETURN MEMBER. '(' OR ')' MAY BE PART OF THE FILE                         
*     NAME.   WHAT DO YOU SAY !                                                 
*                                                                               
RETFNGDG XPROC ,                                                                
*                                                                               
         LR    R2,R1               R2,R3,R4 - START,END,INDEX                   
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         LR    R4,R3                                                            
         DECR  R4                                                               
         IF    ((R0,POS),AND,(@R4,EQ,')')),BEGIN  IF NAME, MEMBER               
         WHILE ((R4,GT,R2),AND,(@R4,NE,'(')),BEGIN                              
         DECR  R4                                                               
         END                                                                    
         LR    R5,R4               R5,R4 - AT '('                               
         LA    R2,1(,R5)           POINT PAST '('                               
         IF    ((@R2,EQ,C'+'),OR,(@R2,EQ,C'-'),OR,                     X        
               (@R2,EQ,'0)')),BEGIN                                             
         END                                                                    
         ELSE  BEGIN                                                            
         SR    R5,R1                                                            
         SR    R3,R4                                                            
         IF    ((R5,POS),AND,(R3,LE,10)),BEGIN                                  
         LR    R0,R4                                                            
         SR    R0,R1                                                            
         PRETURN (R0)                                                           
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*   RETFMEM - RETURN FILE MEMBER, IF ANY                                        
*                                                                               
*   RETURN 'MEMBER' FROM 'FILE_NAME(MEMBER)', IF ANY                            
*                                                                               
*   ON ENTRY:                                                                   
*      R1,R0 - FILE NAME LOC, LEN                                               
*                                                                               
*   ON EXIT:                                                                    
*      R1,R0 - 'MEMBER' NAME LOC, LEN                                           
*                                                                               
*                                                                               
*   NOTES: IF NOT TYPE WINGS/OLDIBM WE SHOULD NOT                               
*     RETURN MEMBER. '(' OR ')' MAY BE PART OF THE FILE                         
*     NAME.   WHAT DO YOU SAY !                                                 
*                                                                               
RETFMEM  XPROC ,                                                                
*                                                                               
         LR    R2,R1               R2,R3,R4 - START,END,INDEX                   
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         LR    R4,R3                                                            
         DECR  R4                                                               
         IF    ((R0,POS),AND,(@R4,EQ,')')),BEGIN  IF NAME, MEMBER               
         WHILE ((R4,GT,R2),AND,(@R4,NE,'(')),BEGIN                              
         DECR  R4                                                               
         END                                                                    
         LR    R5,R4               R5,R4 - AT '('                               
         SR    R5,R1                                                            
         SR    R3,R4                                                            
         IF    ((R5,POS),AND,(R3,GE,3),AND,(R3,LE,10)),BEGIN                    
         LR    R0,R3                                                            
         DECR  R0                                                               
         DECR  R0                                                               
         LR    R1,R4                                                            
         INCR  R1                                                               
         END                                                                    
         ELSE  BEGIN                                                            
         AR    R1,R0                                                            
         CLEAR R0                                                               
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         AR    R1,R0                                                            
         CLEAR R0                                                               
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*   RETLIBMB - RETURN FILE NAME IN LIB#MEMBER FORMAT                            
*                                                                               
*                                                                               
*   ON ENTRY:                                                                   
*      R1,R0 - FILE NAME LOC, LEN                                               
*      R2 - LOCATION TO PUT NAME                                                
*                                                                               
*   ON EXIT:                                                                    
*      R1,R0 - FILE NAME (LIB#MEM) LOC, LEN                                     
*              (R1=R2 ON EXIT; R0 IS ACTUAL LEN)                                
*                                                                               
*   NOTE:                                                                       
*     IF NO MEMBER, FILE NAME IS RETURNED AS EXPECTED                           
*                                                                               
*   NOTE:                                                                       
*     IF NOT TYPE WINGS/OLDIBM WE SHOULD NOT                                    
*     RETURN MEMBER. '(' OR ')' MAY BE PART OF THE FILE                         
*     NAME.   WHAT DO YOU SAY !                                                 
*                                                                               
*   NOTES, CAUTION!                                                             
*     RETURN AREA (@R2) SHOULD BE BIG ENOUGH TO HOLD                            
*     MAX FILE NAME (CURRENTLY 128 BYTES).  ON EXIT                             
*     R1 = R2.                                                                  
*                                                                               
*                                                                               
RETLIBMB XPROC ,                                                                
*                                                                               
         COMMENT                   MOVE FILE NAME                               
         LR    R3,R0                                                            
         MOVE  R3,@R2,@R1                                                       
         LR    R1,R2               R1 - NEW NAME LOC, R0 - LEN                  
*                                                                               
         COMMENT                   R2,R3,R4 - START,END,INDEX                   
         LR    R3,R1                                                            
         AR    R3,R0                                                            
         LR    R4,R3                                                            
         DECR  R4                                                               
         IF    ((R0,POS),AND,(@R4,EQ,')')),BEGIN  IF NAME, MEMBER               
         WHILE ((R4,GT,R2),AND,(@R4,NE,'(')),BEGIN                              
         DECR  R4                                                               
         END                                                                    
         LR    R5,R4               R5,R4 - AT '('                               
         SR    R5,R1                                                            
         LR    R6,R3                                                            
         SR    R6,R4               R6 - MEMBER LEN+2                            
         IF    ((R5,POS),AND,(R6,LE,10)),BEGIN    IF MEMBER                     
         MVI   @R4,C'#'                                                         
         DECR  R3                                                               
         MVI   @R3,C' '                                                         
         DECR  R0                                                               
         END                                                                    
         END                                                                    
*                                                                               
         PRETURN (R0,R1)                                                        
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  File parameter area (needed for BUILDDSN).                                   
*                                                                               
FILPB    RECORD 'FILPB'            File parameter block                         
*-                                                                              
*-       XFILPB is just a copy of FILPB, so we can have                         
*-         addressability to two FILPBs at once.                                
*-                                                                              
XFILPB   RECORD BEGIN                                                           
         FILPB PFX=XFILPB                                                       
         END                                                                    
         EJECT                                                                  
*                                                                               
*                                                                               
*  IBM (WINGS, OLDIBM) FILE NAME RESOLUTION                                     
*                                                                               
*  ON ENTRY:                                                                    
*    @R1 - FFINFO DSECT                                                         
*    FFINFO CONTAINS FFNAME, FFHOST.                                            
*  ON EXIT:                                                                     
*    FFRNAME SET IN FFINFO.                                                     
*                                                                               
*   NOTE:                                                                       
*   SOME SCAN ERRORS EXIT VIA CVERROR                                           
*                                                                               
*                                                                               
WNWA     RECORD BEGIN                                                           
WNERRID  DS    CL8                 Space for ERRID                              
WNSG     SEGCB                                                                  
WNSGBUF  DS    CL256               Error message buffer                         
WNFILPB  DS    XL(L'FILPB)                                                      
         END                                                                    
*-                                                                              
WGSRESLV XPROC WNWA                                                             
         CLEAR WNWA                                                             
*                                                                               
         LR    R6,R1                                                            
         USING FFINFO,R6                                                        
         LA    R5,WNFILPB                                                       
         USING FILPB,R5                                                         
*                                                                               
         COMMENT                   SET UP FILE PARM BLOCK                       
         MVC   FILPBNAME,CVBLANKS  FILE NAME                                    
         L     R3,FFNAMEL                                                       
         CEIL  R3,L'FILPBNAME                                                   
         MOVE  R3,FILPBNAME,FFNAME                                              
         IF    (FFPNAMEL,NE,0),BEGIN IF PREV (CURRENT) FILE NAME                
         MVC   FILPBCNAME,CVBLANKS PREVIOUS                                     
         L     R3,FFPNAMEL                                                      
         CEIL  R3,L'FILPBCNAME                                                  
         MOVE  R3,FILPBCNAME,FFPNAME                                            
         END                                                                    
         MVC   FILPBSETLIB,CVBLANKS  DEFAULT LIB                                
         MVC   FILPBSETLIB(L'CPLIB),CPLIB                                       
         COMMENT                   FIND USER, GROUP, ACCOUNT OPTIONS            
         SCPUSH ,                                                               
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  RESLVPRT                                                         
         SCANCHK                                                                
         SCPOP ,                                                                
         MVC   FILPBUSER,CPCUSER                                                
         MVC   FILPBGROUP,CPCGRP                                                
         MVC   FILPBSETUSER,CPUSERSV                                            
         MVC   FILPBSETGROUP,CPGRPSV                                            
         SET   FILPBFTYPWYL        WE WANT WYLBUR FORMAT NAME                   
*                                                                               
         COMMENT                   INIT ERROR BUFFER                            
         SEGINIT WNSGBUF,,WNSG                                                  
*                                                                               
         COMMENT                   BUILD FULL DSNAME                            
         LA    R1,FILPB                                                         
         LA    R2,WNERRID                                                       
         LA    R15,WNSG                                                         
         ACALL BUILDDSN            Build full dsname                            
*                                                                               
         IF    (R15,NZ),BEGIN                                                   
         MVC   CPERRID,WNERRID                                                  
         IF    ^CPFDSNNE,BEGIN                                                  
         TSEG  (R1),(R0)                                                        
         END                                                                    
         LA    R15,4                                                            
         B     WGSREXIT                                                         
         END                                                                    
*                                                                               
         COMMENT                   RETURN DATA SET NAME IN FFINFO               
         LA    R2,FILPBDSNAME      CALC R2 FILE NAME LEN                        
         LA    R3,FILPBDSNAME+L'FILPBDSNAME                                     
         IF    (@R2,NE,0),BEGIN                                                 
         WHILE ((@R2,NE,' '),AND,(R2,LT,R3)),BEGIN                              
         INCR  R2                                                               
         END                                                                    
         END                                                                    
         LA    R3,FILPBDSNAME                                                   
         SR    R2,R3                                                            
         LA    R4,FILPBDSMEM       CALC R4 FILE NAME LEN                        
         LA    R3,FILPBDSMEM+L'FILPBDSMEM                                       
         IF    (@R4,NE,0),BEGIN                                                 
         WHILE ((@R4,NE,' '),AND,(R4,LT,R3)),BEGIN                              
         INCR  R4                                                               
         END                                                                    
         END                                                                    
         LA    R3,FILPBDSMEM                                                    
         SR    R4,R3                                                            
         COMMENT                   MOVE IN FILE NAME                            
         LR    R3,R2                                                            
         MOVE  R3,FFRNAME,FILPBDSNAME                                           
         ST    R2,FFRNAMEL                                                      
         IF    (R4,POS),BEGIN      IF MEMBER, MOVE IT TOO                       
         LA    R2,FFRNAME                                                       
         A     R2,FFRNAMEL                                                      
         MVI   @R2,C'('                                                         
         INCR  R2                                                               
         LR    R3,R4                                                            
         MOVE  R3,@R2,FILPBDSMEM                                                
         AR    R2,R4                                                            
         MVI   @R2,C')'                                                         
         A     R4,FFRNAMEL                                                      
         A     R4,=A(2) FOR '()'                                                
         ST    R4,FFRNAMEL                                                      
         END                                                                    
         CLEAR R15                                                              
*                                                                               
WGSREXIT LABEL ,                                                                
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
         EJECT                                                                  
*                                                                               
*  SCKWS AND ROUTINES TO SCAN ACCOUNT, GROUP, USER                              
*  FROM FILE OPTIONS IN FFOPTS IN FFINFO                                        
*                                                                               
RESLVPRT SCKW  ,V(ACCTPSH),PUSH                                                 
         SCKW  PUBLIC,RESLVPUB,A                                                
         SCKW  ,V(SCNNOOP)                                                      
*                                                                               
RESLVPUB PROC  ,                                                                
         MVC   CPCACCT,=C'&PUBUSER&PUBGRP'  Public account                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  BUILDDSN -- Routine to build the OS file name given the name                 
*    and all the other pieces.                                                  
*                                                                               
*    On entry:                                                                  
*       R1  - FILPB ptr                                                         
*       R2  - Pointer to store ERRID                                            
*       R15 - Error message SEGCB ptr                                           
*                                                                               
*    On exit, R15 (and cc):                                                     
*       0 = ok; DSNAME and MEMBER filled in:                                    
*             FILPBDSNAME - 44 character OS file name                           
*             FILPBDSMEM  - 8 character member name                             
*                             (binary zero if none)                             
*             FILPBDSACCT - account (uuugg) of dsname owner                     
*                             (binary zero if non-standard ds)                  
*                                                                               
*       nz= error, R1,R0 = error message loc, len, ERRID stored                 
*                                                                               
BDSNWA   RECORD BEGIN                                                           
BDSNERAD DS    A                   ERRID ptr                                    
BDSNESGP DS    A                   Error SEGCB ptr                              
BDSNERID DS    CL8                 ERRID save                                   
BDSNSG   SEGCB ,                   Filname expansion SEGCB                      
BDSNBUF  DS    CL128               Full filename area                           
         END                                                                    
*-                                                                              
BUILDDSN PROC  BDSNWA                                                           
         LR    R3,R1                                                            
         WITH  (FILPB,R3)                                                       
*                                                                               
         CLEAR BDSNWA                                                           
         ST    R15,BDSNESGP        Save error SEGCB ptr                         
         ST    R2,BDSNERAD         Save ERRID ptr                               
         SEGCLR .(R15)             Reset error SEGCB                            
*                                                                               
         CLEAR FILPBDSNAME         No dsname yet                                
         CLEAR FILPBDSMEM          No member speified                           
         CLEAR FILPBDSACCT         No account for this dsname yet               
*                                                                               
         SEGINIT BDSNBUF,,BDSNSG                                                
         MVC   BDSNBUF,CVBLANKS    Pre-blank filename area                      
*-                                                                              
*-       Check for too many levels of recursion (this could                     
*-         happen, for example, if the current filename                         
*-         was "*").                                                            
*-                                                                              
         INCR  R15,FILPBLEVEL      Count nest level                             
         IF    (R15,GT,3),BEGIN    Nested too deep...                           
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': file name can not be resolved.',,.L:BDSNESGP                  
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
*-                                                                              
*-       Process USER/GROUP/ACCOUNT overrides.                                  
*-                                                                              
         IF    ((FILPBACCOUNT,NZ),AND,                                 *        
               ((FILPBUSER,NZ),OR,(FILPBGROUP,NZ))),BEGIN                       
         CLEAR R0                                                               
*                                                                               
         IF    (FILPBUSER,NZ),BEGIN                                             
         SEG   (R1),(R0),.L:BDSNESGP                                            
         SEG   'USER=',,.L:BDSNESGP                                             
         SEGT  FILPBUSER,,.L:BDSNESGP                                           
         SETMSG ' and '                                                         
         END                                                                    
*                                                                               
         IF    (FILPBGROUP,NZ),BEGIN                                            
         SEG   (R1),(R0),.L:BDSNESGP                                            
         SEG   'GROUP=',,.L:BDSNESGP                                            
         SEGT  FILPBGROUP,,.L:BDSNESGP                                          
         SETMSG ' and '                                                         
         END                                                                    
*                                                                               
         IF    (FILPBACCOUNT,NZ),BEGIN                                          
         SEG   (R1),(R0),.L:BDSNESGP                                            
         SEG   'ACCOUNT=',,.L:BDSNESGP                                          
         SEGT  FILPBACCOUNT,,.L:BDSNESGP                                        
         END                                                                    
*                                                                               
         SEG   ' are mutually exclusive options.',,.L:BDSNESGP                  
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
*-                                                                              
*-       Check out ACCOUNT option and convert it to USER and GROUP.             
*-                                                                              
         IF    (FILPBACCOUNT,NZ),BEGIN  Validate account option...              
         SETMSG FILPBACCOUNT        Account                                     
         VCALL RTRIM                                                            
         IF    ((R0,EQ,6),AND,(@R1+3,EQ,'$')),BEGIN  uuu$gg...                  
         MVC   @R13(5),FILPBACCOUNT  Save "uuu$gg"                              
         MVC   FILPBACCOUNT(2),@R13+4  Convert...                               
         MVI   FILPBACCOUNT+2,C'.'       to...                                  
         MVC   FILPBACCOUNT+3(3),@R13      gg.uuu                               
         END                                                                    
*                                                                               
         IF    ((R0,NE,6),OR,                                          *        
               ((@R1+2,NE,'.'),AND,(@R1+2,NE,'$'))),BEGIN  Bad...               
         SEG   'ACCOUNT=',,.L:BDSNESGP                                          
         SEGT  FILPBACCOUNT,,.L:BDSNESGP                                        
         SEG   ' is incorrectly specified.',,.L:BDSNESGP                        
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
*                                                                               
         MVC   FILPBGROUP,FILPBACCOUNT  Save group                              
         MVC   FILPBUSER,FILPBACCOUNT+3  Save user                              
         CLEAR FILPBACCOUNT        Reset account                                
         END                                                                    
*-                                                                              
*-       Isolate file name and member name.                                     
*-                                                                              
         SETMSG FILPBNAME           Filename                                    
         VCALL LRTRIM              Strip blanks                                 
         VCALL TOUPPER             Upper case                                   
         LR    R6,R1                                                            
         LR    R5,R0               R6,R5 = file name loc, len                   
*-                                                                              
*-       Allow quoted names but do no special processing                        
*-                                                                              
         VCALL LRTRIM                                                           
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         INCR  R1                                                               
         DECR  R0                                                               
         DECR  R0                                                               
         VCALL LRTRIM                                                           
         IF    (R0,EQ,0),BEGIN     If no name, default is lib                   
         MVC   FILPBNAME,=CL(L'FILPBNAME)'LIB '                                 
         SETMSG FILPBNAME,3                                                     
         END                                                                    
         LR    R6,R1                                                            
         LR    R5,R0                                                            
         END                                                                    
*-                                                                              
*-       It's a library, case 1A:  filename(member).                            
*-                                                                              
         LR    R2,R6                                                            
         AR    R2,R5                                                            
         DECR  R2                  R2 = last char of dsname                     
         IF    (@R2,EQ,')'),BEGIN  It's file(member)...                         
         DECR  R5                  Account for ")"                              
         DECR  R2                  Back up ptr                                  
         CLEAR R15                 Init member length                           
*                                                                               
         LOOP  BEGIN               Backscan for "("...                          
         IF    (R5,NP),BDSNERR     Bad dsname format                            
         IF    (@R2,EQ,'('),EXIT   Found start of member, scram                 
         DECR  R2                  Back up ptr                                  
         DECR  R5                  Reduce length                                
         LA    R15,@R15+1          One more character                           
         IF    (R15,GT,8),BEGIN    Member name too long...                      
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': member name is too long.',,.L:BDSNESGP                        
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
         END                                                                    
*                                                                               
         DECR  R5                  Account for "("                              
*                                                                               
         MVC   FILPBDSMEM,CVBLANKS  Pre-blank                                   
         MOVE  R15,FILPBDSMEM,@R2+1  Save member name                           
*-                                                                              
*-       Its really a GDG name, case 1B:  filename(gdg #).                      
*-                                                                              
         COMMENT                   Check if member is really GDG                
         LA    R2,FILPBDSMEM                                                    
         IF    ((@R2,EQ,C'+'),OR,(@R2,EQ,C'-'),OR,                     X        
               (@R2,EQ,'0)')),BEGIN                                             
         LR    R5,R0               If GDG, restore orig len                     
         CLEAR FILPBDSMEM                                                       
         END                                                                    
         END                                                                    
*-                                                                              
*-       It's a library, case 2:  filename#member.                              
*-                                                                              
         ELSE  BEGIN               Might be "file#member"...                    
         SETMSG (R6),(R5)           Complete filename                           
*                                                                               
         WHILE (R0,POS),BEGIN      Look for "#"...                              
         IF    (@R1,EQ,'#'),EXIT   Start of member name, scram                  
         LA    R1,@R1+1                                                         
         DECR  R0                                                               
         END                                                                    
*                                                                               
         IF    (R0,POS),BEGIN      Found a member name...                       
         LR    R5,R1                                                            
         SR    R5,R6               Calculate length before "#"                  
*                                                                               
         LA    R1,@R1+1            Skip past "#"                                
         DECR  R0                  Adjust remaining length                      
*                                                                               
         IF    (R0,GT,8),BEGIN     Member name too long...                      
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': member name is too long.',,.L:BDSNESGP                        
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
*                                                                               
         MVC   FILPBDSMEM,CVBLANKS  Pre-blank                                   
         LR    R15,R0                                                           
         MOVE  R15,FILPBDSMEM,@R1  Save member name                             
         END                                                                    
         END                                                                    
*-                                                                              
*-       Handle "$filename" fully qualified dsnames.                            
*-                                                                              
         IF    (@R6,EQ,'$'),BEGIN  $-style...                                   
         IF    ((FILPBUSER,NZ),OR,(FILPBGROUP,NZ)),BEGIN                        
         SEG   'ACCOUNT/USER/GROUP override not ',,.L:BDSNESGP                  
         SEG   'allowed with $ file names.',,.L:BDSNESGP                        
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
         LA    R6,@R6+1                                                         
         DECR  R5                                                               
         SEG   (R6),(R5)           Full dsname                                  
         B     BDSNCHK             All done                                     
         END                                                                    
*-                                                                              
*-       Process "&uuu." and "@gg." prefixes (in any order).                    
*-                                                                              
         WHILE ((R5,POS),AND,((@R6,EQ,'&&'),OR,(@R6,EQ,'@'))),BEGIN             
*-                                                                              
*-       Handle "&.filename" or "&uuu.filename".                                
*-                                                                              
         IF    (@R6,EQ,'&&'),BEGIN  Starts with ampersand...                    
         IF    ((R5,GE,2),AND,(@R6+1,EQ,'.')),BEGIN  &.file...                  
         MVC   FILPBUSER,CPSUSER   Use requestor's user code                    
         LA    R6,@R6+2                                                         
         SH    R5,=H'2'                                                         
         END                                                                    
*                                                                               
         ELSEIF ((R5,GE,5),AND,(@R6+4,EQ,'.')),BEGIN  &uuu.file...              
         MVC   FILPBUSER,@R6+1     Save userid                                  
         LA    R6,@R6+5            Skip past "&uuu"                             
         SH    R5,=H'5'            Adjust length                                
         END                                                                    
*                                                                               
         ELSEIF (R5,EQ,1),BEGIN    &...                                         
         MVC   FILPBUSER,CPSUSER   Use requestor's user code                    
         LA    R6,@R6+1            Skip past "&"                                
         DECR  R5                  Adjust length                                
         END                                                                    
*                                                                               
         ELSEIF (R5,EQ,4),BEGIN    &uuu...                                      
         MVC   FILPBUSER,@R6+1     Save userid                                  
         LA    R6,@R6+4            Skip past "&uuu"                             
         SH    R5,=H'4'            Adjust length                                
         END                                                                    
*                                                                               
         ELSE  BEGIN               Bad format file name...                      
         SEG   (R6),(R5),.L:BDSNESGP                                            
         SEG   ': invalid user.',,.L:BDSNESGP                                   
         MVC   BDSNERID,=CL8'INVUSER'                                           
         B     BDSNERR                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Handle "@.filename" or "@gg.filename".                                 
*-                                                                              
         ELSEIF (@R6,EQ,'@'),BEGIN  Starts with at-sign...                      
         IF    ((R5,GE,2),AND,(@R6+1,EQ,'.')),BEGIN  @.file...                  
         MVC   FILPBGROUP,CPSGRP   Use requestor's group code                   
         LA    R6,@R6+2                                                         
         SH    R5,=H'2'                                                         
         END                                                                    
*                                                                               
         ELSEIF ((R5,GE,2),AND,(@R6+3,EQ,'.')),BEGIN  @gg.file...               
         MVC   FILPBGROUP,@R6+1    Save group                                   
         LA    R6,@R6+4            Skip past "@gg"                              
         SH    R5,=H'4'            Adjust length                                
         END                                                                    
*                                                                               
         ELSEIF (R5,EQ,1),BEGIN    @...                                         
         MVC   FILPBGROUP,CPSGRP   Use requestor's group                        
         LA    R6,@R6+1            Skip past "@"                                
         DECR  R5                  Adjust length                                
         END                                                                    
*                                                                               
         ELSEIF (R5,EQ,3),BEGIN    @gg...                                       
         MVC   FILPBGROUP,@R6+1    Save group                                   
         LA    R6,@R6+3            Skip past "@gg"                              
         SH    R5,=H'3'            Adjust length                                
         END                                                                    
*                                                                               
         ELSE  BEGIN               Bad format file name...                      
         SEG   (R6),(R5),.L:BDSNESGP                                            
         SEG   ': invalid group.',,.L:BDSNESGP                                  
         MVC   BDSNERID,=CL8'INVGRP'                                            
         B     BDSNERR                                                          
         END                                                                    
         END                                                                    
         END                                                                    
*-                                                                              
*-       Handle resolving "*" member name specification.                        
*-         This is done by expanding CNAME (a recursive call                    
*-         to BUILDDSN) and then saving the member name.                        
*-                                                                              
         IF    (FILPBDSMEM,EQ,'* '),BEGIN  Expand * member...                   
         CLEAR FILPBDSMEM          Reset member name                            
*                                                                               
         IF    (FILPBCNAME,NZ),BEGIN  We have a current name...                 
         XPUSH ,,L'XFILPB,PTR=R4   Make a new FILPB                             
         WITH  (XFILPB,R4),BEGIN                                                
         MOVEL XFILPB,FILPB,L'FILPB  Initialize it with our FILPB               
         MVC   XFILPBNAME,FILPBCNAME  Set name to resolve                       
         LA    R1,XFILPB           File parameters                              
         L     R15,BDSNESGP        Pass our error SEGCB ptr                     
         ACALL BUILDDSN            Expand current dsname                        
         BNZ   BDSNEXIT            Nested error, scram                          
         MVC   FILPBDSMEM,XFILPBDSMEM  Save current member name                 
         END                                                                    
         XPOP  ,,L'XFILPB                                                       
         END                                                                    
*                                                                               
         IF    (FILPBDSMEM,Z),BEGIN  No member name...                          
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': illegal, no current member name.',,.L:BDSNESGP                
         MVC   BDSNERID,=CL8'NOSTAR'                                            
         B     BDSNERR                                                          
         END                                                                    
         END                                                                    
*-                                                                              
*-       Add default library if member specified with no file name.             
*-                                                                              
         IF    ((R5,Z),AND,(FILPBDSMEM,NZ)),BEGIN  Mem w/no lib...              
         XPUSH ,,L'XFILPB,PTR=R4   Make a new FILPB                             
         WITH  (XFILPB,R4),BEGIN                                                
         MOVEL XFILPB,FILPB,L'FILPB  Initialize with our FILPB                  
         MVC   XFILPBNAME,FILPBSETLIB  Set name to resolve                      
         IF    (XFILPBNAME,Z),BEGIN If no default lib                           
         MVC   XFILPBNAME,CVBLANKS  Default lib to LIB                          
         MVC   XFILPBNAME(3),=CL3'LIB'                                          
         END                                                                    
         LA    R1,XFILPB           File parameters                              
         L     R15,BDSNESGP        Pass our error SEGCB ptr                     
         ACALL BUILDDSN            Expand library prefix                        
         BNZ   BDSNEXIT            Nested error, scram                          
         MVC   FILPBDSNAME,XFILPBDSNAME  Save full library name                 
         MVC   FILPBDSACCT,XFILPBDSACCT  Copy ownership account too             
         END                                                                    
         XPOP  ,,L'XFILPB                                                       
*                                                                               
         CLEAR R15                 All done                                     
         B     BDSNEXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Handle resolving "*" file name.                                        
*-         We expand CNAME with a recursive call to BUILDDSN                    
*-         and then exit.                                                       
*-                                                                              
         IF    ((R5,POS),AND,(@R6,EQ,'*')),BEGIN  * file name...                
         IF    (R5,GT,1),BEGIN                                                  
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': invalid characters in dsname.',,.L:BDSNESGP                   
         MVC   BDSNERID,=CL8'STARBAD'                                           
         B     BDSNERR                                                          
         END                                                                    
         IF    (FILPBCNAME,Z),BEGIN  No current filename...                     
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': illegal, no current file name.',,.L:BDSNESGP                  
         MVC   BDSNERID,=CL8'NOSTAR'                                            
         B     BDSNERR                                                          
         END                                                                    
*                                                                               
         XPUSH ,,L'XFILPB,PTR=R4   Make a new FILPB                             
         WITH  (XFILPB,R4),BEGIN                                                
         MOVEL XFILPB,FILPB,L'FILPB  Initialize it with our FILPB               
         MVC   XFILPBNAME,FILPBCNAME  Set name to resolve                       
         LA    R1,XFILPB           File parameters                              
         L     R15,BDSNESGP        Pass our error SEGCB ptr                     
         LA    R2,BDSNERID          and our ERRID pointer                       
         ACALL BUILDDSN            Expand library prefix                        
         BNZ   BDSNEXIT            Nested error, scram                          
*                                                                               
         MVC   FILPBDSNAME,XFILPBDSNAME  Save expanded dsname                   
         MVC   FILPBDSACCT,XFILPBDSACCT  Save ownership account too             
         IF    (FILPBDSMEM,Z),BEGIN      If not *#member                        
         MVC   FILPBDSMEM,XFILPBDSMEM    Save current member name               
         END                                                                    
         END                                                                    
         XPOP  ,,L'XFILPB                                                       
*                                                                               
         CLEAR R15                 All done                                     
         B     BDSNEXIT            Scram                                        
         END                                                                    
*-                                                                              
*-       Check for "WYL.gg.uuu.<anything>" filenames.                           
*-                                                                              
         IF    ((R5,GT,11),AND,(@R6,EQ,'WYL.')),BEGIN                           
         IF    (@R6+3,NE,'.'),EXIT                                              
         IF    (@R6+6,NE,'.'),EXIT                                              
         IF    (@R6+10,NE,'.'),EXIT                                             
         COMMENT                   Specified user/grp override                  
         IF    (FILPBGROUP,NZ),'MVC @R6+4(2),FILPBGROUP'                        
         IF    (FILPBUSER,NZ),'MVC @R6+7(3),FILPBUSER'                          
         SEG   (R6),(R5)           Full dsname                                  
         B     BDSNCHK                                                          
         END                                                                    
*-                                                                              
*-       Check for "gg$uuu.<anything>" filenames.                               
*-                                                                              
         IF    ((R5,GT,7),AND,(@R6+2,EQ,'$')),BEGIN                             
         IF    (@R6+6,NE,'.'),EXIT                                              
         COMMENT                   Specified user/grp override                  
         IF    (FILPBGROUP,NZ),'MVC @R6(2),FILPBGROUP'                          
         IF    (FILPBUSER,NZ),'MVC @R6+3(3),FILPBUSER'                          
         SEG   (R6),(R5)           Full dsname                                  
         B     BDSNCHK                                                          
         END                                                                    
*-                                                                              
*-       Check for "SYSn.<anything>" filenames.                                 
*-                                                                              
         IF    ((R5,GT,5),AND,(@R6,EQ,'SYS')),BEGIN                             
         IF    ((@R6+3,LT,'0'),OR,(@R6+3,GT,'9')),EXIT                          
         IF    (@R6+4,NE,'.'),EXIT                                              
         SEG   (R6),(R5)           Full dsname                                  
         B     BDSNCHK                                                          
         END                                                                    
*-                                                                              
*-       If Type=Wylbur means "WYL.gg.uuu.filename".                            
*-                                                                              
         IF    FILPBFTYPWYL,BEGIN  Wylbur format...                             
         SEG   'WYL.'                                                           
*                                                                               
         SETMSG FILPBGROUP          Group override                              
         IF    (FILPBGROUP,Z),BEGIN                                             
         SETMSG FILPBSETGROUP       Default group                               
         IF    (FILPBSETGROUP,Z),'SETMSG CPSGRP'                                
         END                                                                    
         SEG   (R1),(R0)                                                        
         SEG   '.'                                                              
*                                                                               
         SETMSG FILPBUSER           User override                               
         IF    (FILPBUSER,Z),BEGIN                                              
         SETMSG FILPBSETUSER        Default user                                
         IF    (FILPBSETUSER,Z),'SETMSG CPSUSER'                                
         END                                                                    
         SEG   (R1),(R0)                                                        
         SEG   '.'                                                              
*                                                                               
         SEG   (R6),(R5)                                                        
         B     BDSNCHK                                                          
         END                                                                    
*-                                                                              
*-       If Type=TSO means "gg$uuu.filename".                                   
*-                                                                              
         IF    FILPBFTYPTSO,BEGIN  TSO format...                                
         SETMSG FILPBGROUP          Group override                              
         IF    (FILPBGROUP,Z),BEGIN                                             
         SETMSG FILPBSETGROUP       Default group                               
         IF    (FILPBSETGROUP,Z),'SETMSG CPSGRP'                                
         END                                                                    
         SEG   (R1),(R0)                                                        
         SEG   '$'                                                              
         SETMSG FILPBUSER           User override                               
         IF    (FILPBUSER,Z),BEGIN                                              
         SETMSG FILPBSETUSER        Default user                                
         IF    (FILPBSETUSER,Z),'SETMSG CPSUSER'                                
         END                                                                    
         SEG   (R1),(R0)                                                        
         SEG   '.'                                                              
*                                                                               
         SEG   (R6),(R5)                                                        
         B     BDSNCHK                                                          
         END                                                                    
*-                                                                              
*-       If Type=EXACT then don't change it at all.                             
*-                                                                              
         IF    FILPBFTYPEXACT,BEGIN  Exact format...                            
         SEG   (R6),(R5)                                                        
         B     BDSNOK                                                           
         END                                                                    
*-                                                                              
*-       Unknown file type.                                                     
*-                                                                              
         SETMSG 'Unknown file type (expecting TYPE=WYLBUR).'                    
         B     BDSNERR                                                          
*-                                                                              
*-       Do final data set name cleanup and checking.                           
*-                                                                              
BDSNCHK  IF    (FILPBDSMEM,EQ,CVBLANKS),'CLEAR FILPBDSMEM'                      
*                                                                               
         SETMSG BDSNBUF             Dsname                                      
         VCALL RTRIM               Trim trailing blanks                         
         VCALL TOUPPER             Convert to upper case                        
         L     R2,BDSNSGLENF                                                    
         SH    R2,=AL2(L'FILPBDSNAME)  Too long?                                
         IF    POS,BEGIN           Yes...                                       
         SEGT  FILPBNAME,,.L:BDSNESGP                                           
         SEG   ': file name is ',,.L:BDSNESGP                                   
         SEGDC (R2),,.L:BDSNESGP                                                
         SEG   ' characters too long.',,.L:BDSNESGP                             
         MVC   BDSNERID,=CL8'ERROR'                                             
         B     BDSNERR                                                          
         END                                                                    
         B     BDSNOK              All done                                     
*-                                                                              
*-       Error return.                                                          
*-                                                                              
BDSNERR  L     R5,BDSNESGP                                                      
         WITH  (SEGCB,R5),BEGIN                                                 
         IF    (SEGCBLENF,Z),BEGIN  Generic error message...                    
         SEGT  FILPBNAME,,.SEGCB                                                
         SEG   ': illegal file name.',,.SEGCB                                   
         MVC   BDSNERID,=CL8'INVDSN'                                            
         END                                                                    
*                                                                               
         SETMSG L:SEGCBLOC,L:SEGCBLENF  Error message                           
         END                                                                    
*                                                                               
         L     R15,BDSNERAD                                                     
         MVC   0(8,R15),BDSNERID                                                
         PRETURN (R0,R1)                                                        
         LA    R15,4                                                            
         B     BDSNEXIT                                                         
*-                                                                              
*-       Normal return.                                                         
*-                                                                              
BDSNOK   MVC   FILPBDSNAME,BDSNBUF  Return full dsname                          
*-                                                                              
*-       Save owner's account for this file name.                               
*-         This means picking the "uuugg" out of a                              
*-         file name of the forms:                                              
*-            . WYL.gg.uuu.FILENAME                                             
*-            . gg$uuu.FILENAME                                                 
*-                                                                              
         IF    ((FILPBDSNAME,EQ,'WYL.'),AND,                           *        
               (FILPBDSNAME+3,EQ,'.'),AND,                             *        
               (FILPBDSNAME+10,EQ,'.')),BEGIN                                   
         MVC   FILPBDSACCT(3),FILPBDSNAME+7  uuu                                
         MVC   FILPBDSACCT+3(2),FILPBDSNAME+4  gg                               
         END                                                                    
*                                                                               
         ELSEIF ((FILPBDSNAME+2,EQ,'$'),AND,                           *        
               (FILPBDSNAME+6,EQ,'.')),BEGIN                                    
         MVC   FILPBDSACCT(3),FILPBDSNAME+3  uuu                                
         MVC   FILPBDSACCT+3(2),FILPBDSNAME  gg                                 
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
BDSNEXIT PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CHECKDSN -- Routine to check that the file name conforms                     
*    to the OS data set name conventions.                                       
*                                                                               
*      On entry:                                                                
*        R1  - FILPB ptr                                                        
*        R15 - Error message SEGCB ptr                                          
*                                                                               
*      On exit, R15 (and CC):                                                   
*        0 = ok                                                                 
*        nz= error, R1,R0 = error message loc, len                              
*                                                                               
CDSNWA   RECORD BEGIN                                                           
CDSNESGP DS    A                   Error SEGCB ptr                              
CDSNTYPE DS    F                   Max TRT value for valid dsn chars            
         END                                                                    
*-                                                                              
CHECKDSN PROC  CDSNWA                                                           
         LR    R3,R1                                                            
         WITH  (FILPB,R3)                                                       
*                                                                               
         ST    R15,CDSNESGP        Error SEGCB ptr                              
         SEGCLR .(R15)             Reset error SEGCB                            
*                                                                               
         SETMSG FILPBDSNAME         File name                                   
         VCALL RTRIM               Trim trailing blanks                         
*-                                                                              
*-       Check for null file name.                                              
*-                                                                              
         LR    R6,R1                                                            
         LTR   R5,R0                                                            
         IF    NP,BEGIN            Null filename...                             
         SEG   'Null file name is not allowed.',,.L:CDSNESGP                    
         B     CDSNERR                                                          
         END                                                                    
*-                                                                              
*-       Check file name character by character.                                
*-                                                                              
         LA    R4,8                Max no. of chars per level                   
         MVC   CDSNTYPE,=A(2)      Allow dot, alpha, or national                
         WHILE (R5,POS),BEGIN                                                   
         LC    R2,@R6              Get char                                     
         LC    R0,DSNCHR(R2)       Get char type                                
*                                                                               
         COMMENT                   IF '.',                                      
         IF    (@R6,EQ,'.'),BEGIN                                               
         IF    (R4,GE,8),BEGIN     Another dot...                               
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         SEG   ': illegal file name',,.L:CDSNESGP                               
         SEG   ' (file name has two dots together).',,.L:CDSNESGP               
         B     CDSNERR                                                          
         END                                                                    
         IF    (R5,EQ,1),BEGIN     Ends with a dot...                           
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         SEG   ': illegal file name',,.L:CDSNESGP                               
         SEG   ' (file name ends with a dot).',,.L:CDSNESGP                     
         B     CDSNERR                                                          
         END                                                                    
         LA    R4,8                Allow eight more chars                       
         MVC   CDSNTYPE,=A(2)      Allow dot, alpha, or national                
         END                                                                    
*                                                                               
         COMMENT                   IF '('                                       
         ELSEIF (@R6,EQ,'('),BEGIN Lib Member or GDG                            
         LA    R2,0(R6,R5)                                                      
         DECR  R2                                                               
         IF    (@R2,NE,')'),BEGIN                                               
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         SEG   ': illegal file name.',,.L:CDSNESGP                              
         SEG   'Mismatched parenthesis.',,.L:CDSNESGP                           
         B     CDSNERR                                                          
         END                                                                    
         LC    R2,1(R6)                                                         
         LC    R0,DSNCHR(R2)                                                    
         IF    ((R2,NE,C'+'),AND,(R2,NE,C'-'),AND,                     X        
               (@R6,NE,'(0)')),BEGIN                                            
         COMMENT                   If member, check as if name                  
         DECR  R5  ignore ')'                                                   
         LA    R4,8                                                             
         MVC   CDSNTYPE,=A(2)                                                   
         END                                                                    
         ELSE  BEGIN               Otherwise, check as GDG                      
         INCR  R6                                                               
         DECR  R5                                                               
         DECR  R5  ignore ')'                                                   
         IF    (R0,EQ,4),BEGIN                                                  
         INCR  R6                                                               
         DECR  R5                                                               
         END                                                                    
         IF    ((R5,NP),OR,(R5,GT,4)),BEGIN                                     
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         SEG   ': illegal file name.',,.L:CDSNESGP                              
         SEG   'Invalid member name.',,.L:CDSNESGP                              
         B     CDSNERR                                                          
         END                                                                    
         WHILE (R5,POS),BEGIN                                                   
         LC    R2,@R6                                                           
         LC    R0,DSNCHR(R2)                                                    
         IF    (R0,NE,3),BEGIN                                                  
         SEG   @R6,1,.L:CDSNESGP                                                
         SEG   ': illegal character in file name ',,.L:CDSNESGP                 
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         B     CDSNERR                                                          
         END                                                                    
         INCR  R6                                                               
         DECR  R5                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   OTHER CHARACTERS                             
         ELSE  BEGIN               All other chars                              
         IF    (R0,GT,CDSNTYPE),BEGIN Illegal character ...                     
         SEG   @R6,1,.L:CDSNESGP                                                
         SEG   ': illegal character in file name ',,.L:CDSNESGP                 
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         B     CDSNERR                                                          
         END                                                                    
         DECR  R4                                                               
         IF    (R4,NEG),BEGIN      Over 8 chars w/o a dot...                    
         SEGT  FILPBDSNAME,,.L:CDSNESGP                                         
         SEG   ': illegal file name',,.L:CDSNESGP                               
         SEG   ' (over 8 characters without a dot).',,.L:CDSNESGP               
         B     CDSNERR                                                          
         END                                                                    
         MVC   CDSNTYPE,=A(3)      Allow numbers too now                        
         END                                                                    
*                                                                               
         LA    R6,@R6+1                                                         
         DECR  R5                                                               
         END                                                                    
*                                                                               
         B     CDSNOK              All done                                     
*-                                                                              
*-       Error return.                                                          
*-                                                                              
CDSNERR  L     R15,CDSNESGP                                                     
         WITH  (SEGCB,R15),'SETMSG L:SEGCBLOC,L:SEGCBLENF' Err msg              
         PRETURN (R0,R1)                                                        
         LA    R15,4                                                            
         B     CDSNEXIT                                                         
*-                                                                              
*-       Normal return.                                                         
*-                                                                              
CDSNOK   CLEAR R15                 A-ok                                         
CDSNEXIT PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Neatness)                                   
*                                                                               
         QLTORG                                                                 
*-                                                                              
*-       OS file name character type table.                                     
*-                                                                              
*-         0 - Dot (.)                                                          
*-         1 - Alphabetic                                                       
*-         2 - National ($,@,#)                                                 
*-         3 - Numeric                                                          
*-         4 - GDG start chars (+-)                                             
*-       255 - Everything else                                                  
*-                                                                              
DSNCHR   DC    256X'FF'                                                         
         ORG   DSNCHR+C'.'                                                      
         DC    X'00'                                                            
         ORG   DSNCHR+C'A'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'J'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'S'                                                      
         DC    X'0101010101010101'                                              
         ORG   DSNCHR+C'$'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'@'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'#'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'0'                                                      
         DC    X'03030303030303030303'                                          
         ORG   DSNCHR+C'+'                                                      
         DC    X'04'                                                            
         ORG   DSNCHR+C'-'                                                      
         DC    X'04'                                                            
         ORG   ,                                                                
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
*  ** DEBUG **                                                                  
*  WE NEED SOME FIXING OF THE ERROR INTERFACE AND CLOSING OF                    
*  THE WINGS PATH SHOULD AN ERROR OCCUR,,, HOW COULD WE RETURN                  
*  AN ERROR AND AN END OF FILE ON THE EXTREAD CALL ,,, SHOULD                   
*  WE ???                                                                       
*                                                                               
*  WHAT ABOUT A EXT -- DSECT AREA ,,, WHAT ABOUT PASSING OPTIONS                
*  ON EXTOPEN ,,, ETC . ETC.                                                    
*                                                                               
*                                                                               
         EJECT                                                                  
         QLTORG                                                                 
*                                                                               
         VLTORG                                                                 
         END   .                                                                
