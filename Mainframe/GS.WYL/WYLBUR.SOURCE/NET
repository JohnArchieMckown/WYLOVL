NET      TITLE 'WYLBUR''s Network Interface'                                    
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
*                                                                               
WYLNET   CSECT                                                                  
*                                                                               
NET      IDENT 2025                11:49:25 01/25/02  (SLP)                     
         REGS  FSR,,,,,,,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         COPY  CONTROL                                                          
*                                                                               
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
*                                                                               
KWR      RECORD 'KWR'                                                           
*                                                                               
UPATH    RECORD BEGIN                                                           
UPAT     UPATH                                                                  
         END                                                                    
*                                                                               
UNET     RECORD BEGIN                                                           
UNET     UNET                                                                   
         END                                                                    
*                                                                               
SEGCB    RECORD 'SEGCB'                                                         
*                                                                               
         POP   DSECTS                                                           
         EJECT                                                                  
         COPY  FFINFO                                                           
         EJECT                                                                  
*box                                                                            
*                                                                               
*  Network File Device Work Area                                                
*                                                                               
NETWA    RECORD BEGIN                                                           
         DS    C'NET '             Self identification                          
*                                                                               
NWAFLAG  FLAG                                                                   
         FLAG  NWAFOPEN            - File is open                               
         FLAG  NWAFWR              - Writing data (SAVE command)                
NWAFLAG2 FLAG  ,                                                                
         FLAG  NWAFGUEST                                                        
         FLAG  NWAFASCII                                                        
         FLAG  NWAFTRAN                                                         
         FLAG  NWAFTABS                                                         
*                                                                               
NWAFNAMEL DS   F                   File name length                             
NWAFNAME DS    XL(L'FFNAME)        File name                                    
*                                                                               
NWAHOSTL DS    F                   Host name length                             
NWAHOST  DS    XL(L'FFHOST)         Host name                                   
*                                                                               
NWALIKAD DS    A                   Show dir like string                         
NWALIKLN DS    F                                                                
NWANEWAD DS    A                   Rename new name                              
NWANEWLN DS    F                                                                
*                                                                               
NWANUSER DS    CL32                Network user id                              
NWANPASS DS    CL32                Network password                             
*                                                                               
NWAUNETP DS    A                   Primary UNET ptr                             
NWAUSG   DS    A                   Primary UNET output SEGCB ptr                
*                                                                               
NWADNETP DS    A                   Data UNET ptr                                
NWADSG   DS    A                   Data UNET output SEGCB ptr                   
*                                                                               
NWALNO   DS    F                   Working line-number                          
*                                                                               
NWAOHEAP DS    A                   Old heap ptr                                 
NWANHEAP DS    A                   Current heap ptr                             
         END                                                                    
         EJECT                                                                  
*-                                                                              
*-       Well known sockets.                                                    
*-                                                                              
FTPPORT# EQU   21,,C'N'            File transfer socket                         
         SPACE 2                                                                
*-                                                                              
*-       Special telnet control byte values.                                    
*-                                                                              
$CTRLC   EQU   X'03',,C'N'         ASCII ctrl-C (treated as break)              
$TAB     EQU   X'09',,C'N'         ASCII tab                                    
$LF      EQU   X'0A',,C'N'         ASCII line-feed                              
$CR      EQU   X'0D',,C'N'         ASCII carriage return                        
$BLANK   EQU   X'20',,C'N'         ASCII blank                                  
*                                                                               
$SE      EQU   X'F0',,C'N'         240: End of subparms                         
$NOP     EQU   X'F1',,C'N'         241: No operation                            
$DM      EQU   X'F2',,C'N'         242: Data mark                               
$BRK     EQU   X'F3',,C'N'         243: Break                                   
$IP      EQU   X'F4',,C'N'         244: Interrupt process                       
$ABORT   EQU   X'F5',,C'N'         245: Abort output                            
$AYT     EQU   X'F6',,C'N'         246: Are you there                           
$EC      EQU   X'F7',,C'N'         247: Erase character                         
$EL      EQU   X'F8',,C'N'         248: Erase line                              
$GA      EQU   X'F9',,C'N'         249: Go ahead                                
$SB      EQU   X'FA',,C'N'         250: Start of subparms                       
$WILL    EQU   X'FB',,C'N'         251: Will                                    
$WONT    EQU   X'FC',,C'N'         252: Won't                                   
$DO      EQU   X'FD',,C'N'         253: Do                                      
$DONT    EQU   X'FE',,C'N'         254: Don't                                   
$IAC     EQU   X'FF',,C'N'         255: I am a control byte                     
*                                                                               
$BINARY  EQU   X'00',,C'N'         0:   Transmit-binary                         
$ECHO    EQU   X'01',,C'N'         1:   Echo                                    
$SGA     EQU   X'03',,C'N'         3:   Suppress go ahead                       
$TTYPE   EQU   X'18',,C'N'         24:  Terminal type                           
*                                                                               
$CRLF    EQU   1000,,C'N'          Our internal code for CR/LF                  
$STOP    EQU   1001,,C'N'          Our internal code for "stop!"                
         TITLE 'SET/SHOW/CLEAR HOSTS Commands'                                  
HOSTWA   RECORD BEGIN                                                           
HOSTFLAG FLAG                                                                   
         FLAG  HOSFUNIX            - Unix/NoUnix option                         
         FLAG  HOSFNOUNIX          - NoUnix option                              
         FLAG  HOSFDCUR            - We displayed current host                  
*                                                                               
HOSTHOST DS    CL(L'CPHOST)        Working host name                            
HOSTUSER DS    CL32                Working user name                            
HOSTPASS DS    CL32                Working password                             
*                                                                               
HOSTKEY  DS    CL64                Working record key area                      
*                                                                               
HOSTDATA DS    CL64                Working record data area                     
         END                                                                    
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  HOSTINIT -- Routine to set up the user's default network host                
*    and network user-id.  This routine is called whewn the user                
*    first logs on.                                                             
*                                                                               
*    On entry:                                                                  
*      R1 - KWR ptr                                                             
*                                                                               
HOSTINIT XPROC HOSTWA                                                           
         CLEAR HOSTWA                                                           
*                                                                               
         LR    R5,R1                                                            
         WITH  (KWR,R5)                                                         
*-                                                                              
*-       Build default network host and userid.                                 
*-                                                                              
         MVC   HOSTHOST,CVBLANKS                                                
         IF    (KWRNETH,NZ),'MVC HOSTHOST(L"KWRNETH),KWRNETH'                   
         ELSE  'MVC HOSTHOST(5),=C"LINDY"'  Universal host                      
*                                                                               
         MVC   HOSTUSER,CVBLANKS                                                
         IF    (KWRNETU,NZ),'MVC HOSTUSER(L"KWRNETU),KWRNETU'                   
         ELSE  'MVC HOSTUSER(5),=C"GUEST"'  Universal userid                    
*                                                                               
         L     R15,CVLOWTBL                                                     
         TR    HOSTUSER,@R15       Convert userid to lower case                 
*-                                                                              
*-       Save information in user's host list.                                  
*-                                                                              
         LA    R0,CP#HOSTI         Host information record set                  
         LA    R15,CPRG64          Misc keylen 64 record group                  
         VCALL XSETSET             Switch into correct RG                       
*                                                                               
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'USER_'                                             
         MVC   HOSTKEY+5(L'HOSTHOST),HOSTHOST                                   
*                                                                               
         LA    R2,HOSTKEY          Key                                          
         SETMSG HOSTUSER            Data                                        
         LA    R15,CPRG64                                                       
         VCALL XPUT                Save the user-id                             
*-                                                                              
*-       Make this the current host.                                            
*-                                                                              
         MVC   CPHOST,HOSTHOST     Set current host name                        
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SETHOST -- SET HOST Command.                                                 
*                                                                               
SETHOST  XPROC HOSTWA                                                           
         LA    R6,HOSTWA                                                        
         USING HOSTWA,R6                                                        
         CLEAR HOSTWA                                                           
*                                                                               
         SCANSTR ,                 Pick up next token                           
         SCANCHK                                                                
         IF    (R0,Z),BEGIN                                                     
         TSEG  'Missing host name; '                                            
         ERROR 'expecting "SET HOST host USER userid".'                         
         END                                                                    
*                                                                               
         IF    (R0,GT,L'CPHOST),BEGIN                                           
         TSEG  (R1),(R0)                                                        
         ERROR ': host name is too long.'                                       
         END                                                                    
*                                                                               
         SCUPCASE HOSTHOST,L'HOSTHOST                                           
*                                                                               
         SCAN  SETHPRT             Scan SET HOST options                        
         SCANCHK                                                                
*-                                                                              
*-       Prompt for password.                                                   
*-                                                                              
         WHILE (HOSTPASS,EQ,'* '),BEGIN                                         
         TSEG  'Enter password for '                                            
         IF    (HOSTUSER,NZ),'TSEG HOSTUSER,,T; TSEG " on "'                    
         TSEG  HOSTHOST,,T                                                      
         SET   CPFNECHO                                                         
         TREAD (R1),0,Q                                                         
         CLEAR CPFNECHO                                                         
*                                                                               
         IF    (R15,NZ),'CLEAR HOSTPASS'  Attn means don't set pw               
*                                                                               
         ELSE  BEGIN               Otherwise save new password...               
         VCALL LRTRIM                                                           
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTPASS                                                   
         MVC   HOSTPASS,CVBLANKS                                                
         MOVE  R15,HOSTPASS,@R1    Save new password                            
         END                                                                    
         END                                                                    
*                                                                               
         LA    R0,CP#HOSTI         Host information record set                  
         LA    R15,CPRG64          Misc keylen 64 record group                  
         VCALL XSETSET             Switch into correct RG                       
*-                                                                              
*-       SET HOST USER userid option.                                           
*-                                                                              
         IF    (HOSTUSER,NZ),BEGIN  Userid was specified...                     
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'USER_'                                             
         MVC   HOSTKEY+5(L'HOSTHOST),HOSTHOST                                   
*                                                                               
         LA    R2,HOSTKEY          Key                                          
         SETMSG HOSTUSER            Data                                        
         LA    R15,CPRG64                                                       
         VCALL XPUT                Save the user-id                             
*                                                                               
         MVC   HOSTKEY(5),=C'PASS_'                                             
         LA    R2,HOSTKEY                                                       
         LA    R15,CPRG64                                                       
         VCALL XDELETE             Delete any previous password                 
         END                                                                    
*-                                                                              
*-       SET HOST PASS password option.                                         
*-                                                                              
         IF    (HOSTPASS,NZ),BEGIN  Password was specified...                   
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'PASS_'                                             
         MVC   HOSTKEY+5(L'HOSTHOST),HOSTHOST                                   
*                                                                               
         LA    R2,HOSTKEY          Key                                          
         SETMSG HOSTPASS            Data                                        
         LA    R15,CPRG64                                                       
         VCALL XPUT                Save the password                            
         END                                                                    
*-                                                                              
*-       SET HOST UNIX option.                                                  
*-                                                                              
         IF    HOSFUNIX,BEGIN      Unix/NoUnix was specified...                 
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'UNIX_'                                             
         MVC   HOSTKEY+5(L'HOSTHOST),HOSTHOST                                   
*                                                                               
         LA    R2,HOSTKEY          Key                                          
         SETMSG 'TRUE'              Data                                        
         LA    R15,CPRG64                                                       
         IF    HOSFNOUNIX,'VCALL XDELETE'  NOUNIX                               
         ELSE  'VCALL XPUT'        UNIX                                         
         END                                                                    
*-                                                                              
*-       Make this the current host.                                            
*-                                                                              
         MVC   CPHOST,HOSTHOST     Set current host name                        
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
*-                                                                              
*-       SET HOST Options.                                                      
*-                                                                              
SETHPRT  SCKW  USER,SETHUSER,A                                                  
         SCKW  PASSWORD,SETHPASS,A                                              
         SCKW  UNIX,SETHUNIX,A                                                  
         SCKW  NOUNIX,SETHNOUX,A                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SETHUSER PROC  ,                                                                
         SCANSTR ,                 Get next token                               
         SCANCHK                                                                
         IF    (R0,Z),CVABSENT                                                  
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTUSER                                                   
         MVC   HOSTUSER,CVBLANKS                                                
         MOVE  R15,HOSTUSER,@R1    Save userid                                  
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETHPASS PROC  ,                                                                
         SCANSTR ,                 Get next token                               
         SCANCHK                                                                
         IF    (R0,Z),CVABSENT                                                  
         LR    R15,R0                                                           
         CEIL  R15,L'HOSTPASS                                                   
         MVC   HOSTPASS,CVBLANKS                                                
         MOVE  R15,HOSTPASS,@R1    Save password                                
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SETHUNIX PROC  ,                                                                
         SET   HOSFUNIX                                                         
         CLEAR HOSFNOUNIX                                                       
         PEND  ,                                                                
*                                                                               
SETHNOUX PROC  ,                                                                
         SET   HOSFUNIX                                                         
         SET   HOSFNOUNIX                                                       
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CLRHOSTS -- CLEAR HOSTS Command.                                             
*                                                                               
CLRHOSTS XPROC HOSTWA                                                           
         CLEAR HOSTWA                                                           
*                                                                               
         SCANSTR ,                 Pick up host name                            
         SCANCHK                                                                
         IF    (R0,NZ),BEGIN       We got a host name...                        
         IF    (R0,GT,L'CPHOST),BEGIN                                           
         TSEG  (R1),(R0)                                                        
         ERROR ': host name is too long.'                                       
         END                                                                    
         SCUPCASE HOSTHOST,L'HOSTHOST                                           
         END                                                                    
*-                                                                              
*-       CLEAR HOST hostname                                                    
*-                                                                              
         IF    (HOSTHOST,NZ),BEGIN  Specific host...                            
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'USER_'                                             
         MVC   HOSTKEY+5(L'HOSTHOST),HOSTHOST                                   
*                                                                               
         LA    R2,HOSTKEY                                                       
         LA    R15,CPRG64                                                       
         VCALL XDELETE             Delete "USER_hostname" record                
         IF    NZ,BEGIN            Nothing to delete...                         
         TSEG  'Host '                                                          
         TSEG  HOSTHOST,,T                                                      
         ERROR ' is not defined.'                                               
         END                                                                    
*                                                                               
         MVC   HOSTKEY(4),=C'PASS'                                              
         LA    R2,HOSTKEY                                                       
         LA    R15,CPRG64                                                       
         VCALL XDELETE             Delete "PASS_hostname" record                
*                                                                               
         MVC   HOSTKEY(4),=C'UNIX'                                              
         LA    R2,HOSTKEY                                                       
         LA    R15,CPRG64                                                       
         VCALL XDELETE             Delete "UNIX_hostname" record                
*                                                                               
         IF    (CPHOST,EQ,HOSTHOST),'CLEAR CPHOST'  Reset cur host              
*                                                                               
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       CLEAR HOSTS.                                                           
*-                                                                              
         CLEAR CPHOST              Reset current host                           
*                                                                               
         LA    R0,CP#HOSTI                                                      
         LA    R15,CPRG64                                                       
         VCALL XDELSET             Delete all saved host info                   
*                                                                               
         B     CVNEXT                                                           
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWHOST -- SHOW HOST Command.                                               
*                                                                               
SHOWHOST XPROC HOSTWA                                                           
         CLEAR HOSTWA                                                           
*                                                                               
         VCALL COLLOPTS                                                         
*                                                                               
         IF    (CPHOST,Z),BEGIN    No host set...                               
         SETMSG 'No current host is set.'                                       
         B     CVNXTMSG                                                         
         END                                                                    
*-                                                                              
*-       Get current host options.                                              
*-                                                                              
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY(5),=C'USER_'                                             
         MVC   HOSTKEY+5(L'CPHOST),CPHOST                                       
*                                                                               
         LA    R0,CP#HOSTI                                                      
         LA    R15,CPRG64                                                       
         VCALL XSETSET             Select our record set                        
*                                                                               
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTUSER                                                        
         LA    R15,CPRG64                                                       
         VCALL XGET                Get userid                                   
*                                                                               
         MVC   HOSTKEY(4),=C'PASS_'                                             
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTPASS                                                        
         LA    R15,CPRG64                                                       
         VCALL XGET                Get password                                 
*                                                                               
         MVC   HOSTKEY(4),=C'UNIX_'                                             
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTDATA                                                        
         LA    R15,CPRG64                                                       
         VCALL XGET                Get Unix/NoUnix flag                         
         IF    Z,'SET HOSFUNIX'                                                 
*-                                                                              
*-       Display host information line.                                         
*-                                                                              
         TSEG  '=> '                                                            
         TSEG  CPHOST              Current host name                            
*                                                                               
         IF    (HOSTUSER,NZ),BEGIN  Display userid...                           
         TSEG  ' User='                                                         
         TSEG  HOSTUSER,,T                                                      
         END                                                                    
*                                                                               
         IF    (HOSTPASS,NZ),BEGIN  Password is set...                          
         TSEG  ' Password=*'                                                    
         END                                                                    
*                                                                               
         IF    HOSFUNIX,'TSEG " Unix"'  Unix mode                               
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWHSTS -- SHOW HOSTS Command.                                              
*                                                                               
SHOWHSTS XPROC HOSTWA                                                           
         CLEAR HOSTWA                                                           
*                                                                               
         VCALL COLLOPTS                                                         
*                                                                               
         IF    (CPHOST,Z),BEGIN    No host set...                               
         SETMSG 'No hosts have been set.'                                       
         B     CVNXTMSG                                                         
         END                                                                    
*-                                                                              
*-       Get current host options.                                              
*-                                                                              
         MVC   HOSTKEY,CVBLANKS                                                 
         MVC   HOSTKEY+5(L'CPHOST),=(L'CPHOST)X'00'                             
*                                                                               
         LA    R0,CP#HOSTI                                                      
         LA    R15,CPRG64                                                       
         VCALL XSETSET             Select our record set                        
*                                                                               
         LOOP  BEGIN                                                            
         MVC   HOSTKEY(5),=C'USER_'                                             
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTUSER                                                        
         LA    R15,CPRG64                                                       
         VCALL XGETNEXT            Get userid                                   
         IF    NZ,EXIT                                                          
*                                                                               
         MVC   HOSTKEY(4),=C'PASS_'                                             
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTPASS                                                        
         LA    R15,CPRG64                                                       
         VCALL XGET                Get password                                 
*                                                                               
         MVC   HOSTKEY(4),=C'UNIX_'                                             
         LA    R2,HOSTKEY                                                       
         SETMSG HOSTDATA                                                        
         LA    R15,CPRG64                                                       
         VCALL XGET                Get Unix/NoUnix flag                         
         IF    Z,'SET HOSFUNIX'                                                 
*-                                                                              
*-       Display host information line.                                         
*-                                                                              
         SETMSG '   '                                                           
         IF    (CPHOST,EQ,HOSTKEY+5),'SETMSG "=> "; SET HOSFDCUR'               
         TSEG  (R1),(R0)                                                        
         TSEG  HOSTKEY+5,L'CPHOST  Host name                                    
*                                                                               
         IF    (HOSTUSER,NZ),BEGIN  Display userid...                           
         TSEG  ' User='                                                         
         TSEG  HOSTUSER,,T                                                      
         END                                                                    
*                                                                               
         IF    (HOSTPASS,NZ),BEGIN  Password is set...                          
         TSEG  ' Password=*'                                                    
         END                                                                    
*                                                                               
         IF    HOSFUNIX,'TSEG " Unix"'  Unix mode                               
         TCR                                                                    
         END                                                                    
*                                                                               
         IF    ^HOSFDCUR,BEGIN     Display current host now...                  
         TSEG  '=> '                                                            
         TSEG  CPHOST,,T                                                        
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         TITLE 'SET/SHOW WNET Commands'                                         
*box                                                                            
*                                                                               
*  SETWNET -- SET WNET Command.                                                 
*                                                                               
SETWNET  XPROC                                                                  
         SCAN  SETPRT              SET WNET Options                             
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
*-                                                                              
*-       SET WNET Options                                                       
*-                                                                              
SETPRT   SCKW  TRACE,SXTRC,A                                                    
         SCKW  NOTRACE,SXNOTRC,A                                                
         SCKW  ON,SXON                                                          
         SCKW  OFF,SXOFF                                                        
         SCKW  TCLEAR,SXTCLR,A                                                  
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
SXTRC    PROC  ,                                                                
         SET   CVFNTRC                                                          
         PEND  ,                                                                
*                                                                               
SXNOTRC  PROC  ,                                                                
         CLEAR CVFNTRC                                                          
         PEND  ,                                                                
*                                                                               
SXON     PROC  ,                                                                
         IF    (CPSFSPR+CPSFOPR,Z),'VCALL NOTVALID'                             
         CLEAR CVFNONET                                                         
         INCR  R15,CVCHKUC         Checkpoint write needed                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SXOFF    PROC  ,                                                                
         IF    (CPSFSPR+CPSFOPR,Z),'VCALL NOTVALID'                             
         SET   CVFNONET                                                         
         INCR  R15,CVCHKUC         Checkpoint write needed                      
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
SXTCLR   PROC                                                                   
         WHILE ('LT R1,CVETRCP',NZ),BEGIN  Free all trace entries...            
         VCALL FREEETRC            Free this entry                              
         END                                                                    
         CLEAR R15                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWWNET -- SHOW WNETWORK Command.                                           
*                                                                               
SHOWWNET XPROC                                                                  
         CLEAR R5                  Default is all UNETs                         
         SCAN  WNETPRT                                                          
         SCANCHK                                                                
*-                                                                              
*-       Do the "SHOW WNET n" command.                                          
*-                                                                              
         IF    (R5,NZ),BEGIN       Specific UNET specified...                   
         LR    R6,R5                                                            
         DECR  R6                  Relative to one, not zero                    
         SLL   R6,2                Times 4 for word offset                      
         A     R6,CVNETP                                                        
         LT    R15,@R6             Get UNET ptr                                 
         IF    NZ,'VCALL DISPUNET'  Display UNET info                           
         ELSE  BEGIN               Not active...                                
         TSEG  'Network connection '                                            
         TNUM  (R5)                                                             
         ERROR ' is not active.'                                                
         END                                                                    
*                                                                               
         B     CVNEXT                                                           
         END                                                                    
*-                                                                              
*-       Do the "SHOW WNET" command.                                            
*-                                                                              
*                                                                               
         ACALL NETDISP             Display general information                  
         B     CVNEXT                                                           
         PEND  ,                                                                
*-                                                                              
*-       SHOW WNET options.                                                     
*-                                                                              
WNETPRT  SCKW  ,WNETNO,PI                                                       
         SCKW  TRACE,SHOWTRC,A                                                  
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
*-                                                                              
WNETNO   PROC  ,                                                                
         IF    ((R0,NEG),OR,(R0,GT,CVNET#)),'VCALL NOTVALID'                    
         LR    R5,R0                                                            
         PRETURN (R5)                                                           
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SHOWTRC -- SHOW WNET TRACE Routine.                                          
*                                                                               
SHOWTRC  XPROC                                                                  
*                                                                               
         IF    ^CPSFSPR,'VCALL NOTVALID'   Priv'd                               
*                                                                               
         VCALL NTRCDISP                                                         
         B     CVNEXT                                                           
*                                                                               
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETDISP -- Routine to display general network information.                   
*                                                                               
*    This routine is called by the SHOW WNET command and also the               
*    dump formatter.                                                            
*                                                                               
NETDISP  XPROC                                                                  
         TSEG  CVWYLSSN,,T                                                      
         TSEG  ' network information:'                                          
         TCR                                                                    
         TCR                                                                    
*                                                                               
         TSEG  'Amelia interface is turned '                                    
         SETMSG 'ON.'                                                           
         IF    CVFNONET,'SETMSG "OFF."'                                         
         TSEG  (R1),(R0),CR                                                     
         TCR                                                                    
         TNUM  L:CVENCMD,7                                                      
         TSEG  ' network commands',,CR                                          
         TNUM  L:CVENUSE,7                                                      
         TSEG  ' network uses',,CR                                              
         TNUM  L:CVENSAVE,7                                                     
         TSEG  ' network saves',,CR                                             
         TNUM  L:CVENSCR,7                                                      
         TSEG  ' network scratches',,CR                                         
         TNUM  L:CVENREN,7                                                      
         TSEG  ' network renames',,CR                                           
         TNUM  L:CVENDIR,7                                                      
         TSEG  ' network show directories',,CR                                  
         TCR                                                                    
*                                                                               
         TNUM  L:CVENIO,7                                                       
         TSEG  ' network I/O''s',,CR                                            
         TCR                                                                    
*                                                                               
         IF    (CVENERR,NZ),BEGIN  We have some errors...                       
         TSEG  '*** '                                                           
         TNUM  L:CVENERR                                                        
         TSEG  ' error packets; last error packet follows:',,CR                 
         HEXDUMP L:CVENERP,LH:CVENERL,FORM=OFFSET,CHARSET=EBCDIC                
         TCR                                                                    
         END                                                                    
*                                                                               
         TSEG  'AMELIA UPATH at '                                               
         THEX  L:CVAMYP,8                                                       
         TSEG  ', TESTAMY UPATH at '                                            
         THEX  L:CVTAMYP,8                                                      
         TCR                                                                    
         TCR                                                                    
*                                                                               
         TSEG  'UNET index table at '                                           
         THEX  L:CVNETP,8                                                       
         TCR                                                                    
         TCR                                                                    
*                                                                               
         LA    R5,1                Net path number                              
         L     R6,CVNETP           Start of net path table                      
         WHILE (R5,LE,CVNET#),BEGIN  Go through entries...                      
         IF    ('LT R15,@R6',NZ),'VCALL DISPUNET'  Display UNET info            
*                                                                               
         LA    R6,@R6+4            Next net path ptr                            
         LA    R5,@R5+1            Next net path number                         
         END                                                                    
*                                                                               
         PEND                                                                   
         TITLE 'SHOW/CLEAR FTPLOG Commands'                                     
*box                                                                            
*                                                                               
*  SHOWFTP -- SHOW FTPLOG Command.                                              
*                                                                               
SHOWFTP  XPROC                                                                  
         ABORT 'SHOW FTPLOG is not supported.'                                  
         PEND  ,                                                                
         EJECT                                                                  
*box                                                                            
*                                                                               
*  CLRFTP -- CLEAR FTPLOG Command.                                              
*                                                                               
CLRFTP   XPROC                                                                  
         ABORT 'CLEAR FTPLOG is not supported.'                                 
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Network File Access Routines'                                   
*box                                                                            
*                                                                               
*  NETOPEN -- Routine to open a Network file.                                   
*                                                                               
*    On entry:                                                                  
*      R1 - FILE INFO DSECT                                                     
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
NETOPEN  XPROC                                                                  
         LR    R5,R1                                                            
         USING FFINFO,R5                                                        
*-                                                                              
*-       Allocate heap space for NETWA.                                         
*-                                                                              
         VCALL MARKHEAP            Get current heap ptr                         
         LR    R2,R15                                                           
*                                                                               
         LH    R0,=AL2(L'NETWA)                                                 
         VCALL GETHEAP             Get work area                                
         ST    R1,CPDEVP                                                        
         LR    R6,R1                                                            
         USING NETWA,R6                                                         
         CLEAR NETWA                                                            
         MVC   NETWA(3),=C'NET'                                                 
         MVC   NWANUSER,CVBLANKS                                                
         MVC   NWANPASS,CVBLANKS                                                
*                                                                               
         ST    R2,NWAOHEAP         Save old heap ptr                            
         VCALL MARKHEAP            Get current heap ptr                         
         ST    R15,NWANHEAP        Save new heap ptr                            
*                                                                               
         COMMENT                   Statistics                                   
         IF    FFFREAD,'INCR R15,CVENUSE'    use                                
         ELSE  'INCR R15,CVENSAVE'           save                               
*                                                                               
         COMMENT                   SCAN NET OPTIONS                             
         SCPUSH                                                                 
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  NWAOPRT                                                          
         SCANCHK                   ERROR EXIT FROM HERE !                       
         SCPOP                                                                  
*                                                                               
         COMMENT                   RESOLVE NAME                                 
         VCALL NRESOLVE                                                         
*                                                                               
         COMMENT                   SET FILE, HOST NAMES                         
         ACALL SETFNAME                                                         
         ACALL SETFHOST                                                         
*-                                                                              
*-       Allocate Amelia network path.                                          
*-                                                                              
         CLEAR R0                  User UNET                                    
         VCALL NPATALLO            Allocate communications path                 
         IF    NZ,BEGIN            Trouble...                                   
         XPUSH R0,R1                                                            
         TSEG  'Can''t allocate network connection -- '                         
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),CR                                                     
         MVC   CPERRID,=CL8'NETERR'                                             
         LA    R15,4                                                            
         B     NETOEXIT                                                         
         END                                                                    
*-                                                                              
*-       Establish connection to remote host.                                   
*-                                                                              
         ST    R1,NWAUNETP         Save primary UNET path ptr                   
         LR    R4,R1                                                            
         WITH  (UNET,R4)                                                        
         LA    R15,UNETOUTSG                                                    
         ST    R15,NWAUSG          Save output SEGCB ptr                        
*                                                                               
         MVC   UNETRNAME,CVBLANKS  Pre-blank                                    
         L     R3,NWAHOSTL                                                      
         CEIL  R3,L'UNETRNAME                                                   
         MOVE  R3,UNETRNAME,NWAHOST Set remote host name                        
         MVC   UNETRPORT,=A(FTPPORT#)  Set FTP port number                      
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATOPEN            Open the network path                        
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'NO NETWORK SERVER'),BEGIN                               
         TSEG  'Amelia is not running; '                                        
         SETMSG 'network access is not possible.'                               
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'ERROR: UNKNOWN HOST'),BEGIN                            
         TSEG  NWAHOST,L:NWAHOSTL                                               
         SETMSG ': host not found.'                                             
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'TIMEOUT'),BEGIN                                        
         TSEG  'Unable to do this command, no response from '                   
         SETMSG NWAHOST,L:NWAHOSTL                                              
         VCALL RTRIM                                                            
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'ATTN'),BEGIN                                           
         TSEG  'Can''t connect to '                                             
         SETMSG NWAHOST,L:NWAHOSTL                                              
         VCALL RTRIM                                                            
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         XPUSH R0,R1                                                            
         TSEG  'Can''t open network connection -- '                             
         XPOP  R0,R1                                                            
         END                                                                    
*                                                                               
         TSEG  (R1),(R0),CR                                                     
         LA    R15,4                                                            
         B     NETOEXIT            Trouble                                      
         END                                                                    
*                                                                               
         IF    ^CPSTVIRT,BEGIN     Real terminals only...                       
         SET   DSNFSTAT            Give "in progress" status msgs               
         END                                                                    
*                                                                               
         ACALL FTPOPEN             Do initial FTP dialog                        
         IF    Z,BEGIN             Connection is established...                 
         SET   NWAFOPEN            File is open                                 
         COMMENT                   If open for write                            
         IF    (FFFWRITE,OR,FFFAPPD,OR,FFFREPL),BEGIN                           
         SET   NWAFWR                                                           
         CLEAR CPLFLG2.CPFSCRTC    Reset REPLACE (not implemented)              
         ACALL FTPSAVE             Go do FTP STOR operation                     
         END                                                                    
*                                                                               
         COMMENT                   If open for read                             
         ELSE  'ACALL FTPUSE'      Go do FTP RETR operation                     
         END                                                                    
*                                                                               
NETOEXIT PEND                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
*  NET OPEN OPTIONS                                                             
*                                                                               
*                                                                               
NWAOPRT  SCKW  GUEST,NSCGUEST                                                   
         SCKW  ASCII,NSCASCII                                                   
         SCKW  TABS,NSCTABS,A                                                   
         SCKW  TRANSPARENT,NSCTRANS,A                                           
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
*                                                                               
*  NET SCRATCH OPTIONS                                                          
*                                                                               
*                                                                               
NWASPRT  SCKW ,V(NOTVALID)                                                      
*                                                                               
*                                                                               
*  NET DIRECTORY OPTIONS                                                        
*                                                                               
*                                                                               
NWADPRT  SCKW ,V(NOTVALID)                                                      
*                                                                               
*                                                                               
*                                                                               
NSCGUEST PROC                                                                   
         SET   NWAFGUEST                                                        
         PEND                                                                   
*                                                                               
NSCASCII PROC                                                                   
         SET   NWAFASCII                                                        
         PEND                                                                   
*                                                                               
NSCTRANS PROC                                                                   
         SET   NWAFTRAN                                                         
         PEND                                                                   
*                                                                               
NSCTABS  PROC                                                                   
         SET   NWAFTABS                                                         
         PEND                                                                   
*                                                                               
         DROP  R5                                                               
         DROP  R6                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETREAD -- Routine to read the next line from a network file.                
*                                                                               
*    On entry:                                                                  
*      R1,R0 - loc, len of line buffer                                          
*      DEVICE work area contains information about the file.                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      R1,R0 - text loc,len                                                     
*      R2 - line number                                                         
*      R15=ZERO, line read.  R15=NZ, end of file                                
*                                                                               
*                                                                               
NRDWA    RECORD BEGIN                                                           
NRDLOC   DS    F                                                                
NRDLEN   DS    F                                                                
         END                                                                    
*                                                                               
*                                                                               
NETREAD  XPROC NRDWA                                                            
         ST    R1,NRDLOC                                                        
         ST    R0,NRDLEN                                                        
*                                                                               
         L     R6,CPDEVP           UNET ptr                                     
         WITH  (NETWA,R6)                                                       
         FAIL  (NETWA,NE,'NET'),BADNET,'Bad NETWA ptr'                          
*-                                                                              
*-       Get the next line.                                                     
*-                                                                              
NETRLOOP LOOP  BEGIN                                                            
         CLEAR R0                  0 = EBCDIC mode                              
         IF    NWAFASCII,'LA R0,1'  1 = ASCII mode                              
         L     R15,NWADNETP                                                     
         IF    ^NWAFTRAN,BEGIN     Regular text read                            
         VCALL NPATREAD                                                         
         END                                                                    
         IF    NWAFTRAN,BEGIN      Transparent mode                             
         VCALL NPATXRD                                                          
         END                                                                    
         IF    (R15,Z),EXIT        We have the line, scram                      
*-                                                                              
*-       Normal end of file.                                                    
*-                                                                              
         IF    (@R1,EQ,'END OF FILE'),BEGIN  End of file...                     
         LA    R15,4                                                            
         B     NETREXIT                                                         
         END                                                                    
*-                                                                              
*-       Network error of some sort.                                            
*-                                                                              
         XPUSH R0,R1                                                            
         TSEG  'Network error while reading -- '                                
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),WR                                                     
         LA    R15,4                                                            
         B     NETREXIT                                                         
         END                                                                    
*-                                                                              
*-       Return line to caller                                                  
*-                                                                              
         L      R2,NWALNO                                                       
         A      R2,CPDELTA                                                      
         ST     R2,NWALNO         New lineno                                    
         CLEAR  R15                                                             
*                                                                               
NETREXIT LABEL ,                                                                
         IF    (R15,Z),BEGIN       RETURN TEXT, LINE NUMBER                     
         L     R2,NRDLOC           MOVE TEXT TO LINE BUFFER                     
         L     R3,NRDLEN                                                        
         LR    R4,R1                                                            
         LR    R5,R0                                                            
         IF    (R5,GT,R3),'LR R5,R3'                                            
         MVCL  R2,R4                                                            
         L     R1,NRDLOC           RETURN LOC,LEN,LNO                           
         L     R2,NWALNO                                                        
         PRETURN (R0,R2)                                                        
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETWRITE -- Routine to write the next line to a network file.                
*                                                                               
*    On entry:                                                                  
*      R1,R0 - line location, len                                               
*      R2 - line number (ignored)                                               
*      Device work area contains information about the file.                    
*                                                                               
*    Only returns if ok.                                                        
*                                                                               
NETWRITE XPROC                                                                  
         L     R6,CPDEVP           NETWA ptr                                    
         WITH  (NETWA,R6)                                                       
         FAIL  (NETWA,NE,'NET'),BADNET,'Bad NETWA ptr'                          
*                                                                               
         FAIL  (R1,Z),NETERR9,'NETWRITE interface error.'                       
*-                                                                              
*-       Add the new data line to the seg buffer.  The seg                      
*-         routines will automatically write the buffer if                      
*-         it overflows.                                                        
*-                                                                              
         IF    ^NWAFTRAN,BEGIN     Normal text mode...                          
         SEGDEF L:NWADSG                                                        
         SEG   (R1),(R0)           Write line                                   
         SEG   X'0D25'             CR LF                                        
         END                                                                    
*-                                                                              
*-       Transparent write data.                                                
*-                                                                              
         ELSE  BEGIN               Transparent mode...                          
         LR    R15,R1              R1 = first character on line                 
         AR    R15,R0                                                           
         DECR  R15                 R15 = last character on line                 
         IF    ((R0,LT,2),OR,                                          *        
               ('CLC @R1(1),@R15',NE),OR,                              *        
               ((@R1,NE,'|'),AND,(@R1,NE,'@'))),BEGIN                           
         TSEG  'Line '                                                          
         L     R0,CPLCNO                                                        
         VCALL CVEXNO                                                           
         TSEG  (R1),(R0)                                                        
         SETMSG ' is not valid TRANSPARENT data.'                               
         IF    ((@R4,EQ,'-'),OR,(@R4,EQ,X'2D')),BEGIN                           
         TSEG  ' has Macintosh transparent data which can not '                 
         SETMSG 'be saved over the net.'                                        
         END                                                                    
         TSEG  (R1),(R0),CR                                                     
         ABORT 'SAVE command incomplete.'                                       
         END                                                                    
*-                                                                              
*-       If the first character is an "@" then the data is in ASCII,            
*-         and if the first character is a "|" then it is in EBCDIC.            
*-                                                                              
         IF    (@R1,EQ,'|'),BEGIN  ASCII...                                     
         VCALL TOASCII             If EBCDIC, convert to ASCII                  
         END                                                                    
*                                                                               
         L     R5,NWADNETP                                                      
         WITH  (UNET,R5),BEGIN                                                  
         SET   UNETFNOXL           Don't translate data                         
         SEGDEF L:NWADSG                                                        
         SH    R0,=H'2'            Deduct len of guard chars                    
         SEG   @R1+1,(R0)          Write line w/o translation                   
         CLEAR UNETFNOXL           Reset                                        
         END                                                                    
         END                                                                    
*-                                                                              
*-       Ask the user if he wants to stop if ATTN has been pressed.             
*-                                                                              
         L     R5,CVCURJCB                                                      
         WITH  (JCB,R5),BEGIN                                                   
         IF    JCBFATTN,BEGIN      Attn received...                             
         CLEAR JCBFATTN            Reset ATTN flag                              
*-                                                                              
*-       Our message will erase the "(xxx lines written)" status                
*-         message, so we reset the flag saying a status message                
*-         has been written.                                                    
*-                                                                              
         CLEAR CPFSTMSG            Turn off status message flag                 
*-                                                                              
*-       Write out our own message.                                             
*-                                                                              
         TNUM  L:DSNLCNT           Current number of lines                      
         IF    (DSNLTOT,NZ),BEGIN  Give total line count too...                 
         TSEG  ' of '                                                           
         TNUM  L:DSNLTOT                                                        
         END                                                                    
         TSEG  ' lines have been written so far.'                               
         TSEG  CVBLANKS,20,CR      (Extra padding erases old msg)               
*                                                                               
         SETMSG 'Do you want to continue saving this file'                      
         VCALL YESNO               We want a YES or a NO                        
         IF    Z,EXIT              Yes, scram                                   
*                                                                               
         TSEG  'File transfer stopped, your file is only '                      
         ABORT 'partially saved.'                                               
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         SEGDEF DUMMY              (Safety)                                     
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETCLOSE -- Routine to close a network file.                                 
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
NETCLOSE XPROC                                                                  
         L     R6,CPDEVP           NETWA ptr                                    
         WITH  (NETWA,R6)                                                       
         FAIL  (NETWA,NE,'NET'),NETCLOSE,'Bad NETWA ptr'                        
*-                                                                              
*-       Close any data connection.                                             
*-                                                                              
         IF    ('LT R5,NWADNETP',NZ),BEGIN  Data connection...                  
         WITH  (UNET,R5)                                                        
*                                                                               
         IF    (UNETFOUTEOF+UNETFOUTABORT,Z),BEGIN  Still active...             
         IF    NWAFWR,BEGIN        Write last buffer...                         
         IF    (UNETOUTSGLENF,NZ),'SEGWR ,,UNETOUTSG'  Write buffer             
         END                                                                    
         SEGCLR ,UNETOUTSG         (Neatness)                                   
*                                                                               
         LA    R1,=C'EOF'          Normal end of file                           
         LR    R15,R5                                                           
         VCALL NPATCTRL            Send end of file                             
         END                                                                    
*                                                                               
         LR    R15,R5                                                           
         VCALL NPATCLS             Close and free data path                     
         END                                                                    
*-                                                                              
*-       Do FTP signoff if appropriate.                                         
*-                                                                              
         IF    NWAFOPEN,BEGIN                                                   
         CLEAR NWAFOPEN            No longer open                               
         ACALL FTPCLOSE            Close FTP connection                         
         END                                                                    
*-                                                                              
*-       Close the control connection.                                          
*-                                                                              
         IF    ('LT R5,NWAUNETP',NZ),BEGIN  Control connection...               
         WITH  (UNET,R5)                                                        
*                                                                               
         IF    (UNETFOUTEOF+UNETFOUTABORT,Z),BEGIN  Still active...             
         LA    R1,=C'EOF'          Normal end of file                           
         LR    R15,R5                                                           
         VCALL NPATCTRL            Send end of file                             
         END                                                                    
*                                                                               
         LR    R15,R5                                                           
         VCALL NPATCLS             Close and free data path                     
         END                                                                    
*-                                                                              
*-       Release NETWA if possible.                                             
*-                                                                              
         MVC   NETWA(3),=C'ONT'    Invalidate NETWA                             
         CLEAR CPDEVP                                                           
         VCALL MARKHEAP            Get current heap ptr                         
         IF    (R15,EQ,NWANHEAP),BEGIN  We can reduce the heap...               
         L     R15,NWAOHEAP                                                     
         VCALL RELHEAP             Release NETWA                                
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETSCR -- Routine to scratch a network file.                                 
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
NETSCR   XPROC                                                                  
*                                                                               
         L     R4,CPFINFOP                                                      
         USING FFINFO,R4                                                        
*-                                                                              
*-       Allocate heap space for NETWA.                                         
*-                                                                              
         VCALL MARKHEAP            Get current heap ptr                         
         LR    R2,R15                                                           
*                                                                               
         LH    R0,=AL2(L'NETWA)                                                 
         VCALL GETHEAP             Get work area                                
         ST    R1,CPDEVP                                                        
         LR    R6,R1                                                            
         WITH  (NETWA,R6)                                                       
         CLEAR NETWA                                                            
         MVC   NETWA(3),=C'NET'                                                 
         MVC   NWANUSER,CVBLANKS                                                
         MVC   NWANPASS,CVBLANKS                                                
*                                                                               
         ST    R2,NWAOHEAP         Save old heap ptr                            
         VCALL MARKHEAP            Get current heap ptr                         
         ST    R15,NWANHEAP        Save new heap ptr                            
*                                                                               
         INCR  R15,CVENSCR         Count net scratch command                    
*                                                                               
         COMMENT                   SCAN NET OPTIONS                             
         SCPUSH                                                                 
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  NWASPRT                                                          
         SCANCHK                   ERROR EXIT FROM HERE !                       
         SCPOP                                                                  
*                                                                               
         COMMENT                   RESOLVE NAME                                 
         VCALL NRESOLVE                                                         
*                                                                               
         COMMENT                   SET FILE, HOST NAMES                         
         ACALL SETFNAME                                                         
         ACALL SETFHOST                                                         
*-                                                                              
*-       Allocate Amelia network path.                                          
*-                                                                              
         CLEAR R0                  User UNET                                    
         VCALL NPATALLO            Allocate communications path                 
         IF    NZ,BEGIN            Trouble...                                   
         XPUSH R0,R1                                                            
         TSEG  'Can''t allocate network connection -- '                         
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),CR                                                     
         MVC   CPERRID,=CL8'NETERR'                                             
         LA    R15,4                                                            
         B     NETSEXIT                                                         
         END                                                                    
*-                                                                              
*-       Establish connection to remote host.                                   
*-                                                                              
         ST    R1,NWAUNETP         Save primary UNET path ptr                   
         LR    R5,R1                                                            
         WITH  (UNET,R5)                                                        
         LA    R15,UNETOUTSG                                                    
         ST    R15,NWAUSG          Save output SEGCB ptr                        
*                                                                               
         MVC   UNETRNAME,CVBLANKS  Pre-blank                                    
         L     R3,NWAHOSTL                                                      
         CEIL  R3,L'UNETRNAME                                                   
         MOVE  R3,UNETRNAME,NWAHOST Host name                                   
         MVC   UNETRPORT,=A(FTPPORT#)  Set FTP port number                      
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATOPEN            Open the network path                        
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'NO NETWORK SERVER'),BEGIN                               
         TSEG  'Amelia is not running; '                                        
         SETMSG 'network access is not possible.'                               
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'ERROR: UNKNOWN HOST'),BEGIN                            
         TSEG  NWAHOST,L:NWAHOSTL                                               
         SETMSG ': host not found.'                                             
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'TIMEOUT'),BEGIN                                        
         TSEG  'Unable to do this command, no response from '                   
         SETMSG 'the remote computer.'                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         XPUSH R0,R1                                                            
         TSEG  'Can''t open network connection -- '                             
         XPOP  R0,R1                                                            
         END                                                                    
*                                                                               
         TSEG  (R1),(R0),CR                                                     
         LA    R15,4                                                            
         B     NETSEXIT            Trouble                                      
         END                                                                    
*                                                                               
         ACALL FTPOPEN             Do FTP dialog                                
         IF    Z,BEGIN             Connection is open...                        
         ACALL FTPSCR              Scratch the file                             
         END                                                                    
*                                                                               
NETSEXIT PEND                                                                   
         DROP  R4                                                               
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETREN -- Routine to rename a network file.                                  
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
NETREN   XPROC                                                                  
         ABORT 'Network RENAME command not yet supported.'                      
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  NETDIR -- Routine to do a network "SHOW DIRECTORY" command.                  
*                                                                               
*    On entry:                                                                  
*    R1,R0 - loc, len of like string                                            
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
NETDIR   XPROC                                                                  
*                                                                               
         L     R4,CPFINFOP                                                      
         USING FFINFO,R4                                                        
*-                                                                              
*-       Allocate heap space for NETWA.                                         
*-                                                                              
         VCALL MARKHEAP            Get current heap ptr                         
         LR    R2,R15                                                           
*                                                                               
         XPUSH R0,R1                                                            
         LH    R0,=AL2(L'NETWA)                                                 
         VCALL GETHEAP             Get work area                                
         ST    R1,CPDEVP                                                        
         LR    R6,R1                                                            
         WITH  (NETWA,R6)                                                       
         XPOP  R0,R1                                                            
*                                                                               
         CLEAR NETWA                                                            
         MVC   NETWA(3),=C'NET'                                                 
         MVC   NWANUSER,CVBLANKS                                                
         MVC   NWANPASS,CVBLANKS                                                
*                                                                               
         ST    R0,NWALIKLN                                                      
         ST    R1,NWALIKAD                                                      
*                                                                               
         ST    R2,NWAOHEAP         Save old heap ptr                            
         VCALL MARKHEAP            Get current heap ptr                         
         ST    R15,NWANHEAP        Save new heap ptr                            
*                                                                               
         INCR  R15,CVENDIR         Count net directory command                  
*                                                                               
         COMMENT                   SCAN NET OPTIONS                             
         SCPUSH                                                                 
         SCINIT FFOPTS,L:FFOPTSL                                                
         SCAN  NWADPRT                                                          
         SCANCHK                   ERROR EXIT FROM HERE !                       
         SCPOP                                                                  
*                                                                               
         COMMENT                   IF DIRECTORY FILE NAME GIVEN                 
         IF    (FFNAMEL,NZ),BEGIN                                               
         COMMENT                   RESOLVE NAME                                 
         VCALL NRESOLVE                                                         
         COMMENT                   SET FILE                                     
         ACALL SETFNAME                                                         
         END                                                                    
         ACALL SETFHOST            SET HOST EVEN IF NO FILE NAME                
*-                                                                              
*-       Allocate Amelia network path.                                          
*-                                                                              
         CLEAR R0                  User UNET                                    
         VCALL NPATALLO            Allocate communications path                 
         IF    NZ,BEGIN            Trouble...                                   
         XPUSH R0,R1                                                            
         TSEG  'Can''t allocate network connection -- '                         
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),CR                                                     
         MVC   CPERRID,=CL8'NETERR'                                             
         LA    R15,4                                                            
         B     NETDEXIT                                                         
         END                                                                    
*-                                                                              
*-       Establish connection to remote host.                                   
*-                                                                              
         ST    R1,NWAUNETP         Save primary UNET path ptr                   
         LR    R5,R1                                                            
         WITH  (UNET,R5)                                                        
         LA    R15,UNETOUTSG                                                    
         ST    R15,NWAUSG          Save output SEGCB ptr                        
*                                                                               
         MVC   UNETRNAME,CVBLANKS  Pre-blank                                    
         L     R3,NWAHOSTL                                                      
         CEIL  R3,L'UNETRNAME                                                   
         MOVE  R3,UNETRNAME,NWAHOST Host name                                   
         MVC   UNETRPORT,=A(FTPPORT#)  Set FTP port number                      
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATOPEN            Open the network path                        
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'NO NETWORK SERVER'),BEGIN                               
         TSEG  'Amelia is not running; '                                        
         SETMSG 'network access is not possible.'                               
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'ERROR: UNKNOWN HOST'),BEGIN                            
         TSEG  NWAHOST,L:NWAHOSTL                                               
         SETMSG ': host not found.'                                             
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'TIMEOUT'),BEGIN                                        
         TSEG  'Unable to do this command, no response from '                   
         SETMSG 'the remote computer.'                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         XPUSH R0,R1                                                            
         TSEG  'Can''t open network connection -- '                             
         XPOP  R0,R1                                                            
         END                                                                    
*                                                                               
         TSEG  (R1),(R0),CR                                                     
         LA    R15,4                                                            
         B     NETDEXIT            Trouble                                      
         END                                                                    
*                                                                               
         ACALL FTPOPEN             Do FTP dialog                                
         IF    Z,BEGIN             Connection is open...                        
         ACALL FTPDIR              Send LIST (show directory) cmd               
         END                                                                    
*                                                                               
NETDEXIT PEND                                                                   
         DROP  R4                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*                                                                               
*  RESOLVE NETWORK NAME                                                         
*                                                                               
*  PROCESS FILE NAME IN FFNAME IN FFINFO DSECT                                  
*  WE HAVE A FEW SIMPLE RULES (COMPARED TO IBM FILES):                          
*    IF QUOTED, WE UNQUOTE.                                                     
*    IF '*', WE EXPAND.                                                         
*                                                                               
*                                                                               
NETRESLV XPROC                                                                  
         LR    R6,R1                                                            
         WITH  (FFINFO,R6)                                                      
*                                                                               
         COMMENT                   NORMAL RESOLVED NAME IS FNAME                
         L     R2,FFNAMEL                                                       
         ST    R2,FFRNAMEL                                                      
         MOVE  R2,FFRNAME,FFNAME                                                
*                                                                               
         COMMENT                   IF *, EXPAND FILE NAME, AND                  
         COMMENT                     HOST NAME TOO IF NEEDED                    
         SETMSG FFNAME,L:FFNAMEL                                                
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         L     R2,FFPNAMEL                                                      
         ST    R2,FFRNAMEL                                                      
         MOVE  R2,FFRNAME,FFPNAME                                               
         IF    (FFHOSTL,Z),BEGIN   SET HOST TOO                                 
         L     R2,FFPHOSTL                                                      
         ST    R2,FFHOSTL                                                       
         MOVE  R2,FFHOST,FFPHOST                                                
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   IF QUOTED                                    
         SETMSG FFNAME,L:FFNAMEL                                                
         IF    (R0,POS),BEGIN                                                   
         IF    ((@R1,EQ,''''),OR,(@R1,EQ,'"')),BEGIN                            
         INCR  R1                                                               
         DECR  R0                                                               
         DECR  R0                                                               
         ST    R0,FFRNAMEL                                                      
         LR    R2,R0                                                            
         MOVE  R2,FFRNAME,@R1                                                   
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  SET FNAME                                                                    
*                                                                               
*  USE RESOLVED FILE NAME. SET VALUE IN NWA DSECT.                              
*                                                                               
*  NOTE/CAUTION !                                                               
*  SET FNAME USES RESOLVED FORM OF NET FILE NAME SO                             
*  NAME RESOLVE MUST BE CALLED PRIOR TO THIS CALL.                              
*  WOULD IT NOT BE BETTER JUST TO ISSUE NRESOLVE CALL                           
*  FROM THIS ROUTINE.  BE BETTER TO BUNDLE THEM UP                              
*  TOGETHER. ??                                                                 
*                                                                               
*                                                                               
SETFNAME PROC                                                                   
         L     R6,CPFINFOP                                                      
         WITH  (FFINFO,R6)                                                      
         L     R5,CPDEVP                                                        
         WITH  (NETWA,R5)                                                       
*                                                                               
         L     R2,FFRNAMEL                                                      
         ST    R2,NWAFNAMEL                                                     
         MOVE  R2,NWAFNAME,FFRNAME                                              
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*                                                                               
*  SET HOST NAME                                                                
*                                                                               
*  PROCESS HOST NAME, MOVE HOST NAME FROM FFINFO TO NWA DSECT.                  
*  IF NO HOST NAME, USE CPHOST.                                                 
*                                                                               
SETFHOST PROC                                                                   
         L     R6,CPFINFOP                                                      
         WITH  (FFINFO,R6)                                                      
         L     R5,CPDEVP                                                        
         WITH  (NETWA,R5)                                                       
*                                                                               
         CLEAR NWAHOSTL                                                         
*                                                                               
         SETMSG FFHOST,L:FFHOSTL                                                
         IF    (R0,POS),BEGIN      IF HOST NAME                                 
         ST    R0,NWAHOSTL                                                      
         LR    R2,R0                                                            
         MOVE  R2,NWAHOST,@R1                                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NO HOST, USE DEFAULT                      
         ELSE  BEGIN                                                            
         SETMSG CPHOST                                                          
         IF    ((@R1,NE,X'0'),AND,(@R1,NE,' ')),BEGIN                           
         VCALL LRTRIM                                                           
         ST    R0,NWAHOSTL                                                      
         LR    R2,R0                                                            
         MOVE  R2,NWAHOST,@R1                                                   
         END                                                                    
         END                                                                    
*                                                                               
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPOPEN -- Local routine to do initial FTP "signon" dialog.                  
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
FOPWA    RECORD BEGIN                                                           
FOPFLAG  FLAG                                                                   
         FLAG  FOPFASKUSER         - Ask for user-id                            
         FLAG  FOPFASKPASS         - Ask for password                           
*                                                                               
FOPKEY   DS    CL64                Working key area                             
*                                                                               
FOPTEMP  DS    CL16                Temporary area                               
         END                                                                    
*-                                                                              
FTPOPEN  PROC  FOPWA                                                            
         CLEAR FOPWA                                                            
*                                                                               
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPOPEN,'Bad NETWA ptr'                         
*                                                                               
         L     R5,NWAUNETP                                                      
         WITH  (UNET,R5)                                                        
         FAIL  (UNET,NE,'UNET'),FTPOPEN,'Bad UNET ptr'                          
*                                                                               
         L     R4,CPFINFOP                                                      
         WITH  (FFINFO,R4)                                                      
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Wait for initial ready message from FTP server.                        
*-                                                                              
         LA    R1,=C'...'          Wait for positive completion                 
         ACALL FTPREAD             Get greeting (ready) message                 
         BNZ   FTPOEXIT            Trouble, scram                               
*-                                                                              
*-       Send user-id.                                                          
*-                                                                              
         IF    (NWAFGUEST),BEGIN                                                
         MVC   NWANUSER,=CL(L'NWANUSER)'guest'                                  
         END                                                                    
*                                                                               
         IF    (NWANUSER,EQ,CVBLANKS),BEGIN  Prompt for userid...               
         LA    R0,CP#HOSTI         Host information record set                  
         LA    R15,CPRG64                                                       
         VCALL XSETSET                                                          
*                                                                               
         MVC   FOPKEY,CVBLANKS                                                  
         MVC   FOPKEY(5),=C'USER_'                                              
         L     R2,NWAHOSTL                                                      
         CEIL  R2,L'FOPKEY-5                                                    
         MOVE  R2,FOPKEY+5,NWAHOST  Host name                                   
         L     R15,CVUPPTBL        Upper case host name                         
         TR    FOPKEY,@R15                                                      
*                                                                               
         LA    R2,FOPKEY                                                        
         SETMSG NWANUSER            Put data here                               
         LA    R15,CPRG64                                                       
         VCALL XGET                Lookup saved userid                          
*                                                                               
         WHILE (NWANUSER,EQ,CVBLANKS),BEGIN                                     
         SET   FOPFASKUSER         We asked for the user-id                     
         TSEG  'Enter your '                                                    
         TSEG  UNETRNAME,,T                                                     
         TREAD ' user-id',,Q       Ask for userid                               
         BNZ   FTPOEXIT            Scram if logging off                         
         VCALL LRTRIM                                                           
*                                                                               
         MVC   NWANUSER,CVBLANKS                                                
         LR    R15,R0                                                           
         CEIL  R15,L'NWANUSER                                                   
         MOVE  R15,NWANUSER,@R1    Save network userid                          
         END                                                                    
         END                                                                    
*                                                                               
         SEG   'USER '                                                          
         SEGT  NWANUSER                                                         
         SEGCR                                                                  
         SEGWR                                                                  
*-                                                                              
*-       Process the acceptance of the user-id and possibly                     
*-         send the password if one is requested.                               
*-                                                                              
         LOOP  BEGIN                                                            
         LA    R1,=C'3..'          Wait for completion or 3xx msg               
         ACALL FTPREAD             Wait for USER to be accepted                 
         BNZ   FTPOEXIT                                                         
*                                                                               
         IF    (@R1,EQ,'2'),EXIT   USER/PASS accepted, scram                    
*-                                                                              
*-       Send password if one is requested.                                     
*-                                                                              
         IF    (@R1,EQ,'331'),BEGIN  Password is needed...                      
*-                                                                              
*-       This is a temporary solution to let Forsythe users                     
*-         use/save files as GUEST on Lindy/Morrow without                      
*-         supplying the password.  We don't want to use Anonymous              
*-         FTP because the Forsythe users need access to the real               
*-         files.  Essentially, Forsythe is a trusted host (but                 
*-         FTP doesn't know about things like that).                            
*-                                                                              
         MVC   FOPTEMP,NWANUSER    Copy net userid                              
         OC    FOPTEMP,CVBLANKS    Convert to caps                              
         IF    (FOPTEMP,EQ,'GUEST '),BEGIN  User GUEST...                       
         IF    (NWANPASS,EQ,CVBLANKS),BEGIN  No password...                     
         MVC   NWANPASS,=CL(L'NWANPASS)'Temp4U2'                                
         END                                                                    
         END                                                                    
*                                                                               
         IF    (NWANPASS,EQ,CVBLANKS),BEGIN  Prompt for password...             
         LA    R0,CP#HOSTI         Host information record set                  
         LA    R15,CPRG64                                                       
         VCALL XSETSET                                                          
*                                                                               
         MVC   FOPKEY,CVBLANKS                                                  
         MVC   FOPKEY(5),=C'PASS_'                                              
         L     R2,NWAHOSTL                                                      
         CEIL  R2,L'FOPKEY-5                                                    
         MOVE  R2,FOPKEY+5,NWAHOST  Host name                                   
         L     R15,CVUPPTBL        Upper case host name                         
         TR    FOPKEY,@R15                                                      
*                                                                               
         LA    R2,FOPKEY                                                        
         SETMSG NWANPASS            Put data here                               
         LA    R15,CPRG64                                                       
         VCALL XGET                Lookup saved password                        
*                                                                               
         WHILE (NWANPASS,EQ,CVBLANKS),BEGIN                                     
         SET   FOPFASKPASS         We assked for the password                   
*                                                                               
         SETMSG 'Password'                                                      
         IF    ^FOPFASKUSER,BEGIN  Expand the message a little...               
         TSEG  (R1),(R0)                                                        
         TSEG  ' (for '                                                         
         TSEG  NWANUSER,,T         User-id                                      
         TSEG  ' at '                                                           
         TSEG  UNETRNAME,,T                                                     
         SETMSG ')'                                                             
         END                                                                    
*                                                                               
         SET   CPFNECHO                                                         
         TREAD (R1),(R0),Q         Ask for password                             
         CLEAR CPFNECHO                                                         
         IF    (R15,NZ),FTPOEXIT   Scram if logging off                         
         VCALL LRTRIM                                                           
*                                                                               
         MVC   NWANPASS,CVBLANKS                                                
         LR    R15,R0                                                           
         CEIL  R15,L'NWANPASS                                                   
         MOVE  R15,NWANPASS,@R1    Save network password                        
         END                                                                    
         END                                                                    
*                                                                               
         SEG   'PASS '                                                          
         SEGT  NWANPASS                                                         
         SEGCR                                                                  
         SEGWR                                                                  
         END                                                                    
*                                                                               
         ELSE  BEGIN               Write other msgs to terminal...              
         TSEG  (R1),(R0),WR                                                     
         BNZ   FTPOEXIT                                                         
         END                                                                    
         END                                                                    
*-                                                                              
*-       Open a separate TCP connection for the FTP data.                       
*-                                                                              
         CLEAR R0                  User UNET                                    
         VCALL NPATALLO            Allocate communications path                 
         IF    NZ,BEGIN                                                         
         XPUSH R0,R1                                                            
         TSEG  'Can''t allocate data connection -- '                            
         XPOP  R0,R1                                                            
         TSEG  (R1),(R0),CR                                                     
         MVC   CPERRID,=CL8'NETERR'                                             
         LA    R15,4                                                            
         B     FTPOEXIT                                                         
         END                                                                    
*-                                                                              
*-       Establish connection to remote host.                                   
*-                                                                              
         ST    R1,NWADNETP         Save data UNET path ptr                      
         LR    R3,R1                                                            
         WITH  (UNET,R3),BEGIN                                                  
         FAIL  (UNET,NE,'UNET'),NETOERR,'Network interface error.'              
         LA    R15,UNETOUTSG                                                    
         ST    R15,NWADSG          Save data output SEGCB ptr                   
         L     R15,NWAUNETP        Primary UNET ptr                             
         MVC   UNETLIP,UNETLIP-UNET(R15)  Copy our IP address                   
         MVC   UNETRNAME,UNETRNAME-UNET(R15)  Copy host name                    
         MVC   UNETRIP,UNETRIP-UNET(R15)  Copy host IP address                  
*                                                                               
         IF    (NWAFTABS),'SET UNETFKEEPTABS'                                   
*                                                                               
         LA    R15,UNET                                                         
         VCALL NPATOPEN            Open the network path                        
         IF    NZ,BEGIN            Trouble...                                   
         IF    (@R1,EQ,'NO NETWORK SERVER'),BEGIN                               
         TSEG  'Amelia is not running; '                                        
         SETMSG 'network access is not possible.'                               
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'ERROR: UNKNOWN HOST'),BEGIN                            
         TSEG  NWAHOST,L:NWAHOSTL                                               
         SETMSG ': host not found.'                                             
         END                                                                    
*                                                                               
         ELSEIF (@R1,EQ,'TIMEOUT'),BEGIN                                        
         TSEG  'Unable to do this command, no response from '                   
         SETMSG 'the remote computer.'                                          
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         XPUSH R0,R1                                                            
         TSEG  'Can''t open data connection -- '                                
         XPOP  R0,R1                                                            
         END                                                                    
*                                                                               
         TSEG  (R1),(R0),CR                                                     
         LA    R15,4                                                            
         B     FTPOEXIT            Trouble                                      
         END                                                                    
         END                                                                    
*-                                                                              
*-       If we had to ask for the user/password then save that                  
*-         information.                                                         
*-                                                                              
         LA    R0,CP#HOSTI         Host information record set                  
         LA    R15,CPRG64                                                       
         VCALL XSETSET                                                          
*                                                                               
         IF    FOPFASKUSER,BEGIN   Save user-id...                              
         MVC   FOPKEY,CVBLANKS                                                  
         MVC   FOPKEY(5),=C'USER_'                                              
         L     R2,NWAHOSTL                                                      
         CEIL  R2,L'FOPKEY-5                                                    
         MOVE  R2,FOPKEY+5,NWAHOST  Host name                                   
         L     R15,CVUPPTBL        Upper case host name                         
         TR    FOPKEY,@R15                                                      
*                                                                               
         LA    R2,FOPKEY                                                        
         SETMSG NWANUSER            Save this userid                            
         LA    R15,CPRG64                                                       
         VCALL XPUT                Save this userid                             
         END                                                                    
*                                                                               
         IF    FOPFASKPASS,BEGIN   Save password...                             
         MVC   FOPKEY,CVBLANKS                                                  
         MVC   FOPKEY(5),=C'PASS_'                                              
         L     R2,NWAHOSTL                                                      
         CEIL  R2,L'FOPKEY-5                                                    
         MOVE  R2,FOPKEY+5,NWAHOST  Host name                                   
         L     R15,CVUPPTBL        Upper case host name                         
         TR    FOPKEY,@R15                                                      
*                                                                               
         LA    R2,FOPKEY                                                        
         SETMSG NWANPASS            Save this password                          
         LA    R15,CPRG64                                                       
         VCALL XPUT                Save this userid                             
         END                                                                    
*                                                                               
         CLEAR R15                                                              
FTPOEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPUSE -- Local routine to do the FTP dialog to start the                    
*    retreval of a remote file.                                                 
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
FTPUSE   PROC                                                                   
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPOPEN,'Bad NETWA ptr'                         
*                                                                               
         L     R4,CPFINFOP                                                      
         WITH  (FFINFO,R4)                                                      
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Tell FTP server host and port number of data connection.               
*-                                                                              
         L     R3,NWADNETP         Data UNET path ptr                           
         WITH  (UNET,R3),BEGIN                                                  
         SEG   'PORT '                                                          
         SEGDC LC:UNETLIP          Our IP address                               
         SEG   ','                                                              
         SEGDC LC:UNETLIP+1                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+2                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+3                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+2      Port number                                  
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+3                                                   
         SEGCR                                                                  
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Wait for acceptance of PORT command.                                   
*-                                                                              
         LA    R1,=C'...'          Wait for completion msg                      
         ACALL FTPREAD             Get greeting (ready) message                 
         BNZ   FTPUEXIT            Trouble, scram                               
*-                                                                              
*-       Ask for image mode transfer if transparent.                            
*-                                                                              
         IF    NWAFTRAN,BEGIN      Transparent mode...                          
         SEG   'TYPE I'            Image mode                                   
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for completion msg                      
         ACALL FTPREAD             Get response                                 
         BNZ   FTPUEXIT            Trouble, scram                               
         END                                                                    
*-                                                                              
*-       Send the RETR command to use a remote file.                            
*-                                                                              
         SEG   'RETR '             Retreive file                                
*                                                                               
         SEG   NWAFNAME,L:NWAFNAMEL    Original file name                       
*                                                                               
         SEGCR                                                                  
         SEGWR                                                                  
*-                                                                              
*-       Wait for response indicating RETR is in progress.                      
*-                                                                              
         LA    R1,=C'1..'          Wait for preliminary reply                   
         ACALL FTPREAD             Get next message                             
         IF    (R15,EQ,=F'8'),BEGIN Error signalled                             
         XPUSH R15                                                              
*                                                                               
* Close the data connection (since it won't be needed)                          
* and signal QUIT to the other side.                                            
*                                                                               
         IF    ('LT R15,NWADNETP',NZ),BEGIN  Data connection...                 
         VCALL NPATCLS             Close and free data path                     
         CLEAR NWADNETP                                                         
         END                                                                    
         CLEAR NWAFOPEN            No longer open                               
         SEG   'QUIT'              Say goodbye                                  
         SEGCR                                                                  
         SEGWR                                                                  
         XPOP  R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
*-                                                                              
*-       RETR completion won't happen until the file has retreived.             
*-                                                                              
         CLEAR R15                 A-ok                                         
         END                                                                    
*                                                                               
FTPUEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPSAVE -- Local routine to do the FTP dialog to start the                   
*    storing of a remote file.                                                  
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
FTPSAVE  PROC                                                                   
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPOPEN,'Bad NETWA ptr'                         
*                                                                               
         L     R4,CPFINFOP                                                      
         WITH  (FFINFO,R4)                                                      
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Tell FTP server host and port number of data connection.               
*-                                                                              
         L     R3,NWADNETP         Data UNET path ptr                           
         WITH  (UNET,R3),BEGIN                                                  
         SEG   'PORT '                                                          
         SEGDC LC:UNETLIP          Our IP address                               
         SEG   ','                                                              
         SEGDC LC:UNETLIP+1                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+2                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+3                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+2      Port number                                  
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+3                                                   
         SEGCR                                                                  
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Wait for acceptance of PORT command.                                   
*-                                                                              
         LA    R1,=C'...'          Wait for completion msg                      
         ACALL FTPREAD             Get greeting (ready) message                 
         BNZ   FTPSEXIT            Trouble, scram                               
*-                                                                              
*-       Ask for image mode transfer if transparent.                            
*-                                                                              
         IF    NWAFTRAN,BEGIN      Transparent mode...                          
         SEG   'TYPE I'            Image mode                                   
         SEGCR                                                                  
         SEGWR                                                                  
*                                                                               
         LA    R1,=C'...'          Wait for completion msg                      
         ACALL FTPREAD             Get response                                 
         BNZ   FTPSEXIT            Trouble, scram                               
         END                                                                    
*-                                                                              
*-       Send the STOR command to save a remote file.                           
*-                                                                              
         SETMSG 'STOR '             Store file                                  
         IF    FFFAPPD,'SETMSG "APPE "'  Append file                            
         SEG   (R1),(R0)                                                        
*                                                                               
         SEG   NWAFNAME,L:NWAFNAMEL    Original file name                       
*                                                                               
         SEGCR                                                                  
         SEGWR                                                                  
*-                                                                              
*-       Wait for response indicating STOR is in progress.                      
*-                                                                              
         LA    R1,=C'1..'          Wait for preliminary reply                   
         ACALL FTPREAD             Get next message                             
         BNZ   FTPSEXIT            Trouble, scram                               
*-                                                                              
*-       Wait for the remote host to open the data connection port.             
*-                                                                              
         CLEAR R0                  No timeout                                   
         LA    R1,UNETSFOPEN                                                    
         L     R15,NWADNETP                                                     
         VCALL NPATWAIT            Wait for open                                
         BNZ   FTPSEXIT            !!!!!!!! no msg to term here???              
*-                                                                              
*-       STOR completion won't happen until the file has been                   
*-         stored.                                                              
*-                                                                              
         CLEAR R15                 A-ok                                         
FTPSEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPSCR -- Local routine to do the FTP dialog to scratch a file.              
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
FTPSCR   PROC                                                                   
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPOPEN,'Bad NETWA ptr'                         
*                                                                               
         L     R4,CPFINFOP                                                      
         WITH  (FFINFO,R4)                                                      
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Delete the file by sending a DELE command.                             
*-                                                                              
         SEG   'DELE '             Delete file                                  
*                                                                               
         SEG   NWAFNAME,L:NWAFNAMEL    Original file name                       
*                                                                               
         SEGCR                                                                  
         SEGWR                                                                  
*-                                                                              
*-       Wait for response.                                                     
*-                                                                              
         LA    R1,=C'...'          Wait for reply                               
         ACALL FTPREAD             Get next message                             
         BNZ   FTPXEXIT            Trouble, scram                               
*                                                                               
         CLEAR R15                 A-ok                                         
FTPXEXIT PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPDIR -- Local routine to do the FTP dialog to do list the                  
*    directory in a remote file system.                                         
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - opened successfully                                                  
*      nz- not opened (CPERRID contains error id)                               
*                                                                               
FTPDIR   PROC                                                                   
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPOPEN,'Bad NETWA ptr'                         
*                                                                               
         L     R4,CPFINFOP                                                      
         WITH  (FFINFO,R4)                                                      
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Tell FTP server host and port number of data connection.               
*-                                                                              
         L     R3,NWADNETP         Data UNET path ptr                           
         WITH  (UNET,R3),BEGIN                                                  
         SEG   'PORT '                                                          
         SEGDC LC:UNETLIP          Our IP address                               
         SEG   ','                                                              
         SEGDC LC:UNETLIP+1                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+2                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLIP+3                                                     
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+2      Port number                                  
         SEG   ','                                                              
         SEGDC LC:UNETLPORT+3                                                   
         SEGCR                                                                  
         SEGWR                                                                  
         END                                                                    
*-                                                                              
*-       Wait for acceptance of PORT command.                                   
*-                                                                              
         LA    R1,=C'...'          Wait for completion msg                      
         ACALL FTPREAD             Get greeting (ready) message                 
         BNZ   FTPDEXIT            Trouble, scram                               
*-                                                                              
*-       Send the RETR command to use a remote file.                            
*-                                                                              
         SEG   'LIST'              List directory                               
*                                                                               
         COMMENT                   If show dir for ... [lik ...]                
         IF    (NWAFNAMEL,NZ),BEGIN                                             
         SEG   ' '                                                              
         SEG   NWAFNAME,L:NWAFNAMEL    Original file name                       
         IF    (NWALIKLN,NZ),BEGIN                                              
         SEG   '/'                                                              
         SEG   L:NWALIKAD,L:NWALIKLN   Like string                              
         SEG   '*'                                                              
         END                                                                    
         END                                                                    
*                                                                               
         COMMENT                   If show dir like ..                          
         IF    (NWAFNAMEL,Z),BEGIN (no for ...)                                 
         IF    (NWALIKLN,NZ),BEGIN                                              
         SEG   ' '                                                              
         SEG   L:NWALIKAD,L:NWALIKLN                                            
         SEG   '*'                                                              
         END                                                                    
         END                                                                    
*                                                                               
         SEGCR                                                                  
         SEGWR                                                                  
*-                                                                              
*-       Wait for response indicating LIST is in progress.                      
*-                                                                              
         LA    R1,=C'1..'          Wait for preliminary reply                   
         ACALL FTPREAD             Get next message                             
         BNZ   FTPDEXIT            Trouble, scram                               
*-                                                                              
*-       Get the LIST responses.                                                
*-                                                                              
         CLEAR R5                  Flag: no response                            
*                                                                               
         WHILE ('L R15,NWADNETP; VCALL NPATREAD',Z),BEGIN                       
         TSEG  (R1),(R0),CR                                                     
         BNZ   FTPDEXIT                                                         
         LA    R5,1                We've written something                      
         END                                                                    
*                                                                               
         IF    (R5,Z),BEGIN        No response...                               
         TSEG  'No files'                                                       
         IF    (NWAFNAMEL,NZ),BEGIN                                             
         TSEG  ' like '                                                         
         TSEG  NWAFNAME,L:NWAFNAMEL                                             
         END                                                                    
         TSEG  ' found.',,CR                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
FTPDEXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPREAD -- Local routine to get the next FTP control line                    
*    from the network path.                                                     
*                                                                               
*    On entry:                                                                  
*      R1 -  "x.."   String (the first character is a code which                
*                    will complete the read -- see FTP doc).                    
*                                                                               
*    On exit, R15 (and cc):                                                     
*      0 - ok;  R1,R0 - data line loc, len                                      
*      4 - Path error (message already TSEG'd)                                  
*      8 - FTP error message (4xx or 5xx) (message already TSEG'd)              
*                                                                               
FRDWA    RECORD BEGIN                                                           
FRDOPT   DS    CL3                 Option code from entry                       
FRDCOMP  EQU   FRDOPT,1,C'X'       Stop on this numeric code                    
         END                                                                    
*-                                                                              
FTPREAD  PROC  (R2,LSR),FRDWA                                                   
         MVC   FRDOPT,@R1          Save option code                             
*                                                                               
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPREAD,'Bad NETWA ptr'                         
*                                                                               
FTPRLOOP LOOP  BEGIN                                                            
         L     R15,NWAUNETP                                                     
         VCALL NPATREAD            Get input line                               
*                                                                               
         IF    NZ,BEGIN            Connection problems...                       
         TSEG  'Remote host unexpectedly closed file transfer '                 
         TSEG  'connection.',,WR                                                
         SET   CPFERROR                                                         
         LA    R15,4                                                            
         B     FTPREXIT                                                         
         END                                                                    
*-                                                                              
*-       Handle continued messages, of the form (for example):                  
*-            500-This is the first part of a message                           
*-            500 This is the message                                           
*-                                                                              
         IF    (@R1+3,EQ,'-'),BEGIN  Continued message...                       
         IF    ((@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN  Error msg...               
         LA    R1,@R1+4            Skip over error id                           
         SH    R0,=H'4'                                                         
         TSEG  (R1),(R0),CR        Write it                                     
         END                                                                    
*                                                                               
         B     FTPRLOOP            Loop back                                    
         END                                                                    
*-                                                                              
*-       Return if we get what we were waiting for, or if                       
*-         we get a final completion.                                           
*-                                                                              
         IF    (@R1,EQ,FRDCOMP),EXIT    We got what we wanted                   
*                                                                               
         IF    (@R1,EQ,'2'),EXIT        Positive completion                     
*-                                                                              
*-       Check out the response line to see if it is a fatal error.             
*-         (FTP control messages begin with a three digit code.)                
*-                                                                              
         IF    ((@R1,EQ,'4'),OR,(@R1,EQ,'5')),BEGIN  Error...                   
         MVC   CPERRID,=CL8'FTPERR'                                             
*                                                                               
         WHILE ((R0,POS),AND,(@R1,NE,' ')),'LA R1,@R1+1; DECR R0'               
         WHILE ((R0,POS),AND,(@R1,EQ,' ')),'LA R1,@R1+1; DECR R0'               
         IF    (R0,NP),'SETMSG "(Error: no message text, sorry.)"'              
         TSEG  (R1),(R0),WR        Write out message text                       
         SET   CPFERROR                                                         
         LA    R15,8                                                            
         B     FTPREXIT                                                         
         END                                                                    
         END                                                                    
*                                                                               
         CLEAR R15                 A-ok                                         
FTPREXIT PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  FTPCLOSE -- Local routine to do FTP "signoff" dialog.                        
*                                                                               
*    On entry:                                                                  
*    NET DEVICE WORK AREA (NWA) contains info about the file                    
*                                                                               
FTPCLOSE PROC                                                                   
         WITH  (NETWA,R6)          Entry assumption                             
         FAIL  (NETWA,NE,'NET'),FTPCLOSE,'Bad NETWA ptr'                        
*                                                                               
         SEGDEF L:NWAUSG                                                        
*-                                                                              
*-       Wait for response indicating file transfer completed.                  
*-                                                                              
         LA    R1,=C'2..'          Wait for 226                                 
         ACALL FTPREAD             Get next message                             
         IF    ((R15,Z),OR,(R15,EQ,=F'8')),BEGIN                                
*-                                                                              
*-       Tell FTP server to quit                                                
*-                                                                              
         SEG   'QUIT'              Say goodbye                                  
         SEGCR                                                                  
         SEGWR                                                                  
         END                                                                    
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         SEGDEF DUMMY              (Safety)                                     
*                                                                               
         VLTORG                                                                 
         END   .                                                                
