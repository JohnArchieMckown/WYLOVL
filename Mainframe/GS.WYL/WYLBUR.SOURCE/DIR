DIR      TITLE 'WYLBUR''s Directory Commands'                                   
*******************************************************************             
*                                                                 *             
*     WYLBUR/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
WYLDIR   CSECT                                                                  
DIR      IDENT 2025                11:49:20 01/25/02  (SLP)                     
*                                                                               
         REGS   FSR,,,,,,IOWAR,BR,(WAR,LSR),PAR,PBR,CPR,CVR,SPR,RAR             
         SPACE 2                                                                
         SYSDEFN ,                 Define installation params                   
         GBLC  &PUBUSER,&PUBGRP                                                 
         GBLB  &ICF                                                             
         SPACE 2                                                                
         PUSH  DSECTS                                                           
         EJECT                                                                  
         COPY  CONTROL                                                          
         EJECT                                                                  
IOWA     IOWA  DSECT                                                            
*                                                                               
         COPY  RTNCODES                                                         
         TITLE 'DSECTS'                                                         
JCB      RECORD BEGIN                                                           
         COPY  JCB                                                              
         END                                                                    
SEGCB    RECORD 'SEGCB'                                                         
         COPY  FFINFO                                                           
         COPY  SHDRARGS                                                         
         COPY  SHDSARGS                                                         
         COPY  DSNINFO                                                          
         COPY  SHCTARGS                                                         
*                                                                               
         POP   DSECTS                                                           
         TITLE 'Local DSECTS'                                                   
         COPY  LUVHDR                                                           
         SPACE 2                                                                
         COPY  LUVENTRY                                                         
         SPACE 2                                                                
SDSNBUF  RECORD 'SDSNBUF; SCATBUF'                                              
         EJECT                                                                  
         VOLINFO                                                                
         TITLE 'WYLBUR''s Directory Commands'                                   
*box                                                                            
*                                                                               
*  SHOWTRKS -- SHOW TRACKS command.                                             
*                                                                               
SHOWTRKS XPROC                                                                  
*                                                                               
         SCAN STRKPRT                                                           
         IF    (DSNON,Z),BEGIN     No volume specified...                       
         ERROR 'Volume must be specified for SHOW TRACKS.'                      
         END                                                                    
*                                                                               
         VCALL MKFFINFO                                                         
         L     R5,CPFINFOP                                                      
         USING FFINFO,R5                                                        
*                                                                               
         MVC   FFHOST,DSNON6                                                    
         LA    R0,6                                                             
         ST    R0,FFHOSTL                                                       
*                                                                               
         VCALL WGSTRKS                                                          
*                                                                               
         B     CVNEXT                                                           
         PEND                                                                   
*                                                                               
* for show tracks...                                                            
*                                                                               
STRKPRT  SCKW  ,V(DSNPSHON),PUSH                                                
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(NOTVALID)                                                     
         SPACE 2                                                                
         QLTORG                                                                 
         TITLE 'SHOW DSNAMES Command'                                           
*box                                                                            
*                                                                               
*  SHODSN  -- SHOW DSNAMES Command.                                             
*                                                                               
*  SHOW DSN BROKEN OUT INTO FILE TYPE (SAM,NET,WINGS,ETC)                       
*  CODE.                                                                        
*                                                                               
*                                                                               
SHODSN   XPROC SHDSARGS                                                         
         LA    R6,SHDSARGS                                                      
         USING SHDSARGS,R6                                                      
         CLEAR SHDSARGS                                                         
*                                                                               
         VCALL MKFFINFO                                                         
         L     R5,CPFINFOP                                                      
         USING FFINFO,R5                                                        
*                                                                               
         COMMENT                   SCAN SHOW DSN OPTIONS                        
         SCAN  DSONPRT                                                          
         SCANCHK                                                                
*                                                                               
         COMMENT                   VALIDITY                                     
         IF    ((SHDSONLY),AND,((SHDSLOWL,NE,0),OR,                    X        
               (SHDSUPL,NE,0))),BEGIN                                           
         ERROR '"FROM","EXCLUDE","THROUGH", and "TO" conflict with "ONLX        
               Y" option"',,ONLYCONF                                            
         END   ,                                                                
*                                                                               
         IF    (SHDSSCR,AND,CPFDUMP),BEGIN                                      
         TSEG  'SCRATCH',,B        Tell which bad option                        
         ERROR 'conflicts with dump command or collect options.'                
         END                                                                    
*                                                                               
         VCALL FNAMEFIN            Process dsname parameters                    
*                                                                               
         COMMENT                   SAMSON SHOW DSN                              
         IF    FFFSAM,BEGIN                                                     
         TSEG  FFHOST,L:FFHOSTL,T                                               
         TSEG  ': volume not valid.',,CR                                        
         TSEG  '(Use "SHOW DIRECTORY ON volume" '                               
         ERROR 'for Samson directory.)'                                         
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   NETWORK SHOW DSNAMES                         
         IF    FFFNET,BEGIN                                                     
         TSEG  FFHOST,L:FFHOSTL,T                                               
         TSEG  ': volume not valid.',,CR                                        
         TSEG  '(Use "SHOW DIRECTORY ON host" '                                 
         ERROR 'for network directory.)'                                        
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   WINGS SHOW DSNAMES                           
         IF    FFFWINGS,BEGIN                                                   
*                                                                               
         IF    (SHDSUPL,GT,0),BEGIN                                             
         L     R1,SHDSUPL                                                       
         ST    R1,FFNAMEL                                                       
         MOVE  R1,FFNAME,SHDSUPB                                                
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         ST    R1,SHDSUPL                                                       
         MOVE  R1,SHDSUPB,FFRNAME                                               
         END                                                                    
*                                                                               
         IF    (SHDSLOWL,GT,0),BEGIN                                            
         L     R1,SHDSLOWL                                                      
         ST    R1,FFNAMEL                                                       
         MOVE  R1,FFNAME,SHDSLOWB                                               
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         ST    R1,SHDSLOWL                                                      
         MOVE  R1,SHDSLOWB,FFRNAME                                              
         END                                                                    
*                                                                               
         IF    (SHDSLIKL,GT,0),BEGIN                                            
         L     R0,SHDSLIKL                                                      
         LA    R1,SHDSLIKE                                                      
         VCALL TOUPPER                                                          
         IF    (@R1,EQ,'$'),BEGIN                                               
         INCR  R1                                                               
         DECR  R0                                                               
         ST    R0,SHDSLIKL                                                      
         LTR   R2,R0                                                            
         IF    NZ,BEGIN                                                         
         MOVE  R2,SHDSLIKE,SHDSLIKE+1                                           
         END                                                                    
         END                                                                    
         ELSEIF ((R0,GE,4),AND,(@R1,EQ,'WYL.')),BEGIN                           
         END                                                                    
         ELSEIF ((R0,GE,5),AND,                                        X        
               (@R1,EQ,'SYS'),AND,                                     X        
               (@R1+3,GE,'0'),AND,(@R1+3,LE,'9'),AND,                  X        
               (@R1+4,EQ,'.')),BEGIN                                            
         END                                                                    
         ELSE  BEGIN                                                            
         ST    R0,FFNAMEL                                                       
         LR    R1,R0                                                            
         MOVE  R1,FFNAME,SHDSLIKE                                               
         VCALL NRESOLVE                                                         
         L     R0,FFRNAMEL                                                      
         LA    R1,FFRNAME                                                       
         VCALL RETFNGDG                                                         
         ST    R0,SHDSLIKL                                                      
         LR    R1,R0                                                            
         MOVE  R1,SHDSLIKE,FFRNAME                                              
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R0,1                                                             
         MVI   FFNAME,C'A'                                                      
         ST    R0,FFNAMEL                                                       
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         S     R1,=F'2'                                                         
         ST    R1,SHDSLIKL                                                      
         MOVE  R1,SHDSLIKE,FFRNAME                                              
         END                                                                    
*                                                                               
         LA    R1,SHDSARGS                                                      
         VCALL WGSHODSN                                                         
*                                                                               
* It makes no sense at all to set the prefix on SHOW                            
* DSNAMES.                                                                      
*                                                                               
* Removed 01/05/1995 by GG.MPD                                                  
*                                                                               
*        IF    CPLFLG3.CPFSET,'VCALL SETPFXNS'  set prefix                      
         IF    (R15,EQ,4),BEGIN                                                 
         B     CVERROR                                                          
         END                                                                    
         IF    (R15,EQ,8),BEGIN                                                 
         IF    (SHDSPGCT,Z),BEGIN                                               
         VCALL NOMORPG                                                          
         END                                                                    
         TSEG  'Too many dsname items. Partial listing follows.'                
         END                                                                    
*                                                                               
* If the user specified a volume, list the volume name first,                   
* unless it's a SHOW DSNAME INTERNAL command.                                   
*                                                                               
         IF    (^SHDSINT),BEGIN                                                 
         IF    (FFHOSTL,EQ,6),BEGIN                                             
         SETMSG FFHOST,L:FFHOSTL                                                
         VCALL UPCASE                                                           
         TSEG  (R1),(R0),CR                                                     
         END                                                                    
         END                                                                    
*                                                                               
         IF    (SHDSPGCT,Z),'B CVNEXT'                                          
         LA    R1,SHDSARGS                                                      
         ACALL DSPDSN              No return                                    
         BOMB                                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  For Show Dsnames                                                             
*                                                                               
DSONPRT  SCKW DATED,DSNMDTED,A                                                  
         SCKW LIKE,ADLIKE,A                                                     
         SCKW ONLY,ADONLY,A                                                     
         SCKW FROM,DSNMFROM,(P,A)                                               
*        SCKW EXCLUDE,DSNMEXCL,(P,A)                                            
*        SCKW THROUGH,DSNMTHRU,(P,A)                                            
*        SCKW THRU,DSNMTHRU,P                                                   
         SCKW TO,DSNMTO,P                                                       
         SCKW SPACE,DSNMSPAC,A                                                  
         SCKW SIZE,DSNMSPAC,A                                                   
         SCKW TRACKS,DSNMSPAC,A                                                 
         SCKW TYPE,DSNMTYP,A                                                    
         SCKW INTERNAL,DSNMINT,A                                                
         SCKW ALL,DSNMALL                                                       
         SCKW SCRATCH,DSNMSCRT,A                                                
         SCKW NEW,DSNMNEW                                                       
         SCKW OLD,DSNMOLD                                                       
*                                                                               
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
ADLIKE   PROC  ,                                                                
         SCANSTR ,                 SKIP=' ', STOP=' ,=;'                        
         IF    (R15,Z),BEGIN                                                    
         IF    (R0,POS),BEGIN                                                   
         LR    R3,R0                                                            
         CEIL  R3,L'SHDSLIKE                                                    
         ST    R3,SHDSLIKL                                                      
         MOVE  R3,SHDSLIKE,@R1                                                  
         CLEAR SHDSONLY                                                         
         SET   SHDSLIK                                                          
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'Missing dataset name.',,MISSDSN                                 
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
DSNMFROM PROC  ,                                                                
         SCTOK (R1)                                                             
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         END                                                                    
         LR    R3,R0                                                            
         CEIL  R3,L'SHDSLOWB                                                    
         ST    R3,SHDSLOWL                                                      
         MOVE  R3,SHDSLOWB,@R1                                                  
         PEND                                                                   
*                                                                               
DSNMTO   PROC  ,                                                                
         SCTOK (R1)                                                             
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         END                                                                    
         LR    R3,R0                                                            
         CEIL  R3,L'SHDSUPB                                                     
         ST    R3,SHDSUPL                                                       
         MOVE  R3,SHDSUPB,@R1                                                   
         PEND                                                                   
*                                                                               
ADONLY   PROC  ,                                                                
         SCANSTR ,                 SKIP=' ', STOP=' ,=;'                        
         IF    (R15,Z),BEGIN                                                    
         IF    (R0,POS),BEGIN                                                   
         LR    R3,R0                                                            
         CEIL  R3,L'SHDSLIKE                                                    
         ST    R3,SHDSLIKL                                                      
         MOVE  R3,SHDSLIKE,@R1                                                  
         SET   SHDSONLY                                                         
         CLEAR SHDSLIK                                                          
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'Missing dataset name.',,MISSDSN                                 
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
DSNMSPAC PROC                                                                   
         SET   SHDSTRK                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMALL  PROC                                                                   
         SET   SHDSDAT+SHDSTYP+SHDSTRK                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMDTED PROC                                                                   
         SET   SHDSDAT                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMTYP  PROC                                                                   
         SET   SHDSTYP                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMNEW  PROC                                                                   
         SET   SHDSNEW                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMOLD  PROC                                                                   
         CLEAR SHDSNEW                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMINT  PROC                                                                   
         SET   SHDSINT                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
DSNMSCRT PROC                                                                   
         SET   SHDSSCR                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
         TITLE 'Get next line for display'                                      
*                                                                               
*    This routine is called by the DSPDSN subroutine to get the                 
*    next line for display.                                                     
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DSNAMES work area which                        
*             contains a work area used by this routine.                        
*                                                                               
*      R2  -  Points to an area of length L'DSNINFO where                       
*             the dataset information line should be stored,                    
*             padded out to its full length with blanks.                        
*                                                                               
*      PAR -  Points to the current page being displayed.                       
*                                                                               
*    On output:                                                                 
*      R0,R1 - describe the length and location of the retrieved                
*              line                                                             
*      R15=0   means a line was retrieved                                       
*         =NZ  means EOF (no more lines in the display).                        
*                                                                               
NEXTDSLN PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHDSARGS,R6                                                      
*                                                                               
         L     R1,SHDSCPG          Make sure we have the page in core           
         SLL   R1,2                                                             
         L     R0,SHDSPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
*                                                                               
         LH    R3,@PAR                                                          
         IF    (SHDSCOFF,GE,R3),BEGIN                                           
         L     R1,SHDSCPG                                                       
         LA    R1,1(,R1)                                                        
         IF    (R1,LT,SHDSPGCT),BEGIN                                           
         ST    R1,SHDSCPG                                                       
         MVC   SHDSCOFF,=F'2'                                                   
         PFREE PAR,EMPTY                                                        
         L     R1,SHDSCPG          Get the new page into core                   
         SLL   R1,2                                                             
         L     R0,SHDSPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
         LH    R3,@PAR                                                          
         FAIL  (R3,LE,SHDSCOFF),DSNERR1,'NEXTDSLN error.'                       
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         B     NXTDSDON                                                         
         END                                                                    
         END                                                                    
*                                                                               
         L     R4,SHDSCOFF                                                      
         LA    R1,0(R4,PAR)                                                     
         LC    R0,@R1                                                           
         INCR  R1                                                               
         IF    (R0,NZ),BEGIN                                                    
         MVC   0(L'DSNINFO,R2),CVBLANKS                                         
         LR    R3,R0                                                            
         DEX   R3,'MVC 0(0,R2),0(R1)'                                           
         END                                                                    
         AR    R4,R0                                                            
         INCR  R4                                                               
         ST    R4,SHDSCOFF                                                      
         IF    (R0,Z),BEGIN                                                     
         PFREE PAR,EMPTY                                                        
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         LR    R1,R2                                                            
         LA    R0,L'DSNINFO                                                     
         CLEAR R15                                                              
         END                                                                    
*                                                                               
NXTDSDON LABEL                                                                  
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG ,                                                               
         EJECT                                                                  
         TITLE 'Release remaining display pages'                                
*                                                                               
*    This subroutine releases all pages containing the                          
*    information used by SHOW DSNAMES.                                          
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DSNAMES work area.                             
*                                                                               
*    The current page (in PAR) and the remainder of the                         
*    pages not yet accessed in the array are released.                          
*                                                                               
RLSDSPGS PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHDSARGS,R6                                                      
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         END                                                                    
         L     R3,SHDSCPG                                                       
         INCR  R3                                                               
         WHILE (R3,LT,SHDSPGCT),BEGIN                                           
         LR    R1,R3                                                            
         SLL   R1,2                                                             
         L     R2,SHDSPAGS(R1)                                                  
         PGET  PAR,(R2)                                                         
         PFREE PAR,EMPTY                                                        
         LA    R3,1(,R3)                                                        
         END                                                                    
         DROP  R6                                                               
         PEND  ,                                                                
         EJECT                                                                  
         TITLE 'Handle scratch option of SHOW DSNAMES'                          
*                                                                               
*   Prompt the user to see if it's okay to scratch the indicated                
*   member and scratch it if so.                                                
*                                                                               
*   On entry:                                                                   
*      R1 - Points to the DSNINFO work area                                     
*      R2 - Points to the SHOW DSNAMES work area                                
*                                                                               
*   On output:                                                                  
*      R15=0   If user wanted file scratched, file scratched,                   
*              else file not scratched.                                         
*      R15>0   Some error occurred                                              
*      R15<0   User pressed attention                                           
*                                                                               
SCRDSN   PROC  FFINFO                                                           
         LR    R6,R2                                                            
         USING SHDSARGS,R6                                                      
         LR    R4,R1                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
         LA    R1,DSNDSN                                                        
         LA    R0,L'DSNDSN                                                      
         VCALL RTRIM                                                            
         END                                                                    
         LR    R3,R0                                                            
         LR    R4,R1               Save filename information                    
         SETMSG 'Scratch'                                                       
         VCALL YESRTN                                                           
         BM    SCRDSATN                                                         
         BNZ   SCRDSABT            OK if we want to skip it                     
*                                                                               
         XPUSH R3,R4                                                            
         LA    R2,FFINFO           Save the current                             
         LA    R3,L'FFINFO             FFINFO data                              
         L     R4,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         L     R5,CPFINFOP         Get the FFINFO                               
         WITH  (FFINFO,R5),BEGIN                                                
         CLEAR FFOPTSL             No options                                   
         CLEAR FFRNAMEL                                                         
         XPOP  R2,R3               Recover file name                            
         ST    R2,FFRNAMEL                                                      
         ST    R2,FFNAMEL                                                       
         MOVE  R2,FFRNAME,@R3                                                   
         L     R2,FFNAMEL                                                       
         MOVE  R2,FFNAME,FFRNAME                                                
         VCALL WGSCRTCH            Scratch the file                             
         IF    NZ,BEGIN                                                         
         IF    (CPERRID,EQ,'NOTYOURS'),BEGIN                                    
         CLEAR SHDSSCR             Don't scratch any more                       
         END                                                                    
         CLEAR CPERRID                                                          
         END                                                                    
         TCCR                                                                   
         ACALL CTWRITE             Write out the message                        
         END                                                                    
*                                                                               
         LA    R4,FFINFO           Restore the original                         
         LA    R3,L'FFINFO             FFINFO data                              
         L     R2,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         B     SCRDSOK                                                          
*                                                                               
SCRDSABT LABEL                                                                  
         LA    R15,4                                                            
         B     SCRDSDON                                                         
*                                                                               
SCRDSATN LABEL                                                                  
         L     R15,=F'-4'                                                       
         B     SCRDSDON                                                         
*                                                                               
SCRDSOK  CLEAR R15                                                              
         B     SCRDSDON                                                         
*                                                                               
SCRDSDON LABEL                                                                  
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Build SHOW DSN INTERNAL output line'                            
*                                                                               
* This routine formats a line in INTERNAL format for SHOW DSNAMES.              
*                                                                               
* Input:                                                                        
*                                                                               
*    R0, R1 - Describe the length and location of the output buffer             
*    R2     - Points to the DSNINFO record containing the information           
*             for the dataset in question.                                      
*                                                                               
* Output:                                                                       
*    R0,R1 -  Contain the length and location of the output line.               
*                                                                               
DSNINWA  RECORD BEGIN                                                           
DSNSG    SEGCB  ,                                                               
         END                                                                    
*                                                                               
DSPDSNIN XPROC DSNINWA                                                          
         LR    R4,R2                                                            
         SEGINIT (R1),(R0),DSNSG                                                
         WITH  (DSNINFO,R4),BEGIN                                               
         SEG   DSNVOL,L'DSNVOL,.DSNSG,B                                         
         SEG   DSNDSN,L'DSNDSN,.DSNSG,B                                         
         DTB   DSNTRKUS,L'DSNTRKUS-3                                            
         LR    R3,R15                                                           
         DTB   DSNTRKUS+L'DSNTRKUS-2,2                                          
         IF    (R15,NZ),BEGIN                                                   
         INCR  R3                                                               
         XPUSH ,,8,PTR=R1                                                       
         LA    R0,5                                                             
         LR    R15,R3                                                           
         VCALL BTDZEROS                                                         
         SEG   (R1),(R0),.DSNSG                                                 
         XPOP  ,,8                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   DSNTRKUS,L'DSNTRKUS-3,.DSNSG                                     
         END                                                                    
         SEG   DSNERRCD,L'DSNERRCD,.DSNSG                                       
         SEG   DSNTRKAL,L'DSNTRKAL,.DSNSG,B                                     
         SEG   DSNXTNTS,L'DSNXTNTS,.DSNSG,B                                     
         SEG   DSNDSORG,L'DSNDSORG,.DSNSG,B                                     
         SEG   DSNRECFM,L'DSNRECFM,.DSNSG,B                                     
         SEG   DSNLRECL,L'DSNLRECL,.DSNSG,B                                     
         SEG   DSNBKSIZ,L'DSNBKSIZ,.DSNSG,B                                     
         SEG   DSNKEYLN,L'DSNKEYLN,.DSNSG,B                                     
         IF    (DSNCRDAT,NE,' '),BEGIN                                          
         SEG   DSNCRDAT+5,5,.DSNSG                                              
         SEG   '/',,.DSNSG                                                      
         SEG   DSNCRDAT+2,2,.DSNSG,B                                            
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         IF    (DSNLADAT,NE,' '),BEGIN                                          
         SEG   DSNLADAT+5,5,.DSNSG                                              
         SEG   '/',,.DSNSG                                                      
         SEG   DSNLADAT+2,2,.DSNSG,B                                            
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         SEG   DSNCHGED,L'DSNCHGED,.DSNSG,B                                     
         SEG   DSNUCBTP,L'DSNUCBTP,.DSNSG,B                                     
         SEG   DSNDVDES,L'DSNDVDES,.DSNSG,B                                     
         SEG   DSNKBYTU,L'DSNKBYTU,.DSNSG,B                                     
         SEG   DSNKBYTA,L'DSNKBYTA,.DSNSG,B                                     
         SEG   DSNSTORC,L'DSNSTORC,.DSNSG,B                                     
         SEG   DSNMGMTC,L'DSNMGMTC,.DSNSG,B                                     
         SEG   DSNDATAC,L'DSNDATAC,.DSNSG,B                                     
*                                                                               
* Add new format creation and last access dates at                              
* end                                                                           
*                                                                               
         IF    (DSNCRDAT,NE,' '),BEGIN                                          
         SEG   DSNCRDAT,L'DSNCRDAT,.DSNSG,B                                     
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         IF    (DSNLADAT,NE,' '),BEGIN                                          
         SEG   DSNLADAT,L'DSNLADAT,.DSNSG,B                                     
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         END                                                                    
         SETMSG L:DSNSGLOC,L:DSNSGLENF                                          
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Build SHOW DSN INTERNAL output line (NEW format)'               
*                                                                               
* This routine formats a line in INTERNAL format for SHOW DSNAMES.              
*                                                                               
* Input:                                                                        
*                                                                               
*    R0, R1 - Describe the length and location of the output buffer             
*    R2     - Points to the DSNINFO record containing the information           
*             for the dataset in question.                                      
*                                                                               
* Output:                                                                       
*    R0,R1 -  Contain the length and location of the output line.               
*                                                                               
DSPDSINN XPROC DSNINWA                                                          
         LR    R4,R2                                                            
         SEGINIT (R1),(R0),DSNSG                                                
         WITH  (DSNINFO,R4),BEGIN                                               
         SEG   DSNVOL,L'DSNVOL,.DSNSG,B                                         
         SEG   DSNDSN,L'DSNDSN,.DSNSG,B                                         
         DTB   DSNTRKUS,L'DSNTRKUS-3                                            
         LR    R3,R15                                                           
         DTB   DSNTRKUS+L'DSNTRKUS-2,2                                          
         IF    (R15,NZ),BEGIN                                                   
         INCR  R3                                                               
         XPUSH ,,8,PTR=R1                                                       
         LA    R0,5                                                             
         LR    R15,R3                                                           
         VCALL BTDZEROS                                                         
         SEG   (R1),(R0),.DSNSG                                                 
         XPOP  ,,8                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   DSNTRKUS,L'DSNTRKUS-3,.DSNSG                                     
         END                                                                    
         SEG   DSNERRCD,L'DSNERRCD,.DSNSG                                       
         SEG   DSNTRKAL,L'DSNTRKAL,.DSNSG,B                                     
         SEG   DSNXTNTS,L'DSNXTNTS,.DSNSG,B                                     
         SEG   DSNDSORG,L'DSNDSORG,.DSNSG,B                                     
         SEG   DSNRECFM,L'DSNRECFM,.DSNSG,B                                     
         SEG   DSNLRECL,L'DSNLRECL,.DSNSG,B                                     
         SEG   DSNBKSIZ,L'DSNBKSIZ,.DSNSG,B                                     
         SEG   DSNKEYLN,L'DSNKEYLN,.DSNSG,B                                     
         IF    (DSNCRDAT,NE,' '),BEGIN                                          
         SEG   DSNCRDAT,L'DSNCRDAT,.DSNSG,B                                     
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         IF    (DSNLADAT,NE,' '),BEGIN                                          
         SEG   DSNLADAT,L'DSNLADAT,.DSNSG,B                                     
         END                                                                    
         ELSE  BEGIN                                                            
         SEG   CVBLANKS,8,.DSNSG,B                                              
         END                                                                    
         SEG   DSNCHGED,L'DSNCHGED,.DSNSG,B                                     
         SEG   DSNUCBTP,L'DSNUCBTP,.DSNSG,B                                     
         SEG   DSNDVDES,L'DSNDVDES,.DSNSG,B                                     
         SEG   DSNKBYTU,L'DSNKBYTU,.DSNSG,B                                     
         SEG   DSNKBYTA,L'DSNKBYTA,.DSNSG,B                                     
         SEG   DSNSTORC,L'DSNSTORC,.DSNSG,B                                     
         SEG   DSNMGMTC,L'DSNMGMTC,.DSNSG,B                                     
         SEG   DSNDATAC,L'DSNDATAC,.DSNSG,B                                     
         END                                                                    
         SETMSG L:DSNSGLOC,L:DSNSGLENF                                          
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
*box                                                                            
*                                                                               
*  DSNCHK -- Routine to check that dsname conforms to OS                        
*    data set name conventions.                                                 
*                                                                               
*      On entry:                                                                
*        R0,R1 - length and location of name to check                           
*                                                                               
*      On exit, R15 (and CC):                                                   
*        0 - ok                                                                 
*        4 - bad os dsname                                                      
*        8 - bad chars in dsname                                                
*                                                                               
*  Note: This routine is a duplicate of one in DOPT.  WGS                       
*        contains a similar routine that's never called.                        
*        We have to correct all that someday.                                   
*                                                                               
DSNCHK   PROC  ,                                                                
         CLEAR R15                 Assume ok                                    
*                                                                               
         LR    R6,R1                                                            
         LTR   R5,R0                                                            
         BNP   DSNRC4              Null dsname is n.g.                          
*                                                                               
         LA    R4,8                Max no. of chars per level                   
         LA    R3,2                Allow dot, alpha, or national                
         WHILE (R5,POS),BEGIN                                                   
         LC    R2,@R6              Get char                                     
         LC    R0,DSNCHR(R2)       Get char type                                
         IF    (R0,Z),BEGIN        It's a dot...                                
         IF    (R4,GE,8),DSNRC4    Dot can't be first char                      
         LA    R4,8                Allow eight more chars                       
         LA    R3,2                Allow dot, alpha, or national                
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (R0,GT,R3),'LA R15,8; B DSNCHKX'  bad chars                      
         DECR  R4                                                               
         IF    (R4,NEG),DSNRC4     Over 8 chars w/o a dot                       
         LA    R3,3                Allow numbers too now                        
         END                                                                    
         LA    R6,@R6+1                                                         
         DECR  R5                                                               
         END                                                                    
         B     DSNCHKX             All done, aok -- scram                       
*                                                                               
DSNRC4   LA    R15,4                                                            
DSNCHKX  PEND                                                                   
         SPACE 2                                                                
*-                                                                              
*-        Dsname character type table.                                          
*-                                                                              
*-       0 - Dot (.)                                                            
*-       1 - Alphabetic                                                         
*-       2 - National ($,@,#)                                                   
*-       3 - Numeric                                                            
*-           255 - Everything else                                              
*-                                                                              
DSNCHR   DC    256X'FF'                                                         
         ORG   DSNCHR+C'.'                                                      
         DC    X'00'                                                            
         ORG   DSNCHR+C'A'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'J'                                                      
         DC    X'010101010101010101'                                            
         ORG   DSNCHR+C'S'                                                      
         DC    X'0101010101010101'                                              
         ORG   DSNCHR+C'$'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'@'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'#'                                                      
         DC    X'02'                                                            
         ORG   DSNCHR+C'0'                                                      
         DC    X'03030303030303030303'                                          
         ORG                                                                    
         EJECT                                                                  
*box                                                                            
*                                                                               
*  SEGFNAME Routine to seg dsname for SHOW CAT and SHOW DSNAME.                 
*                                                                               
*  Inputs:                                                                      
*    R0, R1 - File name length and location                                     
*    R2     - If nonzero means disk dataset                                     
*                                                                               
*  Output:                                                                      
*    File name TSEGed in appropriate format.                                    
*                                                                               
SEGFILE  PROC  ,                                                                
         LR    R3,R0                                                            
         LR    R4,R1                                                            
         ACALL DSNCHK                                                           
         IF    (R15,NZ),'LA R5,1'                                               
         ELSE  'CLEAR R5'                                                       
         IF    (R2,NZ),BEGIN       It's a disk file                             
         IF    (R5,NZ),'TSEG '''''                                              
         IF    ((R3,GE,11),AND,(@R4,EQ,'WYL.'),AND,                    X        
               (@R4+6,EQ,'.'),AND,(@R4+10,EQ,'.')),BEGIN                        
         IF    ((CPSGRP,EQ,@R4+4),AND,(CPSUSER,EQ,@R4+7)),BEGIN                 
         LA    R4,@R4+11           Skip past the standard prefix                
         SH    R3,=H'11'                                                        
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  '$'                                                              
         END                                                                    
         END                                                                    
         ELSE  BEGIN               It's a tape file                             
         IF    ((R3,LT,11),OR,(@R4,NE,'WYL.')),BEGIN                            
         TSEG  '$'                                                              
         END                                                                    
         END                                                                    
         TSEG  (R4),(R3)                                                        
         IF    (R2,NZ),BEGIN                                                    
         IF    (R5,NZ),'TSEG '''''                                              
         END                                                                    
         TSEG  CVBLANKS,1                                                       
         PEND                                                                   
         EJECT                                                                  
         TITLE 'Build SHOW DSN output line'                                     
*                                                                               
* This routine formats a line for SHOW DSNAMES.                                 
*                                                                               
* Input:                                                                        
*                                                                               
*    R4     - Points to the DSNINFO record containing the information           
*             for the dataset in question.                                      
*                                                                               
*    R6     - Points to the SHOW DSNAME argument block                          
*                                                                               
* Output:                                                                       
*    DSNAME information built and TSEGed.                                       
*                                                                               
*                                                                               
DSPDSLIN PROC  ,                                                                
         USING SHDSARGS,R6                                                      
         WITH  (DSNINFO,R4),BEGIN                                               
*                                                                               
* Write out the filename                                                        
*                                                                               
         LA    R1,DSNDSN                                                        
         LA    R0,L'DSNDSN                                                      
         VCALL RTRIM                                                            
         L     R5,CPFINFOP         Get the FFINFO                               
         WITH  (FFINFO,R5),BEGIN                                                
         ST    R0,FFRNAMEL                                                      
         LR    R2,R0                                                            
         MOVE  R2,FFRNAME,@R1                                                   
         LA    R2,1                It's a disk                                  
         ACALL SEGFILE                                                          
         END                                                                    
*                                                                               
         IF    (DSNERRCD,EQ,'A'),BEGIN  It's been migrated....                  
         TSEG  '(Migrated)'                                                     
         END                                                                    
         ELSE  BEGIN                                                            
         IF    (SHDSTRK,OR,SHDSTYP,OR,SHDSDAT),BEGIN                            
         IF    SHDSTRK,BEGIN                                                    
         TSEG  '--',,B                                                          
         DTB   DSNTRKUS,L'DSNTRKUS-3  Tracks used                               
         LR    R5,R15                                                           
         DTB   DSNTRKUS+L'DSNTRKUS-2,2                                          
         IF    (R15,NZ),BEGIN                                                   
         INCR  R5                                                               
         END                                                                    
         DTB   DSNTRKAL,L'DSNTRKAL                                              
         LR    R2,R15              Tracks allocated                             
         IF    (R5,LT,R2),BEGIN                                                 
         TNUM  (R5)                                                             
         TSEG  ' OF '                                                           
         END                                                                    
         TNUM  (R2)                                                             
         TSEG  ' TRACKS'                                                        
         DTB   DSNXTNTS,L'DSNXTNTS                                              
         LR    R5,R15              Extents                                      
         IF    (R5,GT,1),BEGIN                                                  
         TSEG  ', '                                                             
         TNUM  (R5)                                                             
         TSEG  ' EXTENTS'                                                       
         END                                                                    
         END                                                                    
*                                                                               
         TSEG  CVBLANKS,1                                                       
*                                                                               
         IF    SHDSTYP,BEGIN                                                    
         TSEG  '--',,B                                                          
         SETMSG DSNDSORG,L'DSNDSORG                                             
         VCALL RTRIM                                                            
         TSEG  (R1),(R0)                                                        
         TSEG  ',',,B                                                           
         SETMSG DSNRECFM,L'DSNRECFM                                             
         VCALL RTRIM                                                            
         TSEG  (R1),(R0)                                                        
         TSEG  '/'                                                              
         DTB   DSNLRECL,L'DSNLRECL                                              
         TNUM  (R15)                                                            
         TSEG  '/'                                                              
         DTB   DSNBKSIZ,L'DSNBKSIZ                                              
         TNUM  (R15)                                                            
         DTB   DSNKEYLN,L'DSNKEYLN                                              
         IF    (R15,NZ),BEGIN                                                   
         LR    R2,R15                                                           
         TSEG  '/'                                                              
         TNUM  (R2)                                                             
         END                                                                    
         END                                                                    
*                                                                               
         TSEG  CVBLANKS,1                                                       
*                                                                               
         IF    SHDSDAT,BEGIN                                                    
         TSEG  '--',,B                                                          
         TSEG  'CR:  '                                                          
         IF    SHDSNEW,BEGIN                                                    
         TSEG  DSNCRDAT,L'DSNCRDAT                                              
         TSEG  '  LA:  '                                                        
         TSEG  DSNLADAT,L'DSNLADAT                                              
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG  DSNCRDAT+5,5                                                     
         TSEG  '/'                                                              
         TSEG  DSNCRDAT+2,2                                                     
         TSEG  '  LA:  '                                                        
         TSEG  DSNLADAT+5,5                                                     
         TSEG  '/'                                                              
         TSEG  DSNLADAT+2,2                                                     
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         PEND  ,                                                                
         DROP  R6                                                               
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Output for SHOW DSNAMES'                                        
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DSNAMES work area,                             
*             which contains the page array and options                         
*                                                                               
*             The first halfword on each page is the total                      
*             number of bytes of information on that page.                      
*             Each line on the page is preceded by a byte                       
*             which indicates the line length. A zero byte                      
*             indicates all done.                                               
*                                                                               
*    Note: This routine does not return to its caller.                          
*                                                                               
DSPDSWA  RECORD BEGIN                                                           
DSPERRSV DS    XL256                                                            
DSPXLINE DS    CL(L'DSNINFO)         Expanded info line                         
         END                                                                    
DSPDSN   PROC  DSPDSWA                                                          
         LR    R6,R1                                                            
         USING SHDSARGS,R6                                                      
*                                                                               
         L     R0,SHDSPAGS                                                      
         PGET  PAR,(R0)                                                         
         CLEAR SHDSCPG                                                          
         MVC   SHDSCOFF,=F'2'                                                   
*                                                                               
         LA    R1,SHDSARGS                                                      
         LA    R2,DSPXLINE                                                      
         ACALL NEXTDSLN                                                         
*                                                                               
         IF    (R15,Z),BEGIN                                                    
         LR    R4,R1                                                            
         LR    R3,R0               Save length in case of error                 
         WITH  (DSNINFO,R4),BEGIN                                               
         IF    (DSNCATTP,NE,'+'),BEGIN                                          
*                                                                               
         IF    ((R0,LT,=A(DSNERRCD-DSNCATTP)),OR,                      X        
               (DSNERRCD,EQ,' '),OR,(DSNERRCD,EQ,'A')),BEGIN                    
         IF    SHDSINT,BEGIN                                                    
         XPUSH ,,256,PTR=R1                                                     
         LA    R0,256                                                           
         LA    R2,DSPXLINE                                                      
         IF    SHDSNEW,BEGIN                                                    
         ACALL DSPDSINN                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL DSPDSNIN                                                         
         END                                                                    
         TSEG  (R1),(R0)                                                        
         XPOP  ,,256                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL DSPDSLIN                                                         
         END                                                                    
         TCCR                                                                   
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPEXIT'                                                   
         ELSE  BEGIN                                                            
         IF    SHDSSCR,BEGIN                                                    
         LA    R1,DSNINFO                                                       
         LA    R2,SHDSARGS                                                      
         ACALL SCRDSN                                                           
         IF    M,'B DSPEXIT'                                                    
         END                                                                    
         LA    R1,SHDSARGS                                                      
         LA    R2,DSPXLINE                                                      
         ACALL NEXTDSLN                                                         
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
*                                                                               
*  Originally, if an error occurred, WYLBUR printed the                         
*  message only if the LIKE or ONLY options had been used                       
*  and information about exactly one dataset was returned.                      
*  Otherwise, WYLBUR just ignored the error. We reproduce                       
*  that behavior here.                                                          
*                                                                               
         MOVE  R3,DSPERRSV,@R1     Save the line                                
         LA    R1,SHDSARGS                                                      
         LA    R2,DSPXLINE                                                      
         ACALL NEXTDSLN                                                         
         IF    (R15,NZ),BEGIN                                                   
         IF    (SHDSLIK,OR,SHDSONLY),BEGIN                                      
*                                                                               
         LA    R4,DSPERRSV         Switch back to the old line                  
         IF    (DSNERRCD,EQ,'M'),BEGIN                                          
         TSEG  'Volume not currently available to WYLBUR',,CR                   
         LA    R1,SHDSARGS                                                      
         ACALL RLSDSPGS                                                         
         B     CVNOACTN                                                         
         END                                                                    
*                                                                               
         ELSEIF (DSNERRCD,EQ,'D'),BEGIN                                         
         TSEG  'Dataset information not on volume',,CR                          
         LA    R1,SHDSARGS                                                      
         ACALL RLSDSPGS                                                         
         B     CVNOACTN                                                         
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         TSEG  'Unknown OBTAIN error, RC =',,B                                  
         TSEG  DSNTRKUS,L'DSNTRKUS,B                                            
         TCCR                                                                   
         LA    R1,SHDSARGS                                                      
         ACALL RLSDSPGS                                                         
         B     CVNOACTN                                                         
         END                                                                    
*                                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
*   Process 2nd through last lines here                                         
*                                                                               
         WHILE (R15,Z),BEGIN                                                    
         LR    R4,R1                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
         IF    (DSNCATTP,NE,'+'),BEGIN                                          
         IF    ((R0,LT,=A(DSNERRCD-DSNCATTP)),OR,                      X        
               (DSNERRCD,EQ,' '),OR,(DSNERRCD,EQ,'A')),BEGIN                    
         IF    SHDSINT,BEGIN                                                    
         XPUSH ,,256,PTR=R1                                                     
         LA    R0,256                                                           
         LR    R2,R4                                                            
         IF    SHDSNEW,BEGIN                                                    
         ACALL DSPDSINN                                                         
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL DSPDSNIN                                                         
         END                                                                    
         TSEG  (R1),(R0)                                                        
         XPOP  ,,256                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         ACALL DSPDSLIN                                                         
         END                                                                    
         TCCR                                                                   
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPEXIT'                                                   
         ELSE  BEGIN                                                            
         IF    SHDSSCR,BEGIN                                                    
         LA    R1,DSNINFO                                                       
         LA    R2,SHDSARGS                                                      
         ACALL SCRDSN                                                           
         IF    M,'B DSPEXIT'                                                    
         END                                                                    
         END                                                                    
         END                                                                    
         END                                                                    
         LA    R1,SHDSARGS                                                      
         LA    R2,DSPXLINE                                                      
         ACALL NEXTDSLN                                                         
         END                                                                    
         END                                                                    
*                                                                               
DSPEXIT  LABEL                                                                  
         LA    R1,SHDSARGS                                                      
         ACALL RLSDSPGS                                                         
         B     CVNEXT                                                           
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         DROP  R6                                                               
         EJECT                                                                  
         TITLE 'SHOW CATALOG Command'                                           
*box                                                                            
*                                                                               
*  SHOWCAT -- SHOW CATALOG Command.                                             
*                                                                               
CTERRWK  RECORD BEGIN                                                           
CTERRID  DS    CL8                                                              
CTMSGL   DS    F                                                                
CTMSG    DS    CL&LINESZ                                                        
         END                                                                    
*                                                                               
SHOWCAT  XPROC SHCTARGS                                                         
         LA    R6,SHCTARGS                                                      
         USING SHCTARGS,R6                                                      
         CLEAR SHCTARGS                                                         
*                                                                               
         VCALL MKFFINFO                                                         
         L     R5,CPFINFOP                                                      
         USING FFINFO,R5                                                        
*                                                                               
         COMMENT                   SCAN SHOW CAT OPTIONS                        
         SCAN  CATPRT                                                           
         SCANCHK                                                                
*                                                                               
         IF    (SHCTSCR,AND,CPFDUMP),BEGIN                                      
         TSEG  'SCRATCH',,B        Tell which bad option                        
         ERROR 'conflicts with dump command or collect options.'                
         END                                                                    
*                                                                               
         VCALL FNAMEFIN            Process dsname parameters                    
*                                                                               
         COMMENT                   SAMSON SHOW CAT                              
         IF    FFFSAM,BEGIN                                                     
         ERROR 'SHOW CAT not supported for Samson.'                             
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   NETWORK SHOW CAT                             
         IF    FFFNET,BEGIN                                                     
         ERROR 'SHOW CAT not supported for network files.'                      
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   WINGS SHOW CAT                               
         IF    FFFWINGS,BEGIN                                                   
*                                                                               
         IF    (SHCTUPL,GT,0),BEGIN                                             
         L     R1,SHCTUPL                                                       
         ST    R1,FFNAMEL                                                       
         MOVE  R1,FFNAME,SHCTUPB                                                
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         ST    R1,SHCTUPL                                                       
         MOVE  R1,SHCTUPB,FFRNAME                                               
         END                                                                    
*                                                                               
         IF    (SHCTLOWL,GT,0),BEGIN                                            
         L     R1,SHCTLOWL                                                      
         ST    R1,FFNAMEL                                                       
         MOVE  R1,FFNAME,SHCTLOWB                                               
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         ST    R1,SHCTLOWL                                                      
         MOVE  R1,SHCTLOWB,FFRNAME                                              
         END                                                                    
*                                                                               
         IF    (SHCTLIKL,GT,0),BEGIN                                            
         L     R0,SHCTLIKL                                                      
         LA    R1,SHCTLIKE                                                      
         VCALL TOUPPER                                                          
         IF    ((@R1,EQ,'$'),AND,(R0,GT,1)),BEGIN                               
         INCR  R1                                                               
         DECR  R0                                                               
         ST    R0,SHCTLIKL                                                      
         LR    R2,R0                                                            
         MOVE  R2,SHCTLIKE,SHCTLIKE+1                                           
         END                                                                    
         ELSEIF ((R0,GE,4),AND,(@R1,EQ,'WYL.')),BEGIN                           
         END                                                                    
         ELSEIF ((R0,GE,5),AND,                                        X        
               (@R1,EQ,'SYS'),AND,                                     X        
               (@R1+3,GE,'0'),AND,(@R1+3,LE,'9'),AND,                  X        
               (@R1+4,EQ,'.')),BEGIN                                            
         END                                                                    
         ELSE  BEGIN                                                            
         ST    R0,FFNAMEL                                                       
         LR    R1,R0                                                            
         MOVE  R1,FFNAME,SHCTLIKE                                               
         VCALL NRESOLVE                                                         
         L     R0,FFRNAMEL                                                      
         LA    R1,FFRNAME                                                       
         VCALL RETFNAME                                                         
         ST    R0,SHCTLIKL                                                      
         LR    R1,R0                                                            
         MOVE  R1,SHCTLIKE,FFRNAME                                              
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         LA    R0,1                                                             
         MVI   FFNAME,C'A'                                                      
         ST    R0,FFNAMEL                                                       
         VCALL NRESOLVE                                                         
         L     R1,FFRNAMEL                                                      
         S     R1,=F'2'                                                         
         ST    R1,SHCTLIKL                                                      
         MOVE  R1,SHCTLIKE,FFRNAME                                              
         END                                                                    
*                                                                               
         LA    R1,SHCTARGS                                                      
         VCALL WGSHOCAT                                                         
         IF    (R15,EQ,4),BEGIN                                                 
*                                                                               
* If we got an error from WINGS, there still may be catalog entries             
* that can be displayed. So, save the error message and error id,               
* display the entries and then restore the error information.                   
*                                                                               
         IF    (SHCTPGCT,NZ),BEGIN                                              
         XPUSH ,,L'CTERRWK,PTR=R2                                               
         WITH  (CTERRWK,R2),BEGIN                                               
         MVC   CTERRID,CPERRID                                                  
         L     R1,CPSEGLOC                                                      
         L     R3,CPSEGLENF                                                     
         IF    (R3,POS),BEGIN                                                   
         AR    R3,R1                                                            
         DECR  R3                                                               
         IF    (@R3,NE,X'15'),BEGIN   IF TRAILING NL, STRIP IT                  
         INCR  R3                                                               
         END                                                                    
         SR    R3,R1                                                            
         IF    (R3,POS),BEGIN                                                   
         CEIL  R3,L'CTMSG                                                       
         ST    R3,CTMSGL                                                        
         MOVE  R3,CTMSG,CPSEGLOC                                                
         END                                                                    
         TCLEAR ,                                                               
         END                                                                    
         LA    R1,SHCTARGS                                                      
         ACALL DSPCAT                                                           
         TWRITE ,                      Flush the last entry out                 
         MVC   CPERRID,CTERRID                                                  
         TSEG  CTMSG,L:CTMSGL,CR                                                
         END                                                                    
         XPOP  ,,L'CTERRWK                                                      
         END                                                                    
         B     CVABORT                                                          
         END                                                                    
         IF    (R15,EQ,8),BEGIN                                                 
         IF    (SHCTPGCT,Z),BEGIN                                               
         VCALL NOMORPG                                                          
         END                                                                    
*        IF    (CPFDUMP),BEGIN                                                  
*        CLEAR CPFDUMP                                                          
*        TSEG  'Too many dsname items. Partial listing follows.',,CR            
*        TWRITE                                                                 
*        SET   CPFDUMP                                                          
*        END                                                                    
*        ELSE  BEGIN                                                            
         TSEG  'Too many dsname items. Partial listing follows.',,CR            
*        END                                                                    
         END                                                                    
         IF    (SHCTPGCT,Z),BEGIN                                               
         IF    ^SHCTINT,BEGIN                                                   
*        CLEAR CPFDUMP                                                          
         TSEG  'No data sets found in the catalog.'                             
         END                                                                    
         B CVNEXT                                                               
         END                                                                    
         LA    R1,SHCTARGS                                                      
         ACALL DSPCAT                                                           
         B     CVNEXT                                                           
         END                                                                    
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  For Show Catalog                                                             
*                                                                               
CATPRT   SCKW  LIKE,CATLIKE,A                                                   
         SCKW  FROM,CATFROM,(P,A)                                               
*        SCKW EXCLUDE,CATEXCL,(P,A)                                             
*        SCKW THROUGH,CATTHRU,(P,A)                                             
*        SCKW THRU,CATTHRU,P                                                    
         SCKW  TO,CATTO,P                                                       
         SCKW  FOR,CATFOR,(P,A)                                                 
         SCKW  VOLUME,CATOPTVL,A                                                
         SCKW  UNIT,CATOPTDV,A                                                  
         SCKW  DISK,CATOPTDK,A                                                  
         SCKW  TAPE,CATOPTTA,A                                                  
         SCKW  INTERNAL,CATINT,A                                                
         SCKW  SCRATCH,CATSCRT,A                                                
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
*                                                                               
CATFOR   PROC  ,                                                                
         XPUSH R0,R1                                                            
         TSEG  'Please use LIKE instead of FOR in the future.',,CR              
         XPOP  R0,R1                                                            
         SCTOK (R1)                                                             
         IF    (@R1,EQ,'* '),BEGIN                                              
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         END                                                                    
         LR    R3,R0                                                            
         CEIL  R3,L'SHCTLIKE                                                    
         ST    R3,SHCTLIKL                                                      
         MOVE  R3,SHCTLIKE,@R1                                                  
         PEND                                                                   
*                                                                               
CATLIKE  PROC  ,                                                                
         SCANSTR ,                 SKIP=' ', STOP=' ,=;'                        
         IF    (R15,Z),BEGIN                                                    
         IF    (R0,POS),BEGIN                                                   
         LR    R3,R0                                                            
         CEIL  R3,L'SHCTLIKE                                                    
         ST    R3,SHCTLIKL                                                      
         MOVE  R3,SHCTLIKE,@R1                                                  
         CLEAR R15                                                              
         END                                                                    
         ELSE  BEGIN                                                            
         ERROR 'Missing dataset name.',,MISSDSN                                 
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
CATFROM  PROC  ,                                                                
         SCTOK (R1)                                                             
         LR    R3,R0                                                            
         CEIL  R3,L'SHCTLOWB                                                    
         ST    R3,SHCTLOWL                                                      
         MOVE  R3,SHCTLOWB,@R1                                                  
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         LTR   R3,R0                                                            
         IF    NZ,BEGIN                                                         
         CEIL  R3,L'SHCTLOWB                                                    
         ST    R3,SHCTLOWL                                                      
         MOVE  R3,SHCTLOWB,@R1                                                  
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
CATTO    PROC  ,                                                                
         SCTOK (R1)                                                             
         LR    R3,R0                                                            
         CEIL  R3,L'SHCTUPB                                                     
         ST    R3,SHCTUPL                                                       
         MOVE  R3,SHCTUPB,@R1                                                   
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFNAME                                                         
         LTR   R3,R0                                                            
         IF    NZ,BEGIN                                                         
         CEIL  R3,L'SHCTUPB                                                     
         ST    R3,SHCTUPL                                                       
         MOVE  R3,SHCTUPB,@R1                                                   
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
CATOPTVL PROC                                                                   
         SET   SHCTVOL                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CATOPTDV PROC                                                                   
         SET   SHCTUNIT                                                         
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CATOPTDK PROC                                                                   
         SET   SHCTDSK                                                          
         CLEAR SHCTTAP                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CATOPTTA PROC                                                                   
         SET   SHCTTAP                                                          
         CLEAR SHCTDSK                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CATINT   PROC                                                                   
         SET   SHCTINT                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
CATSCRT  PROC                                                                   
         SET   SHCTSCR                                                          
         CLEAR R15                                                              
         PEND                                                                   
         QLTORG                                                                 
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
         TITLE 'Get next line for display'                                      
*                                                                               
*    This routine is called by the DSPCAT subroutine to get the                 
*    next line for display.                                                     
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW CATALOG work area which                        
*             contains a work area used by this routine.                        
*                                                                               
*      PAR -  Points to the current page being displayed.                       
*                                                                               
*    On output:                                                                 
*      R0,R1 - describe the length and location of the retrieved                
*              line                                                             
*      R15=0   means a line was retrieved                                       
*         =NZ  means EOF (no more lines in the display).                        
*                                                                               
NEXTCTLN PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHCTARGS,R6                                                      
*                                                                               
         L     R1,SHCTCPG          Make sure we have the page in core           
         SLL   R1,2                                                             
         L     R0,SHCTPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
*                                                                               
         LH    R3,@PAR                                                          
         IF    (SHCTCOFF,GE,R3),BEGIN                                           
         L     R1,SHCTCPG                                                       
         LA    R1,1(,R1)                                                        
         IF    (R1,LT,SHCTPGCT),BEGIN                                           
         ST    R1,SHCTCPG                                                       
         MVC   SHCTCOFF,=F'2'                                                   
         PFREE PAR,EMPTY                                                        
         L     R1,SHCTCPG          Get the new page into core                   
         SLL   R1,2                                                             
         L     R0,SHCTPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
         LH    R3,@PAR                                                          
         FAIL  (R3,LE,SHCTCOFF),CATERR1,'NEXTCTLN error.'                       
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         B     NXTCTDON                                                         
         END                                                                    
         END                                                                    
*                                                                               
         L     R2,SHCTCOFF                                                      
         LA    R1,0(R2,PAR)                                                     
         LC    R0,@R1                                                           
         INCR  R1                                                               
         AR    R2,R0                                                            
         INCR  R2                                                               
         ST    R2,SHCTCOFF                                                      
         IF    (R0,Z),BEGIN                                                     
         PFREE PAR,EMPTY                                                        
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
NXTCTDON LABEL                                                                  
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG ,                                                               
         EJECT                                                                  
         TITLE 'Release remaining display pages'                                
*                                                                               
*    This subroutine releases all pages containing the                          
*    information used by SHOW CATALOG.                                          
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW CATALOG work area.                             
*                                                                               
*    The current page (in PAR) and the remainder of the                         
*    pages not yet accessed in the array are released.                          
*                                                                               
RLSCTPGS PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHCTARGS,R6                                                      
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         END                                                                    
         L     R3,SHCTCPG                                                       
         INCR  R3                                                               
         WHILE (R3,LT,SHCTPGCT),BEGIN                                           
         LR    R1,R3                                                            
         SLL   R1,2                                                             
         L     R2,SHCTPAGS(R1)                                                  
         PGET  PAR,(R2)                                                         
         PFREE PAR,EMPTY                                                        
         LA    R3,1(,R3)                                                        
         END                                                                    
         DROP  R6                                                               
         PEND  ,                                                                
         EJECT                                                                  
         TITLE 'Handle scratch option of SHOW CATALOG'                          
*                                                                               
*   Prompt the user to see if it's okay to scratch the indicated                
*   member and scratch it if so.                                                
*                                                                               
*   On entry:                                                                   
*      R1 - Points to the DSNINFO work area                                     
*      R2 - Points to the SHOW CATALOG work area                                
*                                                                               
*   On output:                                                                  
*      R15=0   If user wanted file scratched, file scratched,                   
*              else file not scratched.                                         
*      R15>0   Some error occurred                                              
*      R15<0   User pressed attention                                           
*                                                                               
SCRCAT   PROC  FFINFO                                                           
         LR    R6,R2                                                            
         USING SHCTARGS,R6                                                      
         LR    R4,R1                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
         LA    R1,DSNDSN                                                        
         LA    R0,L'DSNDSN                                                      
         VCALL RTRIM                                                            
         END                                                                    
         LR    R3,R0                                                            
         LR    R4,R1               Save filename information                    
         SETMSG 'Scratch'                                                       
         VCALL YESRTN                                                           
         BM    SCRCTATN                                                         
         BNZ   SCRCTABT            OK if we want to skip it                     
*                                                                               
         XPUSH R3,R4                                                            
         LA    R2,FFINFO           Save the current                             
         LA    R3,L'FFINFO             FFINFO data                              
         L     R4,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         L     R5,CPFINFOP         Get the FFINFO                               
         WITH  (FFINFO,R5),BEGIN                                                
         CLEAR FFOPTSL             No options                                   
         CLEAR FFRNAMEL                                                         
         XPOP  R2,R3               Recover file name                            
         ST    R2,FFRNAMEL                                                      
         ST    R2,FFNAMEL                                                       
         MOVE  R2,FFRNAME,@R3                                                   
         L     R2,FFNAMEL                                                       
         MOVE  R2,FFNAME,FFRNAME                                                
         VCALL WGSCRTCH            Scratch the file                             
         IF    NZ,BEGIN                                                         
         IF    (CPERRID,EQ,'NOTYOURS'),BEGIN                                    
         CLEAR SHCTSCR             Don't scratch any more                       
         END                                                                    
         CLEAR CPERRID                                                          
         END                                                                    
         TCCR                                                                   
         ACALL CTWRITE             Write out the message                        
         END                                                                    
*                                                                               
         LA    R4,FFINFO           Restore the original                         
         LA    R3,L'FFINFO             FFINFO data                              
         L     R2,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         B     SCRCTOK                                                          
*                                                                               
SCRCTABT LABEL                                                                  
         LA    R15,4                                                            
         B     SCRCTDON                                                         
*                                                                               
SCRCTATN LABEL                                                                  
         L     R15,=F'-4'                                                       
         B     SCRCTDON                                                         
*                                                                               
SCRCTOK  CLEAR R15                                                              
         B     SCRCTDON                                                         
*                                                                               
SCRCTDON LABEL                                                                  
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Build SHOW CAT INTERNAL output line'                            
*                                                                               
* This routine formats a line in INTERNAL format for SHOW CATALOG.              
*                                                                               
* Input:                                                                        
*                                                                               
*    R2     - Points to the DSNINFO record containing the information           
*             for the dataset in question.                                      
*                                                                               
*                                                                               
DSPCATIN XPROC                                                                  
         LR    R4,R2                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
*         IF    SHCTVOL,BEGIN                                                   
         TSEG   DSNVOL,L'DSNVOL,B                                               
         TSEG   DSNDEVTP,L'DSNDEVTP                                             
         IF    (DSNDEVTP,EQ,'T'),BEGIN                                          
         DTB   DSNFLSQ,L'DSNFLSQ                                                
         LR    R2,R15                                                           
         TNUM  (R2),4                                                           
         DTB   DSNVLCT,L'DSNVLCT                                                
         LR    R2,R15                                                           
         TNUM  (R2),3                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         TSEG   CVBLANKS,7                                                      
         END                                                                    
*         END                                                                   
*         IF    SHCTUNIT,BEGIN                                                  
         TSEG   CVBLANKS,1                                                      
         TSEG   DSNUCBTP,L'DSNUCBTP,B                                           
*         END                                                                   
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Build SHOW CAT output line'                                     
*                                                                               
* This routine formats a line for SHOW CATALOG.                                 
*                                                                               
* Input:                                                                        
*                                                                               
*    R2     - Points to the DSNINFO record containing the information           
*             for the dataset in question.                                      
*                                                                               
*                                                                               
DSPCATEX XPROC                                                                  
         LR    R4,R2                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
*                                                                               
* Write out the filename                                                        
*                                                                               
         LA    R1,DSNDSN                                                        
         LA    R0,L'DSNDSN                                                      
         VCALL RTRIM                                                            
         L     R5,CPFINFOP         Get the FFINFO                               
         WITH  (FFINFO,R5),BEGIN                                                
         ST    R0,FFRNAMEL                                                      
         LR    R2,R0                                                            
         MOVE  R2,FFRNAME,@R1                                                   
         IF    (DSNDEVTP,EQ,'D'),'LA R2,1'                                      
         ELSE  'CLEAR R2'                                                       
         ACALL SEGFILE                                                          
         END                                                                    
*                                                                               
         IF    (DSNCATTP,NE,'B'),BEGIN                                          
*                                                                               
         IF    SHCTVOL,BEGIN                                                    
         TSEG  'on '                                                            
         TSEG  DSNVOL,L'DSNVOL,T                                                
         IF    (DSNDEVTP,EQ,'T'),BEGIN                                          
         TSEG  ', file '                                                        
         DTB   DSNFLSQ,L'DSNFLSQ                                                
         LR    R1,R15                                                           
         TNUM  (R1)                                                             
         DTB   DSNVLCT,L'DSNVLCT                                                
         LR    R2,R15                                                           
         IF    (R2,GT,1),BEGIN                                                  
         TSEG  ',',,B                                                           
         TNUM  (R2)                                                             
         TSEG  ' volumes'                                                       
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         IF    SHCTUNIT,BEGIN                                                   
         TSEG  ' Unit '                                                         
         LR    R0,R3                                                            
         S     R0,=A(DSNDVDES-DSNCATTP)                                         
         CEIL  R0,L'DSNDVDES                                                    
         TSEG  DSNDVDES,(R0),T                                                  
         END                                                                    
*                                                                               
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         ACALL CATTYPE                                                          
         END                                                                    
*                                                                               
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Output for SHOW CATALOG'                                        
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW CATALOG work area,                             
*             which contains the page array and options                         
*                                                                               
*             The first halfword on each page is the total                      
*             number of bytes of information on that page.                      
*             Each line on the page is preceded by a byte                       
*             which indicates the line length. A zero byte                      
*             indicates all done.                                               
*                                                                               
DSPCAT   PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHCTARGS,R6                                                      
*                                                                               
         L     R0,SHCTPAGS                                                      
         PGET  PAR,(R0)                                                         
         CLEAR SHCTCPG                                                          
         MVC   SHCTCOFF,=F'2'                                                   
*                                                                               
         LA    R1,SHCTARGS                                                      
         ACALL NEXTCTLN                                                         
*                                                                               
         WHILE (R15,Z),BEGIN                                                    
*                                                                               
         LR    R4,R1                                                            
         LR    R3,R0                                                            
         WITH  (DSNINFO,R4),BEGIN                                               
         IF    (DSNCATTP,NE,'+'),BEGIN                                          
*                                                                               
         IF    (SHCTINT),BEGIN                                                  
         TSEG  DSNCATTP,L'DSNCATTP,B                                            
         TSEG  DSNDSN,L'DSNDSN                                                  
         IF    (DSNCATTP,EQ,'A'),BEGIN                                          
         LR    R2,R4                                                            
         ACALL DSPCATIN                                                         
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         IF    (DSNCATTP,EQ,'X'),DSPCNEXT                                       
         LR    R2,R4                                                            
         ACALL DSPCATEX                                                         
         END                                                                    
*                                                                               
         TCCR                                                                   
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPCXIT'                                                   
         ELSE  BEGIN                                                            
         IF    ((SHCTSCR),AND,                                         X        
               (DSNCATTP,EQ,'A'),AND,                                  X        
               (DSNDEVTP,EQ,'D')),BEGIN                                         
         LA    R1,DSNINFO                                                       
         LA    R2,SHCTARGS                                                      
         ACALL SCRCAT                                                           
         IF    M,'B DSPCXIT'                                                    
         END                                                                    
         END                                                                    
         END                                                                    
DSPCNEXT LABEL ,                                                                
         LA    R1,SHCTARGS                                                      
         ACALL NEXTCTLN                                                         
         END                                                                    
         END                                                                    
*                                                                               
DSPCXIT  LABEL                                                                  
         LA    R1,SHCTARGS                                                      
         ACALL RLSCTPGS                                                         
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         DROP  R6                                                               
         DROP  R5                                                               
         EJECT                                                                  
CATTYPE  PROC  ,                                                                
         TSEG  '-',,B              Add separator to message                     
         L     R1,=A(CATTAB)       Address of conversion table                  
         CLEAR R0                  Loop control register                        
         LOOP  BEGIN               Go find the entry                            
         CLC   @R4(1),@R1          Do we have ignition?                         
         IF    (E,OR,(@R1,EQ,' ')),BEGIN  Add type to message                   
         IC    R0,@R1+1            Set message length                           
         LA    R1,@R1+2            And message address                          
         END   ELSE,BEGIN          Nope -- bump to next message                 
         IC    R0,@R1+1            Get length                                   
         AR    R1,R0               Bump address                                 
         LA    R1,@R1+2            By correct amount                            
         CLEAR R0                  Zero for next pass                           
         END                                                                    
         UNTIL (R0,NZ),END         Loop until entry found                       
         TSEG  (R1),(R0),B         Finish off message                           
         PEND ,                                                                 
         QLTORG                                                                 
         EJECT                                                                  
CATTAB   LABEL                                                                  
         DC    C'A'                ALIEN TYPE                                   
         DC    AL1(L'CTATYP)                                                    
CTATYP   DC    C'Data set'                                                      
         DC    C'B'                GENERATION DATA GROUP BASE                   
         DC    AL1(L'CTBTYP)                                                    
CTBTYP   DC    C'GDG base'                                                      
         DC    C'C'                VSAM CLUSTER ASSOCIATION                     
         DC    AL1(L'CTCTYP)                                                    
CTCTYP   DC    C'Vsam cluster'                                                  
         DC    C'D'                VSAM DATA                                    
         DC    AL1(L'CTDTYP)                                                    
CTDTYP   DC    C'Vsam data'                                                     
         DC    C'F'                FREE (Hummm?, beats me)                      
         DC    AL1(L'CTFTYP)                                                    
CTFTYP   DC    C'Free'                                                          
         DC    C'G'                ALTERNATE INDEX                              
         DC    AL1(L'CTGTYP)                                                    
CTGTYP   DC    C'Alternate index'                                               
         DC    C'I'                INDEX POINTER                                
         DC    AL1(L'CTITYP)                                                    
CTITYP   DC    C'Index'                                                         
         DC    C'M'                MASTER CATALOG                               
         DC    AL1(L'CTMTYP)                                                    
CTMTYP   DC    C'Master catalog'                                                
         DC    C'P'                PAGE SPACE                                   
         DC    AL1(L'CTPTYP)                                                    
CTPTYP   DC    C'Page space'                                                    
         DC    C'R'                PATH                                         
         DC    AL1(L'CTRTYP)                                                    
CTRTYP   DC    C'Path'                                                          
         DC    C'U'                USERCATALOG ENTRY                            
         DC    AL1(L'CTUTYP)                                                    
CTUTYP   DC    C'User catalog'                                                  
         DC    C'V'                VOLUME                                       
         DC    AL1(L'CTVTYP)                                                    
CTVTYP   DC    C'Volume'                                                        
         DC    C'X'                ALIAS NAME                                   
         DC    AL1(L'CTXTYP)                                                    
CTXTYP   DC    C'Alias name'                                                    
         DC    C' '                STOPPER ELEMENT                              
         DC    AL1(L'CTUNK)                                                     
CTUNK    DC    C'Unknown'                                                       
         TITLE 'SHOW DIRECTORY Command'                                         
*box                                                                            
*                                                                               
*  SHOWDIR -- SHOW DIRECTORY Command.                                           
*                                                                               
*  SHOW DIR BROKEN OUT INTO FILE TYPE (SAM,NET,WINGS,ETC)                       
*  CODE.                                                                        
*                                                                               
*                                                                               
SHOWDIR  XPROC SHDRARGS                                                         
         LA    R6,SHDRARGS                                                      
         USING SHDRARGS,R6                                                      
         CLEAR SHDRARGS                                                         
*                                                                               
*  Remove this when we make the default for ALL the new                         
*  format as opposed to the old format.                                         
*                                                                               
*  Removed 10/17/95 M. Durket                                                   
*                                                                               
*        SET   SHDROLD             Default is old format if ALL                 
*                                                                               
         VCALL MKFFINFO                                                         
         L     R5,CPFINFOP                                                      
         USING FFINFO,R5                                                        
*                                                                               
*                                                                               
         COMMENT                   SCAN SHOW DIR OPTIONS                        
         SCAN  MBRONPRT                                                         
         SCANCHK                                                                
*                                                                               
         COMMENT                   VALIDITY                                     
         IF    (SHDRSCR,AND,CPFDUMP),BEGIN                                      
         TSEG  'SCRATCH',,B        Tell which bad option                        
         ERROR 'conflicts with dump command or collect options.'                
         END                                                                    
*                                                                               
         COMMENT                   VALIDITY                                     
         IF    (SHDRSCR,AND,SHDRTAB),BEGIN                                      
         TSEG  'SCRATCH',,B        Tell which bad option                        
         ERROR 'conflicts with TABLE option.'                                   
         END                                                                    
*                                                                               
         COMMENT                   IF NO DIR SPECIFIED, USE DEFAULT             
         IF    (FFNAMEL,EQ,0),BEGIN                                             
         SET   SHDRDFLT                                                         
         MVC   FFNAME(3),=CL3'LIB'                                              
         MVC   FFNAMEL,=A(3)                                                    
         COMMENT                   IF LIB SET, USE IT                           
         COMMENT                   ** DEBUG ** SET LIB VS. SET FPRE             
         SETMSG CPLIB,L'CPLIB                                                   
         IF    ((@R1,NE,' '),AND,(@R1,NE,X'00')),BEGIN                          
         MVC   FFNAME(L'CPLIB),CPLIB                                            
         VCALL LRTRIM                                                           
         ST    R0,FFNAMEL                                                       
         END                                                                    
         END                                                                    
*                                                                               
         VCALL FNAMEFIN            Process dsname parameters                    
*                                                                               
         COMMENT                   SAMSON SHOW DIRECTORY                        
         IF    FFFSAM,BEGIN                                                     
         IF    SHDRDFLT,BEGIN                                                   
         CLEAR FFNAMEL                                                          
         CLEAR DSNWLEN             ** DEBUG ** DEL LINE 1/92                    
         END                                                                    
         IF    CPLFLG3.CPFSET,BEGIN                                             
         TSEG  'SET option ignored for SHOW DIRECTORY.',,CR                     
         END                                                                    
         SETMSG SHDRLIKE,L:SHDRLIKL                                             
         VCALL SAMDIR              Do Samson show directory                     
         BNZ   CVABORT             It didn't work, scram                        
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   NETWORK SHOW DIRECTORY                       
         IF    FFFNET,BEGIN                                                     
         IF    SHDRDFLT,BEGIN                                                   
         CLEAR FFNAMEL                                                          
         CLEAR DSNWLEN             ** DEBUG ** DEL LINE 1/92                    
         END                                                                    
         IF    CPLFLG3.CPFSET,BEGIN                                             
         TSEG  'SET option ignored for SHOW DIRECTORY.',,CR                     
         END                                                                    
         SETMSG SHDRLIKE,L:SHDRLIKL                                             
         VCALL NETDIR              Do network show directory                    
         BNZ   CVABORT             It didn't work, scram                        
         B     CVNEXT                                                           
         END                                                                    
*                                                                               
*                                                                               
         COMMENT                   WINGS SHOW DIRECTORY                         
         IF    FFFWINGS,BEGIN                                                   
         VCALL NRESOLVE                                                         
         L     R0,FFRNAMEL                                                      
         LA    R1,FFRNAME                                                       
         VCALL RETFNAME                                                         
         ST    R0,FFRNAMEL                                                      
         LA    R1,SHDRARGS                                                      
         VCALL WGSHODIR                                                         
         IF    CPLFLG3.CPFSET,'VCALL SETPFXNS'  set prefix                      
         IF    (R15,EQ,4),BEGIN                                                 
         B     CVERROR                                                          
         END                                                                    
         IF    (R15,EQ,8),BEGIN                                                 
         IF    (SHDRPGCT,Z),BEGIN                                               
         VCALL NOMORPG                                                          
         END                                                                    
         TSEG  'Too many directory items. Partial listing follows.'             
         END                                                                    
         IF    (SHDRPGCT,Z),'B CVNEXT'                                          
         LA    R1,SHDRARGS                                                      
         ACALL DSPDIR              No return                                    
         BOMB                                                                   
         END                                                                    
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  for show directory                                                           
*                                                                               
MBRONPRT SCKW  IN,FORFILE                                                       
         SCKW  FOR,FORFILE                                                      
         SCKW  HEX,SHDHEX                                                       
         SCKW  ALL,SHDALL                                                       
         SCKW  SCRATCH,SHDSCR,A                                                 
         SCKW  TABLE,SHDTAB,A                                                   
         SCKW  BLOCKS,SHDBLK,A                                                  
         SCKW  LIKE,MBRLIKE,(P,A)                                               
         SCKW  FROM,MBRFROM,(P,A)                                               
         SCKW  TO,MBRTO,P                                                       
         SCKW  SET,DIRSET                                                       
         SCKW  NOSET,DIRNOSET,A                                                 
         SCKW  OLD,OLDFMT                                                       
         SCKW  NEW,NEWFMT                                                       
*                                                                               
         SCKW  ,V(COLLPSH),PUSH                                                 
         SCKW  ,V(FNAMPSH),PUSH                                                 
         SCKW  ,V(ACCTPSHC),PUSH                                                
         SCKW  ,V(NOTVALID)                                                     
         EJECT                                                                  
*                                                                               
DIRSET   PROC  ,                                                                
         SET   CPLFLG3.CPFSET                                                   
         PEND                                                                   
*                                                                               
DIRNOSET PROC  ,                                                                
         CLEAR CPLFLG3.CPFSET                                                   
         PEND                                                                   
*                                                                               
FORFILE  PROC  ,                                                                
         VCALL DOFNAME                                                          
         CLEAR R15                                                              
         PEND  ,                                                                
*                                                                               
MBRLIKE  PROC  ,                                                                
         SCTOK (R1)                                                             
         LR    R3,R0                                                            
         CEIL  R3,L'SHDRLIKE                                                    
         ST    R3,SHDRLIKL                                                      
         MOVE  R3,SHDRLIKE,@R1                                                  
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         LTR   R2,R0                                                            
         IF    NZ,BEGIN                                                         
         CEIL  R2,L'SHDRLIKE                                                    
         ST    R2,SHDRLIKL                                                      
         MOVE  R2,SHDRLIKE,@R1                                                  
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
MBRFROM  PROC  ,                                                                
         SCUPTOK (R1)                                                           
         LR    R3,R0                                                            
         CEIL  R3,L'SHDRLOWB                                                    
         ST    R3,SHDRLOWL                                                      
         MOVE  R3,SHDRLOWB,@R1                                                  
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         LTR   R2,R0                                                            
         IF    NZ,BEGIN                                                         
         CEIL  R2,L'SHDRLOWB                                                    
         ST    R2,SHDRLOWL                                                      
         MOVE  R2,SHDRLOWB,@R1                                                  
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
MBRTO    PROC  ,                                                                
         SCUPTOK (R1)                                                           
         LR    R3,R0                                                            
         CEIL  R3,L'SHDRUPB                                                     
         ST    R3,SHDRUPL                                                       
         MOVE  R3,SHDRUPB,@R1                                                   
         IF    ((R0,EQ,1),AND,(@R1,EQ,'*')),BEGIN                               
         SETMSG CPAFNAME,L:CPAFNAMEL                                            
         VCALL RETFMEM                                                          
         LTR   R2,R0                                                            
         IF    NZ,BEGIN                                                         
         CEIL  R2,L'SHDRUPB                                                     
         ST    R2,SHDRUPL                                                       
         MOVE  R2,SHDRUPB,@R1                                                   
         END                                                                    
         END                                                                    
         PEND                                                                   
*                                                                               
SHDHEX   PROC                                                                   
         CLEAR SHDRALL                                                          
         CLEAR SHDRTAB                                                          
         SET   SHDRHEX                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
OLDFMT   PROC                                                                   
         SET   SHDROLD                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
NEWFMT   PROC                                                                   
         CLEAR SHDROLD                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHDALL   PROC                                                                   
         CLEAR SHDRTAB                                                          
         CLEAR SHDRHEX                                                          
         SET   SHDRALL                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHDBLK   PROC                                                                   
         SET   SHDRBLK                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHDTAB   PROC                                                                   
         CLEAR SHDRALL                                                          
         CLEAR SHDRHEX                                                          
         SET   SHDRTAB                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
SHDSCR   PROC                                                                   
         SET   SHDRSCR                                                          
         CLEAR R15                                                              
         PEND                                                                   
*                                                                               
         DROP  R6                                                               
         DROP  R5                                                               
*                                                                               
         QLTORG                                                                 
         TITLE 'GETTPAG Routine'                                                
GETTPAGY XENTER R15,R8,44          Save space (used by show dsnames)            
GETTPAG  EQU   GETTPAGY                                                         
         LT    R2,CPMDLN           No pages already                             
         BNP   GETTNO1                                                          
         PMARK PAR                                                              
         CH    R2,=H'10'           10 max                                       
         BNL   SENSAME                                                          
GETTNO1  VCALL PGETNEW                                                          
         BNZ   TRUBBLE                                                          
         LA    R2,@R2+1            One more                                     
         ST    R2,CPMDLN           Store                                        
         SLL   R2,2                Point at place to store page no              
         ST    R0,CPMDAD-4(R2)     Store page no                                
SENSAME  LTR   PAR,PAR                                                          
         XEXIT R15,R8                                                           
TRUBBLE  IF    (R2,Z),SENSAME      No page yet                                  
         SLL   R2,2                                                             
         L     R0,CPMDAD-4(R2)     Last page no                                 
         PGET  PAR,(R0)                                                         
         B     SENSAME                                                          
*                                                                               
         QLTORG                                                                 
         TITLE 'Get next line for display'                                      
*                                                                               
*    This routine is called by the DSPDIR subroutine to get the                 
*    next line for display.                                                     
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DIRECTORY work area which                      
*             contains a work area used by this routine.                        
*                                                                               
*      PAR -  Points to the current page being displayed.                       
*                                                                               
*    On output:                                                                 
*      R0,R1 - describe the length and location of the retrieved                
*              line                                                             
*      R15=0   means a line was retrieved                                       
*         =NZ  means EOF (no more lines in the display).                        
*                                                                               
NEXTLINE PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHDRARGS,R6                                                      
*                                                                               
         L     R1,SHDRCPG          Make sure we have the page in core           
         SLL   R1,2                                                             
         L     R0,SHDRPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
*                                                                               
         LH    R3,@PAR                                                          
         IF    (SHDRCOFF,GE,R3),BEGIN                                           
         L     R1,SHDRCPG                                                       
         LA    R1,1(,R1)                                                        
         IF    (R1,LT,SHDRPGCT),BEGIN                                           
         ST    R1,SHDRCPG                                                       
         MVC   SHDRCOFF,=F'2'                                                   
         PFREE PAR,EMPTY                                                        
         L     R1,SHDRCPG          Get the new page into core                   
         SLL   R1,2                                                             
         L     R0,SHDRPAGS(R1)                                                  
         PGET  PAR,(R0)                                                         
         LH    R3,@PAR                                                          
         FAIL  (R3,LE,SHDRCOFF),DIRERR1,'NEXTLINE error.'                       
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R15,4                                                            
         B     NEXTDONE                                                         
         END                                                                    
         END                                                                    
*                                                                               
         L     R2,SHDRCOFF                                                      
         LA    R1,0(R2,PAR)                                                     
         LC    R0,@R1                                                           
         INCR  R1                                                               
         AR    R2,R0                                                            
         INCR  R2                                                               
         ST    R2,SHDRCOFF                                                      
         IF    (R0,Z),BEGIN                                                     
         PFREE PAR,EMPTY                                                        
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
*                                                                               
NEXTDONE LABEL                                                                  
         PRETURN (R0,R1)                                                        
         PEND  ,                                                                
         DROP  R6                                                               
         QLTORG ,                                                               
         EJECT                                                                  
         TITLE 'Release remaining display pages'                                
*                                                                               
*    This subroutine releases all pages containing the                          
*    information used by SHOW DIRECTORY.                                        
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DIRECTORY work area.                           
*                                                                               
*    The current page (in PAR) and the remainder of the                         
*    pages not yet accessed in the array are released.                          
*                                                                               
RLSPAGES PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHDRARGS,R6                                                      
         IF    (PAR,NZ),BEGIN                                                   
         PFREE PAR,EMPTY                                                        
         END                                                                    
         L     R3,SHDRCPG                                                       
         INCR  R3                                                               
         WHILE (R3,LT,SHDRPGCT),BEGIN                                           
         LR    R1,R3                                                            
         SLL   R1,2                                                             
         L     R2,SHDRPAGS(R1)                                                  
         PGET  PAR,(R2)                                                         
         PFREE PAR,EMPTY                                                        
         LA    R3,1(,R3)                                                        
         END                                                                    
         DROP  R6                                                               
         PEND  ,                                                                
         EJECT                                                                  
         TITLE 'Conditional write to terminal'                                  
*                                                                               
*    To avoid having a TSEG in the SHOW DIRECTORY code                          
*    cause a TWRITE to occur that's not under our control,                      
*    this routine is called. It will perform the write                          
*    if there are less than 120 characters left in the                          
*    CPSEG buffers.                                                             
*                                                                               
*                                                                               
*     On exit:                                                                  
*       R15=0  Normal processing occurred                                       
*          =NZ Attention signalled.                                             
*                                                                               
CTWRITE  PROC  ,                                                                
         L     R3,CPSEGLENF                                                     
         LA    R3,@R3+120                                                       
         IF    (R3,GE,CPSEGMAXF),BEGIN                                          
         PNUM  PAR                                                              
         XPUSH R0                                                               
         TWRITE ,                                                               
         IF    NZ,BEGIN                                                         
         XPOP  R0                                                               
         PGET  PAR,(R0)                                                         
         LA    R15,4                                                            
         END                                                                    
         ELSE  BEGIN                                                            
         XPOP  R0                                                               
         PGET  PAR,(R0)                                                         
         CLEAR R15                                                              
         END                                                                    
         END                                                                    
         ELSE  BEGIN                                                            
         CLEAR R15                                                              
         END                                                                    
         PEND  ,                                                                
         EJECT                                                                  
         TITLE 'Handle scratch option of SHOW DIRECTORY'                        
*                                                                               
*   Prompt the user to see if it's okay to scratch the indicated                
*   member and scratch it if so.                                                
*                                                                               
*   On entry:                                                                   
*      R1 - Points to the line containing the member name                       
*      R2 - Points to the SHOW DIRECTORY work area                              
*                                                                               
*   On output:                                                                  
*      R15=0   If user wanted member scratched, member scratched,               
*              else member not scratched.                                       
*      R15>0   Some error occurred                                              
*      R15<0   User pressed attention                                           
*                                                                               
SCRMBR   PROC  FFINFO                                                           
         LR    R6,R2                                                            
         USING SHDRARGS,R6                                                      
         LA    R0,8                                                             
         VCALL RTRIM                                                            
         LR    R3,R0               Save name information                        
         LR    R4,R1                                                            
         SETMSG 'Scratch'                                                       
         VCALL YESRTN                                                           
         BM    SCRATTN                                                          
         BNZ   SCRABORT            OK if we want to skip it                     
*                                                                               
         XPUSH R3,R4                                                            
         LA    R2,FFINFO           Save the current                             
         LA    R3,L'FFINFO             FFINFO data                              
         L     R4,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         L     R5,CPFINFOP         Get the FFINFO                               
         WITH  (FFINFO,R5),BEGIN                                                
         CLEAR FFOPTSL             No options                                   
         L     R0,FFRNAMEL                                                      
         LA    R1,FFRNAME                                                       
         AR    R1,R0                                                            
         MVI   0(R1),C'('                                                       
         INCR  R1                                                               
         XPOP  R2,R3               Recover member name info                     
         MOVE  R2,@R1,@R3                                                       
         INCR  R2                                                               
         AR    R1,R2                                                            
         MVI   0(R1),C')'                                                       
         INCR  R1                                                               
         LA    R2,FFRNAME                                                       
         SR    R1,R2                                                            
         ST    R1,FFRNAMEL                                                      
         ST    R1,FFNAMEL                                                       
         MOVE  R1,FFNAME,FFRNAME                                                
         VCALL WGSCRTCH            Scratch the member                           
         IF    NZ,BEGIN                                                         
         IF    (CPERRID,EQ,'NOTYOURS'),BEGIN                                    
         CLEAR SHDRSCR             Don't scratch any more                       
         END                                                                    
         CLEAR CPERRID                                                          
         END                                                                    
         TCCR                                                                   
         ACALL CTWRITE             Write out the message                        
         END                                                                    
*                                                                               
         LA    R4,FFINFO           Restore the original                         
         LA    R3,L'FFINFO             FFINFO data                              
         L     R2,CPFINFOP                                                      
         LR    R5,R3                                                            
         MVCL  R2,R4                                                            
         B     SCRMBOK                                                          
*                                                                               
SCRABORT LABEL                                                                  
         LA    R15,4                                                            
         B     SCRMBDON                                                         
*                                                                               
SCRATTN  LABEL                                                                  
         L     R15,=F'-4'                                                       
         B     SCRMBDON                                                         
*                                                                               
SCRMBOK  CLEAR R15                                                              
         B     SCRMBDON                                                         
*                                                                               
SCRMBDON LABEL                                                                  
         PEND  ,                                                                
         EJECT                                                                  
         QLTORG                                                                 
         TITLE 'Output for SHOW DIR'                                            
*                                                                               
*    On entry:                                                                  
*      R1  -  Points to the SHOW DIRECTORY work area,                           
*             which contains the page array and options                         
*                                                                               
*             The first halfword on each page is the total                      
*             number of bytes of information on that page.                      
*             Each line on the page is preceded by a byte                       
*             which indicates the line length. A zero byte                      
*             indicates all done.                                               
*                                                                               
*    Note: This routine does not return to its caller.                          
*                                                                               
DSPDIR   PROC  ,                                                                
         LR    R6,R1                                                            
         USING SHDRARGS,R6                                                      
*                                                                               
         L     R0,SHDRPAGS                                                      
         PGET  PAR,(R0)                                                         
         CLEAR SHDRCPG                                                          
         MVC   SHDRCOFF,=F'2'                                                   
*                                                                               
         TSEG  'Directory of '                                                  
         L     R5,CPFINFOP                                                      
         WITH  (FFINFO,R5),BEGIN                                                
         TSEG  FFRNAME,L:FFRNAMEL,CR                                            
         END                                                                    
         TCR                                                                    
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPDONE'                                                   
*                                                                               
         IF    SHDRALL,BEGIN                                                    
         IF    SHDROLD,BEGIN                                                    
         SETMSG 'Member     Creation Date  Creator   Title'                     
         END                                                                    
         ELSE  BEGIN                                                            
         LA    R1,SHDRARGS                                                      
         ACALL NEXTLINE            Get the column heading line                  
         IF    NZ,'B DSPDONE'                                                   
         END                                                                    
         TSEG  (R1),(R0),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPDONE'                                                   
*                                                                               
         LA    R1,SHDRARGS         Get 1st member line (if any)                 
         ACALL NEXTLINE                                                         
         WHILE (R15,Z),BEGIN                                                    
         LR    R3,R0               Save "previous" line                         
         LR    R4,R1                                                            
         LA    R1,SHDRARGS         Get next member or blocks line               
         ACALL NEXTLINE                                                         
         IF    (R15,Z),BEGIN       Print previous line                          
         XPUSH R15,R1                                                           
         TSEG  (R4),(R3),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,BEGIN                                                         
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         IF    SHDRSCR,BEGIN                                                    
         LR    R1,R4                                                            
         LA    R2,SHDRARGS                                                      
         ACALL SCRMBR                                                           
         IF    M,BEGIN                                                          
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         END                                                                    
         XPOP  R15,R1                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         LR    R0,R3               Get the directory blocks line                
         LR    R1,R4                                                            
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         ELSEIF SHDRHEX,BEGIN                                                   
         LA    R1,SHDRARGS                                                      
         ACALL NEXTLINE            Get the column heading line                  
         IF    Z,BEGIN                                                          
         TSEG  (R1),(R0),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPDONE'                                                   
         END                                                                    
         ELSE  'B DSPDONE'         This shouldn't happen                        
*                                                                               
         LA    R1,SHDRARGS         Get 1st member line (if any)                 
         ACALL NEXTLINE                                                         
         WHILE (R15,Z),BEGIN                                                    
         LR    R3,R0               Save "previous" line                         
         LR    R4,R1                                                            
         LA    R1,SHDRARGS         Get next member or blocks line               
         ACALL NEXTLINE                                                         
         IF    (R15,Z),BEGIN       Print previous line                          
         XPUSH R15,R1                                                           
         TSEG  (R4),(R3),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,BEGIN                                                         
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         IF    SHDRSCR,BEGIN                                                    
         LR    R1,R4                                                            
         LA    R2,SHDRARGS                                                      
         ACALL SCRMBR                                                           
         IF    M,BEGIN                                                          
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         END                                                                    
         XPOP  R15,R1                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         LR    R0,R3               Get the directory blocks line                
         LR    R1,R4                                                            
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         ELSEIF SHDRTAB,BEGIN                                                   
*                                                                               
         LA    R1,SHDRARGS         Get 1st member line (if any)                 
         ACALL NEXTLINE                                                         
         WHILE (R15,Z),BEGIN                                                    
         LR    R3,R0               Save "previous" line                         
         LR    R4,R1                                                            
         LA    R1,SHDRARGS         Get next member or blocks line               
         ACALL NEXTLINE                                                         
         IF    (R15,Z),BEGIN       Print previous line                          
         XPUSH R15,R1                                                           
         TSEG  (R4),(R3)                                                        
         L     R15,CPSEGLENF                                                    
         S     R15,CPSEGMRKF                                                    
         LA    R15,@R15+10                                                      
         LH    R0,CPSWID                                                        
         IF    CPFDUMP,'LH R0,CPLENGTH'                                         
         IF    (R15,LE,R0),'TSEG CVBLANKS,2'                                    
         ELSE  'TCR'                                                            
         ACALL CTWRITE                                                          
         IF    NZ,BEGIN                                                         
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         IF    SHDRSCR,BEGIN                                                    
         LR    R1,R4                                                            
         LA    R2,SHDRARGS                                                      
         ACALL SCRMBR                                                           
         IF    M,BEGIN                                                          
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         END                                                                    
         XPOP  R15,R1                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         LR    R0,R3               Get the directory blocks line                
         LR    R1,R4                                                            
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
         ELSE  BEGIN                                                            
         LA    R1,SHDRARGS                                                      
         ACALL NEXTLINE            Get the column heading line                  
         IF    Z,BEGIN                                                          
         TSEG  (R1),(R0),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,'B DSPDONE'                                                   
         END                                                                    
         ELSE  'B DSPDONE'         This shouldn't happen                        
*                                                                               
         LA    R1,SHDRARGS         Get 1st member line (if any)                 
         ACALL NEXTLINE                                                         
         WHILE (R15,Z),BEGIN                                                    
         LR    R3,R0               Save "previous" line                         
         LR    R4,R1                                                            
         LA    R1,SHDRARGS         Get next member or blocks line               
         ACALL NEXTLINE                                                         
         IF    (R15,Z),BEGIN       Print previous line                          
         XPUSH R15,R1                                                           
         TSEG  (R4),(R3),CR                                                     
         ACALL CTWRITE                                                          
         IF    NZ,BEGIN                                                         
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         IF    SHDRSCR,BEGIN                                                    
         LR    R1,R4                                                            
         LA    R2,SHDRARGS                                                      
         ACALL SCRMBR                                                           
         IF    M,BEGIN                                                          
         XPOP  R15,R1                                                           
         B     DSPDONE                                                          
         END                                                                    
         END                                                                    
         XPOP  R15,R1                                                           
         END                                                                    
         ELSE  BEGIN                                                            
         LR    R0,R3               Get the directory blocks line                
         LR    R1,R4                                                            
         END                                                                    
         END                                                                    
         END                                                                    
*                                                                               
*  Print the directory blocks line if the user asked                            
*  for it                                                                       
*                                                                               
         IF    SHDRBLK,BEGIN                                                    
         TSEG  (R1),(R0),CR                                                     
         END                                                                    
*                                                                               
DSPDONE  LABEL                                                                  
         LA    R1,SHDRARGS                                                      
         ACALL RLSPAGES                                                         
         B     CVNEXT                                                           
         PEND  ,                                                                
*                                                                               
         QLTORG                                                                 
         DROP  R6                                                               
         EJECT                                                                  
         TITLE 'DYNAMIC ALLOCATION - VARY COMMAND'                              
         PUSH  DSECTS                                                           
VOLDSECT VOLUME DSECT=YES                                                       
VOLENTSZ EQU   *-VOLDSECT                                                       
VARFLG1  DC    X'00'  -             Reset VFLG1 flags                           
VARFLG2  DC    X'00'               -             Request flags                  
VARFLG3  DC    X'00'                                                            
*                                                                               
VRF2ON   EQU   X'80'  -             Vary online request                         
VRF2OFF  EQU   X'40'  -             Vary offline request                        
VRF2SLOT EQU   X'20'  -             Used spare slot for volume                  
VRF2MNT  EQU   X'08'                Request DYNALLOC to mount vol               
*                                                                               
         DS    0F  -                To fullword boundary                        
*                                                                               
VOLDSSZ  EQU   *-VOLDSECT                                                       
         POP   DSECTS                                                           
VARY     GENTER DSAVSIZ+VOLDSSZ                                                 
         IF    (CPSFOPR+CPSFSPR,Z),CVNVALID  Priv'd                             
*                                                                               
VARYOK   CLEAR (CPDSNWA,DSAVSIZ)   Clear area                                   
         LA    R6,DSAVSIZ(0,R8)    Point at vol entry area                      
         USING VOLDSECT,R6         ##                                           
         CLEAR (VOLDSECT,VOLENTSZ+4)                                            
         OSCAN ,                   Get volume name                              
         BZ    CVABSENT            BR if missing                                
         BM    CVNVALID            BR if invalid                                
         CH    R0,=AL2(L'VNAME)    Is it too big?                               
         BH    CVNVALID            BR if so                                     
         MVC   VNAME,CVBLANKS      Move in blanks                               
         LR    R15,R0              Copy length                                  
         DEX   R15,VOLMVC          Move in vol name                             
         LA    R1,VNAME            Point at volume name                         
         DROP  VOLDSECT            ####                                         
*                                                                               
         L     R3,CVVOLIST         Get ptr to volume table                      
         USING VOLDSECT,R3         ##                                           
VARYCOMP CLI   VOLDSECT,X'FF'      End of list?                                 
         BE    VNOTFND             Yes, go look for empty slot                  
         CLC   VNAME,@R1           Same volume?                                 
         BE    VARYVOL             Branch if so                                 
         LA    R3,VOLENTSZ(,R3)    Point to next entry                          
         B     VARYCOMP            Otherwise keep looking                       
*                                                                               
         DROP  R3                  ## VOLDSECT                                  
         USING VOLDSECT,R6         ## back to temp volentry                     
VOLMVC   MVC   VNAME(0),@R1        ** EXECUTED **                               
*                                                                               
VARYVOL  WITH  (VOLDSECT,R3),'CLEAR VFLG1.VF1PRIV'  Reset priv                  
         ST    R3,VOLWRITS         Save address of real in temp                 
VNOTFND  SR    R3,R3               Forget address                               
*                                                                               
         SET   VARFLG1.VF1PRIV     NOPRIV, God dammit!                          
         OSCAN VARYPRT             Go get options                               
*                                                                               
         ICM   R3,7,VOLWRITS+1     Get address                                  
         BNZ   VHVSLO              BR if have slot                              
         TM    VARFLG2,VRF2OFF     Vary offline?                                
         BO    VARYNTAL            BR if we don't know about it                 
         TM    VARFLG2,VRF2ON      Vary online?                                 
         BZ    VOLNFND             BR if we didn't find it                      
*                                                                               
         DROP  R6                  ## VOLDSECT                                  
         L     R3,CVVOLIST         Get vol list addr                            
         USING VOLDSECT,R3         ##                                           
VSPRFND  CLI   VOLDSECT,X'FF'      End of table                                 
         BE    VNOSLOT             BR if so no slot                             
         CLI   VNAME,C' '          Empty slot?                                  
         BNE   VSPRNXT             BR if so                                     
         TM    VFLG2,VF2ADACT      Volume slot active?                          
         BZ    VHVSLOT             BR if not                                    
VSPRNXT  LA    R3,VOLENTSZ(0,R3)   Point at next                                
         B     VSPRFND             Loop lookin for spare                        
*                                                                               
VHVSLOT  OI    VARFLG2-VOLDSECT(R6),VRF2SLOT  Set got new slot                  
         OI    VFLG1,VF1MNTD+VF1CLR  Set mounted & clear reqst                  
         MVC   VTYPE,CVSTYPE       Set default type as save                     
         MVC   VNAME,VNAME-VOLDSECT(R6)  Move in name                           
         OI    VFLG3-VOLDSECT(R6),VF3SHOW    Display on show vols               
VHVSLO   LC    R5,VFLG1-VOLDSECT(R6)  Get flags to set                          
         LC    RAR,VARFLG1-VOLDSECT(R6)  Get flag                               
         CLEAR R1                  Clear error indicator                        
         NR    R5,RAR              Any conflicting requests?                    
         IF    NZ,'LA R1,1'        (ERROR)                                      
         LC    R4,VFLG3-VOLDSECT(R6)  Get flags to set                          
         LC    R15,VARFLG3-VOLDSECT(R6)  Get flag                               
         NR    R4,R15              Check for conflicts                          
         IF    NZ,'LA R1,1'        (ERROR)                                      
         IF    (R1,Z),VARDO                                                     
         TSEG  'Inconsistent options specified.'                                
         TM    VARFLG2-VOLDSECT(R6),VRF2SLOT                                    
         BZ    CVERROR             BR if not an empty slot                      
         LCALL VARCLEAR            Go clear entry                               
         B     CVERROR             Abort request                                
VARDO    IF    VFLG1.VF1CLR,'CLEAR VFLG2.VF2ADACT'  Gotchha                     
         TM    VFLG2,VF2ADACT      Already active?                              
         BO    VARYBSY             BR if very busy                              
         TM    VARFLG2-VOLDSECT(R6),VRF2OFF  Offline request?                   
         BZ    VARGO               BR no,don't check resident                   
         TM    VFLG1,VF1PRES       Is it premanently resident?                  
         BZ    VARGO               BR if not                                    
VARGO    CLC   VTYPE-VOLDSECT(L'VTYPE,R6),=D'0'  Type given?                    
         BE    VHVTYP              BR if yes                                    
         MVC   VTYPE,VTYPE-VOLDSECT(R6)  Move in type                           
VHVTYP   LA    R15,255             Get X'FF'                                    
         LC    RAR,VARFLG1-VOLDSECT(R6)   Get reset value                       
         XR    R15,RAR             Get complement                               
         IC    RAR,VFLG1           Get flag                                     
         NR    RAR,R15             Reset flags                                  
         STC   RAR,VFLG1           Put flags back                               
         OC    VFLG1,VFLG1-VOLDSECT(R6)  Set any flags                          
         LA    R15,255             Repeat for other flag                        
         LC    RAR,VARFLG3-VOLDSECT(R6)                                         
         XR    R15,RAR                                                          
         IC    RAR,VFLG3                                                        
         NR    RAR,R15                                                          
         STC   RAR,VFLG3                                                        
         OC    VFLG3,VFLG3-VOLDSECT(R6)                                         
         TM    VARFLG2-VOLDSECT(R6),VRF2OFF  Make it offline?                   
         BZ    VONLIN              BR if not                                    
*                                                                               
         IF    VFLG1.VF1CLR,'CLEAR VFLG1.VF1PDA'  Gotcha                        
         TM    VFLG1,VF1PDA        Pending deallo?                              
         BO    VISOFF              BR if so                                     
         TM    VFLG1,VF1ON         Is it online?                                
         BZ    VARYNON             BR if not online                             
*                                                                               
*                                                                               
VOFFLIN  NI    VFLG1,255-VF1ON     No longer online                             
         LH    R4,VOLUSECT         Get use count                                
         IF    VFLG1.VF1CLR,'CLEAR R4,VOLUSECT'  Gotcha                         
         LTR   R4,R4               Is it positive?                              
         BP    VISOFF              BR if so, let it dry out                     
         MVC   DSNON,CVBLANKS                                                   
         MVC   DSNON6,VNAME        Set vol name                                 
         CLC   VOLUCBAD+2(2),=D'0' Is it already offline?                       
         BE    VISOFF              BR if so                                     
         SET   VFLG1.VF1AVAIL      Set available                                
         OI    VFLG2,VF2DALLO      Request deallo                               
         DOPEN CVONE,CVONE,VALO    Open for allocation/deallo                   
         IF    NZ,'VCALL NTGDOPN'  If failure, error                            
*                                                                               
VISOFF   NI    VFLG1,255-VF1MNTD   No longer mounted                            
         TSEG  'Volume',,B         Set volume                                   
         LA    R0,L'VNAME          Get length of name                           
         LA    R1,VNAME-VOLDSECT(R6)  Point at name                             
         TSEG  (R1),(R0),B         Out with it                                  
         LTR   R4,R4               Pending offline?                             
         BZ    VOFPDN              BR if not                                    
         SET   VFLG1.VF1PDA        Set pending offline                          
         TSEG  'Pending',,B        Tell oper its drying out                     
VOFPDN   TSEG  'Offline'           Set offline                                  
         LTR   R4,R4               Pending off ?                                
         BP    CVNEXT              BR if so                                     
         LCALL VARCLEAR            Go check/clear entry                         
         B     CVNEXT              Go get new command                           
*                                                                               
VONLIN   TM    VARFLG2-VOLDSECT(R6),VRF2ON  Vary online?                        
         BZ    VAROND              BR if not                                    
         TM    VFLG1,VF1ON         Already online?                              
         BO    VAROND              BR if so all done.                           
         TM    VARFLG1-VOLDSECT(R6),VF1AVAIL  To be unavail?                    
         BO    VAROND              BR if not                                    
         OI    VFLG1,VF1AVAIL      Set available                                
         MVC   DSNON,CVBLANKS                                                   
         MVC   DSNON6,VNAME        Set volume name                              
         SET   VFLG2.VF2ALLO       Allocate the volume please                   
         TM    VARFLG2-VOLDSECT(R6),VRF2MNT  Mount?                             
         IF    O,'SET VFLG2.VF2MOUNT'  Set mount req                            
         DOPEN CVONE,CVONE,VALO    Open for allocation                          
         IF    NZ,BEGIN            If error, clean up                           
         NI    VFLG1,255-VF1ON     Reset access                                 
         LCALL VARCLEAR            Go clear out if necc                         
         VCALL NTGDOPN             Go tell oper what went wrong                 
         END   ,                                                                
*                                                                               
VARISON  OI    VFLG1,VF1ON         Turn it online                               
         LH    R4,VOLUSECT         Get use count                                
         DECR  R4                  Decrement one                                
         STH   R4,VOLUSECT         Resave use count                             
*                                                                               
VAROND   TSEG  'Volume '                                                        
         TSEG  VNAME,L'VNAME,B     Out with name                                
         IF    VFLG1.VF1PRES,'TSEG "Resident "'                                 
         IF    VFLG1.VF1AVAIL,'TSEG "Access "'                                  
         IF    VFLG1.VF1PRIV,'TSEG "Privileged "'                               
         IF    VFLG1.VF1PDA,'TSEG "Pending deallocation "'                      
         IF    VFLG1.VF1CLR,'TSEG "Clear "'                                     
         IF    VFLG1.VF1AJCL,'TSEG "Allocated via JCL "'                        
         IF    VFLG1.VF1MNTD,'TSEG "Mounted "'                                  
         IF    VFLG1.VF1ON,'TSEG "Online "'                                     
         IF    VFLG3.VF3SHOW,'TSEG "Show "'                                     
         B     CVNEXT                                                           
*                                                                               
VARCLEAR TM    VFLG1,VF1CLR        Entry to be cleared?                         
         BZR   RAR                 BR if not                                    
         CLEAR (VOLDSECT,VOLENTSZ) Clear the entry                              
         MVC   VNAME,CVBLANKS      Blank out name                               
         MVC   VTYPE,CVSTYPE       Clear type                                   
         BR    RAR                 Return to caller                             
         DROP  R3                  ## VOLDSECT                                  
*                                                                               
         USING VOLDSECT,R6         ##                                           
*                                                                               
VARYON   OI    VARFLG2,VRF2ON      Set to turn online                           
         BR    RAR                 Scan on                                      
*                                                                               
VARYMNT  OI    VARFLG2,VRF2MNT+VRF2ON  Mount implies online                     
         BR    RAR                 Onward                                       
VARYOFF  OI    VARFLG2,VRF2OFF     Turn offline                                 
*                                  Will force deallocation                      
         BR    RAR                 Scan on                                      
*                                                                               
VARNOACC OI    VARFLG1,VF1AVAIL    Set so reset avail flag                      
         BR    RAR                                                              
*                                                                               
VARACC   OI    VFLG1,VF1AVAIL      Mark available                               
         BR    RAR                 Onward                                       
*                                                                               
VARRES   OI    VFLG1,VF1PRES       Set resident                                 
         BR    RAR                                                              
*                                                                               
VARNORES OI    VARFLG1,VF1PRES     Set so reset resident flag                   
         BR    RAR                                                              
*                                                                               
VARSAV   MVC   VTYPE,CVSTYPE       Set use & save type                          
         BR    RAR                                                              
*                                                                               
VARUSE   MVC   VTYPE,CVUTYPE       Set use only volume                          
         BR    RAR                                                              
*                                                                               
VARTMP   MVC   VTYPE,CVTTYPE       Set scratch type                             
         BR    RAR                                                              
*                                                                               
VARCLR   OI    VFLG1,VF1CLR        Clear at deallocation                        
         BR    RAR                                                              
*                                                                               
VARNOCLR OI    VARFLG1,VF1CLR      Set so reset clear req                       
         BR    RAR                                                              
*                                                                               
VARPRV   OI    VFLG1,VF1PRIV       Set for priv access                          
         NI    VARFLG1,255-VF1PRIV                                              
         BR    RAR                 Onward                                       
*                                                                               
VARNOPRV OI    VARFLG1,VF1PRIV     Set so reset priv flg                        
         BR    RAR                                                              
*                                                                               
VARSHOW  OI    VFLG3,VF3SHOW                                                    
         BR    RAR                                                              
*                                                                               
VARNSHOW OI    VARFLG3,VF3SHOW                                                  
         BR    RAR                                                              
*                                                                               
VARYNTAL NI    VFLG1,255-VF1AVAIL-VF1PDA-VF1CLR                                 
         TSEG  'Volume',,B                                                      
         TSEG  VNAME,L'VNAME,B                                                  
         TSEG  'not currently allocated by WYLBUR',,WR                          
         B     CVERROR                                                          
*                                                                               
VARYNON  TSEG  'Volume not online',,B                                           
         B     CVERROR                                                          
*                                                                               
VARYBSY  TSEG  'Volume',,B         Set volume                                   
         TSEG  'Busy'              Is busy                                      
         B     CVERROR             Go exclaim to caller                         
*                                                                               
VOLNFND  TSEG  'Volume not found',,B                                            
         B     CVERROR                                                          
*                                                                               
VNOSLOT  TSEG  'No room for volume'                                             
         B     CVERROR                                                          
         SPACE 2                                                                
VARYPRT  DS    0X                                                               
         OSCKW ONLINE,VARYON,A     Vary volume online                           
         OSCKW ON,VARYON                                                        
         OSCKW OFFLINE,VARYOFF,A                                                
         OSCKW ACCESS,VARACC,A     Allow access                                 
         OSCKW NOACCESS,VARNOACC,A Disallow access                              
         OSCKW RESIDENT,VARRES,A   Make resident volume                         
         OSCKW NORESIDENT,VARNORES,A                                            
         OSCKW NONRESIDENT,VARNORES,A  Make non resident                        
         OSCKW SAVE,VARSAV,A       Make a use & save vol                        
         OSCKW USE,VARUSE,A        Make into useonly vol                        
         OSCKW TEMPORARY,VARTMP,A                                               
         OSCKW SCRATCH,VARTMP,A    Make into a temp use/save vol                
         OSCKW NOCLEAR,VARNOCLR,A                                               
         OSCKW CLEAR,VARCLR,A      Clear slot after deallocation                
         OSCKW CLR,VARCLR                                                       
         OSCKW PRIVILEGE,VARPRV,A  Make access privileged                       
         OSCKW NOPRIVILEGE,VARNOPRV,A                                           
         OSCKW SHOW,VARSHOW,A                                                   
         OSCKW NOSHOW,VARNSHOW,A                                                
         OSCKW MOUNT,VARYMNT,A                                                  
         OSCKW ,V(INVALID)                                                      
         DROP  R6,BR                                                            
*                                                                               
         VLTORG                                                                 
         END   .                                                                
