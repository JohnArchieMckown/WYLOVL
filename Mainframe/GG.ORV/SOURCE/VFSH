VFSH     TITLE 'ORVYL''S FILE SYSTEM SHOW COMMANDS'
********************************************************************
*                                                                  *
*       ORVYL/370  RELEASE III - CLASS I - STANFORD PROPRIETARY    *
*                                                                  *
********************************************************************
         SPACE 2
         HIBAL ,
         SPACE 2
* MODIFICATIONS
*
         EJECT ,
VFSH     CSECT
         IDENT 00
         SPACE 2
         COPY  ORVDEFN
         SPACE 3
         REGS  ,,SR,TR,QR,FSUBR,BRR,BR,WAR,RAR,CLWR,,GPWR
         EJECT ,
         MACRO ,
&LABEL   LSCAN &GETADC,&SAME       SO WE CAN PUT SCKWS AT END
&LABEL   L     R1,=A(&GETADC)
         SCAN  (1),&SAME
         MEND
*
         MACRO
&L       BTD   &LOC,&LEN,&WORD,&SW,&COMMAS=NO
         LCLC  &CM
         AIF   ('&COMMAS' NE 'YES').NOCM
&CM      SETC  'C'
.NOCM    ANOP
&L       SYSLR 15,&WORD,OP=L
         SYSLR 0,&LEN,TYPE=&SW
         SYSLR 1,&LOC
         XCALL BTD&CM
         MEND
*
         MACRO
&L       BTX   &LOC,&LEN,&WORD
&L       SYSLR 15,&WORD,OP=L
         SYSLR 0,&LEN
         SYSLR 1,&LOC
         XCALL BTX
         MEND
         EJECT ,
*GEN
         COPY  CLW
         EJECT ,
         COPY  WYLMDWD
         EJECT ,
         COPY  GPW
         EJECT ,
         COPY  PRGCAT
         SPACE 3
         COPY  DENTRY
         SPACE 3
         COPY  DIRB
         EJECT ,
         SPACE ,
WA       DSECT
         VSA   2,11
WAFL     DS    X                       SPECIAL FLAGS
WAFSHFER EQU   WAFL,X'80',1            SHOW FILES ERASE IN PROGRESS
WAFSHFBL EQU   WAFL,X'40',1            SHOW FILES BLOCKS IN PROGRESS
WAFSPA   EQU   WAFL,X'20',1            SHO PER ACC IN PROGRESS
*
WADEVFL  DS    X
WADEVPR  DS    X
WADEVNM  DS    44X
WADEVINF EQU   WADEVFL,*-WADEVFL,C'X'
*
         ORG   WADEVFL
WASAVESR DS    F                       SAVE FOR SR
WASAVETR DS    F                       SAVE FOR TR
         ORG   ,
WASIZE   EQU   *-WA
         EJECT ,
*NOGEN
VFSH     CSECT ,
         USING CLW,CLWR                ##
         USING WA,WAR                  ##
FILESHOW VENTER  2,11,WASIZE
         LM    BRR,BR,=A(BASE+4096,BASE)
         USING BASE,BR             ##  ##
         USING BASE+4096,BRR       ##  ##
         LR    SR,R0                   SAVE KEYWORD LENGTH
         LR    TR,15                   SAVE ENTRY CODE
         CLEAR WAFL                    SET SPECIAL FLAGS TO 0
         STM   BRR,WAR,CLWQRET         QUICK EXIT SAVE
         FSCALL FILSETUP
         B     SETUPRTN
BASE     EQU   *
*
* EXIT BRANCH TABLE
         B     EXIT                    NORMAL EXIT
         B     TYPERR                  ERROR MESSAGE EXIT
*
EXIT     LABEL ,
         IF    WAFSHFER,SHFERRET       PROCESSING SHOW FILES ERASE?
         IF    WAFSHFBL,SHFBLRET       PROCESSING SHOW FILES BLOCKS ?
         LR    R15,SR                  RETURN CODE
         LR    0,TR
         VEXIT 2,11,LTR
         LTORG
*
TYPERR   VCALL TYPEIT
         CLEAR WAFSHFER
         CLEAR WAFSHFBL
         B     FILEXIT                 DROP FILES
         EJECT ,
         USING GPW,GPWR                ##
SETUPRTN LABEL ,
*GEN
         VFSHTAB FSHTAB,TR               BRANCH ON CODE
*NOGEN
         EJECT ,
* SHOW CLP STATUS FOR FILE
*
SHCLP    LABEL ,
         FSCALL GETNAME                GET FILENAME ROOT
         LSCAN USRGPTBL,CLWKWCB        LOOK FOR USER OR GROUP
         BNZ   SCRET                   ERROR IN SCAN
         FSCALL ASMBLNAM               GET FILENAME
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                ATTACH FILE
         FSCALL SEGNAME                WRITE FILENAME
         SEG   'may '                  BUILD PROPER MESSAGE
         FSCALL DOSENCLP               CHECK FOR CLP/NOCLP
         IF    NZ,'SEG  "not "'        NO CLP SET
         SEG   'be changed by CLP'     FINISH OUT RESPONSE
         B     WRITEXIT
         EJECT ,
SHFILEMA FSCALL GETNAME
         LSCAN USRGPTBL,CLWKWCB
         FSCALL ASMBLNAM               PUT NAME TOGETHER
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                ATTACH THE FILE
         FSCALL SEGNAME
         SEG   'Filemask: '
         FSCALL SENUPRV                GET FILEMASK,PUB PRIVS
         STC   R1,GPWPERM              SET FILE MASK
         CLEAR GPWPRMEX                NO EXTEND
         LCALL SEGPRIV
         B     FILEXIT
         EJECT ,
* SHOW SPIRES ONLY STATUS FOR FILE
*
SHSPIFIL LABEL ,
         FSCALL GETNAME                GET FILENAME ROOT
         LSCAN USRGPTBL,CLWKWCB        LOOK FOR USER OR GROUP
         BNZ   SCRET                   ERROR IN SCAN
         FSCALL ASMBLNAM               GET FILENAME
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                ATTACH FILE
         FSCALL SEGNAME                WRITE FILENAME
         SEG   'accessible by '        BUILD PROPER MESSAGE
         FSCALL DOSENSPI               CHECK FOR SPIRES ONLY
         IF    Z,'SEG  "any program"'  ITS ALL
         ELSE  'SEG  "Spires-only"'    OR NOTHING
         B     WRITEXIT
         EJECT ,
*  SHOW FILES
*
SHFILES  LABEL ,
         LSCAN SHFILTBL,CLWKWCB
         BNZ   SCRET                   ERROR IN SCAN
         IF    GPWSFATT,SHFILATT       ATTACHED OPTION?
         SEG   'Orvyl File System'
         VWRITE ,
         BNZ   FILEXIT                 QUIT IF ATTN
         IF    (GPWFNL,EQ,0),BEGIN    ANY FILE NAME SPECIFIED?
         SETMSG ' '                    SET DUMMY FILE NAME
         FSCALL GOTNAME                INIT FILE CONTROL BLOCK
         END   ,
         SPACE ,
         FSCALL ASMBLNAM               PUT FILENAME TOGETHER
         IF    (GPWSFLK+GPWSFUNL+GPWSFFRO,NZ),BEGIN   NAME COMPARE?
         MVC   GPWAFN,GPWFGRP          SAVE FILE NAME
         LC    R15,GPWFNL              LENGTH
         SH    R15,=Y(4)               DELETE ORV.
         STC   R15,GPWAFNL
         IF    GPWSFUNL,BEGIN          UNLIKE OPTION?
         MVI   GPWFNAM,C' '            SHO ALL FILES (ALMOST)
         MVC   GPWFNAM+1(L'GPWFNAM),GPWFNAM
         END   ,
         END   ,
SHFSEN   FSCALL SENFILE                GET FILES
         LTR   TR,R1                   ANY?
         BZ    SHFEXIT                 NO, TAKE NO ERROR EXIT
         L     SR,GPWBUFA              POINT TO BUFFER
         SL    SR,=F'56'               BACK OFF ONE FILE NAME
FILELOOP LA    SR,@SR+56               TO NEXT FILE
         MVC   GPWFILN(L'GPWFILN),@SR
         LA    QR,L'GPWFILN
         LA    R1,GPWFILN+L'GPWFILN-1  END OF NAME
SHFFNSCN CLI   @R1,C' '
         DECR  R1
         BNE   SHFFNSC1
         BCT   QR,SHFFNSCN
SHFFNSC1 STC   QR,GPWFNL
         EJECT ,
* SHOW FILES - LIKE AND UNLIKE OPTIONS
*
         IF    (GPWSFLK+GPWSFUNL,Z),SHFCONT  LIKE OR UNLIKE?
         LC    QR,GPWAFNL              LEN
         DECR  QR
         IF    ^GPWSFLK,NOTLIKE        LIKE?
         EX    QR,SHFCLC               COMPARE
         BH    FILEXIT                 ALL DONE IF BEYOND RANGE
         BNE   SHFNEXT                 NOT THIS ONE
         B     SHFCONT                 CONTINUE IF ON TARGET
*
NOTLIKE  EX    QR,SHFCLC               COMPARE
         BE    SHFNEXT                 NOT THIS ONE
SHFCONT  LABEL ,
         EJECT ,
* SHOW FILES - BLOCKS AND FORMAT OPTIONS
*
*
         FSCALL SEGNAME                FORMAT FILE NAME
         IF    GPWSFBLO+GPWSFFMT,BEGIN      BLOCKS OR FORMAT?
         CLEAR GPWFFREE                DON'T FREE WORK PAGE
         SET   WAFSHFBL                RETURN TO SHOW FILES
         SET   GPWFERS                 SET FLAG FOR ERASE
         STM   SR,TR,WASAVESR          SAVE SR AND TR
         FSCALL ATTOLDX                ATTACH FILE
         IF    GPWSFBLO,'LCALL SEGBLKS'      FORMAT BLOCKS
         IF    GPWSFFMT,'LCALL SEGFMT'       SET FORMAT INFO
         CLEAR GPWFERS                 OK TO TRY ERASE
SHFBLRET CLEAR WAFSHFBL                UNDO  FLAGS
         SET   GPWFFREE
         IF    GPWFATT,'FSCALL DETACH' DETACH FILE IN CASE OF ERASE
         LM    SR,TR,WASAVESR          RESTORE SR AND TR
         IF    GPWFERS,SHFNEXT         SKIP IF DETACH BLEW
         END   ,
         SPACE ,
* SHOW FILES - DATE OPTION
*
         IF    GPWSFDT,BEGIN          DATE OPTION SPECIFIED?
         SEG   '-- Cr'
         LA    R1,@SR+L'GPWFN          POINT TO CREATE DATE
         LCALL SEGDATE                 FORMAT
         SEG   '  La'
         LA    R1,@SR+L'GPWFN+4        POINT TO LAST REFERENCE DATE
         LCALL SEGDATE                 FORMAT
         SEG   ' '                    PRETTY
         END   ,
         IF    (GPWSFBLO,AND,GPWFHYBR),BEGIN HYBRID FILE TYPE
         L     R15,GPWHCNT             GET COUNT OF IXRS
         SEG   '(Hybrid '
         BTD   CLWTEMP,0,(R15)         CONVERT TOTAL
         SEG   (1),(0)
         SEG   ') '
         END   ,
         IF    GPWSFER,SHFASKER        ASK USER IF ERASE OPTION
         VWRITE ,                      SEND FILE
         BNZ   FILEXIT                 DONE IF ATTENTION HIT
SHFNEXT  LABEL ,
         BCT   TR,FILELOOP             CONTINUE
         LC    QR,GPWFNL               LEN
         CH    QR,=Y(L'GPWFN)          MAX?
         LA    QR,GPWFILN(QR)          POINT TO END
         BNL   SHFSENX                 DUPLICATE IF SO
         MVI   @QR,C'.'                PREVENT DUPLICATION
         B     SHFSEN                  GET NEXT BUFFER
SHFSENX  LABEL ,
         MVI   @QR,C' '                PREVENT ERRORS
         FSCALL SENFILE                GET FILES
         L     SR,GPWBUFA              POINT TO BUFFER
         LR    TR,R1
         BCT   TR,FILELOOP             JUMP IF ANY
         B     SHFEXIT                 EXIT
         EJECT ,
* SHOW FILES - ERASE OPTION
*
SHFASKER LABEL ,
         CLEAR GPWFERS                 CLEAR FLAG BEFORE QUERY
         VMARK ,                       MARK START OF PROMPT
         SEG   'Erase'
         FSCALL RDERASE                DOES HE REALLY WANT TO ERASE?
         IF    ^GPWFERS,SHFNEXT        PERMISSION?
         SET   WAFSHFER                RETURN TO SHOW FILES WHEN DONE
         CLEAR GPWFFREE                DON'T FREE WORK PAGE
         STM   SR,TR,WASAVESR          SAVE SR AND TR
         FSCALL ATTOLDE                GET FILE
         IF    GPWUILIB,BEGIN
         FSCALL SEGNAME
         SEG   'is a library'
         B     WRITEXIT
         END   ,
         FSCALL DOERASE                ZOT THE FILE
         CLEAR GPWFATT                 ERASE DETACHES FILE
         FSCALL SEGNAME                FORMAT
         SEG   'Erased '               ANSWER
         B     VFMSG                   WILL RETURN EVENTUALLY
*
SHFERRET CLEAR WAFSHFER
         SET   GPWFFREE                DROP WORK PAGE ON EXIT
         LM    SR,TR,WASAVESR          RESTORE SR AND TR
         B     SHFNEXT
*
SHFCLC   CLC   GPWFGRP(*-*),GPWAFN
*
SHFEXIT  CLEAR CLWFCERR                TURN OFF ERRORS
         B     FILEXIT                  AND TAKE LEAVE
         EJECT ,
* SHOW FILES - ATTACHED OPTION
*
SHFILATT LABEL ,
         IF    GPWSFER+GPWSFDT+GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC
         SEG   'Attached Orvyl Files:'
         VWRITE ,
         BNZ   FILEXIT                 QUIT IF ATTENTION
         LA    SR,1
         LOOP  BEGIN
         CLPSERV DEVSCAN,(SR),8        SCAN for file devices
         IF    NZ,EXIT                 All done
         LR    SR,R1                   copy device id
         BTD   CLWTEMP,2,(SR)
         SEG   (1),(0)
         SEG   '  '
         CLPSERV DEVINFO,WADEVFL,L'WADEVINF,(SR)
         SEG   WADEVNM,L'GPWFN
         IF    WADEVFL.X'80','SEG  "  Shared"'
         IF    WADEVFL.X'40','SEG  "  Reserved"'
         VWRITE ,
         IF    NZ,EXIT
         END   ,
         B     FILEXIT
         EJECT ,
*  SHOW FILE SCAN EXITS
*
SHFERASE IF    GPWSFBLO+GPWSFFMT,NOCANDO
         SET   GPWSFER                 SET ERASE
         SCRTN (15)
*
DATED    SET   GPWSFDT                 SET DATED
         SCRTN (15)
*
LIKE     IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC   ALREADY SET?
         SET   GPWSFLK                 SET LIKE
         B     LIKENAME                FIX UP AND CONTINUE SCAN
*
UNLIKE   IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC   ALREADY SET?
         SET   GPWSFUNL                SET UNLIKE
LIKENAME FSCALL GETNAME                FIND MASK
         B     SHFILES                 CONTINUE SCAN
*
SHFILFRO IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC
         SET   GPWSFFRO                SET FROM
         B     LIKENAME
*
SHFATT   SET   GPWSFATT                'ATTACHED'
         SCRTN (15)
*
BLOCKS   IF    GPWSFER,NOCANDO
         SET   GPWSFBLO                BLOCKS
         SCRTN (15)
*
FORMAT   IF    GPWSFER,NOCANDO
         SET   GPWSFFMT                FORMAT
         SCRTN (15)
*
DTBLOFMT IF    GPWSFER,NOCANDO
         SET   GPWSFDT+GPWSFBLO+GPWSFFMT     ALL
         SCRTN (15)
*
         EJECT ,
*  SHOW FILE PRIVILEGES
*
SHPERMIT LABEL ,
         LCALL CHECKFOR                CHECK ACCT OR FILENAME
         B     *+4(R15)                RETURN CODE IS IN 15
         B     SHPERACO                0  ACCT OMITTED
         B     SHPERFIL                4  FILE NAME SPECIFIED
         B     SHPERACC                8  ACCT SPECIFIED
         B     SHPERACF                12 FOR SPECIFIED
*
SHPERFIL FSCALL GETNAME                GET FILENAME
         LSCAN FORTBL2,CLWKWCB         LOOK FOR OPTIONS
         BNZ   SCRET                   NG
         B     SHPERFL2
SHPERFFR LCALL FOR                     PROCESS FOR
SHPERFL2 FSCALL ASMBLNAM               PUT NAME TOGETHER
* SHO PERMITS FOR A FILE
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                GET FILE
         FSCALL SEGNAME                FORMAT NAME
         SEG   'File Permits:'
*         LA    R1,=X'15'               INSERT CARRIAGE RETN
*         SEG   (1),1
         VWRITE ,
         FSCALL SENUPRV                GET PRIVILEGES
DOSHP    LR    TR,14                   SAVE COUNT
         LR    QR,1                    AND SOME PRIVS
         IF    GPWFPUB,DOSHPUB         JUST PUBLIC?
         LTR   TR,TR                   ANY MEMBERS?
         BZ    SHPNOMEM                NO MEMBERS
         L     SR,GPWBUFA            POINT TO MEMBERS
         MVI   GPWPERM,0               INITIALIZE
SHPLOOP  IF    ^GPWFACC,SHPGRP         GROUP SPECIFIED?
         CLC   GPWACCT(L'GG),@SR       IS IT SAME GROUP ACCOUNT
         BH    SHPNEXT                 NOT AT IT YET
         BL    SHPNONE                 DONE WITH GROUP
         CLC   GPWACCT+L'GG+1(L'UUU),L'GG+1(SR)  TEST USER
         BE    SHPGRP                  YES
         CLC   @SR+L'GG+1(L'UUU),=C'PUB'    GROUP LEVEL PERMIT
         BNE   SHPNEXT                 NO
SHPGRP   MVC   GPWPERM(1),L'ACCT(SR)   SET PRIVS
         LR    R1,SR                   POINT TO ACCOUNT
         FSCALL TSEGACC                WRITE ACCT
         LCALL SEGAPRIV                DISPLAY
SHPNEXT  LA    SR,@SR+7                TO NEXT
         BCT   TR,SHPLOOP              CONTINUE
         IF    ^GPWFACC,DOSHPUB        ACCT SPECIFIED?
SHPNONE  CLI   GPWPERM,0               HAS AN ACCOUNT BEEN FOUND?
         BNE   DOSHPUB                 YES
         LA    R1,GPWACCT               WRITE ACCT WITH NONE
         FSCALL TSEGACC
         LCALL SEGAPRIV
*
DOSHPUB  LR    SR,QR                   SAVE FILEMASK
         SRL   QR,8                    SHIFT PAST FILEMASK
         STC   QR,GPWPERM              STORE MASK IN GPWPERM
         IF    GPWFPUB,SHPPUBLC        PUB SPECIFIED?
         CLI   GPWPERM,0               NO PUBLIC PERMIT?
         BE    SHPFLMSK                YES - TRY FILEMASK
SHPPUBLC SEG   'Public- '
         LCALL SEGPRIV                 PRINT PUBLIC PERMITS
SHPFLMSK IF    WAFSPA,FILEXIT          IS THIS SHO PER ACCT?
         STC   SR,GPWPERM              PUT FILEMASK IN
         CLEAR GPWPRMEX                NO EXTEND BIT
         IF    GPWPRMWR,FILEXIT        RELEVANT?
         SEG   'Filemask allows '
         LCALL SEGPRIV                 DO FILEMASK
         B     FILEXIT
*
SHPNOMEM LH    SR,=XL2'FFF7'           MASK OFF EXTEND BIT
         NR    SR,QR                     FROM FILEMASK
         CH    SR,=XL2'0007'           ANYTHING INTERESTING?
         BNE   DOSHPUB                 YES
         B     NOMEM                   NO
         EJECT ,
* SHO PERMITS FOR ACCOUNT
*
SHPERACC FSCALL FORGROUP                GET ACCT
         MVC   GPWAACCT,GPWACCT        MOVE ACCT INTO ALT
         CLEAR GPWFACC                 AND UNSET BIT
         SET   GPWFAACC                SETTING RIGHT ONE
*
SHPERACF LSCAN FORTBL3,CLWKWCB         LOOK FOR 'FOR'
         BNZ   SCRET                   ERROR IN SCAN
         B     SHPERACO
SHPERACN LCALL FOR                     PROCESS FOR
*
SHPERACO IF    GPWFAACC,BEGIN          ACCT GIVEN
         LA    R1,GPWAACCT             PRINT SPECIFIED ACCT
         FSCALL SEGACCT
         END   ELSE,'SEG  CLWACCT,L"CLWACCT'    PRINT CALLER ACCT
         SEG   ' Account permits:'
*         LA    R1,=X'15'
*         SEG   (1),1
         VWRITE ,
         FSCALL SENMEM                 GET MEMBERS
         SET   WAFSPA                  SET SHO PER ACCT BIT
         IF    (R14,NZ),DOSHP          MEMBER - GO AHEAD
         IF    (R1,NZ),DOSHP           PUBLIC - GO AHEAD
         B     NOMEM                   NO PRIVS TO SHOW
*
         EJECT ,
*
*  SHOW ACCESS FILE <FILENAME>
SHACCESS LABEL ,
* SHOW ACC COULD BE SHOW ACCOUNT OR SHOW ACCESS
         CLC   CLWKWKW(4),=C'ACC '     JUST ACC?
         BNE   SHACCGO
         SET   GPWFACC                 INDICATE POSSIBLY ACCOUNT
*
SHACCGO  LCALL CHECKFOR                CHECK ACCT OR FILENAME
         B     *+4(15)                 RETURN CODE IN 15
         B     CKSHACCT                0  NEITHER
         B     SHACCFIL                4  FILENAME
         B     SHACCACC                8  ACCOUNT
         B     SHACCFIL                12 FOR MUST BE FILE
*
SHACCFIL FSCALL GETNAME                GET FILENAME
         LSCAN USRGPTBL,CLWKWCB        GET USER AND GROUP
         FSCALL ASMBLNAM               PUT IT TOGETHER
         CLI   GPWFNL,0                FILE NAME SPECIFIED?
         BE    MISSING                 NO
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                GET FILE
         FSCALL SEGNAME
         SEG   'Access: '
         CLPSERV DEVINFO,WADEVINF,L'WADEVINF,LC:GPWFID
         CLC   WADEVNM+4+L'GG+1(L'UUU),CLWUSER      OWN UUU?
         BNE   SHACNOWN                NO
         CLC   WADEVNM+4(L'GG),CLWGROUP             OWN GG?
         BNE   SHACNOWN                NO
         SEG   'Owner- '
SHACNOWN MVC   GPWPERM(1),WADEVPR      FETCH PRIVELEGES
         CLEAR GPWPRMEX                DON'T LIST EXTEND
         LCALL SEGPRIV                 WRITE PERMITS
         B     FILEXIT
*
SHACCACC FSCALL FORGROUP                GET ACCT
         CLC   CLWUSER(L'UUU),GPWACCT+L'GG+1   CHECK FOR
         BNE   SHACCDIF                NO - OK
         CLC   CLWGROUP(L'GG),GPWACCT          OWN ACCT
         BE    ACCTINC                 YES - ACCT INCONSISTENT
SHACCDIF MVC   GPWAACCT,GPWACCT        MOVE ACCT FOR SENSE
         SET   GPWFAACC                SET BIT APPROPRIATELY
         FSCALL SENMEM                 GET MEMBERS
         LTR   R14,14                  ANY MEMBERS?
         BZ    SHACNFND                NOPE
         L     SR,GPWBUFA              SET UP FOR LOOP
         MVC   GPWACCT(L'GG),CLWGROUP   ASSEMBLE ACCT
         MVI   GPWACCT+L'GG,C'.'
         MVC   GPWACCT+L'GG+1(L'UUU),CLWUSER
SHACACLP CLC   GPWACCT,@SR             ACCT MATCH?
         BE    SHACCFND                YES
         LA    SR,L'ACCT+@SR+1         BUMP POINTER
         BCT   R14,SHACACLP            AND DO REST
         B     SHACNFND                DIDN'T FIND ACCT
SHACCFND MVC   GPWPERM(1),@SR+L'ACCT   GET PRIVS
SHACNFND IC    0,GPWPERM               GET PERMITS FOR OR
         OR    R1,R0                   OR WITH PUBLIC PERMITS
         STC   R1,GPWPERM              STORE
         CLEAR GPWPRMEX                NO EXTEND
         LA    R1,GPWAACCT
         FSCALL SEGACCT                PRINT ACCT
         SEG   ' access:'
         LCALL SEGPRIV
         B     FILEXIT
*
CKSHACCT IF    GPWFACC,'FSCALL MILWYL'       YES, SEND TO MILTEN
         B     MISSING                 NO, MISSING ACCOUNT OR FILE NAME
         EJECT ,
*  SHOW ATTACHABILITY STATUS
*
SHSHARE  LABEL ,
         FSCALL GETNAME                GET FILE NAME
         LSCAN USRGPTBL,CLWKWCB
         FSCALL ASMBLNAM               PUT NAME TOGETHER
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                GET FILE
         FSCALL SEGNAME                PUT FILE NAME
         SEG   'attachable '
         FSCALL SENSHR                 GET STATUS
         IF    Z,'SEG  "normally"'
         ELSE  'SEG  "share only"'
         B     WRITEXIT
         EJECT ,
*  SHOW SPACE ALLOCATION
*
SHBLOCKS LABEL ,
         LCALL CHECKFOR                CHECK ACCT OR FILENAME
         B     *+4(R15)                RETURN CODE IN 15
         B     SHBLKNAC                0  NO ACCT
         B     SHBLOCKF                4  FILENAME
         B     SHBLKACC                8  ACCT
*        PROCESS FOR                   12 'FOR' FOUND
*
         LSCAN FORTBL5,CLWKWCB         LOOK FOR "FOR"
         B     SCRET                   ERROR IN SCAN
SHBLKACN LCALL FOR                     PROCESS FOR
         IF    ^GPWFPUB,SHBLKNAC       PUBLIC?
         SETMSG 'PUBLIC'               ERROR
         B     UNREC
*
SHBLKACC FSCALL FORGROUP                GET ACCT
SHBLKNAC FSCALL SENSP                  SENSE SPACE
         LR    SR,14                   SAVE SPACE USED
         LR    TR,1                    SAVE BLOCKS ALLOCATED
         IF    GPWFACC,BEGIN           GROUP SPECIFIED?
         LA    R1,GPWACCT              POINT TO ACCOUNT
         FSCALL SEGACCT                WRITE USER'S ACCOUNT
         SEG   ' - '
         END   ,
         BTD   CLWTEMP,0,(SR)          CONVERT USED BLOCKS
         SEG   (1),(0)
         SEG   ' Blocks accounted'
         LTR   TR,TR                   ANY MAXIMUM?
         BM    WRITEXIT                DONE IF NOT
         SEG   ', '
         BTD   CLWTEMP,0,(TR)          CONVERT ALLOCATION
         SEG   (1),(0)
         SEG   ' blocks allowed'
         B     WRITEXIT
         EJECT ,
*  SHOW SPACE ALLOCATION FOR FILE
*
SHBLOCKF LABEL ,
         FSCALL GETNAME                GET FILENAME
         LSCAN USRGPTBL,CLWKWCB        CHECK FOR USER OR GROUP
         BNZ   SCRET                   SCAN ERROR
         FSCALL ASMBLNAM               PUT NAME TOGETHER
         SET   GPWFNSF                 DO NOT SENSE USER INFO
         FSCALL ATTOLDX                GET FILE
         FSCALL SEGNAME
         LA    RAR,WRITEXIT            AFTER MESSAGE GENERATED
*
SEGBLKS  VPUSH RAR,,*                  SAVE RETURN
         FSCALL SENNREC                FIND NUMBER OF BLOCKS
         LTR   SR,1                    ANY BLOCKS?
         BZ    EMPTY                   NO
         SEG   '-- '                   PRETTY
         BTD   CLWTEMP,0,(SR)          CONVERT TOTAL
         SEG   (1),(0)
         SEG   ' Blks ('               MESSAGE
         IF    (GPWFNIXR,AND,^GPWFHYBR),'SEG  "Non-IXR"'
         ELSE  BEGIN
         SEG   'F: '                   DO FIRST AND LAST
         FSCALL SENFREC                GET FIRST
         BTD   CLWTEMP,0,(1)           FORMAT IT
         SEG   (1),(0)
         SEG   ', L: '
         FSCALL SENLREC                GET LAST
         BTD   CLWTEMP,0,(1)           FORMAT IT
         SEG   (1),(0)
         END   ,
         SEG   ') '
SEGBLKSE LABEL ,                       AND SEG IT
         VPOP  RAR,,*                  RESTORE RETURN
         BR    RAR
*
EMPTY    SEG   '-- No contents '
         B     SEGBLKSE
         EJECT ,
* SHOW FILE FORMAT
*
SHFORMAT LABEL ,
         FSCALL GETNAME
         LSCAN USRGPTBL,CLWKWCB
         FSCALL ASMBLNAM
         FSCALL ATTOLDX
         FSCALL SEGNAME
         LA    RAR,WRITEXIT            AFTER MESSAGE SET
*
SEGFMT   VPUSH RAR,,*
         SEG   '-- '
         FSCALL SENUINFO
         BNZ   FMTUINF                 IF NOT FMT, GIVE IT IN HEX
         IF    (GPWUIMSK,Z),FMTUSER    ANY FORMATS SET?
         IF    GPWUILIB*GPWUILDF,FMTLDFIL    LOADFILE?
         IF    NZ,'SEG  "Library, "'
         IF    GPWUIFLR,FMTLRECL       LRECL?
         IF    GPWUIFE,FMTEDIT         EDIT?
         IF    GPWUIFV,FMTSEGV         VARIABLE?
         IF    GPWUIUND,FMTUND         UNDEFINED?
         SEG   'VBS'                   ASSUME VBS
         B     FMTSEGL                 ADD LRECL
*
FMTUND   SEG   'Undefined'
         B     FMTSEGL
*
FMTLRECL LABEL ,
         LH    R15,GPWUILRC            GET LRECL
         CH    R15,=Y(80)              RIGHT LRECL FOR CARD?
         BE    FMTCARD                 BRANCH IF SO
         CH    R15,=Y(133)             HOW ABOUT PRINT?
         BE    FMTPRINT                LOOKS LIKE IT
         SEG   'Fixed'
FMTSEGL  IF    ^GPWUIDA,FMTLNDA        DIRECT ACCESS?
         SEG   '(DA)'
FMTLNDA  SEG   '/'
         BTD   CLWTEMP,0,LH:GPWUILRC   CONVERT LRECL
         SEG   (1),(0)
         B     FMTSEGE
*
FMTCARD  SEG   'Card'
         IF    (GPWUIFN+GPWUIFI,NZ),FMTLRENI   INTEGER OR NUMBERED?
         B     FMTSEGE
*
FMTLRENI LABEL ,
         IF    GPWUIFN,'SEG  ", Numbered"'      NUMBERED
         ELSE  'SEG  ", Integer"'     INTEGER
         B     FMTSEGE
*
FMTEDIT  SEG   'Edit'
         B     FMTSEGE                 DONE
*
FMTSEGV  SEG   'Variable'
         B     FMTSEGE
*
FMTPRINT SEG   'Print'
         B     FMTSEGE
*
FMTLDFIL SEG   'Loadfile'
         B     FMTSEGE
*
FMTUINF  LR   SR,R1                    SAVE FORMAT
         SEG   'User info: '
         B     FMTSHEX
*
FMTUSER  LR   SR,R1
         SEG   'User format: '
*
FMTSHEX  LR    R15,SR
         BTX   CLWTEMP,8,(15)
         SEG   (1),(0)
*
FMTSEGE  LABEL ,
         SEG   ' '
         VPOP  RAR,,*
         BR    RAR
         EJECT ,
*  SHOW PROGRAMS IN A LIBRARY
*
SHPROGRA LABEL ,
SHCONTEN LABEL ,
         LSCAN LIBROPT,CLWKWCB         GET LIBRARY NAME
         BNZ   SCRET                   BAD SCAN
         CLI   GPWFNL,0                ANY LIBRARY NAME?
         BNE   SHPRONAM                YES
         IF    GPWFPUB,SHPROPUB        PUBLIC PROGRAMS?
         SETMSG 'PROGLIB'              IF NONE, USE DEFAULT
         FSCALL GOTNAME
SHPRONAM FSCALL ASMBLNAM               PUT NAME TOGETHER
         FSCALL ATTOLDX                GET FILE
         FSCALL GETDIR                 READ IN DIRECTORY
         SEG   'Members in '
         FSCALL SEGNAME                WRITE NAME
         VWRITE ,
         BNZ   FILEXIT                 QUIT IF ATTENTION ON WRITE
*
         LA    SR,DENTSIZ              LENGTH OF ENTRY
         L     TR,GPWDIRAE             HOW FAR TO GO
         L     QR,GPWDIRAP             WHERE TO START
         USING DIRB,QR                 ##
         LA    QR,DIRBDENT             BUMP PAST HEADER
         USING DENTRY,QR               ##
SHPROGLP CLI   DNAME,C' '              USED ENTRY?
         BNH   SHPROGLE                NO
         IF    GPWSFLK+GPWSFUNL+GPWSFFRO,BEGIN
         LC    R14,GPWAFNL             LENGTH TO COMPARE
         IF    GPWSFLK,BEGIN           LIKE
         EX    R14,SHMCLC
         BNE   SHPROGLE                SKIP THIS
         B     SHPROGSG
         END   ,
         IF    GPWSFUNL,BEGIN          UNLIKE
         EX    R14,SHMCLC
         BE    SHPROGLE                SKIP THIS
         B     SHPROGSG
         END   ,
         EX    R14,SHMCLC            MUST BE FROM
         BL    SHPROGLE
         END   ,
SHPROGSG SEG   DNAME,8                 WRITE NAME
         SEG   '  '
         CLI   DALINAM,DALICD          ALIAS NAME?
         BNH   SHPROMEM                NO
         SEG   ' alias for '
         SEG   DALINAM,8               WRITE ALIASED NAME
         B     SHPROGWR                GO WRITE IT
SHPROMEM LABEL ,                       MEMBER, WRITE ATTRIBUTES
         IF    DFLG1.DFL1SHR,'SEG  " Shared"'  SHARED
         IF    DFLG1.DFL1REF,'SEG  " Protected"'  PROTECTED
         IF    DFLG1.DFL1PRV,'SEG  " Private"'    PRIVATE
         IF    DFLG1.DFL1PAG,'SEG  " Page"'    PAGE ALIGNED
         IF    DFLG1.DFL1M31,'SEG  " Mode31"' START IN 31 BIT
         IF    GPWSFBLO,BEGIN         BLOCKS
         LR    R1,R4
         LCALL SEG#BLKS
         END   ,
         IF    ^GPWSFER,SHPROGWR
         CLEAR GPWFERS                 CLEAR FLAG BEFORE QUERY
         VMARK
         SEG   'Erase'
         FSCALL RDERASE             DOES HE REALLY WANT TO ERASE?
         IF    ^GPWFERS,SHPROGLE       PERMISSION?
         MVI   GPWLMNL,8               NAME LENGTH
         MVC   GPWLMN,DNAME            MOVE NAME
         VPUSH SR,QR,*
         FSCALL DELMEM                 REMOVE MEMBER
         FSCALL PUTDIR                 WRITE DIRECTORY
         FSCALL SEGMEMB                WRITE MEMBER NAME
         SEG   'erased in '
         FSCALL SEGNAME                 WRITE LIBRARY NAME
         VPOP  SR,QR,*
SHPROGWR VWRITE ,
         BNZ   FILEXIT                 QUIT IF ATTENTION ON WRITE
SHPROGLE BXLE  QR,SR,SHPROGLP          LOOP UNTIL DONE
         B     FILEXIT                 THAT'S ALL
*
SHMCLC   CLC   DNAME(0),GPWAFN
         EJECT ,
*  SHOW CONTENTS SCAN EXITS
*
CLIKE    IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC   ALREADY SET?
         SET   GPWSFLK                 SET LIKE
         B     SAVLIKE                 FIX UP AND CONTINUE SCAN
*
CUNLIKE  IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC  ALREADY SET?
         SET   GPWSFUNL                SET UNLIKE
SAVLIKE  LABEL ,                       SAVE STRING FOR SCAN
         CEIL  R0,8                    MAX LENGTH
         LR    SR,R0
         DECR  SR                      DECREMENT FOR MOVE
         STC   SR,GPWAFNL              AND SAVE
         MVC   GPWAFN,=CL8' '          BLANK IT OUT
         EX    SR,'OC  GPWAFN(0),@R1'  MOVE STRING
         SCRTN (15)
*
CFROM    IF    GPWSFLK+GPWSFUNL+GPWSFFRO,UNREC
         SET   GPWSFFRO                SET FROM
         B     SAVLIKE
         EJECT ,
* SHOW PUBLIC PROGRAMS
*
SHPROPUB LABEL ,
         GETCLW 4095,4095              GET ROOM FOR CATALOG
         BNZ   NOCORE                  SORRY
         ST    R1,GPWDIRAP             SAVE
         ST    R0,GPWDIRAL              LENGTHS
         FSCALL GETCAT                 GO GET CATALOG
         LR    QR,R1                   MOVE BASE
         USING PRGCAT,QR               ## (SET BY GETCAT)
         LM    SR,TR,PCATPTR           GET POINTERS
         SR    TR,SR                   GET LENGTH OF CATALOG
         LR    QR,TR                   SAVE FOR LATER
         DROP  QR                      ##
         LR    R1,TR                   ALSO IN R1
         L     R0,GPWDIRAP             ADDRESS OF AREA
         MVCL  R0,SR                   MOVE IT
         FSCALL RELCAT                 RELEASE CATALOG
         L     SR,GPWDIRAP             POINT TO CATALOG
         LA    TR,@SR(QR)              END OF CATALOG
         SEG   'Public Programs'
         VWRITE ,
         BNZ   DONECAT                 CHANGED MIND
SHOWPCAT CR    SR,TR                   ANY MORE?
         BNL   DONECAT                 NO
         UNPRS GPWHXBUF,@SR            UNPRESS NEXT LINE
         BNZ   PCATERR                 ERROR IN CATALOG
         LR    SR,R1                   UPDATE POINTER
         SCINIT GPWHXBUF,(0),CLWKWCB   SET UP TO SCAN LINE
         SCAN  ,CLWKWCB                GET PROGRAM NAME
         MVC   CLWTEMP,CLWKWKW         SAVE NAME
         CLEAR GPWFCB
         FSCALL FNINIT                 CLEAN OUT OLD FILE NAME
         CLEAR GPWMSGL                 RESET SAVED MESSAGE
         SCAN  ,CLWKWCB                GET LIBRARY NAME
         SCAN  CATSCKWS,CLWKWCB        SCAN FOR COMMENT
         BNZ   PCATERR                 ERROR IN CATALOG
         SEG   CLWTEMP,8               WRITE NAME
         IF    (GPWMSGL,NZ),BEGIN     IF COMMENT
         SEG   '  '                    PUT BLANKS
         SEG   L:GPWMSGA,L:GPWMSGL     WRITE SAVED COMMENT
         END   ,
         VWRITE ,
         BNZ   DONECAT                 QUIT
         B     SHOWPCAT                DO NEXT
         SPACE 2
DONECAT  LABEL ,
         B     FILEXIT                 DONE
         SPACE 2
CATSCKWS SCKW  NAME,CATNULL,(A,P)
         SCKW  NOPRINT,SHOWPCAT,A      SKIP WRITE IF NOPRINT
         SCKW  COMMENT,CATCOMM,(A,P)
         SCKW  SPIRES,CATNULL,A
         SCKW  SPL,CATNULL
         SCKW  ,KWFNOPT,PUSH
         SCKW  ,PCATERR
         SPACE 2
CATCOMM  LABEL ,                       FOUND COMMENT
         IF    GPWFNCOM,CATNULL        NO COMMENT?
         IF    (R0,GT,2),BEGIN
         LA    R1,@R1+1                SKIP ENCLOSING
         DECR  R0                      DELIMETERS
         DECR  R0
         END   ,
         STM   R0,R1,GPWMSGL           SAVE COMMENT FOR LATER
*
CATNULL  SCRTN (R15)                   RETURN
         SPACE 2
*
SHPRPUB  SET   GPWFPUB                 INDICATE PUB SPECIFIED
         L     R1,=V(SETPUB)
         BR    R1                      ENTER SCAN EXIT
*
NOCOM    SET   GPWFNCOM                NO COMMENTS
         SCRTN (R15)
         EJECT ,
* MISCELLANEOUS KEYWORD PROCESSING
*
* DETECT WHETHER FILENAME OR ACCOUNT OR NOTHING IS SPECIFIED
*
CHECKFOR SCTELL CLWKWCB                SAVE OLD SCAN
         LR    TR,R0
         LR    QR,1                    SAVE IN TR AND QR
         LSCAN CKFORTBL,CLWKWCB        LOOK FOR 'FOR'
         BNZ   SCRET                   ERROR IN SCAN
         CLEAR R15                     FOUND NOTHING  RC=0
         BR    RAR
*
CKVALFOR LABEL ,                       FOUND VALID 'FOR'
         SCINIT (QR),(TR),CLWKWCB      RESTORE SCAN
         LA    R15,12                  FOUND 'FOR'  RC=12
         BR    RAR
*
CKFNDFIL LABEL ,                       FOUND FILE NAME
         SCINIT (QR),(TR),CLWKWCB      RESTORE SCAN
CKVALFIL LA    R15,4                   FOUND PROBABLE FILENAME  RC=4
         BR    RAR
*
CKFNDACC LABEL ,                       ACCOUNT SPECIFIED
         CH    0,=Y(L'ACCT)            TEST LENTH
         BNE   BADACCT                 NO GOOD
         CLI   L'UUU(1),C'$'           CHECK DELIMITER TYPE
         BE    CKVALACC
         CLI   L'GG(1),C'.'            OTHER OPTION
         BNE   BADACCT                 BAD
         B     CKVALACC
*
CKACCT   LABEL ,                       SCAN DIDN'T FIND 'FOR'
         CH    0,=Y(L'ACCT)            RIGHT LENGTH FOR ACCT?
         BNE   CKFNDFIL                NO - TRY FILE
         CLI   L'UUU(1),C'$'           $ IN RIGHT PLACE?
         BNE   CKFNDFIL                NO - TRY FILE
CKVALACC LA    R15,8                   FOUND ACCT  RC=8
         BR    RAR
*
         EJECT ,
*  FIND GROUP NAME
*
FOR      LABEL ,                       FOR FOUND
         VPUSH RAR,,*                  SAVE RETURN
         MVC   GPWACCT(L'ACCT),GPWGRP  SAVE EXISTING ACCOUNT
         MVC   GPWGRP(L'ACCT),=C'  .   '
         LSCAN FORTBL4,CLWKWCB
         BNZ   SCRET
*  RESTORE THE FILE ACCOUNT TO ITS PROPER PLACE
         LA    R1,GPWAFN               SET UP SAVE ADDRESS
         CLI   GPWUSER,C' '            USER SPECIFIED?
         BNE   FORUSR                  JUMP IF SO
         CLI   GPWGRP,C' '             HOW ABOUT GROUP?
         BE    MISSING                 NOT THERE
         MVC   GPWUSER(L'UUU),=C'PUB'  SET GROUP LEVEL
FORUSR   CLI   GPWGRP,C' '             CHECK FOR GROUP
         BNE   FORUSG                  IT IS THERE
         MVC   GPWGRP,CLWGROUP         SIGNED ON GROUP
FORUSG   MVC   @R1(L'ACCT),GPWGRP
         MVC   GPWGRP(L'ACCT),GPWACCT  RESTORE ACCOUNT
         IF    GPWFPUB,FORRET          PUBLIC?
         LA    R0,L'ACCT
         FSCALL FORGROUP                CHECK ACCOUNT
FORRET   VPOP  RAR,,*
         BR    RAR
         EJECT ,
*  FORMAT MEMBER ENTRY FROM DIRECTORY
*
SEG#BLKS VPUSH SR,RAR,*                SAVE RETURN REGISTERS
         LR    QR,R1                   MOVE DIRECTORY ENTRY ADDR
         USING DENTRY,QR               ##
         L3    SR,DBCT                 BLOCK COUNT FROM ENTRY
         IF    NZ,BEGIN               BLOCKS EXIST FOR MEMBER
         SEG   '-- '                   PRETTY
         BTD   CLWTEMP,0,(SR)          CONVERT TOTAL
         SEG   (1),(0)                 SET BLOCK COUNT
         SEG   ' Blocks (F: '            MESSAGE
         L3    TR,DFBN                 FIRST BLOCK OF MEMBER
         BTD   CLWTEMP,0,(TR)          FORMAT IT
         SEG   (1),(0)                 AND ADD TO MESSAGE
         SEG   ', L: '
         AR    TR,SR                   LAST BLOCK NUMBER
         DECR  TR                      DECREMENT FOR LAST BLOCK
         BTD   CLWTEMP,0,(TR)          FORMAT IT
         SEG   (1),(0)                 ADD TO MESSAGE
         SEG   ') '                    PRETTY THINGS UP
         END   ELSE,BEGIN            NO CONTENTS FOR MEMBER
         SEG   '-- No contents '
         END   ,
         VPOP  SR,RAR,*                RESTORE RETURN
         BR    RAR                     RETURN TO CALLER
         DROP  QR                      ##
         EJECT ,
*  SEG ACCOUNT AND PRIVILEGES
*
SEGAPRIV LABEL ,
         VPUSH RAR,,*                  SAVE RETURN
         SEG   '- '                  ESTHETICS
         B     SEGPRIVN                DO PRIVILEGES
*
SEGPRIV  LABEL ,
         VPUSH RAR,,*                  SAVE RETURN
SEGPRIVN IF    GPWPRMWR,SETALL         TEST PRIVS
         BZ    SETNONE
         IF    GPWPRMRD,SETRO
         B     SETAO
*
SETNONE  SEG   'None '
         B     SGPREX
*
SETALL   SEG   'Write '
         B     SGPREX
*
SETRO    SEG   'Read '
         IF    ^GPWPRMAP,SGPREX        APPEND TOO?
*
SETAO    SEG   'Append '
         B     SGPREX
*
SGPREX   LABEL ,
         IF    ^GPWPRMEX,SGPRWR        EXTEND?
         SEG   '&& Extend'
SGPRWR   VWRITE
         BNZ   FILEXIT                 QUIT IF ATTENTION HIT
         VPOP  RAR,,*                  GET RETURN
         BR    RAR
         EJECT ,
*  FORMAT DATE
*
SEGDATE  LABEL ,
         VPUSH RAR,,*                  SAVE RETURN
         IF    ('CLI  @R1,X"00"',NE),BEGIN  NEW STYLE
         L4    R0,@R1                  GET CLOCK INFO
         CLEAR R1
         LA    R15,CLWTEMP             ANSWER AREA
         XCALL FMTDATE                 FORMAT IT
         END   ELSE,BEGIN
         UNPK  CLWTEMP+8(7),@R1+1(4)   UNPACK AND MOVE DATA
         LA    R1,CLWTEMP+8            SET POINTER
         MVC   CLWTEMP(2),@R1+2        MONTH
         MVI   CLWTEMP+2,C'/'
         MVC   CLWTEMP+3(2),@R1+4      DAY
         MVI   CLWTEMP+5,C'/'
         MVC   CLWTEMP+6,@R1           AND YEAR
         END   ,
         SEG   ': '                    BE NEAT
         SEG   CLWTEMP,8
         VPOP  RAR,,*                  RESTORE
         BR    RAR                     AND RETURN
         EJECT ,
* FILE ERROR MESSAGES
*
NOMEM    SEG   'No accounts given privileges'
         B     WRITEXIT
*
ACCTINC  LA    R1,GPWACCT              PRINT ACCT
         FSCALL SEGACCT
         SEG   ' Invalid in this context'
         B     4(,BR)                  ERROR RETURN
*
SCRET    LPR   R0,R0
         BZ    MISSING
*
ILLSTR   SEG   (1),(0)
         SEG   ': Invalid string'
         B     4(,BR)                  ERROR RETURN
*
MISSING  SEG   'Operand missing'
         B     4(,BR)                  ERROR RETURN
*
UNREC    SEG   (1),(0)
         SEG   ': Invalid'
         B     4(,BR)                  ERROR RETURN
FILEXIT  LABEL ,
         FSCALL FILEXIT                DOES NOT RETURN HERE
*  SET MESSAGE, CLEAN UP AND LEAVE
*
MSGEXIT  LABEL ,
         SEG   (1),(0)
WRITEXIT LABEL ,                       :::
         VWRITE ,                      SEND MESSAGE
         B     FILEXIT                 RETURN
QIETEXIT LABEL ,                       :::
         IF    ^GPWFQIET,WRITEXIT
         CLEAR (CLWOBUF,4)             QUIET!!
         B     FILEXIT
*
REQABORT SEG   'Request aborted'
         B     4(,BR)
VFMSG    SEG   'in Orvyl File System'
         B     QIETEXIT
         SPACE ,
PCATERR  LABEL ,
         SEG   'Program Catalog error'
         B     4(,BR)
         SPACE ,
NOCORE   LABEL ,
         SEG   'Insufficient user memory to process command'
         B     4(,BR)
NOCANDO  LABEL ,
         SEG   'Blocks, Format or All invalid with Erase option'
         B     4(,BR)
BADACCT  LABEL ,
         SEG   (1),(0)
         SEG   ': Invalid account'
         B     4(,BR)
         EJECT ,
*GEN
         FSCALL GOSUB
*NOGEN
         SPACE 2
*  SPACE-SAVING SEG  ROUTINE
*
*
SEG      VPUSH RAR,,*
         VSEG  (1),(0)
         VPOP  RAR,,*
         BR    RAR
         EJECT ,
*  CONSTANTS AND WORK AREA
*
         GENMSGS
         EJECT ,
*  SCAN TABLES
*
         EXTRN KWFNOPT
         EXTRN GOTNKWR
         EXTRN FORPUB
         EXTRN QUIET
         EXTRN FORACT
USRGPTBL SCKW  ,KWFNOPT,PUSH
         SCKW  ,UNREC
*
*
*
SHFILTBL SCKW  DATED,DATED,A
         SCKW  ERASE,SHFERASE,A
         SCKW  LIKE,LIKE,A
         SCKW  UNLIKE,UNLIKE,A
         SCKW  FROM,SHFILFRO,A
         SCKW  ATTACHED,SHFATT,A
         SCKW  BLOCKS,BLOCKS,A
         SCKW  FORMAT,FORMAT,A
         SCKW  ALL,DTBLOFMT,A
         SCKW  QUIET,QUIET,A
         SCKW  ,KWFNOPT,PUSH
         SCKW  ,UNREC
*
CKFORTBL SCKW  FOR,CKVALFOR
         SCKW  ACCOUNT,CKFNDACC,(A,P)
         SCKW  FILE,CKVALFIL
         SCKW  ACCT,CKFNDACC,P
         SCKW  ,CKACCT
*
FORTBL2  SCKW  ,KWFNOPT,PUSH
         SCKW  FOR,SHPERFFR
         SCKW  ,UNREC
*
FORTBL3  SCKW  FOR,SHPERACN
         SCKW  ,UNREC
*
FORTBL4  SCKW  PUBLIC,FORPUB,A
         SCKW  ,KWFNOPT,PUSH
         SCKW  ,FORACT
*
FORTBL5  SCKW  FOR,SHBLKACN
         SCKW  ,UNREC
*
LIBROPT  SCKW  LIBRARY,GOTNKWR,(A,P)
         SCKW  IN,GOTNKWR,P
         SCKW  OF,GOTNKWR,P
         SCKW  FOR,GOTNKWR,P
         SCKW  BLOCKS,BLOCKS,A
         SCKW  ALL,DTBLOFMT,A
         SCKW  LIKE,CLIKE,(A,P)
         SCKW  UNLIKE,CUNLIKE,(A,P)
         SCKW  FROM,CFROM,(A,P)
         SCKW  ERASE,SHFERASE,A
         SCKW  PUBLIC,SHPRPUB,A
         SCKW  NOCOMMENT,NOCOM,A
         SCKW  ,KWFNOPT,PUSH
         SCKW  ,UNREC
*
         END   .
