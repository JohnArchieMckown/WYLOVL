VUNP     TITLE 'ORVYL''S UNPRESS ROUTINE'
********************************************************************
*                                                                  *
*       ORVYL/370  RELEASE III - CLASS I - STANFORD PROPRIETARY    *
*                                                                  *
********************************************************************
         SPACE 2
         HIBAL ,
         SPACE 2
* MODIFICATIONS
*
         EJECT ,
VUNP     CSECT
         IDENT 00
         SPACE 2
         COPY  ORVDEFN
         SPACE 2
         REGS  ,,SR,TR,QR,RR,PR,BR,WAR,RAR
         EJECT ,
*
*              UNPRESS
*
*        ENTRY: (R1) - PRESSED TEXT (INCLUDING 4 BYTE LINE NUMBER)
*
*               (R15)- 133 CHAR OUTPUT BUFFER (WILL BE BLANKED BY
*                      THIS ROUTINE)
*
*        EXIT:  (R1) - POINTS PAST END OF PRESSED INPUT (USEFUL FOR
*                      DEBLOCKING INPUT)
*
*               (R0) - LENGTH OF UNPRESSED TEXT
*
*               (R14)- NUMBER OF UNPRESSED LINE
*
*               (R15)- (AND CONDITION CODE): 0 IF NO ERROR
*                                            4 IF ERROR DETECTED
*
         SPACE 3
UNPRS    VENTER 2,9,*
         LTR   R1,R1                   MODE=UNN?
         BNM   NUM
         LPR   R1,R1
         LC    R3,@R1                  PREST COUNT
         CH    R3,=H'142'              MAX
         BH    NOTEDIT
         XC    @R13(4),@R13            ZERO LINE NUMBER RETURN
         LA    R1,@R1+1                BUMP POINTER TO TEXT
         B     COMMON
*
NUM      DS    0H
         LC    R3,@R1+4            PICK UP PREST COUNT
         CH    R3,=H'142'          IS IT GREATER THAN 142
         BH    NOTEDIT             YES - ERROR
         MVC   @R13(4),@R1         SAVE LINE NO. AT END OF SAVE STACK
         LA    R1,@R1+5            BUMP INPUT PTR BY RECNO,COUNT
COMMON   LA    R2,@R1(R3)          POINTER TO END OF INPUT
         MVI   @R15,C' '           MOVE A BLANK TO OUTBUF
         MVC   @R15+1(132),@R15    BLANK REMAINING 132 CHAR OF OUTBUF
         LA    R14,@R15+133        END OF BUFFER
         LR    R0,R15              SAVE BUFF START
         LTR   R3,R3                   CHECK FOR NULL LINE
         BZ    NULL
         LA    R6,15               X'0000000F' MASK FOR CHAR COUNT
         LA    R9,1                YOU ARE THE ONE
LOOP     IC    R3,@R1              BLANK/CHAR COUNT
         LR    R4,R3               SAVE FOR CHAR COUNT
         SRL   R3,4                BLANK COUNT
         AR    R15,R3
         AR    R1,R9               BUMP PAST BLANK/CHAR
         NR    R4,R6               MASK TO GET CHAR COUNT
         BZ    LOOP                NO CHARS: GET NEXT BALNK/CHAR
         LA    R5,@R15(R4)         END OF NEW TEXT
         CR    R5,R14              CHECK
         BNL   NOTEDIT
         SR    R4,R9
         EX    R4,MOVE
         LR    R15,R5              NEW OUTPUT POINTER
         LA    R1,@R1+1(R4)        NEW INPUT POINTER
         CR    R1,R2
         BL    LOOP
         BH    NOTEDIT
NULL     SR    R15,R0              GET LENGTH
         LR    R0,R15
         LR    R1,R2               RETURN 'NEXT LINE' POINTER
         L     R14,@R13             RETPICK UP LINE NUMBER FROM STACK
         SR    15,15
EXIT     VEXIT 2,9,LTR
*
MOVE     MVC   @R15(*-*),@R1
*
NOTEDIT  LA    R15,4
         B     EXIT
         LTORG ,
         END   .
