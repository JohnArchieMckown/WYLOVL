         MACRO                                                                  
&L       SEGCORE &LOC,&LEN,&SEGCB,&OFFSET=,&ID=,&ALPHA=ON,             *        
               &BNDRY16=YES,&REPEAT=NO                                          
.*  Format memory area                                                          
.*    Specified ID implies OFFSET if none specified                             
         LCLA  &LL                                                              
         LCLC  &FLAGS,&UOFF                                                     
&FLAGS   SETC  ''                                                               
&UOFF    SETC  '&OFFSET'                                                        
&L       PSET  @SEGCORE                                                         
         MOVSTR @SEGXLOC,,&LOC,&LEN    Move loc/len                             
         AIF   ('&ID' EQ '').NOID                                               
&LL      SETA  L'@SEGXID                                                        
         PMOV    @SEGXID,=CL&LL.'&ID',SLEN=&LL                                  
&FLAGS   SETC  '&FLAGS.+@SEGXFID'                                               
         AIF   ('&OFFSET' NE '').NOID                                           
&UOFF    SETC  'L:@SEGXLOC'                     Implicit offset if none given   
.NOID    ANOP  ,                                                                
         AIF   ('&UOFF' EQ '').NOOFF                                            
         PMOV    @SEGXOFF,&UOFF,OP=LA           Address offset                  
&FLAGS   SETC  '&FLAGS.+@SEGXFOF'                                               
.NOOFF   ANOP  ,                                                                
         AIF   ('&ALPHA' NE 'ON').NOALPHA                                       
&FLAGS   SETC  '&FLAGS.+@SEGXFAL'      Alpha translation                        
.NOALPHA ANOP  ,                                                                
         AIF   ('&BNDRY16' NE 'YES').B32                                        
&FLAGS   SETC  '&FLAGS.+@SEGXF16'      16 byte widths                           
.B32     ANOP  ,                                                                
         AIF   ('&REPEAT' EQ 'NO').NOREP                                        
&FLAGS   SETC  '&FLAGS.+@SEGXFRL'      Print repeating lines                    
.NOREP   ANOP  ,                                                                
         AIF   ('&FLAGS' EQ '').NOFLAG                                          
&FLAGS   SETC  '&FLAGS'(2,K'&FLAGS-1)  take off leading +                       
         SET   &FLAGS                                                           
.NOFLAG  ANOP  ,                                                                
         SEGLD &SEGCB,PARM=@SEGXCB                                              
         VCALL SEGCORE                                                          
         MEND                                                                   
