         MACRO                                                                  
&L       MOVSTR &DLOC,&DLEN,&SLOC,&SLEN                                         
.*  &DLOC and &DLEN must be memory areas (designed to be stack area)            
.*    if &DLEN is not specified the length is assumed to                        
.*    be 4 bytes beyond &DLOC                                                   
.*  &SLOC and &SLEN are LA: type parameters.                                    
.*    X'xxxx' and 'aaaa' can be used with implicit or explicit                  
.*    lengths or &SLOC can be a memory location, again with implicit            
.*    or explicit lengths                                                       
         LCLA  &X,&N                                                            
         LCLC  &C,&LQ                                                           
&LQ      SETC  'L'''                                                            
.*  Figure out destination parms                                                
&SDLEN   SETC  '&DLEN'                                                          
         AIF   ('&SDLEN' NE '').DLSPEC Dest length parm specified               
&SDLEN   SETC  '&DLOC.+4'              or specify default loc                   
.DLSPEC  ANOP  ,                                                                
         AIF   ('&SLOC' EQ '').NULL             No string                       
         AIF   ('&SLOC'(1,2) EQ 'X''').HSTR     Hex string                      
         AIF   ('&SLOC'(1,1) EQ '''').STR       Ordinary string                 
.*  Explicit address given                                                      
&SPECAD  SETC  '&SLOC'                                                          
         AIF   ('&SLEN' NE '').LGIVEN                                           
&SPECLEN SETC  '&LQ.&SLOC'             Implicit length                          
         AGO   .SPECDON                                                         
.LGIVEN  ANOP  ,                       Specified length                         
&SPECLEN SETC  '&SLEN'                                                          
         AGO   .SPECDON                                                         
.*  Hex string specified                                                        
.HSTR    ANOP                                                                   
&SPECAD  SETC  '=&SLOC'                                                         
         AIF   ('&SLEN' NE '').HLGIV   Hex length given                         
&N       SETA  (K'&SLOC-2)/2           Implicit length                          
&SPECLEN SETC  '&N'                    | 1/2 as many bytes as chars             
         AGO   .SPECDON                                                         
.HLGIV   ANOP  ,                       Specified length                         
&SPECLEN SETC  '&SLEN'                                                          
         AGO   .SPECDON                                                         
.*  Ordinary string specified                                                   
.STR     ANOP  ,                                                                
&SPECAD  SETC  '=C&SLOC'                                                        
         AIF   ('&SLEN' NE '').OLGIV   Ordinary length given                    
&N       SETA  0                                                                
&X       SETA  1                                                                
&C       SETC  '&&'                                                             
.LOOP    ANOP                                                                   
&X       SETA  &X+1                                                             
         AIF   (&X GE K'&SLOC).OLDONE                                           
&N       SETA  &N+1                                                             
    AIF ('&SLOC'(&X,1) NE '''' AND '&SLOC'(&X,1) NE '&C'(1,1)).LOOP             
&X       SETA  &X+1                                                             
         AGO   .LOOP                                                            
.OLDONE  ANOP  ,                                                                
&SPECLEN SETC  '&N'                                                             
         AGO   .SPECDON                                                         
.OLGIV   ANOP  ,                                                                
&SPECLEN SETC  '&SLEN'                                                          
         AGO   .SPECDON                                                         
.SPECDON ANOP  ,                                                                
         PMOV    &DLOC,&SPECAD,OP=LA   Move in address                          
         PMOV    &SDLEN,&SPECLEN,OP=LA Move in length                           
         MEXIT ,                                                                
.NULL    CLEAR &SDLEN                  No string                                
         MEND                                                                   
