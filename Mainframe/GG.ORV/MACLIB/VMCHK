         MACRO                                                                  
&L       VMCHK &LEN,&A=,&LOC=,&KEY=,&TYPE=,&FETP=,&FIX=,&SP=,&POOL=             
.*                                                                              
.* LEN  -- Length in bytes of the area to check                                 
.*                                                                              
.* A    -- Address of the region to check.                                      
.*                                                                              
.* LOC  -- Where in virtual storage the value comes from. Valid                 
.*         values are:                                                          
.*             LOCUSR -- User memory local to the address space                 
.*             LOCSYS -- System memory local to the address space               
.*             GLOBLO -- Global memory below the line                           
.*             GLOBHI -- Global memory above the line                           
.*                                                                              
.* KEY  -- Key the storage should be in.  Valid values are:                     
.*             USER    -- User key                                              
.*             SYSTEM  -- System storage key                                    
.*             PRIVATE -- Private program storage key                           
.*                                                                              
.* TYPE -- Type of information stored there.  Valid values are:                 
.*             PROG -- Program:  allocated low to high.                         
.*             DATA -- Data: allocated high to low.                             
.*                                                                              
.* FETP -- Fetch protection attributes.  Valid values are:                      
.*             YES -- Storage should be fetch protected.                        
.*             NO  -- Storage should not be fetch protected.                    
.*                                                                              
.* FIX  -- Fix attributes. Valid values are:                                    
.*             YES -- Storage fixed but may be swapped.                         
.*             NO  -- Storage should not be fixed.                              
.*                                                                              
.* PAGE -- Whether the storage is to be allocated on a page boundary.           
.*         Valid values are:                                                    
.*             YES -- Allocate storage on a page boundary.                      
.*             NO  -- Allocate storage on a doubleword boundary.                
.*                                                                              
.* SP   -- Subpool number.  Discriminates between pools having                  
.*         like attributes.  Valid values depend on the pool                    
.*         attributes.                                                          
.*                                                                              
.* POOL -- Gives the pool number directly.  This overrides all the              
.*         above options.  Any other options specified are ignored.             
.*                                                                              
         LCLA  &P,&PP                                                           
.*                                                                              
.* Subpool parm                                                                 
.*                                                                              
         AIF   ('&SP' EQ '').NPP                                                
&PP      SETA  &SP                                                              
.*                                                                              
.* Take care of the POOL parm                                                   
.*                                                                              
.NPP     AIF   ('&POOL' EQ '').NPOOL                                            
&P       SETA  &POOL                                                            
         AGO   .GOTPOOL                                                         
.NPOOL   ANOP                                                                   
.*                                                                              
.* Scan the other parms                                                         
.*                                                                              
         LCLA  &R,&K,&T,&FP,&FX                                                 
.*                                                                              
.* First the LOC parm                                                           
.*                                                                              
         AIF   ('&LOC' NE '').SCNLOC                                            
         MNOTE 8,'LOC PARAMETER MUST BE SPECIFIED'                              
         MEXIT                                                                  
.SCNLOC  AIF   ('&LOC' NE 'LOCUSR').NLU                                         
&R       SETA  0             Region is local user                               
&K       SETA  0             Default KEY is USER                                
&T       SETA  0             Default TYPE is PROG                               
&FP      SETA  0             Default FETP is NO                                 
&FX      SETA  0             Default FIX is NO                                  
         AGO   .SCNKEY                                                          
.NLU     AIF   ('&LOC' NE 'LOCSYS').NLS                                         
&R       SETA  1             Region is local system                             
&K       SETA  1             Default KEY is SYSTEM                              
&T       SETA  1             Default TYPE is DATA                               
&FP      SETA  1             Default FETP is YES                                
&FX      SETA  0             Default FIX is NO                                  
         AGO   .SCNKEY                                                          
.NLS     AIF   ('&LOC' NE 'GLOBLO').NGL                                         
&R       SETA  2             Region is low global                               
&K       SETA  1             Default KEY is SYSTEM                              
&T       SETA  0             Default TYPE is PROG                               
&FP      SETA  0             Default FETP is NO                                 
&FX      SETA  0             Default FIX is NO                                  
         AGO   .SCNKEY                                                          
.NGL     AIF   ('&LOC' NE 'GLOBHI').NGH                                         
&R       SETA  3             Region is high global                              
&K       SETA  1             Default KEY is SYSTEM                              
&T       SETA  1             Default TYPE is DATA                               
&FP      SETA  1             Default FETP is YES                                
&FX      SETA  0             Default FIX is NO                                  
         AGO   .SCNKEY                                                          
.NGH     MNOTE 8,'Invalid LOC parm specified.'                                  
         MEXIT                                                                  
.*                                                                              
.* Scan the KEY parm                                                            
.*                                                                              
.SCNKEY  AIF   ('&KEY' EQ '').SCNTYP                                            
         AIF   ('&KEY' NE 'USER').NKU                                           
&K       SETA  0                                                                
         AGO   .SCNTYP                                                          
.NKU     AIF   ('&KEY' NE 'SYSTEM').NKS                                         
&K       SETA  1                                                                
         AGO   .SCNTYP                                                          
.NKS     MNOTE 8,'Invalid KEY parm specified.'                                  
.*                                                                              
.* Scan the TYPE parm                                                           
.*                                                                              
.SCNTYP  AIF   ('&TYPE' EQ '').SCNFP                                            
         AIF   ('&TYPE' NE 'PROG').NTP                                          
&T       SETA  0                                                                
         AGO   .SCNFP                                                           
.NTP     AIF   ('&TYPE' NE 'DATA').NTD                                          
&T       SETA  1                                                                
         AGO   .SCNFP                                                           
.NTD     MNOTE 8,'Invalid TYPE parm specified.'                                 
.*                                                                              
.* Scan the FETP parm                                                           
.*                                                                              
.SCNFP   AIF   ('&FETP' EQ '').SCNFX                                            
         AIF   ('&FETP' NE 'NO').NFPN                                           
&FP      SETA  0                                                                
         AGO   .SCNFX                                                           
.NFPN    AIF   ('&FETP' NE 'YES').NFPY                                          
&FP      SETA  1                                                                
         AGO   .SCNFX                                                           
.NFPY    MNOTE 8,'Invalid FETP parm specified.'                                 
.*                                                                              
.* Scan the FIX parm                                                            
.*                                                                              
.SCNFX   AIF   ('&FIX' EQ '').CALCD                                             
         AIF   ('&FIX' NE 'NO').NFXN                                            
&FX      SETA  0                                                                
         AGO   .CALCD                                                           
.NFXN    AIF   ('&FIX' NE 'YES').NFXY                                           
&FX      SETA  1                                                                
         AGO   .CALCD                                                           
.NFXY    MNOTE 8,'Invalid FIX parm specified.'                                  
.*                                                                              
.* Calculate the pool number                                                    
.*                                                                              
.CALCD   ANOP                                                                   
&P       SETA  &R*16+&FP*8+&FX*4+&K*2+&T                                        
.*                                                                              
.* Tell all about the pool                                                      
.*                                                                              
.GOTPOOL MNOTE *,'                                     Storage from poo*        
               l &P '                                                           
&R       SETA  &P/16                                                            
&FP      SETA  (&P-&R*16)/8                                                     
&FX      SETA  (&P-(&R*16+&FP*8))/4                                             
&K       SETA  (&P-(&R*16+&FP*8+&FX*4))/2                                       
&T       SETA  &P-(&R*16+&FP*8+&FX*4+&K*2)                                      
.*                                                                              
.* Spell it out                                                                 
.*                                                                              
         AIF   (&R NE 0).R1                                                     
         MNOTE *,'                                     Local user regio*        
               n'                                                               
         AGO   .DOK                                                             
.R1      AIF   (&R NE 1).R2                                                     
         MNOTE *,'                                     Local system reg*        
               ion'                                                             
         AGO   .DOK                                                             
.R2      AIF   (&R NE 2).R3                                                     
         MNOTE *,'                                     Low global regio*        
               n'                                                               
         AGO   .DOK                                                             
.R3      AIF   (&R NE 3).R4                                                     
         MNOTE *,'                                     High global regi*        
               on'                                                              
         AGO   .DOK                                                             
.R4      MNOTE 8,'INVALID POOL NUMBER'                                          
         MEXIT ,                                                                
.*                                                                              
.*                                                                              
.DOK     AIF   (&K NE 0).K1                                                     
         MNOTE *,'                                     User key'                
         AGO   .DOT                                                             
.K1      AIF   (&K NE 1).INTERR                                                 
         MNOTE *,'                                     System key'              
.*                                                                              
.*                                                                              
.DOT     AIF   (&T NE 0).T1                                                     
         MNOTE *,'                                     Program storage'         
         AGO   .DOFP                                                            
.T1      AIF   (&T NE 1).INTERR                                                 
         MNOTE *,'                                     Data storage'            
.*                                                                              
.*                                                                              
.DOFP    AIF   (&FP NE 0).FP1                                                   
         MNOTE *,'                                     Not fetch protec*        
               ted'                                                             
         AGO   .DOFX                                                            
.FP1     AIF   (&FP NE 1).INTERR                                                
         MNOTE *,'                                     Fetch protected'         
.*                                                                              
.*                                                                              
.DOFX    AIF   (&FX NE 0).FX1                                                   
         MNOTE *,'                                     Not fixed'               
         AGO   .DOCALL                                                          
.FX1     AIF   (&FX NE 1).INTERR                                                
         MNOTE *,'                                     Fixed'                   
         AGO   .DOCALL                                                          
.*                                                                              
.INTERR  MNOTE 8,'INTERNAL PROBLEM: &R;&K;&T;&FP;&FX'                           
         MEXIT                                                                  
.DOCALL  ANOP                                                                   
.*                                                                              
.* Do the procedure call                                                        
.*                                                                              
&L       PSET  @VMFREE                                                          
         PMOV    @VMFLEN,&LEN,OP=LA                                             
         PMOV    @VMFLOC,&A                                                     
         MVC   @VMFPOOL,=Y(&P*256+&PP)                                          
         VCALL VMCHK                                                            
         MEND                                                                   
