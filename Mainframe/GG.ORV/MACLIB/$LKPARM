         MACRO ,                       Get parameter characteristics            
         $LKPARM &PARM,&DEFOP=L                                                 
.*  This macro returns the following global set symbols which                   
.*    specify the type of parameter that was passed                             
.*       &PTYPE   = Type of parameter (MEM,CONST,REG)                           
.*       &PVAL    = Value of parameter w/out operation specifiers               
.*       &POP     = The specified/default operator type                         
.*       &PLEN    = Length of parameter (if it is a memory parameter)           
.*                                                                              
         GBLA  &$REG#                                                           
         GBLB  &$HIBAL,&$OK                                                     
         GBLA  &PLEN                   Length of memory operand                 
         GBLC  &PTYPE,&PVAL,&POP       Parm type, Parm value, Op code           
         LCLA  &N                                                               
         LCLB  &OPSPEC                 Operand specified in parm                
         LCLC  &LBL                    Label for labeled usings                 
&LBL     SETC  ''                                                               
&OPSPEC  SETB  0                                                                
&POP     SETC  '&DEFOP'                                                         
&PVAL    SETC  '&PARM'                                                          
      AIF (('&PVAL'(1,1) EQ '(') AND ('&PVAL'(K'&PVAL,1) EQ ')')).REG           
      AIF (('&PVAL'(1,1) EQ '=') OR ('&PVAL'(1,1) EQ '''')).ISMEM               
.*  Parm is not a register, search for LA: type operands                        
&N       SETA  2                                                                
.OPSRCH  AIF   (&N GE K'&PVAL).DEFOP   Use default op code and type             
         AIF   ('&PVAL'(&N,1) EQ ':').GOTOP     Found op code                   
&N       SETA  &N+1                                                             
         AGO   .OPSRCH                                                          
.GOTOP   ANOP  ,                                                                
&OPSPEC  SETB  1                       Operand specified in parm                
&POP     SETC  '&PVAL'(1,&N-1)         This is the operand                      
&PVAL    SETC  '&PVAL'(&N+1,K'&PVAL-2) This is the parameter                    
.DEFOP   ANOP  ,                                                                
&N       SETA  2                                                                
.LBSRCH  AIF   (&N GE K'&PVAL).DEFLB   Use default label                        
         AIF   ('&PVAL'(&N,1) EQ '.').GOTLB     Found label                     
&N       SETA  &N+1                                                             
         AGO   .LBSRCH                                                          
.GOTLB   ANOP  ,                                                                
&LBL     SETC  '&PVAL'(1,&N-1)         This is the operand                      
&PVAL    SETC  '&PVAL'(&N+1,K'&PVAL-2) This is the parameter                    
.DEFLB   ANOP  ,                                                                
         AIF   ('&POP' EQ 'LA').ISCONST         Parm is "constant"              
.* Parm is a memory location                                                    
.ISMEM   ANOP  ,                                                                
&PTYPE   SETC  'MEM'                   Memory operand                           
         AIF   ('&PVAL'(1,1) EQ '=').LT                                         
         AIF   (NOT &OPSPEC).IMEMLEN   Implicit memory length                   
         AIF   (('&POP' EQ 'L') OR  ('&POP' EQ 'L4')).XL4                       
         AIF   ('&POP' EQ 'L3').XL3                                             
         AIF   (('&POP' EQ 'LH') OR ('&POP' EQ 'L2')).XL2                       
         AIF   (('&POP' EQ 'L1') OR ('&POP' EQ 'LC') OR                *        
               ('&POP' EQ 'IC')).XL1                                            
.XL4     ANOP  ,                       Explicit 4 len                           
&PLEN    SETA  4                                                                
&POP     SETC  ''                                                               
         AGO   .MEMDON                                                          
.XL3     ANOP  ,                       Explicit 3 len                           
&PLEN    SETA  3                                                                
&POP     SETC  ''                                                               
         AGO   .MEMDON                                                          
.XL2     ANOP  ,                       Explicit 2 len                           
&PLEN    SETA  2                                                                
&POP     SETC  ''                                                               
         AGO   .MEMDON                                                          
.XL1     ANOP  ,                       Explicit 1 len                           
&PLEN    SETA  1                                                                
&POP     SETC  ''                                                               
         AGO   .MEMDON                                                          
.IMEMLEN ANOP  ,                                                                
&PLEN    SETA  L'&PVAL                 Set length of memory operand             
         AGO   .MEMDON                                                          
.LT      ANOP  ,                       Memory literal                           
&LTTYP   SETC  '&PVAL'(2,1)                                                     
         AIF   (('&LTTYP' GE '0') AND ('&LTTYP' LE '9')).LBAD                   
         AIF   ('&PVAL'(3,1) EQ 'L').CHKLEN                                     
  AIF ('&LTTYP' EQ 'A' OR '&LTTYP' EQ 'V' OR '&LTTYP' EQ 'F').L4                
         AIF   ('&LTTYP' EQ 'H' OR '&LTTYP' EQ 'Y').L2                          
         AIF   ('&LTTYP' EQ 'C' OR '&LTTYP' EQ 'X').L1                          
.LBAD    MNOTE 8,'&LTTYP literal type not supported'                            
         MEXIT ,                                                                
.L4      ANOP  ,                                                                
&PLEN    SETA  4                                                                
         AGO   .MEMDON                                                          
.L2      ANOP  ,                                                                
&PLEN    SETA  2                                                                
         AGO   .MEMDON                                                          
.L1      ANOP  ,                                                                
&PLEN    SETA  1                                                                
         AGO   .MEMDON                                                          
.CHKLEN  ANOP  ,                                                                
&I       SETA  5                                                                
.CHKL1 AIF (('&PVAL'(&I,1) LT '0') OR ('&PVAL'(&I,1) GT '9')).CHKLDON           
&I       SETA  &I+1                                                             
         AGO   .CHKL1                                                           
.CHKLDON ANOP                                                                   
&LV      SETC  '&PVAL'(4,&I-4)                                                  
&PLEN    SETA  &LV                                                              
         AGO   .MEMDON                                                          
.MEMDON  AIF   ('&LBL' EQ '').NOMLBL                                            
&PVAL    SETC  '&LBL..&PVAL'                                                    
.NOMLBL  ANOP  ,                                                                
.*       MNOTE *,'Length = &PLEN'                                               
         MEXIT ,                                                                
.ISCONST ANOP  ,                                                                
.*  Parm is constant, but may resolve to a register if it has                   
.*  an active using on it.                                                      
         $USLKUP &PVAL,LABEL=&LBL                                               
         AIF   (NOT &$OK).ISCON2                                                
&PVAL    SETC  '(&$REG#.)'                                                      
         AGO   .REG                                                             
.ISCON2  ANOP  ,                                                                
.*  Parm is a constant, say so                                                  
&PTYPE   SETC  'CONST'                 Constant operand                         
         AIF   ('&LBL' EQ '').NOCLBL                                            
&PVAL    SETC  '&LBL..&PVAL'                                                    
.NOCLBL  MEXIT ,                                                                
.REG     ANOP  ,                                                                
.*  Parm is a register, set its numeric value if possible.                      
&PTYPE   SETC  'REG'                   Register operand                         
&PVAL    SETC  '&PVAL'(2,K'&PVAL-2)    Strip parenthesis                        
         AIF   (NOT &$HIBAL).NHR       Not a hibal register                     
        $RGLKUP &PVAL                                                           
         AIF   (NOT &$OK).NHR          Not a hibal register                     
&PVAL    SETC  '&$REG#'                                                         
         MEXIT                                                                  
.NHR     ANOP                                                                   
&$REG#   SETA  &PVAL                                                            
         MEND                                                                   
