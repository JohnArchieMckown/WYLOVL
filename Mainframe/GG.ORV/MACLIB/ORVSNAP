         MACRO                                                                  
&L       ORVSNAP &OP,&LEN,&MODE=                                                
.*                                                                              
.*  Orvyl Release 5 QSNAP Service                                               
.*                                                                              
.*       QSNAP  (R0,R2,LINK)       - SNAP UP TO 16 REGISTERS                    
.*                                                                              
.*       QSNAP  ADDRESS,LENGTH     - SNAP (LENGTH) BYTES AT 'ADDRESS'           
.*       QSNAP  =A(ADDR),LENGTH      (0 < LENGTH < 256, 16 DEFAULT)             
.*       QSNAP  =V(ADDR),LENGTH                                                 
.*                                                                              
.*       QSNAP  'MESSAGE'          - WRITE MESSAGE                              
.*                                                                              
.*                                                                              
.*       ALL SNAP MESSAGES WRITTEN TO HASP JOB LOG UNLESS MODE                  
.*       OPERAND USED:                                                          
.*             MODE=WTO            WRITE TO CONSOLE (ROUTCDE=2)                 
.*             MODE=WTL            WRITE TO SYSLOG                              
.*             MODE=LOG            WRITE TO HASP JOB LOG ONLY                   
.*                                                                              
.*       ALL MODE SWITCHES ARE LATCHING, I.E., WILL STAY IN EFFECT              
.*       FOR FUTURE QSNAP CALLS UNLESS MODE OPERAND USED AGAIN,                 
.*       IN WHICH CASE ALL DESIRED (NON-DEFAULT) OPTIONS MUST BE                
.*       RESET.                                                                 
.*                                                                              
.*                                                                              
         GBLA  &$REG#                                                           
         GBLB  &NOQSNAP,&$OK,&MFLGS(10)                                         
         LCLA  &CNT,&FLGS,&N,&RNUM,&M,&FTAB(10)                                 
         LCLB  &R(16)                                                           
         LCLC  &LN,&RN,&TEMP,&LAB,&MTAB(10)                                     
&MTAB(1) SETC  'WTO'                                                            
&MTAB(2) SETC  'WTL'                                                            
&MTAB(3) SETC  'LOG'                                                            
&FTAB(1) SETA  X'08',X'04',0           WTO, WTL, LOG                            
.*                                                                              
.*  QSNAPS TURNED OFF                                                           
.*                                                                              
         AIF   (NOT &NOQSNAP).DOSNAP                                            
&L       LABEL                                                                  
         MEXIT                                                                  
.DOSNAP  ANOP                                                                   
.*                                                                              
.*  PROCESS MODE OPERAND                                                        
.*                                                                              
         AIF   (N'&MODE EQ 0).ENDMODE        NO NEW MODE SETTINGS               
&MFLGS(1) SETB 0,0,0,0,0,0,0,0,0,0           RESET ALL MODE SWITCHES            
.NLOOP   AIF   (&N GE N'&MODE).ENDMODE                                          
&N       SETA  &N+1                                                             
&M       SETA  1                                                                
.MLOOP   AIF   ('&MODE(&N)' EQ '&MTAB(&M)').MSET                                
&M       SETA  &M+1                                                             
         AIF   (&M LE N'&MTAB).MLOOP                                            
         MNOTE *,'&MODE(&N) - IGNORED'                                          
         AGO   .NLOOP                                                           
.MSET    ANOP                                                                   
&MFLGS(&M) SETB  1                                                              
         AGO   .NLOOP                                                           
.ENDMODE ANOP                                                                   
.*                                                                              
.*  PROCESS CURRENT MODE                                                        
.*                                                                              
&N       SETA  0                                                                
.SETLOOP ANOP                                                                   
         AIF   (&N GE N'&MFLGS).ENDSETS                                         
&N       SETA  &N+1                                                             
&FLGS    SETA  &FLGS+&MFLGS(&N)*&FTAB(&N)                                       
         AGO   .SETLOOP                                                         
.ENDSETS ANOP                                                                   
.*                                                                              
.*  NOTHING TO DO                                                               
.*                                                                              
         AIF   ('&OP' NE '').GOSNAP                                             
&L       LABEL                                                                  
         MEXIT                                                                  
.GOSNAP  ANOP                                                                   
.*                                                                              
.*  ISSUE SYSCTL                                                                
.*                                                                              
&L       SYSCTL TYPE=ORVSNAP                                                    
.SVCDONE AIF   ('&OP'(1,1) EQ '(').REGS      REGISTER LIST                      
         AIF   ('&OP'(1,1) EQ '''').MSG      'MSG TEXT'                         
         AIF   ('&OP'(1,3) EQ '=A(').ADCON   =A(ADDR)                           
         AIF   ('&OP'(1,3) EQ '=V(').ADCON   =V(ADDR)                           
.*                                                                              
.*  HAVE UNQUALIFIED BRANCH LABEL (S-CON FORM)                                  
.*                                                                              
&TEMP    SETC  'AL2(0),S(&OP.)'                                                 
&FLGS    SETA  &FLGS+16                SET X'10' BIT                            
         AGO   .COMACON                                                         
.*                                                                              
.*  PROCESS ACON                                                                
.*                                                                              
.ADCON   ANOP                          ADCON FORM                               
&TEMP    SETC  '&OP'(2,1).'('.'&OP'(4,K'&OP-4).')'                              
&FLGS    SETA  &FLGS+32                SET X'20' BIT                            
.*                                                                              
.*  PROCESS LENGTH                                                              
.*                                                                              
.COMACON AIF   ('&LEN' EQ '').LENDFLT     USER-SPECIFIED LEN?                   
&LN      SETC  '&LEN'                                                           
         AGO   .LENOK                                                           
.LENDFLT ANOP                                                                   
&LN      SETC  '16'                    SET DEFAULT                              
.LENOK   DC    AL1(&FLGS,&LN),&TEMP                                             
         MEXIT                                                                  
.*                                                                              
.*  PROCESS MESSAGE OPERAND                                                     
.*                                                                              
.MSG     ANOP                                                                   
&CNT     SETA  K'&OP-2                 DECR LEN FOR QUOTES                      
&FLGS    SETA  &FLGS+128               SET X'10' BIT                            
         DC    AL1(&FLGS,&CNT)                                                  
         LA    *-*,=C&OP               (NOT EXECUTED - CREATES SCON)            
         MEXIT                                                                  
.*                                                                              
.*  PROCESS REGISTER LIST                                                       
.*                                                                              
.REGS    ANOP                                                                   
&FLGS    SETA  &FLGS+64                                                         
&CNT     SETA  N'&OP                                                            
&RNUM    SETA  &CNT                                                             
&N       SETA  0                                                                
&M       SETA  0                                                                
.REGLOOP ANOP                                                                   
&N       SETA  &N+1                    LOOP/LIST INDEX                          
&RN      SETC  '&OP(&N)'                                                        
&TATT    SETC  T'&RN                                                            
         AIF   ('&TATT' EQ 'U').REGOK                                           
         AIF   ('&TATT' EQ 'N').REGOK                                           
         $RGLKUP &RN                                                            
&RN      SETC  '&$REG#'                                                         
         AIF   (&$OK).REGOK                                                     
         MNOTE 8,'&OP(&N) - INVALID REGISTER SPECIFICATION, IGNORED'            
&CNT     SETA  &CNT-1                                                           
         AGO   .ENDLOOP                                                         
.REGOK   ANOP                                                                   
&M       SETA  &M+1                                                             
&R(&RN+1)  SETB  1                                                              
.ENDLOOP AIF   (&N LT &RNUM).REGLOOP         FINISH OPERAND LIST                
         DC    AL1(&FLGS,&CNT,0,0)                                              
         DC    B'&R(1)&R(2)&R(3)&R(4)&R(5)&R(6)&R(7)&R(8)&R(9)&R(10)&R(*        
               11)&R(12)&R(13)&R(14)&R(15)&R(16)'                               
         MEXIT                                                                  
.NULL    DC    AL1(&FLGS),5AL1(0)                                               
.EXIT    MEND                                                                   
