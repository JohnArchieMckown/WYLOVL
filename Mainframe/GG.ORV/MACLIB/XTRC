         MACRO                                                                  
&L       XTRC  &REG1,&REG2,&NAME,&MODE                                          
.*                                                                              
.* Orvyl Trace macro.  Generates a hardware trace entry.                        
.*                                                                              
.*   REG1    - First of the register range to trace                             
.*   REG2    - Last register in trace range                                     
.*   NAME    - Name to be associated with the entry                             
.*   MODE    - ENABLED or DISABLED                                              
.*                                                                              
         GBLA  &$TRMOD,&$TRNUM                                                  
         GBLA  &$COUNT                                                          
         LCLC  &LBL,&TL,&DT                                                     
         LCLA  &TRN,&LTN                                                        
.*                                                                              
.* Make sure the person wants tracing in this module                            
.*                                                                              
         AIF   (&$TRMOD NE 0).TRACEON                                           
         MNOTE 8,'Tracing invalid.  Module TRCNUM not specified.'               
         MEXIT ,                                                                
.TRACEON ANOP                                                                   
.*                                                                              
.* Decide on the trace number for this one                                      
.*                                                                              
&LTN     SETA  &$TRNUM                                                          
         AIF   (&LTN LT 100).NUMOK                                              
         MNOTE 4,'Too many traces in one module, this one ignored.'             
&L       LABEL ,                                                                
         MEXIT ,                                                                
.NUMOK   ANOP  ,                                                                
&$TRNUM  SETA  &$TRNUM+1                                                        
&TRN     SETA  &$TRMOD*100+&LTN                                                 
         MNOTE *,'                                     **TRACE &TRN**'          
.*                                                                              
.* Get a label for the data area                                                
.*                                                                              
&LBL     SETC  '$$$TR$&LTN'                                                     
.*                                                                              
.* Get a label for the instruction                                              
.*                                                                              
&TL      SETC  '&L'                                                             
         AIF   ('&TL' NE '').GOTLAB                                             
&TL      SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
.GOTLAB  ANOP                                                                   
.*                                                                              
.* Take care of it if it needs to be disabled.                                  
.*                                                                              
         AIF   ('&MODE' NE 'DISABLED').NOTDIS                                   
&DT      SETC  '+X''80000000'''                                                 
.NOTDIS  ANOP                                                                   
.*                                                                              
.* Issue the instruction                                                        
.*                                                                              
&TL      TRACE &REG1,&REG2,&LBL                                                 
.*                                                                              
.* Sack away data for the trace                                                 
.*                                                                              
         DATA  BEGIN                                                            
&LBL     DC    A(&LBL&DT)              Trace parameter                          
         DC    A(&TRN)                 Trace number                             
         DC    AL1(&REG1,&REG2)        Register range included                  
         DC    CL8'&NAME'              And the name itself                      
         END   ,                                                                
         MEND                                                                   
