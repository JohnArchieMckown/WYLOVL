         MACRO                                                                  
&L       POOLFREE &LEN,&A=,&TYPE=,&SUBPOOL=                                     
.********************************************************************           
.* LEN         LA: Length of memory to be freed.                                
.* A           L:  Location of memory to be freed                               
.* TYPE        One or more of the following options (first item of              
.*             each pair is the default):                                       
.*                                                                              
.*   LOCAL     Memory associated with this task                                 
.*   GLOBAL    Memory shared with other tasks                                   
.*                                                                              
.*   NONFIX    Non page fixed memory                                            
.*   FIX       Page fixed memory                                                
.*                                                                              
.*   ABOVE16M  Memory is located above the 16M line                             
.*   BELOW16M  Memory is located below the 16M line                             
.*                                                                              
.*   DWORD     Memory is located on doubleword boundary                         
.*   PAGE      Memory is located on page boundary                               
.*                                                                              
.* SUBPOOL     Request for specific MVS subpool (TYPE operand                   
.*             is ignored)                                                      
.*                                                                              
.********************************************************************           
         GBLA  &$REG#                                                           
         GBLB  &$OK                                                             
         LCLB  &A16M,&PAGE,&FIX,&GLOB                                           
         LCLC  &AR,&POOL                                                        
         AIF   ('&A' NE '').AOK                                                 
         MNOTE 8,'ADDRESS MUST BE SPECIFIED'                                    
         MEXIT                                                                  
.AOK     ANOP                                                                   
         PSET  @MEMORY,CLEAR=YES                                                
&L       LABEL ,                                                                
&AR      SETC  '&LEN'                                                           
         AIF   ('&LEN'(1,1) NE '(' OR '&LEN'(K'&LEN,1) NE ')').NRR              
&AR      SETC  '&LEN'(2,K'&LEN-2)                                               
         AGO   .AREG                                                            
.NRR     ANOP                                                                   
        $LA    R0,&LEN                                                          
&AR      SETC  'R0'                                                             
.AREG    ANOP                                                                   
         ST    &AR,@MEMLEN              Set length parameter                    
&AR      SETC  '&A'                                                             
         AIF   ('&A'(1,1) NE '(' OR '&A'(K'&A,1) NE ')').NRR2                   
&AR      SETC  '&A'(2,K'&A-2)                                                   
         AGO   .AREG2                                                           
.NRR2    ANOP                                                                   
        $L     R0,&AR                                                           
&AR      SETC  'R0'                                                             
.AREG2   ANOP                                                                   
         ST    &AR,@MEMLOC                                                      
         AIF   ('&SUBPOOL' NE '').MVSPOOL   MVS pool explicitly set             
&A16M    SETB  1                       DEFAULT IS ABOVE 16 MEG                  
&N       SETA  0                                                                
         AGO   .NEXT                                                            
.LOOP    AIF   ('&TYPE(&N)' NE 'LOCAL').NL                                      
&GLOB    SETB  0                                                                
         AGO   .NEXT                                                            
.NL      AIF   ('&TYPE(&N)' NE 'GLOBAL').NG                                     
&GLOB    SETB  1                                                                
         AGO   .NEXT                                                            
.NG      AIF   ('&TYPE(&N)' NE 'NONFIX').NUF                                    
&FIX     SETB  0                                                                
         AGO   .NEXT                                                            
.NUF     AIF   ('&TYPE(&N)' NE 'FIX').NF                                        
&FIX     SETB  1                                                                
         AGO   .NEXT                                                            
.NF      AIF   ('&TYPE(&N)' NE 'ABOVE16M').NA                                   
&A16M    SETB  1                                                                
         AGO   .NEXT                                                            
.NA      AIF   ('&TYPE(&N)' NE 'BELOW16M').NB                                   
&A16M    SETB  0                                                                
         AGO   .NEXT                                                            
.NB      AIF   ('&TYPE(&N)' NE 'DWORD').ND                                      
&PAGE    SETB  0                                                                
         AGO   .NEXT                                                            
.ND      AIF   ('&TYPE(&N)' NE 'PAGE').NP                                       
&PAGE    SETB  1                                                                
         AGO   .NEXT                                                            
.NP      ANOP                                                                   
         MNOTE 8,'&TYPE(&N) INVALID TYPE'                                       
.NEXT    ANOP                                                                   
&N       SETA  &N+1                                                             
         AIF  (&N LE N'&TYPE).LOOP                                              
.*                                                                              
         AIF   (NOT &A16M).DP1                                                  
&POOL    SETC  'MEMTA16M'                                                       
.DP1     AIF   (NOT &PAGE).DP2                                                  
         AIF   ('&POOL' EQ '').DP1X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP1X    ANOP                                                                   
&POOL    SETC  '&POOL'.'MEMTPAGE'                                               
.DP2     AIF   (NOT &FIX).DP3                                                   
         AIF   ('&POOL' EQ '').DP2X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP2X    ANOP                                                                   
&POOL    SETC  '&POOL'.'MEMTFIX'                                                
.DP3     AIF   (NOT &GLOB).DP4                                                  
         AIF   ('&POOL' EQ '').DP3X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP3X    ANOP                                                                   
&POOL    SETC  '&POOL'.'MEMTGLOB'                                               
.DP4     AIF   ('&POOL' EQ '').DODEF                                            
         SET   (&POOL,EQ)                                                       
         AGO   .OK                                                              
.DODEF   ANOP                                                                   
         CLEAR @MEMPOOL                                                         
.OK      ANOP                                                                   
         AGO   .CALL                                                            
.MVSPOOL ANOP  ,                        MVS pool explicitly set                 
         MVI   @MEMPOOL,&SUBPOOL       Set MVS subpool                          
.CALL    ANOP  ,                        Do call to POOLGET                      
         VCALL POOLFREE                                                         
         MEND                                                                   
