         MACRO                                                                  
&L       MEMFREE &LEN,&A=,&TYPE=,&SUBPOOL=                                      
.********************************************************************           
.* LOC         L: Location of memory to be freed                                
.* LEN         LA: Length of memory to be freed                                 
.* TYPE        One or more of the following options (first item of              
.*             each pair is the default):                                       
.*                                                                              
.*   LOCAL     Memory associated with this task                                 
.*   GLOBAL    Memory shared with other tasks                                   
.*                                                                              
.*   NONFIX    Non page fixed memory                                            
.*   FIX       Page fixed memory                                                
.*                                                                              
.*   ABOVE16M  Memory may be located above the 16M line                         
.*   BELOW16M  Memory must be located below the 16M line                        
.*                                                                              
.*   DWORD     Memory may be located on doubleword boundary                     
.*   PAGE      Memory must be located on page boundary                          
.*                                                                              
.* SUBPOOL     Request for specific MVS subpool (TYPE operand                   
.*             is ignored)                                                      
.*                                                                              
.********************************************************************           
         GBLA  &$REG#                                                           
         GBLB  &$OK                                                             
         LCLB  &A16M,&PAGE,&FIX,&GLOB                                           
         LCLC  &AR,&POOL,&PTYPE                                                 
&L       LABEL ,                                                                
         PSET  @MEMFREE,CLEAR=YES                                               
.*  Put Length parameter on stack, register or otherwise                        
&AR      SETC  '&LEN'                                                           
         AIF   ('&LEN'(1,1) NE '(' OR '&LEN'(K'&LEN,1) NE ')').NRR              
&AR      SETC  '&LEN'(2,K'&LEN-2)                                               
         AGO   .AREG                                                            
.NRR     ANOP                                                                   
&AR      SETC  'RVR'                                                            
        $LA    &AR,&LEN                                                         
.AREG    ANOP                                                                   
         ST    &AR,@MFLEN              Set length parameter                     
.*  Put Location parameter on stack, register or otherwise                      
&AR      SETC  '&A'                                                             
         AIF   ('&A'(1,1) NE '(' OR '&A'(K'&A,1) NE ')').NRRL                   
&AR      SETC  '&A'(2,K'&A-2)                                                   
         AGO   .AREGL                                                           
.NRRL    ANOP  ,                                                                
&AR      SETC  'RVR'                                                            
        $L     &AR,&A                                                           
.AREGL   ANOP  ,                                                                
         ST    &AR,@MFLOC              Set length parameter                     
         AIF   ('&SUBPOOL' NE '').MVSPOOL   MVS pool explicitly set             
&A16M    SETB  1                       DEFAULT IS ABOVE 16 MEG                  
&N       SETA  0                                                                
         AGO   .NEXT                                                            
.LOOP    AIF   ('&TYPE(&N)' NE 'LOCAL').NL                                      
&GLOB    SETB  0                                                                
         AGO   .NEXT                                                            
.NL      AIF   ('&TYPE(&N)' NE 'GLOBAL').NG                                     
&GLOB    SETB  1                                                                
         AGO   .NEXT                                                            
.NG      AIF   ('&TYPE(&N)' NE 'NONFIX').NUF                                    
&FIX     SETB  0                                                                
         AGO   .NEXT                                                            
.NUF     AIF   ('&TYPE(&N)' NE 'FIX').NF                                        
&FIX     SETB  1                                                                
         AGO   .NEXT                                                            
.NF      AIF   ('&TYPE(&N)' NE 'ABOVE16M').NA                                   
&A16M    SETB  1                                                                
         AGO   .NEXT                                                            
.NA      AIF   ('&TYPE(&N)' NE 'BELOW16M').NB                                   
&A16M    SETB  0                                                                
         AGO   .NEXT                                                            
.NB      AIF   ('&TYPE(&N)' NE 'DWORD').ND                                      
&PAGE    SETB  0                                                                
         AGO   .NEXT                                                            
.ND      AIF   ('&TYPE(&N)' NE 'PAGE').NP                                       
&PAGE    SETB  1                                                                
         AGO   .NEXT                                                            
.NP      ANOP  ,                                                                
         MNOTE 8,'&TYPE(&N) INVALID TYPE'                                       
.NEXT    ANOP                                                                   
&N       SETA  &N+1                                                             
         AIF  (&N LE N'&TYPE).LOOP                                              
.*                                                                              
         AIF   (NOT &A16M).DP1                                                  
&POOL    SETC  'MFA16M'                                                         
.DP1     AIF   (NOT &PAGE).DP2                                                  
         AIF   ('&POOL' EQ '').DP1X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP1X    ANOP                                                                   
&POOL    SETC  '&POOL'.'MFPAG'                                                  
.DP2     AIF   (NOT &FIX).DP3                                                   
         AIF   ('&POOL' EQ '').DP2X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP2X    ANOP                                                                   
&POOL    SETC  '&POOL'.'MFFIX'                                                  
.DP3     AIF   (NOT &GLOB).DP4                                                  
         AIF   ('&POOL' EQ '').DP3X                                             
&POOL    SETC  '&POOL'.'+'                                                      
.DP3X    ANOP  ,                                                                
&POOL    SETC  '&POOL'.'MFGLOB'                                                 
.DP4     AIF   ('&POOL' EQ '').DODEF                                            
         SET   (&POOL,EQ)                                                       
         AGO   .OK                                                              
.DODEF   ANOP                                                                   
.*       CLEAR @MFTYPE                 Already cleared                          
.OK      ANOP                                                                   
         AGO   .CALL                                                            
.MVSPOOL ANOP  ,                       MVS pool explicitly set                  
         MVI   @MFFTYPE,&SUBPOOL       Set MVS subpool                          
.CALL    ANOP  ,                       Do call to MEMFREE                       
         VCALL MEMFREE                                                          
         MEND                                                                   
