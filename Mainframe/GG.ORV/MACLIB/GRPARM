*****************************************************************               
*                                                                               
* General Resource API                                                          
*                                                                               
*****************************************************************               
@GRES   RECORD BEGIN               General Resource Routine api                 
                                                                                
@GRCID   DS    A                   Caller id                                    
@GRID    DS    A                   Resource id                                  
                                                                                
@GRFLAG  FLAG  ,                                                                
         FLAG  @GRWAIT             Wait for resource to come free               
         FLAG  @GREXCL             Get hold exclusively                         
         FLAG  @GRCANAS            Cancel only this address space               
                                                                                
         END   ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Create a general resource item                                                
*                                                                               
*   CALLID is a four byte caller identifier                                     
*                                                                               
*   The resource id is returned in RVR                                          
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRCRE &CID=                                                            
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&CID' EQ '').ERR                                               
         PSET  @GRES                                                            
         PMOV  @GRCID,&CID         Set caller id                                
         VCALL GRCRE               Create a general resource item               
         AGO   .EXIT                                                            
.ERR     MNOTE 8,'CID is a required parameter'                                  
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Delete a general resource item                                                
*                                                                               
*   RID is a four byte resource id returned from GRCRE                          
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRDEL &RID=                                                            
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&RID' EQ '').ERR                                               
         PSET  @GRES                                                            
         PMOV  @GRID,&RID          Set resource id                              
         VCALL GRDEL               Delete a general resource item               
         AGO   .EXIT                                                            
.ERR     MNOTE 8,'Resource ID is a requried parameter'                          
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Get a general resource item hold                                              
*                                                                               
*   RID is a four byte resource id returned from GRCRE                          
*                                                                               
*   The resource hold id is returned in RVR                                     
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRGET &RID=,&WAIT=YES,&EXCL=NO                                         
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&RID' EQ '').ERR1                                              
         PSET  @GRES                                                            
         PMOV  @GRID,&RID                                                       
         AIF   ('&WAIT' EQ 'NO').EXCL                                           
         AIF   ('&WAIT' NE 'YES').ERR2                                          
         SET   @GRWAIT                                                          
.EXCL    AIF   ('&EXCL' EQ 'NO').VCALL                                          
         AIF   ('&EXCL' NE 'YES').ERR3                                          
         SET   @GREXCL                                                          
.VCALL   VCALL GRGET               Get general resource item                    
         AGO   .EXIT                                                            
.ERR1    MNOTE 8,'Resource ID is a requried parameter'                          
         AGO   .EXIT                                                            
.ERR2    MNOTE 8,'Valid values for WAIT= are YES or NO'                         
         AGO   .EXIT                                                            
.ERR3    MNOTE 8,'Valid values for EXCL= are YES or NO'                         
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Release a general resource item hold                                          
*                                                                               
*   RID is a four byte resource hold id returned from GRGET                     
*     Note: The RID can be the resource id or hold id if the                    
*           get was exclusive. See GRREL in RESMAN.                             
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRREL &RID=                                                            
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&RID' EQ '').ERR                                               
         PSET  @GRES                                                            
         PMOV  @GRID,&RID                                                       
         VCALL GRREL               Release a general resource item              
         AGO   .EXIT                                                            
.ERR     MNOTE 8,'Resource ID is a requried parameter'                          
         AGO   .EXIT                                                            
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Change an exclusive hold to a share hold                                      
*                                                                               
*   RID is a four byte resource id returned from GRGET                          
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRX2S &RID=                                                            
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&RID' EQ '').ERR                                               
         PSET  @GRES                                                            
         PMOV  @GRID,&RID                                                       
         VCALL GRX2S     Change an exclusive hold to a share hold               
         AGO   .EXIT                                                            
.ERR     MNOTE 8,'Resource ID is a requried parameter'                          
         AGO   .EXIT                                                            
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
*****************************************************************               
*                                                                               
* Cancel a general resource item waiting for hold                               
*                                                                               
*   RID is a four byte resource id returned from GRGET                          
*   TYPE is either ALL for cancel all waiters or AS for all                     
*        waiters in this address space only                                     
*                                                                               
*****************************************************************               
         MACRO ,                                                                
&LABEL   GRCAN &RID=,&TYPE=AS                                                   
         AIF   ('&LABEL' EQ '').NOLAB                                           
&LABEL   LABEL ,                                                                
.NOLAB   ANOP  ,                                                                
         AIF   ('&RID' EQ '').ERR                                               
         PSET  @GRES                                                            
         AIF   ('&TYPE' EQ 'ALL').CANALL                                        
         AIF   ('&TYPE' NE 'AS').ERR2                                           
         SET   @GRCANAS            Cancel only waiters of this AS               
.CANALL  ANOP  ,                                                                
         PMOV  @GRID,&RID                                                       
         VCALL GRCAN               Cancel a general resource item               
         AGO   .EXIT                                                            
.ERR2    MNOTE 8,'Type must be ALL or AS'                                       
         AGO   .EXIT                                                            
.ERR     MNOTE 8,'Resource ID is a requried parameter'                          
.EXIT    ANOP  ,                                                                
         MEND  ,                                                                
                                                                                
                                                                                
