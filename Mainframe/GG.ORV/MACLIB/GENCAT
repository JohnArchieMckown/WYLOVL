         MACRO                                                                  
         GENCAT &OP,&REG=                                                       
         GBLA   &NCONST           NUMBER OF CONSTANTS                           
         GBLC   &LABEL(25),&DESC(25)                                            
&NCONST  SETA   0                                                               
&BL      SETC   '          '                                                    
&NCONST  SETA   0                                                               
         CATCODE LOOK,'Inspect/Change Entry'                                    
         CATCODE INS,'Insert Entry'                                             
         CATCODE DEL,'Delete Entry'                                             
         CATCODE LIS,'List Entrys'                                              
         CATCODE RENAM,'Rename'                                                 
         CATCODE SEQ,'Sequential Access'                                        
         CATCODE CONT,'Continue operation'                                      
         CATCODE REC,'Checkpoint Recovery'                                      
         AIF    ('&OP' EQ 'TABLE').TABLE        GEN BRANCH TABLE                
         AIF    ('&OP' EQ 'CONST').EQUCON       GEN CONSTANT TABLE              
         AIF    ('&OP' EQ 'NAMES').NAME         GEN =C' ' NAME TABLE            
         MNOTE  8,'&OP is not a valid GENCAT operation'                         
         MEXIT  ,                                                               
.EQUCON  ANOP   ,                       GENERATE EQU CONSTANTS                  
&N       SETA   1                                                               
*PUSH,NOGEN                                                                     
*PUSH,GEN                                                                       
.CONLOOP ANOP   ,                                                               
         AIF    (&N GT &NCONST).CONLOOX                                         
&CODE    SETA   (2*&N)-2                                                        
&EQUCON  SETC   'KAT&LABEL(&N)'                                                 
&EQUCON  EQU    &CODE,&CODE,C'N'                                                
         MNOTE  *,'&BL&DESC(&N)'                                                
&N       SETA   &N+1                                                            
         AGO    .CONLOOP                                                        
.CONLOOX ANOP   ,                                                               
*POP                                                                            
*POP                                                                            
         MEXIT  ,                                                               
.TABLE   ANOP   ,                       GENERATE BRANCH TABLE                   
*PUSH,NOGEN                                                                     
*PUSH,GEN                                                                       
         CL     &REG,=A(&NCONST*2-2)    HIGHEST VALID NUM                       
         BH     CATINVAL                                                        
         LH     &REG,CATTABL(&REG)                                              
         A      &REG,=A(&SYSECT)                                                
         BR     &REG                                                            
CATTABL  DS     0H                                                              
&N       SETA   1                                                               
.TABLOOP ANOP   ,                                                               
         AIF    (&N GT &NCONST).TABLOOX                                         
         DC     AL2(CAT&LABEL(&N)-&SYSECT)                                      
         MNOTE  *,'&BL&DESC(&N)'                                                
&N       SETA   &N+1                                                            
         AGO    .TABLOOP                                                        
.TABLOOX ANOP   ,                                                               
*POP                                                                            
*POP                                                                            
CATINVAL ABORT  CATCODE                                                         
         MEXIT  ,                                                               
.NAME    ANOP   ,                       GENERATE =C' ' NAMES                    
&N       SETA   1                                                               
*PUSH,NOGEN                                                                     
*PUSH,GEN                                                                       
.NAMLOOP ANOP   ,                                                               
         AIF    (&N GT &NCONST).NAMLOOX                                         
         DC     CL5'&LABEL(&N)'                                                 
&N       SETA   &N+1                                                            
         AGO    .NAMLOOP                                                        
.NAMLOOX ANOP   ,                                                               
*POP                                                                            
*POP                                                                            
         MEND                                                                   
