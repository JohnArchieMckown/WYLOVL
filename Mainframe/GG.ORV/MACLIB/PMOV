         MACRO                                                                  
&L       PMOV    &DEST,&SRC,&SLEN=,&DLEN=,&OP=,&REG=,&LEFTADJ=NO                
.*  Move parameters from &SRC to the program stack area &DEST                   
.*  &OP refers to the source operand (can be used to default to LA)             
.*  &SLEN refers to an explicit source length                                   
.*  &DLEN refers to an explicit destination length                              
.*  If &REG is specified, that register will contain the                        
.*   value of the parm at the end of the macro generated code                   
         GBLA  &PLEN,&$PMOV                                                     
         GBLC  &PTYPE,&PVAL,&POP                                                
         LCLA  &SPLEN,&DPLEN                                                    
         LCLC  &SPTYPE,&DPTYPE,&SPVAL,&DPVAL,&SPOP,&DPOP                        
         LCLC  &DOFF,&SOFF,&MLEN,&USREG                                         
&L       LABEL ,                                                                
         AIF   ('&DEST'(1,1) NE '(').NOTREG                                     
         LF    &DEST,&SRC,LEN=&SLEN                                             
         MEXIT                                                                  
.NOTREG  ANOP  ,                                                                
         $LKPARM &DEST                 Get parameter characteristics            
         AIF   ('&PTYPE' EQ 'MEM').DESTOK                                       
         MNOTE 8,'Destination must be a memory location'                        
         MEXIT ,                                                                
.DESTOK  AIF   ('&DLEN' NE '').SPECLEN Length specified                         
&DPLEN   SETA  &PLEN                   Implicit length got                      
         AGO   .IMDLEN                                                          
.SPECLEN ANOP  ,                                                                
&$PMOV   SETA  &$PMOV+1                This garbage is because                  
&EQU     SETC  '$$PMOV&$PMOV'           we need to force the                    
&EQU     EQU   0,&DLEN,C'N'              assembler to evaluate the              
&DPLEN   SETA  L'&EQU                     length of our item before             
         ANOP  ,                           the 'second pass'                    
.IMDLEN  ANOP  ,                                                                
&DPTYPE  SETC  '&PTYPE'                                                         
&DPVAL   SETC  '&PVAL'                                                          
&DPOP    SETC  '&POP'                                                           
         $LKPARM &SRC,DEFOP=&OP        Get parameter characteristics            
         AIF   ('&SLEN' NE '').SPECSLN Length specified                         
&SPLEN   SETA  &PLEN                   Implicit length got                      
         AGO   .IMSLEN                                                          
.SPECSLN ANOP  ,                                                                
&$PMOV   SETA  &$PMOV+1                This garbage is because                  
&EQU     SETC  '$$PMOV&$PMOV'           we need to force the                    
&EQU     EQU   0,&SLEN,C'N'              assembler to evaluate the              
&SPLEN   SETA  L'&EQU                     length of our item before             
.IMSLEN  ANOP  ,                           the 'second pass'                    
&SPTYPE  SETC  '&PTYPE'                                                         
&SPVAL   SETC  '&PVAL'                                                          
&SPOP    SETC  '&POP'                                                           
.*  Now finally figure out what to do...                                        
&USREG   SETC  'RVR'                                                            
         AIF   ('&REG' EQ '').NOREG             Don't need a reg                
&USREG   SETC  '&REG'                           Must use spec reg               
.NOREG   AIF   ('&SPTYPE' NE 'CONST').NSCON                                     
.*    source is a constant                                                      
        $LA    &USREG,&SPVAL                                                    
&DOFF    SETC  ''                                                               
         AIF   ('&LEFTADJ' EQ 'YES').LADJST                                     
         AIF   (&DPLEN LE 4).LADJST                                             
&DOFF    SETC  '+'.'&DPLEN-4'                                                   
.LADJST  ANOP                                                                   
         STF   &USREG,&DPVAL.&DOFF,LEN=&DPLEN                                   
         MEXIT ,                                                                
.NSCON   AIF   ('&SPTYPE' NE 'REG').NSREG                                       
.*    source is a register                                                      
&DOFF    SETC  ''                                                               
         AIF   ('&LEFTADJ' EQ 'YES').LADJST2                                    
         AIF   (&DPLEN LE 4).LADJST2                                            
&DOFF    SETC  '+'.'&DPLEN-4'                                                   
.LADJST2 ANOP                                                                   
         STF   &SPVAL,&DPVAL.&DOFF,LEN=&DPLEN                                   
         AIF   ('&REG' EQ '').SROK              Can use source reg              
        $L     &USREG,(&SPVAL)                  Must set spec reg               
.SROK    MEXIT ,                                                                
.*    source is a memory location                                               
.NSREG   ANOP  ,                                                                
         AIF   ('&REG' EQ '').MROK                                              
         LF    &USREG,&SPVAL,LEN=&SPLEN         Must use spec reg               
         STF   &USREG,&DPVAL,LEN=&DPLEN                                         
         MEXIT ,                                                                
.MROK    AIF   ('&SPOP' EQ '').MEMIMP           Implicit memory ref             
        $L     RVR,&SPOP:&SPVAL                 or use explicit ref             
         STF   RVR,&DPVAL,LEN=&DPLEN                                            
         MEXIT ,                                                                
.MEMIMP  ANOP  ,                                                                
&SOFF    SETC  ''                               No source offset                
&DOFF    SETC  ''                               No dest offset                  
&MLEN    SETC  '&DPLEN'                                                         
         AIF   (&SPLEN NE 0).LENSP              Use dest len for 0              
&SPLEN   SETA  &DPLEN                                                           
.LENSP   ANOP  ,                                                                
         AIF   (&SPLEN EQ &DPLEN).NOADJ         Don't need adjust               
         AIF   (&SPLEN LT &DPLEN).LTSRC                                         
.* Source is long                                                               
&SOFF    SETC  '+'.'&SPLEN-&DPLEN'                                              
         AGO   .NOADJ                                                           
.LTSRC   ANOP  ,                                                                
.* Source is short                                                              
.*       CLEAR (&DPVAL,&DPLEN)                                                  
         AIF   ('&LEFTADJ' EQ 'YES').LADJ                                       
&DOFF    SETC  '+'.'&DPLEN-&SPLEN'                                              
.LADJ    ANOP                                                                   
&MLEN    SETC  '&SPLEN'                                                         
.NOADJ   ANOP  ,                                                                
         MVC   &DPVAL.&DOFF.(&MLEN),&SPVAL.&SOFF                                
         MEND  ,                                                                
