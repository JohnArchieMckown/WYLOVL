         MACRO                                                                  
&L       SYSCTL &REG1,&REG2,&TYPE=                                              
.*       Modified, 7/90, Niz                                                    
.*                                                                              
.* This macro defines the diagnose instructions that may be                     
.* used by the guest machine to request services or information                 
.* from the host.                                                               
.*                                                                              
         LCLA  &T                                                               
         LCLC  &R1,&R2                                                          
&R1      SETC  '0'                                                              
&R2      SETC  '0'                                                              
.*                                                                              
.DTNOTDMP AIF   ('&TYPE' NE 'DUMPNOT').DTERMCOM                                 
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  29                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DTERMCOM AIF   ('&TYPE' NE 'DTERMCOM').DTCLOS                                  
         AIF   ('&REG1' NE '').NOREGS                                           
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  28                                                               
         AGO   .DODIAG                                                          
.*                                                                              
.DTCLOS  AIF   ('&TYPE' NE 'DTCLOS').DTWRIT                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  27                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DTWRIT  AIF   ('&TYPE' NE 'DTWRIT').DTALLO                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  26                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DTALLO  AIF   ('&TYPE' NE 'DTALLO').QSERV                                      
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  25                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.QSERV   AIF   ('&TYPE' NE 'QSERV').CPUINFO                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  24                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.CPUINFO AIF   ('&TYPE' NE 'CPUINFO').DOPARM2                                   
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  23                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DOPARM2 AIF   ('&TYPE' NE 'PARM2').HOSTCPU                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  22                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.HOSTCPU AIF   ('&TYPE' NE 'HOSTCPU').DOM                                       
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  21                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DOM     AIF   ('&TYPE' NE 'DOM').WTO                                           
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  20                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.WTO     AIF   ('&TYPE' NE 'WTO').SNAP                                          
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  19                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.SNAP    AIF   ('&TYPE' NE 'ORVSNAP').MAXCPU                                    
         AIF   ('&REG1' NE '').NOREGS                                           
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  18                                                               
         AGO   .DODIAG                                                          
.*                                                                              
.MAXCPU  AIF   ('&TYPE' NE 'MAXCPU').MAXREAL                                    
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  17                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MAXREAL AIF   ('&TYPE' NE 'MAXREAL').DOPARM                                    
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  16                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.DOPARM  AIF   ('&TYPE' NE 'PARM').MVSENQ                                       
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  15                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MVSENQ  AIF   ('&TYPE' NE 'MVSENQ').SMF                                        
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  14                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.SMF     AIF   ('&TYPE' NE 'SMF').MVSNAM                                        
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  13                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MVSNAM  AIF   ('&TYPE' NE 'MVSNAM').MVSMWR                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' NE '').NOREGS                                           
&T       SETA  12                                                               
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MVSMWR  AIF   ('&TYPE' NE 'MVSMWR').MVSMRD                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  11                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MVSMRD  AIF   ('&TYPE' NE 'MVSMRD').NOSPIN                                     
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  10                                                               
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.NOSPIN  AIF   ('&TYPE' NE 'NOSPIN').SPIN                                       
         AIF   ('&REG2' NE '').NOREGS                                           
         AIF   ('&REG1' EQ '').REG1BAD                                          
&T       SETA  9                                                                
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.SPIN    AIF   ('&TYPE' NE 'SPIN').ALLOC                                        
         AIF   ('&REG2' NE '').NOREGS                                           
         AIF   ('&REG1' NE '').NOREGS                                           
&T       SETA  8                                                                
         AGO   .DODIAG                                                          
.*                                                                              
.ALLOC   AIF   ('&TYPE' NE 'ALLOC').NOPAGE                                      
         AIF   ('&REG2' NE '').NOREGS                                           
         AIF   ('&REG1' EQ '').REG1BAD                                          
&T       SETA  7                                                                
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.NOPAGE  AIF   ('&TYPE' NE 'NOPAGE').PAGE                                       
         AIF   ('&REG2' NE '').NOREGS                                           
         AIF   ('&REG1' EQ '').REG1BAD                                          
&T       SETA  6                                                                
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.PAGE    AIF   ('&TYPE' NE 'PAGE').LOADEND                                      
         AIF   ('&REG2' NE '').NOREGS                                           
         AIF   ('&REG1' EQ '').REG1BAD                                          
&T       SETA  5                                                                
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.LOADEND AIF   ('&TYPE' NE 'LOADEND').LOADSTA                                   
         AIF   (('&REG1' NE '') OR ('&REG2' NE '')).NOREGS                      
&T       SETA  4                                                                
         AGO   .DODIAG                                                          
.*                                                                              
.LOADSTA AIF   ('&TYPE' NE 'LOADSTA').LOADBLK                                   
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  3                                                                
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.LOADBLK AIF   ('&TYPE' NE 'LOADBLK').MEMSIZE                                   
         AIF   ('&REG1' EQ '').REG1BAD                                          
         AIF   ('&REG2' EQ '').REG2BAD                                          
&T       SETA  2                                                                
&R1      SETC  '&REG1'                                                          
&R2      SETC  '&REG2'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.MEMSIZE AIF   ('&TYPE' NE 'MEMSIZE').ABEND                                     
&T       SETA  1                                                                
         AIF   ('&REG1' EQ '').REG1BAD                                          
&R1      SETC  '&REG1'                                                          
         AGO   .DODIAG                                                          
.*                                                                              
.ABEND   AIF   ('&TYPE' NE 'ABEND').INVTYPE                                     
         AIF   (('&REG1' NE '') OR ('&REG2' NE '')).NOREGS                      
&T       SETA  0                                                                
         AGO   .DODIAG                                                          
.*                                                                              
.* Take care of the error conditions.  All these set an error code              
.* of 12 and cause the abend action to be taken.                                
.*                                                                              
.INVTYPE MNOTE 12,'INVALID SYSCTL TYPE SPECIFIED'                               
         AGO   .INVALID                                                         
.REG1BAD MNOTE 12,'FIRST REGISTER OPERAND INVALID'                              
         AGO   .INVALID                                                         
.REG2BAD MNOTE 12,'SECOND REGISTER OPERAND INVALID'                             
         AGO   .INVALID                                                         
.NOREGS  MNOTE 12,'REGISTER OPERANDS SUPPLIED IN ERROR'                         
.INVALID ANOP                                                                   
&T       SETA  0                                                                
.*                                                                              
.*                                                                              
.DODIAG  ANOP                                                                   
&L       DIAG  &R1,&R2,&T                                                       
         MEND                                                                   
