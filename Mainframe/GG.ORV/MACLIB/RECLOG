         MACRO                                                                  
&L       RECLOG ,                                                               
.*  Sets the &L symbol to the logarithm base 2 of the length of the             
.*  current HIBAL record DSECT. (From the start to the current                  
.*  location.)  The length must be a power of 2.                                
         GBLC  &$BSTK(0),&$BLABSTK(0)                                           
         GBLA  &$BPTR                                                           
         LCLA  &LEN,&N,&LOG                                                     
         LCLC  &PFX,&LABEL,&TMP                                                 
&PFX     SETC  '&$BSTK(&$BPTR)'(1,1)                                            
&LABEL   SETC  '&$BLABSTK(&$BPTR)'                                              
*PUSH,NOGEN                                                                     
$$L1&PFX&LABEL DS 0X                                                            
$$L2&PFX&LABEL EQU 0,$$L1&PFX&LABEL-$$$&PFX&LABEL,C'N'                          
.* Following changed from *POP,GEN to *POP by AOS on 06/23/2000                 
*POP                                                                            
&TMP     SETC  '$$L2&PFX&LABEL'                                                 
&LEN     SETA  L'&TMP                                                           
&N       SETA  1                                                                
&LOG     SETA  0                                                                
.DSLOOP  ANOP  ,                                                                
         AIF   (&N EQ &LEN).DONE                                                
         AIF   (&N GT &LEN).ERR                                                 
&LOG     SETA  &LOG+1                                                           
&N       SETA  &N*2                                                             
         AGO   .DSLOOP                                                          
.ERR     ANOP  ,                                                                
         MNOTE 8,'The current DSECT length is not an even power of 2'           
         MEXIT ,                                                                
.DONE    ANOP  ,                                                                
&L       EQU   &LOG,,C'N'                                                       
         MEND  ,                                                                
