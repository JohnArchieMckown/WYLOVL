;
;  Wings - Show space on volume
;
xproc () begin

   declare number  x.pathno
   declare string  open_str
   declare string  open_opt
   declare string  msg
   declare string  rc

   on error cmd 'pcall err_routine'
   on attn  cmd 'pcall err_routine'

   msg = 'yourtag=&(fpath) noheader wait=2'
   syscall path_open(x.pathno, msg, rc)
   if (rc ne 'OK') begin
      syscall path_close(x.pathno)
      if  (rc eq 'TIMEOUT') begin
         comm The WINGS file server is not available at this time
      end
      else begin
         comm Path error on open = &(rc)
      end
      xreturn
   end

   open_str = sub('FILE_LSPACE:',1,16)
   open_opt = parm_string
   msg = open_str || open_opt

   syscall path_write(x.pathno, ' ', msg, rc)
   if (rc ne 'OK') begin
      syscall path_close(x.pathno)
      comm Path write error - &(rc)
      xreturn
   end

   pcall getdata(msg)
   while (sub(msg,1,16) eq sub('DATA:',1,16)) begin
      msg = substr(msg,17)
      comm &(msg)
      pcall getdata(msg)
   end

   if (sub(msg,1,16) eq sub('FILE_ERROR:',1,16)) begin
      msg = substr(msg,17)
      comm &(msg)
   end

   syscall path_close(x.pathno)

end

proc getdata(msg) begin
   declare string rc
   syscall path_read(x.pathno, ' ', msg, rc)
   if (rc ne 'OK') begin
      syscall path_close(x.pathno)
      comm path read error = &(rc)
      xreturn
   end
   return (msg)
end

proc err_routine() begin

   syscall path_close(x.pathno)
   xreturn

end
