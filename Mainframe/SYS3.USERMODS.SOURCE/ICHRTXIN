//ICHRTXIN  JOB ,'XA ICHRTXIN',CLASS=E,TIME=(2),REGION=1000K
/*JOBPARM LINES=50,CHARS=CU12
//STEP1   EXEC PGM=IEV90,
//             PARM='DECK,NOLOAD,BUFSIZE(MAX)'
//STEPLIB DD DSN=SYS2.ASMH.SLACV29.LINKLIB,DISP=SHR
//SYSPRINT DD  SYSOUT=A
//SYSUDUMP DD  SYSOUT=A
//SYSUT1   DD  UNIT=VIO,
//             SPACE=(CYL,(20,5))
//SYSLIB  DD DSN=SYS1.AMACLIB,DISP=SHR,
//           DCB=BLKSIZE=6320
//        DD DSN=SYS1.AMODGEN,DISP=SHR
//        DD DSN=WYL.GG.ACS.MACLIB,DISP=SHR
//        DD DSN=WYL.GG.SYS.MACLIB,DISP=SHR
//        DD DSN=SYS3.MACLIB,DISP=SHR
//SYSPUNCH DD DSN=WYL.GG.AOS.OBJECT(ICHRTXIN),DISP=SHR
//SYSTERM DD SYSOUT=C
//SYSIN DD *
*
*    THIS MODULE IS CALLED BY USER EXIT ICHRTX00 WHEN A RACROUTE
*    RACINIT REQUEST HAS BEEN MADE.  ITS PURPOSE IS TO CREATE A RACF
*    USERID FROM THE JES2-FURNISHED JOB ACCOUNTING INFORMATION IF IT
*    HAS NOT BEEN SUPPLIED BY THE USER.  IN ADDITION, PASSWORD
*    CHECKING IS BYPASSED FOR JOBS WHICH HAVE PREVIOUSLY UNDERGONE
*    JES2 VERIFICATION.  ON RETURN TO CALLER, RC 0 INDICATES THAT
*    ICHRTXIN PROCESSING HAS BEEN SUCCESSFUL.  RC 4 MEANS ICHRTXIN
*    PROCESSING HAS FAILED AND LEAVES IT TO SAF TO REISSUE THE
*    RACINIT REQUEST AND FAIL THE USER IF WARRANTED.
*
          EJECT
ICHRTXIN START 0
         USING PSA,0
         USING ICHRWA,RD
         ST    RC,ICHRWBAS             SAVE CALLER'S BASE REG
         BALR  RC,R0
         USING *,RC
         USING SAFP,R3
         NOP   PATCH               GET ADDRESSABILITY FOR PATCH AREA
         NOP   RC4                 MANUAL BYPASS SWITCH
         B     MAINLINE
         DC    C'*** ICHRTXIN ***'
         DC    C'&SYSDATE'
MAINLINE LR    R7,R3               GET OFFSET TO
         A     R7,SAFPRACP             RACF PARAMETER LIST
         USING RACP,R7
         TM    RACPFLG1,B'11000000'    CHECK FOR ENVIR CREATE 00
         BNZ   RC4                     IF NOT, USERID ISN'T USED
         XC    ICHPUSER,ICHPUSER       CLEAR NEW USERID FIELD
         MVC   ICHPSPRO,RACPSPRO       SAVE STC NAME JUST IN CASE
         L     R8,RACPUSER             GET USERID FROM JOB CARD
         LTR   R8,R8                   IF IT WAS SUPPLIED, LET
         BNZ   RESTSPRO                SAF ISSUE RACINIT AS IS
*
* IF THE USERID HAS NOT BEEN SUPPLIED IN THE JOB CARD THEN IT MUST
* BE FORMATTED FROM THE ACCOUNTING INFORMATION IF AVAILABLE.  ELSE
* USE HASPSERV SVC TO RETURN THE USER'S ACCOUNT.
*
         MVI   ICHPUSRL,X'06'          PUT IN LENGTH OF USERID FIELD
         L     R8,RACPACC              GET ACCOUNTING INFO
         LTR   R8,R8                   STARTED TASKS DO NOT HAVE
         BZ    NOACINFO                   ACCOUNTING INFO
         SR    R9,R9                   CLEAR R9
         ICM   R9,3,0(R8)              GET LENGTH FIELD OF USERID
         CH    R9,=H'6'                IS IT AT LEAST 6 BYTES LONG?
         BL    NOACINFO                IF NOT, FORGET IT
         CLI   4(R8),C'.'              IS USER FORMAT GG.UUU?
         BE    NEWFMAT                 IT IS
         CLI   4(R8),C'$'              IS USER FORMAT GG$UUU
         BNE   OLDFMAT                 IF NOT GO CHECK IF UUU$GG
NEWFMAT  MVC   ICHPUSER,2(R8)          MOVE UUU TO RACF USERID
         B     RINITCHK                AND UPDATE RACINIT PLIST PTR
OLDFMAT  CLI   5(R8),C'$'              IS USER FORMAT UUU$GG?
         BE    FORMATID                IF SO, FORMAT USER ID
*
* THIS IS NORMALLY ENTERED ONLY FOR STARTED TASKS WHICH DO NOT HAVE
* A USERID OR ACCOUNTING INFORMATION FROM WHICH TO DERIVE THE RACF
* USERID.  A HASPSERV IS ISSUED TO OBTAIN THE USER ID FROM THE JES2
* STCPARMS.
*
NOACINFO NOP   RESTSPRO                MANUAL BYPASS SWITCH
         HASPSERV JOBACCT,ICHACCWA
         LTR   RF,RF                   DID JES RETURN USER ID?
         BNZ   RESTSPRO                IF NOT, USE ICHRIN03 DEFAULT
         MVC   ICHPUSER(2),ICHACCWA+3  MOVE IN GG
         MVC   ICHPUSER+3(3),ICHACCWA  MOVE IN UUU
*
* ZERO OUT STC NAME IN THE RACINIT PARM LIST SO RACINIT WILL BYPASS
* SEARCH OF ICHRIN03 STARTED TASK TABLE FOR THE USERID.
*
         XC    RACPSPRO,RACPSPRO       CLEAR STC NAME IN RAC PARM
         B     RINITCHK
FORMATID MVC   ICHPUSER(2),6(R8)       MOVE IN GG TO RACF USERID
         MVC   ICHPUSER+3(3),2(R8)     MOVE IN UUU
RINITCHK MVI   ICHPUSER+2,C'$'         MOVE IN DELIMITER
*
* NOW ISSUE RACINIT USING THE CONVERTED USERID
*
         L     R5,FLCCVT          GET CVT POINTER
         L     R5,CVTSAF-CVT(R5)  LOAD SAF VECTOR
         USING SAFV,R5
         L     R4,SAFPWA
         USING SAFW,R4
         SR    R2,R2              ZERO R2
         ST    R2,SAFWRC          ZERO EXIT RETURN CODE
         ST    R2,SAFWRSC         ZERO EXIT REASON CODE
         L     RF,SAFVRACR        LOAD RACF ROUTER ADDRESS
         LTR   RF,RF              IS RACF ROUTER PRESENT
         BZ    RESTSPRO           IF NOT BYPASS RACF CHECK
UPDTRACP LA    R9,ICHPUSRL             CHOOSE A SPOT IN W/A
         ST    R9,RACPUSER             FOR A USERID
         L     R9,RACPPASS             CHECK IF PASSWORD
         LTR   R9,R9                       IS SUPPLIED
         BNZ   RACINIT                 IF SO DO NOT BYPASS PSWDCHK
         OI    RACPFLG1,X'08'          OTHERWISE, BYPASS PSWDCHK
RACINIT  NI    SAFWCTF1,X'FF'-SAFWEXAF TURN OFF EXIT EXECUTING FLAG
         OI    SAFWCTF1,SAFWRFFP  TURN ON RACF ROUTER IN CONTROL
         LA    R5,ICHREND          POINT PAST ICHRWA FOR NEW SAVE AREA
         ST    R5,ICHRPRM2            FOR RACF CALL
         ST    R3,ICHRPRM1         SAVE SAF PARMLIST FOR RACF CALL
         LA    R1,ICHRPRM          POINT TO NEW RACF CALL PARMLIST
         LR    RA,RET           SAVE CALLER'S RET REG
         BALR  RET,RF            TO ICHRFR00
         LR    RET,RA           REPLACE CALLER'S RET REG
         NI    SAFWCTF1,X'FF'-SAFWRFFP TURN OFF ROUTER IN CONTROL
         OI    SAFWCTF1,SAFWEXAF  TURN ON SAFWEXAF EXIT EXECUTING
         LTR   RF,RF            DID RACF OK THIS REQUEST
         BZ    RETURN           RETURN TO CALLER WITH RC 0
         XC    RACPUSER,RACPUSER  ELSE RESTORE MODIFIED FIELDS
         NI    RACPFLG1,X'F7'     AND LET SAF REISSUE RACINIT
RESTSPRO MVC   RACPSPRO,ICHPSPRO  RESTORE STARTED PROC NAME
RC4      LA    RF,4
RETURN   L     RC,ICHRWBAS
         BR    RET
         LTORG
         DC    C'PATCH AREA FOLLOWS:'
         DS    0H
PATCH    DC    200X'00'
         DC    C'&SYSDATE'
         PRINT NOGEN
         IECEQU AOS=YES
         IECDSECS CVT,PSA,UCB,EXPAND=YES
         ICHSAFP
         ICHSAFV
         ICHSAFW
ICHRWA   DSECT
ICHRSAV  DS    18F
ICHREXIT DS    F
ICHRWBAS DS    F
ICHRPRM  DS    0F
ICHRPRM1 DS    F
ICHRPRM2 DS    F
ICHPUSRL DS    CL1                     USERID LENGTH
ICHPUSER DS    CL6                     USERID
ICHPSPRO DS    CL4
ICHWYLFL DS    CL1
ICHACCWA DS    CL9
         DS    0F
ICHREND  EQU   *
RACP DSECT                         RACF PARAMETER LIST MAP
RACPLNTH DS    CL1                     PARAMETER LENGTH
RACPSPNO DS    CL1                     SUBPOOL NUMBER
         DS    CL1                     RESERVED
RACPFLG1 DS    CL1                     FUNCTION BYTE
RACPUSER DS    F                       ADDR USERID
RACPPASS DS    F                       ADDR PASSWORD
RACPSPRO DS    F                       ADDR STARTED PROC NAME
RACPIEP  DS    F                       RESERVED FOR THE USER
RACPGRPN DS    F                       ADDR GROUP NAME
RACPNPAS DS    F                       ADDR NEW PASSWORD
RACPPGR  DS    F                       ADDR PROGRAMMER NAME
RACPACC  DS    F                       ADDR ACCOUTING INFORMATION
RACPOID  DS    F                       ADDR OPERATOR ID CARD INFO
RACPTRM  DS    F                       ADDR TERMINAL ID
RACPJOBN DS    F                       ADDR OF JOBNAME
RACPAPPN DS    F                       ADDR OF APPLICATION NAME
RACACEE  DS    F                       ADDR ACEE
         END
