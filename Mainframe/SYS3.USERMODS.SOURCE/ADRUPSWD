         TITLE 'ADRUPSWD - EXIT FOR ADRDSSU DECISION'
*
*
* ADRUPSWD -
*              FRONT END DECISION FOR EXECUTION OF SENSITIVE PROGRAMS
*              PARKER J GILLESPIE, SCIP SYSTEMS
*              DECEMBER 3, 1973
*
*              MODIFIED NOV. 29,83 FOR ADRDSSU EXIT
*              STU MILLER, ITS SYSTEMS
*
*
* FUNCTION -
*              DETERMINE WHETHER AN ACCOUNT NUMBER IS VALID FOR
*              EXECUTING SENSITIVE PROGRAMS
*
* ENTRY -
*              DEPENDENT ON THE PROGRAM BEING PROTECTED
*
* INPUTS -
*              NONE
*
* OUTPUTS -
*              RETURN R15=0 IF QUALIFIED USER........
*
*
         EJECT ,
ADRUPSWD CSECT
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    R12,R15             MOVE PROGRAM BASE ADDRESS
         USING ADRUPSWD,R12        DECLARE BASING
         SPACE ,
         LA    R0,WORKSIZ          LENGTH OF REQUIRED WORK CORE
         GETMAIN R,LV=(0)          ACQUIRE DYNAMIC WORK SPACE
         LA    R0,WORKSIZ           FUSSY -- VERY FUSSY VS
         USING WORKAREA,R1         DECLARE TEMPORARY BASING
         STM   R0,R1,FREEMAIN      SAVE FOR FREEMAIN
         LA    R11,SAVEAREA        ADDRESS OF OUR SAVE AREA
         ST    R11,8(,R13)         CHAIN BACK
         ST    R13,4(,R11)         CHAIN FORWARD
         LR    R13,R11             MOVE SA AND WORK ADDRESS
         DROP  R1                  KILL TEMPORARY BASING
         USING WORKAREA,R13        DECLARE ROUTINE WORKAREA BASE
         EJECT ,
*
* GET USERS ACCOUNT NUMBER
*
         SPACE ,
         HASPSERV JOBACCT,JOBACCT  GIVE ME A NUMBER
         CH    R15,=H'4'           SUCESSFUL RETURN FROM HASPSERV?
         BH    BYPASS              HASP NOT ACTIVE, BYPASS SECURITY
         BE    BYPASS              INVALID PARAMETER, BYPASS SECURITY
         SPACE ,
*
* DETERMINE USER TYPE: INTERNAL, OPEN-SHOP, . . .
*
         SPACE ,
         HASPSERV USERTYPE,JOBACCT TEST USER TYPE
         LTR   R15,R15             SUCESSFUL RETURN FROM HASPSERV?
         BNZ   BYPASS              SOMETHING BAD, BYPASS SECURITY
         N     R0,=A($USINTAL)     ALLOW ALL INTERNAL ACCOUNTS
         BNZ   VALIDUSE            BRANCH IF VALID USE OF PROGRAM
         SPACE 2
*
* KILL USER SINCE HE DIDN'T QUALIFY TO EXECUTE THE PROGRAM
*
         SPACE ,
         LM    R0,R1,FREEMAIN      RELOAD REGISTERS TO FREE WORKAREA
         L     R13,4(,R13)         RELOAD CALLERS SA ADDRESS
         FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC CORE
         LM    R14,R12,12(R13)     RELOAD CALLERS REGISTERS
         SPACE ,
         XR    R15,R15             CLEAR RETURN CODE TO ZERO
         XR    R0,R0               CLEAR R0 FOR GOOD MEASURE
         LA    R1,X'913'           ABEND CODE FOR SECURITY
         SLL   R1,12               MAKE INTO A SYSTEM ABEND
         SVC   13                  KILL WITH RELISH
         EJECT ,
*
* VALID USER, CONTINUE WITH PROGRAM EXECUTION
*
         SPACE 2
VALIDUSE DS    0H
BYPASS   DS    0H
         SPACE 2
         LM    R0,R1,FREEMAIN      RELOAD TO FREEMAIN WORKAREA
         L     R13,4(,R13)         RELOAD CALLERS SA ADDRESS
         FREEMAIN R,LV=(0),A=(1)   RELEASE DYNAMIC CORE
         LM    R14,R12,12(R13)
         SR    R15,R15             CONTINUE EXECUTION AND
         BR    R14                 DO NOT RETURN
         EJECT ,
*
* DEFINE DYNAMIC WORK AREA FOR ADRUPSWD
*
         SPACE 2
WORKAREA DSECT ,
SAVEAREA DS    18F
FREEMAIN DS    2F
JOBACCT  DS    CL9
WORKSIZ  EQU   *-WORKAREA          LENGTH OF DYNAMIC AREA
         SPACE ,
ADRUPSWD CSECT ,                   CONTINUE WITH PROGRAM NOW
         EJECT ,
*
* LITERAL POOL AND WORK CONSTANTS
*
         SPACE 2
         LTORG
         EJECT ,
*
* REGISTER EQUATES AND CONTROL BLOCK DEFINITIONS
*
         SPACE ,
         COPY  UTYPDEFN
         SPACE ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         END   ADRUPSWD
