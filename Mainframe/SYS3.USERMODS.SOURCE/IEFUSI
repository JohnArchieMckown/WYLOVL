//IEFUSI JOB ,'ASSEMBLE XA IEFUSI',CLASS=E,TIME=2
/*JOBPARM CHARS=CU12
//S1 EXEC ASMCL
//ASM.SYSIN DD *
IEFUSI   TITLE 'SMF STEP INITIATION EXIT ROUTINE               '
IEFUSI   CSECT ,                       CSECT NAME DECLARED
*
* REGISTER EQUATES
*
R0      EQU   00                       REGISTER 0
R1      EQU   01                       REGISTER 1
R2      EQU   02                       REGISTER 2
R3      EQU   03                       REGISTER 3
R4      EQU   04                       REGISTER 4
R5      EQU   05                       REGISTER 5
R6      EQU   06                       REGISTER 6
R7      EQU   07                       REGISTER 7
R8      EQU   08                       REGISTER 8
R9      EQU   09                       REGISTER 9
R10     EQU   10                      REGISTER 10
R11     EQU   11                      REGISTER 11
R12     EQU   12                      REGISTER 12
R13     EQU   13                      REGISTER 13
R14     EQU   14                      REGISTER 14
R15     EQU   15                      REGISTER 15
         EJECT
         SAVE  (14,12),,IEFUSI
         USING IEFUSI,R12              SET UP BASE ADDRESSABILITY
*
* NOTE THAT NO SAVE_AREA WAS GETMAINED SINCE THIS ROUTINE
*      CALLS NO OTHER ROUTINE OR ISSUES ANY SVC'S
*
         LR    R12,R15                 LOAD BASE REG WITH ENTRY POINT
         L     R2,16(,R1)              LOAD VSM PARAMETER LIST
         L     R3,4(,R2)               CHECK REGION SIZE REQUESTED
         LTR   R3,R3                   IF REGION SPECIFIED, GO TO CHK
         BP    CHKHIGH                      FOR HIGH LIMIT OF 8M
         SR    R3,R3                   ELSE SET TO ZERO THEN
         B     ADD                     GO ADD 2 MEGS
CHKHIGH  C     R3,=F'8388608'          IF REGION REQ IS MORE THAN 8M
         BH    EXIT                         NO NEED TO ADD TO IT
ADD      A     R3,=F'2097152'          INCREMENT BY 2 MEGS
         ST    R3,8(,R2)               STORE INTO LIMIT VALUE
         OI    0(R2),X'80'             SIGNAL VSM IEFUSI CHANGED LIMIT
EXIT     DS    0H
         RETURN (14,12),RC=0           RETURN TO CALLER
         EJECT
         DS    0F
MODLEN   EQU   *-IEFUSI           EQUATE FOR MODULE LENGTH(- PATCH)
PTCHLEN  EQU   ((MODLEN+7)/8+7)/8*8 PATCH AREA LENGTH EQUATE
PTCHLNTH DC    Y(PTCHLEN)          LENGTH OF PATCH AREA
PTCHBASE DC    S(PATCH)            BASE DISPLACED ADDRESS OF PATCH
PATCH    DC    XL(PTCHLEN)'00'     PATCH AREA
         END   IEFUSI
//LKED.SYSLMOD DD DSN=SYS2.XA.RESLIB(IEFUSI),DISP=SHR
