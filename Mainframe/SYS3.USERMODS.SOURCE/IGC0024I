//SLPSMFWR JOB ,'SMFWRITE- SVC 249',TIME=(,15)
//COMPILE EXEC ASMCL,REGION=1024K,PARM.ASM='RENT',PARM.LKED='RENT'
//ASM.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR,DCB=(BLKSIZE=6320)
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//         DD DSN=WYL.GG.SYS.MACLIB,DISP=SHR
//ASM.SYSIN   DD *
SMFWRITE TITLE '            S M F W R I T E   S V C'
         SPACE 8
********************************************************************
*                                                                  *
*                                                                  *
*         SMFWRITE SVC - Class I - Stanford Proprietary            *
*                                                                  *
*                                                                  *
*                                                                  *
*                                                                  *
*      (c) Copyright 1985 by the Board of Trustees of the          *
*               Leland Stanford Junior University                  *
*                      All rights reserved                         *
*                                                                  *
*                                                                  *
********************************************************************
         EJECT ,
********************************************************************
*                                                                  *
*                                                                  *
*                          SMFWRTE SVC                             *
*                                                                  *
*                                                                  *
*    This SVC routine will allow an unauthorized program to        *
*    issue SMF records, subject to password control.               *
*                                                                  *
*                                                                  *
*    Registers at entry:                                           *
*                                                                  *
*        R0        Same as when SVC was issued                     *
*        R1        address of SMF record to write                  *
*                  (must reside below 16 meg line)                 *
*        R2        Contents are unpredictable                      *
*        R3        Address of CVT                                  *
*        R4        Address of TCB                                  *
*        R5        SVRB                                            *
*        R6        Entry point address                             *
*        R7        Address of ASCB                                 *
*        R8-R12    Contents are unpredictable                      *
*        R13       Same as when SVC was issued                     *
*        R14       Return address                                  *
*        R15       Address of Secret Decoder Word                  *
*                                                                  *
*                                                                  *
*    Register usage                                                *
*                                                                  *
*        R1        Address of SMF header                           *
*        R5        Address of SVBB                                 *
*        R6        Module base                                     *
*        R8        ABEND reason code                               *
*        R11       Return address save                             *
*                                                                  *
*                                                                  *
*    Registers at exit                                             *
*                                                                  *
*        R0-R1     Contents are unpredictable                      *
*        R2-R13    Will be restored by SVC SLIH                    *
*        R14       SVC SLIH return address                         *
*        R15       Return code indicating:                         *
*                      0 - record written without error            *
*                                                                  *
*                                                                  *
*    ABENDs                                                        *
*                                                                  *
*        X'BAD'    Reason code indicating:                         *
*                      4 - bad record type                         *
*                      8 - valid password not provided             *
*                                                                  *
*                                                                  *
********************************************************************
         EJECT ,
********************************************************************
*                                                                  *
*    Modification log:                                             *
*                                                                  *
*        12/06/85  SLP  Initial development                        *
*        12/10/85  SLP  Correct 'tombstone' documentation          *
*                                                                  *
********************************************************************
         EJECT ,
         MACRO
&L       EQUREGS
.*
.* DEFINES REGISTERS R0-R15 AND F0-F6
.*
         PRINT NOGEN
         GBLB  &EQUREGS
         AIF   (&EQUREGS).END
&EQUREGS SETB  1
         LCLA  &RNO
&RNO     SETA  0
.LOOP    ANOP
R&RNO    EQU   &RNO
&RNO     SETA  &RNO+1
         AIF   (&RNO LE 15).LOOP
&RNO     SETA  0
.LP2     ANOP
F&RNO    EQU   &RNO
&RNO     SETA  &RNO+2
         AIF   (&RNO LE 6).LP2
.END     ANOP
         PRINT GEN
         MEND
         EJECT ,
SMFRCD   DSECT ,                   SMF RECORD
SMFLEN   DS    BL2'0'              RECORD LENGTH
SMFSEG   DS    BL2'0'              SEGMENT DESCRIPTOR
SMFFLG   DC    BL1'0'              HEADER FLAG BYTE
SMFRTY   DC    BL1'0'              RECORD TYPE
SMFTME   DC    BL4'0'              TOD FROM TIME MACRO BIN
SMFDTE   DC    PL4'0000'           DATE IN PACKED DECIMAL: 00YYDDDF
SMFSID   DC    CL4' '              SYSTEM IDENTIFICATION
         EJECT ,
RB       DSECT ,
         IKJRB ,
         EJECT ,
IGC0024I CSECT
IGC0024I AMODE 31
IGC0024I RMODE ANY
         EQUREGS
         USING IGC0024I,R6         ESTABLISH MODULE BASE
         USING RBBASIC,R5          LAY DSECT OVER SVBB
         USING SMFRCD,R1           LAY DSECT OVER SMF HEADER
         B     SKIPID              BRANCH PAST MODULE ID
         IDENT ,                   MODULE ID FOOTPRINT
SKIPID   DS    0H
         LR    R11,R14             SAVE RETURN ADDRESS
         LA    R8,8                SET BAD PASSWORD RC
         CLC   =X'1BD958EDDA20E769',0(R15)  CHECK FOR
         BNE   ABEND               PASSWORD
         LA    R8,4                SET BAD RECTYPE RC
         CLI   SMFRTY,128          MAKE SURE THIS IS
         BL    ABEND                USER RECORD TYPE
         SMFWTM (1)                WRITE OUT THE RECORD
         SR    R15,R15             SET ZERO RETURN CODE
         LM    R0,R1,RBGRS0        RESTORE USER REGS
         LR    R14,R11             GET THE RETURN ADDRESS
         BR    R14                  AND GO BACK
         SPACE 2
ABEND    DS    0H
         WTO   'INVALID SMFWRITE SVC CALL',ROUTCDE=11
         ABEND X'BAD',REASON=(8)
         END   ,
//*LKED.SYSLMOD DD DSN=SYS1.LPALIBD(IGC0024I),DISP=OLD
