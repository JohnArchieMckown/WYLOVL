IEFU84   TITLE 'IEFU84 - SMF RECORD EXIT'
********************************************************************
*                                                                  *
*  IEFU84 and IEFU85 SMF RECORD EXITS                              *
*                                                                  *
********************************************************************
*                                                                  *
*  IEFU84 and IEFU85 are essentially the same SMF record exit.     *
*  U84 is the standard exit; U85 is invoked when the caller is     *
*  running in cross-memory mode.  Since we're not doing anything   *
*  particularly fancy with this exit, IEFU85 is just an alias for  *
*  IEFU84.                                                         *
*                                                                  *
*  On entry:                                                       *
*              R1  - ADDRESS OF A FULLWORD THAT POINTS TO THE      *
*                    RECORD THE SMF IS TO WRITE.                   *
*                                                                  *
*              R13 - REGISTER SAVE AREA                            *
*                                                                  *
*              R14 - RETURN ADDRESS                                *
*                                                                  *
*              R15 - ENTRY POINT ADDRESS                           *
*                                                                  *
*  At exit:                                                        *
*              R15 - SET TO ZERO TO INDICATE 'WRITE RECORD'        *
*                                                                  *
*              For certain records,                                *
*                    the second byte of SMF30JPT is set to the     *
*                    job execution time block                      *
*                                                                  *
*  Modifications:                                                  *
*                                                                  *
*  02/28/2002  slp  Add comments after figuring out why there was  *
*                   no separate source for IEFU85                  *
*                                                                  *
*                                                                  *
********************************************************************
         SPACE 2
         HIBAL ,
IEFU84   CSECT ,                   Define control section name
IEFU84   AMODE 31                  Set for 31-bit addressing
IEFU84   RMODE ANY                 Set for any residency mode
         SPACE ,
         IFASMFR 30                Define type 30 format
         CVT   DSECT=YES           Generate CVT
SCIPCVT  SCIPCVT ,                 Generate SCIP CVT format
         EJECT ,
*box
*
*  IEFU84 Module entry point
*
         SPACE 2
IEFU84   CSECT ,                   Continue exit control section
         USING IEFU84,R15          Declare temporary addressability
         B     IEFU84ID            Skip module identifier
         DC    CL8'IEFU84'         Generate moduel identitifer
         DC    CL8'&SYSTIME'       Assembly time
         DC    CL8'&SYSDATE'       Assembly date
         SPACE ,
IEFU84ID LABEL ,
         STM   R14,R12,@R13+12     Save entry registers
         LR    R12,R15               Move exit base register
         DROP  R15                     Release temporary base
         USING IEFU84,R12                Declare exit addressability
         SPACE ,
*-
*-       Test for type-30 SMF record
*-
         L     R2,@R1              Load address of smf record
         USING SMFRCD30,R2         Addressabilty to smf record
         CLI   SMF30RTY,30         Is this a type-30 record?
         IF    NE,IEFU84X          No, ignore record
         SPACE ,
*-
*-       Test for type-30 SMF interval record, (subtype 2)
*-       Okay everybody, let's make up our mind, initially this was
*-       only supposed to be for sub-type 2's!!!
*-
         LT    R3,SMF30SOF         Retreive subsystem section offset
         IF    Z,IEFU84X           Ignore if missing section
         CLC   SMF30SON,=XL2'0000' Test count of sections
         IF    Z,IEFU84X           Ignore if count is zero
         ALR   R3,R2               Address of subsystem section
         USING SMF30PSS,R3         Addressability to subsystem
         IF    (SMF30TYP,LT,1),IEFU84X ignore if not an interval
         IF    (SMF30TYP,GT,3),IEFU84X
*-
*-       Plug current time block from SCIPCVT into type-30 record
*-
         LT    R3,SMF30IOF         Offset of identification section
         IF    Z,IEFU84X           No identification section, ignore
         CLC   SMF30ION,=XL2'0000' Test count of ident sections
         IF    Z,IEFU84X           Ignore record if zero count
         ALR   R3,R2               Address of identification section
         USING SMF30ID,R3          Addressability to id section
         L     R15,CVTPTR          Access cvt address
         L     R15,CVTUSER-CVT(,R15)   address of scip-cvt
         USING SCIPCVT,R15
         MVC   SMF30JPT+1(1),USEBLOCK  Set execution time block
         DROP  R15
         DROP  R3
         DROP  R2
         SPACE 2
IEFU84X  LABEL ,
         CLEAR R15                 Zero return code, (write record)
         L     R14,@R13+12         Restore return address
         LM    R0,R12,@R13+20      Resotre remaining registers
         BR    R14                 Return to caller
         SPACE 2
         LTORG ,
         DC    10A(0)              Module patch area
         END   .
