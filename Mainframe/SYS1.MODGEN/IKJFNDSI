       MACRO
&LABEL IKJFNDSI &DBCSSTRT,&CBEND,&DBCSEND,&FLAG,&PARMLST,&WORKREG,     +
               &HIBIT
*  **PROPRIETARY_STATEMENT********************************************
* *01* PROPRIETARY STATEMENT=                                        *
*                                                                    *
*                                                                    *
*   LICENSED MATERIALS - PROPERTY OF IBM                             *
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      *
*   5685-025 (C) COPYRIGHT IBM CORP. 1981, 1989                      *
*   SEE COPYRIGHT INSTRUCTIONS                                       *
*                                                                    *
*   STATUS= JTE21X2                                                  *
*                                                                    *
*  **END_OF_PROPRIETARY_STATEMENT*************************************
.**********************************************************************
.*                                                                    *
.* MACRO   NAME = IKJFNDSI                                            *
.*                                                                    *
.* DESCRIPTIVE NAME = PARAMETER LIST AND BAS TO FINDSI IN IKJEFP06    *
.*                                                                    *
.*                                                                    *
.* FUNCTION = IKJFNDSI SETS UP THE PARAMETERS TO BE PASSED TO FINDSI  *
.*             AND INVOKES FINDSI.                                    *
.*                                                                    *
.* MACRO TYPE = MAPPING MACRO                                         *
.*                                                                    *
.* PROCESSOR = ASSEMBLER                                              *
.*                                                                    *
.* ATTRIBUTES = PRIVATE KEY 8 STORAGE                                 *
.*                                                                    *
.* INVOCATION = THIS MACRO CAN ONLY BE INVOKED FROM THE ASSEMBLER     *
.*                                                                    *
.*   {LABEL} IKJFNDSI DBCSSTRT,CBEND,DBCSEND,DBCSFLAGS,DBCSPL,2,HIBIT *
.*                                                                    *
.* STORAGE REQUIRED:                                                  *
.*         DBCSSTRT  - IN RX FORMAT, IS 1 WORD.                       *
.*         CBEND     - IN RX FORMAT, IS 1 WORD.                       *
.*         DBCSEND   - IN RX FORMAT, IS 1 WORD.                       *
.*         DBCSFLAGS - IN RX FORMAT, IS 1 WORD.                       *
.*         DBCSPL    - RX FORMAT, IS 4 WORDS LONG, MINIMUM            *
.*         FINDSI@   - IN RX FORMAT HAS THE ADDRESS OF FINDSI IN      *
.*                     IKJEFP06                                       *
.*         HIBIT     - A FULLWORD CONSTANT SET TO X'80000000'
.*         THE LAST ADDRESS IN THE PARM LIST WILL HAVE IT'S HIGH      *
.*         ORDER BIT SET  TO FOLLOW STANDARD LINKAGE CONVENTIONS.     *
.*                                                                    *
.* REGS USED:                                                         *
.*         USER SUPPLIED WORK REGISTER                                *
.*         REG1 - POINTS TO THE LIST OF PARAMETER ADDRESSES           *
.*         REG14 - USED FOR THE RETURN ADDRESS                        *
.*         REG15 - USED FOR THE EP AND THE RETURN CODE.               *
.*                                                                    *
.* MESSAGES ISSUED:                                                   *
.*         IF ANY OF THE PARAMETERS IS MISSING, A MESSAGE WILL        *
.*         BE PRINTED.                                                *
.*                                                                    *
.* CHANGE ACTIVITIES =                                                *
.*        OZ91711 - CREATED FOR PARSE DOUBLE BYTE CHARACTER SET       *
.*                  SUPPORT.                                          *
.*                                                                    *
.**********************************************************************
.*A000000-999999                                               @ZA91711
        AIF ('&DBCSSTRT' EQ '').ERRSTART
        AIF ('&CBEND' EQ '').ERREND
        AIF ('&DBCSEND' EQ '').ERRDBCSEND
        AIF ('&FLAG' EQ '').ERRFLAG
        AIF ('&PARMLST' EQ '').ERRPLST
        AIF ('&WORKREG' EQ '').ERRREG
        AIF ('&HIBIT' EQ '').ERRBIT
&LABEL    DS  0H
          LA  1,&PARMLST              REG 1 POINTS TO PARM LIST
          LA  &WORKREG,&DBCSSTRT      PUT ADDRESS OF WORD CONTAINING
          ST  &WORKREG,0(,1)          ADDRESS OF SHIFT-OUT CHAR IN
*                                     FIRST WORD OF PARM LIST
          LA  &WORKREG,&CBEND         PUT ADDRESS OF WORD CONTAINING
          ST  &WORKREG,4(,1)          THE ADDRESS OF THE END OF COMMAND
*                                     BUFFER IN SECOND WORD OF
*                                     PARAMETER LIST
          LA  &WORKREG,&DBCSEND       PUT ADDRESS OF WORD THAT WILL
          ST  &WORKREG,8(,1)          CONTAIN THE POINTER TO THE
*                                     SHIFT-IN CHARACTER IN THE THIRD
*                                     WORD OF THE PARAMETER LIST.
          LA  &WORKREG,&FLAG          PUT ADDRESS OF WORD THAT WILL
          O   &WORKREG,&HIBIT         CONTAIN THE DBCS ERROR FLAGS
          ST  &WORKREG,12(,1)         IN FOURTH WORD OF PARM LIST
*                                     AND SET THE HIGH ORDER BIT.
          L   15,FINDSI@              SET UP FOR BALR
          BALR 14,15                  BRANCH TO FINDSI
           AGO  .DONE
.ERRSTART  ANOP
           MNOTE 8,'DBCS STRING START VARIABLE MISSING'
           AGO  .DONE
.ERREND    ANOP
           MNOTE 8,'COMMAND BUFFER END VARIABLE MISSING'
           AGO  .DONE
.ERRDBCSEND ANOP
           MNOTE 8,'DBCS STRING END VARIABLE MISSING'
           AGO  .DONE
.ERRFLAG   ANOP
           MNOTE 8,'DBCS ERROR FLAG VARIABLE MISSING'
           AGO  .DONE
.ERRPLST   ANOP
           MNOTE 8,'MISSING STORAGE FOR THE DBCMSG PARAMETER ADDRESSES'
           AGO  .DONE
.ERRREG    ANOP
           MNOTE 8,'MISSING WORK REGISTER'
           AGO  .DONE
.ERRBIT    ANOP
           MNOTE 8,'MISSING HIGH ORDER BIT CONSTANT'
.DONE   ANOP
        MEXIT
        MEND
