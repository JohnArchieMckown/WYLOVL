*% GOTO PLASEDT2;               /* go to the PLAS version            */ 00050000
*/*                                                                     00100000
         MACRO                                                          00150000
         ICVEDT02 &DSECT=YES,                                          X00200000
               &DT2PRE=,                                               X00250000
               &ENTRY=123                                               00300000
.********************************************************************** 00350000
.*                                                                    * 00400000
.*$MAC(ICVEDT02):  COMMON CVAF/DADSM EXTENT DESCRIPTION TABLE         * 00450000
.*                                                                    * 00500000
.********************************************************************** 00550000
.*                                                                    * 00555500
.*PROPRIETARY V3 STATEMENT                                            * 00561800
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00568100
.*"RESTRICTED MATERIALS OF IBM"                                       * 00574400
.*5695-DF1                                                            * 00580700
.*(C) COPYRIGHT 1993  IBM CORP.                                       * 00587000
.*END PROPRIETARY V3 STATEMENT                                        * 00593300
.*                                                                    * 00600000
.* STATUS = DFSMS/MVS  1.3.0                                          * 00616600
.*                                                                    * 00633200
.* This macro describes the DT2 (NEW) Format for the                  * 00650000
.* INTERNAL DADSM/CVAF EXTENT DESCRIPTOR TABLE                        * 00700000
.*                                                                    * 00750000
.* This is a bi-lingual macro.                                        * 00800000
.*                                                                    * 00850000
.* LINKAGE =                                                          * 00900000
.*                                                                    * 00950000
.*  ASSEMBLER:                                                        * 01000000
.*     LABEL  ICVEDT02 DSECT=YES|NO                                   * 01050000
.*                     DT2PRE=xxx                                     * 01100000
.*                     ENTRY =123 = default                           * 01150000
.*                                                                    * 01200000
.*  Description of assembler keywords.                                * 01250000
.*                                                                    * 01300000
.*                                                                    * 01350000
.*    DSECT     - This keyword indicates whether a DSECT card is      * 01400000
.*                created.                                            * 01450000
.*                If DSECT=NO is specified then, a CSECT CARD IS      * 01500000
.*                NOT generated.                                      * 01550000
.*                                                                    * 01600000
.*                DSECT=YES is the default.                           * 01650000
.*                                                                    * 01700000
.*    DT2PRE    - This keyword allows the caller to specify the       * 01750000
.*                first three letters of the labels associated with   * 01800000
.*                the fields described in the DT2 (NEW) format of the * 01850000
.*                DADSM table this is requested.                      * 01900000
.*                                                                    * 01950000
.*                DT2PRE=DT2 is the DEFAULT value.                    * 02000000
.*                                                                    * 02050000
.*    ENTRY     - This keyword allows the caller to specify the       * 02100000
.*                number of entries that will be in the DADSM         * 02150000
.*                table.   If TYPE=BOTH is specifed, ENTRY will       * 02200000
.*                create the DT1 and DT2 format with the same number  * 02250000
.*                of entries.                                         * 02300000
.*                                                                    * 02350000
.*                ENTRY=123 is the DEFAULT value.                     * 02400000
.********************************************************************** 02450000
.*  PLAS:                                                             * 02500000
.*   %INCLUDE SYSLIB(ICVEDT02)                                        * 02550000
.*                                                                    * 02600000
.*   Description of PLAS macro variables.                             * 02650000
.*                                                                    * 02700000
.*   %DT2_EDT_BASE  - If not blank, contains the basing expression    * 02750000
.*                    for the DT2 format extent descriptor table (EDT)* 02800000
.*                                                                    * 02850000
.*                    If blank then the structure is BASED            * 02900000
.*                                                                    * 02950000
.*                    If 'AUTO' is specified then the structure       * 03000000
.*                    is placed in the callers AUTODATA area.         * 03050000
.*                                                                    * 03100000
.*   %DT2_EDT_ENTRY_BASE -                                            * 03150000
.*                    If not blank, contains the basing expression    * 03200000
.*                    for a single DT2 format extent descripton entry.* 03250000
.*                                                                    * 03300000
.*                    If blank then the structure is BASED.           * 03350000
.*                                                                    * 03400000
.*                    If 'AUTO' is specified then the structure       * 03450000
.*                    is placed in the callers AUTODATA area.         * 03500000
.*                                                                    * 03550000
.*                                                                    * 03600000
.*   %EDT_ENTRY     - The number of extent descrition entries the     * 03650000
.*                    table is to contain.                            * 03700000
.*                                                                    * 03750000
.*                    If '*' is specified then an open ended array    * 03760000
.*                    is created.  The LENGHTC constant is not        * 03770000
.*                    created.                                        * 03780000
.*                                                                    * 03790000
.*                    The default value = 123.                        * 03800000
.*                                                                    * 03850000
.*--------------------   DT2 FORMAT DESCRIPTION    -------------------* 03900000
.*                                                                    * 03950000
.* This section describes the DT2 (NEW) DADSM Format for the          * 04000000
.* INTERNAL DADSM/CVAF EXTENT DESCRIPTOR TABLE                        * 04050000
.*                                                                    * 04100000
.* Each data set extent in the table is represented by 2 4-byte       * 04150000
.* relative track addresses                                           * 04200000
.*                                                                    * 04250000
.*    Start RTA - This extent starts on this relative track from      * 04300000
.*                the start of the volume.                            * 04350000
.*                                                                    * 04400000
.*    End RTA+1 - This extent ends on this relativie track address    * 04450000
.*                + 1. (start value of the next extent)               * 04500000
.*                                                                    * 04550000
.*    The COMMON DADSM/CVAF EXTENT TABLE contains 36 bytes of header  * 04600000
.*    information followed by 123 8-byte extent entries.              * 04650000
.*                                                                    * 04700000
.*    (123 extent entries is the default value.                       * 04750000
.*     If the keyword %EDT_ENTRY is not specified.)                   * 04800000
.*                                                                    * 04850000
.*       EYECATCHER   - 8 byte EBCDIC field  (set to ICVEDT02)        * 04852500
.*       LENGTH       - 4 byte fixed field                            * 04855000
.*       LEVEL        - 1 byte fixed field                            * 04857500
.*       FLAG         - 1 byte binary                                 * 04860000
.*       HOLENUM      - 2 byte field,  number of F0's in the vtoc     * 04862500
.*       CCHHR CURSOR - 5 byte field, initialized to binary zero's    * 04865000
.*                      by the caller and ONLY modified by CVAF       * 04867500
.*                      modules.                                      * 04870000
.*                      (Used by CVAF as a starting point when        * 04872500
.*                       multiple (looping) calls are made.)          * 04875000
.*                                                                    * 04877500
.*       RTA CURSOR   - 4 byte field, initialized to binary zero's    * 04880000
.*                      by the caller and ONLY modified by CVAF       * 04882500
.*                      modules.                                      * 04885000
.*                      (Used by CVAF as a starting point when        * 04887500
.*                       multiple (looping) calls are made.)          * 04890000
.*                                                                    * 04892500
.*       # OF ENTRIES - number of entries in the COMMON DADSM/CVAF    * 04895000
.*                      EXTENT TABLE.  (DEFAULT = 123)                * 04897500
.*                                                                    * 04900000
.* NOTE - THIS MACRO INVOCATION CREATES A CONSTANT.  IT MUST NOT      * 04950000
.*        BE INVOKED IN THE MIDDLE OF EXECUTABLE CODE.                * 05000000
.*                                                                    * 05050000
.*                                                                    * 05100000
.*     DT2 (NEW) COMMON DADSM/CVAF EXTENT DESCRIPTION TABLE           * 05150000
.*        +-------------------------------------------+               * 05200000
.*  0   0 |  EYECATCHER (ICVEDT02)                    |               * 05207600
.*  4   4 |                                           |               * 05215200
.*        +-------------------------------------------+               * 05222800
.*  8   8 |  LENGTH                                   |               * 05230400
.*        +-------------------------------------------+               * 05238000
.* 12   C |  LEVEL   |  FLAG     |  HOLENUM           |               * 05245600
.*        +-------------------------------------------+               * 05253200
.* 16  10 | CCHHR CURSOR FIELD                        |               * 05255300
.*        +          +--------------------------------+               * 05257400
.* 20  14 |          |     3 BYTES RESERVED           |               * 05259200
.*        +----------+--------------------------------+               * 05261000
.* 24  18 |  RTA CURSOR FIELD                         |               * 05262800
.*        +-------------------------------------------+               * 05264600
.* 28  1C |  4   BYTES RESERVED                       |               * 05266400
.*        +-------------------------------------------+               * 05268400
.* 32  20 | NUMBER OF ENTRIES                         |               * 05309200
.*        +-------------------------------------------+               * 05350000
.* 36  24 |     START RTA of the extent               |               * 05400000
.*        +-------------------------------------------+               * 05750000
.*        |     END RTA + 1 of the extent             |               * 05800000
.*        +-------------------------------------------+               * 05850000
.*                   :                    :                           * 05900000
.*                   :                    :                           * 05950000
.*        +-------------------------------------------+               * 06000000
.*        |     START RTA of the extent               |               * 06050000
.*        +-------------------------------------------+               * 06100000
.*        |     END RTA + 1 of the extent             |               * 06150000
.*        +-------------------------------------------+               * 06200000
.*                                                                    * 06250000
.*                                                                    * 06300000
.*                                                                    * 06350000
.********************************************************************** 06400000
.*MODULE-TYPE = MACRO                                                 * 06450000
.*                                                                    * 06500000
.*PROCESSOR = ASSEMBLER                                               * 06550000
.*                                                                    * 06600000
.*CHANGE ACTIVITY =                                                   * 06650000
.*                                                                    * 06700000
.*                                                                    * 06750000
.*$L0=64KTRACK,JDZ1110,920310,SJPLMKD: 64K TRACK SUPPORT          @L0A* 06800000
.*$01=OW06564,HDZ11B0,940815,SJPLRAT: ASSY ERRORS ~CALLED FR DSECT@01A* 06825000
.*                                                                    * 06850000
.**** END OF SPECIFICATIONS ******************************************* 06900000
.********************************************************************** 06950000
.*    declare local variables                                         * 07000000
.********************************************************************** 07050000
         LCLB  &GDSECT      GENERATE DSECT                              07100000
         LCLB  &NODSECT     NO DSECT STATEMENT                          07150000
         LCLB  &ERRSW       ERROR MESSAGE MNOTE ISSUED                  07200000
         LCLC  &N           VARIABLE PREFIX TO USE FOR DT2 TABLE FORMAT 07250000
         LCLA  &DT2LEN      NUMBER OF CHARACTERS IN DT2 PREFIX          07300000
         LCLA  &DT2SIZE     TOTAL SIZE OF THE DT2 DADSM TABLE           07350000
.********************************************************************** 07400000
.*    SET DEFAULT VALUES                                              * 07450000
.********************************************************************** 07500000
&GDSECT  SETB  1             ASSUME THAT DSECT SHOULD BE GENERATED      07550000
.********************************************************************** 07600000
.*    SET PREFIX VALUES FOR THE VARIABLES                             * 07650000
.*    EACH VARIABLE WILL HAVE THE VALUE XXXyyyyy                      * 07700000
.*    Where XXX is the value supplied by the caller.                  * 07750000
.********************************************************************** 07800000
.C1N     AIF   ('&DT2PRE' EQ '').C1NDEF                                 07850000
&N       SETC  '&DT2PRE'                        SET USER SUPPLIED VALUE 07900000
         AGO   .C1NEND                                                  07950000
.C1NDEF  ANOP                                                           08000000
&N       SETC  'DT2'                            SET DEFAULT VALUE       08050000
.C1NEND  ANOP                                                           08100000
.********************************************************************** 08150000
.* CHECK THE LENGTH of the DT2PRE                                     * 08200000
.********************************************************************** 08250000
&DT2LEN  SETA  K'&DT2PRE              OBTAIN THE LENGTH OF &DT2PRE      08300000
.C1      AIF   (&DT2LEN LT 4).C1DT1                                     08350000
.********************************************************************** 08400000
.C1ERR   ANOP                           ERROR MESSAGE                   08450000
         SPACE 1                                                        08500000
         MNOTE 8,'ICVEDT02 - INVALID OPERAND - DT2PRE=&N'               08550000
&ERRSW   SETB  1                                                        08600000
.C1DT1   ANOP                                                           08650000
.********************************************************************** 08700000
.* VERIFY THE DSECT PARAMETER                                         * 08750000
.********************************************************************** 08800000
.V2      AIF   ('&DSECT' EQ 'YES').V2X  IF =YES, BRANCH                 08850000
         AIF   ('&DSECT' NE 'NO').V2ER1 IF NOT =NO, ERROR               08900000
&NODSECT SETB  1                        NO DSECT STATEMENT              08950000
         AGO   .V2X                     VERIFICATION COMPLETE           09000000
.*                                                                      09050000
.V2ER1   ANOP                           ERROR MESSAGE                   09100000
         SPACE 1                                                        09150000
         MNOTE 8,'ICVEDT02 - INVALID OPERAND - DSECT=&DSECT'            09200000
&ERRSW   SETB  1                                                        09250000
.*                                                                      09300000
.V2X     ANOP                                                           09350000
.********************************************************************** 09400000
.* IF DSECT=YES THEN &DT2PRE IS NOT ALLOWED.                          * 09450000
.********************************************************************** 09500000
.V3      AIF   ('&DSECT' EQ 'NO').V3X   IF =NO, BRANCH, NO CHECKING     09550000
         AIF   ('&DT2PRE' NE '').V3ER1                                  09600000
         AGO   .V3X                                                     09650000
.*                                                                      09700000
.V3ER1   ANOP                           ERROR MESSAGE                   09750000
         SPACE 1                                                        09800000
         MNOTE 8,'ICVEDT02 - INVALID OPERAND COMBINATION'               09850000
         MNOTE 8,'ICVEDT02 - DSECT=YES AND DT2PRE=&DT2PRE ARE INVALID'  09900000
&ERRSW   SETB  1                                                        09950000
.*                                                                      10000000
.V3X     ANOP                                                           10050000
.********************************************************************** 10100000
.* CHECK ERRORS                                                       * 10150000
.********************************************************************** 10200000
.*                                                                      10250000
.ERRXX   AIF   (&ERRSW).FINISH              BRANCH TO THE END IF ERRORS 10300000
.*                                                                      10350000
.********************************************************************** 10400000
.* BUILD CORRECT MACRO TYPE (DSECT or CSECT)                          * 10450000
.********************************************************************** 10500000
         SPACE 2                                                        10550000
*********************************************************************** 10600000
*        DT2 FORMAT DADSM/CVAF EXTENT DESCRIPTOR TABLE                * 10650000
*********************************************************************** 10700000
         AIF   (&NODSECT).S1B                                           10750000
&N.EDT02 DSECT                                                          10800000
         AGO   .S1C                                                     10850000
.S1B     ANOP                                                           10900000
&N.EDT02 DS    0F                                                       10950000
.S1C     ANOP                                                           11000000
.********************************************************************** 11050000
&DT2SIZE SETA  &ENTRY*8+36              SIZE = NUMBER OF SPECIFIED      11100000
.*                                      ENTRIES*LENGTH OF EACH ENTRY+   11150000
.*                                      LENGTH OF THE HEADER            11200000
         DS    0CL&DT2SIZE              DADSM EXTENT TABLE              11250000
&N.X7HDR DS    0CL36                    CONTROL BLOCK HEADER            11300000
&N.X7EYE DS    CL8                      EYECATCHER                      11350000
&N.X7LEN DS    XL4                      LENGTH OF DADSM EXTENT TABLE    11400000
&N.X7LEV DS    XL1                      LEVEL NUMBER                    11450000
&N.X7FLG DS    XL1                      TYPE FLAG                       11500000
*                                                                       11550000
&N.X7NF0 DS    XL2                      NUMBER OF F0'S CREATED          11600000
&N.X7CSR DS    CL5                      CCHHR CURSOR                    11616600
&N.RESRV DS    CL3                      RESERVED SPACE                  11624900
&N.X7RTA DS    CL4                      RTA CURSOR                      11633200
&N.RESR2 DS    CL4                      RESERVED SPACE                  11641500
&N.X7ENT DS    XL4                      NUMBER OF ENTRIES IN DADSMTBL   11650000
         ORG   &N.X7ENT                                                 11700000
&N.ENTNM DS    XL4                      NUMBER OF ENTRIES IN DADSMTBL   11750000
*********************************************************************** 11800000
*        COMMON DADSM TABLE HEADER CONSTANTS.                         * 11850000
*********************************************************************** 11900000
&N.LEV   EQU   X'01'                                                    11950000
&N.LEN   EQU   &N.END-&N.X7HDR                                          12000000
*********************************************************************** 12050000
*        SPACE FOR SPECIFIED DADSM TABLE ENTRIES                      * 12100000
*********************************************************************** 12150000
&N.ENTRY DS    CL(&ENTRY*L'&N.EXT)     8 BYTES * NUMBER OF ENTRIES      12200000
*                                      SPECIFIED                        12250000
&N.END   EQU   *                                                        12300000
&N.MEN   EQU   &ENTRY                                                   12350000
*                                                                       12400000
&N.ARENT DSECT                                                          12450000
*********************************************************************** 12500000
*        SINGLE COMMON DADSM TABLE ENTRY DESCRIPTION  (8 BYTES)       * 12550000
*********************************************************************** 12600000
&N.EXT   DS    0XL8                    MAPPING OF ONE DT2 EXTENT        12650000
*                                      DESCRIPTOR                       12700000
&N.RTAST DS    CL4                     RTA OF FIRST TRACK OF FREE       12750000
*                                      EXTENT                           12800000
&N.RTAED DS    CL4                     RTA+1 OF LAST TRACK OF FREE      12850000
*                                      EXTENT                           12900000
         MEND                                                           13050000
*                                                                       13100000
*---------------------- END OF ASSEMBLER PORTION --------------------*/ 13150000
*/********************************************************************/ 13200000
*/*                                                                  */ 13250000
*/*                                                                  */ 13300000
*/*      PLAS VERSION OF THE EXTENT DESCRIPTION TABLE                */ 13350000
*/*                                                                  */ 13400000
*/*                                                                  */ 13450000
*/********************************************************************/ 13500000
* %PLASEDT2:;                                                           13550000
*@SPACE(2);                                                             13600000
*                                                                       13650000
*                                                                       13700000
*%DCL DT2_EDT_BASING       CHAR INT;                                    13750000
*%DCL DT2_EDT_ENTRY_BASING CHAR INT;                                    13800000
*                                                                       13850000
*%DCL DT2_PRE        CHAR INT;                                          13900000
*                                                                       13950000
*%DCL ENTRY_NUM2     CHAR  INT;                                         14000000
*                                                                       14050000
*%IF DT2_EDT_BASE = ''                                                  14300000
*    %THEN  %DT2_EDT_BASING = 'BASED';                                  14350000
*    %ELSE  %DT2_EDT_BASING = 'BASED('||DT2_EDT_BASE||')';              14400000
*%IF DT2_EDT_BASE = 'AUTO'                                              14450000
*    %THEN  %DT2_EDT_BASING = '/* */';                                  14500000
*                                                                       14550000
*%IF DT2_EDT_ENTRY_BASE = ''                                            14600000
*    %THEN  %DT2_EDT_ENTRY_BASING = 'BASED';                            14650000
*    %ELSE  %DT2_EDT_ENTRY_BASING = 'BASED('||DT2_EDT_ENTRY_BASE||')';  14700000
*%IF DT2_EDT_ENTRY_BASE = 'AUTO'                                        14750000
*    %THEN  %DT2_EDT_ENTRY_BASING = '/* */';                            14800000
*                                                                       14850000
*                                                                       15050000
*%IF EDT_ENTRY = ' '                                                    15100000
*    %THEN  %ENTRY_NUM2 = '123';                                        15150000
*    %ELSE  %ENTRY_NUM2 = EDT_ENTRY;                                    15200000
*@SPACE(2);                                                             15250000
*                                                                       15300000
*      DECLARE                                                          15350000
*         1 DT2EDT02 DT2_EDT_BASING BDY(DWORD),                         15400000
*           3 DT2X7HDR,                                                 15450000
*             5 DT2X7EYE    CHAR(8),      /*  eyecatcher             */ 15493700
*             5 DT2X7LEN    BINARY(31),   /*  length of EDT02        */ 15537400
*             5 DT2X7LEV    BINARY(8),    /*  level number           */ 15581100
*             5 DT2X7FLG    BINARY(8),    /*  flag byte.             */ 15624800
*             5 DT2X7NF0    BINARY(16),   /*  number of F0's created */ 15668500
*             5 DT2X7CSR    CHAR(5),      /*  CCHHR cursor field.    */ 15697600
*                                         /*  USED by CVAF.          */ 15701200
*                                         /*  Initialize to ZERO's,  */ 15704800
*                                         /*  then DO NOT MODIFY.    */ 15708400
*             5 DT2X7RES    CHAR(3),      /*  reserved space         */ 15712100
*             5 DT2X7RTA    BINARY(32),   /*  RTA cursor field.      */ 15726600
*                                         /*  USED by CVAF.          */ 15730200
*                                         /*  Initialize to ZERO's,  */ 15733800
*                                         /*  then DO NOT MODIFY.    */ 15737400
*             5 DT2X7RE2    CHAR(4),      /*  reserved space         */ 15741100
*             5 DT2X7ENT,                 /*  number of entries      */ 15755900
*               7 DT2ENTNM  FIXED(31),                                  15800000
*%IF EDT_ENTRY = '*'                                                    15806200
*    %THEN                                                              15812400
*       %DO;                                                            15818600
*           3 DT2ENTRIES    CHAR(*);                                    15824800
*       %END;                                                           15831000
*    %ELSE                                                              15837200
*       %DO;                                                            15843400
*           3 DT2ENTRIES    CHAR(ENTRY_NUM2*LENGTH(DT2EXT));            15850000
*       %END;                                                           15875000
*                                                                       15900000
*      @SPACE(2);                                                       15950000
*                                                                       16000000
*      DECLARE                                                          16050000
*         1 DT2EXT DT2_EDT_ENTRY_BASING BDY(DWORD),                     16100000
*           3 DT2RTAST      FIXED(32),                                  16150000
*           3 DT2RTAED      FIXED(32);                                  16200000
*                                                                       16250000
*      DECLARE                                                          16300000
*         1 DT2ENTRY_ARRAY(*) BASED(ADDR(DT2ENTRIES)),                  16350000
*           3 DT2RTAST_ENT      FIXED(32),                              16400000
*           3 DT2RTAED_ENT      FIXED(32);                              16450000
*                                                                       16500000
*      /**************************************************************/ 16550000
*      /*  The following are constant declarations for DT2EDT02      */ 16600000
*      /**************************************************************/ 16650000
*                                                                       16700000
*      DECLARE                                                          16750000
*         EYECATCHER CHAR(8) CONSTANT('ICVEDT02'),                      16800000
*                                    /* DT2 format EDT eyecatcher    */ 16850000
*%IF EDT_ENTRY ~= '*'                                                   16862500
*    %THEN                                                              16875000
*       %DO;                                                            16887500
*         LENGTHC    FIXED(31) CONSTANT(LENGTH(DT2EDT02)),              16900000
*                                    /* length of DT2 format EDT.    */ 16950000
*       %END;                                                           16975000
*         LEVELC     FIXED(8) CONSTANT(1); /* DT2EDT02 level number  */ 17000000
*                                                                       17050000
*                                                                       17100000
