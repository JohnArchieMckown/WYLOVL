         MACRO                                                          00050000
&NAME    IOSINTRP &UCB=(1),&VAR=1,&BRANCH=YES                  @ZA16163 00100000
.********************************************************************** 00150000
.*   COPYRIGHT =                                                        00200000
.*     5695-047                                                         00250000
.*     THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      00300000
.*     (C) COPYRIGHT IBM CORP. 1982, 1992                               00350000
.*     LICENSED MATERIALS - PROPERTY OF IBM                             00400000
.*                                                                      00550000
.*   STATUS = HBB4430                                                   00600000
.*                                                                      00650000
.*   CHANGE ACTIVITY =                                                  00675000
.*                                                                      00700000
.* $MAC(IOSINTRP),COMP(SC1C3): Simulate an I/O interruption             00707100
.*                                                                      00714200
.* $L1=VERS    HBB3310 860416 PDLE: Versioning macro into product       00721300
.* $P1=PKB3741 HBB4430 920729 PDLH: Support 31 bit addressing           00724800
.* $P2=PKB3920 HBB4430 920729 PDLH: Fix period before AGO problem       00726600
.*                                                                      00728400
.*                                                                      00735500
.********************************************************************** 00742600
.*       CHECK FOR MISSING PARAMETERS                                   00750000
.*                                                                      00800000
         AIF   ('&BRANCH' NE 'YES').IOSGEN3                             00850000
         AIF   ('&VAR' EQ '3').IOSGEN4  Version 3 not supported    @P1C 00883300
         AIF   ('&VAR' EQ '2').SIM001   Version 2 for 31 bit addr  @P1C 00916600
         AIF   ('&VAR' NE '1').IOSGEN3  Version 1 base code    @G860PLT 00950000
.SIM001  ANOP                                                      @P1A 00975000
         SPACE 2                                                        01000000
*********************************************************************** 01044900
*                                                                     * 01089800
*01*    MACRO NAME = IOSINTRP                                         * 01134700
*                                                                     * 01179600
*01*    DESCRIPTIVE NAME = INTRP - Simulate an I/O Interruption       * 01224500
*                                                                     * 01269400
*01*    FUNCTION =                                                    * 01314300
*         Simulates a Device End if UCBNRY or UCBNOPTH is ON.         * 01359200
*         In all cases an interrupt is simulated via a LPSW           * 01404100
*         which in turn will cause a REDRIVE to be done.       @G860PLT 01449000
*                                                                     * 01493900
*01*    MACRO CALL =                                                  * 01538800
*         LABEL  IOSINTRP UCB=(REG),VAR=1                      @G860PLT 01583700
*                                                                     * 01628600
*               PLS - GEN REFS(CVTPTR,CVTIXAVL,CVTMAP,                * 01673500
*                     IOCGENA,IOCOM)                                  * 01718400
*                     (IOSINTRP KEYWORDS AS IN BAL);                  * 01763300
*                                                                     * 01808200
*01*    INPUT =                                                       * 01853100
*          UCB=  May specify a GPR (Symbolic or absolute), which      * 01898000
*               is to contain the address of the UCB common segment   * 01942900
*               for which the interrupt is to be simulated.           * 01987800
*               The default is UCB=(1).                               * 02032700
*          VAR=  Version 1 is the default.  VAR=2 is to be used with  * 02047600
*                31 bit addresses                                     * 02062500
*                                                                     * 02077600
*          LABEL - Optional.                                          * 02122500
*                                                                     * 02167400
*01*    OUTPUT =                                                      * 02212300
*          If the UCB for the subchannel has UCBNRY (Not Ready) or    * 02257200
*          UCBNOPTH (No Paths) ON, then an unsolicited device end     * 02302100
*          will be simulated (via a call to IOSVIRBA).                * 02347000
*          In all cases an interrupt is simulated via a LPSW which    * 02391900
*          in turn will cause a subchannel redrive to occur.          * 02436800
*                                                              @G860PLT 02481700
*                                                                     * 02526600
*01*    ATTRIBUTES =                                                  * 02571500
*          This function is expanded as re-entrant code.              * 02616400
*                                                                     * 02661300
*01*    OTHER =                                                       * 02706200
*          Register 13 must contain the address of a 16 word          * 02751100
*          save area. The registers are stored 0 thru 15 from         * 02796000
*          offset 0.                                                  * 02840900
*                                                                     * 02885800
*          The expansion of this function is dependent on the         * 02930700
*          specification of the following mapping macros              * 02975600
*          in the calling CSECT assembly:                             * 03020500
*               IECDIOCM                                              * 03065400
*               CVT DSECT=YES                                         * 03110300
*                                                                     * 03155200
*          This routine modifies the I/O OLD PSW and serializes       * 03200100
*          its operation via STNSM instruction throughout the         * 03245000
*          interrupt process.      THE RESUME ADDRESS FIELD OF THE    * 03289900
*          The resume field of the I/O OLD PSW is set to the last     * 03334800
*          instruction of the expansion.                              * 03379700
*                                                                     * 03424600
*          The UCB lock is held while simulating the unsolicited      * 03469500
*          device end interrupt.                               @G860PLT 03514400
*                                                                     * 03559300
*********************************************************************** 03604200
         SPACE 2                                                        03650000
&NAME    STM   0,15,0(13)       Save all registers             @ZA16163 03700000
         AIF   ('&UCB' EQ '(1)').SIM010                        @G860PLT 03750000
         LR    1,&UCB(1)        Put UCB address in register 1  @G860PLT 03800000
.SIM010  ANOP                                                  @G860PLT 03850000
         L     15,CVTPTR(0,0)   Get CVT address                @ZA16163 03900000
         L     15,CVTIXAVL-CVTMAP(15) Get IOCOM address        @ZA16163 03950000
         L     15,IOCGENA-IOCOM(15) Get IECVGENA address       @ZA16163 04000000
         AIF   ('&VAR' EQ '2').SIMX                                @P1A 04025000
         BAL   14,8(15)         Call simulate function         @ZA16163 04050000
         AGO  .BYSIMX                                              @P2C 04075000
.SIMX    BAL   14,40(15)        Call simulate extended function    @P1A 04100000
.BYSIMX  LM    0,15,0(13)       Restore registers                  @P1C 04125000
         SPACE                                                          04150000
         MEXIT                                                          04200000
.*                                                                      04250000
.IOSGEN3 MNOTE 8,'*** ERROR IN PARAMETER SET UP.'                       04300000
         MEXIT                                                 @G860PLT 04350000
.IOSGEN4 MNOTE 8,'*** VAR=3 IS NOT A SUPPORTED OPTION.'            @P1C 04400000
         MEND                                                           04450000
