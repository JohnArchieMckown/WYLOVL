         MACRO                                                          00050000
&NAME    IOHALT  &UCB,&OFFSET                                           00100000
.********************************************************************** 00150000
.*01* PROPRIETARY STATEMENT:                                          * 00175000
.*                                                                    * 00200000
.*     LICENSED MATERIALS - PROPERTY OF IBM                           * 00450000
.*     THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    * 00458300
.*     5695-047 (C) COPYRIGHT IBM CORP. 1982, 1992                    * 00466600
.*     SEE COPYRIGHT INSTRUCTIONS                                     * 00474900
.*                                                                    * 00483200
.* STATUS= HBB4430                                                    * 00491500
.*                                                                    * 00500000
.********************************************************************** 00550000
.********************************************************************** 00600000
.*                     IOHALT MACRO                                     00650000
.*  OS/VS2                                                              00700000
.*  FUNCTION:                                                           00750000
.*       SETS UP REGISTERS 0 & 1 IN FORMAT REQUIRED BY SVC33            00800000
.*                                                                      00850000
.*  MACRO CALL:                                                         00900000
.*       LABEL  IOHALT &UCB,&OFFSET                                     00950000
.*                                                                      01000000
.*       WHERE:                                                         01050000
.*             LABEL - OPTIONAL LABEL                                   01100000
.*                                                                      01150000
.*             &UCB = (REGISTER), SYMBOLIC OR ABSOLUTE, WHICH           01200000
.*                    CONTAINS THE ADDRESS OF THE UCB COMMON SEGMENT    01250000
.*                                     -OR-                             01300000
.*                    LABEL OF UCB COMMON SEGMENT (LA WILL BE DONE)     01350000
.*                                                                      01400000
.*             &OFFSET = (REGISTER), SYMBOLIC 0,2-15 OR ABSOLUTE 0-15,  01450000
.*                    WHICH CONTAINS THE OFFSET FROM THE IOB TO THE     01500000
.*                    CCW TO BE MODIFIED TO A NOP                       01550000
.*                                     -OR-                             01600000
.*                    VALUE, SYMBOLIC OR ABSOLUTE, OF THE OFFSET FROM   01650000
.*                    THE IOB TO THE CCW TO BE MODIFIED TO A NOP (LA    01700000
.*                    WILL BE DONE)                                     01750000
.*                                                                      01800000
.*             NOTE:                                                    01850000
.*                  &UCB MUST BE SUPPLIED                               01900000
.*                                                                      01950000
.*                  &OFFSET IS OPTIONAL.                                02000000
.*                       IF CODED, SVC33 WILL CALL THE EXCP             02050000
.*                       MODIFY ROUTINE. OTHERWISE, SVC33 WILL          02100000
.*                       CALL THE IOS HALT I/O ROUTINE.                 02150000
.*                                                                      02200000
.*                  IF THE OFFSET PARAMETER IS CODED USING A SYMBOLIC   02250000
.*                  REGISTER 1, AN ASSEMBLY ERROR WILL OCCUR.           02300000
.*                                                                      02350000
.*  OUTPUT:                                                             02400000
.*       REGISTER 0 - CONTAINS THE UCB COMMON SEGMENT ADDRESS           02450000
.*                                                                      02500000
.*       REGISTER 1 -                                                   02550000
.*             BYTE 0-1 = IOB OFFSET (IF SUPPLIED) OR ZERO              02600000
.*             BYTE   2 = ZERO (RESERVED)                               02650000
.*             BYTE   3 = OPTION INDICATOR                              02700000
.*                        X'01' - USE IOS HALT I/O ROUTINE              02750000
.*                                (&OFFSET NOT CODED)                   02800000
.*                        X'81' - USE EXCP MODIFY ROUTINE               02850000
.*                                (&OFFSET WAS CODED)                   02900000
.*                                                                      02950000
.*      SVC 33 ISSUED                                                   03000000
.*                                                                      03050000
.*  CHANGE ACTIVITY                                                     03100000
.*   A-000000-999999                                           @ZA43366 03150000
.*                                                                      03200000
.*   $MAC(IOHALT),COMP(SC1C3):                                          03250000
.*                                                                      03300000
.*   $L1=VERS    ,HBB3310,860416,PDLE: VERSIONING MACRO INTO PRODUCT    03350000
.*                                                                 @L1A 03400000
.*   $P1=PKB0500 ,HBB4430,920318,PDLL: 10X Defect Elimination           03412500
.*                                     Remove MNOTE because it is       03425000
.*                                     misleading and confusing.   @P1A 03437500
.********************************************************************** 03450000
**** THIS VERSION OF THE IOHALT MACRO USES THE ALTERNATE ****      @P1C 03500000
****                 INTERFACE TO SVC 33                 ****      @P1C 03550000
* IOHALT - THIS MACRO SETS UP REGISTERS 0 AND 1 FOR SVC33 AS FOLLOWS:   03600000
*          REGISTER 0 - UCB COMMON SEGMENT ADDRESS (1ST PARAMETER)      03650000
*          REGISTER 1 - IOB OFFSET, IF SUPPLIED (2ND PARAMETER)         03700000
*                       AND OPTION INDICATOR (GENERATED BY MACRO BASED  03750000
*                       ON WHETHER THE 2ND POSITIONAL PARAMETER WAS     03800000
*                       SUPPLIED)                                       03850000
*                                                                       03900000
         AIF   ('&UCB' EQ '').E1        ERROR IF UCB PARM NOT SUPPLIED  03950000
         AIF   ('&NAME' EQ '').CHKUCB                                   04000000
&NAME    DS    0H                                                       04050000
.CHKUCB  ANOP                                                           04100000
         AIF   ('&OFFSET' NE '').MODOPT IF MODIFY OPTION,GO TO MOD RTN  04150000
.* ****** IOHALT  ONLY, PROCESSED HERE                                  04200000
         AIF   ('&UCB' EQ '(0)').HALTCD  UCB ALREADY IN REG 0           04250000
         AIF   ('&UCB'(1,1) EQ '(').HALTREG  UCB IN REGISTER            04300000
         LA    0,&UCB               PUT UCB ADDRESS IN REG0             04350000
         AGO   .HALTCD                                                  04400000
.HALTREG ANOP                                                           04450000
         LR    0,&UCB(1)            PUT UCB ADDRESS IN REG0             04500000
.HALTCD  ANOP                                                           04550000
         LA    1,1(0)               TURN ON LOW ORDER BIT TO           X04600000
                                    INDICATE IOHALT REQUESTED           04650000
         SVC   33                   ISSUE IOHALT SVC                    04700000
         MEXIT                                                          04750000
.* MODIFY OPTION REQUESTED                                              04800000
.*                                                                      04850000
.*    **** BECAUSE IT MAY NOT BE POSSIBLE TO DETERMINE IF THE UCB       04900000
.*    **** IS BEING PASSED IN REG1 AND/OR THE OFFSET IN REG0 (WHICH     04950000
.*    **** WAS THE OLD INTERFACE TO SVC33), THE REGS ARE                05000000
.*    **** FIRST SET UP IN THE OLD FORMAT (IE. UCB IN REG1 AND          05050000
.*    **** OFFSET IN REG0) AND THEN EXCHANGED.                          05100000
.*    **** BEFORE THIS IS DONE THOUGH, CHECKS ARE MADE TO DETERMINE     05150000
.*    **** IF THE PARAMETERS CAN BE PUT DIRECTLY INTO THE NEW           05200000
.*    **** PARAMETER REGISTERS (IE. UCB IN REG0, OFFSET IN REG1)        05250000
.*    **** IN ORDER TO AVOID THE EXTRA CODE CAUSED BY THE XR'S.         05300000
.MODOPT  ANOP                                                           05350000
         AIF   ('&UCB' EQ '(1)' AND '&OFFSET' EQ '(0)').XCHNG           05400000
         AIF   ('&UCB' EQ '(0)' AND '&OFFSET' EQ '(1)').MODCD           05450000
.*                             PARAMETERS ALREADY SET UP                05500000
         AIF   ('&UCB'(1,1) EQ '('  OR '&OFFSET'(1,1)  EQ '(').MOD010   05550000
.* ******************                                                   05600000
.*    **** THIS SECTION HANDLES THE CASE WHERE NEITHER PARAMETER IS     05650000
.*         IN A REGISTER                                                05700000
.* ******************                                                   05750000
         LA    0,&UCB               PUT UCB ADDRESS IN REG0             05800000
         LA    1,&OFFSET            PUT OFFSET IN REG1                  05850000
         AGO   .MODCD                                                   05900000
.MOD010  ANOP                   AT LEAST ONE PARAMETER IS IN A REGISTER 05950000
         AIF   ('&UCB' NE '(0)').MOD020                                 06000000
.* ******************                                                   06050000
.*    **** UCB ALREADY IN REG0, PUT OFFSET IN REG1                      06100000
.* ******************                                                   06150000
         AIF   ('&OFFSET'(1,1) EQ '(').MOD015 CHECK IF OFFSET IN REG    06200000
         LA    1,&OFFSET            PUT OFFSET IN REG1                  06250000
         AGO   .MODCD                                                   06300000
.MOD015  ANOP                                                           06350000
         LR    1,&OFFSET(1)         PUT OFFSET IN REG1                  06400000
         AGO   .MODCD                                                   06450000
.MOD020  ANOP                                                           06500000
         AIF   ('&UCB'(1,1) NE '(' OR '&OFFSET'(1,1) EQ '(').MOD030     06550000
.* ******************                                                   06600000
.*    **** UCB IN REG AND OFFSET NOT IN REG                             06650000
.* ******************                                                   06700000
         LR    0,&UCB(1)            PUT UCB ADDRESS IN REG0             06750000
         LA    1,&OFFSET            PUT OFFSET IN REG1                  06800000
         AGO   .MODCD                                                   06850000
.MOD030  ANOP                                                           06900000
         AIF   ('&OFFSET' NE '(1)').MOD040  CHECK IF OFFSET IN R1       06950000
.* ******************                                                   07000000
.*    **** OFFSET ALREADY IN REG1, PUT UCB IN REG0                      07050000
.* ******************                                                   07100000
         AIF   ('&UCB'(1,1) EQ '(').MOD035  UCB IN REG                  07150000
         LA    0,&UCB               PUT UCB ADDRESS IN REG0             07200000
         AGO   .MODCD                                                   07250000
.MOD035  ANOP                                                           07300000
         LR    0,&UCB(1)            PUT UCB ADDRESS IN REG0             07350000
         AGO   .MODCD                                                   07400000
.MOD040  ANOP CHECK TO SEE IF OFFSET IN ABSOLUTE REG 2-15               07450000
         AIF   ('&OFFSET' EQ '(2)').MOD050                              07500000
         AIF   ('&OFFSET' EQ '(3)').MOD050                              07550000
         AIF   ('&OFFSET' EQ '(4)').MOD050                              07600000
         AIF   ('&OFFSET' EQ '(5)').MOD050                              07650000
         AIF   ('&OFFSET' EQ '(6)').MOD050                              07700000
         AIF   ('&OFFSET' EQ '(7)').MOD050                              07750000
         AIF   ('&OFFSET' EQ '(8)').MOD050                              07800000
         AIF   ('&OFFSET' EQ '(9)').MOD050                              07850000
         AIF   ('&OFFSET' EQ '(10)').MOD050                             07900000
         AIF   ('&OFFSET' EQ '(11)').MOD050                             07950000
         AIF   ('&OFFSET' EQ '(12)').MOD050                             08000000
         AIF   ('&OFFSET' EQ '(13)').MOD050                             08050000
         AIF   ('&OFFSET' EQ '(14)').MOD050                             08100000
         AIF   ('&OFFSET' EQ '(15)').MOD050                             08150000
         AGO   .MOD090 OFFSET NOT ABSOLUTE REG 2-15                     08200000
.MOD050  ANOP                                                           08250000
.* ******************                                                   08300000
.*    **** OFFSET SPECIFIED BY AN ABSOLUTE REG IN RANGE OF 2-15         08350000
.* ******************                                                   08400000
         AIF   ('&UCB'(1,1) EQ '(').MOD052  UCB IN REG                  08450000
         LA    0,&UCB               PUT UCB ADDRESS IN REG0             08500000
         AGO   .MOD054                                                  08550000
.MOD052  ANOP                                                           08600000
         LR    0,&UCB(1)            PUT UCB ADDRESS IN REG0             08650000
.MOD054  ANOP                                                           08700000
         LR    1,&OFFSET(1)         PUT OFFSET IN REG1                  08750000
         AGO   .MODCD                                                   08800000
.MOD090  ANOP                                                           08850000
.* ******************                                                   08900000
.*     **** COULD NOT PUT PARAMETERS IN NEW REGISTER FORMAT             08950000
.*     **** DIRECTLY, SO PUT IN OLD FORMAT AND THEN EXCHANGE.           09000000
.* ******************                                                   09050000
         AIF   ('&UCB'(1,1) EQ '(').UREG   UCB IN REG                   09100000
         LA    1,&UCB               PUT UCB ADDRESS IN REG1             09150000
         AGO   .CHKOFF              EXCHANGE REGS                       09200000
.UREG    ANOP                                                           09250000
         AIF   ('&UCB' EQ '(1)').CHKOFF  UCB ALREADY IN REG1            09300000
         LR    1,&UCB(1)            PUT UCB ADDRESS IN REG1             09350000
.CHKOFF  ANOP                                                           09400000
         AIF   ('&OFFSET'(1,1) EQ '(').OREG  IS OFFSET IN A REGISTER    09450000
         LA    0,&OFFSET            PUT OFFSET IN REG0                  09500000
         AGO   .XCHNG                                                   09550000
.OREG    ANOP                                                           09600000
         AIF   ('&OFFSET' EQ '(0)').XCHNG  OFFSET ALREADY IN REG0       09650000
***************                                                         09700000
*              NOTE:IF THE IOB OFFSET IS BEING PASSED TO IOHALT IN      09750000
*              REGISTER 1, AN ABSOLUTE REGISTER MUST BE CODED ON THE    09800000
*              MACRO INVOCATION FOR THIS PARAMETER.                     09850000
***************                                                         09900000
         LR    0,&OFFSET(1)         PUT OFFSET IN REG0                  09950000
         DC    0AL4(X'7FFFFFFF'*(16*((&OFFSET(1)+16)/17)*(17/(&OFFSET+1X10000000
               6))))                                                    10050000
*              IF THE OFFSET IS PASSED IN A SYMBOLIC REG1,THIS DC WILL  10100000
*              CAUSE AN ASSEMBLER ERROR                                 10150000
.XCHNG   ANOP                                                           10200000
         XR    1,0                  EXCHANGE REG1 AND REG0              10250000
         XR    0,1                                                      10300000
         XR    1,0                                                      10350000
.MODCD   ANOP                                                           10400000
         SLL   1,16                 SHIFT IOB OFFSET INTO              X10450000
                                    HIGH ORDER TWO BYTES                10500000
         IC    1,*+8                INDICATE MODIFY OPTION             X10550000
                                    TO BE USED                          10600000
         B     *+6                  BRANCH AROUND CONSTANT              10650000
         DC    X'81'                CONSTANT TO INDICATE               X10700000
                                    MODIFY OPTION                       10750000
         DC    X'00'                UNUSED BYTE                         10800000
         SVC   33                   ISSUE IOHALT SVC                    10850000
         MEXIT                                                          10900000
.E1      IHBERMAC  1                                                    10950000
         MEND                                                           11000000
