*%GOTO PLSCMNT;                                                         00050000
*/*                                                                     00100000
         MACRO                                                          00150000
         IGGUCSIT &MF=LIST,&NAME=,&ALIAS=,&VLENGTH=,&DEFAULT=NO,&DESCR=X00200000
               ,&FOLD=NO,&DEVICE=                                 @P1A  00250000
         GBLA  &UCSIL                                                   00300000
         GBLB  &IGGUCSI,&IGGUCSC                                        00350000
         GBLC  &DEVTYPE                                        @42480JF 00400000
         LCLA  &NVLINES,&N,&TVL                                         00450000
         LCLB  &ERROR,&F1D,&F2D                                   @P1A  00500000
         LCLC  &NAME1,&FLAG1,&VLL,&VLO,&DESCRD,&DESCRO                  00550000
         LCLC  &IGGUCSB,&IGGUVL,&IGGUDS,&IGGUDD,&IGGUEND                00600000
         AIF   (&IGGUCSC).SKIPCMT  PRINT COMMENTS ONLY ONCE/ASSEMBLY    00650000
&IGGUCSC SETB  1                   SET COMMENT GLOBAL                   00700000
**/                                                                     00750000
*%PLSCMNT:;                                                             00800000
*/********************************************************************* 00850000
**                                                                   ** 00900000
**    $MAC (IGGUCSIT):                                       @42480JF** 00950000
**                                                                   */ 01000000
*/*PROPRIETARY V3 STATEMENT                                          */ 01031200
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 01062400
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 01093600
*/*5695-DF1                                                          */ 01124800
*/*(C) COPYRIGHT 1982,1985  IBM CORP.                                */ 01156000
*/*END PROPRIETARY V3 STATEMENT                                      */ 01203000
*/*                                                                  ** 01250000
**    STATUS = MVS/XA DFP RELEASE 2.1.0                      @42480JF** 01300000
**                                                                   ** 01350000
**    DESCRIPTIVE-NAME = UCS IMAGE TABLE ENTRY GENERATION OR         ** 01400000
**                       MAPPING MACRO.                              ** 01450000
**                                                                   ** 01500000
**    FUNCTION = BILINGUAL MACRO FOR MAPPING A UCS IMAGE TABLE       ** 01550000
**               ENTRY OR AN ASSEMBLER MACRO FOR BUILDING UCS        ** 01600000
**               IMAGE TABLE ENTRIES IN A DEVICE DEPENDENT UCS       ** 01650000
**               IMAGE TABLE MODULE.                                 ** 01700000
**                                                                   ** 01750000
**    MODULE-TYPE = MACRO                                            ** 01800000
**                                                                   ** 01850000
**    MACRO-SIZE = N/A FOR MAPPING MACRO.                            ** 01900000
**                 PRODUCES A VARIABLE LENGTH UCS IMAGE TABLE        ** 01950000
**                 ENTRY.                                            ** 02000000
**                                                                   ** 02050000
**    PARAMETERS PLS - NONE, MAPPING MACRO ONLY.                     ** 02100000
**                                                                   ** 02150000
**    PARAMETERS ASM -                                               ** 02200000
**                   DEVICE=  4245 | 4248 - SPECIFY DEVICE     @42480JF 02250000
**                            DEFAULT IS 4245                  @42480JF 02300000
**                   MF=      LIST | DSECT - GENERATE A LIST ENTRY   ** 02350000
**                            OR A DSECT.  DEFAULT IS LIST.          ** 02400000
**      PARAMETERS (REQUIRED WHEN MF=LIST)                           ** 02450000
**                   NAME=    1-4 CHARACTER UCS IMAGE NAME           ** 02500000
**      PARAMETERS (OPTIONAL WHEN MF=LIST)                           ** 02550000
**                   ALIAS=   1-4 CHARACTER UCS IMAGE ALIAS          ** 02600000
**                   DEFAULT= NO | YES - WHETHER OR NOT THIS ENTRY IS** 02650000
**                            TO BE A DEFAULT IMAGE.  DEFAULT IS NO. ** 02700000
**                   VLENGTH= LENGTH(S) OF EACH VERIFICATION LINE    ** 02750000
**                            FOR 4245 ONLY                    @42480JF 02800000
**                   DESCR=   1-32 CHARACTER DESCRIPTIVE DATA TO BE  ** 02850000
**                            PRINTED WHEN VERIFYING THE UCS IMAGE.  ** 02900000
**                            DATA MAY BE ENTERED IN CHARACTER OR    ** 02950000
**                            HEXADECIMAL FORM.                      ** 03000000
**                                                                   ** 03050000
**    TABLES = NONE                                                  ** 03100000
**                                                                   ** 03150000
**    INNER MACROS = NONE                                            ** 03200000
**                                                                   ** 03250000
**    SYSTEM MACROS = NONE                                           ** 03300000
**                                                                   ** 03350000
**    CHANGE-ACTIVITY =                                              ** 03400000
** $S1=4245,    HDP2210,,STLDMM: NEW FOR 4245 DEVICE SUPPORT   @42450JF 03450000
** $S2=4248,    HDP2210,,STLDMM: 4248 DEVICE SUPPORT           @42480JF 03500000
** $P1=KKC0134, HDP2210,,STLTSL: FOLD MODE OPTION ADDED            @P1A 03525000
**                                                                   ** 03550000
**                                                                   ** 03600000
**                                                                   ** 03650000
**********************************************************************/ 03700000
*%GOTO PLSMAP;                                                          03750000
*/*                                                                     03800000
.SKIPCMT ANOP                                                           03850000
         AIF   ('&MF' EQ 'LIST').GENLIST CHECK IF LIST TYPE             03900000
         AIF   ('&MF' EQ 'DSECT').GENMAP CHECK IF DSECT TYPE            03950000
         MNOTE 12,'MF= SPECIFICATION NOT RECOGNIZED, MACRO NOT EXPANDEDX04000000
               '                                                        04050000
         MEXIT                                                          04100000
.GENMAP  ANOP                                                           04150000
.*                                                                      04200000
.*             GENERATE A UCS IMAGE LIST ENTRY DSECT                    04250000
.*                                                                      04300000
IGGUCSIT DSECT                                                          04350000
IGGUCSEL DS    AL1                 ENTRY LENGTH                         04400000
IGGUCSIA DS    CL4                 UCS IMAGE ALIAS/NAME                 04450000
IGGUCSIN DS    CL4                 UCS IMAGE NAME                       04500000
IGGUCSF1 DS    X                   FLAG BYTE                            04550000
IGGUCSD  EQU   X'80'               THIS IMAGE IS A DEFAULT IMAGE        04600000
IGGUCSFD EQU   X'40'               THIS IMAGE IS A FOLD IMAGE @P1A      04650000
*        EQU   X'20'               RESERVED                             04700000
*        EQU   X'10'               RESERVED                             04750000
*        EQU   X'08'               RESERVED                             04800000
*        EQU   X'04'               RESERVED                             04850000
*        EQU   X'02'               RESERVED                             04900000
*        EQU   X'01'               RESERVED                             04950000
IGGUCSVO DS    X                   OFFSET TO NUMBER OF VERIFICATION     05000000
*                                  LINES                                05050000
IGGUCSDO DS    X                   OFFSET TO DESCRIPTIVE DATA LENGTH    05100000
         DS    CL4                 RESERVED                             05150000
*                                                                       05200000
*        START OF VARIABLE INFORMATION                                  05250000
*                                                                       05300000
IGGUCSNL DS    X                   NUMBER OF VERIFICATION LINES         05350000
*        THE LENGTH IN BYTES OF IGGUCSVL IS DETERMINED                  05400000
*        BY THE CONTENTS OF IGGUCSNL                                    05450000
IGGUCSVL DS    X                   VERIFICATION LINE LENGTH(S)          05500000
*                                                                       05550000
IGGUCSDL DS    X                   LENGTH OF DESCRIPTIVE DATA           05600000
*        THE LENGTH IN BYTES OF IGGUCSDD IS DETERMINED                  05650000
*        BY THE CONTENTS OF IGGUCSDL                                    05700000
IGGUCSDD DS    C                   DESCRIPTIVE DATA                     05750000
         MEXIT                                                          05800000
**/                                                                     05850000
*%PLSMAP:;                                                              05900000
*  DECLARE 1 IGGUCSIT BASED,       /* MAPPING MACRO FOR              */ 05950000
*                                  /* UCS IMAGE TABLE                */ 06000000
*            2 IGGUCSEL FIXED(8),  /* ENTRY LENGTH                   */ 06050000
*            2 IGGUCSIA CHAR(4),   /* UCS IMAGE ALIAS/NAME           */ 06100000
*            2 IGGUCSIN CHAR(4),   /* UCS IMAGE NAME                 */ 06150000
*            2 IGGUCSF1 BIT(8),    /* FLAG BYTE                      */ 06200000
*              3 IGGUCSD  BIT(1),  /* THIS IMAGE IS A DEFAULT IMAGE  */ 06250000
*              3 IGGUCSFD BIT(1),  /* THIS IMAGE IS A FOLD IMAGE @P1A*/ 06283300
*              3 *        BIT(6),  /* RESERVED                       */ 06316600
*            2 IGGUCSVO PTR(8),    /* OFFSET TO NUMBER OF            */ 06350000
*                                  /* VERIFICATION LINES             */ 06400000
*            2 IGGUCSDO PTR(8),    /* OFFSET TO DESCRIPTIVE DATA     */ 06450000
*                                  /* LENGTH                         */ 06500000
*            2 *        CHAR(4);   /* RESERVED                       */ 06550000
*  DECLARE 1 * BASED(ADDR(IGGUCSIT) +  /* LOCATE THE FIRST           */ 06600000
*                         IGGUCSVO),   /* VARIABLE LENGTH FIELD      */ 06650000
*            2 IGGUCSNL FIXED(8),  /* NUMBER OF VERIFICATION LINES   */ 06700000
*            2 IGGUCSVL(*)         /* VERIFICATION LINE LENGTHS      */ 06750000
*                       FIXED(8);                                       06800000
*  DECLARE 1 * BASED(ADDR(IGGUCSIT) +  /* LOCATE THE SECOND          */ 06850000
*                         IGGUCSDO),   /* VARIABLE LENGTH FIELD      */ 06900000
*            2 IGGUCSDL FIXED(8),  /* LENGTH OF DESCRIPTIVE DATA     */ 06950000
*            2 IGGUCSDD CHAR(*);   /* DESCRIPTIVE DATA FOR HEADER    */ 07000000
*%GOTO PLSEND;                                                          07050000
*/*                                                                     07100000
.GENLIST ANOP                                                           07150000
&IGGUCSB SETC  'IGG&SYSNDX.U'                                           07200000
&IGGUVL  SETC  'IGG&SYSNDX.V'                                           07250000
&IGGUDS  SETC  'IGG&SYSNDX.S'                                           07300000
&IGGUDD  SETC  'IGG&SYSNDX.D'                                           07350000
&IGGUEND SETC  'IGG&SYSNDX.E'                                           07400000
.*                                                             @42480JF 07450000
.*       PROCESS THE DEVICE PARAMETER                          @42480JF 07500000
.*                                                             @42480JF 07550000
         AIF   ('&DEVTYPE' NE '').NOT1ST BR IF NOT FIRST ENTRY @42480JF 07600000
&UCSIL   SETA  350                 SET MAXIMUM IMAGE LENGTH             07650000
         AIF   ('&DEVICE' EQ '4245').SETDT BRANCH IF 4245      @42480JF 07700000
         AIF   ('&DEVICE' EQ '4248').SETDT BRANCH IF 4248      @42480JF 07750000
         AIF   ('&DEVICE' EQ '').SETDDEF OK IF NOT SPECIFIED   @42480JF 07800000
&ERROR   SETB  1                   SET ERROR FLAG              @42480JF 07850000
.SETDDEF ANOP                                                  @42480JF 07900000
&DEVTYPE SETC  '4245'              SET DEFAULT DEVICE TYPE     @42480JF 07950000
         AGO   .DEVEND             CONTINUE                    @42480JF 08000000
.SETDT   ANOP                                                  @42480JF 08050000
&DEVTYPE SETC  '&DEVICE'           SET DEVICE TYPE             @42480JF 08100000
         AGO   .DEVEND             CONTINUE                    @42480JF 08150000
.NOT1ST  ANOP                                                  @42480JF 08200000
         AIF   ('&DEVICE' EQ '').DEVOK  OK IF NOT SPECIFIED    @42480JF 08250000
         AIF   ('&DEVICE' EQ '&DEVTYPE').DEVOK  OK IF EQUAL    @42480JF 08300000
&ERROR   SETB  1                   SET ERROR FLAG              @42480JF 08350000
.DEVEND  ANOP                                                  @42480JF 08400000
         AIF   (NOT &ERROR).DEVOK BRANCH IF NO ERROR           @42480JF 08450000
         MNOTE 8,'DEVICE= SPECIFICATION INVALID, &DEVTYPE ASSUMED'      08500000
.*                                                             @42480JF 08550000
.DEVOK   ANOP                                                  @42480JF 08600000
.*                                                                      08650000
.*       PROCESS THE NAME PARAMETER                                     08700000
.*                                                                      08750000
         AIF   ('&NAME' EQ '').NAMEERR ERROR IF NAME OMITTED            08800000
         AIF   (K'&NAME LE 4).NAMEOK   ERROR IF NAME LONGER THAN 4      08850000
.*                                 CHARACTERS                           08900000
.NAMEERR MNOTE 12,'NAME= SPECIFICATION OMITTED/LONGER THAN 4 CHAR.'     08950000
&ERROR   SETB  1                   SET ERROR FLAG                       09000000
.NAMEOK  ANOP                                                           09050000
.*                                                                      09100000
.*       PROCESS THE ALIAS PARAMETER                                    09150000
.*                                                                      09200000
&NAME1   SETC  '&NAME'             SET ALIAS NAME                       09250000
         AIF   ('&ALIAS' EQ '').ALIASOK CONTINUE IF NO ALIAS            09300000
&NAME1   SETC  '&ALIAS'            SET ALIAS NAME                       09350000
         AIF   (K'&ALIAS LE 4).ALIASOK  ERROR IF ALIAS LONGER THAN 4    09400000
.*                                 CHARACTERS                           09450000
         MNOTE 12,'ALIAS= SPECIFICATION LONGER THAN 4 CHAR.'            09500000
&ERROR   SETB  1                   SET ERROR FLAG                       09550000
.ALIASOK ANOP                                                           09600000
.*                                                                      09650000
.*       PROCESS THE DEFAULT PARAMETER                                  09700000
.*                                                                      09750000
         AIF   ('&DEFAULT' EQ 'NO').DEFLTOK CONTINUE IF NO DEFAULT      09800000
&F1D     SETB  1                   SET DEFAULT BIT (FLAG 1 BIT 0)       09850000
         AIF   ('&DEFAULT' EQ 'YES').DEFLTOK CONTINUE IF DEFAULT        09900000
         MNOTE 12,'DEFAULT= SPECIFICATION INVALID'                      09950000
&ERROR   SETB  1                   SET ERROR FLAG                       10000000
.DEFLTOK ANOP                                                 @P1A      10009000
.*                                                            @P1A      10018000
.*       PROCESS THE FOLD MOLD PARAMETER                      @P1A      10027000
.*                                                            @P1A      10036000
         AIF   ('&FOLD' EQ 'NO').FOLDOK CONTINUE IF NO FOLD   @P1A      10045000
&F2D     SETB  1                   SET FOLD BIT (FLAG 1 BIT 1)@P1A      10054000
         AIF   ('&FOLD' EQ 'YES').FOLDOK CONTINUE IF FOLD     @P1A      10063000
         MNOTE 12,'FOLD= SPECIFICATION INVALID'               @P1A      10072000
&ERROR   SETB  1                   SET ERROR FLAG             @01A      10081000
.FOLDOK ANOP                                                  @P1A      10090000
.*                                                                      10100000
.*       END OF FLAG BYTE PROCESSING                                    10150000
.*                                                                      10200000
&FLAG1   SETC  '&F1D.&F2D.000000'  SET FLAG BYTE              @P1A      10250000
.*                                                                      10300000
.*       PROCESS THE VERIFICATION LINE LENGTH PARAMETER                 10350000
.*                                                                      10400000
&VLO     SETC  '0'                 INITIALIZE OFFSET VALUE              10450000
         AIF   ('&DEVTYPE' EQ '4245').VLSTART CHK VERIFICATION @42480JF 10500000
         AIF   (N'&VLENGTH EQ 0).VLLOK  BR. IF NO VERIFICATION @42480JF 10550000
         MNOTE 4,'VLENGTH NOT VALID FOR DEVICE=&DEVTYPE, SPECIFICATION X10600000
               IGNORED'                                        @42480JF 10650000
         AGO   .VLLOK                                          @42480JF 10700000
.VLSTART ANOP                                                  @42480JF 10750000
&NVLINES SETA  N'&VLENGTH          SET NUMBER OF VERIFICATION LINES     10800000
         AIF   (&NVLINES EQ 0).NOVLL BRANCH IF NO VERIFICATION          10850000
.*                                 LINE LENGTHS GIVEN                   10900000
&N       SETA  1                   INITIALIZE COUNTER                   10950000
         AIF   ('&VLENGTH(&N)' EQ '').VLLERR  ERROR IF OMITTED          11000000
         AIF   (T'&VLENGTH(&N) NE 'N').VLLERR ERROR IF NOT A NUMBER     11050000
         AIF   (&VLENGTH(&N) EQ 0).VLLERR     ERROR IF ZERO             11100000
         AIF   (&VLENGTH(&N) GT 132).VLLERR   ERROR IF TOO LARGE        11150000
&VLO     SETC  '&IGGUVL-&IGGUCSB'  SET OFFSET VALUE                     11200000
&VLL     SETC  'AL1'.'&VLENGTH'    INITIALIZE VLENGTH SPECIFICATION     11250000
&TVL     SETA  &VLENGTH(&N)        GET VERIFICATION LINE 1 LENGTH       11300000
         AIF   ('&VLENGTH'(1,1) EQ '(').VLLOOP                          11350000
&VLL     SETC  'AL1('.'&VLENGTH'.')' SET VLENGTH SPECIFICATION          11400000
.VLLOOP  ANOP                                                           11450000
&N       SETA  &N+1                INCREMENT COUNTER                    11500000
         AIF   (&N GT &NVLINES).TVLCHK CONTINUE WHEN ALL SUB-PARAMETERS 11550000
.*                                 HAVE BEEN PROCESSED                  11600000
         AIF   ('&VLENGTH(&N)' EQ '').VLLERR  ERROR IF OMITTED          11650000
         AIF   (T'&VLENGTH(&N) NE 'N').VLLERR ERROR IF NOT A NUMBER     11700000
         AIF   (&VLENGTH(&N) LT 1).VLLERR     ERROR IF TOO SMALL        11750000
         AIF   (&VLENGTH(&N) GT 132).VLLERR   ERROR IF TOO LARGE        11800000
&TVL     SETA  &TVL+&VLENGTH(&N)   TOTAL VERIFICATION LENGTHS           11850000
         AIF   (&TVL GT &UCSIL).VLLERR BRANCH IF IMAGE LENGTH EXCEEDED  11900000
         AGO   .VLLOOP             PROCESS REMAINING SUB-PARAMETERS     11950000
.TVLCHK  ANOP                                                           12000000
         AIF   (&TVL EQ &UCSIL).VLLOK BRANCH IF ENTIRE IMAGE VERIFIED   12050000
         MNOTE 4,'VLENGTH SPECIFICATION DOES NOT TOTAL &UCSIL'          12100000
         AGO   .VLLOK              CONTINUE                             12150000
.VLLERR  ANOP                                                           12200000
         MNOTE 12,'VLENGTH= SPECIFICATION (&N.) OMITTED OR INVALID'     12250000
&ERROR   SETB  1                   SET ERROR FLAG                       12300000
         AGO   .VLLOK              CONTINUE                             12350000
.NOVLL   ANOP                                                           12400000
         MNOTE ,'VLENGTH SPECIFICATION OMITTED, VERIFICATION NOT POSSIBX12450000
               LE FOR THIS UCS IMAGE'                                   12500000
.VLLOK   ANOP                                                           12550000
.*                                                                      12600000
.*       PROCESS THE DESCRIPTIVE DATA PARAMETER                         12650000
.*                                                                      12700000
&DESCRO  SETC  '0'                 INITIALIZE OFFSET VALUE              12750000
         AIF   ('&DESCR' EQ '').DDOK CONTINUE OF DESCR. DATA OMITTED    12800000
         AIF   ('&DEVTYPE' NE '4245').DDSTART CONT. IF NOT 4245@42480JF 12850000
         AIF   (&NVLINES EQ 0).DDIGN TEST OF VERIFICATION LINES OMITTED 12900000
.DDSTART ANOP                                                  @42480JF 12950000
&DESCRO  SETC  '&IGGUDS-&IGGUCSB'                                       13000000
         AIF   ('&DESCR'(1,2) EQ 'X''').DDHEX TEST IF HEXADECIMAL       13050000
         AIF   ('&DESCR'(1,1) EQ '''').DDQUOTE TEST FOR QUOTED STRING   13100000
&DESCRD  SETC  'C'''.'&DESCR'.'''' COPY DESCRIPTIVE DATA                13150000
         AIF   (K'&DESCR LE 32).DDOK TEST DESCR. DATA LENGTH            13200000
&DESCRD  SETC  'C'''.'&DESCR'(1,32).'''' COPY FIRST 32 CHARACTERS       13250000
         AGO   .DDLONG             ISSUE MNOTE                          13300000
.DDQUOTE ANOP                                                           13350000
&DESCRD  SETC  'C'.'&DESCR'        COPY DESCRIPTIVE DATA                13400000
         AIF   (K'&DESCR LE 34).DDOK TEST DESCR. DATA LENGTH            13450000
&DESCRD  SETC  'C'.'&DESCR'(1,33).'''' COPY FIRST 32 CHARACTERS         13500000
         AGO   .DDLONG             ISSUE MNOTE                          13550000
.DDHEX   ANOP                                                           13600000
         AIF   (K'&DESCR/2*2 EQ K'&DESCR).DDERR                         13650000
&DESCRD  SETC  '&DESCR'            COPY DESCRIPTIVE DATA                13700000
         AIF   (K'&DESCR LE 67).DDOK                                    13750000
&DESCRD  SETC  '&DESCR'(1,66).'''' COPY FIRST 32 CHARACTERS             13800000
.DDLONG  MNOTE 4,'DESCR= SPECIFICATION TRUNCATED TO 32 BYTES'           13850000
         AGO   .DDOK               CONTINUE                             13900000
.DDIGN   ANOP                                                           13950000
         MNOTE 4,'NO VERIFICATION LINES SPECIFIED, DESCRIPTIVE DATA IGNX14000000
               ORED'                                                    14050000
         AGO   .DDOK               CONTINUE                             14100000
.DDERR   MNOTE 12,'DESCR= SPECIFICATION INVALID'                        14150000
&ERROR   SETB  1                   SET ERROR FLAG                       14200000
.DDOK    ANOP                                                           14250000
.*                                                                      14300000
.*             END OF PARAMETER PROCESSING                              14350000
.*                                                                      14400000
         AIF   (&ERROR).MEND       DO NOT CONTINUE IF A TERMINATING     14450000
.*                                 ERROR WAS DETECTED                   14500000
.*                                                                      14550000
.*             GENERATE A UCS IMAGE LIST ENTRY                          14600000
.*                                                                      14650000
         AIF   (NOT &IGGUCSI).FIRST BRANCH IF FIRST TIME                14700000
         ORG   *-1                 OVERLAY PREVIOUS NULL ENTRY          14750000
.FIRST   ANOP                                                           14800000
&IGGUCSI SETB  1                   SET INVOKED FLAG                     14850000
&IGGUCSB DC    AL1(&IGGUEND-*)     ENTRY LENGTH                         14900000
         DC    CL4'&NAME1'         UCS IMAGE ALIAS/NAME                 14950000
         DC    CL4'&NAME'          UCS IMAGE NAME                       15000000
         DC    B'&FLAG1'           FLAG BYTE                            15050000
         DC    AL1(&VLO)           OFFSET TO NO. OF VER. LINES          15100000
         DC    AL1(&DESCRO)        OFFSET TO DESCRIPTIVE DATA LENGTH    15150000
         DC    XL4'0'              RESERVED                             15200000
         AIF   ('&VLO' EQ '0').SKIPVLL                                  15250000
&IGGUVL  EQU   *                   START OF VERIFICATION INFORMATION    15300000
         DC    AL1(&NVLINES)       NUMBER OF VERIFICATION LINES         15350000
         DC    &VLL                VERIFICATION LINE LENGTHS            15400000
.SKIPVLL ANOP                                                           15450000
         AIF   ('&DESCRO' EQ '0').SKIPDD SKIP DESCR. DATA IF OMITTED    15500000
&IGGUDS  EQU   *                   START OF DESCRIPTIVE DATA            15550000
&IGGUDS  SETC  'L'''.'&IGGUDD'     SET CHARACTER STRING                 15600000
         DC    AL1(&IGGUDS)        LENGTH OF DESCRIPTIVE DATA           15650000
&IGGUDD  DC    &DESCRD             DESCRIPTIVE DATA FOR HEADER          15700000
.SKIPDD  ANOP                                                           15750000
&IGGUEND EQU   *                   END OF UCS IMAGE ENTRY               15800000
         DC    X'0'                END-OF-LIST ENTRY                    15850000
.MEND    ANOP                                                           15900000
         MEND                                                           15950000
**/                                                                     16000000
*%PLSEND:;                                                              16050000
