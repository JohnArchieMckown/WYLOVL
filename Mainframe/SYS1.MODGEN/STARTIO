* %/*                                                                   00050000
         MACRO                                                          00100000
&SYMBOL  STARTIO    &SRB=,&TCB=,&IOSB=                                  00150000
         LCLB  &TCBOSW,&SRBNEG                                          00200000
.********************************************************************** 00250000
.*01* MACRO NAME = STARTIO                                            * 00300000
.*                                                                    * 00350000
.*01* DESCRIPTIVE NAME = Start I/O request                            * 00353800
.*                                                                    * 00357600
.*01* COPYRIGHT =                                                     * 00362600
.*        5740-XC6                                                    * 00367600
.*        THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                 * 00372600
.*        (C) COPYRIGHT IBM CORP. 1982                                * 00377600
.*        LICENSED MATERIALS - PROPERTY OF IBM                        * 00382600
.*                                                                    * 00388000
.*01* STATUS = HBB2102                                                * 00391800
.*                                                                    * 00395600
.*01* MACRO CALL                                                      * 00400000
.*    ASSEMBLER:                                                      * 00450000
.*       LABEL STARTIO IOSB=VAR OR (REG),TCB=SRB OR 0 OR VAR OR (REG) * 00500000
.*                           - OR -                                   * 00550000
.*       LABEL STARTIO SRB=VAR OR (REG),TCB=SRB OR 0 OR VAR OR (REG)  * 00600000
.*                                                                    * 00650000
.*    PLS:                                                            * 00700000
.*       ?LABEL: STARTIO IOSB(VARIABLE OR ADDR(VARIABLE))             * 00750000
.*                       TCB(SRB OR 0 OR VARIABLE OR ADDR(VARIABLE))  * 00800000
.*                           - OR -                                   * 00850000
.*       ?LABEL: STARTIO SRB(VARIABLE OR ADDR(VARIABLE))              * 00900000
.*                       TCB(SRB OR 0 OR VARIABLE OR ADDR(VARIABLE))  * 00950000
.*                                                                    * 01000000
.*01* FUNCTION = Provide linkage to the Start Subchannel              * 01050000
.*               Queue routine to initiate an I/O request.            * 01100000
.*                                                                    * 01400000
.*01* INPUT  =                                                        * 01420000
.*       SRB or IOSB parameter must be supplied.                      * 01440000
.*       If this parameter specifies a general purpose register,      * 01460000
.*       it must be in the range of 1-12.                             * 01480000
.*                                                                    * 01500000
.*       ASSEMBLER - If this parameter specifies a variable, it must  * 01525000
.*       be the label of the SRB or SRB (I.E., a LA instruction will  * 01550000
.*       be generated.                                                * 01575000
.*                                                                    * 01600000
.*       SRB=                                                         * 01625000
.*           Specifies the address of the SRB. If this option is      * 01650000
.*           used, the SRBPARM field must contain the IOSB address.   * 01675000
.*                                                                    * 01700000
.*       IOSB=                                                        * 01742800
.*            Specifies the address of the IOSB.  If this option is   * 01785600
.*            used, both the SRBPARM and IOSSRB fields must contain   * 01828400
.*            the IOSB and SRB addresses, respectively.               * 01871200
.*            IOSLEVEL field in the IOSB must contain the appropriate * 01914000
.*            value returned from the IOSLEVEL macro.                 * 01956800
.*                                                                    * 02000000
.*       TCB=                                                         * 02050000
.*           SRB - Indicates the TCB address has been placed          * 02100000
.*                 in SRBPTCB prior to issuing STARTIO and is not     * 02150000
.*                 to be changed.                                     * 02200000
.*                                                                    * 02250000
.*                 0  - Indicates current TCB address (PSATOLD) is    * 02300000
.*                      to be used.                                   * 02350000
.*                                                                    * 02650000
.*                 Variable or register - If a general register is    * 02675000
.*                 specified, it must be in the range of 0,2-12.      * 02700000
.*                  ASSEMBLER -                                       * 02725000
.*                           GPR(absolute or symbolic) specifying     * 02750000
.*                                address of TCB                      * 02775000
.*                           VAR - Label of TCB to be used            * 02800000
.*                  PLS -                                             * 02825000
.*                           Variable which specifies the address     * 02850000
.*                           of the TCB.                              * 02875000
.*                                                                    * 02900000
.*                 OMITTED - Indicates that the operation is not      * 02930000
.*                           task related or eligible for task        * 02960000
.*                           purge. Zero will be moved to SRBPTCB.    * 02990000
.*        LABEL - Optional                                            * 03020000
.*                                                                    * 03050000
.*01* REGISTERS AND DATA AREAS CHANGED BY THE STARTIO MACRO:          * 03850000
.*                                                                    * 03900000
.*       If the SRB parameter is coded, register 0 & 1 are set to     * 03950000
.*       indicate which SRB/TCB parameters were supplied as follows:  * 04000000
.*                                                                    * 04050000
.*             INPUT TCB          REG1            REG0                * 04100000
.*             parameter         output          output               * 04150000
.*             ---------         ------          ------               * 04200000
.*               =SRB         -(SRB ADDRESS)        -                 * 04250000
.*               = 0            SRB ADDRESS    -(SRB ADDRESS)         * 04300000
.*               =VARIABLE      SRB ADDRESS      TCB ADDRESS          * 04350000
.*                OR REGISTER                                         * 04400000
.*               OMITTED        SRB ADDRESS         0                 * 04450000
.*                                                                    * 04500000
.*                                                                    * 04550000
.*       If the IOSB parameter is coded:                              * 04600000
.*                                                                    * 04650000
.*             REG1 = IOSB address                                    * 04700000
.*                                                                    * 04750000
.*             SRBPTCB may be modified depending on the TCB           * 04800000
.*             parameter as follows:                                  * 04850000
.*                                                                    * 04900000
.*             TCB PARAMETER       SRBPTCB                            * 04950000
.*              -----------         -----                             * 05000000
.*                 =SRB            Not changed                        * 05050000
.*                 = 0             PSATOLD moved to SRBPTCB           * 05100000
.*                 =VARIABLE       Supplied value moved to SRBPTCB    * 05150000
.*                  OR REGISTER                                       * 05200000
.*                 OMITTED         Zeros are moved to SRBPTCB         * 05250000
.*                                                                    * 05300000
.*01* ATTRIBUTES -                                                    * 05350000
.*                                                                    * 05450000
.*02*      TYPE - Expands as re-entrant code                          * 05500000
.*                                                                    * 05550000
.*01* DEPENDENCIES - None                                             * 05600000
.*                                                                    * 05650000
.*01* RESTRICTIONS - None                                             * 05700000
.*                                                                    * 05750000
.*01* MODULE TYPE - Executable macro                                  * 05800000
.*                                                                    * 05900000
.*01* PROCESSOR  - Assembler (Assembler-H if the IOSB parameter is    * 05950000
.*                 codec) and PLS2                                    * 06000000
.*                                                                    * 06050000
.*01* NOTES -                                                         * 06083300
.*       Invokers of this macro must be in supervisor state, KEY 0.   * 06116600
.*                                                                    * 06150000
.*       Only register 13 will be maintained across the interface.    * 06200000
.*                                                                    * 06250000
.*       Addressing mode is maintained.                               * 06275000
.*                                                                    * 06300000
.*       The following mapping macros are required:                   * 06325000
.*             CVT  DSECT=YES                                         * 06350000
.*             IECDIOCM                                               * 06400000
.*             IECDIOSB (necessary only if the IOSB parameter         * 06450000
.*                       is used).                                    * 06500000
.*             IHAPSA (necessary if the PLS macro is used or if       * 06550000
.*                     IOSB and TCB=0 is specified)                   * 06600000
.*             IHASRB (necessary only if the IOSB parameter is        * 06650000
.*                     used and TCB=SRB is not specified)             * 06700000
.*             EPILOG (necessary only in PLS)                         * 06750000
.*                                                                    * 06800000
.*       PLS - If the IOSB parameter is used, %IOSAMODE='YES' must    * 06850000
.*             be specified prior to the INCLUDE for IECDIOCM.        * 06900000
.*                                                                    * 06950000
.*       If the SRB parameter is specified, the expansion will        * 06985700
.*       branch to a compatibility interface routine which will       * 07021400
.*       fill in the following:                                       * 07057100
.*            IOSLEVEL with the 'NORMAL' level                        * 07092800
.*            IOSSRB with the SRB address                             * 07128500
.*            and SRBPTCB, if applicable                              * 07164200
.*                                                                    * 07200000
.*       If the IOSB parameter is specified, the macro expansion      * 07250000
.*       will fill in SRBPTCB inline.                                 * 07300000
.*                                                                    * 07350000
.*01* CHANGE ACTIVITY -                                               * 07400000
.*           A000000-999999                                   @G860PLT* 07550000
.*                                                                    * 07600000
.*  $MAC(STARTIO),COMP(SC1C3): Start I/O Request                      * 07625000
.*                                                                    * 07650000
.*  $L1=VERS    HBB3310 860416 PDLE: Versioning macro into product    * 07675000
.*                                                                @L1A* 07700000
.********************************************************************** 07750000
         AIF   (T'&IOSB NE 'O').IOSBRT     IOSB PARAM. CODED            07800000
         AIF   (T'&SRB EQ 'O').ERRNONE     REQ PARAM. MISSING           07850000
         AIF   (T'&SYMBOL EQ 'O').NOSYM    ANY NAME FIELD?              07900000
&SYMBOL  DS    0H                                                       07950000
.NOSYM   AIF   (T'&TCB NE 'O').TCBOK       IS TCB SPECIFIED?            08000000
&TCBOSW  SETB  1                           NO, SET INDICATOR            08050000
         AGO   .SRBTST                     GO TEST SRB                  08100000
.TCBOK   AIF   ('&TCB' NE 'SRB').SRBTST    YES, IS IT SRB?              08150000
&SRBNEG  SETB  1                           YES, SET INDICATOR           08200000
.SRBTST  AIF   ('&SRB'(1,1) EQ '(').SRBREG IS REGISTER SPECIFIED?       08250000
         LA    1,&SRB                      SRB PARAMETER REG            08300000
         AIF   ('&SRBNEG' NE '1').TCBTST   TCB=SRB?                     08350000
         LNR   1,1                         TCB PARAMETER REG            08400000
         AGO   .CVT                                                     08450000
.SRBREG  AIF   ('&SRBNEG' NE '1').POSREG                                08500000
         LNR   1,&SRB(1)                   SRB PARAMETER REG            08550000
         AGO   .CVT                                                     08600000
.POSREG  AIF   ('&SRB' EQ '(1)').TCBTST    ALREADY IN REG 1?            08650000
         LR    1,&SRB(1)                   SRB PARAMETER REG            08700000
.TCBTST  AIF   ('&TCBOSW' NE '1').ZEROTST  TCB OMITTED?                 08750000
         SR    0,0                         TCB PARAMETER REG            08800000
         AGO   .CVT                                                     08850000
.ZEROTST AIF   ('&TCB' NE '0').REGTST      TCB=0?                       08900000
         LNR   0,1                         TCB PARAMETER REG            08950000
         AGO   .CVT                                                     09000000
.REGTST  AIF   ('&TCB'(1,1) NE '(').NOREG  TCB IN REGISTER?             09050000
         AIF   ('&TCB' EQ '(0)').CVT       ALREADY IN REG 0?            09100000
         LR    0,&TCB(1)                   TCB PARAMETER REG            09150000
         AGO   .CVT                                                     09200000
.NOREG   LA    0,&TCB                      TCB PARAMETER REG            09250000
.CVT     L     15,CVTPTR(0,0)              CVT ADDRESS                  09300000
         L     15,CVTIXAVL-CVTMAP(,15)     IOCOM ADDRESS                09350000
         L     15,IOCSTIO-IOCOM(,15)       START SUBCHANNEL             09400000
*                                          COMPATIBILITY RTN            09450000
         BALR  14,15                       INITIATE THE I/O             09500000
         MEXIT                                                          09550000
.IOSBRT  ANOP                                                           09600000
         AIF   (T'&SRB NE 'O').ERRBOTH     SRB & IOSB CODED             09650000
         AIF   (T'&SYMBOL EQ 'O').INOSYM   NO LABEL REQUESTED           09700000
&SYMBOL  DS    0H                                                       09750000
.INOSYM  ANOP                                                           09800000
         AIF   ('&IOSB'(1,1) EQ '(').IOSBREG  IOSB ADDR IN REG          09850000
         LA    1,&IOSB                  LOAD IOSB ADDR IN REG1          09900000
         AGO   .IOSBTCB                    GO CHECK TCB PARAM           09950000
.IOSBREG ANOP                                                           10000000
         AIF   ('&IOSB' EQ '(1)').IOSBTCB ADDR ALREADY IN REG1          10050000
         LR    1,&IOSB(1)               LOAD IOSB ADDR IN REG1          10100000
.IOSBTCB ANOP                                                           10150000
         AIF   ('&TCB' EQ 'SRB').IOSBCVT   SRBPTCB ALREADY SUPPLIED     10200000
.*                                                                      10250000
         AIF   (T'&TCB EQ 'O').I0TCB       TCB PARAM NOT CODED          10300000
         AIF   ('&TCB' EQ '0').ICURTCB     USE CURRENT TCB              10350000
         AIF   ('&TCB'(1,1) EQ '(').ITCBREG  TCB ADDR IN REGISTER       10400000
.*                                                                      10450000
         LA    14,&TCB                     GET TCB ADDRESS              10500000
         L     15,IOSSRB-IOSB(0,1)      GET SRB ADDR FROM IOSB          10550000
         ST    14,SRBPTCB-SRB(0,15)     STORE TCB ADDR IN SRB           10600000
         AGO   .IOSBCVT                                                 10650000
.I0TCB   ANOP                                                           10700000
         L     15,IOSSRB-IOSB(0,1)         GET SRB ADDR FROM IOSB       10750000
.*                                                                      10800000
         SR    14,14                       ZERO REGISTER                10850000
         ST    14,SRBPTCB-SRB(0,15)        SRBPTCB=0                    10900000
         AGO   .IOSBCVT                                                 10950000
.ICURTCB ANOP                                                           11000000
         L     15,IOSSRB-IOSB(0,1)      GET SRB ADDR FROM IOSB          11050000
         MVC   SRBPTCB-SRB(4,15),PSATOLD-PSA(0) SRBPTCB=CURRENT TCB     11100000
*                                                                       11150000
         AGO   .IOSBCVT                                                 11200000
.ITCBREG ANOP                                                           11250000
         L     15,IOSSRB-IOSB(0,1)      GET SRB ADDR FROM IOSB          11300000
         ST    &TCB(1),SRBPTCB-SRB(0,15)  STORE TCB IN SRBPTCB          11350000
.IOSBCVT ANOP                                                           11400000
         L     15,CVTPTR(0,0)              GET CVT POINTER              11450000
         L     15,CVTIXAVL-CVTMAP(0,15)    GET IOCOM POINTER            11500000
         L     15,IOCSSCQ-IOCOM(0,15)      GET IOSVSSCQ ENTRY POINT     11550000
*                                                                       11600000
         BASSM 14,15                       CALL START SUBCHANNEL        11650000
*                                          QUEUE ROUTINE                11700000
         MEXIT                                                          11750000
.ERRBOTH MNOTE 8,'*** BOTH SRB AND IOSB PARAMETERS HAVE BEEN CODED. ONLX11800000
               Y ONE IS ALLOWED.'                                       11850000
         MEXIT                                                          11900000
.ERRNONE MNOTE 8,'*** IOSB OR SRB PARAMETER REQUIRED.  NEITHER CODED.'  11950000
.*                                                                      12000000
         MEND                                                           12050000
*        */                                                             12100000
*STARTIO:                                                               12150000
*  MACRO                                                                12200000
*  KEYS(IOSB,SRB,TCB);             /*                                */ 12250000
*/*      EXTERNAL VARIABLES                                          */ 12300000
*DCL EHAREGS   CHAR  EXT;          /* REGISTER TO BE DCLD BY            12350000
*                                     EPILOG                         */ 12400000
*/*      INTERNAL VARIABLES                                          */ 12450000
*DCL INDENT    FIXED;              /* INDENT 2 COLUMNS               */ 12500000
*DCL IERRLVL   FIXED;              /* ERROR LEVEL                    */ 12550000
*DCL IMACRO    CHAR;               /* MACRO PARAMETERS               */ 12600000
*DCL IERRMSG   CHAR;               /* ERROR MESSAGE                  */ 12650000
*/*      INITIALIZE VARIABLES                                        */ 12700000
*  IERRLVL=0;                                             /*         */ 12750000
*  INDENT=MACCOL+2;                                       /*         */ 12800000
*  IMACRO=' ?'||MACLABEL||MACNAME||MACLIST||' '||MACKEYS||' ';          12850000
*  IF MACLABEL~='' THEN                                                 12900000
*     ANS(MACLABEL)  COL(MACLMAR);     /* GENERATE MACLABEL          */ 12950000
*  ANS('DO; '||COMMENT(IMACRO))        /* START MACRO & LIST         */ 13000000
*     COL(MACCOL);                     /* MACRO PARAMETERS           */ 13050000
*/* CHECK FOR SRB AND IOSB PARAMETER.                                   13100000
*   ONE AND ONLY ONE CAN BE CODED.                                   */ 13150000
*  IF IOSB='' THEN                      /* NEITHER IOSB OR           */ 13200000
*     IF SRB=''THEN                     /*         SRB CODED         */ 13250000
*       DO;                                               /*         */ 13300000
*         IERRMSG='IOSB OR SRB PARAMETER REQUIRED. NEITHER SUPPLIED.';  13350000
*                                                         /*         */ 13400000
*         ANS MSG(IERRMSG);                               /*         */ 13450000
*         ANS(COMMENT(IERRMSG))          COL(INDENT);     /*         */ 13500000
*         IERRLVL=8;                                      /*         */ 13550000
*       END;                                              /*         */ 13600000
*  IF IOSB~='' THEN                     /* BOTH IOSB AND             */ 13650000
*     IF SRB~='' THEN                   /*      SRB CODED            */ 13700000
*        DO;                                              /*         */ 13750000
*          IERRMSG='BOTH IOSB AND SRB PARAMETERS HAVE BEEN CODED. ONLY  13800000
* ONE IS ALLOWED.';                                       /*         */ 13850000
*          ANS MSG(IERRMSG);                              /*         */ 13900000
*          ANS(COMMENT(IERRMSG))          COL(INDENT);    /*         */ 13950000
*          IERRLVL=8;                                     /*         */ 14000000
*        END;                                             /*         */ 14050000
*  IF IOSB~='' THEN                     /* IOSB CODED                */ 14100000
*     IF IOSB(1)='' THEN                /* BUT NO VARIABLE              14150000
*                                          SUPPLIED                  */ 14200000
*        DO;                                              /*         */ 14250000
*          IERRMSG='IOSB CODED BUT REQUIRED VARIABLE NOT SUPPLIED.';    14300000
*                                                         /*         */ 14350000
*          ANS MSG(IERRMSG);                              /*         */ 14400000
*          ANS(COMMENT(IERRMSG))          COL(INDENT);    /*         */ 14450000
*          IERRLVL=8;                                     /*         */ 14500000
*        END;                                             /*         */ 14550000
*  IF SRB~='' THEN                      /* SRB CODED                */  14600000
*     IF SRB(1)='' THEN                 /* BUT NO VARIABLE              14650000
*                                          SUPPLIED                  */ 14700000
*        DO;                                              /*         */ 14750000
*          IERRMSG='SRB CODED BUT REQUIRED VARIABLE NOT SUPPLIED.';     14800000
*                                                         /*         */ 14850000
*          ANS MSG(IERRMSG);                              /*         */ 14900000
*          ANS(COMMENT(IERRMSG))          COL(INDENT);    /*         */ 14950000
*          IERRLVL=8;                                     /*         */ 15000000
*        END;                                             /*         */ 15050000
*  IF TCB~='' THEN                      /* TCB CODED                */  15100000
*     IF TCB(1)='' THEN                 /* BUT NO VARIABLE              15150000
*                                          SUPPLIED                  */ 15200000
*        DO;                                              /*         */ 15250000
*          IERRMSG='TCB CODED BUT REQUIRED VARIABLE NOT SUPPLIED.';     15300000
*                                                         /*         */ 15350000
*          ANS MSG(IERRMSG);                              /*         */ 15400000
*          ANS(COMMENT(IERRMSG))          COL(INDENT);    /*         */ 15450000
*          IERRLVL=8;                                     /*         */ 15500000
*        END;                                             /*         */ 15550000
*  IF IERRLVL~=0 THEN                  /* IOSB, SRB OR               */ 15600000
*     DO;                              /*       TCB ERROR            */ 15650000
*       ANS(COMMENT('INVALID PARAMETERS HAVE BEEN ENCOUNTERED. NO CODE  15700000
*GENERATED'))       COL(MACCOL);                          /*         */ 15750000
*       ANS('END; '||COMMENT(IMACRO))   COL(MACCOL);   /* END MACRO  */ 15800000
*       RETURN CODE(IERRLVL);                             /*         */ 15850000
*     END;                                                /*         */ 15900000
*  IF INDEX(EHAREGS,'01P')=0 THEN      /* HAS REG1 BEEN                 15950000
*                                         DECLARED?                  */ 16000000
*     EHAREGS=EHAREGS||'01P';          /* NO, ADD TO EHAREGS         */ 16050000
*  IF IOSB~='' THEN                    /* IOSB PARAM CODED           */ 16100000
*     DO;                                                 /*         */ 16150000
*       ANS('RFY GPR01P  RSTD; ')                         /*         */ 16200000
*          COL(INDENT);                                   /*         */ 16250000
*       ANS('GPR01P='||IOSB(1)||'; '||                    /*         */ 16300000
*          COMMENT('REG1=IOSB ADDRESS'))     COL(INDENT); /*         */ 16350000
*       IF TCB(1)='SRB'  THEN        /* TCB=SRB -TCB ALREADY         */ 16400000
*          ;                         /* SUPPLIED IN SRBPTCB          */ 16450000
*       ELSE                                              /*         */ 16500000
*          IF TCB='' THEN                                 /*         */ 16550000
*             ANS('GPR01P->IOSSRB->SRBPTCB=0; '||         /*         */ 16600000
*                COMMENT('SRBPTCB=0'))       COL(INDENT); /*         */ 16650000
*          ELSE                                           /*         */ 16700000
*             IF TCB(1)='0' THEN        /* TCB=0 -                      16750000
*                                          USE CURRENT TCB           */ 16800000
*                ANS('GPR01P->IOSSRB->SRBPTCB=PSATOLD; '|| /*        */ 16850000
*                   COMMENT('SRBPTCB=CURRENT TCB'))   COL(INDENT);      16900000
*                                                         /*         */ 16950000
*             ELSE                                        /*         */ 17000000
*               ANS('GPR01P->IOSSRB->SRBPTCB='||TCB(1)||'; '||          17050000
*                                                         /*         */ 17100000
*                  COMMENT('SRBPTCB=TCB ADDRESS')) COL(INDENT);         17150000
*                                                         /*         */ 17200000
*      ANS('CALL FLCCVT->CVTIXAVL->IOCSSCQ->IOCSSCQE; '|| /*         */ 17250000
*         COMMENT('CALL IOSVSSCQ - START SUBCHANNEL QUEUE ROUTINE'))    17300000
*         COL(INDENT);                                    /*         */ 17350000
*      ANS('RFY GPR01P  UNRSTD; ') COL(INDENT);           /*         */ 17400000
*    END;                                                 /*         */ 17450000
*  ELSE                                 /* SRB PARAM CODED           */ 17500000
*     DO;                                                 /*         */ 17550000
*       IF INDEX(EHAREGS,'00P')=0 THEN  /* HAS REG0 BEEN                17600000
*                                          DECLARED                  */ 17650000
*          EHAREGS=EHAREGS||'00P';     /* NO, ADD TO EHAREGS         */ 17700000
*       ANS('RFY(GPR00P,GPR01P) RSTD; ')   COL(INDENT);   /*         */ 17750000
*       IF TCB(1)='SRB' THEN            /* TCB=SRB CODED             */ 17800000
*          ANS('GPR01P=-'||SRB(1)||'; '||                               17850000
*             COMMENT('REG1=-(SRB ADDRESS). INDICATES TCB=SRB CODED.')) 17900000
*             COL(INDENT);                                /*         */ 17950000
*       ELSE                                              /*         */ 18000000
*         DO;                                             /*         */ 18050000
*           ANS('GPR01P='||SRB(1)||'; '||                 /*         */ 18100000
*              COMMENT('REG1=SRB ADDRESS'))        COL(INDENT);         18150000
*                                                         /*         */ 18200000
*           IF TCB=''  THEN              /* NO TCB PARAMETER            18250000
*                                           CODED                    */ 18300000
*              ANS('GPR00P=0; '||                         /*         */ 18350000
*                 COMMENT('REG0=ZERO'))            COL(INDENT);         18400000
*                                                         /*         */ 18450000
*           ELSE                                          /*         */ 18500000
*             IF TCB(1)='0' THEN        /* TCB=0 -                      18550000
*                                          USE CURRENT TCB           */ 18600000
*                ANS('GPR00P=-GPR01P; '||                 /*         */ 18650000
*                   COMMENT('REG0=-(SRB ADDRESS). INDICATES USE CURRENT 18700000
* TCB.'))                                         COL(INDENT);          18750000
*                                                         /*         */ 18800000
*             ELSE                      /* TCB=LABEL CODED           */ 18850000
*                ANS('GPR00P='||TCB(1)||'; '||            /*         */ 18900000
*                   COMMENT('REG0=TCB ADDRESS'))  COL(INDENT);          18950000
*                                                         /*         */ 19000000
*         END;                                            /*         */ 19050000
*       ANS('CALL FLCCVT->CVTIXAVL->IOCSTIO->IOCSTIOE; '|| /*        */ 19100000
*          COMMENT('CALL IOSVSTIO - STARTIO COMPATABILITY MODULE'))     19150000
*          COL(INDENT);                                   /*         */ 19200000
*       ANS('RFY(GPR00P,GPR01P) UNRSTD; ')        COL(INDENT);          19250000
*                                                         /*         */ 19300000
*     END;                                                /*         */ 19350000
*  ANS('END; '||COMMENT(IMACRO))   COL(MACCOL); /* END MACRO         */ 19400000
*%END STARTIO;                                            /*         */ 19450000
