* /******** START OF SPECIFICATIONS **********************************  00050000
*                                                                       00100000
*01* MACRO-NAME: IECVUCB                                          @L2C  00150000
*                                                                       00200000
*01* DESCRIPTIVE-NAME: Generate UCBs                              @L2C  00250000
*                                                                       00300000
*02*     ACRONYM: GUCB                                            @L2A  00325000
*                                                                       00350000
*01* DSECT_NAME:  GUCB                                            @L2A  00400000
*                                                                       00750000
*/*01* PROPRIETARY STATEMENT:                                     @L2A  00757600
*/***PROPRIETARY STATEMENT****************************************@L2A  00765200
*/*                                                                     00772800
*/*                                                                     00780400
*/*    5655-068                                                         00788000
*/*    THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      00795600
*/*    (C) COPYRIGHT IBM CORP. 1981, 1993                         @L2C  00803200
*/*    LICENSED MATERIALS - PROPERTY OF IBM                             00810800
*/*                                                                     00818400
*/*  STATUS = HBB5510                                             @L2C  00826000
*/*                                                                     00833600
*/***END OF PROPRIETARY STATEMENT*********************************@L2A  00841200
*                                                                       00850000
*01* COMPONENT: IOS (SC1C3)                                       @L2A  00861100
*                                                                       00872200
*01* EYE CATCHER: NONE                                            @L2A  00883300
*                                                                       00894400
*01* STORAGE ATTRIBUTES:                                          @L2A  00905500
*                                                                       00916600
*02*      SUBPOOL:   CALLER-PROVIDED                              @L2A  00927700
*02*      KEY:       CALLER-PROVIDED                              @L2A  00938800
*02*      RESIDENCY: CALLER-PROVIDED                              @L2A  00949900
*                                                                       00961000
*01* METHOD OF ACCESS                                             @L2A  00972100
*       Execute the macro within the code with the desired        @L2A  00983200
*       parameters that is explained under inputs.                @L2A  00994300
*                                                                       01005400
*01* FUNCTION:                                                          01016500
*       This macro generates the common parts of the UCB (base and      01027600
*       common extension).                                              01038700
*                                                                       01050000
*01* NOTES= This macro is only invoked by module IEECVUCM to build      01100000
*           Subsystem UCBs.                                             01150000
*                                                                       01153300
*01* CREATED BY: N/A                                              @L2A  01156600
*                                                                       01159900
*01* DELETED BY: N/A                                              @L2A  01163200
*                                                                       01166500
*01* POINTED TO BY: N/A                                           @L2A  01169800
*                                                                       01173100
*01* SIZE:                                                        @L2A  01176400
*                                                                       01179700
*01* EXTERNAL CLASSIFICATION: NONE                                @L2A  01183000
*                                                                       01186300
*01* END OF EXTERNAL CLASSIFICATION:                              @L2A  01189600
*                                                                       01192900
*01* SERIALIZATION: NONE                                          @L2A  01196200
*                                                                       01200000
*01* MODULE-TYPE= MACRO                                                 01250000
*                                                                       01300000
*02*   PROCESSOR= ASSEMBLER                                             01350000
*                                                                       01400000
*01* INPUT=                                                             01450000
*         &UCBNO   - 4 digit sequence number for use in labels.         01500000
*         &DEVAD   - 3 character device address.                        01550000
*         &TYPE    - general device type classification.                01600000
*         &TYPENO  - values to be used in chaining the UCBs together    01650000
*                    by device class.                                   01700000
*                      &TYPENO(1) is the chaining label for this UCB.   01750000
*                      &TYPENO(2) is the chaining label for the next.   01800000
*           Note: &TYPENO is ignored when generating JES dummy UCBs.    01850000
*         &ERRTAB  - value to be placed in UCBETI.                      01900000
*         &STATAB  - value to be placed in UCBSTI.                      01950000
*         &STLI    - value to be placed in UCBSTLI.                     02000000
*         &DEVNDX  - 2250 device index value.                           02050000
*         &TYPEA   - 8 character value for UCBTYPE.                     02100000
*         &FL5     - value to be placed in UCBFL5.                      02150000
*         &SYSRES  - non-removable indicator.                           02200000
*                    1 if device is non-removable,                      02250000
*                    0 otherwise.                                       02300000
*         &DDTNM   - the name of the Device Descriptor Table (DDT).     02350000
*         &OFF     - offline indicator.                                 02400000
*                    1 if OFFLINE=YES was specified,                    02450000
*                    0 otherwise.                                       02500000
*         &GR      - indicates that GAM is in the system.               02550000
*         &TMSK    - additional options byte.                           02600000
*         &EXT     - common extention indicator (old interface).        02650000
*                    1 causes nothing to be generated (previously       02700000
*                      caused the common extension to be generated),    02750000
*                    0 causes normal UCB generation (previously caused  02800000
*                      the common and device-dependent segments to be   02850000
*                      generated).                                      02900000
*         &NOP2    - 2250-3 buffer table name.                          02950000
*         &DEVADE  - 3 character device address ('JES' for dummy        03000000
*                    consoles, base address for 2305 devices).          03050000
*         &SHR     - shareable indicator for 3420 devices.              03100000
*                    1 if shareable,                                    03150000
*                    0 otherwise.                                       03200000
*         &SHRUP   - shareable in uniprocessor mode (DASD only)         03250000
*                    1 if shareable,                                    03300000
*                    0 otherwise.                                       03350000
*         &FLP1    - value to be placed in UCBFLP1.                     03400000
*         &CLEXT   - class extension indicator.                         03450000
*                    1 if extension exists,                             03500000
*                    0 otherwise.                                       03550000
*                                                                       03600000
*01* OUTPUT=                                                            03650000
*       The following UCB sections are generated in order -             03700000
*                   - UCB common extension with a label of UCBXnnnn     03750000
*                     (where nnnn is the sequence number).              03800000
*                   - Base UCB with labels of UCBnnnn                   03850000
*                     (where nnnn is the sequence number),              03900000
*                     and Uttzzzz (where tt is a class identifier       03950000
*                     and zzzz is the sequence number for that          04000000
*                     class).                                           04050000
*                   - Optional device dependent segment.                04100000
*                                                                       04150000
*01* EXIT-NORMAL= Return to assembler                                   04200000
*                                                                       04250000
*01* EXIT-ERROR= NONE                                                   04300000
*                                                                       04350000
*01* CHANGE-ACTIVITY=                                                   04400000
*                                                                       04450000
*    $MAC(IECVUCB),COMP(SC1C3): Generate UCBs                           04500000
*                                                                       04550000
*          G860PLN: New UCB                                             04600000
*    $L0=IOGEN,JBB2220,840813,PDLR: IOGEN Restructure                   04650000
*    $L1=VERS ,HBB3310,860416,PDLE: Versioning macro into product       04700000
*                                                                  @L1A 04750000
*    $L2=4DGIT,HBB5510,930304,PDKB: Four Digit Support             @L2A 04766600
*                                                                       04783200
*                                                                       04800000
*************** END OF SPECIFICATIONS ********************************/ 04850000
         MACRO                                                          04900000
         IECVUCB &UCBNO,&DEVAD,&TYPE,&TYPENO,&ERRTAB,&STATAB,&STLI,    *04950000
               &DEVNDX,&TYPEA,&FL5,&SYSRES,&DDTNM,&OFF,&GR,&TMSK,      *05000000
               &EXT,&NOP2,&DEVADE,&SHR,&SHRUP,&FLP1,&CLEXT     @G860PLN 05050000
         GBLA  &IECIZS1,&IECIZCT                               @G860PLN 05100000
         LCLA  &COUNT,&A                                                05150000
         LCLC  &DEVADDR                                                 05200000
         LCLA  &BITS          FOR BYTES OF COMMON EXT          @Y40MPLG 05250000
         AIF   ('&EXT' EQ '0').GENUCB   TEST FOR OLD INTERFACE @ZMC0022 05300000
         MEXIT ,                        EXIT IF OLD INTERFACE  @ZMC0022 05350000
.GENUCB  ANOP                                                  @ZMC0022 05400000
         SPACE 2                                                        05450000
**************************************************************          05500000
.********************************************************************** 05550000
.* THIS SECTION OF CODE WAS MOVED FROM THE BOTTOM TO THE TOP   @G860PLN 05600000
.* OF THE MACRO SO THAT THE COMMON EXTENSION WILL BE           @G860PLN 05650000
.* GENERATED BEFORE THE PREFIX.                                @G860PLN 05700000
.********************************************************************** 05750000
.* BEGINNING OF SECTION MOVED FROM BOTTOM TO TOP OF MACRO      @G860PLN 05800000
.********************************************************************** 05850000
         DS    0D                                              @G860PLN 05900000
UCBX&UCBNO DC  XL1'&ERRTAB',AL1(&STATAB),AL1(0)                @G860PLN 05950000
.********************************************************************** 06000000
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE* 06050000
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND * 06100000
.*   THE 3525 CARD PRINT. ONLY THE 3211 AND A UCS 1403 PRINTER GET    * 06150000
.*   A UCB EXTENSION.                                                 * 06200000
.*   SET A SENSE LENGTH OF TWO BYTES FOR 1403 PRINTER.                * 06250000
.*   SET A SENSE LENGTH OF FOUR BYTES FOR A 3505 AND A 3525.          * 06300000
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3211 PRINTER.              * 06350000
.*   SET A SENSE LENGTH OF 24 BYTES FOR A 3800 PRINTER         @Y40MPLG 06400000
.*   SET A SENSE LENGTH OF TWO BYTES FOR A 3895 RDR-INSCRIBER  @G30HNLO 06450000
.*   SET A SENSE LENGTH OF SIX BYTES FOR A 3540 DISKET RDR/WTR @Y30OPLA 06500000
.********************************************************************** 06550000
         AIF   ('&TYPE' EQ 'WR').UC3211A TEST FOR 3211 OR A 3505/3525   06600000
         AIF   ('&TYPE' NE 'UR').NOPRT TEST FOR UR                      06650000
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540A 3540 DISKET    @Y30OPLA 06700000
         AIF   ('&TYPEA'(6,3) EQ '842').URDSMA 3851 DEVICE     @G860PLN 06750000
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*06800000
               )).NOPRT                 IT IS NOT A PRINTER WITH UCS    06850000
         DC    X'00'                    ATI/UCS PRINTER        @G860PLN 06900000
         DC    X'02'                    SENSE COUNT/UCS PRINTER         06950000
         AGO   .UCB5                                                    07000000
.UC3211A AIF   ('&TYPEA'(7,2) NE '09').NEWUR  IT IS NOT A 3211          07050000
.UR3540A DC    X'00'                    ATI/UCS PRINTER,3540   @G860PLN 07100000
         DC    X'06'               SENSE COUNT/3211 PRNTR,3540 @Y30OPLA 07150000
         AGO   .UCB5                                                    07200000
.NEWUR   AIF   ('&TYPEA'(7,2) EQ '0E').UCB3800  3800 PRINTER   @Y40MPLG 07250000
         AIF   ('&TYPEA'(7,2) EQ '19').UCB3895  3895           @G30HNLO 07300000
         DC    X'00'                    ATI/NEW UR             @G860PLN 07350000
         DC    X'04'                    SENSE COUNT/NEW UR              07400000
         AGO   .UCB5                                                    07450000
.UCB3800 DC    X'00'                ATI/3800 PRT               @G860PLN 07500000
         DC    X'18'                SENSE COUNT/3800 PRT       @Y40MPLG 07550000
         AGO   .UCB5                                                    07600000
.UCB3895 DC    X'00'                ATI/3895                   @G860PLN 07650000
         DC    X'02'                SENSE COUNT/3895           @G30HNLO 07700000
         AGO   .UCB5                                           @G30HNLO 07750000
.URDSMA  ANOP                                                  @Y30LPLC 07800000
         DC    X'38'                    ATI/ 3851              @G860PLN 07850000
         DC    X'20'                    SENSE COUNT/ 3851      @G860PLN 07900000
         AGO   .UCB5                                           @Y30LPLC 07950000
.********************************************************************** 08000000
.*                                                                    * 08050000
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    * 08100000
.*                                                                    * 08150000
.********************************************************************** 08200000
.NOPRT  AIF   ('&TYPE' NE 'GR').URDUMM TEST FOR GRAPHICS                08250000
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X08300000
               2) NE '0B').NOANR      ANR?                     @G860PLN 08350000
         DC    X'1C'                    ATI/ANR GRAPHIC        @G860PLN 08400000
         DC    X'06'                    SENSE COUNT/ANR GRAPHICS        08450000
         AGO   .UCB5                                                    08500000
.NOANR   ANOP                                                           08550000
         DC    X'10'                    ATI/NON-ANR GRAPHICS   @G860PLN 08600000
         DC    X'06'                    SENSE COUNT/NON-ANR GRAPHICS    08650000
         AGO   .UCB5                                                    08700000
.********************************************************************** 08750000
.*                                                                    * 08800000
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      * 08850000
.********************************************************************** 08900000
.URDUMM  AIF   (NOT('&TYPE' EQ 'UR' OR '&TYPE' EQ 'DUM')).TPC           08950000
         AIF   ('&TYPEA'(6,3) EQ '817').OCR3886 CHECK FOR 3886 @ZA05374 09000000
         AIF   ('&TYPEA'(6,3) NE '84C').URDUM1 IS IT 3838      @G29ANLO 09050000
         DC    X'00'                    ATI/3838               @G860PLN 09100000
         DC    X'20'                    SENSE COUNT/3838       @G29ANLO 09150000
         AGO   .UCB5                                           @G29ANLO 09200000
.OCR3886 ANOP                                                  @ZA05374 09250000
         DC    X'00'                    ATI                    @G860PLN 09300000
         DC    X'06'                    SENSE COUNT FOR 3886.  @ZA05374 09350000
         AGO   .UCB5                                           @ZA05374 09400000
.URDUM1  ANOP                                                  @G29ANLO 09450000
         DC    X'00'                    ATI/UR OR DUMMY UCB'S  @G860PLN 09500000
         DC    X'02'                    SENSE COUNT/UR OR DUMMY UCB'S   09550000
         AGO   .UCB5                                                    09600000
.TPC     AIF   (NOT('&TYPE' EQ 'TP')).TAPEC IS IT TP                    09650000
         AIF   ('&TYPEA'(5,4) EQ '4100').TPCTC                 @YM08445 09700000
         DC    X'00'                    ATI/TP UCB'S           @G860PLN 09750000
         DC    X'02'                    SENSE COUNT/TP UCB'S            09800000
         AGO   .UCB5                                                    09850000
.TPCTC   ANOP                                                           09900000
         DC    X'2C'                    ATI/TP CTC UCB         @G860PLN 09950000
         DC    X'01'                    SENSE COUNT/TP UCB   DCRR21211  10000000
         AGO   .UCB5                                                    10050000
.********************************************************************** 10100000
.*                                                                    * 10150000
.*       THIS SECTION SETS UP THE TAPE UCB AND SEGMENTS TO IT         * 10200000
.*             3420 HAS 24 SENSE BYTES                                * 10250000
.********************************************************************** 10300000
.TAPEC   AIF   ('&TYPEA'(5,2) NE '80').DASDC BR IF NOT TAPE             10350000
         DC    X'00'                    ATI/TAPE UCB'S         @G860PLN 10400000
         DC    X'18'                    SENSE COUNT/TAPE UCB'S          10450000
         AGO   .UCB5                                                    10500000
.********************************************************************** 10550000
.*                                                                    * 10600000
.*       DIRECT ACCESS DEVICES UCB SENSE COUNTS AND SEGMENTS.         * 10650000
.*             THE 2305 AND 33XX'S HAVE 24 SENSE BYTES.        @G860PLN 10700000
.********************************************************************** 10750000
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA 10800000
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA 10850000
.DASDC   AIF   (NOT('&TYPE' EQ 'ML')).NVML                     @G860PLN 10900000
.* NOTE: THIS TEST ASSUMES NO OTHER BIT FOR V3330 IN HALF BYTE @ZM30SS1 10950000
         AIF   ('&TYPEA'(4,1) NE '8').NVML  IF NOT VIRT 3330   @ZM30SS1 11000000
         DC    X'00'               ATI V3330                   @G860PLN 11050000
         DC    X'1C'               SENSE COUNT 28 FOR V3330    @ZM30SS1 11100000
         AGO   .UCB5A                                          @G860PLN 11150000
.NVML    DC    X'00'                    ATI/DA MERLIN UCB'S    @G860PLN 11200000
         DC    X'18'                    SENSE COUNT/DA MERLIN UCB'S     11250000
.UCB5A   ANOP                                                  @G860PLN 11300000
&BITS    SETA  32                  SET RESIDENT ERP FLAG       @G860PLN 11350000
.UCB5    ANOP                                                           11400000
         AIF   ('&SHRUP' EQ '').UCB6                           @ZM30409 11450000
         AIF   (&SHRUP EQ 0).UCB6  IS IT SHARED IN UP, NO BRANCH        11500000
&BITS    SETA  &BITS+64            X'40' SHARED UP BIT         @Y40MPLG 11550000
.UCB6    AIF   ('&FLP1' EQ '').UCB65                           @Y40MPLG 11600000
&BITS    SETA  &BITS+&FLP1         ERROR LOG, ETC.             @G860PLN 11650000
.UCB65   DC    AL1(&BITS),AL1(&STLI),XL1'0'  UCBFLP1,STLI,FL7  @G860PLN 11700000
         DC    A(UCBPRFX)               UCBIEXT,                   @L2C 11733300
         DC    XL1'0'                   UCBCHPCT,                  @L2A 11766600
         DC    XL1'0',XL2'0'            UNUSED,UCBASID         @G860PLN 11800000
         DC    X'00',XL3'00'            UNUSED,UCBWTOID        @G860PLN 11850000
.* IF NO DDT IS SPECIFIED OR THE PARAMETER SPECIFIED IS 0 THEN @ZM48987 11900000
.* GENERATE AN ADCON OF 0 RATHER THAN A V-CON.                 @ZM48987 11950000
         AIF   ('&DDTNM' EQ '' OR '&DDTNM' EQ '0').NODDT       @ZM48987 12000000
         DC    V(&DDTNM)                DDT POINTER            @ZM48987 12050000
         AGO   .CLEXT1                                         @G81OPLN 12100000
.NODDT   ANOP                                                  @ZM48987 12150000
         DC    A(0)                     DDT POINTER            @ZM48987 12200000
.CLEXT1  AIF   ('&CLEXT' EQ '' OR '&CLEXT' EQ '0').CLEXT3      @G81OPLN 12250000
.*                                      CLASS EXTENSION EXIST? @G81OPLN 12300000
         AIF   ('&TYPE' EQ 'ZS').CLEXT2 2305?                  @G81OPLN 12350000
         DC    A(UCBL&DEVAD)            DEV CLASS EXT PTR      @G81OPLN 12400000
         AGO   .CLEXT4                                         @G81OPLN 12450000
.CLEXT2  DC    A(UCBL&DEVADE)           DEV CLASS EXT PTR      @G81OPLN 12500000
         AGO   .CLEXT4                                         @G81OPLN 12550000
.CLEXT3  DC    A(0)                     DEV CLASS EXT PTR      @G81OPLN 12600000
.CLEXT4  DC    2H'0'                    UNUSED                 @G860PLN 12650000
.********************************************************************** 12700000
.* END OF SECTION MOVED FROM BOTTOM TO TOP OF MACRO            @G860PLN 12750000
.********************************************************************** 12800000
.********************************************************************** 12850000
.*                                                                    * 12900000
.*                   THIS SETS UP THE PREFIX.                         * 12950000
.*                                                                    * 13000000
.********************************************************************** 13050000
UCBPRFX  DS    0D                       UCB PREFIX                 @L2C 13100000
         AIF   ('&TYPE' NE 'TP').DOMIH  CHECK FOR TP           @G860PLN 13150000
         AIF   ('&TYPEA'(8,1) EQ '5' OR '&TYPEA'(8,1) EQ '0' OR        *13200000
               '&TYPEA'(7,2) EQ 'F1').DOMIH  SKIP IF NOT 270X  @G860PLN 13250000
         DC    XL2'0',XL1'40',XL1'0'  UNUSED,UCBMIHTI,UCBHOTIO @G860PLN 13300000
         AGO   .CONTIN                SKIP                     @G860PLN 13350000
.DOMIH   DC    XL2'0',XL1'0',XL1'0'   UNUSED,UCBMIHTI,UCBHOTIO @G860PLN 13400000
.CONTIN  DC    2A(0)                    UCBIOQF,IOQL           @G860PLN 13450000
         DC    F'0',XL2'0',XL2'0'       UCBSID,PMCW1,MBI       @G860PLN 13500000
         DC    4XL1'0'               UCBLPM,UNUSED,UCBLPUM,PIM @G860PLN 13550000
         DC    XL8'0'                   UCBCHPID               @G860PLN 13600000
         DC    XL1'01',XL1'0',XL2'0'    UCBLEVEL,IOSF1,MIHCT   @G860PLN 13650000
         DC    XL4'00000001'            UCBLVMSK               @G860PLN 13700000
         DC    2F'0'                    UCBIOQ AND UCBLOCK     @G860PLN 13750000
         DS    0F                       UCB COMMON SEGMENT     @G860PLN 13800000
.********************************************************************** 13850000
.*                                                                    * 13900000
.*    THIS SECTION SETS UP FOR GRAPHICS OR TP IN THE STANDARD PORTION * 13950000
.*                                                                    * 14000000
.********************************************************************** 14050000
&DEVADDR SETC  '&DEVADE'                 SET CAR ADDRESS                14100000
         AIF   ('&TYPE' NE 'TP' AND '&TYPE' NE 'GR').ST GRAPHIC OR TP?  14150000
&DEVADDR SETC  '&DEVAD'                SET TP CAR ADDRESS               14200000
.********************************************************************** 14250000
.*                                                                    * 14300000
.*        THIS SETS UP THE STANDARD PORTION OF ALL UCBS.              * 14350000
.*             UCBJBNR,UCBFL5,UCBID,UCBSTAT                           * 14400000
.*             UCBCHAN(UCBCHA/UCBUA),UCBFLA,UCBFLB                    * 14450000
.*                                                                    * 14500000
.********************************************************************** 14550000
.ST      ANOP                                                           14600000
UCB&UCBNO  DC  AL1(0),AL1(&FL5),X'FF'   JBNR,UCBFL5,ID         @G860PLN 14650000
         AIF   ('&DEVADE' EQ 'JES').SKIP1  DUMMY JES CONSOLE   @ZMC0022 14700000
&TYPENO(1) EQU UCB&UCBNO                                       @G860PLN 14750000
.SKIP1   AIF   (&OFF EQ 0).ST1         TEST FOR DEVICE OFFLINE @ZMC0022 14800000
         DC    X'&SYSRES'           UCBSTAT FOR DEVICE OFFLINE @G860PLN 14850000
         AGO   .ST2                                                     14900000
.ST1     DC    X'8&SYSRES'              UCBSTAT                         14950000
.ST2     DC    XL2'&DEVAD'              UCBCHAN                @G860PLN 15000000
         DC    XL2'0020'       UCBFLA,UCBFLB (UCBNOCON SET ON) @G860PLN 15050000
         AIF   ('&DEVADE' NE 'JES').SKIP2  NO CHAINING FOR JES @ZMC0022 15100000
         DC    A(0)                     UCBNXUCB               @ZMC0022 15150000
         AGO   .SKIP3                                          @ZMC0022 15200000
.SKIP2   DC    A(&TYPENO(2).)           UCBNXUCB               @ZMC0022 15250000
.SKIP3   AIF   ('&TYPE' NE 'ZS').REG    IF NOT A 2305, BRANCH  @ZMC0022 15300000
         DC    X'08'                    UCBWGT                 @G860PLN 15350000
         AGO   .UCB3                                                    15400000
.REG     DC    X'00'                    UCBWGT                 @G860PLN 15450000
.UCB3    DC    CL3'&DEVADDR',XL4'&TYPEA' UCBNAME,UCBTYPE                15500000
         DC    XL1'0'                   UCBFLC                          15550000
         DC    AL3(UCBX&UCBNO)          POINTER TO UCB EXTENSION        15600000
.********************************************************************** 15650000
.*   TEST FOR A PRINTER AND THE UCS FEATURE OR A 3505 OR A 3525 DEVICE* 15700000
.*   THE 'WR' DEVICE TYPE ACRONYM IS USED FOR THE 3505 CARD PUNCH AND * 15750000
.*   THE 3525 CARD PRINT. ONLY THE 3211, A UCS 1403 PRINTER, A 3540   * 15800000
.*   DISKET READER AND A 3800 PRINTER GET A UCB EXTENSION.    @Y40OPLG* 15850000
.*                                                             @G860PLN 15900000
.********************************************************************** 15950000
         AIF   ('&TYPEA'(6,3) EQ '842').URDSM 3851 DEVICE      @G860PLN 16000000
         AIF   ('&TYPEA'(6,3) EQ '844').UR3540 3540 DISKET     @Y30OPLA 16050000
         AIF   ('&TYPE' EQ 'WR').UC3211 TEST FOR 3211 OR A 3505/3525    16100000
         AIF   ('&TYPE' NE 'UR').NOTPRT TEST FOR UR                     16150000
         AIF   (NOT(('&TYPEA'(6,3) EQ '808') AND ('&TYPEA'(3,1) EQ '8')*16200000
               )).NOTPRT                IT IS NOT A PRINTER WITH UCS    16250000
         DC    A(UCBW&UCBNO)                                            16300000
         AGO   .END                                                     16350000
.UC3211  AIF   ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0E').END    16400000
.*                                                             @Y40MPLG 16450000
.UR3540  DC    A(UCBW&UCBNO)       3211,3540,3800              @Y30OPLA 16500000
         AGO   .END                                                     16550000
.URDSM   ANOP                      3851 DEVICE                 @G860PLN 16600000
         DC    2F'0'               2 WORD DEVICE DEPEND. SECT. @Y30LPLC 16650000
         AGO   .END                                            @Y30LPLC 16700000
.********************************************************************** 16750000
.*                                                                    * 16800000
.*       GRAPHICS AND ANR GRAPHICS SEGMENT SECTION                    * 16850000
.*                                                                    * 16900000
.********************************************************************** 16950000
.NOTPRT  AIF   ('&TYPE' NE 'GR').URCHECK  TEST FOR GRAPHICS             17000000
  AIF  ('&TYPEA'(7,2) NE '09' AND '&TYPEA'(7,2) NE '0A' AND '&TYPEA'(7,X17050000
               2) NE '0B').NOTANR                              @G860PLN 17100000
         DC    XL2'&TMSK',XL2'0'        ADDITIONAL OPTIONS BYTE         17150000
         DC    3F'0'                    IRB ADD,IOB ADD,D.I.            17200000
         AGO   .END                                                     17250000
.NOTANR  ANOP                                                           17300000
         DC    3F'0'                    SENSE,DUC,GCB                   17350000
         AIF   ('&ERRTAB' EQ '66').SDA0 IS THIS 2260                    17400000
         DC    XL1'&DEVNDX'             DEVICE INDEX           @ZA40477 17450000
         AIF   (&GR EQ 1).GRF           TEST FOR GAM                    17500000
         DC    XL3'00'                  NO GAM SUPPORT                  17550000
         AGO   .END                                                     17600000
.GRF     AIF   ('&TYPEA'(1,2) EQ '31').MOD1  CHECK FOR 2250-1           17650000
         DC    VL3(IFFB&NOP2)           BUFFER TABLE NAME               17700000
         AGO   .END                                                     17750000
.MOD1    DC    VL3(IFFB&DEVAD)          BUFFER TABLE NAME               17800000
         AGO   .END                                                     17850000
.SDA0    DC    F'0'                     2260                   @G860PLN 17900000
         AGO   .END                                                     17950000
.********************************************************************** 18000000
.*                                                                    * 18050000
.*            UNIT RECORD,TELE-PROCESSING AND DUMMY UCB SEGMENTS      * 18100000
.********************************************************************** 18150000
.URCHECK AIF   ('&TYPE' NE 'UR' ).DUMMY                                 18200000
         AIF   ('&TYPEA'(6,3) NE '84C').UR3886 IS IT A 3838    @G29ANLO 18250000
         DC    2F'0'                 3838 DEVICE DEPEND. SECT. @G29ANLO 18300000
         AGO   .END                                            @G29ANLO 18350000
.UR3886  ANOP                                                  @G29ANLO 18400000
         AIF   ('&TYPEA'(6,3) NE '817').END  BRANCH IF IT IS NOT A 3886 18450000
         DC    F'0'                     FOUR BYTES FOR THE 3886         18500000
         AGO   .END                                                     18550000
.DUMMY   AIF   ('&TYPE' NE 'DUM').TP                                    18600000
         DC    F'0'                     EXTRA WORD FOR DUM UCB USE      18650000
         AGO   .END                                                     18700000
.TP      AIF   (NOT('&TYPE' EQ 'TP')).TAPE IS IT TP                     18750000
   AIF   (NOT('&TYPEA'(8,1) EQ '5' OR '&TYPEA'(8,1) EQ '6'  OR         *18800000
               '&TYPEA'(5,4) EQ '4100' OR '&TYPEA'(7,2) EQ 'F1' )).END  18850000
         DC    2F'0'                    3704/3705 SEGMENT               18900000
*                                  ALSO CTC & 3791             @Z40KPLG 18950000
         AIF   (NOT('&TYPEA'(5,4) EQ '4100')).END              @ZMC1166 19000000
         DC    2F'0'                    ADDITIONAL CTC WORDS   @ZMC1166 19050000
         AGO   .END                                                     19100000
.********************************************************************** 19150000
.*                                                                    * 19200000
.*             THIS SECTION SETS UP THE TAPE SEGMENT                  * 19250000
.*                                                             @G860PLN 19300000
.********************************************************************** 19350000
.TAPE    AIF   ('&TYPEA'(5,2) NE '80').DASD BR IF NOT TAPE              19400000
         AIF   (&SHR EQ 1).NOASP        3400 SHARABLE?                  19450000
         DC    H'0',H'0',XL6'0',XL2'8000',2F'0'   VOLSER                19500000
*          FILE SEQ CT, FILE SEQ NO,NOT SHARABLE/STATAB & DMCT,DS SERNO 19550000
         AGO   .REST1                                                   19600000
.NOASP   DC    H'0',H'0',XL6'0',XL2'0',2F'0'   VOLSER                   19650000
*               FILE SEQ CT, FILE SEQ NO,STATAB & DMCT,DS SERNO         19700000
.REST1   DC    AL1(&TMSK),AL3(UCBW&UCBNO) UCBVOPT AND UCBXTNB           19750000
         AGO   .END                     END OF TAPE UCB                 19800000
.********************************************************************** 19850000
.*                                                                    * 19900000
.*             DIRECT ACCESS DEVICES SEGMENTS.                        * 19950000
.*                                                             @G860PLN 20000000
.********************************************************************** 20050000
.*             ML (MERLIN) TEST INCLUDES 3330,3340,3350        @Z30RSTA 20100000
.*             REFERENCE MACRO SGIEC202 IN AGENLIB             @Z30RSTA 20150000
.DASD    DC    F'0',XL6'0',XL2'0',XL4'0'                       @G860PLN 20200000
*               VTOC, STATB & DMCT, UCBSQC,UCBFL4,UCBUSER,RESERVED      20250000
         AIF   ('&TYPE' EQ 'ML').END                                    20300000
         AIF   (&IECIZCT  NE 0).ZSET    TEST FOR 1ST ZEUS UCB           20350000
&IECIZS1 SETA  &UCBNO                   SET POINTER TO FIRST UCB        20400000
.ZSET    ANOP                                                           20450000
&IECIZCT   SETA  &IECIZCT+1             UPDATE COUNTER                  20500000
         AIF   (&IECIZCT  EQ 8).ZSET1   TEST FOR LAST                   20550000
&A       SETA  &UCBNO+1                 SET PTR TO NEXT UCB             20600000
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB       20650000
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING         20700000
         AGO   .END                     NOT LAST 2305                   20750000
.ZSET1   ANOP                           LAST DEVICE ON 2305 CHAIN       20800000
&A       SETA  &IECIZS1+1               SET LAST TO POINT TO SECOND     20850000
         DC    A(UCB&IECIZS1-512)       POINTER TO FIRST ZEUS UCB       20900000
         DC    A(UCB&A-512)             POINTER TO NEXT IN RING         20950000
&IECIZCT   SETA  0                      CLEAR COUNTER                   21000000
.END     ANOP                                                  @ZM48987 21050000
         MEND                                                           21100000
