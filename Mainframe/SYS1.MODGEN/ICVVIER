*%GOTO   CVAFVIER;     /* VIER MAPPING MACRO                            00050000
         MACRO                                                          00100000
&LABEL   ICVVIER  &DSECT=YES                                            00150000
.***** START OF SPECIFICATIONS **************************************** 00200000
.*                                                                      00250000
.*$MAC(ICVVIER) COMP(DM1CV) PROD(DMS-XZ): CVAF VIER MAPPING MACRO       00300000
.*                                                                      00350000
.* DESCRIPTIVE-NAME = CVAF - VIER MAPPING MACRO                         00400000
.*                                                                      00450000
.*PROPRIETARY V3 STATEMENT                                              00462500
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00475000
.*"RESTRICTED MATERIALS OF IBM"                                         00487500
.*5695-DF1                                                              00500000
.*(C) COPYRIGHT 1980,1985  IBM CORP.                                    00512500
.*END PROPRIETARY V3 STATEMENT                                          00531200
.*                                                                      00550000
.* STATUS = RELEASE 2.1.0                                          @L1C 00600000
.*                                                                      00650000
.*                                                                      00675000
.* FUNCTION = THIS MACRO MAPS THE VTOC INDEX ENTRY RECORD (VIER),       00700000
.*             A VIER SECTION AND A VTOC INDEX ENTRY (VIE).             00750000
.*                                                                      00800000
.* NOTES = NONE                                                         00850000
.*                                                                      00900000
.* PATCH-LABEL = NOT APPLICABLE                                         00950000
.*                                                                      01000000
.* MODULE-TYPE = MACRO                                                  01050000
.*                                                                      01100000
.*    PROCESSOR = PLS OR BAL                                            01150000
.*                                                                      01200000
.*    ATTRIBUTES = MAPPING MACRO                                        01250000
.*                                                                      01300000
.* ENTRY-POINT = ICVAFVXR                                               01350000
.*                                                                      01400000
.*    PURPOSE = SEE FUNCTION ABOVE.                                     01450000
.*                                                                      01500000
.*    LINKAGE = PLS -  INCLUDE ICVVIER                                  01550000
.*              BAL -  LABEL   ICVVIER  DSECT=NO | DSECT=YES            01600000
.*                                                                      01650000
.*    INPUT   = PLS - NONE                                              01700000
.*              BAL - 'LABEL' WILL CAUSE A LABEL DSECT STATEMENT        01750000
.*                    INSTEAD OF 'VIER' DSECT STATEMENT.                01800000
.*                    DSECT=NO IS REQUIRED IF DSECT STATEMENT           01850000
.*                    IS NOT DESIRED.                                   01900000
.*                                                                      01950000
.*    OUTPUT  = VIER, VIER SECTION AND VIE EXPANSION.                   02000000
.*                                                                      02050000
.* EXIT-NORMAL = END OR MEND STATEMENT                                  02100000
.*                                                                      02150000
.* EXIT-ERROR = AN MNOTE WILL BE GENERATED IF AN INVALID KEYWORD IS     02200000
.*              PRESENT FOR BAL.                                        02250000
.*                                                                      02300000
.* EXTERNAL-REFERENCES = N/A                                            02350000
.*                                                                      02400000
.* TABLES = NONE                                                        02450000
.*                                                                      02500000
.* MACROS = NONE                                                        02550000
.*                                                                      02600000
.* CHANGE-ACTIVITY = NEW MACRO                                          02650000
.*                                                                      02666600
.*$L0=IXVTOC,JDM1134,T790225,STLBS: INDEXED VTOC SUPPORT           @L0A 02683200
.*$L1=SVCR,HDP2210,T840521,STLMDB: CVAF VSCR                       @L1A 02691600
.*                                                                      02700000
.*** END OF SPECIFICATIONS ******************************************** 02750000
         LCLC  &L                      DEFINE LOCAL CHAR VARIABLE       02800000
&L       SETC  'VIER'                                                   02850000
         AIF   ('&LABEL' EQ '').CDSECT                                  02900000
&L       SETC  '&LABEL'                                                 02950000
.CDSECT  ANOP                                                           03000000
         SPACE 1                                                        03050000
*********************************************************************** 03100000
*        VIER - VTOC INDEX ENTRY RECORD EXPANSION                       03150000
*********************************************************************** 03200000
         SPACE 1                                                        03250000
         AIF   ('&DSECT' EQ 'NO').NDSECT1                               03300000
         AIF   ('&DSECT' EQ 'YES').DSECT1                               03350000
         MNOTE 12,'***  IHB001 INVALID OPERAND - DSECT=&DSECT'          03400000
         AGO   .FINISH                                                  03450000
         SPACE 1                                                        03500000
*********************************************************************** 03550000
*        VIER - HEADER EXPANSION                                        03600000
*********************************************************************** 03650000
         SPACE 1                                                        03700000
.DSECT1  ANOP                                                           03750000
&L       DSECT                         VIER                             03800000
         DS    0F                                                       03850000
         AGO   .VIER                                                    03900000
.NDSECT1 ANOP                                                           03950000
&L       DS    0F                      VIER                             04000000
.VIER    ANOP                                                           04050000
VXHR     DS    0F                      VIER HEADER                      04100000
VXID     DS    F                       EBCDIC 'VIER'                    04150000
VXRBA    DS    F                       RBA OF THIS VIER                 04200000
VXHZPTR  DS    F                       HORIZONTAL RBA PTR. TO THE       04250000
*                                      NEXT VIER AT THIS INDEX LEVEL    04300000
VXOHP    DS    F                       OLD HORIZONTAL POINTER           04350000
VXLVL    DS    XL1                     LEVEL NUMBER OF THIS VIER        04400000
VXFLG1   DS    XL1                     VIER FLAG BYTE                   04450000
VXFHLV   EQU   X'80'                   FIRST HIGH LEVEL VIER            04500000
VXVISE   EQU   X'40'                   INDEX STRUCTURE ERROR (FIRST     04550000
*                                      HIGH LEVEL VIER ONLY)            04600000
         DS    H                       RESERVED                         04650000
VXPTRL   DS    XL1                     LENGTH OF PTR. FIELD IN THE      04700000
*                                      INDEX ENTRY:                     04750000
*                                      - FIVE BYTES(CCHHR) FOR LEVEL    04800000
*                                        1 VIER'S.                      04850000
*                                      - FOUR BYTES(RBA) FOR LEVEL 2    04900000
*                                        OR HIGHER LEVEL VIER'S.        04950000
VXSECNO  DS    XL1                     NUMBER OF SECTIONS IN THIS VIER  05000000
VXSECL   DS    H                       SECTION LENGTH                   05050000
VXFOFST  DS    F                       OFFSET INTO THIS VIER FOR THE    05100000
*                                      FIRST SECTION.                   05130000
VXLSOFST DS    F                       OFFSET INTO THIS VIER FOR THE    05160000
*                                      LAST SECTION CONTAINING ENTRIES. 05250000
VXHIKEY  DS    CL44                    THE HIGH DEY VALUE OF THIS VIER  05300000
VXSECS   EQU   *                       START OF SECTIONS CONTAINING     05350000
*                                      INDEX ENTRIES.                   05400000
VXHRLEN  EQU   *-&L                    LENGTH OF VIER HEADER            05450000
         SPACE 1                                                        05500000
*********************************************************************** 05550000
*        VIER - SECTION DSECT                                           05600000
*********************************************************************** 05650000
         SPACE 1                                                        05700000
VXSEC    DSECT                         LABEL FOR INDEX SECTION          05750000
VXLEOFST DS    H                       OFFSET TO LAST ENTRY IN THIS     05800000
*                                      SECTION.                         05850000
VXENTS   EQU   *                       START OF INDEX ENTRIES           05900000
         SPACE 1                                                        05950000
*********************************************************************** 06000000
*        VIER - ENTRY DSECT                                             06050000
*********************************************************************** 06100000
         SPACE 1                                                        06150000
VXENT    DSECT                         INDEX ENTRY                      06200000
VXEFLG   DS    XL1                     INDEX ENTRY FLAG BYTE            06250000
VXEKEYL  DS    XL1                     LENGTH OF THE KEY IN THIS INDEX  06300000
*                                      ENTRY.                           06350000
VXEFC    DS    XL1                     FRONT COMPRESSION COUNT          06400000
VXERPTR  EQU   *                       RECORD POINTER:                  06450000
*                                      - CCHHR OF THE DATA RECORD FOR   06500000
*                                        LEVEL 1 INDEX ENTRIES.         06550000
*                                      - RBA OF THE LOWER LEVEL VIER    06600000
*                                        FOR ALL OTHERS.                06650000
         SPACE 1                                                        06700000
*********************************************************************** 06750000
*        VIER - ENTRY KEY DSECT                                         06800000
*********************************************************************** 06850000
         SPACE 1                                                        06900000
VXEKEY   DSECT                         KEY OF THE DATA RECORD IF LEVEL  06950000
*                                      1 INDEX ENTRY; THE HIGH KEY      07000000
*                                      IN THE HEADER OF THE LOWER VIER  07050000
*                                      FOR OTHER LEVELS.                07100000
         SPACE 1                                                        07150000
.FINISH  MEND                                                           07200000
**********************************************************************/ 07250000
*%CVAFVIER: ;                                                           07300000
*                                                                       07350000
*/********************************************************************/ 07400000
*/*      VIER - VTOC INDEX ENTRY RECORD EXPANSION                    */ 07450000
*/********************************************************************/ 07500000
*                                                                       07550000
* DCL 1 VIER BASED(VIERPTR) BDY(WORD), /* VIER                       */ 07600000
*      2 VXHR  BDY(WORD),          /* VIER HEADER                    */ 07650000
*       3 VXID       CHAR(4),      /* EBCDIC 'VIER'                  */ 07700000
*       3 VXRBA    FIXED(32),      /* RBA OF THIS VIER               */ 07750000
*       3 VXHZPTR  FIXED(32),      /* HORIZONTAL RBA POINTER TO THE     07800000
*                                     NEXT VIER AT THIS INDEX LEVEL  */ 07850000
*       3 VXOHP    FIXED(32),      /* OLD HORIZONTAL POINTER         */ 07900000
*       3 VXLVL     FIXED(8),      /* LEVEL NUMBER OF THIS VIER      */ 07950000
*       3 VXFLG1      BIT(8),      /* VIER FLAG BYTE                 */ 08000000
*        4 VXFHLV     BIT(1),      /* FIRST HIGH LEVEL VIER          */ 08050000
*        4 VXVISE     BIT(1),      /* INDEX STRUCTURE ERROR (FIRST      08100000
*                                     HIGH LEVEL VIER ONLY)          */ 08150000
*        4 *          BIT(6),      /* RESERVED                       */ 08200000
*       3 *          CHAR(2),      /* RESERVED                       */ 08250000
*       3 VXPTRL    FIXED(8),      /* LENGTH OF POINTER FIELD IN THE    08300000
*                                     INDEX ENTRY -                     08350000
*                                     - FIVE BYTES (CCHHR) FOR LEVEL    08400000
*                                       1 VIERS.                        08450000
*                                     - FOUR BYTES (RBA) FOR LEVEL 2    08500000
*                                       OR HIGHER LEVEL VIERS.       */ 08550000
*       3 VXSECNO   FIXED(8),      /* NUMBER OF SECTIONS IN THIS VIER*/ 08600000
*       3 VXSECL   FIXED(15),      /* SECTION LENGTH                 */ 08650000
*       3 VXFSOFST FIXED(31),      /* OFFSET INTO THIS VIER FOR THE     08700000
*                                     FIRST SECTION.                 */ 08730000
*       3 VXLSOFST FIXED(31),      /* OFFSET INTO THIS VIER FOR THE     08760000
*                                     LAST SECTION CONTAINING ENTRIES*/ 08850000
*       3 VXHIKEY   CHAR(44),      /* HIGH KEY VALUE OF THIS VIER    */ 08900000
*      2 VXSECS     CHAR(*);       /* START OF SECTIONS CONTAINING      08950000
*                                     INDEX ENTRIES.                 */ 09000000
* DCL 1 VXSEC BASED(VXSECPTR) BDY(WORD), /* INDEX SECTION            */ 09050000
*      2 VXLEOFST FIXED(15),       /* OFFSET TO LAST ENTRY IN THIS      09090000
*                                     SECTION.                       */ 09130000
*      2 VXENTS    CHAR(*);        /* START OF INDEX ENTRIES         */ 09170000
* DCL 1 VXENT BASED(VXENTPTR) BDY(WORD), /* INDEX ENTRY              */ 09210000
*      2 VXEGLC BDY(BYTE),         /* FIXED LENGTH FIELDS            */ 09300000
*       3 VXEFLG    BIT(8),        /* FLAG BYTE - RESERVED           */ 09340000
*       3 VXEKEYL FIXED(8),        /* LENGTH OF THE KEY IN THIS         09380000
*                                     INDEX ENTRY                    */ 09420000
*       3 VXEFC   FIXED(8),        /* FRONT COMPRESSION COUNT        */ 09460000
*      2 VXERPTR  CHAR(*);         /* RECORD POINTER - CCHHR OF THE     09500000
*                                     DATA RECORD FOR LEVEL 1 INDEX     09550000
*                                     ENTRIES. RBA OF THE LOWER         09600000
*                                     LEVEL VIER FOR ALL OTHERS.     */ 09650000
* DCL VXEKEY CHAR(*)               /* KEY OF THE DATA RECORD IF LEVEL*/ 09700000
*     BASED(ADDR(VXERPTR)+VXPTRL); /*1 INDEX ENTRY. THE HIGH KEY IN     09750000
*                                     THE HEADER OF THE LOWER VIER      09800000
*                                     FOR OTHER LEVELS.              */ 09850000
*/********************************************************************/ 09900000
