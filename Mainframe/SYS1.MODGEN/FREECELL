         MACRO                                                          00050000
&NAME    FREECELL   &CPID=,&CELL=,&BRANCH=YES,&SAVE=YES,&RELATED=       00100000
.*       OS/VS2 RELEASE 2   ---  9/15/73                                00120000
.********************************************************************   00121800
.*                                                                      00123600
.* COPYRIGHT       5685-001                                             00125400
.*                 THIS MODULE IS "RESTRICTED MATERIALS OF IBM"         00127200
.*                 (C) COPYRIGHT IBM CORP. 1982, 1988                   00129000
.*                 LICENSED MATERIALS - PROPERTY OF IBM                 00130800
.*                                                                      00136200
.* STATUS          HBB3310                                              00138000
.*                                                                      00139800
.* CHANGE ACTIVITY $L1=PROD310,HBB3310,860716,PDF5:                     00141600
.*                     Add a copyright notice to the prolog.     @L1A   00143400
.*                                                                      00145200
.*                 $L2=PROD310,HBB3310,870603,PDF5:                     00145600
.*                     Copyright changes                         @L2A   00146000
.*                                                                      00146400
.********************************************************************   00147000
         AIF   ('&BRANCH' NE 'YES').ERR1  BRANCH=YES MANDATORY          00150000
         AIF   ('&CPID' EQ '' ).ERR3        CPID REQUIRED               00160000
         AIF   ('&CELL' EQ '').ERR3A        CELL REQUIRED               00170000
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE A REGISTER     00200000
         AIF   ('&CELL'(1,1) NE '(').ERR2B  CELL MUST BE A REGISTER     00250000
         AIF   ('&CELL(1)' EQ '0').ERR2B   REG. ZERO RESERVED FOR CPID  00252000
         AIF   ('&SAVE' EQ 'YES' OR '&SAVE' EQ 'NO').SAVOK              00256000
         IHBERMAC 1015,&SAVE,''SAVE='',YES    INCORRECT 'SAVE=' VALUE   00258400
.*       SYNTAX OK, CONTINUE WITH EXPANSION OF MACRO                    00258800
.SAVOK   AIF   ('&NAME' EQ '').SKPN                                     00260000
&NAME    DS    0H                  FREECELL MACRO                       00300000
.SKPN    AIF   ('&SAVE' EQ 'NO').NRGS                                   00350000
         STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12          00400000
.NRGS    AIF   ('&CPID(1)' EQ '0').NZLD                                 00450000
         LR    0,&CPID(1)          LOAD R0 WITH CPID                    00500000
.NZLD    AIF   ('&CELL(1)' EQ '1').NOLD                                 00550000
         LR    1,&CELL(1)          LOAD R1 WITH CELL ADDRESS            00600000
.NOLD    L     3,16(0,0)           CVT ADDRESS FOR FREECELL     YM02296 00700000
         L     15,CVTFRECL-CVTMAP(0,3)    LOAD FREECELL RT ENTRY POINT  00710000
         BALR  14,15               GO TO FREECELL ROUTINE               00800000
         AIF   ('&SAVE' EQ 'NO').NRGR                                   00850000
         LM    2,12,28(13)         RESTORE CALLERS REGISTERS            00900000
.NRGR    MEXIT                                                          00950000
.ERR1    IHBERMAC  1006,''BRANCH=YES''                                  01000000
         MEXIT                                                          01050000
.ERR2    IHBERMAC  1001,CPID,&CPID                                      01100000
         MEXIT                                                          01150000
.ERR2B   IHBERMAC  1001,CELL,&CELL                                      01200000
         MEXIT                                                          01202000
.ERR3    IHBERMAC  1006,CPID                                            01210000
         MEXIT                                                          01220000
.ERR3A   IHBERMAC  1006,CELL                                            01230000
         MEND                                                           01250000
