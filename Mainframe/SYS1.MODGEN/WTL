         MACRO                                                          00050000
&NAME    WTL   &MESG,&OPTION=,&MF=I                                @L1C 00052300
.********************************************************************** 00054600
.*                                                                    * 00056900
.* MACRO NAME = WTL                                               @L1A* 00059200
.*                                                                    * 00061500
.* DESCRIPTIVE NAME = WRITE-TO-LOG MACRO                          @L1A* 00063800
.*                                                                    * 00066100
.*                                                                  */  00067400
.*                                                                    * 00069000
.* COPYRIGHT =                                                    @L1A* 00070600
.*              THIS PRODUCT CONTAINS RESTRICTED MATERIALS OF IBM.    * 00072200
.*              5740-XC6 COPYRIGHT IBM CORPORATION 1984.              * 00073800
.*                                                                    * 00075400
.* STATUS = OS/VS2 JBB2125                                        @L1A* 00077000
.*                                                                  */  00079100
.* FUNCTION =   BUILD A WRITE-TO-LOG PARAMETER LIST AND/OR        @L1A* 00081000
.*              THE CODE WHICH WILL INVOKE SVC 36 TO ISSUE        @L1A* 00082100
.*              THE MESSAGE.                                      @L1A* 00083200
.*                                                                    * 00084500
.* INVOCATION = SPECIFY: WTL &MESG <,&OPTION=...> <,&MF=...>      @L1A* 00086800
.*                                                                    * 00089100
.*              WHERE:                                            @L1A* 00091400
.*                MESG=    THE MESSAGE TEXT TO BE WRITTEN         @L1A* 00093700
.*                                                                    * 00096000
.*                OPTION=  SPECIFIES WHETHER OR NOT A SYSLOG      @L1A* 00097700
.*                         PREFIX IS SUPPLIED BY THE ISSUER.      @L1A* 00099400
.*                         VALID VALUES ARE:                      @L1A* 00101100
.*                           PREFIX   (AUTHORIZED PROGRAMS ONLY)  @L1A* 00102900
.*                           NOPREFIX (ALL PROGRAMS, THE DEFAULT) @L1A* 00105200
.*                                                                    * 00107500
.*                MF=      SPECIFIES THE TYPE OF EXPANSION        @L1A* 00109800
.*                         REQUIRED. VALID VALUES ARE:            @L1A* 00112100
.*                           I (STANDARD FORM, THE DEFAULT)       @L1A* 00114400
.*                           L (LIST FORM)                        @L1A* 00116700
.*                           E (EXECUTE FORM)                     @L1A* 00119000
.*                                                                    * 00121300
.* CHANGE ACTIVITY =                                              @L1A* 00137400
.* $L1=MRRHCLOG, JBB2125, 830608, PD2G: HARDCOPY LOG FORMAT           * 00139700
.*                                                                    * 00142000
.********************************************************************** 00144300
         GBLB  &IHBWTL                                                  00150000
*        MACDATE = 6/8/83                                          @L1A 00175000
&IHBWTL  SETB  1                                                        00200000
&NAME    WTO   &MESG,MF=&MF                                             00250000
&IHBWTL  SETB  0                                                        00300000
         AIF   ('&MF' NE 'L').CHKOPTN  WAS MF=L SPECIFIED?         @L1C 00308300
         AIF   (T'&OPTION EQ 'O').END  'OPTION' SPECIFIED, TOO?    @L1A 00316600
         MNOTE 4,'''OPTION'' IGNORED WITH ''MF=L'''  YES, ERROR    @L1A 00324900
         MEXIT                     PROCESSING IS DONE              @L1A 00333200
.CHKOPTN ANOP                                                      @L1A 00341500
         AIF   (T'&OPTION EQ 'O' OR '&OPTION' EQ 'NOPREFIX').NOPRFX    X00349800
                                   'NOPREFIX' DESIRED?             @L1A 00358100
         AIF   ('&OPTION' EQ 'PREFIX').PRFX  'PREFIX' DESIRED?     @L1A 00366400
         MNOTE 8,'''OPTION'' VALUE INVALID.  MUST BE ''PREFIX'' OR ''NOX00374700
               PREFIX''.  ''NOPREFIX'' ASSUMED'  INDICATE ERROR    @L1A 00383000
.NOPRFX  ANOP                                                      @L1A 00391300
         LA    0,0                 INDICATE NO PREFIX SUPPLIED     @L1A 00399600
         AGO   .SVC                GENERATE SVC INSTRUCTION        @L1A 00407900
.PRFX    ANOP                                                      @L1A 00416200
         LA    0,4                 INDICATE PREFIX SUPPLIED        @L1A 00424500
.SVC     ANOP                                                      @L1A 00432800
         SVC   36                  ISSUE WTL SVC                        00441100
.END     MEND                                                           00450000
