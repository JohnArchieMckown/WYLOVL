********************************************************************    00050000
*                                                                       00100000
*  $MAC(ICHRFRTB) COMP(XXH00) PROD(RACF):                               00150000
*                                                                       00200000
*  DESCRIPTIVE NAME = RACF ROUTER TABLE ENTRY GENERATOR                 00250000
*                                                                       00300000
*  COPYRIGHT =                                                          00350000
*              5740-XXH COPYRIGHT IBM CORP 1976, 1985                   00400000
*              LICENSED MATERIAL - PROGRAM PROPERTY OF IBM              00450000
*              REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083    00500000
*                                                                       00550000
*  STATUS = RACF VERSION 1 RELEASE 7                               @L7A 00600000
*                                                                       00650000
*  FUNCTION = THE ICHRFRTB MACRO CREATES ENTRIES TO BE USED IN THE      00700000
*             RACF ROUTER TABLE.                                        00750000
*             (MODULE NAME - ICHRFR0X/ICHRFR01)                    @L7A 00800000
*                                                                       00850000
*  INVOCATION = A TABLE ENTRY IS CREATED BY THE ICHRFRTB MACRO,         00900000
*               ISSUED WITH PARAMETERS WHOSE VALUES ARE TO APPEAR       00950000
*               IN THAT ENTRY. THE PARAMETERS ARE:                      01000000
*                REQSTOR - 8 CHARACTER REQUESTOR NAME.  OPTIONAL.       01050000
*                          DEFAULTS TO BLANKS.                          01100000
*                TYPE    - THE ONLY VALUE ALLOWED IS END. TYPE=END      01150000
*                          WILL CAUSE A BLANK ENTRY TO BE GENERATED     01200000
*                          AT THE END OF THE ROUTER TABLE.  IT MUST     01250000
*                          BE SPECIFIED IF RESOURCE CLASS IS NOT.       01300000
*                CLASS   - 8 CHARACTER RESOURCE CLASS NAME.             01350000
*                          CLASS IS A REQUIRED PARAMETER.               01400000
*                SUBSYS  - 8 CHARACTER SUBSYSTEM NAME.  OPTIONAL.       01450000
*                          DEFAULTS TO BLANKS.                          01500000
*                ACTION  - ONE OF THE FOLLOWING VALUES:                 01550000
*                            NONE, RACF                                 01600000
*                          ACTION IS A REQUIRED PARAMETER.  IT          01650000
*                          SPECIFIES WHETHER RACF IS TO BE CALLED       01700000
*                          FOR THIS ENTRY, OR NOT                       01750000
*                                                                       01800000
*   REQUIREMENTS FOR USE - NONE                                         01850000
*                                                                       01900000
*  MODULE TYPE = MACRO                                                  01950000
*                                                                       02000000
*  CHANGE ACTIVITY=                                                     02050000
*              $T0=AZ59621,SAF/RF14,811102,CNTDSD:                      02100000
*              $L7=HRF1702,RACF/170,841030,CNTDSD:                 @L7A 02150000
*              OY04384 ALLOW 0 IN FIRST 4 CHARACTERS OF CLASS  @YA04384 02175000
*                                                                       02200000
********************************************************************    02250000
         MACRO                                                          02300000
&NAME    ICHRFRTB &REQSTOR=,                                           *02350000
               &TYPE=,                                                 *02400000
               &CLASS=,                                                *02450000
               &SUBSYS=,                                               *02500000
               &ACTION=                                                 02550000
.**************************                                             02600000
.*   DEFINE GLOBAL VARIABLE                                             02650000
.**************************                                             02700000
         GBLB  &ICHIBM             SET TO INDICATE MACRO BEING USED TO  02750000
.*                                 BUILD IBM SUPPLIED ROUTER TABLE @L7A 02800000
         GBLB  &X                  USED FOR DETECTION OF MISSING        02850000
.*                                 TYPE=END ENTRY                       02900000
.**************************                                             02950000
.*   DEFINE LOCAL VARIABLES                                             03000000
.**************************                                             03050000
         LCLC  &RQSTR              USED TO CONTAIN REQUESTOR NAME       03100000
         LCLC  &SUB                USED TO CONTAIN SUBSYSTEM NAME       03150000
         LCLA  &ACTCODE            USED TO CONTAIN NUMERIC VALUE        03200000
.*                                 FOR ACTION CODE, AS FOLLOWS:         03250000
.*                                   0 - DO NOT CALL RACF               03300000
.*                                   1 - CALL RACF                      03350000
         LCLA  &N                  INDEX INTO CHAR STRINGS         @L7A 03400000
.*********************************************                          03450000
.*   CLASS OR TYPE=END IS A REQUIRED PARAMETER                          03500000
.*********************************************                          03550000
         AIF   ('&CLASS' EQ '' AND '&TYPE' EQ '').ERR01                 03600000
         AIF   ('&TYPE' EQ 'END').ENDMAC  BUILD LAST ENTRY              03650000
         AIF   ('&TYPE' NE '').ERR05      ONLY NONNULL TYPE VALUE       03700000
.*                                        ALLOWED IS END                03750000
.********************************************************************** 03800000
.*  CLASS AND ACTION ARE REQUIRED PARAMETERS AND                        03850000
.*  SCREEN FOR FIRST 4 CHARS CONTAINING NATIONAL OR DIGIT          @L7A 03900000
.********************************************************************** 03950000
         AIF   ('&CLASS' EQ '').ERR02                                   04000000
         AIF   (K'&CLASS LT 4 OR                                       *04050000
               K'&CLASS GT 8).BADCLAS                              @L7A 04100000
         AIF   (&ICHIBM).CLASSET                                   @L7A 04150000
&N       SETA  4                             NEXT CHARACTER        @L7A 04200000
.CKLOOP1 ANOP                                                           04250000
         AIF   ('&CLASS'(&N,1) EQ '@').CLASSET                     @L7A 04300000
         AIF   ('&CLASS'(&N,1) EQ '#').CLASSET                     @L7A 04350000
         AIF   ('&CLASS'(&N,1) EQ '$').CLASSET                     @L7A 04400000
         AIF   ('&CLASS'(&N,1) GE '0' AND                              *04450000
               '&CLASS'(&N,1) LE '9').CLASSET                  @YA04384 04500000
&N       SETA  &N-1                          NEXT CHARACTER        @L7A 04550000
         AIF   (&N GT 0).CKLOOP1                                   @L7A 04600000
.NATCHAR ANOP                                                           04650000
         AGO   .WRNCHAR                                            @L7A 04700000
.CLASSET ANOP                                                           04750000
         AIF   ('&ACTION' EQ '').ERR03                             @L7A 04800000
.***************************************************                    04850000
.*   VALIDATE ACTION PARAMETER AND SET CORRESPONDING                    04900000
.*   VALUE FOR THE TABLE ENTRY.                                         04950000
.***************************************************                    05000000
         AIF   ('&ACTION' NE 'NONE').TAB0010                            05050000
&ACTCODE SETA  0                                                        05100000
         AGO   .CONT00                                                  05150000
.TAB0010 ANOP                                                           05200000
         AIF   ('&ACTION' NE 'RACF').ERR04                              05250000
&ACTCODE SETA  1                                                        05300000
.CONT00  ANOP                                                           05350000
.****************************************************                   05400000
.*   SET DEFAULT REQUESTOR NAME IF NO VALUE SPECIFIED                   05450000
.****************************************************                   05500000
         AIF   ('&REQSTOR' NE '').CONT01                                05550000
&RQSTR   SETC  '        '            DEFAULT VALUE IS BLANK             05600000
         AGO   .CONT02                                                  05650000
.CONT01  ANOP                                                           05700000
&RQSTR   SETC  '&REQSTOR'            COPIES SPECIFIED VALUE             05750000
.CONT02  ANOP                                                           05800000
.***************************************************                    05850000
.*  SET DEFAULT SUBSYSTEM NAME IF NO VALUE SPECIFIED                    05900000
.***************************************************                    05950000
         AIF   ('&SUBSYS' NE '').CONT03                                 06000000
&SUB     SETC  '        '           DEFAULT VALUE IS BLANK              06050000
         AGO   .CONT04                                                  06100000
.CONT03  ANOP                                                           06150000
&SUB     SETC  '&SUBSYS'            COPYS SPECIFIED VALUE               06200000
.CONT04  ANOP                                                           06250000
.**********************************************                         06300000
.*  FORCE ASSEMBLY ERROR IF TYPE=END IS MISSING                         06350000
.**********************************************                         06400000
         AIF   (&X).CONT05          THIS IS NOT THE FIRST ENTRY         06450000
&X       SETB  1                    SET TO ONE WHEN FIRST ENTRY         06500000
.*                                  BUILT.  THE CONSTANT REFERS TO      06550000
.*                                  A LABEL ON THE FINAL TABLE ENTRY.   06600000
         DC    0A(IHB0001)          IF FLAGGED, TYPE=END IS MISSING     06650000
.CONT05  ANOP                                                           06700000
.***********************************************************            06750000
.*  GENERATE THE TABLE ENTRY WITH THE PARAMETER VALUES IN IT            06800000
.***********************************************************            06850000
&NAME    DC    CL8'&RQSTR'          REQUESTOR NAME                      06900000
         DC    CL8'&CLASS'          RESOURCE CLASS NAME                 06950000
         DC    CL8'&SUB'            SUBSYSTEM NAME                      07000000
         DC    AL2(&ACTCODE)        ACTION CODE VALUE                   07050000
         DS    CL4                  RESERVED                            07100000
         MEXIT                      EXIT FROM THE MACRO                 07150000
.***********************************************************            07200000
.*  GENERATE THE BLANK TABLE ENTRY THAT MARKS THE END OF THE            07250000
.*  TABLE.                                                              07300000
.***********************************************************            07350000
.ENDMAC  ANOP                                                           07400000
IHB0001  DC    CL24' '              END-OF-TABLE MARKER                 07450000
         MEXIT                      EXIT FROM THE MACRO                 07500000
.*****************                                                      07550000
.*  ERROR MESSAGES                                                      07600000
.*****************                                                      07650000
.ERR01   ANOP                                                           07700000
         MNOTE 12,'*** CLASS AND TYPE PARAMETERS MISSING'               07750000
         MEXIT                                                          07800000
.ERR02   ANOP                                                           07850000
         MNOTE 12,'*** CLASS PARAMETER MISSING'                         07900000
         MEXIT                                                          07950000
.ERR03   ANOP                                                           08000000
         MNOTE 12,'*** ACTION PARAMETER MISSING'                        08050000
         MEXIT                                                          08100000
.ERR04   ANOP                                                           08150000
         MNOTE 12,'*** INVALID VALUE FOR ACTION PARAMETER'              08200000
         MEXIT                                                          08250000
.ERR05   ANOP                                                           08300000
         MNOTE 12,'*** INVALID VALUE FOR TYPE PARAMETER'                08350000
         MEXIT                                                     @L7A 08400000
.BADCLAS ANOP                                                           08450000
         MNOTE 12,'*** INVALID CLASS NAME LENGTH ***'              @L7A 08500000
         MEXIT                                                     @L7A 08550000
.WRNCHAR ANOP                                                           08600000
         MNOTE 4,'*** CLASS NAME DOES NOT CONTAIN NATIONAL CHARACTER   *08650000
               OR DIGIT IN FIRST FOUR POSITIONS ***'               @L7A 08700000
         AGO   .CLASSET                                            @L7A 08750000
         MEND                                                           08800000
