         MACRO                                                          00050000
         EDGMSGB &MN,&MSG,&TYPE=I,&MSGID=YES,&MOD=NO,                  X00100000
               &ROUTCDE=11,&DESC=7,&CC=C' ',&LIST=NO                003 00150000
         AIF   ('&LIST' NE 'YES').NOLIST                                00200000
         EJECT                                                          00250000
*********************************************************************** 00300000
*                                                                     * 00350000
*       MESSAGE TABLE BUILD MACRO                                     * 00400000
*                                                                     * 00450000
*********************************************************************** 00500000
* OS/390 DFSMSrmm V2R10                                               * 00540000
*                                                                     * 00580000
* PROPRIETARY V3 STATEMENT                                            * 00620000
* LICENSED MATERIALS - PROPERTY OF IBM                                * 00660000
* "RESTRICTED MATERIALS OF IBM"                                       * 00700000
* 5647-A01                                                            * 00740000
* (C) COPYRIGHT 1993,2000  IBM CORP.                                  * 00780000
* STATUS = HDZ11F0                                                    * 00820000
* END PROPRIETARY V3 STATEMENT                                        * 00860000
*********************************************************************** 00900000
*                                                                     * 00950000
*   FUNCTION: BUILD CONTENTS OF MODULE EDGMTAB                        * 01000000
*                                                                     * 01050000
*   PARAMETERS:                                                       * 01100000
*   REQUIRED:                                                         * 01150000
*    MN       CHARACTER STRING                                        * 01200000
*                 NUMBER OF MESSAGE TO BE CREATED                     * 01250000
*    MSG      CHARACTER STRING                                        * 01300000
*                 TEXT OF MESSAGE                                     * 01350000
*                                                                     * 01400000
*   OPTIONAL:                                                         * 01450000
*    TYPE     SINGLE CHARACTER, I, E, OR W                            * 01500000
*                 MESSAGE TYPE                                        * 01550000
*    MSGID    CHARACTERS YES OR NO                                    * 01600000
*                 INDICATES WHETHER MEGID IS TO BE CREATED            * 01650000
*    MOD      CHARACTERS YES OR NO                                    * 01700000
*                 INDICATES WHETHER MODIFICATION CHARACTERS EXIST     * 01750000
*    ROUTCDE  AS PER SAME KEYWORD IN MVS WTO MACRO                    * 01800000
*                 ROUTING DEFINITION                                  * 01850000
*    DESC     AS PER SAME KEYWORD IN MVS WTO MACRO                    * 01900000
*                 MESSAGE DESCRIPTOR                                  * 01950000
*    CC       A SDINGLE CHARACTER OR AN X'..' OR C1.' STRING          * 02000000
*                 A CHANNEL CONTROL CHARACTER FOR DCB OUTPUT          * 02050000
*    LIST     CHARACTERS YES OR NO                                    * 02100000
*                 INDICATES WHETHER THESE COMMENT ARE TO BE LISTED    * 02150000
*                                                                     * 02200000
*                                                                     * 02250000
*   METHOD OF ACCESS:                                                 * 02300000
*              ASSEMBLER - SPECIFYING EDGMSGB                         * 02350000
*                                                                     * 02400000
*   LISTING  : ASSEMBLER - SPECIFY LIST=YES ON THE MACRO              * 02450000
*                                                                     * 02500000
*********************************************************************** 02550000
         EJECT                                                          02600000
*********************************************************************** 02650000
*                                                                     * 02700000
*   CHANGE ACTIVITY                                                   * 02750000
*       18/FEB/91  001/177045      ERROR IN MNOTE                   001 02800000
*       18/FEB/91  002/177046      COMPONENT ID CONVERSION ERROR    002 02850000
*       13/JUN/91  001/177338      SUPPORT CHANNEL CONTROL CHARS.   003 02900000
**      09/09/91 DSP   PRODUCT NAME CHANGE                         @RMM 02950000
*          ,AS,010592, REMOVE PRODUCT NO                          @004* 03000000
*          ,MW,110992, CORRECT COPYRIGHT                          @005* 03050000
*          ,MW,070193, ADD SYSPARM TO GET PTF NUMBER IN EYECATCHR @006* 03100000
*   270912 ,DP,090393, ASSEMBLY ERROR WHEN NULL SYSPARM USED    @007  * 03150000
* $08=RMM120 ,120,250893,MJA: ALLOW MULTI-SEGMENT MESSAGE        @008 * 03166600
* $09=RMM310 ,310,990422,GW : Change copyright                   @09A * 03183200
* $K1=K160588,210,991020,BDG: Change OS/390 Product ID           @K1A * 03191600
*                                                                     * 03200000
*********************************************************************** 03250000
.NOLIST  ANOP                                                           03300000
.*                                                                      03350000
         GBLB  &NOT1ST,&INDEX(32768)                                    03400000
         GBLA  &FLAGS(32768)                                            03450000
         GBLC  &CCCHARS(32768)                                      003 03500000
         GBLC  &RMMVER,&RMMFMID,&OS390ID                           @K1C 03550000
         LCLA  &MINDX,&CMPNO,&REM                                       03600000
         LCLA  &I,&I1,&J,&K,&N,&RCLEN                                   03650000
         LCLB  &RB(16),&DB(16),&LOW,&HIGH                               03700000
         LCLC  &MID,&CMPIDS,&CMPID,&DBITS,&RBITS                        03750000
         LCLA  &MSGCT,&MSGIN                                       @008 03775000
.*                                                                      03800000
         AIF  ('&MN' NE '').NONULMN                                     03850000
         AIF   ('&MSG' EQ '').NULMSG                                    03900000
         MNOTE 8,'MESSAGE PARAMETER MAY ONLY BE SUPPLIED IF MSGNUM PARAM03950000
               METER ALSO SUPPLIED.'                                    04000000
         MEXIT                                                          04050000
.NULMSG  AIF   (&NOT1ST).GENINDX                                        04100000
         MNOTE 12,'NO MESSAGES SUPPLIED FOR MESSAGE TABLE'              04150000
         MEXIT                                                          04200000
.GENINDX ANOP                                                           04250000
EDGMTAB  CSECT ,                  RESTART INDEX CSECT                   04300000
&MINDX   SETA  1                                                        04350000
         ACTR  66000                                                    04400000
.GINDL1  AIF   (NOT(&INDEX(&MINDX))).GINDL2                             04450000
         DC    Y(&MINDX),AL1(&FLAGS(&MINDX),&CCCHARS(&MINDX))       003 04500000
         DC    A(MSG&MINDX)                                         003 04550000
.GINDL2  ANOP                                                           04600000
&MINDX   SETA  &MINDX+1                                                 04650000
         AIF   (&MINDX LT 32768).GINDL1                                 04700000
INDXLAST EQU  *-8                 ADDRESS LAST INDEX                    04750000
         MEXIT                                                          04800000
.NONULMN AIF   ('&MSG' NE '').GNMG1                                     04850000
         MNOTE 8,'MESSAGE PARAMETER MUST BE SUPPLIED IF MSGNUM PARAMETER04900000
               METER SUPPLIED.'                                         04950000
         MEXIT                                                          05000000
.GNMG1   ANOP                                                           05050000
&MINDX   SETA  &MN                                                      05100000
         AIF   (&MINDX LE 32767).GNMG2                                  05150000
         MNOTE 8,'MESSAGE NUMBER MUST NOT EXCEED 32767'                 05200000
         MEXIT                                                          05250000
.GNMG2   AIF   (&MINDX GE 1).GNMG3                                      05300000
         MNOTE 8,'MESSAGE NUMBER MUST NOT BE LESS THAN 1'           001 05350000
         MEXIT                                                          05400000
.GNMG3   AIF   (NOT(&INDEX(&MINDX))).GNMG4                              05450000
         MNOTE 8,'MESSAGE NUMBER &MINDX ALREADY SPECIFIED'              05500000
         MEXIT                                                          05550000
.GNMG4   ANOP                                                           05600000
         AIF  (&NOT1ST).NOTFIRST                                        05650000
         EDGSETV ,                 GET VERSION & FMID                   05700000
EDGMTAB  CSECT ,                   CSECT FOR MESSAGE INDICIES           05750000
EDGMTAB  AMODE 31                  PLACE MODULE IN                      05800000
EDGMTAB  RMODE ANY                 EXTENDED STORE                       05850000
EDGMTBD  RMODE ANY                                                      05900000
         SPACE 2                                                        05950000
         DC    A(INDX1ST,INDXLAST)    POINT AT INDEX LIMITS             06000000
         SPACE 2                                                        06050000
         DC    AL1(CPR&SYSNDX-1-*) LENGTH OF ID                    @RMM 06100000
         DC    CL9'EDGMTAB'       MODULE NAME                           06150000
         DC    CL8'&SYSDATE'      ASSEMBLY DATE                         06200000
         DC    CL10'&RMMVER'      VERSION AND RELEASE                   06250000
         DC    CL8'&RMMFMID'      FUNCTION ID                      @RMM 06300000
         DC    CL8'&SYSPARM '     MODIFICATION/PTF LEVEL           @007 06350000
CPR&SYSNDX DS  0C                                                  @RMM 06400000
         DC    C'ALL RIGHTS RESERVED                         '     @005 06450000
         DC    C'&OS390ID (C) COPYRIGHT IBM CORP. 1979, 2000 '     @K1C 06500000
         DC    C'LICENSED MATERIAL - PROGRAM PROPERTY OF IBM '     @005 06550000
         DC    C'US Government Users Restricted Rights -     '     @005 06600000
         DC    C'Use duplication or disclosure restricted by '     @005 06650000
         DC    C'GSA ADP Schedule Contract with IBM Corp. '        @005 06700000
         SPACE 2                                                        06750000
INDX1ST  DS    0D                  START OF INDEX TABLE                 06800000
EDGMTBD  CSECT ,                   START MSG DATA CSECT                 06850000
&NOT1ST   SETB 1                   SHOW CSECTS DEFINED                  06900000
.NOTFIRST ANOP                                                          06950000
.*                                                                      07000000
MSG&MINDX DC   Y(MGL&MINDX),X'8000'                                     07050000
&MSGCT    SETA N'&MSG                                              @008 07066600
&MSGIN    SETA 1                                                   @008 07083200
         AIF   ('&MSGID' EQ 'NO').GNMG7                                 07100000
&CMPNO   SETA  &MINDX/1000                                              07150000
&CMPIDS  SETC  '0123456789ABCDEFGHIJKLMNOPQRSTUVW'                  002 07200000
&CMPID   SETC  '&CMPIDS'(&CMPNO+1,1)                                    07250000
&REM     SETA  &MINDX-(1000*&CMPNO)                                     07300000
&MID     SETC  '&REM'                                                   07350000
.GNMG5   AIF   (K'&MID EQ 3).GNMG6                                      07400000
&MID     SETC  '0'.'&MID'                                               07450000
         AGO   .GNMG5                                                   07500000
.GNMG6   ANOP                                                           07550000
&MID     SETC  'EDG'.'&CMPID'.'&MID'.'&TYPE'                            07600000
&FLAGS(&MINDX) SETA 64              SET FLAG FOR MSGID SUPPLIED         07650000
          DC   CL9'&MID'                                                07700000
.GNMG7    AIF  ('&MSG(&MSGIN)'(1,1) EQ '''').GNMG8                 @008 07750000
          DC   &MSG(&MSGIN)                                        @008 07800000
          AGO  .GNMG9                                                   07850000
.GNMG8    ANOP                                                          07900000
          DC   C&MSG(&MSGIN)                                       @008 07950000
.GNMG9    ANOP                                                          08000000
&MSGIN    SETA &MSGIN+1                                            @008 08016600
          AIF  (&MSGIN LE &MSGCT).GNMG7                            @008 08033200
MGL&MINDX EQU  *-MSG&MINDX                                              08050000
.*                                                                      08100000
.*                                                                      08150000
.********************************************************************** 08200000
.*                                                                    * 08250000
.*  THIS SECTION OF CODE, DOWN TO LABEL .SETNCLR, WHICH DETERMINES    * 08300000
.*  THE PROCESSING OF ROUTCDE AND DESC IS BASED ON THE  WTO MACRO.    * 08350000
.*                                                                    * 08400000
.********************************************************************** 08450000
.*                                                                      08500000
.*                                                                      08550000
         AIF   (T'&ROUTCDE EQ 'O').ENDROUT     ROUTING CODES?           08600000
.*                                                                      08650000
.*    ROUTING CODES HAVE BEEN SPECIFIED. DETERMINE WHICH CODES     @L2C 08700000
.*    ARE TO BE USED AND SET THE APPROPRIATE BITS IN STRING 'RB'.  @L2A 08750000
.*    ROUTING CODE RANGES ARE ACCEPTED AS LONG AS:                 @L2A 08800000
.*      - THE SECOND NUMBER OF THE RANGE  >=  THE FIRST NUMBER     @L2A 08850000
.*      - BOTH NUMBERS ARE WITHIN THE RANGE 1-128                  @L2A 08900000
.*      - THE NUMBERS OF THE RANGE ARE SEPARATED BY A DASH (-)     @L2A 08950000
.*                                                                      09000000
&I       SETA  1                    INITIALIZE LIST INDEX          @L2A 09050000
.DOROUT  ANOP                                                           09100000
&I1      SETA  0                    INITIALIZE ELEMENT INDEX       @L2A 09150000
&RCLEN   SETA  K'&ROUTCDE(&I)       SET LENGTH OF CURRENT ELEMENT  @L2A 09200000
         AIF   (&RCLEN EQ 0).SINGLE IF ELEMENT IS NULL, GO TO SINGLE   X09250000
                                    ROUTING CODE CHECK - IT WILL BE    X09300000
                                    DETERMINED TO ISSUE AN MNOTE   @L2A 09350000
         AIF   (T'&ROUTCDE(&I) EQ 'N').SINGLE IF SINGLE ELEMENT    @L2A 09400000
.*                                                                 @L2A 09450000
.*    A ROUTING CODE RANGE HAS BEEN SPECIFIED.                     @L2A 09500000
.*                                                                 @L2A 09550000
.CHKDASH ANOP                       NOT SINGLE CODE. CHECK RANGE   @L2A 09600000
&I1      SETA  &I1+1                INCREMENT ELEMENT INDEX        @L2A 09650000
         AIF   ('&ROUTCDE(&I)'(&I1,1) EQ '-').GOTDASH              @L2A 09700000
         AIF   (&I1+1 GE &RCLEN).BADRNGE  CHECK INDEX POSITION     @L2A 09750000
         AGO   .CHKDASH             CHECK NEXT CHARACTER           @L2A 09800000
.GOTDASH ANOP                       FOUND A DASH (DELIMITER)       @L2A 09850000
         AIF   (&I1 EQ 1).BADRNGE   IF NO LOWER LIMIT, ISSUE MNOTE @L2A 09900000
&LOW     SETC  '&ROUTCDE(&I)'(1,&I1-1) SET FIRST NUMBER OF RANGE   @L2A 09950000
&HIGH    SETC  '&ROUTCDE(&I)'(&I1+1,&RCLEN) SECOND NUMBER OF RANGE @L2A 10000000
         AIF   ('&LOW' GT '&HIGH').BADRNGE IF FIRST NUMBER GREATER THANX10050000
                                    SECOND NUMBER, ISSUE MNOTE     @L2A 10100000
         AIF   ('&LOW' LT '1' OR '&LOW' GT '16' OR                     X10150000
               '&HIGH' LT '1' OR '&HIGH' GT '16').BADRNGE IF EITHER    X10200000
                                    NUMBER IS NOT WITHIN 1-16      @L2A 10250000
&J       SETA  &LOW                 INITIALIZE INDEX               @L2A 10300000
&K       SETA  &HIGH                SET END OF RANGE               @L2A 10350000
.SETLOOP ANOP                       LOOP TO SET ON ROUTING CODES       X10400000
                                    (RESERVED CODES ALSO SET HERE, BUT X10450000
                                    CLEARED BELOW)                 @L2A 10500000
&RB(&J)  SETB  1                    ROUTING CODE N SPECIFIED       @L2A 10550000
&J       SETA  &J+1                 INCREMENT LOOP INDEX           @L2A 10600000
         AIF   (&J LE &K).SETLOOP   IF MORE CODES TO SET           @L2A 10650000
         AGO   .NEXTRC              DETERMINE NEXT ROUTING CODE    @L2A 10700000
.*                                                                 @L2A 10750000
.*    AN INVALID ROUTING CODE RANGE HAS BEEN SPECIFIED. ISSUE AN   @L2A 10800000
.*    MNOTE AND SKIP TO THE NEXT ROUTING CODE.                     @L2A 10850000
.*                                                                 @L2A 10900000
.BADRNGE ANOP                       INVALID ROUTING CODE RANGE     @L2A 10950000
         MNOTE 4,'INVALID RANGE OF ROUTING CODES IGNORED.'         @L2A 11000000
         AGO   .NEXTRC              CHECK NEXT ROUTING CODE        @L2A 11050000
.*                                                                 @L2A 11100000
.*    A SINGLE ROUTING CODE HAS BEEN SPECIFIED. VERIFY THAT IT IS  @L2A 11150000
.*    WITHIN THE PROPER RANGE AND, IF SO, SET THE CORRECT BIT      @L2A 11200000
.*                                                                 @L2A 11250000
.SINGLE  ANOP                                                      @L2A 11300000
&N       SETA  &ROUTCDE(&I)         GET NEXT ROUTING CODE          @L2C 11350000
         AIF   (&N  GE 1 AND &N  LE 16).SETSNGL VALID CODE?        @L2C 11400000
         MNOTE 4,'ROUTCDE &N  IS AN INVALID ROUTING CODE. IT IS IGNOREDX11450000
               .'                                                  @L2C 11500000
         AGO   .NEXTRC              INVALID ROUTING CODE SPECIFIED.    X11550000
                                    CHECK THE NEXT ONE             @L2A 11600000
.SETSNGL ANOP                       VALID ROUTING CODE SPECIFIED        11650000
&RB(&N)  SETB  1                    ROUTING CODE N SPECIFIED            11700000
.NEXTRC  ANOP                       DETERMINE IF MORE CODES        @L2C 11750000
&I       SETA  &I+1                 INCREMENT INDEX                     11800000
         AIF   (&I LE N'&ROUTCDE).DOROUT IF MORE CODES, CHECK NEXT ONE  11850000
.ENDROUT ANOP                       DONE WITH ROUTING CODES        @L2C 11900000
.*                                                                 @L2A 11950000
.*    SET UP ROUTING CODE DC FIELD FOR AN OLD-FORMAT WPL           @L2A 12000000
.*                                                                 @L2A 12050000
&RBITS    SETC '&RB(01)&RB(02)&RB(03)&RB(04)&RB(05)&RB(06)'             12100000
&RBITS    SETC '&RBITS'.'&RB(07)&RB(08)&RB(09)&RB(10)&RB(11)'           12150000
&RBITS    SETC '&RBITS'.'&RB(12)&RB(13)&RB(14)&RB(15)&RB(16)'           12200000
.********************************************************************** 12250000
.*  START OF DESCRIPTOR CODES PROCESSING                                12300000
.********************************************************************** 12350000
         AIF   (T'&DESC EQ 'O').SETNCLR     IF NO DESCRIPTOR CODES     X12400000
                        SPECIFIED, THEN SKIP DESCRIPTOR CODE SECTION    12450000
.********************************************************************** 12500000
.*  DETERMINE WHICH DESCRIPTOR CODES WERE SPECIFIED                     12550000
.********************************************************************** 12600000
&I       SETA  1                    INIT LOOP CONTROL                   12650000
.DCHK    ANOP                       DESCRIPTOR CODES SPECIFIED          12700000
&N       SETA  &DESC(&I)            GET THE NEXT DESCRIPTOR CODE        12750000
&I       SETA  &I+1                 INCREMENT THE INDEX                 12800000
         AIF   (&N GE 1 AND &N LE 16).ASSIGND VALID DESCRIPTOR CODE?    12850000
         MNOTE 4,'DESC &N  IS AN INVALID DESCRIPTOR CODE. IT IS IGNOREDX12900000
               .'                                                  @L2C 12950000
         AGO   .NXTD                INVALID DESCRIPTOR CODE. ISSUE     X13000000
                                    MNOTE AND CHECK FOR NEXT CODE       13050000
.ASSIGND ANOP                       YES. VALID DESCRIPTOR CODE          13100000
&DB(&N)  SETB  1                   VALID DESCRIPTOR CODE N SPECIFIED    13150000
.NXTD    AIF   (&I LE N'&DESC).DCHK IF MORE CODES, CHECK NEXT ONE       13200000
.********************************************************************** 13250000
.*  SET OUTPUT AREAS AND CLEAR BINARY FLAGS                             13300000
.********************************************************************** 13350000
.SETNCLR ANOP                                                           13400000
&DBITS    SETC '&DB(01)&DB(02)&DB(03)&DB(04)&DB(05)&DB(06)'             13450000
&DBITS    SETC '&DBITS'.'&DB(07)&DB(08)&DB(09)&DB(10)&DB(11)'           13500000
&DBITS    SETC '&DBITS'.'&DB(12)&DB(13)&DB(14)&DB(15)&DB(16)'           13550000
.*                                                                      13600000
.*                                                                      13650000
.********************************************************************** 13700000
.*                                                                    * 13750000
.*                END OF CODE BASED ON WTO MACRO                      * 13800000
.*                                                                    * 13850000
.********************************************************************** 13900000
.*                                                                      13950000
.*                                                                      14000000
          DC   B'&DBITS'                    GENERATE DESCRIPTOR CODES   14050000
          DC   B'&RBITS'                    GENERATE ROUTE CODES        14100000
          AIF  ('&MOD' NE 'YES').GNMG10                                 14150000
&FLAGS(&MINDX) SETA &FLAGS(&MINDX)+128  SET FLAGS FOR MODS REQUIRED     14200000
.GNMG10   AIF  (K'&CC NE  1).GNMG11                                 003 14250000
&CCCHARS(&MINDX) SETC  'C'''.'&CC'.''''                             003 14300000
          AGO  .GNMG12                                              003 14350000
.GNMG11   ANOP ,                                                    003 14400000
&CCCHARS(&MINDX) SETC  '&CC'                                        003 14450000
.GNMG12   ANOP ,                                                    003 14500000
EDGMSGT   DSECT ,                   DSECT FOR INSTR. TO TEST LENGTH     14550000
          LA    0,4095-255+MGL&MINDX   THIS INSTRUCTION WILL CAUSE AN  X14600000
                                  ERROR IF THE MSG STRING IS TOO LONG   14650000
EDGMTBD   CSECT ,                 RESTART DATA CSECT                    14700000
&INDEX(&MINDX) SETB 1                   SHOW MSG DEDFINED               14750000
          MEND                                                          14800000
