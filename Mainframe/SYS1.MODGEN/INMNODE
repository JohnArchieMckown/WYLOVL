* /***START OF SPECIFICATIONS*****************************************/
* / **PROPRIETARY_STATEMENT*******************************************/
* /*01* PROPRIETARY STATEMENT=                                       */
* /*                                                                 */
* /*                                                                 */
* /* LICENSED MATERIALS - PROPERTY OF IBM                            */
* /* THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                     */
* /* 5685-025 (C) COPYRIGHT IBM CORP. 1981, 1989                     */
* /* SEE COPYRIGHT INSTRUCTIONS                                      */
* /*                                                                 */
* /* STATUS= JTE21X2                                                 */
* /*                                                                 */
* /***END_OF_PROPRIETARY_STATEMENT************************************/
*
* /*                                                                 */
* /*01*  MODULE-NAME: INMXPARM                                       */
* /*                                                                 */
* /*01*  MACRO NAME: INMNODE                                         */
* /*                                                                 */
* /*01*  DESCRIPTIVE-NAME: Node name entry macro                     */
* /*                                                                 */
* /*                                                                 */
* /*01*  FUNCTION:                                                   */
* /*                Build node/SMFid entries in the INMXPARM CSECT   */
* /*                to be used in determining which node TRANSMIT    */
* /*                or RECEIVE is running on.                        */
* /*                                                                 */
* /*01*  OPERATION:                                                  */
* /*                1) The INMNODE macro should be repeated once for */
* /*                   each node/SMFid combination on which the      */
* /*                   TRANSMIT or RECEIVE command will be run.      */
* /*                2) Each invocation of INMNODE verifies the       */
* /*                   existence and correct length of the two       */
* /*                   positional parameters and then builds a       */
* /*                   16-byte entry for the node name.              */
* /*                                                                 */
* /*                                                                 */
* /*01*  NOTES:                                                      */
* /*                The INMNODE macro is always used in conjunction  */
* /*                with the INMXP macro and the INMEND macro to     */
* /*                build the INMXPARM CSECT for specifying local    */
* /*                options to the TRANSMIT and RECEIVE commands.    */
* /*                                                                 */
* /*02*    DEPENDENCIES:                                             */
* /*                None.                                            */
* /*                                                                 */
* /*02*    CHARACTER-CODE-DEPENDENCIES: None                         */
* /*                                                                 */
* /*02*    RESTRICTIONS: None                                        */
* /*                                                                 */
* /*01*  MODULE-TYPE: MACRO                                          */
* /*                                                                 */
* /*02*    PROCESSOR: Assembler                                      */
* /*                                                                 */
* /*                                                                 */
* /*01*  EXTERNAL-REFERENCES: None.                                  */
* /*                                                                 */
* /*02*    ROUTINES: None.                                           */
* /*                                                                 */
* /*                                                                 */
* /*02*    CONTROL-BLOCKS: None.                                     */
* /*                                                                 */
* /*01*  CHANGE-ACTIVITY:                                            */
* /*@E1114KJ Interactive Data Transmission Facility          @E1114KJ*/
* /*@ZMT1044 Add STATUS information                          @ZMT1044*/
* /*                                                                 */
* /*01*  MESSAGES:                                                   */
* /*            INMNODE MACRO NOT PRECEDED BY INMXP OR INMNODE       */
* /*            FIRST PARAMETER (NODE NAME) REQUIRED BUT NOT PRESENT */
* /*            FIRST PARAMETER (NODE NAME) WAS TOO LONG             */
* /*            SECOND PARAMETER (SMF ID) REQUIRED BUT NOT PRESENT   */
* /*            SECOND PARAMETER (SMF ID) WAS TOO LONG               */
* /*                                                                 */
* /*01*  ABEND-CODES: None.                                          */
* /*                                                                 */
* /***END OF SPECIFICATIONS*******************************************/
* /*A00000000-99999999                                       @E1114KJ*/
         MACRO
&NAME    INMNODE &NODE,&SMFID
         GBLC  &INMXP1
         AIF   ('&INMXP1' EQ 'INMXP').INMXPOK
         AIF   ('&INMXP1' EQ 'INMNODE').INMXPOK
         MNOTE 12,'INMNODE MACRO NOT PRECEDED BY INMXP OR INMNODE'
.INMXPOK ANOP
&INMXP1  SETC  'INMNODE'
         SPACE 1
         AIF   ('&NODE' EQ '').NODERR1   IF NODE NAME MISSING, ERROR
         AIF   ('&NODE'(1,1) EQ '''').NODEQ  NODE NAME QUOTED
         AIF   (K'&NODE GT 8).NODERR2 IF NODE NAME TOO LONG, ERROR
         DC    CL8'&NODE.'         NODE NAME
         AGO   .NODEEND
.NODEQ   AIF   (K'&NODE GT 10).NODERR2 IF NODE NAME TOO LONG, ERROR
         DC    CL8&NODE.           NODE NAME
         AGO   .NODEEND
.NODERR1 MNOTE 8,'FIRST PARAMETER (NODE NAME) REQUIRED BUT NOT PRESENT'
         AGO   .NODEEND
.NODERR2 MNOTE 8,'FIRST PARAMETER (NODE NAME) WAS TOO LONG'
.NODEEND ANOP
         AIF   ('&SMFID' EQ '').SMFERR1  IF SMFID MISSING, ERROR
         AIF   ('&SMFID'(1,1) EQ '''').SMFQ  SMF ID QUOTED ?
         AIF   (K'&SMFID GT 4).SMFERR2   IF SMFID NAME LONG, ERROR
         DC    CL4'&SMFID.'        SMF ID
         AGO   .SMFEND
.SMFQ    AIF   (K'&SMFID GT 6).SMFERR2   IF SMFID NAME LONG, ERROR
         DC    CL4&SMFID.          SMF ID
         AGO   .SMFEND
.SMFERR1 MNOTE 8,'SECOND PARAMETER (SMF ID) REQUIRED BUT NOT PRESENT'
         AGO   .SMFEND
.SMFERR2 MNOTE 8,'SECOND PARAMETER (SMF ID) WAS TOO LONG'
.SMFEND  ANOP
         DC    XL4'00'             RESERVED
         MEXIT
         MEND
