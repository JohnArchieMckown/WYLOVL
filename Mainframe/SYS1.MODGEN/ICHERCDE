*/* START OF SPECIFICATIONS ******************************************/ 00160000
*/*                                                                  */ 00170000
*/* $MAC(ICHERCDE) COMP(XXH00) PROD(RACF):                           */ 00200000
*/*                                                                  */ 00250000
*/* DESCRIPTIVE NAME = RACF Class Descriptor Generator               */ 00300000
*/*                                                                  */ 00350000
* *01* PROPRIETARY STATEMENT=                                         * 00371800
*  **PROPRIETARY_STATEMENT********************************************* 00393600
*                                                                     * 00415400
*                                                                     * 00437200
*   LICENSED MATERIALS - PROPERTY OF IBM                              * 00459000
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                       * 00480800
*   5647-A01 (C) COPYRIGHT IBM CORP. 1987, 1998                       * 00502600
*                                                                     * 00524400
*   STATUS= HRF2260                                                   * 00546200
*                                                                     * 00568000
*  **END_OF_PROPRIETARY_STATEMENT************************************** 00589800
*/*                                                                  */ 00611600
*/*01* EXTERNAL CLASSIFICATION:                                         00633400
*/*02* GUPI:                                                            00655200
*/*01* END OF EXTERNAL CLASSIFICATION;                                  00677000
*/*                                                                  */ 00700000
*/* FUNCTIONAL DESCRIPTION = This macro builds Class Descriptor      */ 00750000
*/*                          Table entries which describe the        */ 00783300
*/*                          General Resources classes to RACF.      */ 00816600
*/*                                                                  */ 00850000
*/* MACROS = None                                                    */ 00900000
*/*                                                                  */ 00950000
*/* CHANGE ACTIVITY:                                                 */ 01000000
*/*   $L0=HRF1702,RACF/170,841030,CNTDSD                         @L0A*/ 01025000
*/*   OZ84505 Added ACTR instruction                         @ZA84505*/ 01050000
*/*   OY04384 Allow 0 in first 4 characters of class name    @YA04384*/ 01125000
*/*   $L1=HRF1802,RACF/180,860716,CNTDSD:                        @L1A*/ 01150000
*/*   $P1=HRF1802,RACF/180,870515,CNTDSD: PTM PEQ0043            @P1A*/ 01175000
*/*   $L2=MVSB1   HRF1902  880330 PDGTM1: MVS B1 Support         @L2A*/ 01180000
*/*   $L3=MVSB1   HRF1902  880615 PDLCL1: MVS B1 Support         @L3A*/ 01185000
*/*   $P2=PGU0289 HRF1902  881128 PDGTM1: PTM PGU0289            @P2A*/ 01190000
*/*   $L4=VMSFS   HRF1902  881221 PDGTM1: SFS Support            @L4A*/ 01195000
*/*   $P3=PGU0573 HRF1902  890412 PDGTM1: PTM PGU0573            @P3A*/ 01196600
*/*   $D1=DGU0088 HRF1902  890412 PDGTM1: DCRR #0088             @D1A*/ 01198200
*/*   $L5=APPC    JRF1922  891015 PDJAD1: APPC SUPPORT           @L5A*/ 01198800
*/*   $L6=CDTEXP  JRF1922  901207 PDJAS1: CDT EXPANSION          @L6A*/ 01199400
*/*   $P4=CDTEXP  JRF1922  910402 PDJAS1: PTM PIM0136            @P4A*/ 01199500
*/*   $P5=CDTEXP  JRF1922  910402 PDJAS1: PTM PIM0137            @P5A*/ 01199600
*/*   $P6=CDTEXP  JRF1922  910402 PDJAS1: PTM PIM0138            @P6A*/ 01199700
*/*   $OY57303    JRF1922  920902 PDRNH1: PTM PIM0409 TRSQ       @01C*/ 01199800
*/*   $L7=POSIT   HRF2220  940729 PDAHW1: Support for 2.2        @L7A*/ 01199900
*/*   $P7=PT50026 HRF2240  970131 PDR_B1: PTM PT50026            @P7A*/ 01200800
*/*   $L8=NQN     HRF2260  970225 PDR_B1: Network qual. names    @L8A*/ 01201200
*/*   $L9=OW46859 HRF7705  000921 PDBRW1: EJB Roles              @L9A*/ 01201400
*/*   $02=OW49576 HRF7703  010705 PDCGK1: APAR OW49576           @02A*/ 01201500
*/*                                                                  */ 01201700
*/*                                                                  */ 01202800
*/* CHANGE DESCRIPTIONS:                                             */ 01205600
*/*   A00000000-99999999                                         @L0A*/ 01207000
*/*   A - RACLIST/GENLIST Support keywords                       @L1A*/ 01208400
*/*   A - MVS B1 support macro keywords                          @L2A*/ 01209800
*/*   A - Increase length for Data Space STOKEN                  @L3A*/ 01211200
*/*   A - Shared File System (SFS) Long Name Support             @L4A*/ 01212600
*/*   C - Changed the parameter USESLBL to SLBLREQ               @P3A*/ 01213000
*/*   C - Changes per DCRR # 0088                                @D1A*/ 01213400
*/*   C - Check for unique class name                            @L5A*/ 01213600
*/*   C - Extend CDT entries from 256 to 1024--allowing 896      @L6A*/ 01213700
*/*       user defined + 128 IBM defined classes                 @L6A*/ 01213800
*/*   C - Put space between words 'defined classes' in message   @P4A*/ 01214500
*/*   C - Made limit of CDT entries 896, not 895                 @P5A*/ 01215200
*/*   C - made CNSTID limit 255, not 256                         @P6A*/ 01215900
*/*   C - CDT entries max. 768 (1024-256) user defined classes,  @01A*/ 01216100
*/*       256 IBM defined classes, 384 pairs grouping/member     @01A*/ 01216300
*/*       classes allowed                                        @01A*/ 01216500
*/*   C - Allow POSIT values up to 1024.                         @L7A*/ 01216600
*/*   C - Allow the required numeric or national character to be @P7A*/ 01217400
*/*       in any position, not just 1-4, as documented.          @P7A*/ 01218200
*/*   A - Add new MAXLENX keyword                                @L8A*/ 01218600
*/*   A - Add new CASE=UPPER|ASIS keyword                        @L9A*/ 01218800
*/*   A - Issue an MNOTE with return code 8 if class name and    @02A*/ 01218850
*/*       and group name are the same or if class name and       @02A*/ 01218900
*/*       member name are the same.                              @02A*/ 01218950
*/*                                                                  */ 01219000
*/* END OF SPECIFICATIONS ********************************************/ 01220000
*/*                                                                  */ 01240000
         MACRO                                                          01300000
&NAME    ICHERCDE &CLASS=,&ID=,&GROUP=,&MEMBER=,&MAXLNTH=,&FIRST=,     *01350000
               &OTHER=,&POSIT=,&OPER=,&DFTUACC=,&RACLIST=,&GENLIST=,   *01400000
               &DFTRETC=,&RACLREQ=,&PROFDEF=,&SLBLREQ=,&KEYQUAL=,      *01433300
               &RVRSMAC=,&MAXLENX=,&CASE=                          @L9C 01482190
         LCLA  &ITEST,&MAXL,&MAXLX                                 @L8C 01511100
         LCLA  &BYTECT,&MASKLEN                                    @L1A 01550000
         LCLB  &OP,&P(8),&FSW(4),&OSW(4),&MS(5),&US(5)             @L1A 01600000
         LCLC  &FIR,&OTHERS,&CLASS1,&ID1,&POSIT1                        01650000
         LCLA  &N,&M                                                    01700000
         LCLB  &RACL,&GENL,&CASB                                   @L9C 01726990
         LCLB  &B1(4)                                              @D1C 01730000
         LCLB  &RCLRQ,&PRFDF,&SLBRQ,&RVSMC                         @D1C 01735000
         LCLA  &DFTRC                                              @L2A 01740000
         LCLA  &KEYQL                                              @L4A 01745000
         LCLA  &NC               1 = numeric or nat'l char found   @P7A 01747500
         GBLA  &I,&J,&K                                                 01750000
         GBLB  &ICHIBM                                             @L0A 01800000
         GBLC  &GRP(384),&MEM(384),&CLS(768)                       @01C 01850000
         ACTR  1000000                                         @ZA84505 01900000
&NC      SETA  0                 No numeric or nat'l char yet      @P7A 01925000
&MASKLEN SETA  1024                                                @L7C 01950000
.* VALIDITY CHECK INPUT OPERANDS                                        02000000
 AIF (T'&CLASS EQ 'O' AND T'&ID EQ 'O' AND T'&GROUP EQ 'O' AND         *02050000
               T'&MEMBER EQ 'O' AND T'&MAXLNTH EQ 'O' AND T'&FIRST EQ  *02100000
               'O' AND T'&OTHER EQ 'O' AND T'&POSIT EQ 'O').CKMORE      02150000
         AGO   .START                                                   02200000
.CKMORE  ANOP                                                           02250000
         AIF   (T'&OPER EQ 'O' AND T'&DFTUACC EQ 'O' AND               *02287500
               T'&RACLIST EQ 'O' AND                                   *02325000
               T'&GENLIST EQ 'O').CKMORB1                          @L2C 02335700
         AGO   .START                                              @L2A 02346400
.CKMORB1 ANOP                                                      @L2A 02357100
         AIF   (T'&RACLREQ EQ 'O' AND T'&PROFDEF EQ 'O' AND            *02367800
               T'&SLBLREQ EQ 'O' AND T'&DFTRETC EQ 'O' AND             *02378500
               T'&RVRSMAC EQ 'O').CKSFS                            @D1C 02382800
         AGO   .START                                              @L4A 02387100
.CKSFS   ANOP                                                      @L4A 02391400
         AIF   (T'&KEYQUAL EQ 'O').ENDUP     No Operands - END     @L4A 02395700
.START   ANOP                                                           02400000
&CLASS1  SETC  ' '                                                      02450000
         AIF   (T'&CLASS EQ 'O').NOCLASS    NO CLASS-ERROR              02500000
         AIF   (K'&CLASS LT 4 OR                                       *02550000
               K'&CLASS GT 8).BADCLAS       INVALID CLASS LENGTH-ERROR  02600000
         AIF   ('&CLASS' EQ 'GROUP' OR                                 *02650000
               '&CLASS' EQ 'USER' OR                                   *02700000
               '&CLASS' EQ 'DATASET').INVCLAS   RESTRICTED CLASS NAME   02750000
&N       SETA  K'&CLASS                         LENGTH OF CLASS NAME    02800000
.CKLOOP0 ANOP                                                           02850000
         AIF   ('&CLASS'(&N,1) GE 'A' AND                              *02900000
               '&CLASS'(&N,1) LE 'I').CHAROK                            02950000
         AIF   ('&CLASS'(&N,1) GE 'J' AND                              *03000000
               '&CLASS'(&N,1) LE 'R').CHAROK                            03050000
         AIF   ('&CLASS'(&N,1) GE 'S' AND                              *03100000
               '&CLASS'(&N,1) LE 'Z').CHAROK                            03150000
         AIF   (&N EQ 1).PASSFST                                        03200000
         AIF   ('&CLASS'(&N,1) GE '0' AND                              *03250000
               '&CLASS'(&N,1) LE '9').NATCHAR                      @P7C 03300000
.PASSFST ANOP                                                           03350000
         AIF   ('&CLASS'(&N,1) EQ '@').NATCHAR                     @P7C 03400000
         AIF   ('&CLASS'(&N,1) EQ '#').NATCHAR                     @P7C 03450000
         AIF   ('&CLASS'(&N,1) NE '$').BADCHAR                          03500000
.NATCHAR ANOP                                                      @P7M 03516600
&NC      SETA  1                          Numeric or nat'l found   @P7A 03533200
.CHAROK  ANOP                                                           03550000
&N       SETA  &N-1                          NEXT CHARACTER             03600000
         AIF   (&N GT 0).CKLOOP0                                        03650000
         AIF   (&ICHIBM).CLASSET                                   @L0A 03700000
         AIF   (&NC EQ 0).WRNCHAR   Warning if no numeric or nat'l @P7C 03750000
.CLASSET ANOP                                                           04300000
&CLASS1  SETC  '&CLASS'                                                 04350000
.CLASSOP ANOP                                                           04400000
&K       SETA  &K+1                          UP CLASS COUNT             04450000
         AIF   (&K GT 768).EXCLASS          TOO MANY CLASSES-ERROR @01C 04500000
&ID1     SETC  '0'                                                      04550000
&CLS(&K) SETC  '&CLASS1'                     ADD CLASS TO CLASS LIST    04600000
         AIF   (T'&ID EQ 'O').NOID           NO ID-ERROR                04650000
         AIF   (T'&ID NE 'N').IDSET          ID NOT SELF DEFINING       04700000
         AIF   ('&ID' LT '1' OR                                        *04750000
               '&ID' GT '255').BADID        ID VALUE INCORRECT     @P6A 04800000
         AIF   (NOT &ICHIBM AND '&ID' LT '128').WARNID                  04850000
         AIF   (&ICHIBM AND '&ID' GT '127').FAILID                 @L1A 04900000
.*                                                                 @L0A 04950000
.IDSET   ANOP                                                           05000000
&ID1     SETC  '&ID'                                                    05050000
.POSOP   ANOP                                                           05100000
&POSIT1  SETC  '0'                                                      05150000
         AIF   (T'&POSIT EQ 'O').NOPOSIT     NO POSIT-ERROR             05200000
         AIF   (T'&POSIT NE 'N').WRNGPOS     POSIT NOT SELF DEFINING    05250000
         AIF   ('&POSIT' LT '0' OR                                     *05300000
               '&POSIT' GT '&MASKLEN-1').BADPOS BAD POSIT VALUE    @L1A 05350000
         AIF   (NOT &ICHIBM AND                                        *05400000
               (('&POSIT' LT '19') OR                                  *05425000
               ('&POSIT' GT '56' AND '&POSIT' LT '128') OR             *05450000
               ('&POSIT' GT '527'))).WARNPOS                       @L7C 05475000
         AIF   (&ICHIBM AND                                            *05500000
               (('&POSIT' GT '18' AND '&POSIT' LT '57') OR             *05533300
               ('&POSIT' GT '127' AND '&POSIT' LT '528'))).FAILPOS @L7C 05566600
.*                                                                 @L0A 05600000
.POSTSET ANOP                                                           05650000
&POSIT1  SETC  '&POSIT'                                            @L0A 05700000
.GROUPOP ANOP                                                           05750000
         AIF   (T'&GROUP EQ 'O').NXTOP2      NO GROUP-PROCESS NEXT      05800000
         AIF   (T'&MEMBER NE 'O').BADMG      MEMBER AND GROUP INVALID   05850000
         AIF   (K'&GROUP GT 8).BADGRP        GROUP LENGTH INVALID       05900000
         AIF   ('&GROUP' EQ '&CLASS').GCSAME GROUP & CLASS EQUAL   @02A 05920000
.GRPSET  ANOP                                                           05950000
&I       SETA  &I+1                          UP GROUP COUNT             06000000
         AIF   (&I GT 384).TOOMANY           MAX GROUPS EXCEEDED   @01C 06050000
&GRP(&I) SETC  '&GROUP'                      ADD GROUP TO LIST          06100000
.NXTOP2  ANOP                                                           06150000
         AIF   (T'&MEMBER EQ 'O').NXTOP3     NO MEMBER-PROCESS NEXT     06200000
         AIF   (K'&MEMBER GT 8).BADMEM       MEMBER LENGTH INVALID      06250000
         AIF   ('&MEMBER' EQ '&CLASS').MCSAME MEMBER & CLASS EQUAL @02A 06270000
.MEMSET  ANOP                                                           06300000
&J       SETA  &J+1                          UP MEMBER COUNT            06350000
         AIF   (&J GT 384).TOOMANY           MAX MEMBERS EXCEEDED  @01C 06400000
&MEM(&J) SETC  '&MEMBER'                     ADD MEMBER TO LIST         06450000
&MS(1)   SETB  1                             INDICATE RESOURCE GROUP    06500000
.NXTOP3  ANOP                                                           06550000
         AIF   ('&CLASS' EQ 'DASDVOL' OR                               *06600000
               '&CLASS' EQ 'TAPEVOL').MAXL6                             06650000
&MAXL    SETA  8                             SET DEFAULT MAXLNTH        06700000
         AGO   .MAXLCON                                                 06750000
.MAXL6   ANOP                                                           06800000
&MAXL    SETA  6                                                        06850000
.MAXLCON ANOP                                                           06900000
         AIF   (T'&MAXLNTH EQ 'O').NXTOP3A   MAXLNTH NOT SPECIFIED      06950000
         AIF   (T'&MAXLNTH NE 'N').WRNGML    MAXLNTH NOT SELF DEFINING  07000000
         AIF   ('&MAXLNTH' LT '1' OR                                   *07050000
               '&MAXLNTH' GT '246').BADML    MAXLNTH VALUE INVALID @L4C 07100000
         AIF   ('&CLASS' EQ 'DASDVOL' AND                              *07150000
               '&MAXLNTH' NE '6').BADML2     MAXLNTH VALUE INVALID      07200000
         AIF   ('&CLASS' EQ 'TAPEVOL' AND                              *07250000
               '&MAXLNTH' NE '6').BADML2     FOR DASDVOL, TAPEVOL, OR   07300000
         AIF   ('&CLASS' EQ 'TERMINAL' AND                             *07350000
               '&MAXLNTH' NE '8').BADML2     TERMINAL CLASS             07400000
&MAXL    SETA  &MAXLNTH                      SET MAXL TO MAXLNTH        07450000
.NXTOP3A ANOP                                                           07500000
&MAXLX   SETA  &MAXL                         SET DEFAULT MAXLENX   @L8A 07506200
         AIF   (T'&MAXLENX EQ 'O').NXTOP3B   MAXLENX NOT SPECIFIED @L8A 07512400
         AIF   (T'&MAXLENX NE 'N').WRNGMLX   NOT SELF DEFINING     @L8A 07518600
         AIF   ('&MAXLENX' LT '1' OR                                   *07524800
               '&MAXLENX' GT '246').BADMLX   MAXLENX VALUE INVALID @L8A 07531000
&MAXLX   SETA  &MAXLENX                      SET MAXL TO MAXLNTH   @L8A 07537200
.NXTOP3B ANOP                                                      @L8A 07543400
&FIR     SETC  'ALPHA'                       SET DEFAULT FIRST VALUE    07550000
         AIF   (T'&FIRST EQ 'O').NXTOP4      FIRST NOT SPECIFIED        07600000
 AIF ('&FIRST' NE 'ALPHA' AND '&FIRST' NE 'NUMERIC' AND '&FIRST' NE    *07650000
               'ALPHANUM' AND '&FIRST' NE 'ANY' AND '&FIRST' NE        *07700000
               'NONATABC' AND '&FIRST' NE 'NONATNUM').BADFST            07750000
&FIR     SETC  '&FIRST'                      SAVE FIRST VALUE           07800000
.NXTOP4  ANOP                                                           07850000
&OTHERS  SETC  'ALPHA'                       SET DEFAULT OTHER VALUE    07900000
         AIF   (T'&OTHER EQ 'O').NXTOP5      OTHER NOT SPECIFIED        07950000
 AIF ('&OTHER' NE 'ALPHA' AND '&OTHER' NE 'NUMERIC' AND '&OTHER' NE    *08000000
               'ALPHANUM' AND '&OTHER' NE 'ANY' AND '&OTHER' NE        *08050000
               'NONATABC' AND '&OTHER' NE 'NONATNUM').BADOTH            08100000
&OTHERS  SETC  '&OTHER'                      SAVE OTHER VALUE           08150000
.NXTOP5  ANOP                                                           08200000
         AIF   (T'&OPER EQ 'O').NXTOP5A      OPER NOT SPECIFIED         08250000
         AIF   ('&OPER' EQ 'NO').NXTOP6      OPER EQUALS NO             08300000
         AIF   ('&OPER' NE 'YES').BADOP      OPER VALUE INVALID         08350000
.NXTOP5A ANOP                                                           08400000
&OP      SETB  1                             INDICATE OPER EQUALS YES   08450000
.NXTOP6  ANOP                                                           08500000
         AIF   (T'&DFTUACC EQ 'O').NXTOP7    DFTUACC NOT SPECIFIED @P1C 08550000
 AIF ('&DFTUACC' NE 'ALTER' AND '&DFTUACC' NE 'CONTROL' AND '&DFTUACC' *08600000
               NE 'UPDATE' AND '&DFTUACC' NE 'READ' AND '&DFTUACC'     *08650000
               NE 'NONE').BADDFU             DFTUACC VALUE INVALID      08700000
.*IF THE "RACLIST" OPERAND VALUE WAS OMITTED,                      @P1A 08701700
.*DEFAULT TO "DISALLOWED", (SET THE BIT VALUE TO 0).               @P1A 08703400
.*IF "ALLOWED" WAS SPECIFIED, SET THE BIT VALUE TO 1.              @P1A 08705100
.*IF "DISALLOWED" WAS SPECIFIED, SET THE BIT VALUE TO 0.           @P1A 08706800
.*IF A DIFFERENT VALUE WAS SPECIFIED, ISSUE A MESSAGE,             @P1A 08708500
.*AND DEFAULT TO "DISALLOWED", (SET THE BIT VALUE TO 0).           @P1A 08710200
.NXTOP7  ANOP                                                           08711900
         AIF   (T'&RACLIST EQ 'O').NXTOP8                          @P1A 08713600
         AIF   ('&RACLIST' EQ 'ALLOWED').NXTOP7A                   @P1A 08715300
         AIF   ('&RACLIST' EQ 'DISALLOWED').NXTOP8                 @P1A 08717000
         AIF   ('&RACLIST' NE 'ALLOWED' AND                            *08718700
                '&RACLIST' NE 'DISALLOWED').BADRACL                @P1A 08720400
.NXTOP7A ANOP                                                           08722100
&RACL    SETB  1                                                        08723800
.*IF THE "GENLIST" OPERAND VALUE WAS OMITTED,                      @P1A 08725500
.*DEFAULT TO "DISALLOWED", (SET THE BIT VALUE TO 0).               @P1A 08727200
.*IF "ALLOWED" WAS SPECIFIED, SET THE BIT VALUE TO 1.              @P1A 08728900
.*IF "DISALLOWED" WAS SPECIFIED, SET THE BIT VALUE TO 0.           @P1A 08730600
.*IF A DIFFERENT VALUE WAS SPECIFIED, ISSUE A MESSAGE,             @P1A 08732300
.*AND DEFAULT TO "DISALLOWED", (SET THE BIT VALUE TO 0).           @P1A 08734000
.NXTOP8  ANOP                                                           08735700
         AIF   (T'&GENLIST EQ 'O').NXTOP9                          @L2C 08737400
         AIF   ('&GENLIST' EQ 'ALLOWED').NXTOP8A                   @P1A 08739100
         AIF   ('&GENLIST' EQ 'DISALLOWED').NXTOP9                 @L2C 08740800
         AIF   ('&GENLIST' NE 'ALLOWED' AND                            *08742500
                '&GENLIST' NE 'DISALLOWED').BADGENL                @P1A 08744200
.NXTOP8A ANOP                                                           08745900
&GENL    SETB  1                                                        08747600
.NXTOP9  ANOP                                                      @L2A 08749600
&DFTRC   SETA  4                           Set RC to default RC=4  @L2A 08751600
.CKDFTRC ANOP                                                      @L2A 08753600
         AIF   (T'&DFTRETC EQ 'O').CKRCLRQ   Default RC omitted    @L2A 08755600
         AIF   ('&DFTRETC' NE '0' AND '&DFTRETC' NE '4' AND            *08757600
                '&DFTRETC' NE '8').INVDFRC   Incorrect RC value    @L2A 08759600
&DFTRC   SETA  &DFTRETC                      Set DFTRC to DFTRETC  @L2A 08761600
.CKRCLRQ ANOP                                                      @L2A 08763600
         AIF   (T'&RACLREQ EQ 'O').DFPRFDF   RACLREQ not specified @L2A 08765600
         AIF   ('&RACLREQ' EQ 'NO').DFPRFDF  RACLREQ equals NO     @L2A 08767600
         AIF   ('&RACLREQ' NE 'YES').INVRCLR RACLREQ value invalid @L2A 08769600
&RCLRQ   SETB  1                          Indicate RACLREQ = YES   @L2A 08771600
.DFPRFDF ANOP                                                      @L2A 08773600
&PRFDF   SETB  0             Set default value for PROFDEF to YES  @P2C 08775600
.CKPRFDF ANOP                                                      @L2A 08777600
         AIF   (T'&PROFDEF EQ 'O').CKSLBRQ   PROFDEF not specified @P3C 08779600
         AIF   ('&PROFDEF' EQ 'YES').CKSLBRQ PROFDEF equals YES    @P3C 08781600
         AIF   ('&PROFDEF' NE 'NO').INVPRDF  PROFDEF value invalid @L2A 08783600
&PRFDF   SETB  1                          Indicate PROFDEF = NO    @P2C 08785600
.CKSLBRQ ANOP                                                      @P3C 08786000
         AIF   (T'&SLBLREQ EQ 'O').CKRVSMC   SLBLREQ not specified @P3C 08786400
         AIF   ('&SLBLREQ' EQ 'NO').CKRVSMC  SLBLREQ equals NO     @P3C 08786800
         AIF   ('&SLBLREQ' NE 'YES').INVSLBL SLBLREQ value invalid @P3C 08787200
&SLBRQ   SETB  1                          Indicate SLBLREQ = YES   @P3C 08787600
.CKRVSMC ANOP                                                      @P3C 08788000
         AIF   (T'&RVRSMAC EQ 'O').DFTKEYQ   RVRSMAC not specified @D1A 08788400
         AIF   ('&RVRSMAC' EQ 'NO').DFTKEYQ  RVRSMAC equals NO     @D1A 08788800
         AIF   ('&RVRSMAC' NE 'YES').INVRVMC RVRSMAC value invalid @D1A 08789200
&RVSMC   SETB  1                          Indicate RVRSMAC = YES   @D1A 08789600
.DFTKEYQ ANOP                                                      @L4A 08790400
&KEYQL   SETA  0                           Set KEYQUAL to default  @L4A 08791200
.CKKEYQL ANOP                                                      @L4A 08792000
         AIF   (T'&KEYQUAL EQ 'O').DFTCASE   KEYQUAL omitted       @L9C 08793190
         AIF   (T'&KEYQUAL NE 'N').INVKEYQ   INVALID KEYQUAL       @L4A 08793600
         AIF   ('&KEYQUAL' LT '0' OR                                   *08794400
                '&KEYQUAL' GT '123').INVKEYQ  Invalid KEYQUAL      @L4A 08795200
&KEYQL   SETA  &KEYQUAL                      Set KEYQL to KEYQUAL  @L4A 08796000
.********************************************************************** 08796100
.* IF THE "CASE" OPERAND VALUE WAS OMITTED, DEFAULT TO "UPPER"     @L9A 08796200
.*(SET THE BIT VALUE TO 0). IF "UPPER" WAS SPECIFIED, SET THE      @L9A 08796300
.*BIT VALUE TO 0. IF "ASIS" WAS SPECIFIED, SET THE BIT VALUE       @L9A 08796400
.*TO 1. IF A DIFFERENT VALUE WAS SPECIFIED, ISSUE A MESSAGE,       @L9A 08796500
.*AND DEFAULT TO "UPPER", (SET THE BIT VALUE TO 0).                @L9A 08796600
.********************************************************************** 08796700
.DFTCASE ANOP                                                      @L9A 08796800
&CASB    SETB  0                           Set CASE to UPPER       @L9A 08796900
         AIF   (T'&CASE EQ 'O').BUILD                              @L9A 08797000
         AIF   ('&CASE' EQ 'UPPER').BUILD                          @L9A 08797100
         AIF   ('&CASE' NE 'ASIS').INVCASE                         @L9A 08797200
&CASB    SETB  1                                                        08797300
.* BUILD CLASS DESCRIPTOR                                               08797600
.BUILD   ANOP                                                           08800000
&CLASS1  CSECT                                                          08850000
A1&SYSNDX DC   AL2(A2&SYSNDX)                LENGTH OF DESCRIPTOR       08900000
         DC    AL1(&ID1)                     ID                         08950000
         DC    CL8'&CLASS1'                  CLASS NAME                 09000000
         AIF   (T'&MEMBER NE 'O').DCMEM      MEMBER SPECIFIED           09050000
         AIF   (T'&GROUP EQ 'O').GMNULL      GROUP NOT SPECIFIED        09100000
         DC    CL8'&GROUP'                   GROUP CLASS XREF           09150000
         AGO   .BUILD02                                                 09200000
.DCMEM   ANOP                                                           09250000
         DC    CL8'&MEMBER'                  MEMBER CLASS XREF          09300000
         AGO   .BUILD02                                                 09350000
.GMNULL  ANOP                                                           09400000
         DC    CL8' '                        GROUP MEMBER XREF NULL     09450000
.BUILD02 ANOP                                                           09500000
         DC    AL1(&MAXLX)            MAXIMUM RESOURCE NAME LENGTH @L8C 09550000
         AIF   ('&FIR' EQ 'NUMERIC').SKIP01  ALPHABETIC NOT ALLOWED     09600000
&FSW(1)  SETB  1                             ALPHABETIC ALLOWED         09650000
.SKIP01  ANOP                                                           09700000
         AIF   ('&FIR' NE 'ALPHA' AND                                  *09750000
               '&FIR' NE 'ALPHANUM' AND                                *09800000
               '&FIR' NE 'ANY').SKIP02       NATIONAL NOT ALLOWED       09850000
&FSW(2)  SETB  1                             NATIONAL ALLOWED           09900000
.SKIP02  ANOP                                                           09950000
         AIF   ('&FIR' EQ 'ALPHA' OR                                   *10000000
               '&FIR' EQ 'NONATABC').SKIP03  NUMERICS NOT ALLOWED       10050000
&FSW(3)  SETB  1                             NUMERICS ALLOWED           10100000
.SKIP03  ANOP                                                           10150000
         AIF   ('&FIR' NE 'ANY').BUILD03     SPECIAL NOT ALLOWED        10200000
&FSW(4)  SETB  1                             SPECIAL ALLOWED            10250000
.BUILD03 ANOP                                                           10300000
         AIF   ('&OTHERS' EQ 'NUMERIC').SKIP04  ALPHABETIC NOT ALLOWED  10350000
&OSW(1)  SETB  1                                ALPHABETIC ALLOWED      10400000
.SKIP04  ANOP                                                           10450000
         AIF   ('&OTHERS' NE 'ALPHA' AND                               *10500000
               '&OTHERS' NE 'ALPHANUM' AND                             *10550000
               '&OTHERS' NE 'ANY').SKIP05    NATIONAL NOT ALLOWED       10600000
&OSW(2)  SETB  1                             NATIONAL ALLOWED           10650000
.SKIP05  ANOP                                                           10700000
         AIF   ('&OTHERS' EQ 'ALPHA' OR                                *10750000
               '&OTHERS' EQ 'NONATABC').SKIP06  NUMERIC NOT ALLOWED     10800000
&OSW(3)  SETB  1                                NUMERIC ALLOWED         10850000
.SKIP06  ANOP                                                           10900000
         AIF   ('&OTHERS' NE 'ANY').BUILD04  SPECIAL NOT ALLOWED        10950000
&OSW(4)  SETB  1                             SPECIAL ALLOWED            11000000
.BUILD04 ANOP                                                           11050000
         DC    B'&FSW(1)&FSW(2)&FSW(3)&FSW(4).0000' FIRST CHAR FLAGS    11100000
         DC    B'&OSW(1)&OSW(2)&OSW(3)&OSW(4).0000' OTHER CHAR FLAGS    11150000
         AIF   (T'&DFTUACC EQ 'O').NODFTU        NO DFTUACC SPECIFIED   11200000
         AIF   ('&DFTUACC' EQ 'ALTER').SETD01    UACC OF ALTER          11250000
         AIF   ('&DFTUACC' EQ 'CONTROL').SETD02  UACC OF CONTROL        11300000
         AIF   ('&DFTUACC' EQ 'UPDATE').SETD03   UACC OF UPDATE         11350000
         AIF   ('&DFTUACC' EQ 'READ').SETD04     UACC OF READ           11400000
&US(5)   SETB  1                              SET UACC OF NONE          11450000
         AGO   .BUILD05                                                 11500000
.SETD01  ANOP                                                           11550000
&US(1)   SETB  1                              SET UACC OF ALTER         11600000
         AGO   .BUILD05                                                 11650000
.SETD02  ANOP                                                           11700000
&US(2)   SETB  1                              SET UACC OF CONTROL       11750000
         AGO   .BUILD05                                                 11800000
.SETD03  ANOP                                                           11850000
&US(3)   SETB  1                              SET UACC OF UPDATE        11900000
         AGO   .BUILD05                                                 11950000
.SETD04  ANOP                                                           12000000
&US(4)   SETB  1                              SET UACC OF READ          12050000
         AGO   .BUILD05                                                 12100000
.NODFTU  ANOP                                                           12150000
&MS(2)   SETB  1                              SET USE UACC FROM ACEE    12200000
.BUILD05 ANOP                                                           12250000
         DC    B'&US(1)&US(2)&US(3)&US(4).000&US(5)'  DFTUACC FLAGS     12300000
         AIF   (NOT &OP).SKIP07               OPER EQUALS NO            12350000
&MS(3)   SETB  1                              SET OPER EQUALS YES       12400000
.SKIP07  ANOP                                                      @P1A 12415000
         AIF   (NOT &RACL).SKIP08   RACLIST IS "0","DISALLOWED"    @P1A 12430000
&MS(4)   SETB  1                    SET RACLIST TO "ALLOWED"       @P1A 12445000
.SKIP08  ANOP                                                      @P1C 12460000
         AIF   (NOT &GENL).SKIP09   GENLIST IS "0","DISALLOWED"    @P1A 12475000
&MS(5)   SETB  1                    SET GENLIST TO "ALLOWED"       @P1A 12490000
.*SET THE MISCELLANEOUS FLAGS                                      @P1A 12505000
.SKIP09  ANOP                                                      @P1A 12520000
         DC    B'&MS(1)&MS(2)&MS(3)&MS(4)&MS(5).000'  MISC. FLAGS  @P1C 12535000
&BYTECT  SETA  &POSIT1/8                      # OF PRECEDING ZEROS @L1A 12550000
         DC    &BYTECT.B'0'                   POSIT MASK           @L1A 12600000
&P(&POSIT1+1-(&BYTECT*8)) SETB  1             SET FLAG POSITION    @L1A 12650000
         DC    B'&P(1)&P(2)&P(3)&P(4)&P(5)&P(6)&P(7)&P(8)' POSIT MASK  +12700000
                                                                   @L1A 12712500
&BYTECT  SETA  (&MASKLEN-(&POSIT1+1))/8       # OF TRAILING ZEROS  @L1A 12725000
         DC    &BYTECT.B'0'                   POSIT MASK           @L1A 12737500
         DC    AL4(0)                         GENLIST POINTER      @L1A 12750000
         DC    AL4(0)                         RACLIST POINTER      @L1A 12762500
         DC    AL1(&DFTRC)          SET THE DEFAULT RETURN CODE    @L2A 12775000
.CRCLRQB ANOP                                                      @L2A 12787500
         AIF   (NOT &RCLRQ).CPRFDFB RACLREQ IS "NO" ("0")          @L2A 12800000
&B1(1)   SETB  1                    SET RACLREQ TO "YES" ("1")     @L2A 12812500
.CPRFDFB ANOP                                                      @L2A 12825000
         AIF   (NOT &PRFDF).CSLBRQB PROFDEF IS "YES" ("0")         @P3C 12837500
&B1(2)   SETB  1                    SET PROFDEF TO "NO" ("1")      @L2A 12850000
.CSLBRQB ANOP                                                      @P3C 12857100
         AIF   (NOT &SLBRQ).CRVRSMC SLBLREQ IS "NO" ("0")          @D1C 12864200
&B1(3)   SETB  1                    SET SLBLREQ TO "YES" ("1")     @P3C 12871300
.CRVRSMC ANOP                                                      @P3C 12878400
         AIF   (NOT &RVSMC).B1MISC  RVRSMAC IS "NO" ("0")          @D1A 12885500
&B1(4)   SETB  1                    SET RVRSMAC TO "YES" ("1")     @D1A 12892600
.*SET THE B1 MISCELLANEOUS FLAGS                                   @L2A 12900000
.B1MISC  ANOP                                                      @L2A 12912500
         DC    B'&B1(1)&B1(2)&B1(3).&B1(4).0&CASB.00'  MISC FLAGS  @L9C 12930990
         DC    AL1(&KEYQL)          SET THE KEYQUAL VALUE          @L4A 12937500
         DC    AL1(&MAXL)  MAXIMUM RESOURCE NAME LENGTH for ENTITY @L8C 12950000
         DC    XL4'00'              ALLIGN TO DWORD BDY            @L4C 12962500
.*Allow space for the data space STOKEN                            @L3A 12975000
         DC    XL8'00'              STOKEN FOR RACLIST DATA SPACE  @L3A 12987500
         DS    0D                                                       13000000
A2&SYSNDX EQU *-A1&SYSNDX                                               13050000
         MEXIT                                                          13100000
.* PROCESS END OF CLASS DESCRIPTORS                                     13150000
.ENDUP   ANOP                                                           13200000
         AIF   (&K EQ 0).NOCD                 NO CLASS DESCRIPTORS      13250000
&N       SETA  &K                             NUMBER OF CLASSES         13300000
.CKLOOP5 ANOP                                                           13350000
         AIF   (K'&CLS(&N) LT 4).NXTCLAS                                13400000
&M       SETA  &N                                              @PM48909 13450000
         AGO   .NXTCK                                          @PM48909 13500000
.CKLOOP6 ANOP                                                           13550000
         AIF   (K'&CLS(&M) LT 4).NXTCK                                  13600000
         AIF   ('&CLS(&M)'(1,8) EQ '&CLS(&N)'(1,8)).DUPCLAS        @L5C 13650000
.NXTCK   ANOP                                                           13700000
&M       SETA  &M-1                                                     13750000
         AIF   (&M GT 0).CKLOOP6                                        13800000
.NXTCLAS ANOP                                                           13850000
&N       SETA  &N-1                                                     13900000
         AIF   (&N GT 1).CKLOOP5                                        13950000
.CKLOOP1 ANOP                                                           14000000
&ITEST   SETA  1                              CLASS INDEX               14050000
         AIF   (&I EQ 0).CKLOOP3              NO GROUP NAMES            14100000
.CKLOOP2 ANOP                                                           14150000
         AIF   ('&GRP(&I)' EQ '&CLS(&ITEST)').DONXT1 GROUP FOUND        14200000
&ITEST   SETA  &ITEST+1                       UP CLASS INDEX            14250000
         AIF   (&ITEST GT &K).GNOTFND         GROUP NOT FOUND           14300000
         AGO   .CKLOOP2                       CHECK AGAINST NEXT CLASS  14350000
.DONXT1  ANOP                                                           14400000
&I       SETA  &I-1                           CHECK NEXT GROUP          14450000
         AGO   .CKLOOP1                                                 14500000
.CKLOOP3 ANOP                                                           14550000
&ITEST   SETA  1                              CLASS INDEX               14600000
         AIF   (&J EQ 0).GETOUT               NO MEMBER NAMES           14650000
.CKLOOP4 ANOP                                                           14700000
         AIF   ('&MEM(&J)' EQ '&CLS(&ITEST)').DONXT2 MEMBER FOUND       14750000
&ITEST   SETA  &ITEST+1                       UP CLASS INDEX            14800000
         AIF   (&ITEST GT &K).MNOTFND         MEMBER NOT FOUND          14850000
         AGO   .CKLOOP4                       CHECK AGAINST NEXT CLASS  14900000
.DONXT2  ANOP                                                           14950000
&J       SETA  &J-1                           CHECK NEXT MEMBER         15000000
         AGO   .CKLOOP3                                                 15050000
.GETOUT  ANOP                                                           15100000
ICHRRCDE CSECT                                                          15150000
         DC    AL2(0)  END OF CLASS DESCRIPTORS                         15200000
         END                                                            15250000
         MEXIT                                                          15300000
.NOCLASS ANOP                                                           15350000
         MNOTE 8,'*** CLASS OPERAND REQUIRED ***'                       15400000
         AGO   .CLASSOP                                                 15450000
.BADCLAS ANOP                                                           15500000
         MNOTE 8,'*** INVALID CLASS NAME LENGTH ***'                    15550000
         AGO   .CLASSET                                                 15600000
.INVCLAS ANOP                                                           15650000
         MNOTE 8,'*** CLASS NAME &CLASS RESTRICTED ***'                 15700000
         AGO   .CLASSET                                                 15750000
.BADCHAR ANOP                                                           15800000
         MNOTE 8,'*** CLASS NAME CONTAINS INVALID CHARACTERS ***'       15850000
         AGO   .CLASSET                                                 15900000
.EXCLASS ANOP                                                           15950000
         MNOTE  12,'*** NUMBER OF IBM-RESERVED CLASSES PLUS USER DEFINE*15983300
               D CLASSES EXCEEDS 1024 ***'                         @P4C 16016600
         MEXIT                                                          16050000
.WRNCHAR ANOP                                                           16100000
         MNOTE  4,'*** CLASS NAME DOES NOT CONTAIN NATIONAL CHARACTER O*16150000
               R DIGIT ***'                                        @P7C 16200000
         AGO   .CLASSET                                            @L0A 16250000
.NOID    ANOP                                                           16300000
         MNOTE 8,'*** ID OPERAND REQUIRED ***'                          16350000
         AGO   .POSOP                                                   16400000
.BADID   ANOP                                                           16450000
         MNOTE 8,'*** ID VALUE INCORRECT ***'                           16500000
         AGO   .POSOP                                                   16550000
.WARNID  ANOP                                                           16600000
     MNOTE 4,'*** ID VALUE NOT WITHIN RECOMMENDED RANGE (128-255) ***'  16650000
.*                                                                 @L0A 16700000
         AGO   .IDSET                                              @L0A 16750000
.FAILID  ANOP                                                           16800000
     MNOTE 8,'*** ID VALUE NOT WITHIN REQUIRED RANGE (0-127) ***'  @L1A 16850000
         AGO   .IDSET                                              @L0A 16900000
.NOPOSIT ANOP                                                           16950000
         MNOTE 8,'*** POSIT OPERAND REQUIRED ***'                       17000000
         AGO   .GROUPOP                                                 17050000
.WRNGPOS ANOP                                                           17100000
         MNOTE 8,'*** POSIT VALUE MUST BE SELF DEFINING ***'            17150000
         AGO   .GROUPOP                                                 17200000
.BADPOS  ANOP                                                           17250000
         MNOTE 8,'*** POSIT VALUE INCORRECT ***'                        17300000
         AGO   .GROUPOP                                                 17350000
.WARNPOS ANOP                                                           17400000
     MNOTE 4,'*** POSIT VALUE NOT WITHIN RECOMMENDED RANGES (19-56, 128+17450000
               -527) ***'                                          @L7C 17500000
         AGO   .POSTSET                                            @L0A 17550000
.FAILPOS ANOP                                                           17600000
         MNOTE 8,'*** POSIT VALUE NOT WITHIN REQUIRED RANGES (0-18, 57-+17650000
               127, 528-1023) ***'                                 @L7C 17700000
         AGO   .POSTSET                                            @L0A 17750000
.BADMG   ANOP                                                           17800000
         MNOTE 8,'*** MEMBER AND GROUP MUTUALLY EXCLUSIVE ***'          17850000
         AGO   .NXTOP2                                                  17900000
.BADGRP  ANOP                                                           17950000
         MNOTE 8,'*** GROUP CLASS NAME TOO LONG ***'                    18000000
         AGO   .GRPSET                                                  18050000
.GCSAME  ANOP                                                      @02A 18060000
         MNOTE 8,'*** GROUP NAME AND CLASS NAME MUST BE DIFFERENT ***'  18070000
         AGO   .GRPSET                                             @02A 18080000
.BADMEM  ANOP                                                           18100000
         MNOTE 8,'*** MEMBER CLASS NAME TOO LONG ***'                   18150000
         AGO   .MEMSET                                                  18200000
.MCSAME  ANOP                                                      @02A 18210000
         MNOTE 8,'*** MEMBER NAME AND CLASS NAME MUST BE DIFFERENT ***' 18220000
         AGO   .MEMSET                                             @02A 18230000
.TOOMANY ANOP                                                           18250000
         MNOTE 12,'*** GROUP OR MEMBER COUNT EXCEEDS MAXIMUM ***'       18300000
         MEXIT                                                          18350000
.WRNGML  ANOP                                                           18400000
         MNOTE 8,'*** MAXLNTH VALUE MUST BE SELF DEFINING ***'          18450000
         AGO   .NXTOP3A                                                 18500000
.BADML   ANOP                                                           18550000
         MNOTE 8,'*** MAXLNTH VALUE INCORRECT ***'                      18600000
         AGO   .NXTOP3A                                                 18650000
.BADML2  ANOP                                                           18700000
         MNOTE 8,'*** MAXLNTH VALUE INCORRECT FOR CLASS &CLASS ***'     18750000
         AGO   .NXTOP3A                                                 18800000
.WRNGMLX ANOP                                                      @L8A 18807100
         MNOTE 8,'*** MAXLENX VALUE MUST BE SELF DEFINING ***'     @L8A 18814200
         AGO   .NXTOP3B                                            @L8A 18821300
.BADMLX  ANOP                                                      @L8A 18828400
         MNOTE 8,'*** MAXLENX VALUE INCORRECT ***'                 @L8A 18835500
         AGO   .NXTOP3B                                            @L8A 18842600
.BADFST  ANOP                                                           18850000
         MNOTE 8,'*** FIRST VALUE INVALID ***'                          18900000
         AGO   .NXTOP4                                                  18950000
.BADOTH  ANOP                                                           19000000
         MNOTE 8,'*** OTHER VALUE INVALID ***'                          19050000
         AGO   .NXTOP5                                                  19100000
.BADOP   ANOP                                                           19150000
         MNOTE 8,'*** OPER VALUE INVALID ***'                           19200000
         AGO   .NXTOP5A                                                 19250000
.BADDFU  ANOP                                                           19300000
         MNOTE 8,'*** DFTUACC VALUE INVALID ***'                        19350000
         AGO   .NXTOP7                                             @P1A 19362500
.BADRACL ANOP                                                      @P1A 19375000
         MNOTE 8,'*** RACLIST VALUE INVALID ***'                   @P1A 19387500
         AGO   .NXTOP8                                             @P1A 19400000
.BADGENL ANOP                                                      @P1A 19412500
         MNOTE 8,'*** GENLIST VALUE INVALID ***'                   @P1A 19425000
         AGO   .NXTOP9                                             @L2C 19437500
.NOCD    ANOP                                                           19450000
         MNOTE 4,'*** NO CLASS DESCRIPTORS DEFINED ***'                 19500000
         AGO   .GETOUT                                                  19550000
.DUPCLAS ANOP                                                           19600000
         MNOTE 8,'*** CLASS &CLS(&N) NOT UNIQUE ***'                    19650000
         AGO   .NXTCLAS                                                 19700000
.GNOTFND ANOP                                                           19750000
         MNOTE 8,'*** NO CLASS DEFINED FOR GROUP &GRP(&I) ***'          19800000
         AGO   .DONXT1                                                  19850000
.MNOTFND ANOP                                                           19900000
         MNOTE 8,'*** NO CLASS DEFINED FOR MEMBER &MEM(&J) ***'         19950000
         AGO   .DONXT2                                                  20000000
.INVDFRC ANOP                                                           20003500
         MNOTE 8,'*** INVALID DFTRETC VALUE ==> &DFTRETC.. VALID VALUES+20007000
                ARE 0,4, AND 8 ***'                                @L2A 20010500
         AGO   .CKRCLRQ                                            @L2A 20014000
.INVRCLR ANOP                                                           20017500
         MNOTE 8,'*** RACLREQ VALUE INVALID ***'                   @L2A 20021000
         AGO   .DFPRFDF                                            @L2A 20024500
.INVPRDF ANOP                                                           20028000
         MNOTE 8,'*** PROFDEF VALUE INVALID ***'                   @L2A 20031500
         AGO   .CKSLBRQ                                            @P3C 20032900
.INVSLBL ANOP                                                           20034300
         MNOTE 8,'*** SLBLREQ VALUE INVALID ***'                   @P3C 20035700
         AGO   .CKRVSMC                                            @L4C 20037100
.INVRVMC ANOP                                                           20038500
         MNOTE 8,'*** RVRSMAC VALUE INVALID ***'                   @D1A 20039900
         AGO   .DFTKEYQ                                            @D1A 20041300
.INVKEYQ ANOP                                                           20043300
         MNOTE 8,'*** INVALID KEYQUAL VALUE ==> &KEYQUAL.. VALID RANGE +20044900
               OF VALUES IS BETWEEN 0 AND 123 ***'                 @L4A 20046500
         AGO   .DFTCASE                                            @L9C 20048390
.INVCASE ANOP                                                           20048680
         MNOTE 8,'*** INVALID CASE VALUE ==> &CASE.. VALID VALUES ARE  +20048970
               UPPER AND ASIS ***'                                 @L9A 20049260
         AGO   .BUILD                                              @L9A 20049550
         MEND                                                           20050000
