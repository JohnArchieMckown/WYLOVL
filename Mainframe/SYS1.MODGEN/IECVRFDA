*%GOTO   IECVRF;      /* VRF DATA PARAMETER LIST MAPPING MACRO          00050000
         MACRO                                                          00100000
&LABEL   IECVRFDA &DSECT=YES,&FUNCT=,&LABELS=YES                        00150000
.***** START OF SPECIFICATIONS **************************************** 00200000
.*                                                                      00250000
.*$MAC(IECVRFDA):     VRF RECOVERY DATA AREA                            00300000
.*                                                                      00350000
.* DESCRIPTIVE-NAME = VRF DATA PARAMETER LIST MAPPING MACRO             00400000
.*  @L6C */                                                             00450000
*/*PROPRIETARY V3 STATEMENT                                          */ 00462500
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00475000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00487500
*/*5695-DF1                                                          */ 00500000
*/*(C) COPYRIGHT 1980,1993  IBM CORP.                                */ 00512500
*/*END PROPRIETARY V3 STATEMENT                                      */ 00531200
*/*                                                               @L6A  00550000
.* STATUS = MVS/DFSMS 1.1.0                                             00600000
.*                                                                      00650000
.* FUNCTION = THIS MACRO MAPS THE DADSM VRF DATA TO BE RECORDED IN THE  00700000
.*            VIXM.                                                     00750000
.*                                                                      00800000
.* NOTES = NONE                                                         00850000
.*                                                                      00900000
.* PATCH-LABEL = NOT APPLICABLE                                         00950000
.*                                                                      01000000
.* MODULE-TYPE = MACRO                                                  01050000
.*                                                                      01100000
.*    PROCESSOR = PLS OR BAL                                            01150000
.*                                                                      01200000
.*    ATTRIBUTES = MAPPING MACRO                                        01250000
.*                                                                      01300000
.* ENTRY-POINT = IECVRFDA                                               01350000
.*                                                                      01400000
.*    PURPOSE = SEE FUNCTION ABOVE.                                     01450000
.*                                                                      01500000
.*    LINKAGE = PLS -  INCLUDE IECVRFDA                                 01550000
.*              BAL -  LABEL   IECVRFDA DSECT=  ,FUNCT=                 01600000
.*                                                                      01616600
.*    INPUT   = PLS - NONE                                              01633200
.*              BAL - 'LABEL' WILL CAUSE A LABEL TO BE GENERATED.       01649800
.*                    DEFAULT LABEL IS 'VRFDA'.                         01666400
.*                  - DSECT=YES - GENERATES A DSECT STATEMENT           01683000
.*                    AT THE BEGINNING OF THE VRF DATA AREA. DSECT=NO   01699600
.*                    WILL NOT GENERATE THE DSECT STATEMENT.            01716200
.*                    DSECT=YES IS THE DEFAULT.                         01732800
.*                    'DSECT' KEYWORD IS IGNORED IF 'FUNCT' KEYWORD     01749400
.*                    IS OMITTED.                                       01766000
.*                  - FUNCT=ALLOCATE - GENERATES VRF DATA FOR ALLOCATE. 01782600
.*                         = EXTEND  - GENERATES VRF DATA FOR EXTEND.   01799200
.*                         = SCRATCH - GENERATES VRF DATA FOR SCRATCH.  01815800
.*                         = RELEASE - GENERATES VRF DATA FOR PARTIAL   01832400
.*                                     RELEASE.                         01849000
.*                         = RENAME  - GENERATES VRF DATA FOR RENAME.   01865600
.*                         = ''      - CAUSES DSECT STATEMENTS TO BE    01882200
.*                                     GENERATED FOR EACH OF THE 4      01898800
.*                                     DSECTS. 'DSECT' KEYWORD IS       01915400
.*                                     IGNORED.                         01932000
.*                  - LABELS=YES     - GENERATES LABELS WITH A LABEL    01934500
.*                                   - PREFIX OF 'VRF'.                 01937000
.*                          =NO      - COMPILE ERROR - NO LONGER        01939500
.*                                   - SUPPORTED.                       01942000
.*                          =NNN     - GENERATES LABELS WITH A LABEL    01944500
.*                                   - PREFIX OF 'NNN'.                 01947000
.*                                                                      01950000
.*    OUTPUT = VRF DATA PARAMETER LIST EXPANSION.                       02000000
.*                                                                      02050000
.*             THE MAPPING OF THIS PARAMETER LIST IS DEPENDENT     @L6A 02050800
.*             UPON THE VALUE SPECIFIED ON THE FUCT= KEYWORD.      @L6A 02051600
.*             THE HEADER, HOWEVER, IS AS FOLLOWS REGARDLESS OF    @L6A 02052400
.*             FUNCTION.                                           @L6A 02053200
.*                                                                      02054000
.*    +-------------+-------------+-------------+-------------+    @L6A 02054800
.* 0  |     EYECATCHER = 'VRF '                               |    @L6A 02055600
.*    +-------------+-------------+-------------+-------------+    @L6A 02056400
.* 4  |     VRFLEN                |  VRFFCODE   |  VRFFLAG    |    @L6A 02057200
.*    |     LENGTH OF VRFDA       | FUNC. CODE  | FLAG BYTE   |    @L6A 02058000
.*    +-------------+-------------+-------------+-------------+    @L6A 02058800
.* 8  |     VRFOFEXT              |  VRFLEV     | VRFPREXT    |    @L6A 02059600
.*    | OFFSET TO VRFEXTBL OR     |  LEVEL      | # OF PRIMARY|    @L6A 02060400
.*    | VRFNDSN                   |             | EXTENTS     |    @L6A 02061200
.*    +-------------+-------------+-------------+-------------+    @L6A 02062000
.* C  |     VRFDSN                                            |    @L6A 02062800
.*    +-------------+-------------+-------------+-------------+    @L6A 02063600
.*                 ...                                                  02064400
.*    +-------------+-------------+-------------+-------------+    @L6A 02065200
.* 38 |     VRFF1PTR    CCHHR OF F1 DSCB                      |    @L6A 02066000
.*    +             +-------------+-------------+-------------+    @L6A 02066800
.* 3C |             |     VRFF2PTR      CCHHR OF F2 DSCB      |    @L6A 02067600
.*    +-------------+             +-------------+-------------+    @L6A 02068400
.* C  |                           |                                @L6A 02069200
.*    +-------------+-------------+                                @L6A 02070000
.*                                                                      02070800
.*       FOLLOWING THIS HEADER, MAY FOLLOW ONE, TWO OR THREE OF    @L6C 02071600
.*       THE AREAS, FROM THE FOLLOWING LIST OF THREE AREA TYPES,   @L6C 02072400
.*       DEPENDING ON THE FUNCTION SPECIFIED.                      @L6C 02073200
.*         VRFF3PTR : THIS IS AN ARRAY OF 5 BYTE CCHHR FIELDS.     @L6A 02074000
.*                    THESE CCHHR'S ARE THE DASD ADDRESSES OF      @L6A 02074800
.*                    FORMAT 3 DSCB'S.  THE NUMBER OF ENTRIES IN   @L6A 02075600
.*                    THIS ARRAY VARIES BY FUNCTION.               @L6A 02076400
.*         VRFEXTBL : THIS AREA CONTAINS AN OLD AND NEW FORMAT     @L6A 02077200
.*                    EXTENT DESCRIPTOR TABLE.  BOTH TABLES HAVE   @L6A 02078000
.*                    THE SAME NUMBER OF ENTRIES, BUT THE NUMBER   @L6A 02078800
.*                    OF ENTRIES VARIES BY FUNCTION.               @L6A 02079600
.*         VRFEXDSN : THIS IS A 44 BYTE DATA SET NAME.             @L6A 02080400
.*                                                                      02081200
.*       THE FOLLOWING TABLE SHOWS WHICH IF THE ABOVE AREA'S ARE   @L6A 02082000
.*       MAPPED BY FUNCTION, AND IF MAPPED, THE NUMBER OF ENTRIES  @L6A 02082800
.*       MAPPED.                                                   @L6A 02083600
.*                                                                      02084400
.*       FUNCTION   |  VRFF3PTR     VRFEXTBL     VRFNDSN           @L6C 02085200
.*       --------------------------------------------------------- @L6C 02086000
.*       ''         |  10 ENTRIES   0 ENTRIES    1 44BYTE DSN      @L6C 02086800
.*       ALLOCATE   |  10 ENTRIES   123 ENTRIES  NOT MAPPED        @L6C 02087600
.*       EXTEND     |  2 ENTRIES*   5 ENTRIES    NOT MAPPED        @L6C 02088400
.*       RELEASE    |  10 ENTRIES   123 ENTRIES  NOT MAPPED        @L6C 02089200
.*       RENAME     |  NOT MAPPED   NOT MAPPED   1 44BYTE DSN      @L6C 02090000
.*       SCRATCH    |  10 ENTRIES   133 ENTRIES  NOT MAPPED        @L6C 02090800
.*                                                                      02091600
.*                * EXTEND ADDS A SECOND FIELD NAMED VRFFNPTR      @L4A 02093400
.*                                                                 @L4A 02096500
.* EXIT-NORMAL = END OR MEND STATEMENT                                  02100000
.*                                                                      02150000
.* EXIT-ERROR = AN MNOTE WILL BE GENERATED IF AN INVALID KEYWORD IS     02200000
.*              PRESENT FOR BAL.                                        02250000
.*                                                                      02300000
.* EXTERNAL-REFERENCES = N/A                                            02350000
.*                                                                      02400000
.* TABLES = NONE                                                        02450000
.*                                                                      02500000
.* MACROS = NONE                                                        02550000
.*                                                                      02600000
.* CHANGE-ACTIVITY = NEW MACRO                                          02650000
.*                                                                      02666600
.*$L0=IXVTOC,JDM1134,T790225,STLBS: INDEXED VTOC SUPPORT                02673200
.*$L2=ICFCAT,JDM1113,T790073,STLMRD: ICF CATALOG SUPPORT                02679800
.*$01=OZ67804,HDP1102,STLBS:         FIX FOR ENQ LOCKOUT PROBLEM   @01A 02686400
.*$L3=MVS/XA,JDP1111,STLBS:          REALLOC INTERFACE SUPPORT     @L3A 02693000
.*$L4=PDSE,HDP3320,T88095,STLJDY:    RELEASE -- INCRS DADSM TBL    @L4A 02695300
.*$P1=KET1793,HDP3320,890216,STLJDY: DOCUMENT PDSE                 @P1A 02697600
.*$L5=IXPRUNE,JDZ1110,920210,SJPLRG:VTOC INDEX PRUNING             @L5A 02698200
.*$L6=64KTRACK,JDZ1110,T920102,SJPLKSC:DADSM SUPPORT FOR DASD WITH @L6A 02698800
.*                                   MORE THAN 64K TRACKS          @L6A 02699400
.*$P2=KMM3086,JDZ1110,T920827,SJPLKSC:CVAFVRF FAILS FOR LARGE      @P2A 02699600
.*                                    INDEX DATA SETS.             @P2A 02699800
.*                                                                      02700000
.*** END OF SPECIFICATIONS ******************************************** 02750000
         LCLC  &L                      DEFINE LOCAL CHAR VARIABLE       02800000
*********************************************************************** 03100000
*        VRF - VTOC RECORDING FACILITY DATA AREA EXPANSION              03150000
*********************************************************************** 03200000
         SPACE 1                                                        03250000
.********************************************************************** 03250700
.* CHECK THE LENGTH OF THE VALUES SPECIFIED ON LABELS KEYWORD         * 03251400
.********************************************************************** 03252100
&LABLEN  SETA  K'&LABELS              OBTAIN THE LENGTH OF &LABELS      03252800
.LABCHK1 AIF   (&LABLEN GT 3).LABERR1                                   03253500
.********************************************************************** 03254200
.* CHECK THE VALUE SPECIFIED ON LABELS KEYWORD                        * 03254900
.*   'NO' IS NOT SUPPORTED                                            * 03255600
.*   'YES' SETS PREFIX TO DEFAULT OF VRF                              * 03256300
.*   '' SETS PREFIX TO DEFAULT OF VRF                                 * 03257000
.********************************************************************** 03257700
.LABCHK2 AIF   ('&LABELS' EQ 'NO').LABERR1                              03258400
.LABCHK3 AIF   ('&LABELS' EQ 'YES').LABDEF                              03259100
.LABCHK4 AIF   ('&LABELS' EQ '').LABDEF                                 03259800
&P       SETC  '&LABELS'                SET LABEL PREFIX TO VALUE       03260500
         AGO   .FNCCHK                  SPECIFIED ON LABELS KEYWORD     03261200
.LABDEF  ANOP                           SET LABEL PREFIX TO DEFAULT     03261900
&P       SETC  'VRF'                    VALUE                           03262600
&L       SETC  'VRFDA'                                                  03263300
         AIF   ('&LABEL' EQ '').CDSECT                                  03264000
&L       SETC  '&LABEL'                                                 03264700
         AGO   .FNCCHK                  GO CHECK FUNCT KEYWORD          03265400
.CDSECT  ANOP                                                           03266100
&L       SETC  '&P'.'DA'                                                03266800
         SPACE 1                                                        03267500
         AGO   .FNCCHK                  GO CHECK FUNCT KEYWORD          03268200
.********************************************************************** 03268900
.LABERR1 ANOP                           ERROR MESSAGE                   03269600
         SPACE 1                                                        03270300
         MNOTE 8,'IECVRFDA - INVALID OPERAND - LABELS=&P'               03271000
&ERRSW   SETB  1                                                        03271700
.********************************************************************** 03272400
.FNCCHK  ANOP                           ERROR MESSAGE                   03273100
         AIF   ('&FUNCT' EQ '').DSECT1                                  03274900
         AIF   ('&DSECT' EQ 'NO').NDSECT1                               03300000
         AIF   ('&DSECT' EQ 'YES').DSECT1                               03350000
         MNOTE 12,'***  IHB001 INVALID OPERAND - DSECT=&DSECT'          03400000
         AGO   .FINISH                                                  03450000
.DSECT1  ANOP                                                           03500000
&L       DSECT                VRF DATA                                  03550000
         DS    0F                                                       03582000
         AGO   .VRFDA                                                   03614000
.NDSECT1 ANOP                                                           03646000
         AIF   ('&LABELS' EQ 'NO').NOLAB1                          @L3A 03662000
&L       DS    0F             VRF DATA                                  03678000
.VRFDA   ANOP                                                           03710000
&P.ID    DS    CL4            EBCDIC 'VRF '                             03733100
&P.LENG  DS    H              LENGTH OF VRF DATA                        03756200
&P.FCODE DS    XL1            FUNCTION CODE:                            03779300
&P.ALL   EQU   X'01'          ALLOCATE                                  03802400
&P.EXT   EQU   X'02'          EXTEND                                    03825500
&P.SCR   EQU   X'03'          SCRATCH                                   03848600
&P.PR    EQU   X'04'          PARTIAL RELEASE                           03871700
&P.REN   EQU   X'05'          RENAME                                    03894800
&P.DEFRG EQU   X'06'          DFDSS DEFRAG                         @P2A 03898600
&P.IXPSC EQU   X'F3'          SCRATCH WITH HIGHER LEVEL IX PRUNING @L5A 03902500
&P.IXPRE EQU   X'F5'          RENAME  WITH HIGHER LEVEL IX PRUNING @L5A 03910200
&P.FLAG  DS    XL1            VRF FLAG BYTE                             03917900
&P.NEXTD EQU   X'80'          EXTEND ON NEW VOLUME                      03941000
&P.DSNEX EQU   X'40'          1 = VRFNDSN EXISTS AT OFFSET VRFOFEXT.    03964100
.*                            0 = VRFEXTBL EXISTS AT OFFSET VRFOFEXT.   03987200
&P.ISAME EQU   X'20'          1 = ISAM ALLOCATE WITH EXISTING F1 DSCB.  04010300
.*                            0 = NOT ISAM ALLOCATE WITH F1 DSCB.       04033400
&P.RACF  EQU   X'10'          1 = RACDEF OCCURRED, F1 NOT WRITTEN.      04056500
.*                            0 = RACDEF OCCURRED, F1 WRITTEN.     @01A 04079600
&P.OFEXT DS    H              OFFSET TO VRFEXTBL OR VRFNDSN             04102700
.*                            IN VRF DATA                               04125800
&P.LEV   DS    XL1            RESERVED                                  04148900
&P.LEV00 EQU   X'00'          LEVEL NUMBER USED BY DADSM                04172000
.*                            PRIOR TO $L6 SUPPORT.                @L6A 04195100
&P.LEVC  EQU   X'01'          LEVEL NUMBER USED BY DADSM                04218200
.*                            AFTER $L6 SUPPORT.                   @L6A 04241300
&P.PREXT DS    XL1            NUMBER OF PRIMARY EXTENTS (EXTEND ONLY)   04264400
&P.DSN   DS    CL44           DATA SET NAME                             04287500
&P.F1PTR DS    CL5            CCHHR  OF F1 DSCB                         04310600
&P.F2PTR DS    CL5            CCHHR  OF F2 DSCB (ISAM DATA SET ONLY).   04333700
&P.HEN   EQU   *              END OF VRF DATA HEADER                    04356800
         SPACE 1                                                        04382000
         AIF   ('&FUNCT' EQ '').DSECT2                                  04414000
         AGO   .NDSECT2                                                 04446000
.DSECT2  ANOP                                                           04478000
&P.PTRS  DSECT                CCHHR OF F3 DSCB'S                        04510000
.NDSECT2 ANOP                                                           04542000
         AIF   ('&FUNCT' EQ 'RENAME').DSN                               04574000
&P.F3PTR DS    CL5            CCHHR  OF F3 DSCB                         04606000
         AIF   ('&FUNCT' EQ 'RELEASE').F3NINE                      @L4A 04608600
         AIF   ('&FUNCT' EQ 'ALLOCATE').F3NINE                     @L3A 04611300
         AIF   ('&FUNCT' NE 'SCRATCH').ICFTEST                     @L3M 04616600
.F3NINE  ANOP                                                      @L2A 04621900
         DS    9CL5           UP TO NINE F3 CCHHR'S FOR TOTAL=10   @L2A 04627200
.ICFTEST ANOP                                                      @L2A 04632500
         AIF   ('&FUNCT' EQ 'EXTEND').EXTND                             04638000
         AGO   .EXTBL                                                   04670000
.EXTND   ANOP                                                           04702000
&P.RSVD  DS    CL5            RESERVED                                  04734000
         ORG   VRFRSVD        DEFINE NEXT LABEL ON TOP OF VRFRSVD  @L2A 04744600
&P.FNPTR DS    CL5            CCHHR OF THE NTH OR NEW F3 DSCB      @L2A 04755200
.EXTBL   ANOP                                                           04766000
         AIF   ('&FUNCT' EQ '').DSECT3                                  04798000
         AGO   .NDSECT3                                                 04830000
.DSECT3  ANOP                                                           04862000
&P.EXTBL DSECT                DADSM EXTENT TABLE                        04894000
         AGO   .EXTS                                                    04926000
.NDSECT3 ANOP                                                           04958000
&P.EXTBL EQU   *              DADSM EXTENT TABLE                        04990000
.EXTS    ANOP                                                           05022000
         LCLC  &EDTENT        DEFINE MACRO VARIABLE FOR DADSM TABLE@L6A 05029400
.*                            ENTRIES PARM                              05036800
         LCLC  &EDTTYP        DEFINE MACRO VARIABLE FOR DADSM      @L6A 05044200
.*                            TABLE FORMAT TYPE PARM                    05051600
         LCLC  &OLDPRE        DEFINE MACRO VARIABLE FOR PREFIX OF  @L6A 05059000
.*                            OLD FORMAT EDT                            05066400
         LCLC  &NEWPRE        DEFINE MACRO VARIABLE FOR PREFIX OF  @L6A 05073800
.*                            NEW FORMAT EDT                            05081200
         AIF   ('&FUNCT' EQ '').DT0                                     05088600
&EDTTYP  SETC  'BOTH'         SET BOTH FORMATS OF MAPPING          @L6A 05096000
         AIF   ('&FUNCT' EQ 'ALLOCATE').DT12                       @L4C 05103400
         AIF   ('&FUNCT' EQ 'RELEASE').DT34                        @L4C 05110800
         AIF   ('&FUNCT' EQ 'SCRATCH').DT56                        @L2C 05118200
.DT78    ANOP                                                      @L3A 05125600
&EDTENT  SETC  '5'            SET DADSM TABLE ENTRIES TO 5         @L6A 05133000
&OLDPRE  SETC  'DT7'          SET OLD FORMAT TABLE PREFIX          @L6A 05140400
&NEWPRE  SETC  'DT8'          SET NEW FORMAT TABLE PREFIX          @L6A 05147800
         AGO   .DCLEDT        BRANCH TO INCLUDE DADSM TABLE MAPPING@L6A 05155200
.DT0     ANOP                                                      @L3A 05162600
&EDTENT  SETC  '0'            SET DADSM TABLE ENTRIES TO 5         @L6A 05170000
&EDTTYP  SETC  'OLD'          SET OLD FORMAT MAPPING ONLY          @L6A 05177400
&OLDPRE  SETC  'DT0'          SET OLD FORMAT TABLE PREFIX          @L6A 05184800
         AGO   .DCLEDT        BRANCH TO INCLUDE DADSM TABLE MAPPING@L6A 05192200
.DT12    ANOP                                                      @L3A 05199600
&OLDPRE  SETC  'DT1'          SET OLD FORMAT TABLE PREFIX          @L6A 05207000
&NEWPRE  SETC  'DT2'          SET NEW FORMAT TABLE PREFIX          @L6A 05214400
         AGO   .EXT123        BRANCH TO SET ENTRY                  @L6A 05221800
.DT34    ANOP                                                      @L3A 05229200
&OLDPRE  SETC  'DT3'          SET OLD FORMAT TABLE PREFIX          @L6A 05236600
&NEWPRE  SETC  'DT4'          SET NEW FORMAT TABLE PREFIX          @L6A 05244000
         AGO   .EXT123        BRANCH TO SET ENTRY                  @L6A 05251400
.DT56    ANOP                                                      @L3A 05258800
&OLDPRE  SETC  'DT5'          SET OLD FORMAT TABLE PREFIX          @L6A 05266200
&NEWPRE  SETC  'DT6'          SET NEW FORMAT TABLE PREFIX          @L6A 05273600
         AGO   .EXT133        BRANCH TO SET ENTRY                  @L6A 05281000
.EXT133  ANOP                                                      @L2A 05288400
&EDTENT  SETC  '133'          SET DADSM TABLE ENTRIES TO 133       @L6A 05295800
         AGO   .DCLEDT        BRANCH TO INCLUDE DADSM TABLE MAPPING@L6A 05303200
.EXT123  ANOP                                                      @L3A 05310600
&EDTENT  SETC  '123'          SET DADSM TABLE ENTRIES TO 123       @L6A 05318000
         AGO   .DCLEDT        BRANCH TO INCLUDE DADSM TABLE MAPPING@L6A 05325400
.DCLEDT  ANOP                                                      @L2A 05332800
         ICVEDT01 ENTRY=&EDTENT,DT1PRE=&OLDPRE,DSECT=NO                 05340200
         ICVEDT02 ENTRY=&EDTENT,DT2PRE=&NEWPRE,DSECT=NO                 05347600
         AIF   ('&FUNCT' EQ '').DSECT4                                  05355000
&SYSECT  DSECT                          RESUME DSECT                    05362400
         AGO   .FINISH                                                  05374000
.DSECT4  ANOP                                                      @L2A 05416600
&P.DSND  DSECT                NEW DATA SET NAME                         05459200
.DSN     ANOP                                                           05502000
&P.NDSN  DS    CL44           NEW DATA SET NAME FOR RENAME              05534000
         SPACE 1                                                        05566000
         AGO   .FINISH                                             @L3A 05630400
.FINISH  ANOP                                                           05655600
&P.DAEND EQU   *                                                        05658800
         MEND                                                           05662000
**********************************************************************/ 05700000
*%IECVRF: ;                                                             05750000
*%IF VRFDA_BASE = ' ' %THEN %VRFDA_BASE = 'BASED(VRFPTR)';    /* @L6A*/ 05775000
*                                                                       05800000
*/********************************************************************/ 05850000
*/*      VRF - VTOC RECORDING FACILITY DATA AREA EXPANSION           */ 05900000
*/********************************************************************/ 05950000
*                                                                       06000000
* DCL 1 VRFDA VRFDA_BASE BDY(WORD), /* VTOC INDEX MAP            @L6C*/ 06050000
*       2 VRFID      CHAR(4),      /* VRF ID IN EBCDIC 'VRF '        */ 06100000
*       2 VRFLENG  FIXED(15),      /* LENGTH OF VRF DATA             */ 06150000
*       2 VRFFCODE    BIT(8),      /* FUNCTION CODE                  */ 06200000
*       2 VRFFLAG     BIT(8),      /* VRF FLAG BYTE                  */ 06300000
*        3 VRFNEXTD   BIT(1),      /* EXTEND ON NEW VOLUME           */ 06350000
*        3 VRFDSNEX   BIT(1),      /* ON = VRFNDSN EXISTS AT OFFSET     06450000
*                                          VRFOFEXT                     06492800
*                                     OFF= VRFEXTBL EXISTS AT OFFSET    06535600
*                                          VRFOFEXT                  */ 06578400
*        3 VRFISAME   BIT(1),      /* ON = ISAM ALLOCATE WITH AN        06621200
*                                          EXISTING F1 DSCB.            06629700
*                                     OFF= NOT ISAM ALLOCATE WITH AN    06638200
*                                          EXISTING F1 DSCB.         */ 06646700
*        3 VRFRACF    BIT(1),      /* ON = RACDEF OK, F1 NOT WRITTEN    06651000
                                      OFF= RACDEF W/ F1 WRITTEN  @01A*/ 06655300
*        3 *          BIT(4),      /* RESERVED                   @01C*/ 06659600
*       2 VRFOFEXT FIXED(15),      /* OFFSET TO VRFEXTBL OR VRFNDSN     06664000
*                                     IN VRF DATA                    */ 06706800
*       2 VRFLEV    FIXED(8),      /* LEVEL NUMBER               @L6A*/ 06750000
*       2 VRFPREXT  FIXED(8),      /* NUMBER OF PRIMARY EXTENTS      */ 06800000
*       2 VRFDSN    CHAR(44),      /* DATA SET NAME                  */ 06850000
*       2 VRFF1PTR   CHAR(5),      /* CCHHR OF F1 DSCB               */ 06900000
*       2 VRFF2PTR   CHAR(5),      /* CCHHR OF F2 DSCB (ISAM DS ONLY)*/ 06950000
*       2 VRFHEND    CHAR(0);      /* END OF VRF DATA HEADER         */ 07000000
*                                                                       07022500
* DCL VRFF3PTR(*) CHAR(5) BDY(BYTE) DEFINED(VRFHEND); /*CCHHR OF F3     07045000
*                                     DSCB(S) */                        07067500
* DCL VRFNDSN CHAR(44) BDY(BYTE)   /* NEW DATA SET NAME              */ 07090000
*     BASED(ADDR(VRFDA)+VRFOFEXT); /*                                */ 07130000
*                                                                       07170000
*/********************************************************************/ 07193700
*/* EXTENT DESCRIPTOR TABLES.                                        */ 07217400
*/*   THE FOLLOWING STRUCTURE MAPS TWO EXTENT DESCRIPTOR TABLES.     */ 07241100
*/*   MACRO ICVEDT01 IS USED TO MAP AN EXTENT DESCRIPTOR TABLE IN    */ 07264800
*/*     2-BYTE RTA FORMAT.                                           */ 07288500
*/*   MACRO ICVEDT02 IS USED TO MAP AN EXTENT DESCRIPTOR TABLE IN    */ 07312200
*/*     4-BYTE RTA FORMAT.                                           */ 07335900
*/********************************************************************/ 07359600
* DCL 1 VRFEDTS  CHAR(LENGTH(DT1EDT01)+ /* EXTENT DESCRIPTOR     @L6A*/ 07383300
*                     LENGTH(DT2EDT02)) /* TABLES (EDTS)         @L6A*/ 07407000
*                BDY(BYTE)              /* TABLE                 @L6A*/ 07430700
*                BASED(ADDR(VRFDA)+VRFOFEXT),                           07454400
*       2 VRFDT1 CHAR(LENGTH(DT1EDT01)),/* 2-BTYE RTA FORMAT EDT @L6A*/ 07478100
*         3 VRFEXTBL CHAR(LENGTH(DT1EDT01)),/* SYNONYM           @L6A*/ 07501800
*       2 VRFDT2 CHAR(LENGTH(DT2EDT02));/* 4-BTYE RTA FORMAT EDT @L6A*/ 07525500
*                                                                       07550000
* DCL VRFALL    BIT(8) CONSTANT('01'X); /* DADSM ALLOCATE            */ 07600000
* DCL VRFEXT    BIT(8) CONSTANT('02'X); /* DADSM EXTEND              */ 07650000
* DCL VRFSCR    BIT(8) CONSTANT('03'X); /* DADSM SCRATCH             */ 07700000
* DCL VRFPR     BIT(8) CONSTANT('04'X); /* DADSM PARTIAL RELEASE     */ 07750000
* DCL VRFREN    BIT(8) CONSTANT('05'X); /* DADSM RENAME              */ 07800000
* DCL VRFDEFRG  BIT(8) CONSTANT('06'X); /* DFDSS DEFRAG          @P2A*/ 07801000
* DCL VRFIXPSC  BIT(8) CONSTANT('F3'X); /* DADSM SCRATCH WITH HIGHER    07802000
                                LEVEL VTOC INDEX PRUNING         @L5A*/ 07804000
* DCL VRFIXPRE  BIT(8) CONSTANT('F5'X); /* DADSM RENAME WITH HIGHER     07806000
                                LEVEL VTOC INDEX PRUNING         @L5A*/ 07808000
* DCL VRFLEV00  FIXED(8) CONSTANT(00);  /* LEVEL NUMBER USED BY DADSM*/ 07810000
*                                       /* PRIOR TO $L6 SUPPORT.     */ 07820000
* DCL VRFLEVC   FIXED(8) CONSTANT(01);  /* LEVEL NUMBER USED BY DADSM*/ 07830000
*                                       /* AFTER $L6 SUPPORT.    @L6A*/ 07840000
*/********************************************************************/ 07850000
*/*  OLD AND NEW FORMAT EXTENT DESCRIPTOR TABLES.                @L6A*/ 07900000
*/********************************************************************/ 07950000
*    % EDT_TYPE = 'BOTH';                                   /*   @L6A*/ 08000000
*    % DT1_EDT_BASE = 'ADDR(VRFDT1)';                       /*   @L6A*/ 08050000
*    % DT1_EDT_ENTRY_BASE = 'ADDR(DT1SMTRY)';               /*   @L6A*/ 08066600
*    % INCLUDE SYSLIB(ICVEDT01);                            /*   @L6A*/ 08083200
*    % DT2_EDT_BASE = 'ADDR(VRFDT2)';                       /*   @L6A*/ 08100000
*    % DT2_EDT_ENTRY_BASE = 'ADDR(DT2ENTRIES)';             /*   @L6A*/ 08150000
*    % INCLUDE SYSLIB(ICVEDT02);                            /*   @L6A*/ 08200000
