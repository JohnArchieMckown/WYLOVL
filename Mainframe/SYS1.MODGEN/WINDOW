         MACRO                                                          00050000
&NAME    WINDOW &PARM,                 CR0 SAVE AREA           @G875PXUX00100000
               &TYPE=                  DEFAULT EMS/MFA WINDOW  @G875PXU 00150000
.*********************************************************************  00170600
.*                                                                   *  00191200
.*  COPYRIGHT                                                    @01C*  00211800
.*       5685-001                                                    *  00232400
.*       THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                 *  00253000
.*       (C) COPYRIGHT IBM CORP. 1977, 1989                          *  00273600
.*       LICENSED MATERIALS - PROPERTY OF IBM                        *  00294200
.*                                                                   *  00314800
.*  STATUS = JBB3313                                             @01C*  00335400
.*                                                                   *  00356000
.*********************************************************************  00376600
.*                                                                   *  00397200
.*  SYNTAX =                                                         *  00417800
.*           WINDOW  savearea,TYPE=(MFA|MSSF|CLOSE)                  *  00438400
.*                                                                   *  00459000
.*            where savearea is an RX-address or a register, and     *  00479600
.*            TYPE indicates which external interrupt mask bits      *  00500200
.*            are requested (defaults to EMS/MFA).  TYPE=CLOSE       *  00520800
.*            closes the window.                                     *  00541400
.*                                                                   *  00562000
.*********************************************************************  00582600
.*  CHANGE-ACTIVITY =                                                *  00603200
.*                                                                   *  00623800
.*  $01=OY27050  JBB3313  890417  PD16CU:  Encryption Asymmetric     *  00644400
.*                                         Feature Support           *  00665000
.*                                                                   *  00685600
.*********************************************************************  00706200
*        MACDATE = 04/03/89                                        @01C 00726800
         LCLC  &TEMP                                                    00750000
&TEMP    SETC  'IHB1&SYSNDX'           GENERATE UNIQUE LABEL            00850000
         AIF   (T'&NAME EQ 'O').NOLAB  IF NO LABEL, SKIP       @G875PXU 00950000
&NAME    DC    0H'0'                   BRANCH LABEL            @G875PXU 01000000
.NOLAB   ANOP                                                           01050000
         AIF   ('&PARM' EQ '').ERROR   CHECK IF OPERAND SPECIFIED       01100000
         AIF   ('&TYPE' NE '' AND                                      *01120000
               '&TYPE' NE 'MSSF' AND                                   *01140000
               '&TYPE' NE 'MFA' AND                                    *01153300
               '&TYPE' NE 'CLOSE').ERRTYP  IS &TYPE VALID?     @G860PXD 01166600
         AIF   ('&TYPE' EQ 'CLOSE').CLOSWIN  IS &TYPE CLOSE?   @G860PXT 01180000
         AIF   ('&PARM'(1,1) EQ '(').RXFORM1 CHECK IF RX FORM           01200000
         STCTL 0,0,&PARM               SAVE THE CURRENT C.R. 0 VALUE    01250000
         AGO   .COMMON                 PERFORM COMMON WINDOW OPERATION  01300000
.RXFORM1 ANOP                          RX FORM SPECIFIED                01350000
         STCTL 0,0,0&PARM              SAVE THE CURRENT C.R. 0 VALUE    01400000
.COMMON  ANOP                          COMMON WINDOW CODE               01450000
         STCTL 0,0,PSACR0              STORE C.R. 0 IN WORK AREA        01500000
         NC    PSACR0(4),PSACR0M1      Clear all external interrupt    -01540000
                                       mask bits                   @01C 01580000
         OC    PSACR0(4),&TEMP         Set mask for specified external -01620000
                                       interrupts                  @01C 01660000
         MVC   PSACR0SV(4),PSACR0      COPY NEW CR0 FOR PSA PROTECT     01700000
         LCTL  0,0,PSACR0              ESTABLISH INTERRUPT MASK@G875PXU 01750000
         STOSM PSACR0,X'01'            ENABLE EXT. INTERRUPTS  @G875PXU 01800000
         STNSM PSACR0,X'FE'            DISABLE EXT. INTERRUPTS @G875PXU 01850000
.CLOSWIN ANOP                                                  @G860PXT 01875000
         STCTL 0,0,PSACR0              STORE CR0 IN WORK AREA           01900000
         AIF   ('&PARM'(1,1) EQ '(').RXFORM2 CHECK IF RX FORM           01950000
         NC    &PARM.(4),PSACR0M2      Isolate the external interrupt  -01965300
                                       mask bits in the original CR0   -01980600
                                       value                       @01C 01995900
         NC    PSACR0(4),PSACR0M1      Clear all external interrupts   -02011200
                                       in the updated CR0 value    @01A 02026500
         OC    PSACR0(4),&PARM         Restore the external interrupt  -02041800
                                       mask bits to their original     -02057100
                                       values                      @01A 02072400
         MVC   PSACR0SV(4),PSACR0      COPY NEW CR0 FOR PSA PROTECT    -02087700
                                                                   @01C 02103000
         LCTL  0,0,PSACR0              Reload C.R. 0 with the updated  -02118300
                                       value                       @01C 02133600
         AGO   .END                    EXIT                             02150000
.ERROR   ANOP                          NO C.R. 0 SAVE AREA WAS SPEC     02200000
         MNOTE 8,'NO C.R. 0 SAVE AREA ADDRESS WAS SPECIFIED'            02250000
         AGO   .END2                   EXIT                             02300000
.ERRTYP  ANOP                          UNRECOGNIZED TYPE       @G875PXU 02350000
         MNOTE 8,'INVALID TYPE PARAMETER SPECIFIED'            @G875PXU 02400000
         AGO   .END2                   EXIT                    @G875PXU 02450000
.RXFORM2 ANOP                          RX FORM SPECIFIED                02500000
         NC    0(4,&PARM(1).),PSACR0M2 Isolate the external interrupt  -02515300
                                       mask bits in the original CR0   -02530600
                                       value                       @01C 02545900
         NC    PSACR0(4),PSACR0M1      Clear all external interrupts   -02561200
                                       in the updated CR0 value    @01A 02576500
         OC    PSACR0(4),0&PARM        Restore the external interrupt  -02591800
                                       mask bits to their original     -02607100
                                       values                      @01A 02622400
         MVC   PSACR0SV(4),PSACR0      COPY NEW CR0 FOR PSA PROTECT    -02637700
                                                                   @01C 02653000
         LCTL  0,0,PSACR0              Reload C.R. 0 with the updated  -02668300
                                       value                       @01C 02683600
.END     ANOP                          FINISHED                         02700000
         AIF   ('&TYPE' EQ 'CLOSE').END2  IF &TYPE IS CLOSE,   @G860PXT 02716600
*                                      DO NOT GEN CONSTANTS    @G860PXT 02733200
         B     *+8                     BRANCH AROUND CONSTANTS     @01C 02750000
         AIF   ('&TYPE' EQ 'MSSF').MSSFWIN                     @G875PXU 02800000
         AIF   ('&TYPE' EQ 'MFA').MFAXPND                      @G860PXD 02825000
.*                                     EMS/MFA WINDOW ONLY     @G875PXU 02850000
&TEMP    DC    X'0000C000'             ENABLE EMS/MFA MASK     @G875PXU 02900000
         AGO   .END2                                           @G875PXU 03050000
.MFAXPND ANOP                          MFA WINDOW ONLY         @G860PXD 03058300
&TEMP    DC    X'00008000'             ENABLE MFA MASK         @G860PXD 03074900
         AGO   .END2                                           @G860PXD 03091500
.MSSFWIN ANOP                          MSSF WINDOW ONLY        @G875PXU 03100000
&TEMP    DC    X'00000200'             ENABLE MSSF MASK        @G875PXU 03150000
.END2    ANOP                                                           03300000
         MEND                                                           03350000
