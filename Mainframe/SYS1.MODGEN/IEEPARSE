.********************************************************************** 00050000
.*                                                                    * 00100000
.*01* MACRO NAME = IEEPARSE                                           * 00150000
.*                                                                    * 00200000
.*01* DESCRIPTIVE NAME = PARSING MACRO (USED WITH IEEMB887,           * 00250000
.*                       THE GENERALIZED PARSING ROUTINE)             * 00300000
.*                                                                    * 00350000
.**01* PROPRIETARY STATEMENT=                                        *  00374000
.* **PROPRIETARY_STATEMENT********************************************  00398000
.*                                                                   *  00422000
.*                                                                   *  00446000
.*  LICENSED MATERIALS - PROPERTY OF IBM                             *  00470000
.*  THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      *  00494000
.*  5647-A01 (C) COPYRIGHT IBM CORP. 1984, 1999                      *  00518000
.*                                                                   *  00542000
.*  STATUS= JBB6607                                                  *  00566000
.*                                                                   *  00590000
.* **END_OF_PROPRIETARY_STATEMENT*************************************  00614000
.*                                                                    * 00638900
.*    EXTERNAL CLASSIFICATION: NONE                                   * 00642600
.*    END OF EXTERNAL CLASSIFICATION                                  * 00646300
.*                                                                    * 00650000
.*01* FUNCTION = USED TO GENERATE THE INPUT WHICH DESCRIBES THE       * 00700000
.*               SYNTAX THE PARSER IS TO ACCEPT.  ALSO                * 00750000
.*               DESCRIBES THE FLOW OF CONTROL THAT THE PARSER        * 00800000
.*               IS TO HONOR WHEN THE INPUT RECORD IS CORRECT,        * 00850000
.*               INCORRECT, OR HAS BEEN COMPLETELY SCANNED.           * 00900000
.*               ALSO PROVIDES A MEANS FOR THE CALLER OF THE          * 00950000
.*               PARSER TO INDICATE WHAT USER DATA SHOULD BE          * 01000000
.*               PASSED TO THE EXIT ROUTINE (ROUT).  THE MACRO        * 01050000
.*               SCANS THE INPUT PASSED AND BUILDS A PARAMETER        * 01100000
.*               LIST IN THE CALLER'S MODULE, DESCRIBING THE          * 01150000
.*               SYNTAX TO BE PARSED.                                 * 01200000
.*                                                                    * 01250000
.*02*   OPERATION =                                                   * 01300000
.*                                                                    * 01350000
.*               CHECK FOR EXISTENCE OF ALTERNATE, SUCCESSOR,         * 01400000
.*               AND SECONDARY PARSE DESCRIPTION.  IN EACH            * 01450000
.*               CASE, EITHER THE ADDRESS OF THAT PARSE               * 01500000
.*               DESCRIPTION OR 0 IS PLACED IN THE APPROPRIATE        * 01550000
.*               VARIABLE.                                            * 01600000
.*                                                                    * 01650000
.*               CHECK FOR AN EXIT ROUTINE ADDRESS (ROUT).            * 01700000
.*               THEN, ASSUMING AN EXIT ROUTINE ADDRESS EXISTS,       * 01750000
.*               THE EXIT ROUTINE'S ADDRESSING MODE IS CHECKED.       * 01800000
.*               IF THIS VALUE IS OTHER THAN 24 OR 31 THEN            * 01850000
.*               AN MNOTE (ERROR MESSAGE) IS ISSUED.                  * 01900000
.*                                                                    * 01950000
.*               CHECK FOR EXISTENCE OF AN ADDRESS OF A USER'S        * 02000000
.*               PROCESSING TABLE TO BE PASSSED TO AN EXIT            * 02050000
.*               ROUTINE.  SHOULD THIS 'UTABLE' ADDRESS BE GIVEN      * 02100000
.*               WITHOUT AN EXIT ROUTINE ADDRESS, AN MNOTE IS         * 02150000
.*               ISSUED.  THEN, A CHECK IS MADE FOR THE INDEX TO      * 02200000
.*               BE USED WITHIN THIS TABLE.  THE DEFAULT VALUE        * 02250000
.*               FOR THIS INDEX (UINDEX) IS 0.  IF AN INDEX           * 02300000
.*               VALUE IS GIVEN WITHOUT A USER TABLE ADDRESS, AN      * 02350000
.*               MNOTE IS ISSUED.                                     * 02400000
.*                                                                    * 02450000
.*               CHECK FOR A USER'S TRT TABLE ADDRESS; THE            * 02500000
.*               PRESENCE OF AN ADDRESS MEANS THAT TRT                * 02550000
.*               PROCESSING IS REQUESTED.  AFTER THIS, CHECK          * 02600000
.*               TO SEE IF TYPE PROCESSING IS REQUESTED.  EVEN        * 02650000
.*               IF TRT PROCESSING WAS ALREADY REQUESTED, MAKE        * 02700000
.*               THIS CHECK IN CASE BOTH TYPE AND TRT PROCESSING      * 02750000
.*               WERE REQUESTED, RESULTING IN AN MNOTE STATING        * 02800000
.*               THAT THIS IS NOT ALLOWED.  IF JUST TYPE              * 02850000
.*               PROCESSING IS SPECIFIED, THEN THE MACRO CHECKS       * 02900000
.*               WHICH TYPE(S) OF CHARACTERS ARE TO BE                * 02950000
.*               ACCEPTABLE, AND VERIFIES THAT ONLY THE ALLOWED       * 03000000
.*               VALUES FOR THIS VARIABLE HAVE BEEN SPECIFIED.        * 03050000
.*                                                                    * 03100000
.*               CHECK WHETHER THE FIRST CHARACTER OF THE WORD        * 03150000
.*               BEING SCANNED FOR MUST BE OF A SPECIFIC TYPE.        * 03200000
.*               IF SO, THE POSSIBLE TYPES ARE ALPHABETIC OR          * 03250000
.*               ALPHABETIC/NATIONAL (NATIONAL IS @,#,$).  ANY        * 03300000
.*               OTHER VALUES GIVEN WILL RESULT IN AN MNOTE.          * 03350000
.*                                                                    * 03400000
.*               CHECK WHETHER ANY OTHER CHARACTERS IN ADDITION       * 03450000
.*               TO THOSE DEFINED AS ACCEPTABLE ARE ALSO              * 03500000
.*               ACCEPTABLE.  THE CONSTRAINT HERE IS THAT ONLY        * 03550000
.*               UP TO 8 CHARACTERS MAY BE SPECIFIED.                 * 03600000
.*               SPECIFICATION OF MORE THAN 8 CHARACTERS WILL         * 03650000
.*               RESULT IN AN MNOTE BEING ISSUED.                     * 03700000
.*                                                                    * 03750000
.*               CHECK FOR SPECIFICATION OF CHARACTERS DEFINED        * 03800000
.*               AS VALID DELIMITERS.  AGAIN, ONLY UP TO 8            * 03850000
.*               CHARACTERS MAY BE SPECIFIED.  IF NONE ARE GIVEN      * 03900000
.*               THEN THE DEFAULT VALID DELIMITERS ARE A BLANK,       * 03950000
.*               A COMMA, AND A RIGHT PARENTHESIS FOR TYPE OR     @P2C* 03975000
.*               TRT PROCESSING, AND NOTHING (NO DELIMITERS       @P2A* 04000000
.*               NECESSARY) FOR KEY PROCESSING.                   @P2A* 04025000
.*                                                                    * 04050000
.*               CHECK TO SEE IF KEY PROCESSING IS SPECIFIED.         * 04100000
.*               IF SO, THE MACRO VERIFIES THAT NEITHER TRT NOR       * 04150000
.*               TYPE PROCESSING WAS ALREADY SPECIFIED.  OTHER        * 04200000
.*               CHECKS MADE ARE THAT THE KEYWORD GIVEN IS            * 04250000
.*               NOT LONGER THAN 255 CHARACTERS, AND THAT THE         * 04300000
.*               KEYWORD VALUE GIVEN IS NOT NULL.  IF THESE           * 04350000
.*               CHECKS ARE SUCCESSFUL, THEN THE KEYWORD AND          * 04400000
.*               ITS LENGTH ARE STORED.                               * 04450000
.*                                                                    * 04500000
.*               CHECK TO SEE IF KEY ABBREVIATIONS ARE ALLOWED.   @D1A* 04506200
.*               IF SO, THE MACRO VERIFIES THAT THIS MINIMUM      @D1A* 04512400
.*               ABBREVIATED KEYWORD LENGTH FALLS IN THE RANGE    @D1A* 04518600
.*               OF 1 TO THE LENGTH OF THE SPECIFIED KEYWORD.     @D1A* 04524800
.*               IF THE PARAMETER IS NOT WITHIN THIS RANGE, THEN  @D1A* 04531000
.*               AN MNOTE IS ISSUED.                              @D1A* 04537200
.*                                                                @D1A* 04543400
.*               CHECK FOR INFORMATION ON THE HANDLING OF BLANKS.     * 04550000
.*               IF THE VALUE IS OTHER THAN SKIP OR NOSKIP,           * 04600000
.*               THEN AN MNOTE IS ISSUED.  THE DEFAULT VALUE          * 04650000
.*               IS SKIP.  THE SAME CHECK IS MADE FOR THE             * 04700000
.*               HANDLING OF COMMAS, EXCEPT THAT THE DEFAULT          * 04750000
.*               VALUE FOR COMMAS IS NOSKIP.                          * 04800000
.*                                                                    * 04850000
.*               CHECK WHETHER TO READ THE NEXT INPUT RECORD,         * 04900000
.*               IF NECESSARY.  THE POSSIBLE VALUES ARE YES AND       * 04950000
.*               NO, YES BEING THE DEFAULT.  ANY OTHER VALUE          * 05000000
.*               WILL RESULT IN AN MNOTE.  THEN, CHECK IF IT IS       * 05050000
.*               ALWAYS NECESSARY TO CALL THE EXIT ROUTINE (ROUT),    * 05100000
.*               PROVIDED THAT AN EXIT ROUTINE ADDRESS IS GIVEN.      * 05150000
.*                                                                    * 05200000
.*               CHECK FOR THE PRESENCE OF A MINIMUM AND              * 05250000
.*               MAXIMUM LENGTH TO SCAN FOR A WORD.  THE DEFAULT      * 05300000
.*               VALUES ARE 1 FOR THE MINIMUM LENGTH AND 65535    @D2C* 05350000
.*               FOR THE MAXIMUM LENGTH.  NEITHER VALUE MAY BE        * 05400000
.*               OUT OF THIS RANGE OF 1 TO 65535. ALSO, THE       @D2C* 05450000
.*               MINIMUM LENGTH MAY NOT BE GREATER THAN THE           * 05500000
.*               MAXIMUM LENGTH.                                      * 05550000
.*                                                                    * 05600000
.* NOTE: IF 'TYPE' AND 'TRT' AND 'KEY' ARE ALL NULL,                  * 05650000
.*       THE PARAMETER LIST WILL INDICATE THAT THIS PARSE             * 05700000
.*       DESCRIPTION IS A 'ROUT ONLY' PARSE DESCRIPTION AND           * 05750000
.*       REQUIRES NOTHING TO SCAN.  IF A ROUT IS PRESENT THEN         * 05800000
.*       IT WILL BE INVOKED.  IF IT IS NOT, AN MNOTE WILL BE          * 05850000
.*       ISSUED.                                                      * 05900000
.*                                                                    * 05950000
.*01* INVOCATION =                                                    * 06000000
.*                                                                    * 06050000
.*    THE FOLLOWING KEYWORDS CAN BE CODED IN THE PARSE MACRO          * 06100000
.*                                                                    * 06150000
.*      KEY='DATA' - INDICATES THAT THE VALUE SPECIFIED SHOULD        * 06200000
.*         BE COMPARED AGAINST THE NEXT STRING IN THE INPUT RECORD.   * 06250000
.*      ABBRLEN=NNN - INDICATES THAT THE VALUE SPECIFIED VIA THE  @D1A* 06262500
.*         'KEY' PARAMETER MAY BE ABBREVIATED WITH A LENGTH DOWN  @D1A* 06275000
.*         TO A VALUE SPECIFIED VIA THIS PARAMETER.               @D1A* 06287500
.*      MINLEN=NN,MAXLEN=NN - INDICATES THAT THE NEXT STRING          * 06300000
.*         EXPECTED IS A VARIABLE WITH THE MINIMUM AND MAXIMUM        * 06350000
.*         LENGTH SPECIFIED.                                          * 06400000
.*         IF KEY= IS NOT CODED THEN IF EITHER MINLEN OR MAXLEN IS    * 06450000
.*         NOT SPECIFIED THEY DEFAULT TO MINLEN=1 AND             @D2C* 06483300
.*         MAXLEN=65535.                                          @D2A* 06516600
.*      TYPE=X - USED IN CONJUNCTION WITH MINLEN AND MAXLEN. X =      * 06550000
.*         A - INDICATES STRING MUST BE ALPHABETIC OR NATIONAL    @P3C* 06600000
.*         N - INDICATES STRING MUST BE NUMERIC                       * 06650000
.*         H - INDICATES STRING MUST BE HEXADECIMAL                   * 06700000
.*         AN - INDICATES STRING MUST BE ALPHANUMERIC OR NATIONAL @P3C* 06750000
.*      TRT=TRT-LABEL - USED IN CONJUNCTION WITH MINLEN AND MAXLEN,   * 06800000
.*         INDICATES TO THE PARSER (IEEMB887) THAT A 256              * 06850000
.*         CHARACTER TRANSLATE AND TEST TABLE HAS BEEN SUPPLIED       * 06900000
.*         BY THE INVOKER (TO THE PARSER) AT TRT-LABEL.               * 06950000
.*      FIRST=X - INDICATES THAT THE FIRST CHARACTER IN THE STRING    * 07000000
.*                MUST BE OF A SPECIFIC TYPE. X=                      * 07050000
.*         A  - ALPHABETIC                                            * 07100000
.*         A@ - ALPHABETIC OR NATIONAL                                * 07150000
.*      ALSO='CCCCCCCC' - INDICATES TO THE PARSER THAT IN             * 07200000
.*         ADDITION TO THE CHARACTERS INCLUDED IN THE SET DEFINED     * 07250000
.*         BY THE TYPE PARAMETER OR DEFINED AS VALID BY A TRT         * 07300000
.*         TABLE (IF TRT IS SPECIFIED), THE SPECIFIED CHARACTERS      * 07350000
.*         ARE ALSO TO BE CONSIDERED VALID.  UP TO 8 CHARACTERS       * 07400000
.*         MAY BE SPECIFIED.                                          * 07450000
.*      DELIM='CCCCCCCC' - INDICATES TO THE PARSER THAT ANY OF        * 07500000
.*         THE SPECIFIED CHARACTERS ARE TO BE ACCEPTED AS VALID       * 07550000
.*         DELIMITERS FOR TYPE, TRT OR KEY PROCESSING. THE        @P2C* 07580000
.*         DEFAULT DELIMITERS ARE BLANK, COMMA, AND RIGHT         @P2A* 07610000
.*         PARENTHESIS FOR TYPE OR TRT PROCESSING, AND NOTHING    @P2A* 07640000
.*         (NO DELIMITERS NECESSARY) FOR KEY PROCESSING.          @P2A* 07670000
.*         UP TO 8 DELIMITERS MAY BE SPECIFIED.                       * 07700000
.*      SUCC=NAME - INDICATES THE DEFAULT CHOICE FOR THE NEXT         * 07750000
.*         PARSE DESCRIPTION TO BE PROCESSED AFTER THE CHECKS         * 07800000
.*         SPECIFIED BY THE SYNTAX-DESCRIBING PARAMETERS HAVE         * 07850000
.*         BEEN SUCCESSFUL.  NAME IS THE LABEL ON THE PARSE           * 07900000
.*         DESCRIPTION.                                               * 07950000
.*      SECSUCC=NAME - INDICATES AN ALTERNATIVE CHOICE FOR THE NEXT   * 08000000
.*         PARSE DESCRIPTION TO BE PROCESSED AFTER THE CHECKS         * 08050000
.*         SPECIFIED BY THE SYNTAX-DESCRIBING PARAMETERS HAVE         * 08100000
.*         BEEN SUCCESSFUL.  THIS IS DONE IF THE 'SCLSECS' BIT        * 08150000
.*         IS ON.  NAME IS THE LABEL ON THE PARSE DESCRIPTION.        * 08200000
.*      ALT=NAME - INDICATES THE NEXT PARSE DESCRIPTION THAT          * 08250000
.*         SHOULD BE PROCESSED IF THE INPUT STRING DOES NOT           * 08300000
.*         SATISFY THIS SCAN.  IT IS ALSO POSSIBLE THAT ALT           * 08350000
.*         MAY BE USED AS AN ALTERNATIVE CHOICE FOR THE NEXT          * 08400000
.*         PARSE DESCRIPTION TO BE PROCESSED AFTER THE CHECKS         * 08450000
.*         SPECIFIED BY THE SYNTAX-DESCRIBING PARAMETERS HAVE         * 08500000
.*         BEEN SUCCESSFUL.  THIS OCCURS WHEN THE PARSE PARA-         * 08550000
.*         METER LIST INDICATES THAT THERE IS NO SUCCESSOR            * 08600000
.*         PARSE DESCRIPTION TO BE PROCESSED (SCLNOSUC=1).            * 08650000
.*         NAME IS THE LABEL ON THE PARSE DESCRIPTION.                * 08700000
.*      ROUT=NAME - INDICATES THE NAME OF THE ROUTINE TO BE CALLED    * 08750000
.*         WHEN THE INDICATED KEY IS FOUND.  ROUT MAY ALSO BE         * 08800000
.*         USED WITHOUT SPECIFYING ANY SYNTAX-DESCRIBING              * 08850000
.*         PARAMETERS.                                                * 08900000
.*      AMODE=24 OR 31 - INDICATES THE ADDRESSING MODE OF THE         * 08950000
.*         EXIT ROUTINE.  AMODE=31 IS THE DEFAULT.                    * 09000000
.*      UTABLE=USER-TABLE-LABEL - INDICATES THAT THE ROUT EXIT        * 09050000
.*         ROUTINE WILL BE GIVEN CONTROL WITH THE ADDRESS OF          * 09100000
.*         THE USER'S (THE PARSER'S INVOKER'S) PROCESSING TABLE       * 09150000
.*         PROVIDED IN SCLUTAB.                                       * 09200000
.*      UINDEX=NNNNN - INDICATES THAT THE ROUT EXIT ROUTINE WILL      * 09250000
.*         BE GIVEN CONTROL WITH THE VALUE OF NNNNN PROVIDED          * 09300000
.*         IN SCLUINDX.  THE INDEX MUST BE A VALUE IN THE RANGE       * 09350000
.*         0-65535.  THE DEFAULT IS 0.                                * 09400000
.*      IO=YES - INDICATES THAT AT THE COMPLETION OF SCANNING THE     * 09450000
.*         CURRENT INPUT BUFFER, THE PARSER SHOULD CALL THE I/O       * 09500000
.*         EXIT ROUTINE POINTED TO BY SCLIORTN.  THIS IS THE          * 09550000
.*         DEFAULT.  (IT IS POSSIBLE TO INDICATE THAT THE I/O         * 09600000
.*         EXIT ROUTINE SHOULD NOT BE CALLED BY SPECIFYING IO=NO).    * 09650000
.*      CALLRT=YES - INDICATES THAT THE ROUT IS ALWAYS TO BE          * 09700000
.*         CALLED.  THIS PARAMETER OVERRIDES THE SCLNORT FLAG         * 09750000
.*         IN THE SCL.                                                * 09800000
.*      BLANKS=SKIP - INDICATES THAT BLANKS ARE INSIGNIFICANT WHEN    * 09850000
.*         PARSING  SO PARSER SHOULD  SKIP OVER THEM WHEN             * 09900000
.*         CHECKING THE INPUT RECORD.  THIS IS THE DEFAULT.           * 09950000
.*         (IT IS POSSIBLE TO INDICATE THAT BLANKS SHOULD NOT         * 10000000
.*         BE SKIPPED OVER BY SPECIFYING BLANKS=NOSKIP.               * 10050000
.*      COMMA=NOSKIP - INDICATES THAT COMMAS ARE SIGNIFICANT WHEN     * 10100000
.*         PARSING  SO PARSER SHOULD NOT SKIP OVER ANY COMMAS         * 10150000
.*         WHEN CHECKING THE INPUT RECORD.  THIS IS THE DEFAULT.      * 10200000
.*         (IT IS POSSIBLE TO INDICATE THAT COMMAS SHOULD             * 10250000
.*         BE SKIPPED OVER BY SPECIFYING COMMAS=SKIP).                * 10300000
.*                                                                    * 10350000
.*01* CHANGE ACTIVITY =                                               * 10400000
.* $L0=MRRPARSR, JBB2125, 830406, PD2G: GENERALIZED PARSER            * 10450000
.* $D1=DCR22,    JBB2125, 830406, PDF9: KEY ABBREVIATION              * 10460000
.* $D2=DCR31,    JBB2125, 830608, PDF9: MULTIPLE RECORDS              * 10470000
.* $P1=PAJ0031   JBB2125, 830608, PDF9: GENERALIZED PARSER            * 10480000
.* $P2=PAJ0044,  JBB2125, 830608, PDF9: GENERALIZED PARSER            * 10490000
.* $P3=PEO2630,  HBB4410, 891026, PDHH: NATIONAL CHARACTERS           * 10495000
.* $P4=PWA0053,  JBB6607, 980128, PDZH: CHANGED DISTLIB TO AMODGEN    * 10497500
.*                                                                    * 10500000
.*01* MESSAGES=                                                       * 10550000
.*        THE IEEPARSE MACRO WILL ISSUE THE FOLLOWING SET OF          * 10600000
.*        MNOTE'S UPON THE DETECTION OF USER ERRORS:                  * 10650000
.*                                                                    * 10700000
.*02*   WARNINGS = (SEVERITY 4)                                       * 10750000
.*                                                                    * 10800000
.*    'UTABLE'  VALUE SPECIFIED, BUT NO 'ROUT'    SPECIFIED           * 10900000
.*    'UINDEX'  VALUE SPECIFIED, BUT NO 'UTABLE'  SPECIFIED           * 10950000
.*    'ABBRLEN'  VALUE SPECIFIED, BUT NO 'KEY'  SPECIFIED         @D1A* 10975000
.*                                                                    * 11000000
.*02*   ERRORS = (SEVERITY 8)                                         * 11050000
.*                                                                    * 11100000
.*    'MINLEN' CANNOT BE GREATER THAN 'MAXLEN'                        * 11150000
.*                                                                    * 11200000
.*    'UINDEX'  VALUE IS OUT OF RANGE - RANGE IS 0-65535              * 11250000
.*    'MINLEN'  VALUE IS OUT OF RANGE - RANGE IS 1-65535          @D2C* 11300000
.*    'MAXLEN'  VALUE IS OUT OF RANGE - RANGE IS 1-65535          @D2C* 11350000
.*    'ABBRLEN'  VALUE IS OUT OF RANGE - RANGE IS 1-KEYLENGTH     @D1A* 11400000
.*                                                                    * 11450000
.*    MUTUALLY EXCLUSIVE KEYWORDS SPECIFIED - KEY/TYPE/TRT            * 11500000
.*                                                                    * 11550000
.*    ROUT MUST BE SPECIFIED WHEN KEY/TYPE/TRT ARE OMITTED            * 11600000
.*                                                                    * 11650000
.*    'AMODE'  SPECIFIED WITH AN INVALID OPERAND VALUE                * 11700000
.*    'TYPE'   SPECIFIED WITH AN INVALID OPERAND VALUE                * 11750000
.*    'FIRST'  SPECIFIED WITH AN INVALID OPERAND VALUE                * 11800000
.*    'BLANKS' SPECIFIED WITH AN INVALID OPERAND VALUE                * 11850000
.*    'COMMA'  SPECIFIED WITH AN INVALID OPERAND VALUE                * 11900000
.*    'IO'     SPECIFIED WITH AN INVALID OPERAND VALUE                * 11950000
.*    'CALLRT' SPECIFIED WITH AN INVALID OPERAND VALUE                * 12000000
.*                                                                    * 12050000
.*    'KEY'   VALUE IS TOO LONG - MAXIMUM OF 255 CHARACTERS           * 12100000
.*    'ALSO'  VALUE IS TOO LONG - MAXIMUM OF 8 CHARACTERS             * 12150000
.*    'DELIM' VALUE IS TOO LONG - MAXIMUM OF 8 CHARACTERS             * 12200000
.*                                                                    * 12250000
.********************************************************************** 12300000
         MACRO                                                          12350000
&LABEL   IEEPARSE &KEY=,&ALT=0,&SUCC=0,&BLANKS=SKIP,&COMMA=NOSKIP,     X12400000
               &MINLEN=1,&MAXLEN=65535,&TYPE=,&TRT=0,&FIRST=,          X12450000
               &DELIM=,&ROUT=0,&AMODE=31,&SECSUCC=0,&ALSO=,            X12500000
               &IO=YES,&CALLRT=NO,&UTABLE=0,&UINDEX=0,&ABBRLEN=,       X12533300
               &USRDATA=0                                          @P1C 12566600
*        MACDATE = 6/8/83                                               12600000
.********************************************************************** 12650000
.*                                                                    * 12700000
.*       LOCAL SET SYMBOL DEFINITIONS                                 * 12750000
.*                                                                    * 12800000
.********************************************************************** 12850000
         LCLA  &KEYLEN,&ALSOLEN,&DLMLEN,&OPCOUNT,&INDEX,&STRLEN         12883300
         LCLA  &RETNPT,&ABRSIZE                                   @D1C  12916600
         LCLB  &SW1,&SW2,&SW3,&SW4,&SW5                                 12950000
         LCLC  &BITFLAG,&MODE,&KEYPARM,&ALSOPRM,&DLMPARM,&STRING        13000000
.********************************************************************** 13050000
.*                                                                    * 13100000
.*       'KEY', 'TYPE', AND 'TRT' ARE MUTUALLY EXCLUSIVE.             * 13150000
.*       VERIFY THAT NO MORE THAN ONE IS PRESENT.  IF NONE IS         * 13200000
.*       PRESENT, ENSURE THAT 'ROUT' IS PRESENT.                      * 13250000
.*                                                                    * 13300000
.********************************************************************** 13350000
         AIF   ('&KEY' EQ '').NOKEY    'KEY' NOT SPECIFIED              13400000
&OPCOUNT SETA  &OPCOUNT+1              COUNT OPERAND TOTAL              13450000
.NOKEY   ANOP                                                           13500000
         AIF   ('&TYPE' EQ '').NOTYPE  'TYPE' NOT SPECIFIED             13550000
&OPCOUNT SETA  &OPCOUNT+1              COUNT OPERAND TOTAL              13600000
.NOTYPE  ANOP                                                           13650000
         AIF   ('&TRT' EQ '0').NOTRT   'TRT' NOT SPECIFIED              13700000
&OPCOUNT SETA  &OPCOUNT+1              COUNT OPERAND TOTAL              13750000
.NOTRT   ANOP                                                           13800000
         AIF   (&OPCOUNT LE 1).COUNTOK  CORRECT NUMBER OF OPERANDS?     13850000
         MNOTE 8,'MUTUALLY EXCLUSIVE KEYWORDS SPECIFIED - KEY/TYPE/TRT' 13900000
.COUNTOK ANOP                                                           13950000
         AIF   (&OPCOUNT NE 0 OR '&ROUT' NE '0').ROUTOK                X14000000
                                       IS ROUT SPECIFIED WHEN          X14050000
                                       KEY/TYPE/TRT ARE OMITTED?        14100000
         MNOTE 8,'ROUT MUST BE SPECIFIED WHEN KEY/TYPE/TRT ARE OMITTED' 14150000
.********************************************************************** 14200000
.*                                                                    * 14250000
.*       SET 'ALT', 'SUCC', AND 'SECSUCC' ADDRESSES IF SUPPLIED,      * 14300000
.*       OTHERWISE ZERO.                                              * 14350000
.*                                                                    * 14400000
.********************************************************************** 14450000
.ROUTOK  ANOP                                                           14500000
&LABEL   DS    0F                                                       14550000
         DC    A(&ALT)                 ADDR OF ALTERNATE PARSE DESC     14600000
         DC    A(&SUCC)                ADDR OF SUCCESSOR PARSE DESC     14650000
         DC    A(&SECSUCC)             ADDR OF SECONDARY SUCCESSOR DESC 14700000
.********************************************************************** 14750000
.*                                                                    * 14800000
.*       SET 'ROUT' ADDRESS IF SUPPLIED.  TURN ON HIGH-ORDER BIT      * 14850000
.*       IF AMODE IS 31.                                              * 14900000
.*                                                                    * 14950000
.********************************************************************** 15000000
         AIF   ('&ROUT' EQ '0').AMODE24  'ROUT' PARM SPECIFIED?         15050000
         AIF   (&AMODE EQ 31).AMODE31  31-BIT AMODE?                    15100000
         AIF   (&AMODE EQ 24).AMODE24  24-BIT AMODE?                    15150000
         MNOTE 8,'''AMODE'' SPECIFIED WITH AN INVALID OPERAND VALUE'    15200000
.AMODE31 ANOP                                                           15250000
&MODE    SETC  '+X''80000000'''        SET HIGH-ORDER BIT FOR 31-BIT    15300000
.AMODE24 ANOP                                                           15350000
         DC    A(&ROUT.&MODE)          ADDR OF EXIT ROUTINE             15400000
.********************************************************************** 15450000
.*                                                                    * 15500000
.*       SET 'USRDATA' INFORMATION                                    * 15550000
.*                                                                    * 15600000
.********************************************************************** 15650000
         DC    A(&USRDATA)             USER DATA PASSED TO EXIT ROUTINE 15700000
.********************************************************************** 15750000
.*                                                                    * 15800000
.*       SET 'TRT' ADDRESS IF SUPPLIED, OTHERWISE ZERO                * 15850000
.*                                                                    * 15900000
.********************************************************************** 15950000
         DC    3A(0)                   RESERVED                         16000000
         DC    A(&TRT)                 ADDR OF TRANSLATE TABLE          16050000
.********************************************************************** 16100000
.*                                                                    * 16150000
.*       'UTABLE' IS PERMITTED ONLY IF 'ROUT' IS SPECIFIED.           * 16200000
.*       SET 'UTABLE' ADDRESS IF SUPPLIED.  SET TO ZERO IF            * 16250000
.*       AN ERROR EXISTS.                                             * 16300000
.*                                                                    * 16350000
.********************************************************************** 16400000
         AIF   ('&UTABLE' EQ '0').SETTBL  'UTABLE' PARM SPECIFIED?      16450000
         AIF   ('&ROUT' NE '0').SETTBL  'ROUT' SPECIFIED, TOO?          16500000
         MNOTE 4,'''UTABLE'' VALUE SPECIFIED, BUT NO ''ROUT'' SPECIFIEDX16550000
               '                                                        16600000
.SETTBL  ANOP                                                           16650000
         DC    A(&UTABLE)              ADDR OF USER PROCESSING TABLE    16700000
.********************************************************************** 16750000
.*                                                                    * 16800000
.*       'UINDEX' IS PERMITTED ONLY IF 'UTABLE' IS SPECIFIED.         * 16850000
.*       VERIFY 'UINDEX' IS BETWEEN 0 AND 65535.                      * 16900000
.*                                                                    * 16950000
.********************************************************************** 17000000
         AIF   ('&UINDEX' EQ '0').SETNDX  'UINDEX' PARM SPECIFIED?      17050000
         AIF   ('&UTABLE' NE '0').NDXRNG  'UTABLE' SPECIFIED, TOO?      17100000
         MNOTE 4,'''UINDEX'' VALUE SPECIFIED, BUT NO ''UTABLE'' SPECIFIX17150000
               ED'                                                      17200000
.NDXRNG  ANOP                                                           17250000
         AIF   (T'&UINDEX NE 'N').SETNDX  IS IT A SELF-DEFINING TERM?   17275000
         AIF   (&UINDEX GE 0 AND &UINDEX LE 65535).SETNDX  RANGE OK?    17300000
         MNOTE 8,'''UINDEX'' VALUE IS OUT OF RANGE - RANGE IS 0-65535'  17350000
.SETNDX  ANOP                                                           17400000
         DC    AL2(&UINDEX)            USER INDEX                       17450000
.********************************************************************** 17500000
.*                                                                    * 17550000
.*       BUILD FIRST FLAG BYTE                                        * 17600000
.*                                                                    * 17650000
.********************************************************************** 17700000
         AIF   ('&TYPE' EQ '').TYPEOK  'TYPE' PARM SPECIFIED?           17750000
&SW1     SETB  ('&TYPE' EQ 'A' OR '&TYPE' EQ 'AN')  ALPHABETIC CHARS    17800000
&SW2     SETB  ('&TYPE' EQ 'N' OR '&TYPE' EQ 'AN')  NUMERIC CHARS       17850000
&SW3     SETB  ('&TYPE' EQ 'H')        HEXADECIMAL CHARS ONLY           17900000
         AIF   (&SW1 OR &SW2 OR &SW3).TYPEOK  LEGAL VALUE?              17950000
         MNOTE 8,'''TYPE'' SPECIFIED WITH AN INVALID OPERAND VALUE'     18000000
.TYPEOK  ANOP                                                           18050000
         AIF   ('&FIRST' EQ '').FIRSTOK  'FIRST' PARM SPECIFIED?        18100000
&SW4     SETB  ('&FIRST' EQ 'A')       ALPHABETIC CHARS ONLY            18150000
&SW5     SETB  ('&FIRST' EQ 'A@')      ALPHABETIC AND NATIONAL CHARS    18200000
         AIF   (&SW4 OR &SW5).FIRSTOK  LEGAL VALUE?                     18250000
         MNOTE 8,'''FIRST'' SPECIFIED WITH AN INVALID OPERAND VALUE'    18300000
.FIRSTOK ANOP                                                           18350000
&BITFLAG SETC  '&SW1.&SW2.&SW3.&SW4.&SW5.000'                           18400000
         DC    B'&BITFLAG'             TYPE/FIRST FLAGS                 18450000
.********************************************************************** 18500000
.*                                                                    * 18550000
.*       BUILD SECOND FLAG BYTE                                       * 18600000
.*                                                                    * 18650000
.********************************************************************** 18700000
         AIF   ('&BLANKS' EQ 'SKIP').SKIPB  SET UP TO SKIP BLANKS       18750000
         AIF   ('&BLANKS' EQ 'NOSKIP').NOSKIPB  SET UP TO NOT SKIP      18800000
         MNOTE 8,'''BLANKS'' SPECIFIED WITH AN INVALID OPERAND VALUE'   18850000
.SKIPB   ANOP                                                           18900000
&SW1     SETB  0                                                        18950000
         AGO   .SKIPOK                                                  19000000
.NOSKIPB ANOP                                                           19050000
&SW1     SETB  1                                                        19100000
.SKIPOK  ANOP                                                           19150000
         AIF   ('&IO' EQ 'YES').YESIO  SET UP TO CALL I/O EXIT          19200000
         AIF   ('&IO' EQ 'NO').NOIO    SET UP NOT TO CALL I/O EXIT      19250000
         MNOTE 8,'''IO'' SPECIFIED WITH AN INVALID OPERAND VALUE'       19300000
.YESIO   ANOP                                                           19350000
&SW2     SETB  1                                                        19400000
         AGO   .IOOK                                                    19450000
.NOIO    ANOP                                                           19500000
&SW2     SETB  0                                                        19550000
.IOOK    ANOP                                                           19600000
         AIF   ('&COMMA' EQ 'SKIP').SKIPC  SET UP TO SKIP COMMAS        19650000
         AIF   ('&COMMA' EQ 'NOSKIP').NOSKIPC  SET UP TO NOT SKIP       19700000
         MNOTE 8,'''COMMA'' SPECIFIED WITH AN INVALID OPERAND VALUE'    19750000
.NOSKIPC ANOP                                                           19800000
&SW3     SETB  1                                                        19850000
         AGO   .COMMAOK                                                 19900000
.SKIPC   ANOP                                                           19950000
&SW3     SETB  0                                                        20000000
.COMMAOK ANOP                                                           20050000
         AIF   ('&ROUT' EQ '0').NOCALL  MAY ONLY BE SPECIFIED W/'ROUT'  20100000
         AIF   ('&CALLRT' EQ 'YES').YESCALL  SET UP TO CALL EXIT        20150000
         AIF   ('&CALLRT' EQ 'NO').NOCALL  SET UP NOT TO CALL EXIT      20200000
         MNOTE 8,'''CALLRT'' SPECIFIED WITH AN INVALID OPERAND VALUE'   20250000
.NOCALL  ANOP                                                           20300000
&SW4     SETB  0                                                        20350000
         AGO   .CALLOK                                                  20400000
.YESCALL ANOP                                                           20450000
&SW4     SETB  1                                                        20500000
.CALLOK  ANOP                                                           20550000
&SW5     SETB  ('&KEY' EQ '' AND '&TYPE' EQ '' AND '&TRT' EQ '0')      X20600000
                                       'ROUT ONLY' PROCESSING IF 'KEY',X20650000
                                       'TYPE', AND 'TRT' NOT SPECIFIED  20700000
&BITFLAG SETC  '&SW1.&SW2.&SW3.&SW4.&SW5.000'                           20750000
         DC    B'&BITFLAG'             BLANKS/IO/COMMA/ROUT FLAGS       20800000
.********************************************************************** 20900000
.*                                                                    * 20950000
.*       VERIFY AND SET UP MINIMUM AND MAXIMUM STRING LENGTHS         * 21000000
.*                                                                    * 21050000
.********************************************************************** 21100000
         AIF   (T'&MAXLEN NE 'N').SETMAX  IS IT A SELF-DEFINING TERM?   21125000
         AIF   (&MAXLEN GE 1 AND &MAXLEN LE 65535).SETMAX MAX OK? @D2C  21156200
         MNOTE 8,'''MAXLEN'' VALUE IS OUT OF RANGE - RANGE IS 1-65535' X21187400
                                                                 @D2C   21218600
.SETMAX  ANOP                                                           21250000
         DC    AL2(&MAXLEN)            MAXIMUM ARGUMENT LENGTH          21283300
         AIF   (T'&MINLEN NE 'N').SETMIN  IS IT A SELF-DEFINING TERM?   21316600
         AIF   (&MINLEN GE 1 AND &MINLEN LE 65535).SETMIN MIN OK? @D2C  21349900
         MNOTE 8,'''MINLEN'' VALUE IS OUT OF RANGE - RANGE IS 1-65535' X21383200
                                                                  @D2C  21416500
.SETMIN  ANOP                                                           21450000
         DC    AL2(&MINLEN)            MINIMUM ARGUMENT LENGTH          21478500
         AIF   (T'&MAXLEN NE 'N' OR T'&MINLEN NE 'N').KEYTEST          X21507000
                                       VALUES CAN'T BE COMPARED IF NOT X21535500
                                       SELF-DEFINING TERMS              21564000
         AIF   (&MINLEN LE &MAXLEN).KEYTEST  MIN MUST NOT EXCEED MAX    21592500
         MNOTE 8,'''MINLEN'' CANNOT BE GREATER THAN ''MAXLEN'''         21621000
.********************************************************************** 21650000
.*                                                                    * 21700000
.*       SET LENGTH OF 'KEY' STRING                                   * 21749300
.*                                                                    * 21798600
.********************************************************************** 21847900
.KEYTEST ANOP                                                           21897200
&KEYLEN  SETA  K'&KEY                  FIND KEY PARM LENGTH             21946500
         AIF   (&KEYLEN EQ 0).KEYOK    IS THERE A 'KEY' STRING?         21995800
         AIF   ('&KEY'(1,1) NE '''').NOQUOT1  IS OPERAND IN QUOTES?     22045100
&KEYLEN  SETA  K'&KEY-2                YES, DON'T COUNT QUOTES          22094400
         AIF   (&KEYLEN EQ 0).KEYOK    IS ANYTHING IN THE STRING?       22143700
&KEYPARM SETC  '&KEY'(2,&KEYLEN)       SET KEY CHARACTER STRING         22193000
         AGO   .KEYCHK                                                  22242300
.NOQUOT1 ANOP                                                           22291600
&KEYPARM SETC  '&KEY'(1,&KEYLEN)       SET KEY CHARACTER STRING         22340900
.KEYCHK  ANOP                                                           22390200
&STRING  SETC  '&KEYPARM'              COPY STRING FOR LENGTH CHECK     22439500
&STRLEN  SETA  &KEYLEN                 COPY LENGTH FOR STRING CHECK     22488800
&RETNPT  SETA  1                       SET RETURN POINT                 22538100
         AGO   .ADJLEN                 ADJUST LENGTH OF STRING          22587400
.RETURN1 ANOP                                                           22636700
&KEYLEN  SETA  &STRLEN                 SET ADJUSTED STRING LENGTH       22686000
         AIF   (&KEYLEN LE 255).KEYOK  'KEY' LENGTH IS OK               22735300
         MNOTE 8,'''KEY'' VALUE IS TOO LONG - MAXIMUM OF 255 CHARACTERSX22784600
               '                                                        22833900
&KEYLEN  SETA  255                     SET CORRECT KEY LENGTH           22883200
.KEYOK   ANOP                                                           22932500
         DC    AL1(&KEYLEN)            KEY LENGTH                       22981800
.********************************************************************** 23031100
.*                                                                @D1A* 23033400
.*       SET VALUE OF MINIMUM ABBREVIATED KEY LENGTH              @D1A* 23035700
.*                                                                @D1A* 23038000
.********************************************************************** 23040300
         AIF   ('&ABBRLEN' EQ '').NOABBR  NO ABBREV. LENGTH GIVEN? @P1C 23042600
         AIF   (&KEYLEN NE 0).CHKABBR  ABBR. GIVEN. KEY GIVEN?     @D1A 23044900
         MNOTE 4,'''ABBRLEN'' VALUE SPECIFIED, BUT NO ''KEY'' SPECIFIEDX23047200
               '                                                   @D1A 23049500
.NOABBR  ANOP                        NO ABBR. LENGTH GIVEN         @D1A 23051800
&ABRSIZE SETA  &KEYLEN               SET ABBR. LENGTH = KEY LENGTH @D1A 23054100
         AGO   .SETABBR                                            @D1A 23056400
.CHKABBR ANOP                                                      @D1A 23058700
         AIF   (&ABBRLEN GE 1 AND &ABBRLEN LE &KEYLEN).GOODABR     @D1A 23061000
         MNOTE 8,'''ABBRLEN'' VALUE IS OUT OF RANGE - RANGE IS 1-KEYLENX23063300
               GTH'                                                @D1A 23065600
.GOODABR ANOP                                                      @D1A 23067900
&ABRSIZE SETA  &ABBRLEN             SET ABBR. LENGTH = GIVEN ABBR. @D1A 23070200
.SETABBR ANOP                                                      @D1A 23072500
         DC    AL1(&ABRSIZE)       MINIMUM KEY ABBREVIATION LENGTH @D1A 23074800
.********************************************************************** 23077100
.*                                                                    * 23080400
.*       SET LENGTH OF 'ALSO' STRING                                  * 23129700
.*                                                                    * 23179000
.********************************************************************** 23228300
&ALSOLEN SETA  K'&ALSO                 FIND ALSO PARM LENGTH            23277600
         AIF   (&ALSOLEN EQ 0).ALSOOK  IS THERE AN 'ALSO' STRING?       23326900
         AIF   ('&ALSO'(1,1) NE '''').NOQUOT2  IS OPERAND IN QUOTES?    23376200
&ALSOLEN SETA  K'&ALSO-2               YES, DON'T COUNT QUOTES          23425500
         AIF   (&ALSOLEN EQ 0).ALSOOK  IS ANYTHING IN THE STRING?       23474800
&ALSOPRM SETC  '&ALSO'(2,&ALSOLEN)     SET ALSO CHARACTER STRING        23524100
         AGO   .ALSOCHK                                                 23573400
.NOQUOT2 ANOP                                                           23622700
&ALSOPRM SETC  '&ALSO'(1,&ALSOLEN)     SET ALSO CHARACTER STRING        23672000
.ALSOCHK ANOP                                                           23721300
&STRING  SETC  '&ALSOPRM'              COPY STRING FOR LENGTH CHECK     23770600
&STRLEN  SETA  &ALSOLEN                COPY LENGTH FOR STRING CHECK     23819900
&RETNPT  SETA  2                       SET RETURN POINT                 23869200
         AGO   .ADJLEN                 ADJUST LENGTH OF STRING          23918500
.RETURN2 ANOP                                                           23967800
&ALSOLEN SETA  &STRLEN                 SET ADJUSTED STRING LENGTH       24017100
         AIF   (&ALSOLEN LE 8).ALSOOK  'ALSO' LENGTH IS OK              24066400
         MNOTE 8,'''ALSO'' VALUE IS TOO LONG - MAXIMUM OF 8 CHARACTERS' 24115700
&ALSOLEN SETA  8                       SET CORRECT ALSO LENGTH          24165000
.ALSOOK  ANOP                                                           24214300
         DC    AL1(&ALSOLEN)           ALSO LENGTH                      24263600
.********************************************************************** 24312900
.*                                                                    * 24362200
.*       SET LENGTH OF 'DELIM' STRING                                 * 24411500
.*                                                                    * 24460800
.********************************************************************** 24510100
&DLMLEN  SETA  K'&DELIM                FIND DELIM PARM LENGTH           24559400
         AIF   (&DLMLEN EQ 0).SETDEF   IS THERE A 'DELIM' STRING?  @P2C 24608700
         AIF   ('&DELIM'(1,1) NE '''').NOQUOT3 IS OPERAND IN QUOTES?    24658000
&DLMLEN  SETA  K'&DELIM-2              YES, DON'T COUNT QUOTES          24707300
         AIF   (&DLMLEN EQ 0).SETDEF   IS ANYTHING IN THE STRING?  @P2C 24756600
&DLMPARM SETC  '&DELIM'(2,&DLMLEN)     SET DELIM CHARACTER STRING       24805900
         AGO   .DLMCHK                                                  24855200
.NOQUOT3 ANOP                                                           24904500
&DLMPARM SETC  '&DELIM'(1,&DLMLEN)     SET DELIM CHARACTER STRING       24953800
.DLMCHK  ANOP                                                           25003100
&STRING  SETC  '&DLMPARM'              COPY STRING FOR LENGTH CHECK     25052400
&STRLEN  SETA  &DLMLEN                 COPY LENGTH FOR STRING CHECK     25101700
&RETNPT  SETA  3                       SET RETURN POINT                 25151000
         AGO   .ADJLEN                 ADJUST LENGTH OF STRING          25200300
.RETURN3 ANOP                                                           25249600
&DLMLEN  SETA  &STRLEN                 SET ADJUSTED STRING LENGTH       25298900
         AIF   (&DLMLEN LE 8).DELIMOK  'DELIM' LENGTH IS OK             25348200
         MNOTE 8,'''DELIM'' VALUE IS TOO LONG - MAXIMUM OF 8 CHARACTERSX25397500
               '                                                        25446800
&DLMLEN  SETA  8                       SET CORRECT DELIM LENGTH         25496100
         AGO   .DELIMOK                SET FINAL DELIM LENGTH      @P2A 25501500
.SETDEF  ANOP                                                      @P2A 25506900
         AIF   (&KEYLEN EQ 0).TYPETRT  IF NOT A KEY SCAN           @P2A 25512300
&DLMPARM SETC  ''                      'KEY' DELIM DEFAULT         @P2A 25517700
         AGO   .DELIMOK                SET FINAL DELIM LENGTH      @P2A 25523100
.TYPETRT ANOP                                                      @P2A 25528500
&DLMLEN  SETA  3                       'TYPE/TRT' DELIM DEF. LEN.  @P2A 25533900
&DLMPARM SETC  ' ,)'                   'TYPE/TRT' DELIM DEF. CHARS @P2A 25539300
.DELIMOK ANOP                                                           25545400
         DC    AL1(&DLMLEN)            DELIM LENGTH                     25594700
.********************************************************************** 25693300
.*                                                                    * 25742600
.*       SET 'ALSO', DELIM', AND 'KEY' STRINGS                        * 25800000
.*                                                                    * 25850000
.********************************************************************** 25900000
         DC    CL8'&ALSOPRM '          'ALSO' CHARACTERS                25950000
         DC    CL8'&DLMPARM '          'DELIM' CHARACTERS               26000000
         AIF   (&KEYLEN EQ 0).MACEND   ANY 'KEY' CHARACTERS?            26050000
         DC    C'&KEYPARM'             KEY CHARACTER STRING             26100000
         AGO   .MACEND                 PROCESSING IS COMPLETE           26150000
.********************************************************************** 26200000
.*                                                                    * 26250000
.*       TEST STRING FOR PRESENCE OF DOUBLE QUOTES AND/OR             * 26300000
.*       DOUBLE AMPERSANDS.  REDUCE THE CALCULATED LENGTH BY          * 26350000
.*       ONE FOR EACH DOUBLED CHARACTER.                              * 26400000
.*                                                                    * 26450000
.********************************************************************** 26500000
.ADJLEN  ANOP                                                           26550000
&INDEX   SETA  1                       INITIALIZE STRING INDEX          26600000
.ADJLOOP ANOP                                                           26650000
         AIF   (&INDEX GE K'&STRING).RETURN  NO ROOM FOR A DOUBLE CHAR X26700000
                                             IF AT END OF STRING        26750000
         AIF   ('&STRING'(&INDEX,2) EQ '''''').DBLCHAR  IS THERE A     X27000000
                                                        DOUBLE QUOTE?   27050000
         AIF   ('&STRING'(&INDEX,2) EQ '&&').DBLCHAR  OR A DOUBLE      X27100000
                                                      AMPERSAND?        27150000
&INDEX   SETA  &INDEX+1                NEITHER, LOOK AT NEXT CHARACTER  27200000
         AGO   .ADJLOOP                                                 27250000
.DBLCHAR ANOP                                                           27300000
&STRLEN  SETA  &STRLEN-1               REDUCE LENGTH FOR DOUBLED CHAR   27350000
&INDEX   SETA  &INDEX+2                SKIP DOUBLED CHARACTER           27400000
         AGO   .ADJLOOP                CONTINUE SCAN                    27450000
.RETURN  ANOP                                                           27460000
         AIF   (&RETNPT EQ 1).RETURN1  RETURN TO 'KEY' PROCESSING       27470000
         AIF   (&RETNPT EQ 2).RETURN2  RETURN TO 'ALSO' PROCESSING      27480000
         AIF   (&RETNPT EQ 3).RETURN3  RETURN TO 'DELIM' PROCESSING     27490000
.********************************************************************** 27500000
.*                                                                    * 27550000
.*       END OF PROCESSING                                            * 27600000
.*                                                                    * 27650000
.********************************************************************** 27700000
.MACEND  ANOP                                                           27750000
         MEND                                                           27800000
