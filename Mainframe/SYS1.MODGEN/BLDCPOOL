         MACRO                                                          00050000
&NAME    BLDCPOOL  &CPID=,&BRANCH=YES,&SP=,&CSIZE=,&POOLSIZ=,&CPADDR=, *00100000
               &RECOVER=,&RELATED=,&AUTODEL=NO,&CELLCNT=,&BNDRY=FWORD, *00109000
               &SERIAL=NO                                      @ZA28730 00118000
.*                                                                      00127000
.*01*  COPYRIGHT = 5740-XC6 COPYRIGHT IBM CORP 1981,           @G860P29 00136000
.*                 LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM, @G860P29 00145000
.*                 REFER TO COPYRIGHT INSTRUCTIONS FORM        @G860P29 00154000
.*                 NUMBER G120-2083.                           @G860P29 00163000
.*                                                                      00172000
.*01*  STATUS = OS/VS2 HBB2102                                 @G860P29 00181000
.*                                                                      00190000
         LCLA  &REG,&GMFAIL                                             00200000
         LCLB  &R,&G,&C,&DBL,&AUTO,&SER                        @ZA28730 00250000
         LCLC  &NMST,&CSTRNG,&BSTRNG,&ASTRNG                            00252000
&NMST    SETC  'IHA'.'&SYSNDX'                                          00254000
&GMFAIL  SETA  4                   RETURN CODE FOR GETMAIN ERROR        00260000
         AIF   ('&BRANCH' NE 'YES').ERR1    BRANCH=YES MANDATORY        00300000
&DBL     SETB  ('&BNDRY' EQ 'DWORD')                                    00310000
         AIF   ('&BNDRY' EQ 'FWORD' OR '&BNDRY' EQ 'DWORD').BNDRYOK     00320000
         IHBERMAC &BNDRY,BNDRY,FWORD                                    00330000
.BNDRYOK AIF   ('&RECOVER' EQ '').CONT1                                 00350000
         AIF   ('&RECOVER' NE 'YES').ERR5   RECOVER MUST=YES, IF GIVEN  00400000
.*       RECOVER=YES SPECIFIED                                          00410000
         AIF   ('&CSIZE'.'&POOLSIZ'.'&CELLCNT'.'&SP' NE '').ERR9        00420000
.*       NO EXTRANEOUS PARAMETERS SPECIFIED FOR RECOVER=YES             00430000
&R       SETB  1              INDICATE RECOVERY TYPE                    00550000
         AIF   ('&CPID' EQ '').ERR6         CPID MUST BE GIVEN          00600000
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER       00610000
&C       SETB  1              INDICATE CPID PRESENT                     00620000
         AGO   .GEN1                                                    00650000
.*       RECOVER PARAMETER NOT SPECIFIED                                00652000
.CONT1   AIF   ('&CSIZE' EQ '').ERR7        CSIZE MUST BE GIVEN         00660000
         AIF   ('&POOLSIZ' NE '' AND '&CELLCNT' NE '').ERR11            00670000
.*       CELLCNT AND POOLSIZ ARE MUTUALLY EXCLUSIVE PARAMETERS          00680000
         AIF   ('&CPID' EQ '').CONT2        CPID OPTION PRESENT?        00710000
         AIF   ('&CPID'(1,1) NE '(').ERR2   CPID MUST BE REGISTER       00760000
&C       SETB  1              INDICATE CPID PRESENT                     00810000
.CONT2   AIF   ('&CPADDR' NE '').CONT3                                  00850000
&G       SETB  1              INDICATE GETMAIN TO BE ISSUED             00900000
&REG     SETA  4              INDICATE R4 USED INSTEAD OF R0 FOR CPID   00950000
         AGO   .GEN1                                                    01000000
.CONT3   AIF   ('&CPADDR'(1,1) NE '(').ERR2A CPADDR MUST BE REGISTER    01050000
.*       COMMON EXPANSION                                               01060000
.GEN1    ANOP                                                           01100000
&NAME    STM   2,12,28(13)         SAVE CALLERS REGISTERS 2-12          01150000
         AIF   (&C EQ 0).SKIP1          SKIP IF CPID NOT GIVEN          01200000
         AIF   (&G EQ 1).SET            SKIP IF GETMAIN TO BE ISSUED    01250000
         AIF   ('&CPID(1)' EQ '0').SKIP2                                01300000
.SET     LR    &REG,&CPID(1)       PRESERVE CPID VALUE AROUND GETMAIN   01400000
         AGO   .SKIP2                                                   01450000
.SKIP1   SR    &REG,&REG           INDICATE NO CPID (DYNAMIC POOL)      01550000
.SKIP2   AIF   (&R EQ 1).GENR           SKIP IF RECOVER OPTION GIVEN    01600000
         AIF   ('&SP' EQ '').ERR8       SUBPOOL MUST BE GIVEN           01610000
.*       FILTER OUT INVALID SUBPOOL SPECIFICATIONS                      01620000
         AIF   ('&SP'(1,1) EQ '(' OR T'&SP NE 'N').SPOK                 01630000
.*       NO CHECK CAN BE MADE IF SP# EQUATED OR IN A REGISTER           01640000
         AIF   (&SP GT 255).ERR3        SUBPOOL MUST BE LE 255          01650000
.*       VALID SUBPOOL PARAMETER GIVEN                                  01696000
.SPOK    AIF   ('&POOLSIZ' EQ '').PSKIP                                 01698000
&CSTRNG  SETC  'POOLSIZ'                                                01698400
&BSTRNG  SETC  '1024'                                                   01698500
&ASTRNG  SETC  ''                                                       01698600
         AIF   ('&POOLSIZ'(1,1) EQ '(' OR T'&POOLSIZ NE 'N').#SKIP      01698800
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER               01698900
         AIF   (&POOLSIZ LT 1 OR &POOLSIZ GT 255).ERR4A                 01700000
         AGO   .#SKIP         SKIP OVER CELL COUNT CHECKS               01710000
.PSKIP   AIF   ('&CELLCNT' EQ '').ERR10      CELLCNT OR POOLSIZ REQ'D   01720000
&CSTRNG  SETC  'CELLCNT'                                                01730000
&BSTRNG  SETC  'CSIZE+4'                                                01740000
&ASTRNG  SETC  '32+'                                                    01742000
.#SKIP   AIF   ('&CSIZE'(1,1) EQ '(' OR T'&CSIZE NE 'N').CSKIP          01750000
.*       LIMIT TEST CANNOT BE MADE IF EQUATED OR REGISTER               01760000
         AIF   (&CSIZE LT 8 OR &CSIZE GT 4092).ERR4                     01800000
.CSKIP   AIF   ('&CELLCNT' EQ '').POOLS CELL COUNT SPECIFIED?           01850000
         AIF   ('&CSIZE'(1,1) NE '(').CNT#EQ CSIZE IN A REGISTER?       01910000
         LA    15,4(&CSIZE(1).,0)  CELL SIZE FOR MULTIPLICATION YM02296 01930000
         AGO   .MULTP                                                   01940000
.CNT#EQ  LA    15,&CSIZE.+4(0,0)   CELL SIZE FOR MULTIPLICATION YM02296 01942000
.MULTP   LA    14,3(0,0)           SET UP MASK TO ROUND CSIZE   YM02296 01942600
         NR    14,15               REDUCE CSIZE TO REMAINDER OF         01943200
*                                  DIVISION BY 3                        01943600
         IC    14,*+8(14)          SELECT PROPER ROUNDING FACTOR        01943700
         B     *+8                 BRANCH AROUND TABLE                  01943800
         DC    FL1'0,3,2,1'                                             01943900
         AR    15,14               ADD THE ROUNDING FACTOR TO CSIZE     01945600
         AIF   (NOT &DBL).NOADJB NO ADJUSTMENT CODE IF BNDRY=FWORD      01945800
         LA    14,4(0,0)           CELLSIZE IS NOW A MULTIPLE   YM02296 01946000
         NR    14,15               OF 4, IF IT IS ALSO A MULTIPLE       01946200
         BZ    &NMST.0             OF 8, THEN ADD 4 TO FORCE            01946400
         LA    15,4(15,0)          DOUBLE WORD CELL BOUNDARIES. YM02296 01946600
&NMST.0  DS    0H                                                       01946800
.NOADJB  SR    14,14               RESET REGISTER FOR MULTIPLY          01947600
         AIF   ('&CELLCNT'(1,1) EQ '(').CNTREG CELL COUNT IN REG?       01948100
         LA    2,&CELLCNT.(0,0)    CELL COUNT TO MULTIPLY       YM02296 01949800
         MR    14,2                COMPUTE REQUIRED POOL SIZE           01951500
         AGO   .ADDCPAB                                                 01953200
.CNTREG  MR    14,&CELLCNT(1)      COMPUTE REQUIRED POOL SIZE           01954900
.ADDCPAB LA    2,32(15,0)          ADD THE CPAB LENGTH          YM02296 01956600
.POOLS   AIF   ('&CPADDR(1)' EQ '1' OR &G EQ 1).SKIP3                   01958300
         AIF   ('&CPADDR(1)' EQ '0').ERR2A    ERROR, CPID=CPADDR=(0)    01960000
.*       ALL PARAMETERS VALID                                           01970000
         LR    1,&CPADDR(1)        LOAD R1 WITH CPADDR                  02000000
         MNOTE *,'IT IS ASSUMED THAT &ASTRNG.(&CSTRNG.*(&BSTRNG.)) BYTES02010000
               S ARE AVAILABLE STARTING AT CPADDR.'                     02020000
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES                    02050000
.SKIP3   AIF   ('&CELLCNT' NE '').SKIP3C                                02052000
         AIF   ('&POOLSIZ'(1,1) EQ '(').SKIP3A POOLSIZ = REGISTER?      02060000
         LA    2,&POOLSIZ.(0,0)    LOAD POOLSIZE IN 1K BLOCKS   YM02296 02100000
         AGO   .SKIP3B                                                  02110000
.SKIP3A  AIF   ('&POOLSIZ(1)' EQ '2').SKIP3B STORE DIRECT, POOLSIZ=(2)  02120000
         LR    2,&POOLSIZ(1)       LOAD POOL SIZE IN 1K BLOCKS          02130000
.SKIP3B  SLL   2,10(0)             TIMES 1024 FOR SIZE IN BYTES YM02296 02150000
.SKIP3C  AIF   (&G EQ 0).GENC                                           02200000
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES AND CPADDR=(R)     02210000
.*       GETMAIN THE POOL SEGMENT BELOW 16 MEG                          02260000
         GETMAIN RC,LV=(2),SP=&SP,LOC=BELOW                             02310000
         LTR   15,15               TEST GETMAINS RETURN CODE IN R15     02500000
         BZ    &NMST.A             GETMAIN WORKED, SKIP ERROR SEQUENCE  02670000
         LR    0,15                SAVE GETMAIN'S CODE AS REASON CODE   02672000
         LA    15,&GMFAIL.(0,0)    SET UP GETMAIN FAILURE CODE  YM02296 02674000
         B     &NMST.B             RETURN TO USER                       02676000
&NMST.A  LR    0,4                 GETMAIN OK, RESTORE CPID TO REG 0    02680000
.*       COMMON TO ALL EXPANSIONS EXCEPT RECOVER=YES                    02702800
.GENC    XC    0(32,1),0(1)        INITIALIZE CPAB(E)                   02705000
         ST    0,0(0,1)            SAVE CPID IN CPAB(E)                 02710000
         ST    2,24(0,1)           SAVE POOL SIZE IN CPAB(E)            02715000
         AIF   ('&AUTODEL' EQ 'NO').CKSER                      @ZA28730 02718000
&AUTO    SETB  1         AUTO DELETE SPECIFIED                 @ZA28730 02721000
         AIF   ('&AUTODEL' EQ 'YES').CKSER                     @ZA28730 02724000
&AUTO    SETB  0         AUTODEL PARM INVALID--DEFAULT TO NO   @ZA28730 02727000
         IHBERMAC 1015,&AUTODEL,AUTODEL,NO                              02730000
.CKSER   AIF   ('&SERIAL' EQ 'NO').CKBNDRY                     @ZA28730 02731200
         AIF   ('&SERIAL' EQ 'YES').SERON                      @ZA28730 02732400
         IHBERMAC 1015,&SERIAL,SERIAL,NO                       @ZA28730 02733600
         AGO   .CKBNDRY           SERIAL=NO                             02734800
.SERON   ANOP                                                  @ZA28730 02736000
&SER     SETB  1         SERIALIZATION SPECIFIED               @ZA28730 02737200
         AIF   (&AUTO).CKBNDRY    IF SERIAL, MUST HAVE AUTODEL @ZA28730 02738400
&SER     SETB  0         SERIAL PARM INVALID--DEFAULT TO NO    @ZA28730 02739600
         IHBERMAC 1016,''SERIAL=YES'',''AUTODEL=NO''           @ZA28730 02740800
.CKBNDRY AIF   (&SER+&AUTO+&DBL EQ 0).NOAUTO2     ANY OPTIONS? @ZA28730 02742000
         OI    17(1),B'0&DBL&AUTO.00&SER.00' SET OPTION FLAGS  @ZA28730 02743200
.NOAUTO2 AIF   ('&SP'(1,1) EQ '(').SPREG IS SP A REGISTER?              02745000
         MVI   16(1),&SP           SAVE SUBPOOL ID IN CPAB(E)           02750000
         AGO   .GENC1             BACK TO MAINLINE                      02752000
.SPREG   STC   &SP(1),16(0,1)      SAVE SUBPOOL ID IN CPAB(E)           02754000
.GENC1   AIF   ('&CSIZE'(1,1) EQ '(').GENM IS CSIZE A REGISTER?         02756000
         LA    2,&CSIZE.(0,0)      LOAD CELL SIZE               YM02296 02820000
         ST    2,4(0,1)            SAVE CELL SIZE IN CPAB(E)            02850000
         AGO   .GENX              RETURN TO MAINLINE                    02860000
.GENM    ST    &CSIZE(1),4(0,1)    SAVE CELL SIZE IN CPAB(E)            02870000
.*       COMMON TO ALL EXPANSIONS                                       02900000
.GENX    L     3,16(0,0)           CVT ADDRESS FOR BLDCPOOL     YM02296 02950000
         L     15,CVTBLDCP-CVTMAP(0,3)    LOAD BLDCPOOL BRANCH ENTRY    03000000
         BALR  14,15               GO TO BLDCPOOL SERVICE               03050000
         AIF   (&G EQ 0).SKIP4                                          03100000
&NMST.B  DS    0H                  BRANCH HERE ON A GETMAIN FAILURE     03150000
.SKIP4   LM    2,12,28(13)         RESTORE CALLERS REGISTERS            03250000
         MEXIT                                                          03300000
.*       RECOVER=YES REQUEST SPECIAL PROCESSING                         03350000
.GENR    SR    1,1                 INDICATE RECOVERY REQUEST            03400000
         AGO   .GENX                                                    03450000
.ERR1    IHBERMAC 1006,''BRANCH=YES''                                   03500000
         MEXIT                                                          03550000
.ERR2    IHBERMAC 1001,CPID,&CPID                                       03600000
         MEXIT                                                          03650000
.ERR2A   IHBERMAC 1001,CPADDR,&CPADDR                                   03700000
         MEXIT                                                          03750000
.ERR3    IHBERMAC 1001,SP,&SP                                           03800000
         MEXIT                                                          03900000
.ERR4    IHBERMAC 1001,CSIZE,&CSIZE                                     03950000
         MEXIT                                                          04000000
.ERR4A   IHBERMAC 1001,POOLSIZ,&POOLSIZ                                 04060000
         MEXIT                                                          04070000
.ERR5    IHBERMAC 1001,RECOVER,&RECOVER                                 04100000
         MEXIT                                                          04110000
.ERR6    IHBERMAC 1006,CPID                                             04120000
         MEXIT                                                          04122000
.ERR7    IHBERMAC 1006,CSIZE                                            04130000
         MEXIT                                                          04140000
.ERR8    IHBERMAC 1006,SP                                               04142000
         MEXIT                                                          04144000
.ERR9    AIF   ('&POOLSIZ' EQ '').ERR9A                                 04146000
         IHBERMAC 1020,POOLSIZ,''RECOVER=YES''                          04148000
.ERR9A   AIF   ('&SP' EQ '').ERR9B                                      04198000
         IHBERMAC 1020,SP,''RECOVER=YES''                               04200000
.ERR9B   AIF   ('&CSIZE' EQ '').ERR9C                                   04208000
         IHBERMAC 1020,CSIZE,''RECOVER=YES''                            04210000
.ERR9C   AIF   ('&CELLCNT' EQ '').ERR9D                                 04212000
         IHBERMAC 1020,CELLCNT,''RECOVER=YES''                          04214000
.ERR9D   MEXIT                                                          04218000
.ERR10   IHBERMAC 1006,POOLSIZ                                          04298000
         MEXIT                                                          04308000
.ERR11   IHBERMAC 1020,CELLCNT,POOLSIZ                                  04318000
         MEND                                                           04348000
