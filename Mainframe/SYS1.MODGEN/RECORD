         MACRO                                                          00050000
&NAME    RECORD &TYPE=,&RCVRY=,&DATAADR=,&PARMADR=,&LENGTH=,           C00100000
               &BUFFER=YES,&POSTING=NO,&HEADER=NO,&JOBNAME=NO,&TIME=NO,C00150000
               &ERRORID=NO,&FRESTOR=                           @G860P38 00200000
.********************************************************************** 00202700
.*                                                                    * 00205400
.*   Macro Name: RECORD                                               * 00213500
.*                                                                    * 00221600
.*   Descriptive Name: RTM Recording Facility                         * 00221800
.*                                                                    * 00222000
.*   Component: 5752SCRTM - Recovery Termination Manager          @P1C* 00222100
.*                                                                    * 00222200
.*   Copyright: 5645-001                                          @P1C* 00222300
.*              Contains restricted materials of IBM                  * 00222400
.*              (C) Copyright IBM Corp. 1981, 1996                @P1C* 00222600
.*              Licensed materials - property of IBM                  * 00222800
.*              Refer to copyright instructions                       * 00223000
.*              form number G120-2083.                                * 00223200
.*                                                                    * 00223400
.*   Status: OS/VS2 HBB6601                                       @P1C* 00223600
.*                                                                    * 00223800
.*01* EXTERNAL CLASSIFICATION: NONE                                   * 00223900
.*01* END OF EXTERNAL CLASSIFICATION                                  * 00224000
.*                                                                    * 00224100
.*     The RECORD macro processes messages to SYS1.LOGREC,            * 00224300
.*     WTO messages issued in disabled mode, and emergency            * 00227000
.*     requests when the system is terminating.                       * 00229700
.*                                                                    * 00232400
.*   Method of Access: ASM only                                       * 00233700
.*                                                                    * 00235100
.*   Change Activity:                                                 * 00239100
.*                                                                    * 00243200
.*     HBB2102 - MVS/system product version 2 release 1       @G860P38* 00243700
.* $L1=RTM2REC,JBB2125,830216,PDFP: Suppoet for resource manager      * 00244200
.*                             for record                         @L1A* 00244700
.* $L2=SYMREC ,JBB2220,850601,PDFP: Support for SYMREC            @L2A* 00244900
.* $P1=PR20011 HBB6601 950908 PDKD: SC1CM compid split            @P1A* 00245000
.*                                                                    * 00245200
.********************************************************************** 00245900
*  MACDATE  06/01/85                                              @L2A  00295900
         LCLA  &CTR1,&CTR2,&CTR3                              @G17EP5W  00450000
         LCLB  &B(13)                                          @G860P1C 00500000
&NAME    DS    0H                                                       00550000
&B(1)    SETB  ('&TYPE' EQ 'TERM')         SYSTEM TERMINATION CALL      00600000
&B(2)    SETB  ('&TYPE' EQ 'LOGREC')       WRITE TO SYS1.LOGREC         00650000
&B(3)    SETB  ('&TYPE' EQ 'WTO')          WRITE TO OPERATOR            00700000
&B(6)    SETB  ('&BUFFER' EQ 'YES')        DATA MUST BE BUFFERED        00850000
&B(7)    SETB  ('&BUFFER' EQ 'NO')         DATA MUST NOT BE BUFFERED    00900000
&B(8)    SETB  ('&HEADER' EQ 'YES')        BUILD AN AR149 HEADER        00950000
&B(9)    SETB  ('&JOBNAME' EQ 'YES')       INSERT JOBNAME AFTER HEADER  01000000
&B(10)   SETB  ('&TIME' EQ 'YES')          RETURN TIME PLACED IN RECORD 01050000
&B(11)   SETB  ('&POSTING' EQ 'YES')       POST CALLER WHEN BUFFER FREE 01100000
&B(12)   SETB  ('&ERRORID' EQ 'YES')       INCLUDE ERRORID IN RECORD    01150000
&B(13)   SETB  ('&FRESTOR' EQ 'YES')   FREE NON-BUFFERED MSG @G860P1C   01200000
&CTR1    SETA  128*&B(1)+64*&B(2)+32*&B(3)+2*&B(13)+&B(12)   @G860PA8   01241600
&CTR2    SETA  32*&B(6)+16*&B(7)+8*&B(8)+4*&B(9)+2*&B(10)+&B(11) @L1C   01291600
&CTR3    SETA  &B(8)+&B(9)+&B(10)+&B(11)+&B(12)+&B(13)       @G860PA8   01366400
         AIF   (&CTR1 LE 31).ERR1                            @ZMC0176   01408000
         AIF   ('&HEADER' NE 'YES' AND '&HEADER' NE 'NO').ERR10         01450000
         AIF   ('&TIME' NE 'YES' AND '&TIME' NE 'NO').ERR11             01500000
         AIF   ('&JOBNAME' NE 'YES' AND '&JOBNAME' NE 'NO').ERR12       01550000
         AIF   ('&POSTING' NE 'YES' AND '&POSTING' NE 'NO').ERR13       01600000
         AIF   ('&BUFFER' NE 'YES' AND '&BUFFER' NE 'NO' AND '&BUFFER' N01650000
               NE 'OPT').ERR14                                          01700000
         AIF   ('&ERRORID' NE 'YES' AND '&ERRORID' NE 'NO').ERR15       01750000
         AIF   ('&RCVRY' EQ '').SKIP1                            @L1C   01762500
         MNOTE *,'RCVRY OPERAND NO LONGER REQUIRED - IT IS IGNORED'     01775000
.SKIP1   ANOP                                                    @L1A   01787500
         AIF   ('&TYPE' EQ 'WTO' AND '&HEADER' EQ 'YES').ERR9           01800000
         AIF   ('&TYPE' EQ 'TERM' AND ('&LENGTH' EQ '' OR '&DATAADR' EQN01825000
                '')).ERR17                                   @G860PA8   01850000
         AIF   ('&TYPE' EQ 'TERM').LNGR                      @G860PA8   01875000
         AIF   ('&DATAADR' NE '' AND '&PARMADR' NE '').ERR4             01950000
         AIF   ('&DATAADR' EQ '' AND '&PARMADR' EQ '').ERR3             02000000
         AIF   (&CTR3 NE 0 AND '&PARMADR' EQ '').ERR5                   02050000
         AIF   ('&JOBNAME' EQ 'YES' AND '&HEADER' EQ 'NO').ERR6         02100000
         AIF   ('&HEADER' EQ 'YES' AND '&BUFFER' NE 'YES').ERR7         02150000
         AIF   ('&LENGTH' EQ '' AND ('&TYPE' NE 'WTO' OR '&BUFFER' NE  C02200000
               'NO' OR '&FRESTOR' EQ 'YES')).ERR8              @G860PA8 02250000
         AIF   ('&ERRORID' EQ 'YES' AND '&TYPE' NE 'LOGREC').ERR16      02300000
         AIF   ('&FRESTOR' NE 'YES' AND '&FRESTOR' NE 'NO' AND         C02308300
                '&FRESTOR' NE '').ERR18                        @G860P38 02311800
         AIF   ('&FRESTOR' EQ 'YES' AND '&BUFFER' EQ 'YES').ERR19       02322300
.*                                                             @G860P1C 02325800
         AIF   ('&FRESTOR' NE 'NO' AND '&FRESTOR' NE 'YES' AND         C02329700
                '&BUFFER' NE 'YES' AND '&POSTING' EQ 'NO').ERR20       C02333600
                                                               @G860P38 02337500
.LNGR    ANOP                                                           02341500
         AIF   ('&LENGTH' EQ '').LNGR0                                  02350000
         AIF   ('&LENGTH' EQ '(0)').LNGR0                               02400000
         AIF   ('&LENGTH'(1,1) EQ '(').LNGRN                            02450000
         CNOP  0,4                                                      02500000
         B     *+8                        BRANCH AROUND CONSTANTS       02550000
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE          02600000
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS       02650000
         DC    AL2(&LENGTH)               LENGTH OF DATA SUPPLIED       02700000
         L     0,*-4                      REGISTER 0=OPTIONS AND LNGTH  02750000
         AGO   .LOADR1                                                  02800000
.LNGRN   LR    0,&LENGTH                  LENGTH IN REG0                02850000
.LNGR0   B     *+6                        BRANCH AROUND CONSTANTS       02900000
.*                                                             @G860PA8 02950000
         DC    AL1(&CTR1)                 BIT SETTING FOR TYPE          03000000
         DC    AL1(&CTR2)                 BIT SETTING FOR OPTIONS       03050000
         ICM   0,12,*-2                    TYPE AND OPTS IN REG0        03100000
.LOADR1  AIF   ('&TYPE' EQ 'TERM').NOTPARM                     @G860PA8 03137500
         AIF   ('&PARMADR' NE '').PARM                         @G860PA8 03175000
.NOTPARM IHBINNRA &DATAADR                LOAD REG1 WITH ADDR OF DATA   03212500
.*                                                             @G860PA8 03231200
         LCR   1,1                        TWO'S COMPLIMENT FOR DATAADR  03250000
         AGO   .LNKAGE                                                  03300000
.PARM    IHBINNRA &PARMADR                REG1 WITH ADDR OF PARMLIST    03500000
.LNKAGE  L     15,X'10'(0,0)              (FLCCVT) ADDRESS OF CVT  @L2C 03537500
         L     15,X'2BC'(0,15)            (CVTRECRQ) ADDRESS OF    @L2C 03575000
*                                         THE RECORD REQUEST ROUTINE    03612500
         BALR  14,15                      BRANCH TO RECORDING FACILITY  03650000
         AGO   .END                                                     03700000
.ERR1    MNOTE 12,'TYPE OPERAND REQD - NOT SPECIFIED'                   03750000
         MEXIT                                                          03800000
.ERR3    MNOTE 12,'DATAADR OR PARMADR OPERAND REQD - NOT SPECIFIED'     03950000
         MEXIT                                                          04000000
.ERR4    MNOTE 12,'DATAADR AND PARMADR OPERANDS BOTH SPECIFIED'         04050000
         MEXIT                                                          04100000
.ERR5    MNOTE 12,'PARMADR REQD WITH HEADER, JOBNAME, TIME, POSTING, ERC04150000
               RORID, OR FRESTOR'                              @G860PA8 04200000
         MEXIT                                                          04250000
.ERR6    MNOTE 12,'HEADER=YES REQUIRED IF JOBNAME=YES'                  04300000
         MEXIT                                                          04350000
.ERR7    MNOTE 12,'BUFFER=YES REQUIRED IF HEADER=YES'                   04400000
         MEXIT                                                          04450000
.ERR8    MNOTE 12,'LENGTH OPERAND REQD - NOT SPECIFIED'                 04500000
         MEXIT                                                          04550000
.ERR9    MNOTE  12,'HEADER=YES INVALID WITH TYPE=WTO'                   04600000
         MEXIT                                                          04650000
.ERR10   IHBERMAC  54,,&HEADER                                          04700000
         MEXIT                                                          04750000
.ERR11   IHBERMAC 54,,&TIME                                             04800000
         MEXIT                                                          04850000
.ERR12   IHBERMAC 54,,&JOBNAME                                          04900000
         MEXIT                                                          04950000
.ERR13   IHBERMAC 54,,&POSTING                                          05000000
         MEXIT                                                          05050000
.ERR14   IHBERMAC 54,,&BUFFER                                           05100000
         MEXIT                                                          05150000
.ERR15   IHBERMAC 54,,&ERRORID                                          05200000
         MEXIT                                                          05250000
.ERR16   MNOTE 12,'TYPE=LOGREC REQUIRED IF ERRORID=YES'                 05300000
         MEXIT                                                          05350000
.ERR17   ANOP                                                  @G860PA8 05400000
         MNOTE 12,'DATAADR AND LENGTH REQUIRED FOR TYPE=TERM'  @G860PA8 05450000
         MEXIT                                                 @G860PA8 05500000
.ERR18   ANOP                                                  @G860P1C 05550000
         MNOTE 12,'INVALID KEYWORD OPERAND SPECIFIED FRESTOR=&FRESTOR'  05600000
         MEXIT                                                 @G860P1C 05650000
.ERR19   ANOP                                                  @G860P1C 05700000
         MNOTE 0,'FRESTOR IGNORED, BUFFER=YES SPECIFIED'       @G860PA8 05750000
&CTR1    SETA  128*&B(1)+64*&B(2)+32*&B(3)+&B(12)              @G860P1C 05800000
         AGO   .LNGR                                           @G860P1C 05850000
.ERR20   ANOP                                                  @G860PA8 05900000
         MNOTE 0,'NO BUFFERING REQUESTED - STORAGE IS NOT FREED'       C05937500
                                                               @G860P38 05975000
         AGO   .LNGR                                                    06012500
.END     MEND                                                           06050000
