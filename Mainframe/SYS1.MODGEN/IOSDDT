         MACRO  --  DDT BUILD MACRO                                     00050000
&LABEL   IOSDDT   &SIOEXIT=,&TRPEXIT=,&TCCWTAB=,&ERPEXIT=,&DDREXIT=,   *00100000
               &CPSEXIT=,&DSEXIT=,&EOSEXIT=,&MIHEXIT=,&ERASEXIT,       *00150000
               &SNSEXIT=,&SSYSID=,&UNSEXIT=,&RESERP=               @02A 00200000
         AGO   .SKIP                                                 */ 00250000
*/********************************************************************/ 00300000
*/*                                                                  */ 00350000
*/*01* MODULE NAME -  IOSDDT                                         */ 00400000
*/*                                                                  */ 00450000
*/*01* DESCRIPTIVE NAME - IOS Device Descriptor Table Build Macro    */ 00500000
*/*                                                                  */ 00550000
*/*01* MACRO CALL =                                                  */ 00600000
*/*                                                                  */ 00650000
*/*   label IOSDDT   SIOEXIT=entry point name                        */ 00700000
*/*                 ,TRPEXIT=entry point name                        */ 00750000
*/*                 ,TCCWTAB=entry point name                        */ 00800000
*/*                 ,ERPEXIT=(entry point or module name,type)       */ 00850000
*/*                <,DDREXIT=(entry point or module name,type)>      */ 00900000
*/*                <,CPSEXIT=entry point name>                       */ 00950000
*/*                <,DSEXIT=entry point name>                        */ 01000000
*/*                <,EOSEXIT=entry point name>                       */ 01050000
*/*                <,MIHEXIT=entry point name>                       */ 01100000
*/*                <,ERASEXIT=entry point name>                  @T1A*/ 01125000
*/*                <,SNSEXIT=entry point name>                       */ 01150000
*/*                <,SSYSID=subsystem name>                          */ 01200000
*/*                <,UNSEXIT=entry point name>                       */ 01250000
*/*                <,RESERP=entry point name>                        */ 01300000
*/*                                                                  */ 01350000
*/*01* PROPRIETARY STATEMENT =                                       */ 01400000
*/*                                                                  */ 01650000
*/*      LICENSED MATERIALS - PROPERTY OF IBM                        */ 01670000
*/*      THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                 */ 01690000
*/*      5647-A01 (C) COPYRIGHT IBM CORP. 1986, 1998             @P3C*/ 01720000
*/*                                                                  */ 01750000
*/*01* STATUS = HBB6606                                          @P3C*/ 01766600
*/*                                                                  */ 01783200
*/*01* FUNCTION - IOSDDT is an executable macro which generates      */ 01800000
*/*               the required DDT CSECT.                            */ 01850000
*/*                                                                  */ 01900000
*/*01* NOTES =                                                       */ 01950000
*/*        When both ERPEXIT and DDREXIT are specified with type     */ 02000000
*/*        'N', they must start with the same first 4 characters.    */ 02050000
*/*                                                                  */ 02100000
*/*02*   DEPENDENCIES = None                                         */ 02150000
*/*                                                                  */ 02200000
*/*02*   RESTRICTIONS -  The label is required since it will         */ 02250000
*/*                      be used as the CSECT name for the DDT       */ 02300000
*/*                      being generated.                            */ 02350000
*/*                                                                  */ 02400000
*/*02*   REGISTER CONVENTIONS = None                                 */ 02450000
*/*                                                                  */ 02500000
*/*01* MODULE TYPE -  BAL MACRO                                      */ 02550000
*/*                                                                  */ 02600000
*/*02*   PROCESSOR  - ASSEMBLER-H                                    */ 02650000
*/*                                                                  */ 02700000
*/*02*   ATTRIBUTES - A BAL executable macro.                        */ 02750000
*/*                                                                  */ 02800000
*/*01* INPUT -                                                       */ 02850000
*/*      label  - Required                                           */ 02900000
*/*                                                                  */ 02950000
*/*      SIOEXIT  Name of the start I/O exit entry point             */ 03000000
*/*      TRPEXIT  Name of the trap exit entry point                  */ 03050000
*/*      TCCWTAB  Name of the translate CCW table entry point        */ 03100000
*/*      ERPEXIT  Name of the ERP message entry point or module name */ 03150000
*/*      DDREXIT  Name of the DDR exit entry point or module name    */ 03200000
*/*      CPSEXIT  Name of the channel program scan exit entry point  */ 03250000
*/*      DSEXIT   Name of the device initialization exit entry point */ 03300000
*/*      EOSEXIT  Name of the end of sense exit entry point          */ 03350000
*/*      MIHEXIT  Name of the MIH exit entry point                   */ 03400000
*/*      ERASEXIT Name of the Erase exit entry point             @T1A*/ 03425000
*/*      SNSEXIT  Name of the sense exit entry point                 */ 03450000
*/*      SSYSID   Name of the subsystem ID (1-4 characters)          */ 03500000
*/*      UNSEXIT  Name of the unsolicited interrupt exit entry point */ 03550000
*/*      RESERP   Name of the resident ERP entry point               */ 03600000
*/*                                                                  */ 03650000
*/*      entry point name -  All entry point names must be from      */ 03700000
*/*                          one to eight characters in length.      */ 03750000
*/*                                                                  */ 03800000
*/*      module name -       All module names must be from five      */ 03850000
*/*                          to eight characters in length.          */ 03900000
*/*                                                                  */ 03950000
*/*      type -   Type describes whether the entry point name is     */ 04000000
*/*               to be treated as an entry point name address or    */ 04050000
*/*               a module name.                                     */ 04100000
*/*                  type                                            */ 04150000
*/*                   A    Entry point name address                  */ 04200000
*/*                   N    EBCDIC module name      (default)         */ 04250000
*/*               If N is specified, only the last 4 characters      */ 04300000
*/*               of the module name will be placed in the DDT.      */ 04350000
*/*                                                                  */ 04400000
*/*01* OUTPUT -                                                      */ 04450000
*/*          The DDT CSECT.                                          */ 04500000
*/*                                                                  */ 04550000
*/*01* EXECUTABLE MACROS -                                           */ 04600000
*/*               IOSDNAME                                           */ 04650000
*/*                                                                  */ 04662500
*/*01* EXTERNAL CLASSIFICATION: OTHER                            @P3A*/ 04675000
*/*01* END OF EXTERNAL CLASSIFICATION                            @P3A*/ 04687500
*/*                                                                  */ 04700000
*/*01* CHANGE ACTIVITY -                                             */ 04750000
*/*                                                                  */ 04800000
*/*   $MAC(IOSDDT),COMP(SC1C3): Generate DDT CSECT                   */ 04850000
*/*                                                                  */ 04900000
*/*    PN REASON  RLSE    DATE   ORG   COMMENTS                         04950000
*/*   $L0=IOGEN   JBB2220 850211 PDLD: IOGEN Restructure             */ 05000000
*/*   $L1=VERS    HBB3310 860416 PDLE: Versioning macro into product */ 05200000
*/*                                                              @L1A*/ 05250000
*/*   $L2=EMVS1   HBB4410 881114 PDLB: Enterprise/MVS Release 1      */ 05300000
*/*                                    support                   @L2A*/ 05350000
*/*                                                                  */ 05400000
*/*   $P1=PA20149 JBB2220 850211 PDLJ: TITLE Statement placement     */ 05403500
*/*                                    caused assembler error        */ 05407000
*/*   $P2=PA21079 JBB2220 860721 PDLR: Device Service Exit support   */ 05410500
*/*   $P3=PVT0382 HBB6606 970119 PDNO: - Update CPYRIGHT information */ 05411600
*/*                                    - Add External Class      @P3A*/ 05412700
*/*                                                                  */ 05414000
*/*   $T1=TVT0330 HBB6606 971208 PDNO: Erase Exit Support        @T1A*/ 05417500
*/*                                                                  */ 05421000
*/*   $01=OY47085 JBB3313 910911 PDMC: Support for OY43794.      @01A*/ 05425000
*/*                                                                  */ 05437500
*/********************************************************************/ 05450000
*/*                                                                     05500000
.SKIP    ANOP                                                           05550000
         GBLB  &ERROR                 Error flag (also set by IOSDNAME) 05600000
         LCLA  &AWK                   Arithmetic work variable          05650000
         LCLA  &LPERP                   ERPEXIT Type=N flag for &LPAB1  05700000
         LCLA  &LPDDR                   DDREXIT Type=N flag for &LPAB2  05750000
         LCLA  &SIOF,&TRPF,&UNSF,&SNSF  Operand flags for &VALB1        05800000
         LCLA  &EOSF,&TCCF,&ERPF,&MIHF  Operand flags for &VALB1        05850000
         LCLA  &ERAS,&DSEF,&DDRF,&CPSF  Operand flags for &VALB2   @T1C 05900000
         LCLA  &SSYF,&RERPF             Operand flags for &VALB2   @L2C 05950000
         LCLA  &MAXSUB                Maximum number of sublist entries 06000000
         LCLB  &LPAB1(8)              LPA Bit Map Byte 1                06050000
         LCLB  &LPAB2(8)              LPA Bit Map Byte 2                06100000
         LCLB  &VALB1(8)              Validity Bit Map Byte 1           06150000
         LCLB  &VALB2(8)              Validity Bit Map Byte 2           06200000
         LCLC  &DDRPRE,&ERPPRE        Prefix for DDREXIT and ERPEXIT    06250000
         LCLC  &DDRSUF,&ERPSUF        Suffix for DDREXIT and ERPEXIT    06300000
         LCLC  &CWK                   Character work variable           06350000
.*                                                                      06400000
&ERROR   SETB  0                      Initialize error code             06450000
.*                                      (&ERROR also set by IOSDNAME)   06500000
         ACTR  10000                  Set for infinite loop control     06550000
.*                                    Validity Bit Map Byte 1 settings  06600000
&SIOF    SETA    1         X'80'       SIOEXIT specified                06650000
&TRPF    SETA    2         X'40'       TRPEXIT specified                06700000
&UNSF    SETA    3         X'20'       UNSEXIT specified                06750000
&SNSF    SETA    4         X'10'       SNSEXIT specified                06800000
&EOSF    SETA    5         X'08'       EOSEXIT specified                06850000
&TCCF    SETA    6         X'04'       TCCWTAB specified                06900000
&ERPF    SETA    7         X'02'       ERPEXIT specified                06950000
&MIHF    SETA    8         X'01'       MIHEXIT specified                07000000
.*                                    Validity Bit Map Byte 2 settings  07050000
&ERAS    SETA    1         X'80'       ERASEXIT specified          @T1A 07075000
&DSEF    SETA    2         X'40'       DSEXIT  specified                07100000
&DDRF    SETA    3         X'20'       DDREXIT specified                07150000
&CPSF    SETA    4         X'10'       CPSEXIT specified                07200000
&SSYF    SETA    5         X'08'       SSYSID  specified                07250000
&RERPF   SETA    6         X'04'       RESERP  specified           @L2A 07300000
.*                                    LPA Bit Map Byte 1 settings       07350000
&LPDDR   SETA    3         X'20'       DDREXIT contains a 4 character   07400000
.*                                      suffix ID, not an address       07450000
.*                                    LPA Bit Map Byte 2 settings       07500000
&LPERP   SETA    7         X'02'       ERPEXIT contains a 4 character   07550000
.*                                      suffix ID, not an address       07600000
.*                                                                      07650000
.********************************************************************** 07700000
.*                                                                    * 07750000
.*       Validate label                                               * 07800000
.*                                                                    * 07850000
.********************************************************************** 07900000
.*                                                                      07950000
         AIF   (T'&LABEL NE 'O').ENDLAB  Label was specified            08000000
         MNOTE 8,'*** LABEL IS MISSING ON MACRO INVOCATION'             08050000
&ERROR   SETB  1                      Indicate error                    08100000
.ENDLAB  ANOP                                                           08150000
.*                                                                      08200000
.********************************************************************** 08250000
.*                                                                    * 08300000
.*       Validate CPSEXIT             entry point name                * 08350000
.*                                                                    * 08400000
.********************************************************************** 08450000
         ANOP                                                           08500000
         AIF   (T'&CPSEXIT EQ 'O').ENDCPS CPSEXIT not specified         08550000
&VALB2(&CPSF)  SETB 1                 Indicate CPSEXIT specified        08600000
         AIF   (N'&CPSEXIT GT 1).CPSSUB CPSEXIT is in sublist format    08650000
         IOSDNAME PARM=&CPSEXIT(1),KEY=CPSEXIT Validate characters      08700000
         AGO   .ENDCPS                Check next keyword                08750000
.CPSSUB  MNOTE 8,'CPSEXIT PARAMETER MAY NOT BE A SUBLIST.'              08800000
&ERROR   SETB  1                      Indicate error                    08850000
.ENDCPS  ANOP                                                           08900000
.*                                                                      08950000
.********************************************************************** 09000000
.*                                                                    * 09050000
.*       Validate DDREXIT          (entry point or module name,type)  * 09100000
.*                                                                    * 09150000
.********************************************************************** 09200000
         ANOP                                                           09250000
&MAXSUB  SETA  2                      Maximum number of sublists        09300000
         AIF   ('&DDREXIT' EQ '').ENDDDR  DDREXIT not specified         09350000
&VALB2(&DDRF)  SETB 1                     Indicate DDREXIT specified    09400000
         AIF   (N'&DDREXIT GT &MAXSUB).DDRSUB   Too many operands       09450000
.*                                                                      09500000
.********************************************************************** 09550000
.*       Validate type                N | A                           * 09600000
.********************************************************************** 09650000
         ANOP                                                           09700000
         AIF   ('&DDREXIT(2)' EQ 'A').CKDDRNM Address (A) specified     09750000
&LPAB2(&LPDDR)  SETB 1                        Indicate TYPE=N           09800000
         AIF   (T'&DDREXIT(2) EQ 'O').CKDDRNM Name (N) defaulted        09850000
         AIF   ('&DDREXIT(2)' EQ 'N').CKDDRNM Name (N) specified        09900000
         MNOTE 8,'DDREXIT PARAMETER SUBLIST VALUE FOR TYPE MUST BE A OR*09950000
                N.'                                                     10000000
&ERROR   SETB  1                      Indicates error                   10050000
.********************************************************************** 10100000
.*       Validate entry point or module name                          * 10150000
.********************************************************************** 10200000
.CKDDRNM ANOP                                                           10250000
         AIF   (T'&DDREXIT(1) EQ 'O').NODDRNM DDREXIT not specified     10300000
         IOSDNAME PARM=&DDREXIT(1),KEY=DDREXIT  Validate characters     10350000
         AIF   (&LPAB2(&LPDDR) EQ 0).ENDDDR   DDREXIT is an address     10400000
         AIF   (K'&DDREXIT(1) LE 4).DDRSHRT   DDR name is too short     10450000
&DDRPRE  SETC  '&DDREXIT(1)'(1,4)     Isolate DDR prefix                10500000
&AWK     SETA  K'&DDREXIT(1)-4        Number of characters in suffix    10550000
&DDRSUF  SETC  '&DDREXIT(1)'(5,&AWK)  Isolate DDR suffix                10600000
         AGO   .ENDDDR                Check next keyword                10650000
.DDRSHRT MNOTE 8,'DDREXIT MODULE NAME MUST BE AT LEAST 5 CHARACTERS.'   10700000
&ERROR   SETB  1                      Indicates error                   10750000
         AGO   .ENDDDR                Check next keyword                10800000
.NODDRNM MNOTE 8,'ENTRY POINT OR MODULE NAME REQUIRED FOR DDREXIT PARAM*10850000
               ETER.'                                                   10900000
&ERROR   SETB  1                      Indicates error                   10950000
         AGO   .ENDDDR                Check next keyword                11000000
.*                                                                      11050000
.DDRSUB  MNOTE 8,'DDREXIT PARAMETER EXCEEDS &MAXSUB SUBLIST ENTRIES.'   11100000
&ERROR   SETB  1                      Indicates error                   11150000
.ENDDDR  ANOP                                                           11200000
.*                                                                      11250000
.********************************************************************** 11300000
.*                                                                    * 11350000
.*       Validate DSEXIT              entry point name                * 11400000
.*                                                                    * 11450000
.********************************************************************** 11500000
         ANOP                                                           11550000
         AIF   (T'&DSEXIT EQ 'O').ENDDS  DSEXIT not specified           11600000
&VALB2(&DSEF)  SETB 1                    Indicate DSEXIT specified      11650000
         AIF   (N'&DSEXIT GT 1).DSSUB    DSEXIT is in sublist format    11700000
.*                                                                      11750000
         IOSDNAME PARM=&DSEXIT(1),KEY=DSEXIT Validate characters        11800000
         AGO   .ENDDS                    Check next keyword             11850000
.DSSUB   MNOTE 8,'DSEXIT PARAMETER MAY NOT BE A SUBLIST.'               11900000
&ERROR   SETB  1                         Indicate error                 11950000
.ENDDS   ANOP                                                           12000000
.*                                                                      12050000
.********************************************************************** 12100000
.*                                                                    * 12150000
.*       Validate EOSEXIT             entry point name                * 12200000
.*                                                                    * 12250000
.********************************************************************** 12300000
         ANOP                                                           12350000
         AIF   (T'&EOSEXIT EQ 'O').ENDEOS EOSEXIT not specified         12400000
&VALB1(&EOSF)  SETB 1                 Indicate EOSEXIT specified        12450000
         AIF   (N'&EOSEXIT GT 1).EOSSUB EOSEXIT is in sublist format    12500000
         IOSDNAME PARM=&EOSEXIT(1),KEY=EOSEXIT Validate characters      12550000
         AGO   .ENDEOS                Check next keyword                12600000
.EOSSUB  MNOTE 8,'EOSEXIT PARAMETER MAY NOT BE A SUBLIST.'              12650000
&ERROR   SETB  1                      Indicate error                    12700000
.ENDEOS  ANOP                                                           12750000
.*                                                                      12800000
.********************************************************************** 12850000
.*                                                                    * 12900000
.*       Validate ERPEXIT          (entry point or module name,type)  * 12950000
.*                                                                    * 13000000
.********************************************************************** 13050000
         ANOP                                                           13100000
&MAXSUB  SETA  2                      Maximum number of sublists        13150000
         AIF   ('&ERPEXIT' EQ '').NOERP  ERPEXIT not specified          13200000
&VALB1(&ERPF)  SETB 1                 Indicate ERPEXIT specified        13250000
         AIF   (N'&ERPEXIT GT &MAXSUB).ERPSUB   Too many operands       13300000
.*                                                                      13350000
.********************************************************************** 13400000
.*       Validate type                N | A                           * 13450000
.********************************************************************** 13500000
         ANOP                                                           13550000
         AIF   ('&ERPEXIT(2)' EQ 'A').CKERPNM Address (A) specified     13600000
&LPAB1(&LPERP) SETB 1                         Indicate TYPE=N           13650000
         AIF   (T'&ERPEXIT(2) EQ 'O').CKERPNM Name (N) defaulted        13700000
         AIF   ('&ERPEXIT(2)' EQ 'N').CKERPNM Name (N) specified        13750000
         MNOTE 8,'ERPEXIT PARAMETER SUBLIST VALUE FOR TYPE MUST BE A OR*13800000
                N.'                                                     13850000
&ERROR   SETB  1                      Indicates error                   13900000
.*                                                                      13950000
.********************************************************************** 14000000
.*       Validate entry point or module name                          * 14050000
.********************************************************************** 14100000
.CKERPNM ANOP                                                           14150000
         AIF   (T'&ERPEXIT(1) EQ 'O').NOERPNM ERPEXIT not specified     14200000
         IOSDNAME PARM=&ERPEXIT(1),KEY=ERPEXIT  Validate characters     14250000
         AIF   (&LPAB1(&LPERP) EQ 0).ENDERP   ERPEXIT is an address     14300000
         AIF   (K'&ERPEXIT(1) LE 4).ERPSHRT   ERP name is too short     14350000
&ERPPRE  SETC  '&ERPEXIT(1)'(1,4)     Isolate ERP prefix                14400000
&AWK     SETA  K'&ERPEXIT(1)-4        Number of characters in suffix    14450000
&ERPSUF  SETC  '&ERPEXIT(1)'(5,&AWK)  Isolate ERP suffix                14500000
         AGO   .ENDERP                Check next keyword                14550000
.ERPSHRT MNOTE 8,'ERPEXIT MODULE NAME MUST BE AT LEAST 5 CHARACTERS.'   14600000
&ERROR   SETB  1                      Indicates error                   14650000
         AGO   .ENDERP                Check next keyword                14700000
.NOERPNM MNOTE 8,'ENTRY POINT OR MODULE NAME REQUIRED FOR ERPEXIT PARAM*14750000
               ETER.'                                                   14800000
&ERROR   SETB  1                      Indicates error                   14850000
         AGO   .ENDERP                Check next keyword                14900000
.*                                                                      14950000
.ERPSUB  MNOTE 8,'ERPEXIT PARAMETER EXCEEDS &MAXSUB SUBLIST ENTRIES.'   15000000
&ERROR   SETB  1                      Indicates error                   15050000
         AGO   .ENDERP                Check next keyword                15100000
.NOERP   MNOTE 8,'ERPEXIT KEYWORD NOT SPECIFIED. IT IS REQUIRED.'       15150000
&ERROR   SETB  1                      Indicate error                    15200000
.ENDERP  ANOP                                                           15250000
.*                                                                      15300000
.********************************************************************** 15350000
.*                                                                    * 15400000
.*       Validate MIHEXIT             entry point name                * 15450000
.*                                                                    * 15500000
.********************************************************************** 15550000
         ANOP                                                           15600000
         AIF   (T'&MIHEXIT EQ 'O').ENDMIH  MIHEXIT not specified        15650000
&VALB1(&MIHF)  SETB 1                    Indicate MIHEXIT specified     15700000
         AIF   (N'&MIHEXIT GT 1).MIHSUB  MIHEXIT is in sublist format   15750000
         IOSDNAME PARM=&MIHEXIT(1),KEY=MIHEXIT Validate characters      15800000
         AGO   .ENDMIH                   Check next keyword             15850000
.MIHSUB  MNOTE 8,'MIHEXIT PARAMETER MAY NOT BE A SUBLIST.'              15900000
&ERROR   SETB  1                         Indicate error                 15950000
.ENDMIH  ANOP                                                           16000000
.*                                                                      16002500
.********************************************************************** 16005000
.*                                                                    * 16007500
.*       Validate ERASEXIT           entry point name             @T1A* 16010000
.*                                                                    * 16012500
.********************************************************************** 16015000
         ANOP                                                           16017500
         AIF   (T'&ERASEXIT EQ 'O').ENDERAS  ERASEXIT not               16020000
*                                        specified                @T1A  16022500
&VALB1(&ERAS)  SETB 1                    Indicate ERASEXIT              16025000
*                                        specified                @T1A  16027500
         AIF   (N'&ERASEXIT GT 1).ERASSUB  ERASEXIT is in sublist       16030000
*                                        format                   @T1A  16032500
         IOSDNAME PARM=&ERASEXIT(1),KEY=ERASEXIT Validate               16035000
*                                        characters               @T1A  16037500
         AGO   .ENDERAS                  Check next keyword       @T1A  16040000
.ERASSUB MNOTE 8,'ERASEXIT PARAMETER MAY NOT BE A SUBLIST.'       @T1A  16042500
&ERROR   SETB  1                         Indicate error           @T1A  16045000
.ENDERAS ANOP                                                     @T1A  16047500
.*                                                                      16050000
.********************************************************************** 16100000
.*                                                                    * 16150000
.*       Validate SIOEXIT             entry point name                * 16200000
.*                                                                    * 16250000
.********************************************************************** 16300000
         ANOP                                                           16350000
         AIF   (T'&SIOEXIT EQ 'O').NOSIOE  SIOEXIT not specified        16400000
&VALB1(&SIOF)  SETB 1                    Indicate SIOEXIT specified     16450000
         AIF   (N'&SIOEXIT GT 1).SIOSUB  SIOEXIT is in sublist format   16500000
         IOSDNAME PARM=&SIOEXIT(1),KEY=SIOEXIT Validate characters      16550000
         AGO   .ENDSIO                Check next keyword                16600000
.SIOSUB  MNOTE 8,'SIOEXIT PARAMETER MAY NOT BE A SUBLIST.'              16650000
&ERROR   SETB  1                      Indicate error                    16700000
         AGO   .ENDSIO                Check next keyword                16750000
.NOSIOE  MNOTE 8,'SIOEXIT KEYWORD NOT SPECIFIED. IT IS REQUIRED.'       16800000
&ERROR   SETB  1                      Indicate error                    16850000
.ENDSIO  ANOP                                                           16900000
.*                                                                      16950000
.********************************************************************** 17000000
.*                                                                    * 17050000
.*       Validate SNSEXIT             entry point name                * 17100000
.*                                                                    * 17150000
.********************************************************************** 17200000
         ANOP                                                           17250000
         AIF   (T'&SNSEXIT EQ 'O').ENDSNS   SNSEXIT not specified       17300000
&VALB1(&SNSF)  SETB 1                    Indicate SNSEXIT specified     17350000
         AIF   (N'&SNSEXIT GT 1).SNSSUB  SNSEXIT is in sublist format   17400000
         IOSDNAME PARM=&SNSEXIT(1),KEY=SNSEXIT Validate characters      17450000
         AGO   .ENDSNS                Check next keyword                17500000
.SNSSUB  MNOTE 8,'SNSEXIT PARAMETER MAY NOT BE A SUBLIST.'              17550000
&ERROR   SETB  1                      Indicate error                    17600000
.ENDSNS  ANOP                                                           17650000
.*                                                                      17700000
.********************************************************************** 17750000
.*                                                                    * 17800000
.*       Validate SSYSID              subsystem name                  * 17850000
.*                                                                    * 17900000
.********************************************************************** 17950000
         ANOP                                                           18000000
         AIF   (T'&SSYSID EQ 'O').ENDSSY SSYSID not specified           18050000
&VALB2(&SSYF)  SETB 1                    Indicate SSYSID specified      18100000
         AIF   (N'&SSYSID GT 1).SSYSUB   SSYSID is in sublist format    18150000
         IOSDNAME PARM=&SSYSID(1),KEY=SSYSID,MAXLEN=4  Validate chars   18200000
         AGO   .ENDSSY                   Check next keyword             18250000
.SSYSUB  MNOTE 8,'SSYSID PARAMETER MAY NOT BE A SUBLIST.'               18300000
&ERROR   SETB  1                         Indicate error                 18350000
.ENDSSY  ANOP                                                           18400000
.*                                                                      18450000
.********************************************************************** 18500000
.*                                                                    * 18550000
.*       Validate TCCWTAB             entry point name                * 18600000
.*                                                                    * 18650000
.********************************************************************** 18700000
         ANOP                                                           18750000
         AIF   (T'&TCCWTAB EQ 'O').NOTCCW  TCCWTAB not specified        18800000
&VALB1(&TCCF)  SETB 1                      Indicate TCCWTAB specified   18850000
         AIF   (N'&TCCWTAB GT 1).TCCSUB    TCCWTAB is in sublist format 18900000
         IOSDNAME PARM=&TCCWTAB(1),KEY=TCCWTAB Validate characters      18950000
         AGO   .ENDTCC                Check next keyword                19000000
.TCCSUB  MNOTE 8,'TCCWTAB PARAMETER MAY NOT BE A SUBLIST.'              19050000
&ERROR   SETB  1                      Indicate error                    19100000
         AGO   .ENDTCC                Check next keyword                19150000
.NOTCCW  MNOTE 8,'TCCWTAB KEYWORD NOT SPECIFIED. IT IS REQUIRED.'       19200000
&ERROR   SETB  1                      Indicate error                    19250000
.ENDTCC  ANOP                                                           19300000
.*                                                                      19350000
.********************************************************************** 19400000
.*                                                                    * 19450000
.*       Validate TRPEXIT             entry point name                * 19500000
.*                                                                    * 19550000
.********************************************************************** 19600000
         ANOP                                                           19650000
         AIF   (T'&TRPEXIT EQ 'O').NOTRPE  TRPEXIT not specified        19700000
&VALB1(&TRPF)  SETB 1                      Indicate TRPEXIT specified   19750000
         AIF   (N'&TRPEXIT GT 1).TRPSUB    TRPEXIT is in sublist format 19800000
         IOSDNAME PARM=&TRPEXIT(1),KEY=TRPEXIT Validate characters      19850000
         AGO   .ENDTRP                Check next keyword                19900000
.TRPSUB  MNOTE 8,'TRPEXIT PARAMETER MAY NOT BE A SUBLIST.'              19950000
&ERROR   SETB  1                      Indicate error                    20000000
         AGO   .ENDTRP                Check next keyword                20050000
.NOTRPE  MNOTE 8,'TRPEXIT KEYWORD NOT SPECIFIED. IT IS REQUIRED.'       20100000
&ERROR   SETB  1                      Indicate error                    20150000
.ENDTRP  ANOP                                                           20200000
.*                                                                      20250000
.********************************************************************** 20300000
.*                                                                    * 20350000
.*       Validate UNSEXIT             entry point name                * 20400000
.*                                                                    * 20450000
.********************************************************************** 20500000
         ANOP                                                           20550000
         AIF   (T'&UNSEXIT EQ 'O').ENDUNS  UNSEXIT not specified        20600000
&VALB1(&UNSF)  SETB 1                      Indicate UNSEXIT specified   20650000
         AIF   (N'&UNSEXIT GT 1).UNSSUB    UNSEXIT is in sublist format 20700000
         IOSDNAME PARM=&UNSEXIT(1),KEY=UNSEXIT Validate characters      20750000
         AGO   .ENDUNS                Check next keyword                20800000
.UNSSUB  MNOTE 8,'UNSEXIT PARAMETER MAY NOT BE A SUBLIST.'              20850000
&ERROR   SETB  1                      Indicate error                    20900000
.ENDUNS  ANOP                                                           20950000
.*                                                                      21000000
.********************************************************************** 21050000
.*                                                                    * 21100000
.*       Validate RESERP              entry point name            @L2A* 21150000
.*                                                                    * 21200000
.********************************************************************** 21250000
         ANOP                                                      @L2A 21300000
         AIF   (T'&RESERP EQ 'O').ENDRERP  RESERP not specified    @L2A 21350000
&VALB2(&RERPF) SETB 1                      Indicate RESERP specified   X21400000
                                                                   @L2A 21450000
         AIF   (N'&RESERP GT 1).RERPSUB    RESERP is in sublist format X21500000
                                                                   @L2A 21550000
         IOSDNAME PARM=&RESERP(1),KEY=RESERP Validate characters   @L2A 21600000
         AGO   .ENDRERP               Check next keyword           @L2A 21650000
.RERPSUB MNOTE 8,'RESERP PARAMETER MAY NOT BE A SUBLIST.'          @L2A 21700000
&ERROR   SETB  1                      Indicate error               @L2A 21750000
.ENDRERP ANOP                                                      @L2A 21800000
.*                                                                      21850000
.********************************************************************** 21900000
.*                                                                    * 21950000
.*       Verify that the DDR and ERP prefixes are the same            * 22000000
.*                                                                    * 22050000
.********************************************************************** 22100000
         ANOP                                                           22150000
         AIF   (T'&DDREXIT EQ '').ENDPRE     DDREXIT not specified      22200000
         AIF   (&LPAB2(&LPDDR) EQ 0).ENDPRE   DDREXIT is an address     22250000
         AIF   (&LPAB1(&LPERP) EQ 0).ENDPRE   ERPEXIT is an address     22300000
         AIF   (K'&DDREXIT(1) LT 4).BADPRE    DDR prefix is too short   22350000
         AIF   (K'&ERPEXIT(1) LT 4).BADPRE    ERP prefix is too short   22400000
         AIF   ('&DDRPRE' EQ '&ERPPRE').ENDPRE  Prefixes match          22450000
.BADPRE  MNOTE 8,'ERPEXIT AND DDREXIT MUST BEGIN WITH THE SAME FIRST 4 *22500000
               CHARACTERS.'                                             22550000
&ERROR   SETB  1                      Indicate error                    22600000
.ENDPRE  ANOP                                                           22650000
.*                                                                      22700000
.********************************************************************** 22750000
.*                                                                    * 22800000
.*       Generate text record                                         * 22850000
.*                                                                    * 22900000
.********************************************************************** 22950000
         ANOP                                                           23000000
         AIF   (&ERROR EQ 1).EXIT     Exit if an error has been found   23050000
&LABEL   CSECT                      DDT CSECT                           23100000
         DC    CL4'DDT '            Character ID (DDT)                  23150000
         DC    XL1'00'              Flag byte                           23200000
         DC    XL1'00'              Reserved                            23250000
         DC    XL2'0000'            Reserved                            23300000
         DS    0XL4                 Validity Bit Map                    23350000
&CWK     SETC  '&VALB1(1)&VALB1(2)&VALB1(3)&VALB1(4)'                   23400000
&CWK     SETC  '&CWK.&VALB1(5)&VALB1(6)&VALB1(7)&VALB1(8)'              23450000
         AIF   ('&CWK' EQ '00000000').NOVB1                             23500000
         DC    BL1'&CWK'            Bit Map Byte 1                      23550000
         AGO   .EGVB1               Done with VALB1                     23600000
.NOVB1   DC    XL1'00'              Bit Map Byte 1                      23650000
.EGVB1   ANOP                                                           23700000
&CWK     SETC  '&VALB2(1)&VALB2(2)&VALB2(3)&VALB2(4)'                   23750000
&CWK     SETC  '&CWK.&VALB2(5)&VALB2(6)&VALB2(7)&VALB2(8)'              23800000
         AIF   ('&CWK' EQ '00000000').NOVB2                             23850000
         DC    BL1'&CWK'            Bit Map Byte 2                      23900000
         AGO   .EGVB2               Done with VALB2                     23950000
.NOVB2   DC    XL1'00'              Bit Map Byte 2                      24000000
.EGVB2   ANOP                                                           24050000
         DC    XL1'00'              Bit Map Byte 3                      24100000
         DC    XL1'00'              Bit Map Byte 4                      24150000
         DS    0XL4                 LPA Bit Map                         24200000
&CWK     SETC  '&LPAB1(1)&LPAB1(2)&LPAB1(3)&LPAB1(4)'                   24250000
&CWK     SETC  '&CWK.&LPAB1(5)&LPAB1(6)&LPAB1(7)&LPAB1(8)'              24300000
         AIF   ('&CWK' EQ '00000000').NOLB1                             24350000
         DC    BL1'&CWK'            LPA Bit Map Byte 1                  24400000
         AGO   .EGLB1               Done with LPAB1                     24450000
.NOLB1   DC    XL1'00'              LPA Bit Map Byte 1                  24500000
.EGLB1   ANOP                                                           24550000
&CWK     SETC  '&LPAB2(1)&LPAB2(2)&LPAB2(3)&LPAB2(4)'                   24600000
&CWK     SETC  '&CWK.&LPAB2(5)&LPAB2(6)&LPAB2(7)&LPAB2(8)'              24650000
         AIF   ('&CWK' EQ '00000000').NOLB2                             24700000
         DC    BL1'&CWK'            LPA Bit Map Byte 2                  24750000
         AGO   .EGLB2               Done with LPAB2                     24800000
.NOLB2   DC    XL1'00'              LPA Bit Map Byte 2                  24850000
.EGLB2   ANOP                                                           24900000
         DC    XL1'00'              LPA Bit Map Byte 3                  24950000
         DC    XL1'00'              LPA Bit Map Byte 4                  25000000
         AIF   (&LPAB1(&LPERP) EQ 0).NOPRE    ERPEXIT is an address     25050000
         DC    CL4'&ERPPRE'         Four Character prefix to be         25100000
*                                    concatenated with 4 character      25150000
*                                    suffix to build LPA module names   25200000
         AGO   .EGPRE                 Done with UNSEXIT keyword         25250000
.NOPRE   DC    CL4'    '            Prefix for LPA module names         25300000
.EGPRE   ANOP                                                           25350000
         DS    0F                   Start of DDT entries (DDTENTRY)     25400000
         DC    V(&SIOEXIT(1))         SIO Exit Address                  25450000
         ANOP                                                           25500000
         DC    V(&TRPEXIT(1))         Trap Code Exit Address            25550000
         ANOP                                                           25600000
         AIF   (T'&UNSEXIT EQ 'O').NOUNS  UNSEXIT not specified         25650000
         DC    V(&UNSEXIT(1))         Unsolicited Interrupt Exit Addr   25700000
         AGO   .EGUNS                 Done with UNSEXIT keyword         25750000
.NOUNS   DC    A(0)                   Unsolicited Interrupt Exit Addr   25800000
.EGUNS   ANOP                                                           25850000
         AIF   (T'&SNSEXIT EQ 'O').NOSNS  SNSEXIT not specified         25900000
         DC    V(&SNSEXIT(1))         Sense Routine Exit Address        25950000
         AGO   .EGSNS                 Done with SNSEXIT keyword         26000000
.NOSNS   DC    A(0)                   Sense Routine Exit Address        26050000
.EGSNS   ANOP                                                           26100000
         AIF   (T'&EOSEXIT EQ 'O').NOEOS  EOSEXIT not specified         26150000
         DC    V(&EOSEXIT(1))         End-of-sense Exit Address         26200000
         AGO   .EGEOS                 Done with EOSEXIT keyword         26250000
.NOEOS   DC    A(0)                   End-of-sense Exit Address         26300000
.EGEOS   ANOP                                                           26350000
         DC    V(&TCCWTAB(1))         TCCW Op Table Address             26400000
         ANOP                                                           26450000
         AIF   (&LPAB1(&LPERP) EQ 1).SUFERP   ERPEXIT is an ID          26500000
         DC    V(&ERPEXIT(1))         ERP Message Writer Exit Address   26550000
         AGO   .EGERP                 Done with ERPEXIT keyword         26600000
.SUFERP  DC    CL4'&ERPSUF'           ERP Message Writer Exit suffix ID 26650000
.EGERP   ANOP                                                           26700000
         AIF   (T'&MIHEXIT EQ 'O').NOMIH  MIHEXIT not specified         26750000
         DC    V(&MIHEXIT(1))         MIH Exit Address                  26800000
         AGO   .EGMIH                 Done with MIHEXIT keyword         26850000
.NOMIH   DC    A(0)                   MIH Exit Address                  26900000
.EGMIH   ANOP                                                           26950000
         AIF   (T'&ERASEXIT EQ 'O').NOERAS ERASEXIT not specified  @T1A 26966600
         DC    V(&ERASEXIT(1))        Erase Exit Address           @T1A 26983200
         AGO   .EGERAS                Done with ERASEXIT keyword   @T1A 26999800
.NOERAS  DC    A(0)                   Erase Exit Address           @T1A 27016400
.EGERAS  ANOP                                                      @T1A 27033000
         AIF   (T'&DSEXIT EQ 'O').NODS    DSEXIT not specified          27050000
         DC    V(&DSEXIT(1))          Device Service Exit               27100000
         AGO   .EGDS                  Done with DSEXIT keyword          27150000
.NODS    DC    A(0)                   Device Service Exit               27200000
.EGDS    ANOP                                                           27250000
         AIF   (T'&DDREXIT EQ 'O').NODDR      DDREXIT not specified     27300000
         AIF   (&LPAB2(&LPDDR) EQ 1).SUFDDR   DDREXIT is an ID          27350000
         DC    V(&DDREXIT(1))         DDR Exit Address                  27400000
         AGO   .EGDDR                 Done with DDREXIT keyword         27450000
.SUFDDR  DC    CL4'&DDRSUF'           DDR Exit suffix ID                27500000
         AGO   .EGDDR                 Done with DDREXIT keyword         27550000
.NODDR   DC    A(0)                   DDR Exit ID or Address            27600000
.EGDDR   ANOP                                                           27650000
         AIF   (T'&CPSEXIT EQ 'O').NOCPS  CPSEXIT not specified         27700000
         DC    V(&CPSEXIT(1))         Channel Program Scan Exit Addr    27750000
         AGO   .EGCPS                 Done with CPSEXIT keyword         27800000
.NOCPS   DC    A(0)                   Channel Program Scan Exit Addr    27850000
.EGCPS   ANOP                                                           27900000
         AIF   (T'&SSYSID EQ 'O').NOSSY   SSYSID not specified          27950000
         DC    CL4'&SSYSID(1)'        Subsystem name for OPEN           28000000
         AGO   .EGSSY                 Done with SSYSID keyword          28050000
.NOSSY   DC    CL4'    '              Subsystem name for OPEN           28100000
.EGSSY   ANOP                                                           28150000
         AIF   (T'&RESERP EQ 'O').NORERP RESERP not specified      @L2A 28200000
         DC    V(&RESERP(1))          Resident ERP Address         @L2A 28250000
         AGO   .EGRERP                Done with RESERP keyword     @L2A 28300000
.NORERP  DC    A(0)                   Resident ERP Address         @L2A 28350000
.EGRERP  ANOP                                                      @L2A 28400000
         SPACE 1                                                        28450000
.*                                                                      28500000
.********************************************************************** 28550000
.*                                                                    * 28600000
.*       Exit                                                         * 28650000
.*                                                                    * 28700000
.********************************************************************** 28750000
.*                                                                      28800000
.EXIT    MNOTE ' '                    Null MNOTES allow spacing to      28850000
         MNOTE ' '                     occur if NOGEN specified         28900000
         MEND                                                           28950000
