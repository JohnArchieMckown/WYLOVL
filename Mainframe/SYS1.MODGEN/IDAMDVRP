         MACRO                                                          00050000
&NAME    IDAMDVRP &SHRPOOL=,&TYPE=,&STNAME=,&CTOKEN=,&VTOKEN=,         X00100000
               &VSIZE=,&MF=I                                            00150000
.*                                                                      00200000
.*****************************************************************      00216600
.*                                                                      00233200
.* $MAC(IDAMDVRP) COMP(SC1DE):                                          00250000
.*                                                                      00300000
.*PROPRIETARY V3 STATEMENT                                              00350000
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00400000
.*"RESTRICTED MATERIALS OF IBM"                                         00450000
.*5695-DF1                                                              00500000
.*(C) COPYRIGHT 1994,1995  IBM CORP.                                    00550000
.*END PROPRIETARY V3 STATEMENT                                          00600000
.*                                                                      00700000
.*****************************************************************      00716600
.*                                                                      00733200
.* STATUS = DFSMS/MVS VERSION 1 RELEASE 2.0                             00750000
.*                                                                      00752500
.* PROLOG:                                                              00755000
.*                                                                      00757500
.* RESTRICTIONS:                                                        00760000
.*   THIS MACRO EXPECTS THE CALLER TO BE IN 31 BIT ADDRESSING MODE.     00762500
.*   THE LIST FORM DOES NOT PROCESS THE STNAME, CTOKEN, VTOKEN OR       00765000
.*  VSIZE PARAMETERS, SO THEY MUST ALL BE SPECIFIED ON THE EXECUTE      00767500
.*  FORM.                                                               00770000
.*   THE LIST FORM DOES NOT PROCESS THE SHARE POOL ID, SO IT MUST       00772500
.*  BE SPECIFIED ON THE EXECUTE FORM.                                   00775000
.*                                                                      00777500
.* INPUT PARAMETERS:                                                    00780000
.*  SHRPOOL=  SHAREPOOL_ID                                              00782500
.*  TYPE   =  DATA|INDEX                                                00785000
.*  STNAME =  STRUCTURE_NAME                                            00787500
.*  CTOKEN =  CONNECT_TOKEN                                             00790000
.*  VTOKEN =  VECTOR_TOKEN                                              00792500
.*  VSIZE  =  VECTOR_SIZE                                               00795000
.*  MF     =  I|L|E                                                     00797500
.*                                                                      00800000
.* CHANGE ACTIVITY =                                                    00850000
.* $LT=DFSMS,HDZ11B0,07/20/93,SJPLTLD: DFSMS/MVS V1 R2.0 -MACRO CREATED 00875000
.* $LA=DFSMS,HDZ11B0,08/21/93,SJPLTLD: LIST FORM REPORTED PROBS    @LAA 00900000
.* OW19226,HDZ11C0,03/20/96,SJPLTLD: NEED TO SHIP MACRO IN 1.3.0        00912500
.*****************************************************************      00925000
.*                                                                      00950000
&NDX     SETC  '&SYSNDX'                                                01000000
         LCLA  &ORCODE                  CODES TO BE TURNED ON IN LIST   01050000
         LCLA  &ANDCODE                 CODES TO BE TURNED OFF IN LIST  01100000
         LCLA  &I                       WORK VARIABLES                  01150000
         LCLA  &DISP                    DISPLACEMENT TO ACB             01200000
         LCLA  &XILEN                   XI LENGTH WORK VARIABLE         01250000
         LCLB  &MFE                     EXECUTE FORM INDICATOR          01300000
         LCLB  &MFL                     LIST FORM INDICATOR             01350000
         LCLB  &ERR                     ERROR INDICATOR                 01400000
         LCLB  &R31B                    BUFFERS ABOVE THE LINE          01450000
         LCLB  &R31C                    CONTROL BLOCKS ABOVE            01500000
         LCLB  &INXPOOL                 ON IF INDEX RESOURCE POOL       01550000
         LCLB  &XI                      ON IF XI TOKENS SPECIFIED       01600000
         LCLB  &POOL                    SHRPOOL KEYWORD SPECIFIED       01650000
         LCLB  &POOLR                   SHRPOOL KEYWORD REG NOTATION    01700000
         LCLC  &EREG                    EXECUTE FORM LIST ADDR REG      01750000
         LCLC  &LAB                     ACB LABEL                       01800000
         LCLC  &AMODE                   ADDRESSING MODE OF MACRO        01850000
         LCLC  &VECSIZE                 ADDRESS OF VECTOR SIZE          01900000
         LCLC  &CONTKN                  ADDRESS OF CONNECT TOKEN        01950000
         LCLC  &VECTKN                  ADDRESS OF LOCAL VECTOR TOKEN   02000000
         LCLC  &STRNAME                 ADDRESS OF STRUCTURE NAME       02050000
.*                                                                      02100000
&ORCODE  SETA  128+64                   INDICATE DB/DC AND IDAMDVRP     02150000
&ANDCODE SETA  255-32                   RESET DLVRP                     02200000
&DISP    SETA  8                        CHANGE TO DISPL = 8             02250000
&AMODE   SETC  '31'                                                     02300000
&LAB     SETC  'IDAB&SYSNDX.'           GETNERATE ACB LABEL             02350000
&XILEN   SETA  0                        DEFAULT XI LENGTH TO 0          02400000
&INXPOOL SETB  0                        DEFAULT TO DATA POOL            02450000
&XI      SETB  0                        DEFAULT TO NO CROSS INVALD      02500000
&POOL    SETB  0                                                        02550000
&POOLR   SETB  0                                                        02600000
&VECSIZE SETC  ' '                      INITIALIZE TO BLANKS            02650000
&CONTKN  SETC  ' '                      INITIALIZE TO BLANKS            02700000
&VECTKN  SETC  ' '                      INITIALIZE TO BLANKS            02750000
&STRNAME SETC  ' '                      INITIALIZE TO BLANKS            02800000
.*                                                                      02850000
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS    02900000
.POSLOOP ANOP                                                           02950000
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND       03000000
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE             03050000
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE POS OPERANDS      03100000
.NOPOS   ANOP                                                           03150000
         AIF   ('&MF' EQ 'I').MFB       GO IF STANDARD FORM             03200000
         AIF   ('&MF' EQ 'L').MFLST     GO IF LIST FORM                 03250000
         AIF   (N'&MF LT 2).MFERR       GO IF NOT ENOUGH PARMS          03300000
         AIF   ('&MF(1)' NE 'E').MFERR  GO IF NOT EXECUTE FORM          03350000
&MFE     SETB  1                        INDICATE EXECUTE FORM           03400000
         AGO   .MFB                     GO AROUND ERROR                 03450000
.MFLST   ANOP                                                           03500000
&MFL     SETB  1                        INDICATE EXECUTE FORM           03550000
         AGO   .MFB                     GO AROUND ERROR                 03600000
.MFERR   ANOP                                                           03650000
         IDAERMAC 3,MF,&MF              ISSUE ERROR MNOTE               03700000
&ERR     SETB  1                        INDICATE ERROR                  03750000
.MFB     ANOP                                                           03800000
&ORCODE  SETA  &ORCODE+16               INDICATE LOCALLY SHARED         03850000
&ANDCODE SETA  &ANDCODE-8               RESET GLOBALLY SHARED           03900000
.*                                                                      03950000
.*****************************************************************      04000000
.*   TYPE PROCESSING                                                    04050000
.*     TYPE=DATA IS DEFAULT IF TYPE IS NOT SPECIFIED.                   04100000
.*     RMODE31 AND LOC=ANY ARE ASSUMED                                  04150000
.*****************************************************************      04200000
.TYPEDTA AIF   ('&TYPE' EQ '' OR '&TYPE' EQ 'DATA').CHKXI               04250000
         AIF   ('&TYPE' EQ 'INDEX').INDEX                               04300000
         IDAERMAC  3,TYPE,&TYPE         INVALID PARAMETER VALUE         04350000
&ERR     SETB  1                        INDICATE ERROR                  04400000
         AGO   .CONTIN                  GO CHECK OTHER KEYWORDS         04450000
.INDEX   ANOP                                                           04500000
&INXPOOL SETB  1                        TYPE=INDEX WAS SPECIFIED        04550000
.CHKXI   ANOP                                                           04600000
         AIF  (&MFL).SETXI              IF LIST, SKIP MESSAGE           04625000
         AIF (('&CTOKEN' NE '') AND ('&VTOKEN' NE '') AND              X04650000
               ('&STNAME' NE '') AND ('&VSIZE' NE '')).SETXI            04700000
         IDAERMAC 27,CTOKEN,VTOKEN,STNAME,VSIZE                         04750000
.SETXI   ANOP                                                           04800000
&XI      SETB  1                        XI SPECIFIED                    04850000
&XILEN   SETA  100                      BLPSIZEC                        04900000
&VECSIZE SETC  '&VSIZE'                 SAVE ADDR(VECTOR SIZE)          04950000
&CONTKN  SETC  '&CTOKEN'                SAVE ADDR(CONNECT TOKEN)        05000000
&VECTKN  SETC  '&VTOKEN'                SAVE ADDR(VECTOR TOKEN)         05050000
&STRNAME SETC  '&STNAME'                SAVE ADDR(STRUCTURE NAME)       05100000
&R31B    SETB  1                        DEFAULT BFRS 31 BIT             05150000
&R31C    SETB  1                        DEFAULT CBS 31 BIT              05200000
.CONTIN  ANOP                                                           05250000
.*                                                                      05300000
.*****************************************************************      05350000
.*   CHECK IF SHRPOOL SPECIFED.  RMODE31 = YES AND LOC=ANY ARE          05400000
.*   THE DEFAULT VALUES                                                 05450000
.*****************************************************************      05500000
.SHRPL   AIF   ('&SHRPOOL' EQ '').NOSHRPL  SHRPOOL NOT SPECIFIED?       05550000
         AIF   ('&SHRPOOL'(1,1) NE '(').SHPNGPR  IF NOT REG NOTATION    05600000
&POOLR   SETB  1                        SET REG NOTATION INDIC          05650000
         AGO   .SHRCONT                                                 05700000
.SHPNGPR ANOP                                                           05750000
&POOL    SETB  1                        SET POOL SPECIFIED INDIC        05800000
         AGO   .SHRCONT                                                 05850000
.NOSHRPL ANOP                                                           05900000
&POOL    SETB  0                                                        05950000
.SHRCONT ANOP                                                           06000000
         AIF   ('&MF' EQ 'I').SHRLIST   IF STANDARD FORMAT SPECIFIED    06050000
         AGO   .SHRPLC                                                  06100000
.SHRLIST ANOP                                                           06150000
         AIF   (&POOL).SHRNGPR          POOL SPECIFIED, NOT REGISTER    06200000
         AIF   (&POOLR).SHRGPR          POOL SPECIFIED, REG NOTATION    06250000
         MVI   SH&NDX,X'00'             SET POOL ID                     06300000
         AGO   .SHRPLC                                                  06350000
.SHRNGPR ANOP                                                           06400000
         MVC   SH&NDX,&SHRPOOL          MOVE SHARE POOL ID              06450000
         AGO   .SHRPLC                                                  06500000
.SHRGPR  ANOP                                                           06550000
         MVC   SH&NDX,0&SHRPOOL         STORE REGISTER CONTENTS         06600000
.SHRPLC  ANOP                                                           06650000
.*                                                                      06700000
.*     GENERAL NOTE: THE COUNT ATTRIBUTE (K') IS THE NUMBER OF CHARS    06750000
.*     IN ANY MACRO OPERAND; THE NUMBER ATTRIBUTE (N') IS THE NUMBER    06800000
.*     OF SUBLIST ENTRIES IN THE MACRO OPERAND WHERE SUBLIST ITEMS      06850000
.*     ARE SEPARATED BY A COMMA.                                        06900000
.*                                                                      06950000
         AIF   (&ERR).END               GO IF ANY ERRORS                07000000
         AIF   (&MFE).EXEC              GO IF EXECUTE FORM              07050000
         AIF   (&MFL).LIST              GO IF LIST FORM                 07100000
         AIF   ('&NAME' EQ '').NAMEB1   GO IF NO NAME                   07150000
.*                                                                      07200000
.*****************************************************************      07250000
.*  NOT MF=E,MF=L, CREATE IDABLPRM FOR STANDARD MACRO FORMAT            07300000
.*****************************************************************      07350000
&NAME    DS    0H                                                       07400000
.NAMEB1  ANOP                                                           07450000
         AIF   ('&CONTKN'(1,1) NE '(').CTNGPR                           07500000
         MVC   CT&NDX,0&CONTKN                                          07550000
         AGO   .CTCONT                                                  07600000
.CTNGPR  ANOP                                                           07650000
         MVC   CT&NDX,&CONTKN                                           07700000
.CTCONT  ANOP                                                           07750000
         AIF   ('&VECTKN'(1,1) NE '(').VTNGPR                           07800000
         MVC   VT&NDX,0&VECTKN                                          07850000
         AGO   .VTCONT                                                  07900000
.VTNGPR  ANOP                                                           07950000
         MVC   VT&NDX,&VECTKN                                           08000000
.VTCONT  ANOP                                                           08050000
         AIF   ('&STRNAME'(1,1) NE '(').STNGPR                          08100000
         MVC   ST&NDX,0&STRNAME                                         08150000
         AGO   .STCONT                                                  08200000
.STNGPR  ANOP                                                           08250000
         MVC   ST&NDX,&STRNAME                                          08300000
.STCONT  ANOP                                                           08350000
         AIF   ('&VECSIZE'(1,1) NE '(').VSNGPR                          08400000
         MVC   VS&NDX,0&VECSIZE                                         08450000
         AGO   .VSCONT                                                  08500000
.VSNGPR  ANOP                                                           08550000
         MVC   VS&NDX,&VECSIZE                                          08600000
.VSCONT  ANOP                                                           08650000
         CNOP  0,4                      ALIGN ON WORD BOUNDARY          08700000
&I       SETA  112                      CALL DISP TO SVC                08750000
         BAL   1,*+&I                                                   08850000
.GENACB  ANOP                                                           09050000
         OPEN  (&LAB),MF=L,MODE=&AMODE GENERATE OPEN PARM LIST          09100000
.*                                 ADD LABEL TO FOLLOWING LINE          09150000
&LAB     DC    X'A011'                  ID AND SUBTYPE                  09200000
         DC    AL2(0)                   RESERVED                        09250000
         DC    A(*+96)                  BUFFER LIST ADDRESS             09300000
         DC    AL1(0)                   ZERO KEY LENGTH                 09350000
         DC    AL1(1)                   STRING NUMBER = 1               09400000
         DC    AL1(&ORCODE)             VRP CODES                       09450000
         DC    AL1(0)                   RESERVED                        09500000
         DC    1A(0)                    RESERVED                        09550000
         DC    2A(0)                    RESERVED                        09600000
         DC    XL3'0'                   RESERVED                        09650000
         DC    X'08'                    DSORG                           09700000
SH&NDX   DC    AL1(0)                   SHARED RESOURCE POOL ID         09750000
         DC    B'00&INXPOOL&R31C&XI.000' TYPE FLAGS                     09800000
         DC    AL1(0)                   RESERVED                        09850000
         DC    AL1(0)                   ELEMENT SIZE 0                  09900000
         DC    2A(0)                    RESERVED                        09950000
         DC    AL2(&XILEN)              SET XI LENGTH                   09983300
         DC    AL2(0)                                                   10016600
         DC    A(0)                     RESERVED                        10050000
         DC    X'02'                    OFLAGS                          10100000
         DC    XL2'0'                   RESERVED                        10150000
         DC    X'00'                    MUST BE ZERO                    10200000
CT&NDX   DC    CL16' '                  PASSED CONNECT TOKEN            10250000
VT&NDX   DC    CL12' '                  PASSED VECTOR TOKEN             10300000
ST&NDX   DC    CL16' '                  PASSED STRUCTURE NAME           10350000
VS&NDX   DC    XL4'0'                   PASSED VECTOR SIZE              10400000
         AGO   .ISUSVC                  GO IF NOT LIST FORM             10450000
         MEXIT                                                          10500000
.*                                                                      10550000
.*****************************************************************      10600000
.*  MF=L (LIST FORM) USED TO DETERMINE LENGTH OF PARM LIST FOR          10650000
.*        REENTRANT MODULES.  TO INVOKE IDAMDVRP, THE EXECUTE           10700000
.*        FORM MUST BE SPECIFIED WITH ALL PARAMETERS                    10750000
.*****************************************************************      10800000
.*                                                                      10850000
.LIST    ANOP                                                           10900000
         AIF   ('&NAME' EQ '').NAMEL1   GO IF NO NAME                   10950000
.*                                                                      11000000
&NAME    DS    0F                                                       11400000
.NAMEL1  ANOP                                                           11425000
.GENACBL ANOP                                                           11450000
         OPEN  (&LAB),MF=L,MODE=&AMODE GENERATE OPEN PARM LIST          11500000
&LAB     DC    X'A011'                  ID AND SUBTYPE                  11550000
         DC    AL2(0)                   RESERVED                        11600000
         DC    A(*+96)                  BUFFER LIST ADDRESS             11650000
         DC    AL1(0)                   ZERO KEY LENGTH                 11700000
         DC    AL1(1)                   STRING NUMBER = 1               11750000
         DC    AL1(&ORCODE)             VRP CODES                       11800000
         DC    AL1(0)                   RESERVED                        11850000
         DC    1A(0)                    RESERVED                        11900000
         DC    2A(0)                    RESERVED                        11950000
         DC    XL3'0'                   RESERVED                        12000000
         DC    X'08'                    DSORG                           12050000
         DC    AL1(0)                   SHARED RESOURCE POOL ID         12100000
         DC    B'00&INXPOOL&R31C&XI.000' TYPE FLAGS                     12150000
         DC    AL1(0)                   RESERVED                        12200000
         DC    AL1(0)                   ELEMENT SIZE 0                  12250000
         DC    2A(0)                    RESERVED                        12300000
         DC    AL2(&XILEN)              SET XI LENGTH                   12333300
         DC    AL2(0)                                                   12366600
         DC    A(0)                     RESERVED                        12400000
         DC    X'02'                    OFLAGS                          12450000
         DC    XL2'0'                   RESERVED                        12500000
         DC    X'00'                    MUST BE ZERO                    12550000
         DC    CL16' '                  PASSED CONNECT TOKEN            12600000
         DC    CL12' '                  PASSED VECTOR TOKEN             12650000
         DC    CL16' '                  PASSED STRUCTURE NAME           12700000
         DC    XL4'00'                  PASSED VECTOR SIZE              12750000
         MEXIT                                                          12800000
.*                                                                      12850000
.*****************************************************************      12900000
.*  MF=E (EXECUTE FORM)                                                 12950000
.*****************************************************************      13000000
.EXEC    ANOP                                                           13050000
         AIF   ('&NAME' EQ '').NAMEB2   GO IF NO NAME                   13100000
&NAME    DS    0H                                                       13150000
.NAMEB2  ANOP                                                           13200000
.*                                                                      13250000
.* 31-BIT PROCESSING: OPEN (AT ISUSVX) PARAMETER LIST IS 8 BYTES,       13300000
.* SO ADD 8 BYTES TO REG1 TO REFERENCE IDABLPRM FILEDS                  13350000
         AIF   ('&MF(2)'(1,1) EQ '(').REG31A GO IF LST ADDR IN REG      13400000
         LA    1,&MF(2)                 LOAD LIST ADDRESS               13450000
         AGO   .REG31C                  GO AROUND REG CODE              13500000
.REG31A  AIF   ('&MF(2)' NE '(0)').REG31B LIST ADDR NOT IN R0           13550000
         LR    1,0                      ALSO PUT ADDR IN R0             13600000
         AGO   .REG31C                  GO AROUND REG CODE              13650000
.REG31B  AIF   ('&MF(2)' EQ '(1)').REG31C LIST ADDR ALREADY IN 1        13700000
&I       SETA  K'&MF(2)-2               CALC LENGTH OF REG NAME         13750000
&EREG    SETC  '&MF(2)'(2,&I)           PICK UP REG NAME                13800000
         LR    1,&EREG                  LOAD LIST ADDRESS               13850000
.REG31C  NI    18(1),&ANDCODE           REMOVE OLD VRP CODES            13900000
         OI    18(1),&ORCODE            INSERT NEW VRP CODES            13950000
         AIF   ('&SHRPOOL' EQ '').REG31F  BRANCH IF NO SHRPOOL          14000000
         AIF   ('&SHRPOOL'(1,1) NE '(').SPNGPR  NOT REG NOTATION        14050000
         MVC   36(1,1),0(&SHRPOOL)      STORE POOL ID                   14100000
         AGO   .REG31F                                                  14150000
.SPNGPR  ANOP                                                           14200000
         MVC   36(1,1),&SHRPOOL         MOVE POOL ID                    14250000
.REG31F  ANOP                                                           14300000
         AIF   ('&TYPE(1)' EQ '').REG31G  TYPE NOT SPECIFIED            14350000
         NI    37(1),B'11&INXPOOL.11111'  RESET TYPE FLAG               14400000
.REG31G  ANOP                                                           14450000
         OI    37(1),B'&R31B.0&INXPOOL&R31C.0000'   SET FLAGS           14500000
         OI    37(1),B'0000&XI.000'     SET XI BIT FOR THIS IDAMDVRP    14550000
.REGCT   AIF   ('&CONTKN'(1,1) NE '(').CTGPR                            14600000
         MVC   60(16,1),0&CONTKN                                        14650000
         AGO   .CTCONTR                                                 14700000
.CTGPR   ANOP                                                           14750000
         MVC   60(16,1),&CONTKN                                         14800000
.CTCONTR ANOP                                                           14850000
         AIF   ('&VECTKN'(1,1) NE '(').VTGPR                            14900000
         MVC   76(12,1),0&VECTKN                                        14950000
         AGO   .VTCONTR                                                 15000000
.VTGPR   ANOP                                                           15050000
         MVC   76(12,1),&VECTKN                                         15100000
.VTCONTR ANOP                                                           15150000
         AIF   ('&STRNAME'(1,1) NE '(').STGPR                           15200000
         MVC   88(16,1),0&STRNAME                                       15250000
         AGO   .STCONTR                                                 15300000
.STGPR   ANOP                                                           15350000
         MVC   88(16,1),&STRNAME                                        15400000
.STCONTR ANOP                                                           15450000
         AIF   ('&VECSIZE'(1,1) NE '(').VSGPR                           15500000
         MVC   104(4,1),0&VECSIZE                                       15550000
         AGO   .VSCONTR                                                 15600000
.VSGPR   ANOP                                                           15650000
         MVC   104(4,1),&VECSIZE                                        15700000
.VSCONTR ANOP                                                           15750000
         AGO   .ISUSVX                                                  15800000
.ISUSVX  ANOP                                                           15850000
         OPEN  (&DISP.(1)),MF=(E,(1)),MODE=&AMODE ISSUE OPEN SVC        15900000
         AGO   .END                                                     15950000
.ISUSVC  ANOP                                                           16000000
         OPEN  ,MF=(E,(1)),MODE=&AMODE                                  16050000
.END     ANOP                                                           16100000
         MEND                                                           16150000
