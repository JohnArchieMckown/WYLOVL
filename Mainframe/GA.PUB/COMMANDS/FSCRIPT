XPROC () BEGIN
; FETCH a SCRIPT job and create WYLBUR OPTIONS line
; XCALL FCRIPT  'jobid'
; Mark C. Lawrence (GA.MCL)  12/11/89
; Last revision 07/13/93

SET ESC &
DECLARE STRING JOBID RESULT JOBNAM JOBNUM S2 YN PFORMAT FLINE FFD FORMS
DECLARE STRING LDEST
DECLARE BOOLEAN WAITING SCRIPTMG SCRIPT PHOTO DUPLEX POSTS BITCON
DECLARE NUM I OLDACT
OLDACT=ACTNO
IF (LINES EQ 0) OLDACT=0
LDEST='LOCAL'
IF (MACHINE EQ 'RLG') LDEST='RLIN'
WAITING=FALSE
WRI 'FSCRIPT:  Fetch SCRIPT Output                Version of 07/13/93'
WRI ' '
OPEN FETCHWK TEMP TITLE 'WORK FILE FOR FSCRIPT'
JOBID=STRIP(PARM_STRING)
WHILE (JOBID EQ '') BEGIN
   REA STR JOBID PRO 'Enter job ID to fetch (RETURN=*): ' ATTN=NOTFOUND
   JOBID=STRIP(JOBID)
   IF (JOBID EQ '') JOBID='*'
   IF (JOBID EQ '?') BEGIN ; help
      WRI 'Enter the jobname or job number, or * for last job run.'
      JOBID=''
      END
   END

LOCATE:
CLR ACT
LOC &(JOBID) COLLECT
RESULT=UPP(LINE(FIRST))
IF (INDEX(RESULT,'BEING RECEIVED') NE 0) BEGIN ; Being received
   LIST UNN ATTN=NOTFOUND
   WRI 'Waiting...' ATTN=NOTFOUND
   WAITING=TRUE
   WAIT 10 ATTN=NOTFOUND
   GOTO LOCATE
   END
IF (INDEX(RESULT,'EXECUTING') NE 0) BEGIN ; being executed
   LIST UNN ATTN=NOTFOUND
   YN=YESNO('reply YES to fetch, NO to wait: ','YES,NO,ATTN')
   IF (YN EQ 'ATTN') GOTO NOTFOUND
   IF (YN EQ 'YES') GOTO FETCH
   WAITING=TRUE
   Wri 'Waiting...' ATTN=NOTFOUND
   TRY SYNC * WAIT=30
   IF (TRYATTN) GOTO NOTFOUND
   GOTO LOCATE
   END
IF ((SUB(RESULT,1,4) NE 'JOB ' AND SUB(RESULT,1,4) NE 'STC ') OR INDEX(RESULT,'NOT FOUND') NE 0) BEGIN
   LIST UNN ATTN=NOTFOUND                      ; not found
   GOTO NOTFOUND
   END

FETCH:

JOBNAM=SUB(RESULT,10,8)
JOBNUM=SUB(RESULT,5,4)
CLR ACT
FDD='SCRIPT.SYSPRINT CC'
POSTS=FALSE
SCRIPTMG=FALSE
PHOTO=FALSE
FET &(JOBID) JCL
POI 'PGM=POSTS' NOL
IF (* GT 0) BEGIN ; PostScript
   POSTS=TRUE
   FDD='POSTS.SYSOUT'
   GOTO FDATA
   END
POI 'PGM=BITCON' NOL
IF (* GT 0) BEGIN ; BITCON:  PostScript-to-IMG
   BITCON=TRUE
   FDD='PRINT.IMGOUT CC'
   IF (LDEST EQ 'RLIN') LDEST='STANFORD'
   GOTO FDATA
   END
POI 'PGM=SCRIPTMG' NOL
IF (* GT 0) BEGIN ; SCRIPTMG/MGPHOTO
   SCRIPTMG=TRUE
   FDD='CONVERT.SYSOUT CC'
   IF (LDEST EQ 'RLIN') LDEST='SPECIAL'
   GOTO FDATA
   END
POI 'PGM=PHOTO' NOL
IF (* GT 0) BEGIN ; PHOTO, NOT MGPHOTO
   PHOTO=TRUE
   FDD='PHOTO.OUTPUT CC'
   IF (LDEST EQ 'RLIN') LDEST='SPECIAL'
   POI 'EXEC ' AND 'PFORMAT=' NOL
   IF (* LT 0) BEGIN
      WRI 'ERROR, can't get PFORMAT'
      GOTO NOTFOUND
      END
   REA STR FLINE USI *
   I=INDEX(FLINE,'PFORMAT=')
   PFORMAT=SUBSTR(FLINE,I+8,6)
   GOTO FDATA
   END
POI 'PGM=SCRIPT' NOL
IF (* GT 0) BEGIN ; PLAIN SCRIPT
   SCRIPT=TRUE
   PFORMAT='DR12'
   POI 'EXEC ' AND 'CHARS=' NOL
   IF (* LT 0) GOTO FDATA
   REA STR FLINE USI *
   I=INDEX(FLINE,'CHARS=')
   PFORMAT=SUBSTR(FLINE,I+6,4)
   GOTO FDATA
   END

; Fell thru, job unidentified

WRI 'JOB '||JOBID||' is not a SCRIPT job.'
GOTO NOTFOUND

FDATA:

; get form code

POI '*OUTPUT' AND 'FORMS=' NOL
IF (* LT 0) POI '*JOBPARM' AND 'FORMS=' NOL
IF (* LT 0) BEGIN ; No forms
   FORMS='BOND'
   GOTO DCHECK
   END
REA STR FLINE USI *
I=INDEX(FLINE,'FORMS=')
FORMS=SUBSTR(FLINE,I+6,4)

; Duplex option
DCHECK:
DUPLEX=FALSE
POI '*OUTPUT' AND 'DUPLEX=' NOL
IF (* LT 0) POI '*JOBPARM' AND 'DUPLEX=' NOL
IF (* LT 0) GOTO FTEXT ; no duplex option
REA STR FLINE USI *
I=INDEX(FLINE,'DUPLEX=')
FLINE=SUBSTR(FLINE,I+7,2)
IF (FLINE EQ 'YE') DUPLEX=TRUE

FTEXT:
FLINE='OPTIONS:UNN UNFORMAT NOEJECT '
IF (SCRIPTMG) FLINE=FLINE||'SYSOUT=G CC FCB=FULL DEST=&(LDEST) '
IF (BITCON) FLINE=FLINE||'SYSOUT=G CC FCB=FULL DEST=&(LDEST) '
IF (POSTS) FLINE='OPTIONS: POSTS '
IF (PHOTO) FLINE=FLINE||'CC TRC PFORMAT=&(PFORMAT) FCB=FULL DEST=&(LDEST) '
IF (SCRIPT) FLINE=FLINE||'CC CHARS='||PFORMAT||' '
FLINE=FLINE||'FORMS=&(FORMS) '
IF (DUPLEX) FLINE=FLINE||'DUPLEX '
CLOSE
OPEN
TRY FETCH &(JOBID) DD &(FDD)
SET ACT NAME=&(JOBNAM) TITLE='Fetched SCRIPT job &(JOBNUM) '
PUTLINE 0.0 &(FLINE)
WRI 'Done.  SCRIPT output with OPTIONS line is in your active file.'
IF (OLDACT NE 0) WRI 'Type PICK &(OLDACT) to restore your previous active file.'

GOTO OUT

NOTFOUND:
CLOSE
IF (OLDACT NE 0) PICK &(OLDACT)

OUT:
END
