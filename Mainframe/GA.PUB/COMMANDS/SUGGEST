consult
clear exec
;
; The following EXEC file is no longer used as of 1/1/86.
;
; -------------------------------------------------------------
; -------------------------------------------------------------
;
;  SUGGEST exec file.
;
;    Written, 9/84, "Niz"
;
clear values
set esc &
define $goto as "point ';" append " '1(1) exec nolist" replace
;
set val s0 upper(parm)
if (index(s0,'REP ')+index(s0,'REPL ')+index(s0,'REPLY ') ne 0)
  then write 'Please use the "MAIL TO gg.uuu REPLY nnnn" command to answer a'
  then write 'user suggestion.  If you have questions about the new syntax'
  then write 'send mail to GG.JDN.  Thank you.'
  then $goto DONE
;
if (lines ne 0) $goto HAVEACT
write 'Collect your suggestion in the ACTIVE file then issue the SUGGEST command.'
$goto DONEMSG
;
;HAVEACT
write 'Your ACTIVE file will be sent as a suggestion to the computer center staff.'
read string s0 prompt 'Press RETURN to continue, BREAK to abort... ' attn last
write ''
;
set val s0 upper(parm)
if (s0 ne '') $goto HAVEINP
;
;GETINPUT
Read string s0 prompt 'What subject (type ? for a list)? ' upper attn last
if (s0 eq '') $goto GETINPUT
;
;HAVEINP
if (s0 ne '?') $goto HAVENAME
list ~' '1 in 5000/5999 exec unn attn next  ; display list of subjects
write '' attn next
$goto GETINPUT
;
;HAVENAME
if (siz(s0) lt 3) set val s0 ' '||s0  ; too small
point '&s0'1 or ' NOMATCH:'1 (1) in 5000/5999 exec n
;
;SUGGEST
set sugno collect  ; get next suggestion number
read string s2 using * delete
set val s2 sub(s2,1,ind(s2,' ')-1) ; isolate suggestion number
;
try mail to &s1 title "Suggestion #&s2 &s3" quiet
write s2||' is your suggestion number.'
write 'Your message has been mailed to the computer center staff.'
$goto DONE
;
;DONEMSG
write 'No action taken.'
;
;DONE
undefine $goto
set esc
clear exec
;
;
Subject     Description
GENERAL     General questions or problems.
  set val s1 '(GA.SUG,GG.AHJ,GG.JDN,GA.JRG)'
  set val s3 'GENERAL'
  $goto SUGGEST
RATES       Questions/suggestions about computing rates.
  set val s1 '(GA.SUG,GD.ESW)'
  set val s3 'RATES'
  $goto SUGGEST
FORSYTHE    Comments about the Forsythe Computer Building.
  set val s1 '(GA.SUG,GP.PAB,GR.JLV)'
  set val s3 'FORSYTHE'
  $goto SUGGEST
TERMINALS   Questions/comments about terminals, modems, etc.
  set val s1 '(GA.SUG,GT.CYA)'
  set val s3 'TERMINALS'
  $goto SUGGEST
9700        Questions/comments about the Xerox 9700 printer.
  set val s1 '(GA.SUG,GA.CJJ,GG.JWS,GG.SLP)'
  set val s3 '9700'
  $goto SUGGEST
BATCH       Questions/comments about batch job processing.
  set val s1 '(GA.SUG,GA.JRG,GG.SLP)'
  set val s3 'BATCH'
  $goto SUGGEST
WYLBUR      Questions/comments about the WYLBUR text editor.
  set val s1 '(GA.SUG,GG.JDN,GG.SCH,GG.LSO,GG.AHJ)'
  set val s3 'WYLBUR'
  $goto SUGGEST
ORVYL       Questions/comments about the ORVYL timesharing system.
  set val s1 '(GA.SUG,GG.RAL,GG.BPB)'
  set val s3 'ORVYL'
  $goto SUGGEST
SPIRES      Questions/comments about the SPIRES data base system.
  set val s1 '(GA.SUG,GQ.ELL,GQ.VVN,GQ.PBT)'
  set val s3 'SPIRES'
  $goto SUGGEST
ADMIN       Questions/comments about the ADMIN functions.
  set val s1 '(GA.SUG,GE.JRH,HK.DEB)'
  set val s3 'ADMIN'
  $goto SUGGEST
SAMSON      Questions/comments about Samson Micro file transfer program.
  set val s1 '(GA.SUG,GG.JDN,GG.MPD,GG.AHJ,HK.DEB)'
  set val s3 'SAMSON'
  $goto SUGGEST
SCRIPT      Questions/comments about the SCRIPT text formatter.
  set val s1 '(GA.SUG,GA.CJJ)'
  set val s3 'SCRIPT'
  $goto SUGGEST
POTLUCK     Software contributions for (unsupported) public libraries.
  set val s1 '(GA.SUG,GA.JRG)'
  set val s3 'POTLUCK'
  $goto SUGGEST
EMS         Questions/comments about the Electronic Mail System.
  set val s1 '(GA.SUG,GE.EMS)'
  set val s3 'EMS'
  $goto SUGGEST
SOCRATES    Questions/comments about the Online Library Catalog.
  set val s1 '(GA.SUG,GA.SPI,EC.CAT)'
  set val s3 'SOCRATES'
  $goto SUGGEST
PRISM       Questions/comments about PRISM.
  set val s1 '(GA.SUG,GA.SPI,GQ.PUB,GQ.VVN)'
  set val s3 'PRISM'
  $goto SUGGEST
 NOMATCH:
  write s0||' is an unknown subject (type ? for a list of subjects).'
  write ''
  $goto GETINPUT
;
;
$goto DONEMSG
