XPROC (XFORM,RESULT) BEGIN
; FORMCHEK     Author:  GA.MCL
; Check a forms code.  Form code supplied in FORM.  Result of test
; returned in RESULT.  Possible values are:
;
; INVALID      Not a syntactically valid form code
; XEROX        Form valid for Xerox 4090/9790 printers
; 3800         Form valid for IBM 3800 printer
; IMPACT       Form valid on impact printer (not yet implemented)
; UNKNOWN      Form code not recognized

; Last Revision 07/02/90

DECLARE STRING FORM

;  List of allowable 4090/9790 forms:
DECLARE STRING FORM4090
FORM4090  =  ' BOND 3HOL BLUE ARCB RAGB GBAR 3LAN '

;  List of allowable 3800 forms:
DECLARE STRING FORM3800
FORM3800 = ' 9511 1181 1281 BLNK '

;  List of impact printer forms:
DECLARE STRING FORM1403
FORM1403 = ' 1411 1481 AVRX AVR1 AVR3 BLNK CHS1 CHS2 CHS3 1012 1013 1183 1184 1412 1413 1414 8511 8512 8513 8514 '

RESULT = 'INVALID'
FORM = UPP(STRIP(XFORM))
IF (SIZE(FORM) NE 4) GOTO QUIT ; Invalid code, done
IF (VERIFY(FORM,'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789') NE 0) GOTO QUIT

RESULT = 'UNKNOWN'

IF (INDEX(FORM4090,FORM) NE 0) RESULT = 'XEROX'
ELSEIF (INDEX(FORM3800,FORM) NE 0) RESULT = '3800'
ELSEIF (INDEX(FORM1403,FORM) NE 0) RESULT = 'IMPACT'

QUIT:
XRETURN (RESULT)
END
