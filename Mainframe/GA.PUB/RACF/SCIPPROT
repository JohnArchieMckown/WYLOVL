;
;;  SCIPPROT -- SET/SHOW PROTECT for a Stanford protected data set.
;;
;;  Called from the exec wyl.ga.pub.racf#showprot or #setprot
;;
;;  Parms:
;;     cmd     -- command to execute if this is a Stanford protected
;;                file (i.e., 'SET SPROTECT' or 'SHOW SPROTECT').
;;     options -- string containing everything after "SET/SHOW PROT".
;;     our_rc  -- this routine returns either "OURS" meaning that
;;                we handled it, or "NOT OURS" meaning that
;;                someone else needs to handle it.
;;
;;  Created, 6/6/88, Niz
;

xproc (cmd, options, our_rc) begin
   on error cmd 'xreturn error errid=BADSCIPP errmsg=""'
   dcl str dsname, fulldsn wordpos

;  Isolate dsname -- If SET SPROTECT then anything before the word 'for'
;                    except DELETE, RESET, ON vol, OWNER xxx
;                    If SHOW SPROTECT then the whole option (except ALL or WITH)

   if (lower(cmd) eq 'set sprotect') begin
      wordpos = index(lower(' '||options), ' for ')
      if (wordpos ne 0) dsname = left(options, wordpos-1)
      else dsname = options
   end

   if (lower(cmd) eq 'show sprotect') begin
      dsname = options
   end

   if (lower(cmd) eq 'dump sprotect') begin
      dsname = options
   end


;  See if prefix set

   dsname = STRIP(dsname)
   if (dsname EQ '*') BEGIN
      if (PREFIX NE '') GOTO TEST_DSNFORMAT
   END

;  See if any wildcards

   if (find(dsname,'*%#') NE 0) begin
      our_rc = 'NOT OURS'
      xreturn (our_rc)
   end

;  IF words 'DELETE' or 'ALL' or 'WITH' then 'NOT OURS'

   if ((ind(lower(dsname),' delete') gt 0) or  \
         (ind(lower(dsname),' with ') gt 0) or \
         (ind(lower(dsname),'profile ') eq 1) or \
         (ind(lower(dsname),'all') eq 1)) BEGIN
      our_rc= 'NOT OURS'
      xreturn (our_rc)
   end

; IF words 'RESET' or 'ON' or 'OWNER' then remove everything to right
   wordpos = ind(lower(dsname), ' reset')
   if (wordpos ne 0) dsname = left(dsname, wordpos-1)
   wordpos = ind(lower(dsname), ' on ')
   if (wordpos ne 0) dsname = left(dsname, wordpos-1)
   wordpos = ind(lower(dsname), ' owner ')
   if (wordpos ne 0) dsname = left(dsname, wordpos-1)

;  Ask Wylbur who protects this file.

TEST_DSNFORMAT:

   if (strip(dsname) ne '') begin
      fulldsn = dsnformat(dsname)
      ptype = dsnprot(fulldsn)        ; who protects this file

      if (ptype ne 'RACF') begin
         our_rc = 'OURS'
         try &(cmd) &(options)
         if (tryerr) xreturn error errmsg=''
         xreturn (our_rc)
      end
   end

   our_rc = 'NOT OURS'
   xreturn (our_rc)
end
