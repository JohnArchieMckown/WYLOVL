;
;  RACF command.
;
;    Written 1/87, Niz
;
xproc () begin
   on error cmd 'pcall ErrorExit'
   on attn  cmd 'pcall AttnExit'
   x.origact = actno

   if (wylbur eq 'WYLBUR') begin
      try write 'The RACF command is not yet available in WYLBUR.'
      xreturn
   end

   open racfjcl temp
   cmd = strip(parm_string)
   if (substr(cmd,1,1) eq ';') cmd = ''
   if (cmd ne '') begin
      putend &(cmd)
   end
   else begin
      try write 'Enter RACF commands (press BREAK when you are done)...'
      try collect 1 by 1
      if (lines eq 0) begin
         close
         try write 'No commands entered; no action taken.'
         xreturn
      end
      do_it = 'X'
      while (do_it ne 'YES' and do_it ne 'NO') begin
         try do_it = yesno('Execute these RACF commands')
         if (do_it eq 'ATTN') do_it = 'NO'
         if (do_it eq 'NULL') try write 'Answer YES or NO.'
      end
      if (do_it eq 'NO') begin
         try write 'No action taken.'
         close
         xreturn
      end
   end
   number 1 by 1
   putline .01 //&RACF JOB ,CLASS=F
   putline .02 //RACF     EXEC PGM=IKJEFT01
   putline .03 //SYSTSPRT DD  SYSOUT=A
   putline .04 //SYSPRINT DD  SYSOUT=A
   putline .05 //PRINTER  DD  SYSOUT=A
   putline .06 //SYSUT1   DD  UNIT=VIO,SPACE=(CYL,(10,05))
   putline .07 //SYSTSIN    DD  *
   run h quiet nonotify
   sync * wait 60
   open racfout temp
   fet * nom
   quiet pur *
   del '' or 'READY  '1 or 'END  '1 n
   try list unn
   close
   close
end


proc ErrorExit() begin
   while (actno ne x.origact) close
   try write 'An error occurred in the RACF command.'
   try write '  Error info is:'
   try write errinfo
   try write errmsg
   if (actno ne x.origact) pick &(x.origact)
   xreturn
end


proc AttnExit() begin
   try write 'BREAK interrupted command processing.'
   if (actno ne x.origact) pick &(x.origact)
   xreturn
end
