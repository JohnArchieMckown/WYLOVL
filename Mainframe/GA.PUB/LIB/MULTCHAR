CLEAR VARIABLES
COMMENT (THE PRINT COMMAND NOW SUPPORTS MULTIPLE CHARACTER SETS,
COMMENT SEE DOC#WYLBUR PUBLIC FOR DETAILS.)
COMMENT
COMMENT -EXECFILE TO PRINT A MULTI CHARACTER-SET FILE.
COMMENT -ENTER FULLY QUALIFIED DATA SET NAME (WYL.GG.UUU.XXX).
SET VALUE S9 ESCAPE
SET ESCAPE &
SET VALUE S8 CASE
SET CASE UPPER
SET VALUE W0 END
READ STRING S1 PROMPT '-DATA SET NAME? (<RETURN>=ACTIVE FILE) '
IF (S1 NE '') EXEC .13
SET VAL S1 'WYL.'||GROUP||'.'||USER||'.D'||SUBSTR(DATE,1,2)||SUBSTR(DATE,4,2)||SUBSTR(DATE,7,2)
SET VALUE S2 'UNIT=SYSDA,'
SET VALUE S3 'VOL=SER=SCR002,'
SAVE &S1 ON SCR002
EXEC 5
READ STRING S3 PRO '-VOLUME? (<RETURN>=CATLG) '
IF (S3 EQ 'SCR002') SET VALUE S2 'UNIT=SYSDA,'
ELSE IF (S3 NE '') SET VALUE S2 'UNIT=DISK,'
IF (S3 NE '') SET VALUE S3 'VOL=SER='||S3||','
END //   JOB
READ VALUE N0 PROMPT '-FORMS? (<RETURN>=1181) '
IF (N0 EQ 0) SET VALUE N0 1181
IF (N0 NE 1181) IF (N0 NE 9511) IF (N0 NE 1281) COMMENT -NONSTANDARD FORM SELECTED.
IF (N0 EQ 1181)  READ VALUE N1 PROMPT '-FCB? (<RETURN>=8) '
IF (N1 EQ 0) SET VALUE N1 8
IF (N0 EQ 1281)  READ VALUE N1 PROMPT '-FCB? (<RETURN>=8) '
IF (N1 EQ 0) SET VALUE N1 8
IF (N0 EQ 9511)  READ VALUE N1 PROMPT '-FCB? (<RETURN>=6) '
IF (N1 EQ 0) SET VALUE N1 6
IF (N1 NE 6) IF (N1 NE 8) IF (N1 NE 12) COMMENT -ILLEGAL FCB SELECTED.
IF (N1 NE 6) IF (N1 NE 8) IF (N1 NE 12) EXEC 9
IF (SUB('&(GROUP)',1,1) NE 'G')
THEN SET VALUE S7 'N'
THEN EXEC 14.1
READ STRING S7 PROMPT '-ID MAIL ADDRESS? (<RETURN>=NONE) '
IF (S7 NE '') END /*MAIL &S7
READ STRING S7 PROMPT '-BURST? (<RETURN>=NO) '
IF (S7 NE '') SET VALUE S7 = SUB(S7,1,1)
IF (S7 NE 'Y')
THEN IF (S7 NE 'N') SET VALUE S7 = 'N'
END /*JOBPARM LINES=999,DEST=ENCINA,FORMS=&N0,FCB=&N1,BURST=&S7
SET VALUE S7 = ''
IF (PARM NE '') END /*MAIL &(PARM)
READ VALUE N2 PROMPT '-COPIES? (<RETURN>=1) '
IF (N2 EQ 0) SET VALUE N2 1
READ STRING S0 PROMPT '-CARRIAGE CONTROL? (<RETURN>=NO) '
IF (S0 EQ '') SET VALUE S0 'NO'
IF (S0 EQ 'YES') SET VALUE S0 'A'
ELSE SET VALUE S0 ''
IF (S0 EQ '') COMMENT -CHARACTER SELECTION MUST BE IN COLUMN 1.
ELSE COMMENT -CHARACTER SELECTION MUST BE IN COLUMN 2.
READ STRING S4 PROMPT '-CHARACTER SET 0? '
IF (S4 EQ '') EXEC 27
READ STRING S5 PROMPT '-CHARACTER SET 1? '
IF (S5 EQ '') EXEC 29
READ STRING S6 PROMPT '-CHARACTER SET 2? (<RETURN>=NO SET) '
IF (S6 EQ '') EXEC 40
READ STRING S7 PROMPT '-CHARACTER SET 3? (<RETURN>=NO SET) '
IF (S7 EQ '') EXEC 40
END /*OUTPUT 3800 COPIES=&N2
END //  EXEC IOPROGM
END //OUTPUT DD SYSOUT=(A,,3800),DCB=(RECFM=FB&S0,OPTCD=J),
END //          CHARS=(&S4,&S5,&S6,&S7)
CHANGE ',,' TO '' IN CURRENT NOLIST
CHANGE ',)' TO ')' IN LAST NOLIST
END //INPUT DD &S2&S3DISP=SHR,
END // DSN='&S1'
IF (FIND('()',S1) GT 0) LAST // DSN=&S1
READ STRING S7 PROMPT '-RUN? (<RETURN>=YES) '
IF (S7 EQ '') SET VALUE S7 'YES'
IF (S7 EQ 'YES') RUN &W0/LAST
THEN DELETE &W0/LAST
ELSE COMMENT -ISSUE COMMAND "RUN &W0/LAST" WHEN READY.
SET CASE &S8
SET ESCAPE &S9
CLEAR EXEC
