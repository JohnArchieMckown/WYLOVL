;WYL.GA.PUB.LIB#UPDATE -- created 01/04/81 by Lincoln Ong, CIT Technical Support Services
;          latest modification 07/20/84  Version 1.07
;This EXEC files displays the articles in UPDATE (DOC#UPDATE PUBLIC).
;
;          Variables used:
;     N0 = number of articles in DOC#UPDATE PUBLIC
;     N1 = numbers of lines per article
;
;     S0 = original ESC setting
;     S1 = date and subject of article
;     S2 = user response
;     S3 = PRINT options
;
;     W0 = original DELTA
;     W1 = last line of original active file
;     W2 = first line of article
;     W3 = last line of article
;     W4 = line number of prompt section to return to
;     W5 = line number of Print after read prompt section to return to
;
CLR VAL
SET VAL S0=ESC
SET ESC &
SET VAL W0=DEL
SET DEL=1
SET VAL W1=END
SET VAL W4=68                          ;<--this should be changed if Prompt line moves from line 68.
SET VAL W5=107                         ;<--this should be changed if Print after read prompt line moves from line 107.
SET VAL W2=END
WRI '' ATTN=NEX
WRI 'UPDATE   Revised 20 July 1984   Version 1.07' ATTN=NEX
WRI '' ATTN=NEX
IF (IND(UPP(PARM),'SUM')+IND(UPP(PARM),'SUMMARY') EQ 0) WRI "Type '?' for help at a prompt, press <ATTN> to stop current process." ATTN=NEX
   THEN WRI '' ATTN=NEX

COP FRO DOC#UPDATE PUB
COU '*' 1 AND ~'**' 1 IN &W2/L COL      ;Count the number of articles.
REA VAL N0 USI L COL=1/&(IND(LIN(L),' ')-1) DEL
WRI '&N0 articles:' ATTN=*
SET RET=NEX
P '*' 1 AND ~'**' 1 (1) IN &W2/L NOL ATTN=NEX  ;Find first line of the article.
SET VAL W2=*
IF (* EQ -1.000) WRI 'No UPDATE articles.' ATTN=NEX
   THEN EXE L-4
P '*' 1 AND ~'**' 1 (1) IN &(W2+1)/L NOL ATTN=NEX  ;Find the first line of the next article.
SET VAL W3=PREV
IF (* EQ -1.000) SET VAL W3=L          ;If there are no further lines, then this is the last article.
SET VAL S1=LIN(W2)                     ;Read in the title line of the article.
SET VAL N1=W3-W2                       ;Count the number of lines in the article.
; - - - - - - - - - -
;Display format of the title prompt line:
;mm/dd/yy (nnn lines):  title of the article
;   |         |      |           |
;   |         |      |  from the UPDATE data set
;   |     created by the EXEC file
;from the UPDATE data set
;
;Internal format of the title line in the UPDATE data set.
;*mm/dd/yy  title of article
;|   |             |
;|   |      subject matter
;|date of submittal
;marker character  (not listed when viewed with UPDATE EXEC file).
; - - - - - - - - - -
;
;Prompt line
;
IF (IND(UPP(PARM),'SUM')+IND(UPP(PARM),'SUMMARY') EQ 0) WRI '' ATTN=NEX
WRI "&(SUB(S1,2,IND(S1,' ')-2)) (&(SUB('000',1,(3-SIZ(N1)))||N1) lines):&(SUB(S1,IND(S1,' ')+1))" ATTN=NEX
IF (IND(UPP(PARM),'SUM')+IND(UPP(PARM),'SUMMARY') NE 0) EXE L-8
REA UPP STR S2 PRO 'READ or PRINT article (<CR>=skip article, <ATTN>=quit): ' ATTN=L-4
IF (S2 EQ '') EXE L-8
IF (SUB(S2,SIZ(S2),1) EQ ' ') SET VAL S2=(SUB(S2,1,(SIZ(S2)-1)))  ;Deletes trailing blanks.
   THEN EXE PREV
SET VAL S2=SUB(S2,VER(S2||' ',' '))    ;Deletes leading blanks.
IF (IND('N.NO.',S2||'.') NE 0) EXE L-8
IF (IND(S2,' ') NE 0) SET VAL S3=SUB(S2,IND(S2,' ')+1)  ;If there is a space, then the second half of string is the print options.
   THEN SET VAL S2=SUB(S2,1,IND(S2,' ')-1)
IF (IND('?.L.LIS.LIST.N.NO.P.PRI.PRINT.R.REA.READ.Y.YES.',S2||'.') EQ 0) EXE &(W4+2)
IF (S2 EQ '?') WRI '' ATTN=&W4
   THEN WRI 'The following commands are valid:' ATTN=&W4
   THEN WRI 'READ             To read the article at the terminal.' ATTN=&W4
   THEN WRI 'LIST             Same as READ.' ATTN=&W4
   THEN WRI 'YES              Same as READ.' ATTN=&W4
   THEN WRI 'PRINT [options]  To print the article.  If no print options are specified,' ATTN=&W4
   THEN WRI '                 the article will be printed on the current output form' ATTN=&W4
   THEN WRI '                 setting, and numbered.' ATTN=&W4
   THEN WRI '?                To get these instructions.' ATTN=&W4
   THEN WRI '<CR>             Press the CARRIAGE RETURN key to skip article.' ATTN=&W4
   THEN WRI 'NO               Same as pressing the CARRIAGE RETURN key.' ATTN=&W4
   THEN WRI '<ATTN>           Press the BREAK or ATTN key to terminate current process' ATTN=&W4
   THEN WRI '                 or to quit.' ATTN=&W4
   THEN WRI '' ATTN=&W4
   THEN WRI 'All commands can be abbreviated to 1 or 3 characters.' ATTN=&W4
   THEN WRI '' ATTN=&W4
   THEN WRI 'To get the entire UPDATE, press the BREAK or ATTN key to exit' ATTN=&W4
   THEN WRI 'the EXEC file, then type:' ATTN=&W4
   THEN WRI '     USE DOC#UPDATE PUBLIC' ATTN=&W4
   THEN WRI '     LIST' ATTN=&W4
   THEN EXE &W4
IF (IND('P.PRI.PRINT.',S2||'.') NE 0) PRI &W2/&W3 ID=#UPDT &S3
   THEN EXE L-7
L &(W2+1)/&W3 UNN ATTN=&W4
;
;Print after read prompt
;
REA UPP STR S2 PRO 'Print article (YES or NO  <CR>=no, <ATTN>=quit): ' ATTN=L-4
IF (S2 EQ '') EXE L-8
IF (SUB(S2,SIZ(S2),1) EQ ' ') SET VAL S2=(SUB(S2,1,(SIZ(S2)-1)))  ;Deletes trailing blanks.
   THEN EXE PREV
SET VAL S2=SUB(S2,VER(S2||' ',' '))    ;Deletes leading blanks.
IF (IND('N.NO.',S2||'.') NE 0) EXE L-8
IF (IND(S2,' ') NE 0) SET VAL S3=SUB(S2,IND(S2,' ')+1)  ;If there is a space, then the second half of string is the print options.
   THEN SET VAL S2=SUB(S2,1,IND(S2,' ')-1)
IF (IND('?.N.NO.P.PRI.PRINT.Y.YES.',S2||'.') EQ 0) EXE &W5
IF (S2 EQ '?') WRI '' ATTN=&W5
   THEN WRI 'The following responses are allowed:' ATTN=&W5
   THEN WRI 'YES [options]    To print the article.  If no print options are specified,' ATTN=&W5
   THEN WRI '                 the article will be printed on the current output form' ATTN=&W5
   THEN WRI '                 setting, and numbered.' ATTN=&W5
   THEN WRI 'PRINT [options]  Same as YES.' ATTN=&W5
   THEN WRI '?                To get these instructions.' ATTN=&W5
   THEN WRI 'NO               To prevent printing the article.' ATTN=&W5
   THEN WRI '<CR>             Press the CARRIAGE RETURN key to prevent printing the article.' ATTN=&W5
   THEN WRI '<ATTN>           Press the BREAK or ATTN key to exit the EXEC file.' ATTN=&W5
   THEN WRI '' ATTN=&W5
   THEN WRI 'All commands can be abbreviated to 1 or 3 characters.' ATTN=&W5
   THEN WRI '' ATTN=&W5
   THEN EXE &W5
IF (IND('P.PRI.PRINT.Y.YES.',S2||'.') NE 0) PRI &W2/&W3 ID=#UPDT &S3
SET VAL W2=W3+1                        ;Advance to next article.
IF (W3 NE L) EXE RET                   ;If not last line of file, return to get next article.
WRI '' ATTN=NEX
WRI 'End of articles from UPDATE.' ATTN=NEX

DEL &W1/L
SET DEL=&W0
SET ESC=&S0
CLR VAL EXE
