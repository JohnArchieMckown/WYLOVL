;  SIMPLIFIED ORVYL/FORTRAN PROGRAM LINK AND LOAD PROCEDURE
;  LAST UPDATE:  JULY, 1983
;  PROBLEMS: SEE GG.LSO
;
;
CLEAR VALUES
SET ESC &
DEFINE GOTO AS "POINT C';" APPEND " '1(1) EXEC NOLIST" REPLACE
;
;        GIVE ENTRY MESSAGE
;
COMM
;START
COMM * SIMPLIFIED FORTLOAD EXEC FILE - LAST UPDATE JUNE, 1983
COMM * TO ACCESS PREVIOUS VERSION EXEC FROM #FORTLOAD PUBLIC
COMM
;
;
;        GET OBJECT DECK(S) INTO ACTIVE FILE
;
;OBJECT DECKS
COMM ENTER COMMANDS TO PLACE OBJECT DECK(S) INTO ACTIVE FILE.
COMM ENTER A <CR> WHEN FINISHED.  FOR HELP TYPE 'HELP'.
;
;READ
READ STRING S0 UPPER PROMPT '*? '
IF (S0 EQ '') GOTO LINK
IF (S0 EQ 'F') FET * SYS B
IF (S0 EQ 'F') GOTO LINK
IF (S0 EQ 'HELP') GOTO HELP
IF (S0 EQ 'H') GOTO HELP
IF (S0 EQ '?') GOTO HELP
&S0
GOTO READ
;
;
;        SET UP LINK CONTROL CARDS
;
;LINK
IF (LINES EQ 0) GOTO NOACTIVE
NUMBER QUIET
 0. PARM FORT,OSNT,AUTOCALL,LET,SIZE=64K
;;;   ----> user object deck(s) go here
END INCLUDE ORV.GG.PUB.UR2LIB   ALL
END INCLUDE ORV.GG.PUB.FORTSYS0 ALL
END SYSLIB  ORV.GG.PUB.FORTSYS1
END SYSLIB  ORV.GG.PUB.FORTSYS2
END SYSLIB  ORV.GG.PUB.FORTSYS3
END SYSLIB  ORV.GG.PUB.FORTSYS4
END LOADFILE ORV.&(GROUP).&(USER).PROGLIB
END ENTRY   MAIN
END NAME    progname
;
;        SET UP PROGRAM NAME
;
;NAME
READ STRING S0 UPPER PROMPT 'PROGRAM NAME  ? '
IF (S0 EQ '') GOTO NULLNAME
IF (S0 EQ 'HELPNAME') GOTO HELPNAME
IF (S0 EQ 'H') GOTO HELPNAME
IF (S0 EQ '?') GOTO HELPNAME
CHANGE 'progname' TO '&S0' IN LAST NOLIST
READ STRING S0 UPPER PROMPT 'OK TO REPLACE PROGRAM IF IT EXISTS ? '
IF (S0 EQ 'Y')   CHANGE 23/25 TO '(R)' IN LAST NOLIST
IF (S0 EQ 'YES') CHANGE 23/25 TO '(R)' IN LAST NOLIST
IF (S0 EQ 'OK')  CHANGE 23/25 TO '(R)' IN LAST NOLIST
;
;        LINK ACTIVE
;
LINK ACTIVE CLEAR
GOTO EXIT
;
;        ERROR HANDLING
;
;NOACTIVE
COMM NO OBJECT DECK IN ACTIVE FILE, TYPE 'EXEC' TO RESUME.
EXEC PAUSE
GOTO OBJECT
;
;NULLNAME
COMM INVALID (NULL) PROGRAM NAME, TYPE 'EXEC' TO RESUME.
EXEC PAUSE
GOTO OBJECT
;
;        HELP INFORMATION
;
;HELP
COMM YOU MUST PUT THE FORTRAN OBJECT DECK(S) OF YOUR PROGRAM INTO THE
COMM ACTIVE FILE.  THIS CAN BE DONE IN SEVERAL WAYS:
COMM
COMM          USE.. - THE 'USE' COMMAND OR ANY OTHER WYLBUR COMMANDS
COMM                  MAY BE USED TO MOVE OBJECT DECKS INTO THE ACTIVE
COMM                  FILE.
COMM
COMM          'F'   - TYPING 'F' IS THE SAME AS TYPING 'FET * SYS B'
COMM                  USE THIS COMMAND IF YOU USE #FORTCOMP EXEC FILE.
COMM                  THIS COMMAND WILL PUT THE OBJECT DECK OF THE
COMM                  LAST #FORTCOMP PROGRAM INTO THE ACTIVE FILE.
COMM
COMM          INCLUDE ... - YOU MAY INCLUDE AN ORVYL FILE(S) CONTAINING
COMM                  YOUR PROGRAM'S OBJECT DECK.  USE 'COLLECT' COMMAND
COMM                  TO PUT THE INCLUDE COMMANDS INTO THE ACTIVE FILE.
COMM
COMM          <CR>  - THIS TERMINATES THE '*?' PROMPTING FOR OBJECT
COMM                  DECKS.  YOUR PROGRAM WILL BE LINKED AND SAVED.
COMM
READ STRING S0 UPPER PROMPT 'DO YOU WANT MORE INFORMATION ? '
IF (S0 EQ 'N') GOTO OBJECT
IF (S0 EQ 'NO') GOTO OBJECT
COMM
COMM THIS EXEC FILE MAY BE USED TO LINK AND SAVE PROGRAMS COMPILED
COMM BY ANY IBM FORTRAN COMPILER (HX,H, OR G).  YOU MUST PLACE THE
COMM OBJECK DECK(S) OF YOU COMPILED PROGRAM IN THE ACTIVE FILE.
COMM THIS EXEC FILE WILL PROMPT FOR YOU TO DO THAT.  THE EXEC FILE
COMM WILL THEN PROMPT FOR THE PROGRAM FOR THE NAME OF YOUR PROGRAM.
COMM YOUR PROGRAM NAME MUST BE LESS THAN 8 CHARACTERS.  YOUR PROGRAM
COMM WILL BE SAVED IN THE PROGLIB FILE ON YOUR ACCOUNT.  AFTER YOUR
COMM PROGRAM HAS BEEN SAVED YOU MAY EXECUTE YOUR PROGRAM BY TYPING
COMM 'CALL <program name> CLEAR'.
COMM
COMM SHOULD YOU HAVE ANY PROBLEMS LINKING AND SAVING YOUR PROGRAM,
COMM PLEASE REPORT TO CONSULTING VIA MAIL OR AT THE CONSULTING DESK
COMM IN FORSYTHE HALL.  ALSO YOU MAY TRY LINKING YOUR PROGRAM USING
COMM A MORE COMPLICATED EXEC FILE BY TYPING, 'EXEC FROM #FORTLOAD PUB'.
COMM NOTE THAT THIS EXEC FILE DOES NOT PROCESS SYM CARD DEBUGGING
COMM INFORMATION.
COMM
COMM AS A NOTE OF EXPLANATION, THIS EXEC FILE PLACES THE FOLLOWING
COMM LINK EDIT CONTROL CARD INTO THE ACTIVE FILE AND THEN ISSUES A
COMM 'LINK ACTIVE' COMMAND.
COMM
COMM LINK CONTROL CARDS COPIED TO YOUR ACTIVE FILE:
COMM           PARM FORT,OSNT,AUTOCALL,LET,SIZE=64K
COMM            -----> your object decks included here
COMM           INCLUDE ORV.GG.PUB.UR2LIB   ALL
COMM           INCLUDE ORV.GG.PUB.FORTSYS0 ALL
COMM           SYSLIB  ORV.GG.PUB.FORTSYS1
COMM           SYSLIB  ORV.GG.PUB.FORTSYS2
COMM           SYSLIB  ORV.GG.PUB.FORTSYS3
COMM           SYSLIB  ORV.GG.PUB.FORTSYS4
COMM           LOADFILE ORV.gg.uuu.PROGLIB
COMM           ENTRY   MAIN
COMM           NAME    progname (R)
COMM
COMM A VERY BRIEF EXPLANATION OF THESE CONTROL CARDS FOLLOWS.
COMM INCLUDED WITH YOUR PROGRAM ARE A SET OF I/O SUBROUTINES
COMM (UR2LIB) AND A SET OF FORTRAN RUNTIME ROUTINES (FORTSYS0).
COMM OPTIONALLY NUMEROUS FORTRAN ROUTINES MAY OR MAY NOT BE
COMM INCLUDED. THESE SUBROUTINES ARE SPECIFIED BY THE SYSLIB STATMENTS.
COMM YOUR PROGRAM IS SAVED IN THE FILE SPECIFIED ON THE LOADFILE
COMM STATEMENT.  THE NAME STATEMENT SPECIFIES THE NAME OF YOUR
COMM PROGRAM. '(R)' SPECIFIES THAT IT IS OK TO REPLACE AN EXISTING
COMM PROGRAM OF THAT NAME.  FOR MORE DETAILED INFORMATION
COMM REFER TO THE 'ORVYL USERS GUIDE' (CHAPTER 3).
COMM
COMM A FINAL NOTE, THE LINK CONTROL CARDS AND OBJECT DECKS REMAIN IN
COMM YOUR ACTIVE FILE UPON COMPLETION OF EXEC FILE PROCESSING.
COMM
COMM                 ***************************
COMM
COMM
GOTO START
;
;        HELP FOR PROGRAM NAME
;
;HELPNAME
COMM MAKE UP A PROGRAM NAME, <progname>,  OF 8 CHARACTERS OR LESS.
COMM YOU MAY USE ANY NAME YOU LIKE. AFTER YOUR PROGRAM IS LINKED
COMM AND SAVED, YOU MAY EXECUTE IT BY ENTERING THE COMMAND:
COMM       'CALL <progname> CLEAR'
COMM
GOTO NAME
;
;
;        EXIT EXEC FILE
;
;EXIT
CLEAR EXEC
