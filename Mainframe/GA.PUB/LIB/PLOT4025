;WYL.GA.PUB.LIB#PLOT4025 -- (formerly WYL.GA.PUB.PLOT#ON4025)
;          rewritten 05/26/82 by Lincoln Ong, CIT Systems
;          created 12/18/79 by Lincoln Ong, CIT Technical Support Services
;          latest modification 09/25/84  Version 1.02
;This EXEC file is used to display processed plot files (sequential or unloaded pds)
;in either native 4025 mode or 4013 emulation mode on the Tektronix 4025, and/or
;to plot on the Tektronix 4662.
;
;[Since the Tektronix 4662 plotter is interfaced using the GPIB interface, 4013
;files cannot be plotted]
;
;          Variables used:
;   PARM = ([ACTIVE|WYL=dataset|ORV=file][,{CRT|PLOTTER}][,PENS={DEFAULT|(DEFAULT)|pen|(pen1,pen2,pen3,pen4)}]
;           [,NAME={PICTnnn|(PICTnnn,...,PICTnnn)}])
;
;           * NAME option has not been implemented yet *
;
;     N0 = original screen height setting
;     N1 = terminal mode
;          0 -- native Tektronix 4025
;          1 -- Tektronix 4013 emulation
;     N2 = Tektronix 4662
;          0 -- not selected
;          1 -- selected
;     N3 = plot file type (sequential or unloaded pds)
;          0 -- sequential
;          1 -- unloaded pds
;     N4 = error flag (invalid plot file, or missing ./ card)
;          0 -- no error
;          1 -- error
;     N5 = number of pens used
;     N6 = working variable
;
;     S0 = original ESC setting
;     S1 = command for retrieving processed plot file
;     S2 = PICTnnn plot list
;     S3 = * RESERVED *
;     S4 = selected pens
;     S5 = plot data set member (unloaded pds)
;     S6 = alias for plot data set member (unloaded pds)
;     S7 = working variable
;     S8 = working variable
;
;     W0 = original DELTA
;     W1 = last line of ACTIVE file
;     W2 = first line of the current plot
;     W3 = current line pointer of the current plot
;     W4 = line number for pen change
;     W5 = last line of the current plot
;     W9 = line number of the display plot section
;
DEF QUIT AS 'EXE L-6' REP              ;Exit command from the EXEC file.
CLR VAL
SET VAL S0=ESC
SET ESC &
SET VAL W0=DEL
SET DEL=1
DUM VAL PARM                           ;Clean up the PARM list.
CH '  ' TO '' IN L NOL                 ;Remove extra and leading spaces.
CH ' ,' TO ',' IN L NOL
CH ', ' TO ',' IN L NOL
REA UPP VAL S7 USI L DEL
IF (S7 NE '') SET VAL S7=SUB(S7,VER(S7||' ',' '))||','
EXE NEX PAR='&S7'
SET VAL N5=1
SET VAL N6=1
SET VAL W9=238                         ;<--this should be changed if Display area moves from line 238.
                                       ;Retrieve specified plot file.
IF (IND(PARM,'ACTIVE,') NE 0) SET VAL S1='ACTIVE'
IF (IND(PARM,'WYL=') NE 0) SET VAL S1=SUB(PARM,IND(PARM,'WYL=')+4)
   THEN SET VAL S1='USE '||SUB(S1,1,IND(S1,',')-1)
IF (IND(PARM,'ORV=') NE 0) SET VAL S1=SUB(PARM,IND(PARM,'ORV=')+4)
   THEN SET VAL S1='GET '||SUB(S1,1,IND(S1,',')-1)
                                       ;Display specified members.
IF (IND(PARM,'NAME=(') NE 0) SET VAL S2=SUB(PARM,IND(PARM,'NAME=(')+6)
   THEN SET VAL S2=SUB(S2,1,IND(S2,')')-1)||','
   THEN EXE *+3
IF (IND(PARM,'NAME=') NE 0) SET VAL S2=SUB(PARM,IND(PARM,'NAME=')+5)
   THEN SET VAL S2=SUB(S2,1,IND(S2,','))
IF (IND(PARM,'PLOTTER,') NE 0) SET VAL N2=1  ;Plotter was selected.
                                       ;Find the pen selection.
IF (IND(PARM,'PENS=(') NE 0) SET VAL S4=SUB(PARM,IND(PARM,'PENS=(')+6)
   THEN SET VAL S4=SUB(S4,1,IND(S4,')')-1)||','
   THEN EXE *+3
IF (IND(PARM,'PENS=') NE 0) SET VAL S4=SUB(PARM,IND(PARM,'PENS=')+5)
   THEN SET VAL S4=SUB(S4,1,IND(S4,','))
WRI '' ATTN=NEX
WRI 'PLOT4025  Revised 25 September 1984  Version 1.02' ATTN=NEX
WRI '' ATTN=NEX

IF (IND(PARM,'ACTIVE,')+IND(PARM,'WYL=')+IND(PARM,'ORV=') NE 0) IF (SIZ(S1) NE 0) WRI 'Plot file = &S1' ATTN=NEX
IF (S1 EQ 'ACTIVE') EXE *+5
IF (S1 NE '') EXE *+3
WRI 'Enter a command to put the processed plot file into the ACTIVE file.' ATTN=NEX
REA UPP STR S1 PRO '(<CR>=active, <ATTN>=exit): ' ATTN=L-1
&S1
IF (LINES EQ 0) EXE *-3
SET VAL W1=END
SET VAL W3=FIR

DUM TERM                               ;Dump the terminal settings.
POI 'HEIGHT=' NOL                      ;Find out the screen height setting.
REA STR S7 USI *
SET VAL N0=SUB(S7,IND(S7,'HEIGHT=')+7,2)
SET TERM NOH                           ;Set screen height to 0 for no page pausing.
DEL &W1/L                              ;Delete terminal settings from the ACTIVE file.
;
;Check plot mode (native Tektronix 4025 or Tektronix 4013 emulation)
;
POI '!WOR 33 H K' NOL
IF (* EQ -1.000) SET VAL N1=1          ;Tektronix 4013 Emulation mode.
   THEN WRI '!WOR 31 H !GRA 1,37 !SHR !MON K' ATTN=*
   ELSE WRI '!SHR N' ATTN=*            ;Native Tektronix 4025 mode.
POI './' 1 AND ' ADD ' NOL
IF (* EQ -1.000) SET VAL W5=LAS        ;Sequential processed plot file.
   ELSE SET VAL N3=1                   ;Unloaded PDS processed plot file.

IF (IND(PARM,'PLOTTER,') NE 0) IF (N1 EQ 1) WRI S'152F2F'||'Tektronix 4013 plot files are not displayable on the plotter.' ATTN=*
   THEN WRI 'CRT selected instead.' ATTN=*
IF (N1 EQ 1) SET VAL N2=0              ;Force display of the plot to CRT.
   THEN EXE *+12
IF (IND(PARM,'CRT,') NE 0) IF (N2 EQ 0) EXE *+11
IF (N2 EQ 1) EXE *+10                  ;The plotter was selected.
REA UPP STR S7 PRO 'Display plot(s) on the CRT or PLOTTER (<CR>=CRT): ' ATTN=*
IF (S7 EQ '') EXE *+8
IF (S7 EQ '?') EXE *+2
IF (IND('C.P.CRT.PLO.PLOTTER.',S7||'.') NE 0) EXE *+5
WRI 'Answer CRT if you wish to display the plot(s) on the screen, PLOTTER' ATTN=*
WRI 'if you wish to plot the plot(s) on the plotter.' ATTN=*
WRI '' ATTN=*
EXE *-7
IF (IND('P.PLO.PLOTTER.',S7||'.') NE 0) SET VAL N2=1  ;Plotter was selected.
;
;Pen selection
;
IF (N2 EQ 0) EXE *+34                  ;CRT was selected instead.
POI 'PEN=&N5' 1 (1) NOL                ;Find pen selection.
IF (* EQ -1.000) SET VAL N5=N5-1       ;No pens selections and/or changes were found.
   THEN EXE *+3
IF (N5 NE 4) SET VAL N5=N5+1           ;Increment to next pen selection.
   THEN EXE *-4
IF (S4 NE '') SET VAL S7=SUB(S4,1,IND(S4,',')-1)
   THEN SET VAL S4=SUB(S4,IND(S4,',')+1)
   THEN EXE *+3
REA UPP STR S7 PRO 'Enter pen color for PEN&N6 (<CR>=&(SUB(SUB('      BLACK RED   GREEN BLUE  ',N6*6+1,6),1,IND(SUB('      BLACK RED   GREEN BLUE  ',N6*6+1,6),' ')-1))): ' ATTN=*
IF (S7 EQ '') SET VAL S7=SUB(SUB('      BLACK RED   GREEN BLUE  ',N6*6+1,6),1,IND(SUB('      BLACK RED   GREEN BLUE  ',N6*6+1,6),' ')-1)
IF (IND('DEF.BLA.GRE.BLU.DEFAULT.BLACK.RED.GREEN.BLUE.',S7||'.') EQ 0) WRI 'Valid pen colors are:' ATTN=NEX
   THEN WRI 'BLACK, RED, GREEN, and BLUE.' ATTN=NEX
   THEN WRI 'or DEFAULT for PEN1=BLACK, PEN2=RED, PEN3=GREEN, and PEN4=BLUE' ATTN=NEX
   THEN WRI '' ATTN=NEX
   THEN EXE *-6
IF (IND('BLA.BLACK.',S7||'.') NE 0) SET VAL S7='BLACK '
IF (IND('GRE.GREEN.',S7||'.') NE 0) SET VAL S7='GREEN '
IF (IND('BLU.BLUE.',S7||'.') NE 0) SET VAL S7='BLUE  '
IF (IND('RED.',S7||'.') NE 0) SET VAL S7='RED   '
IF (IND('DEF.DEFAULT.',S7||'.') NE 0) SET VAL S4='      BLACK RED   GREEN BLUE  '
   THEN EXE *+23
SET VAL S8=S8||S7
SET VAL N6=N6+1
IF (S4 NE '') EXE *-18                 ;Get the next pen from the PARM.
IF (N6 LE N5) EXE *-16                 ;Reprompt for another pen selection.
;
SET VAL N5=0                           ;Change each pen choice to its respective color.
CH 'PEN=&(N5+1) ' 1 TO 'PENC=&(SUB(S8,N5*6+1,6))' NOL
SET VAL N5=N5+1
IF (N5*6 LT SIZ(S8)) EXE *-2
;
SET VAL N5=0                           ;Remove duplicate pen selections.
SET VAL N6=1
SET VAL S7=S8
SET VAL S8=''
CH 'PENC=&(SUB(S7,N5+1,6))' 1 TO 'PEN=&N6' NOL
IF (* GE 0) SET VAL S8=S8||SUB(S7,N5+1,6)
   THEN SET VAL N6=N6+1
SET VAL N5=N5+6
IF (N5 LT SIZ(S7)) EXE *-4
;
SET VAL N5=SIZ(S7)/6                   ;Number of pens actually used.
SET VAL S4=SUB('',1,6)||S7             ;Pad with leading 6 leading blanks for easier parsing.
;
;Tektronix 4662 plotter display cleanup
;
POI 'PEN=' 1 (1) IN &W3/L NOL          ;Find the first pen selection.
IF (* EQ -1.000) EXE *+11              ;No pens selected.
   ELSE SET VAL W3=*
SET VAL S7=SUB(LIN(W3),1)
POI ~'&S7' IN &(W3+1)/L NOL            ;Check if there are any other pen selections.
IF (* NE -1.000) DEL '&S7' IN &(W3+1)/L NOL  ;If not, delete all duplicates, since they are all the same.
   THEN EXE *+6
POI 'PEN=' 1 (1) IN &(W3+1)/L NOL      ;Get the selection of the next pen.
IF (* EQ -1.000) EXE *+4               ;All done checking pen selections.
IF (S7 EQ SUB(LIN(*),1)) D *           ;Delete duplicate pen selection.
   ELSE SET VAL W3=*
EXE *-11
DEL ' /* ' 1                           ;Now everything will be displayed on the plotter instead of the screen.
SET VAL W3=FIR
;
;Sequential processed plot file
;
SET RET=NEX
POI 'DISPLAY FILE FOR THE TEKTRONIX 4013' NOL  ;Verify plot file type.
IF (* NE -1.000) CH 'DISPLAY FILE FOR THE TEKTRONIX 4013' TO '' NOL
IF (N3 EQ 0) THEN EXE &W9              ;Display the plot.
POI 'DISPLAY FILE FOR THE TEKTRONIX 4025/4662' NOL
IF (* NE -1.000) CH 'DISPLAY FILE FOR THE TEKTRONIX 4025/4662' TO '' NOL
IF (N3 EQ 0) THEN EXE &W9              ;Display the plot.
IF (N3 EQ 0) WRI 'Invalid plot file.  Current plot aborted.' ATTN=NEX
   THEN SET VAL N4=1
   THEN EXE L-26                       ;Invalid plot file.
;
;Unloaded PDS processed plot file
;
IF (N2 EQ 0) IF (N3 EQ 1) DEL 'PEN=' 1 ;Delete non-necessary lines.
   THEN DEL '!SET PL1 1!COP H/P PL1' 1
   THEN DEL ' /* ' 1
IF (N3 EQ 1) POI './' 1 AND ' ADD '(1) IN &W3/L NOL  ;Find start of plot member.
IF (* NE -1.000) SET VAL S5=SUB(LIN(*),IND(LIN(*),' NAME=')+6)  ;Retrieve name of plot.

SET VAL W2=W3
POI './' 1 AND ' ADD ' (1) IN &(W3+1)/L NOL  ;Find the end of the current plot member.
IF (* NE -1.000) SET VAL W5=PREV
   ELSE IF (SUB(LIN(L),1,2) EQ './') IF (IND(LIN(L),' ENDUP') NE 0) SET VAL W5=L-1  ;Last plot of the unloaded PDS.
IF (N2 EQ 0) EXE *+15                  ;The plotter wasn't selected.
POI 'PEN=' 1 (1) IN &W3/&W5 NOL        ;Find pen change line.
IF (* NE -1.000) REA STR S7 USI *
   THEN SET VAL W4=*
   ELSE POI '!COP H/P PL1' (1) IN &W3/L NOL  ;Otherwise, it is the start of the plot.
   ELSE SET VAL W4=*+1
   ELSE IF (SUB(S7,1,3) NE 'PEN') SET VAL S7='PEN=1'  ;Only one pen used in the plot(s).
IF (SUB(LIN(W3+1),1,11) EQ '!WOR 33 H K') LIS &(W3+1)/&(W4-1) UNN NONL CLE  ;List only commands that are to issued once per plot at the start.
   THEN SET VAL W3=W4                  ;Advance to start of the plot.
POI 'PEN=' 1 (1) IN &(W3+1)/&W5 NOL    ;Find the next pen change line.
IF (* EQ -1.000) SET VAL W4=W5
   ELSE SET VAL W4=*
WRI '/*' ATTN=*                        ;Turn off the plotter.
REA PRO "Change pen color to &(SUB(SUB(S4,SUB(S7,IND(S7,'=')+1)*6+1,6),1,IND(SUB(S4,SUB(S7,IND(S7,'=')+1)*6+1,6),' ')-1)), press <CR> when ready: " ATTN=*
WRI '!COP H/P PL1' ATTN=*              ;Direct the output to the plotter.

POI './' 1 AND ' ALIAS ' (1) IN &W3/&W5 NOL  ;Check if there is an alias with this member.
IF (* NE -1.000) SET VAL S6=SUB(LIN(*),IND(LIN(*),' NAME=')+6)
;
;Display plot
;
DUM *+4 EXE                            ;Find the line number of the EXEC file that displays the plot.
DEF REPLOT AS "EXE &(SUB(LIN(L),1,IND(LIN(L),' - ')-1)) " REP  ;Tricky eh?
DEL L
EXE *+4                                ;Skip REPLOT area used for unloaded PDS plots.
IF (N2 EQ 1) WRI '!COP H/P PL1' ATTN=* ;Direct the output to the plotter [REPLOT entry].
IF (N3 EQ 1) IF (W4 EQ W5) COMM SET VAL W3=W2  ;Reset the current line pointer to the start of the current plot.  * TEMP *
   THEN EXE RET                        ;                                                                          * TEMP *
IF (N4 NE 0) EXE L-26                  ;An error occurred in the plot file.
IF (N1 EQ 1) WRI '!WOR 31 H !GRA 1,37 !SHR !MON K' ATTN=L-26
IF (N5 GE 1) IF (N2 EQ 1) LIS ~'./' 1 IN &(W3+1)/&W4 UNN CLE ATTN=L-26  ;Display only current pen selection plot area.
   THEN IF (W4 NE W5) SET VAL W3=W4
   THEN EXE *-32                       ;Continue with the next pen selection.
IF (N2 EQ 0) LIS ~'./' 1 IN &(W3+1)/&W5 UNN NONL CLE ATTN=L-26  ;Display current plot on screen.
   ELSE LIS ~'./' 1 IN &(W3+1)/&W5 UNN CLE ATTN=L-26  ;Display current plot on plotter.
;
;End of current plot and error recovery
;
IF (N2 EQ 1) WRI '/*' ATTN=*           ;End of current plot, turn plotter off.
WRI '!MON H K' ATTN=*
IF (N3 EQ 0) WRI S'2F2F'||'Plot done.  Type EXEC to clear the screen when ready.' ATTN=*+2
   ELSE WRI S'2F2F'||'Plot &S5, &S6 done.  Type EXEC to clear the screen when ready.' NONL ATTN=NEX
EXE *+5
;Error handling (ATTN pressed during or before plot has completed)
IF (N2 EQ 1) WRI '/*' ATTN=*           ;Turn plotter off.
WRI '!MON H K' ATTN=*
WRI S'152F2F'||'Plotting not completed, process terminated.  Type EXEC to clear the screen.' NONL ATTN=NEX

EXE PAU
WRI '!MON 34 H K' ATTN=NEX             ;Reset the screen format (will clear the screen).
IF (N4 EQ 1) EXE *+7

WRI 'To replot the current plot, type REPLOT.' NONL ATTN=NEX
IF (N3 EQ 1) WRI '  To continue with the next plot in the' ATTN=NEX
   THEN WRI 'plot file, type EXEC.  To exit, type QUIT.' ATTN=*+2
   ELSE WRI '  To exit, type EXEC.' ATTN=NEX
EXE PAU
SET VAL N4=0                           ;Reset the error flag.
IF (N3 EQ 1) IF (W5 NE L-1) SET VAL W3=W5+1
   THEN EXE RET                        ;Goto the next plot member.
WRI 'No more plots.  Plotting completed.' ATTN=*

EXE *+2
WRI 'Plotting terminated via QUIT command.' ATTN=*  ;[QUIT entry]
UNDEF QUIT
UNDEF REPLOT
SET TERM HEI=&N0                       ;Reset the screen height.
SET DEL=&W0
SET ESC &S0
CLR VAL EXE
