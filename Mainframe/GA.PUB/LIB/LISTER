SET EXEC NOLOG TERSE
;
;  THIS EXEC FILE WILL GENERATE A JOB TO PRINT A DATA SET WHICH IS
;  ON DISK...SORT OF LIKE A "LIST OFFLINE FROM.."
;  MARK LAWRENCE     SCIP USER SERVICES      7-4976
;
;  last revision 05 June 81
;
comm LISTER version of 05 June 81
;
; save user's values.
SET VAL S0 ESC
set val n0 length
SET VAL W0 END
SET ESC %
set len 71
;
read string s1 prompt 'Do you need instructions?  (<CR>=NO)'
if (UPPER(SUBSTR(S1,1,1)) EQ 'N') exec 150
if (s1 eq '')   exec 150
;
comm This execfile may be used to print a Wylbur data set in its
comm entirety.  If the data set is a library and no member is
comm specified, all of the members will be printed.  In the
comm instructions which follow, the symbol <CR> means "press the
comm carriage return key without typing  anything".
;  get dsname
;
read string s1 prompt 'Enter the DSNAME_'
SET VAL S1 upper(s1)
; see if a member name was specified with #
set val n1 index(s1,'#')
if (n1 eq 0) exec 179            ; no...
; change # to () method of specifying member
set val s9 substr(s1,n1+1,8)       ; member name
set val n2 index(s9,' ')          ;strip the trailing
IF (N2 EQ 0) EXEC 161    ; IF NO TRAILING BLANKS
set val s9 substr(s9,1,n2-1)      ;  blanks
set val s1 substr(s1,1,n1-1)  ; strip "#member"
if (n1 eq 1) set val s1 lib   ; if no lib name specified
set val s1 S1||'('||s9||')'
;
; get volume
read string s2 prompt 'Enter volume or <CR> if cataloged_'
SET VAL S2 upper(s2)
if (substr(s2,1,3) eq 'CAT') set val s2 ''            ; "cat" as volume means no vol
;
; see if a fully qualified data set name was provided.
if (substr(s1,1,1) eq '$') exec 220  ; fully qualified name
if (substr(s1,1,4) ne 'WYL.') exec 210 ; not qualified name
if (substr(s1,7,1) ne '.')    exec 210
if (substr(s1,11,1) ne '.') exec 210
exec 230 ; fully qualified name without the $
;
;  here if not qualified name.  build wyl.gg.uuu qualifier
;
set val s3 group
set val s4 user
set val s1 'WYL.'||'%s3'||'.'||'%s4'||'.'||'%s1'  ; make qualified name
exec 230
;  fully qualified name, with $ prefix
;
set val s1=substr(s1,2,44); strip off $
;
;  at this point we have a fully qualified name.  use it on job card
;
set val s9 substr(s1,12,20) ; unqualified dsname
if (substr(s1,1,4) eq 'WYL.') exec 239  ; ok, it was wylbur name
set val s9 substr(s1,1,20)  ; not wylbur name, use unqualified
;
; if not cataloged or library member, put name in quotes for safety.
if (s2 eq '') exec 249
if (index(s1,'(') ne 0) exec 249
set val s1 "'"||S1||"'"            ; in case >8 chars. between periods
;
; check to see if user wants uplow.
; first, set the default values (for upper)
set val s5 'A'  ;  this will be msgclass
set val s6 'PN' ;  this will be ioprogm's "chain" option
;read string s4 prompt 'Do you want upper-lower case?  (<CR>=NO)'
;set val s4 upper(s4)
;if (substr(s4,1,1) eq 'N') exec 299
;if (s4 eq '') exec 299      ; cr same as no
;if (s4 eq 'OK') exec 290
;if (substr(s4,1,1) eq 'Y') exec 290
;comment %s4:  INVALID.  Please reply YES or NO
;exec 260
;;
;;  here if 'uplow' requested
;set val s5 'D'    ; sysout class
;set val s6 'TN'   ; print train
;
;  start generating jcl
;
%W0+0 //  JOB  ,'%S9',TIME=(,15)
IF (SUBSTR(GRO,1,1) EQ 'P') CHA ",.25" TO ",.1" IN %W0+0   NOL
%W0+1 //  EXEC IOPROGM
%W0+2 //INPUT DD DISP=SHR,UNIT=DASD,VOL=SER=%S2,
%W0+3 //   DSN=%S1
%W0+4 //SYSIN DD *
;
;  check "numbered" option.
;
read str s1 prompt 'Numbered? (<CR>=YES)'
if (s1 eq '') exec 330             ; same as yes
if (upper(s1) eq 'OK') exec 330
if (upper(substr(s1,1,1)) eq 'Y') exec 330
if (upper(substr(s1,1,1)) eq 'N') exec 340  ;no
comment %s1:  INVALID.
exec 320
;
;  here if numbered
;
set val s1 'NUMBERS=WYLBUR '
exec 349
set val s1 ''  ; no numbers
;
comment Does data set contain carriage control characters?
read string s2 prompt 'Reply "CC", "MC" or <CR> if none_'
set val s2 upper(s2)
if (s2 eq '') exec 370; none
if (substr(s2,1,1) eq 'N') exec 370; none
if (s2 eq "CC")  exec 375          ; "cc" or ansi
if (s2 eq "ANSI") exec 375
if (s2 eq "ASA")  exec 375
;
if (s2 eq "MC")   exec 380         ; mc
if (s2 eq "M")    exec 380
;
comment %s2:  INVALID.
exec 351
;
;  here if none.
set val s2 ''   ; clear "cc" option
exec 399
;  here if ansi control ("cc")
set val s2 "CC=ANSI"               ; cc option for ioprogm
exec 390
;
;
;  here for mc
set val s2 "CC=MC"                 ; cc option for ioprogm
exec 390
;
; user has carriage control, tell hasp to leave it alone.
;
%w0+0.5 /*JOBPARM LINECT=0
;
;  generate control statement
%W0+6 PRINT  %S1 %S2 EJECT
;
;
;  READY TO RUN.  WE HAVE COLLECTED JCL AT LINES W0/LAST.
;
READ STR S9 PROMPT'RUN?'
IF (S9 EQ '') EXEC 600  ; NULL SAME AS YES
IF (SUBSTR(UPPER(S9),1,1) EQ 'Y')  EXEC 600
IF (UPPER(S9) EQ 'OK')             EXEC 600
; ANYTHING ELSE IS "NO"
;
COMM The JCL is in lines %W0/LAST of your active file.
COMM To run, issue the command "RUN %W0/L"
;
exec 999

; HERE IF USER WANTS TO RUN.
;
RUN %W0/L
DEL %W0/L
;
READ STR S9 PROMPT 'List another file?  (<CR>=YES)'
IF (S9 EQ '') EXEC 150
IF (UPPER(S9) EQ 'OK') EXEC 150
IF (UPPER(SUBSTR(S9,1,1)) EQ 'Y') EXEC 150
; ANYTHING ELSE IS NO
EXEC 900
; restore the user's values
set len %n0
set esc '%s0'
clr exec
;
