;WYL.GG.LSO.PASCAL#PASCCG -- created 08/02/80 by Eagle Berns and Lincoln Ong
;          latest modification 03/28/84  Version 1.07
;This EXEC file COMPILES, LOADS, and GO (executes) an IBM Pascal/VS program interactively.
;
;See the document "Using Pascal", #504 for further information on using
;this EXEC file.
;
;          Variables used:
;   PARM = [WYL|WYL=dsname|ORV|ORV=filename][,DEBUG][,Pascal-options]
;
;     N9 = condition code (returned from Pascal step)
;
;     S0 = original ESC setting
;     S1 = PARM (contains Pascal options and data set name)
;     S2 = DEBUG option
;     S3 = data set name
;     S4 = * reserved *
;     S5 = * reserved *
;     S9 = program name (returned from the LOAD phase)
;
CLR COR
CLR VAL
SET VAL S0=ESC
SET ESC &
SET VAL N9=255                         ;Non zero so it does not mess up the TRANSLATE phase if it dies.
SET VAL S1=UPP(PARM)
SET VAL N0=IND(S1,'WYL')               ;Go get Pascal source in ACTIVE (if necessary).
IF (IND(S1,'WYL') EQ 0) IF (IND(S1,'ORV') EQ 0) EXE *+14  ;The program should be in the ACTIVE file.
IF (IND(S1,'ORV') NE 0) EXE *+7
IF (IND(S1,'WYL') NE 0) SET VAL S3=SUB(S1,IND(S1,'WYL'))  ;Retrieve the program from a WYLBUR data set.
   THEN SET VAL S3=SUB(S3,1,IND(S3||",",',')-1)
IF (IND(S3,'=') NE 0) SET VAL S3=SUB(S3,IND(S3,'=')+1)
   THEN IF (VER(S3,' ') NE 0) SET VAL S3='USE '||'&S3'
   ELSE SET VAL S3=''                  ;The program is in the ACTIVE file.
EXE *+7
;
IF (IND(S1,'ORV') NE 0) SET VAL S3=SUB(S1,IND(S1,'ORV'))  ;Retrieve the program from an ORYVL file.
   THEN SET VAL S3=SUB(S3,1,IND(S3||",",',')-1)
IF (IND(S3,'=') NE 0) SET VAL S3=SUB(S3,IND(S3,'=')+1)
   ELSE SET VAL S3='PASCAL.SYSIN'      ;The program is in the default ORVYL file.
IF (VER(S3,' ') NE 0) SET VAL S3='GET '||'&S3'
&S3
IF (LINES LE 0) REA UPP STR S3 PRO 'Enter command to put Pascal source (no JCL) in ACTIVE: ' ATTN=L-1
   THEN EXE *-2
PUT PASCAL.SYSIN REP QUI               ;Put away the source.
;
;Pascal COMPILE step
;
COMM Compiling...
CALL PCALL '&S1' PUB CLR               ;LOAD phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')" ATTN=L-1
   THEN EXE L-1
CALL PCALO '&S1' PUB CLR               ;LOAD phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')" ATTN=L-1
   THEN EXE L-1
CALL PCALT '&S1' PUB CLR               ;TRANSLATE phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')" ATTN=L-1
   THEN EXE L-1
;
;Pascal LOAD step
;
COMM Loading...
IF (IND(S1,'DEBUG') NE 0) SET VAL S2='DEBUG/'  ;Check if DEBUG option given.
   THEN GET @GG.&PAS.PCAL.LINK.USER.DEBUG CLR  ;If true, link with DEBUG modules.
   ELSE GET @GG.&PAS.PCAL.LINK.USER CLR
DEL L                                  ;For PASCCG we take off the last card (name card).
CH '@@@@@@@@' TO '&S9' NOL
LINK ACT CLR
;
;Pascal GO step
;
COMM Executing...
GET PASCAL.SYSIN CLR                   ;Input can be in the active file, but must be enclosed by comments.
ENTER '&S1'

SET ESC &S0
CLR VAL
CLR EXE
