;WYL.GA.PUB.LIB#EXECUTAB--created 07/07/80 by Lincoln Ong, C.I.T. Technical Support Services
;          latest modification 04/25/81  Version 1.08
;This EXEC file provides the ability to set tabs on a Execuport 4080
;terminal or any other terminal that uses:
;   <ESC>1 -- set tab
;   <ESC>2 -- clear all tabs
;
;To use this EXEC file, do as follows:
;   EXEC FROM LIB#EXECUTAB PUBLIC PARM='your tabs'
;   for example:
;      EXEC FROM LIB#EXECUTAB PUBLIC PARM='5 10 15 20 25 30 35 40 45 50 55 60 65 70 75 80'
;If no parm is specified or there is an illegal tab set, it will re-prompt
;you for new tab settings.
;
;          Variables used:
;     N0 = number of tabs set
;     N1 = previous tab
;     N2 = work variable
;
;     S0 = original ESC setting
;     S1 = tab string
;     S2 = saved tab string
;     S3 = single tab
;     S4 = tab string
;
;     W0 = line number of tab processing [beginning]
;
CLR VAL
SET VAL S0=ESC
SET ESC &
SET VAL S1=PARM
SET VAL S4=S'27F1'                     ;Setup tab set command string.
SET VAL W0=56                          ;<--this should be changed if the beginning of tab processing moves from line 56.
WRI '' ATTN=NEX
WRI 'EXECUTAB  Revised 25 April 1981  Version 1.08' ATTN=NEX
IF (S1 EQ '') WRI 'This EXEC file provides the ability to set tabs on a Execuport 4080' ATTN=NEX
THEN WRI 'terminal or any other terminal that uses:' ATTN=NEX
THEN WRI '   <ESC>1 -- set tab' ATTN=NEX
THEN WRI '   <ESC>2 -- clear all tabs' ATTN=NEX
THEN WRI '' ATTN=NEX
SET RET=*+3
IF (SIZ(S1) GE 1) EXE &W0

WRI '' ATTN=NEX
REA UPP STR S1 PRO 'Enter tab settings: ' ATTN=L-1
IF (SIZ(S1) EQ 0) EXE RET
IF (IND(S1,'HEL')+IND(S1,'?') EQ 0) EXE *+9
WRI '' ATTN=RET
WRI 'Enter the tab positions desired, seperated by a blank or comma.  Up' ATTN=RET
WRI 'to 16 tabs can be entered.  If more than 16 tabs are entered, only' ATTN=RET
WRI 'the first 16 are used.  Tabs can be set from columns 1 through 80.' ATTN=RET
WRI 'For example:' ATTN=RET
WRI '   Enter tab settings: 1,5 15 30 40 60' ATTN=RET
WRI '' ATTN=RET
EXE RET
SET VAL S3=S1
SET VAL S2=S3                          ;Save user tabs.

SET VAL N2=VER(S3,' =,
')              ;Get the next tab position.
IF (N2 EQ 0) EXE L-6                   ;Set tab stops now.
SET VAL S3=SUB(S3,N2)
SET VAL N2=FIN(S3||' ',' =,
')
SET VAL S1=SUB(S3||' ',1,N2-1)
SET VAL S3=SUB(S3,N2+1)
SET VAL N2=VER(S1,'0123456789')
IF (N2 NE 0) EXE *+3                   ;Invalid tab(s).
SET VAL N2=NCO(S1)
IF (N2 GE 1) IF (N2 LE 80) EXE *+4     ;Descending tabs.
WRI '&S1: INVALID TAB -- PLEASE RE-ENTER TABS.' ATTN=RET
EXE RET

IF (N2 LE N1) WRI 'TAB &N1 BEFORE &N2 -- PLEASE RE-ENTER IN ASCENDING ORDER.' ATTN=NEX
THEN EXE RET
IF (N0 GT 15) WRI 'ONLY FIRST 16 TABS USED.' ATTN=NEX
THEN SET VAL N2=IND(S2,&S1)            ;Strip extra tabs.
THEN SET VAL S2=SUB(S2,1,N2-1)         ;Save first 16 tab settings.
THEN EXE L-6                           ;Set tab stops now.

SET VAL N1=N2
SET VAL N2=N2+2*N0+2
IF (N2 LT 129) SET VAL S4=SUB(S4,1,N2-1)||S'27F1'
THEN SET VAL N0=N0+1                   ;Increment tab count.
THEN EXE &(W0+3)

CLR TABS                               ;Done getting tab positions.
WRI S'27F2'
WRI S4
SET TABS &S2 UNN NOVER
SET FAST
SET ESC=&S0
CLR VAL EXE
