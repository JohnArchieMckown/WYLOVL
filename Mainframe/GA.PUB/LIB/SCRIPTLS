;  SCRIPTLS -- EXEC FILE TO LIST SCRIPT OUTPUT AT A GENCOM

;  REVISED 04/25/83 BY MARK LAWRENCE:  TRANSLATE BOX CHARS, BULLETS

;  New version 04/18/83 BY MARK LAWRENCE:
;        FETCH NOMSG
;        USE WYLBUR "LIST CC" COMMAND INSTEAD OF CONVERTING CC
;        USE PROPER GENCOM SUB/SUPERSCRIPT MODE FOR SUPERSCRIPTS
;        USE GENCOM "SET TOP-OF-FORM" FEATURE FOR CONTINUOUS FORMS
;        ALLOW RESTART AT ANY PAGE
;


;  VARIABLE USAGE
;
;  S0    VARIOUS PROMPTS/RESPONSES
;  S1    "C" FOR CONTINUOUS FORMS, "S" FOR SHEETS
;
;
;
;
;  S9    USER'S ESCAPE CHAR.
;
;  N1    PITCH (10 OR 12)
;  N2    LINES PER INCH (6 OR 8)
;
SET VAL S9 ESCAPE
SET ESC &
SET HEIGHT 0
COMM
COMM SCRIPTLS  version of 04/25/83
COMM This exec file prints SCRIPT output on a Gencom terminal.
COMM
;------ FETCHING-----
READ VALUE N0 PROMPT 'Enter SCRIPT job number. '
FETCH &N0 DDN SYSPRINT CC

;  Set up terminal for the listing.

;  SET HORIZONTAL SPACING
REA STR S0 UPP PRO 'Pitch (horizontal spacing) (10 or 12): '
IF (S0 EQ '?') LIST 1100/1199 EXE UNN ATTN=NEXT
THEN EXEC 100
IF (S0 EQ '') SET VAL S0 '10' ; DEFAULT
THEN EXE 120
IF (S0 EQ '10') EXE 120
IF (S0 EQ '12') EXE 120
WRI S0||':  INVALID.  Specify 10 or 12.'
LIST 1100/1199 EXE UNN ATTN=100
EXE 100
SET VAL N1 10  ; PITCH
IF (S0 EQ '12') SET VAL N1 12
WRI S'27C5' ; SET 10P/6LPI
IF (N1 EQ 12) WRI S'27C6'          ; 12P/8LPI

; VERTICAL
REA STR S0 UPP PRO 'Lines per inch (vertical spacing) (6 or 8): '
IF (S0 EQ '?') LIST 1200/1299 EXE UNN ATTN=130
THEN EXEC 130
IF (S0 EQ '') SET VAL S0 '6' ; DEFAULT
THEN EXE 145
IF (S0 EQ '6') EXE 145
IF (S0 EQ '8') EXE 145
WRI S0||':  INVALID.  Specify 6 or 8.'
LIST 1200/1299 EXE UNN ATTN=130
EXE 130

SET VAL N2 6 ; LINES-PER-INCH
IF (S0 EQ '8') SET VAL N2 8
IF (N2 EQ 6) WRI S'27D1'   ; ESC-J = SET 6 LPI
ELSE WRI S'27D2'           ; ESC-K = SET 8 LPI


;  FORM SETUP

READ STRING S1 UPPER PROMPT 'Continuous forms(C) or sheets(S)? '
IF (S1 NE 'S') IF (S1 NE 'C') EXEC *-1

WRI S'1C1E'    ; TURN ON "ESCAPE" FEATURE IN TERMINAL

IF (S1 NE 'C') EXE 190

; HERE FOR "CONTINUOUS"
COMM Adjust the paper to the desired "Top of Form" position.
COMM Normally, this is 1/2 inch down from the top of the page.
REA STR S0 PRO 'When the paper is in the desired position, press RETURN '

; RESPONSE IS IGNORED...
WRI S'272527E3'; ESC-LF, ESC-T:  MOVE UP ONE LINE (TO WHERE PAPER WAS
;                WHEN USER PRESSED RETURN), THEN SET TOP-OF-FORM

EXE 200

; HERE IF SHEETS

LIS 1400/1499 EXE UNN ATTN=NEXT

;  Now, change the text to make it printable.  First, the superscripts...

; SUPERSCRIPT NUMBER CHANGED TO "ESC-S <NUM> ESC-B"
CHANGE X'B0' TO X'27E2F027C2' NOLIST
CHANGE X'B1' TO X'27E2F127C2' NOLIST
CHANGE X'B2' TO X'27E2F227C2' NOLIST
CHANGE X'B3' TO X'27E2F327C2' NOLIST
CHANGE X'B4' TO X'27E2F427C2' NOLIST
CHANGE X'B5' TO X'27E2F527C2' NOLIST
CHANGE X'B6' TO X'27E2F627C2' NOLIST
CHANGE X'B7' TO X'27E2F727C2' NOLIST
CHANGE X'B8' TO X'27E2F827C2' NOLIST
CHANGE X'B9' TO X'27E2F927C2' NOLIST

CHANGE X'AF' TO '*' NOLIST   ; BULLET
CHANGE X'BF' TO '-' NOLIST   ; LONG DASH
CHANGE X'AB' TO '|' NOLIST   ; CORNER
CHANGE X'AC' TO '|' NOLIST   ; CORNER
CHANGE X'BB' TO '|' NOLIST   ; CORNER
CHANGE X'BC' TO '|' NOLIST   ; CORNER

;  prepare to list

NUM BY 1 STA 1 QUI
SET VAL W1 1   ; START OF THIS PAGE
POI '1' 1 (1) IN 2/L NOL ; START OF NEXT PAGE
SET VAL W2 *-1 ; END OF THIS PAGE
SET VAL W3 W1  ; START OF PREVIOUS PAGE...NONE YET

; START LISTING

IF (S1 EQ 'C') REA STR S0 PRO 'Press RETURN to start the listing.'
; RESPONSE IGNORED, WE JUST WAIT FOR "RETURN"
THEN EXE 500
; SHEETS...
WRI 'Insert the first sheet, adjust to top of page, and press RETURN'
WRI 'to start the listing. '

REA STR S0 UPP PRO ' ' ATTN=900

;  list a page

IF (S1 EQ 'S') LIST &W1 UNN CLEAN COL 2 ATTN=550
ELSE LIST &W1 UNN CC CLEAN ATTN=550                 ; FIRST LINE WITH PAGE EJECT
LIST &W1+1/&W2 UNN CC CLEAN ATTN=550
IF (S1 EQ 'S') REA STR S0 PRO ' ' ATTN=550; VALUE IGNORED, WE JUST WAIT FOR CR
IF (W2 GE LAST) EXE 900 ; DONE
SET VAL W3 W1  ; REMEMBER "PREVIOUS" PAGE
SET VAL W1 W2+1; POINT TO "NEXT" PAGE

; FIND END OF THIS PAGE, ETC.
POI '1' 1 (1) IN &W1+1/L NOL ; FIND NEW "NEXT" PAGE
SET VAL W2 *-1 ; END OF THIS PAGE
IF (W2 LE 0) SET VAL W2 LAST ; IF THIS IS LAST PAGE
EXE 500 ; NEXT PAGE

;  ATTN INTERRUPT

WRI ' '
WRI ' '
REA STR S0 UPP PRO 'Specify RESTART, CONTINUE, BACKSPACE, NEXT, or QUIT, "?" for info: ' ATTN=900
IF (S0 EQ '?') LIST 1300/1399 EXE UNN ATTN=552
THEN EXE 552
IF (SUB(S0,1,3) EQ 'RES') EXE 400 ; RESTART AT BEGINNING
IF (SUB(S0,1,3) EQ 'CON') EXE 520 ; CONTINUE (RESTART THIS PAGE)
IF (SUB(S0,1,3) EQ 'BAC') EXE 580 ; BACKUP TO PREVIOUS PAGE
IF (SUB(S0,1,3) EQ 'NEX') EXE 504 ; NEXT PAGE
IF (SUB(S0,1,3) EQ 'QUI') EXE 900 ; GET OUT
WRI S0||':  INVALID.  '
EXE 550

; BACKUP TO PREVIOUS PAGE

SET VAL W1 W3
EXE 520


;  cleanup
IF (S1 EQ 'S') REA STR S0 PRO ' ' ATTN=550; VALUE IGNORED, WE JUST WAIT FOR CR
ELSE WRI S'0C40'  ; TO NEXT PAGE IF CONTINUOUS FORMS
SET ESC &S9
CLEAR EXEC

; the explain texts
The terminal will print at either 10 or 12 characters per horizontal
inch; the characters-per-inch value is called "pitch".  10 pitch is
the same as PICA type, 12 pitch is the same as ELITE type.  You should
have a type wheel appropriate to the pitch you intend to use, and
should have specified an appropriate line length in your SCRIPT
file (12 pitch allows for more characters in a line).

The terminal will print at either 6 or 8 lines per vertical inch;
6 is standard and corresponds to a page length of 66 lines for standard
(11 inch high) paper.  If you want to use 8 lines per inch, your
SCRIPT file must have made suitable settings (e.g., PL=88).

If you specify RESTART, the listing will start over again from the
beginning.

If you specify CONTINUE, the listing will start over at the top of
the current page.

If you specify BACKSPACE, the listing will start over at the top of
the previous page.

If you specify NEXT, listing will continue with the page after the one
that was being listed.

If you specify QUIT, the listing will terminate.

The terminal will pause at the end of each page.  When that happens,
insert a new sheet, adjust it to the line just before your desired
"top of page" line, then press RETURN.

