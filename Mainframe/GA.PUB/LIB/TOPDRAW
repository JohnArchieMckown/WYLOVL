; revised to include Zeta MCL 11/16/83
CLEAR VARIABLES
SET VALUE S9 ESCAPE
SET ESCAPE &
DEFINE $GOTO AS "POINT ';" APPEND "'1 EXEC NOLIST" REPLACE
DEFINE $EXPLAIN AS "POINT ';EXPLAIN." APPEND "'1 EXEC NOLIST" REPLACE
IF (USER EQ 'TOP') $GOTO ENDINTRO
COMMENT Top Drawer (11/16/83) - Creates JCL for a Top Drawer job.
COMMENT For help, reply '?' to any question.
COMMENT Active file must contain Top Drawer input cards.
COMMENT
IF (LINES EQ 0) COMMENT No input in active file.
ELSE IF (FIRST LT .011) COMMENT Top Drawer JCL needs lines .001/.010.  Input must be renumbered.
THEN COMMENT Rerun this EXEC file with proper input.
THEN CLEAR EXEC

;- - - - - SUPPLY JOB AND EXEC CARDS - - - - -
;ENDINTRO
.001 //    JOB
.003 //DRAW EXEC TOPDRAW

;- - - - - SELECT DEVICE, DELETE 'SET DEVICE' AND BRANCH - - - - -
;PROMPT.DEVICE
SET RETURN NEXT
REA STR S0 UPP PRO 'What Device? (Versatec, Zeta, 4013, 4025, GenCom) '
;DELETE.DEVICE
POINT 'SET DEVICE ' NOLIST
IF (CURRENT EQ -1) $GOTO BRANCH.DEVICE
COMMENT Replacing this line:
DELETE CURRENT LIST
$GOTO DELETE.DEVICE
;BRANCH.DEVICE
IF (SUB(S0,1,3) EQ 'VER') $GOTO VERSATEC
IF (SUB(S0,1,1) EQ 'Z') $GOTO ZETA
IF (S0 EQ '4013') $GOTO 4013
IF (S0 EQ '4025') $GOTO 4025
IF (SUB(S0,1,1) EQ 'G') $GOTO GENCOM
$EXPLAIN DEVICE

;- - - - - SUPPLY DEVICE CARDS AND BRANCH - - - - -
;VERSATEC
;PROMPT.FORMS
SET RETURN NEXT
READ STRING S1 UPPER PROMPT 'Fan-fold or Continuous-roll Paper? (RETURN=fan-fold) '
IF (S1 EQ '') SET VALUE S1 = 'F'
SET VALUE S1 = SUB(S1,1,1)
IF (S1 NE 'C') IF (S1 NE 'F') $EXPLAIN FORMS
IF (S1 EQ 'C') CHANGE 'TOPDRAW' TO 'TOPDRAW,PLTFORM=ROLL' IN .003 NOLIST
.010 SET DEVICE VERSATEC
IF (S1 EQ 'C') CHANGE 'VERSATEC ' TO 'VERSATEC CONTINUOUS' IN CURRENT NOLIST
POINT 'SET INT' NOLIST
IF (CURRENT NE -1) CHANGE 'VERSATEC ' TO 'VERSATEC INTENSITY ' IN 'SET DEVICE VERSATEC' NOLIST
$GOTO SUMMARY.CARDS

;ZETA
CHANGE 'TOPDRAW' TO 'TOPDRAW,TDVER=ZETA' IN '//'1 AND 'EXEC' NOLIST
.010 SET DEVICE SMALL CALCOMP
;PROMPT.SETUP.DEFAULT
SET RETURN NEXT
REA STR S1 UPPER PRO 'Do you want the default pen setup? (RETURN=yes) '
IF (S1 EQ '?') $EXPLAIN DEFAULT
IF (S1 EQ '') SET VALUE S1 'Y'
SET VALUE S1 = SUB(S1,1,1)
IF (S1 EQ 'N') $GOTO PROMPT.PENS
ELSE IF (S1 NE 'Y') $GOTO PROMPT.SETUP.DEFAULT
$GOTO SUMMARY.CARDS

;GENCOM
.010 SET DEVICE GENCOM
$GOTO PROMPT.SEQUENTIAL
;4013
.010 SET DEVICE 4013
$GOTO PROMPT.SEQUENTIAL
;4025
.010 SET DEVICE 4025
;PROMPT.SEQUENTIAL
SET RETURN NEXT
READ STRING S1 UPPER PROMPT 'Do you want sequential file output? (RETURN=y) '
IF (S1 EQ '?') $EXPLAIN SEQUENTIAL
IF (S1 EQ '') SET VALUE S1 'Y'
SET VALUE S1 = SUB(S1,1,1)
IF (S1 EQ 'N') $GOTO SUPPLY.NONSEQUENTIAL
ELSE IF (S1 NE 'Y') $GOTO PROMPT.SEQUENTIAL
$GOTO SUPPLY.SEQUENTIAL

;- - - - - SUPPLY DD STATEMENTS AND SET DEVICE KEYWORDS - - - - -

;PROMPT.PENS
SET RETURN NEXT
REA STR S1 UPP PRO 'Specify pen code ABLK, LBLK, RCOL, or BCOL (RETURN=RCOL) '
IF (S1 EQ '?') $EXPLAIN PENS
IF (S1 EQ '') SET VAL S1 'RCOL'
IF (SIZE(S1) NE 4) $GOTO ERROR
IF (IND('ABLK.LBLK.RCOL.BCOL',S1) EQ 0) $GOTO ERROR
.010 SET DEVICE SMALL CALCOMP 'PENCODE=&S1'
$GOTO SUMMARY.CARDS

;SUPPLY.NONSEQUENTIAL
;PROMPT.PDS
SET RETURN NEXT
COMM Do you want the default DSN,VOL,DISP,SPACE
READ STRING S1 UPPER PROMPT '   for the library? (RETURN=yes) '
IF (S1 EQ '?') $EXPLAIN PDS
IF (S1 EQ '') SET VALUE S1 = 'Y'
.007 //SYSPLOT DD DSN=WYL.&(GROUP).&(USER).TDOUTPUT,DISP=(,CATLG),
.008 //           UNIT=DISK,SPACE=(TRK,(25,5,10)),
COMMENT Library DD statement (without DCB):
LIST PREVIOUS/CURRENT
IF (S1 EQ 'N') COMMENT Now modify as desired:
THEN MODIFY PREVIOUS/CURRENT
$GOTO SUMMARY.CARDS

;SUPPLY.SEQUENTIAL
CHANGE '    ' TO ' SEQUENTIAL' IN 'SET DEVICE' NOLIST
$GOTO SUMMARY.CARDS

;- - - - - EXPLANATIONS FOR PROMPTS - - - - -

;EXPLAIN.DEVICE
COMM
COMM Indicate what type of device you want your plot produced on.
COMM The following types are available:
COMM
COMM      Versatec Electrostatic Plotter
COMM      Zeta 3610 Drum Plotter  (10 inch)
COMM      4013 Tektronics Graphics Terminal
COMM      4025 Tektronics Graphics Terminal
COMM      GenCom 300Q Hard-Copy Terminal
COMM
COMM Use of the Versatec is suggested if you have not plotted before.
COMM
EXEC RETURN

;EXPLAIN.FORMS
COMM
COMM Two forms are available on the Versatec.  One is "fan-fold" paper,
COMM the other is "continuous-roll."  Fan-fold is usually
COMM mounted on the plotter, and so gives the best turnaround.
COMM With fan-fold, each frame of the plot is on a separate page;
COMM no one frame can be larger than 13" wide by 10" high.  With
COMM continuous-roll, the maximum height is 10", but the width is
COMM limited only by a SET SIZE command.
COMM
COMM (Use of the first letter is sufficient: F or C. "F" is the default.)
COMM
EXEC RETURN

;EXPLAIN.DEFAULT
;EXPLAIN.PENS
COMM
COMM You can specify which pen setup is to be used:
COMM
COMM Code              Setup
COMM ----------------------------------------------------------------
COMM ABLK     3 sizes of black pens (felt tip, roller, ballpoint)
COMM LBLK     3 sizes of liquid ink pens with black ink
COMM RCOL     Liquid roller pens (black, red, blue, green)
COMM BCOL     Ballpoint pens (black, red, blue, green)
COMM
COMM Caution:  Multiple pen setups are not working reliably on the Zeta
COMM at this time.  We recommend you not attempt to switch pens.
COMM
EXEC RETURN

;EXPLAIN.SEQUENTIAL
COMM
COMM Sequential output is simple and convenient to use.  After the
COMM Top Drawer job has run, you must logon to a plotting terminal and:
COMM
COMM              FETCH jobname SYSOUT=C
COMM              LIST UNNUMBERED CLEAN NONL
COMM
COMM Sequential output is erased when the job is removed from the system.
COMM You may also choose to have your plot output directed to a library
COMM by responding "NO" to this question.
COMM
COMM If output from a Top Drawer job is placed in a library, you logon
COMM to a plotting terminal after the job has run and:
COMM
COMM              USE TDOUTPUT#PICT001
COMM              LIST UNNUMBERED CLEAN NONL
COMM
COMM (Use of the first letter is sufficient: Y or N. "Y" is the default.)
COMM
EXEC RETURN

;EXPLAIN.PDS
COMM
COMM The defaults for placing plot output in a library are:
COMM
COMM      Dataset name: WYL.&GROUP.&USER.TDOUTPUT
COMM      Volume:       any PUB volume (data set will be cataloged)
COMM      Space:        25 tracks primary, 5 secondary,
COMM                    10 directory blocks
COMM
COMM If you do not want these defaults, reply "NO," and you will be
COMM given an opportunity to alter the JCL.
COMM
COMM (Use of the first letter is sufficient: Y or N; "Y" is the default.)
COMM
EXEC RETURN

;- - - - - ERROR REPORTING - - - - -

;ERROR
COMM
COMM Your response - "&S1" - is invalid.  Please respecify.
COMM
EXEC RETURN

;- - - - - SUMMARY OF JCL CARDS ADDED - - - - -

;SUMMARY.CARDS
COMM The following JCL has been added:
LIST FIRST/.010
SET ESCAPE = &S9
UNDEFINE $GOTO
UNDEFINE $EXPLAIN
CLEAR EXEC
