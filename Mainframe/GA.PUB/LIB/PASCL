;WYL.GG.LSO.PASCAL#PASCL -- created 08/03/80 by Lincoln Ong
;          latest modification 03/28/84  Version 1.09
;This EXEC file LINK EDITS an IBM Pascal/VS program interactively.
;
;See the document "Using Pascal", #504 for further information on using
;this EXEC file.
;
;          Variables used:
;          OBJECT or OBJ is an alias for INCLUDE
;   PARM = [CLEAR|CLE|CLR][,DEBUG][,{INCLUDE|INC}[=(module1[,module2[,...]])]]
;          [,{NAME|NAM}=program-name]
;
;     N9 = * reserved *
;
;     S0 = original ESC setting
;     S1 = PARM (contains Pascal options and program name)
;     S2 = * reserved *
;     S3 = * reserved *
;     S4 = object module name list
;     S5 = object module name
;     S9 = program name (entered by user)
;
CLR VAL
SET VAL S1=UPP(PARM)
IF ((IND(S1,'CLE')+IND(S1,'CLR')) NE 0) EXE *+2
IF (LINES NE 0) REA PRO 'Enter SAVE for ACTIVE file or press <CR>=Clear, <ATTN>=No: ' ATTN=L
CLR COR
CLR ACT
SET VAL S0=ESC
SET ESC &
IF ((IND(S1,'INC')+IND(S1,'OBJ')) NE 0) EXE *+5  ;If INCLUDE or OBJECT option was not specified, default to name found in PASCAL.SYSLIN
IF (IND(S1,'NAM') NE 0) EXE *+4        ;Skip default name, since NAME was specified.
GET PASCAL.SYSLIN
REA STR S9 USI=F COL=17/24
CLR ACT
END PARM OSNT,SIZE=99K,LET,NOWARN
END INCLUDE @GG.&PAS.DUMMY
IF (IND(S1,'INC') NE 0) IF (IND(S1,'OBJ') NE 0) EXE *+3
IF (IND(S1,'INC') NE 0) EXE *+11
IF (IND(S1,'OBJ') EQ 0) EXE *+13
SET VAL S4=SUB(S1,IND(S1,'OBJ'))       ;Beginning of object module name list.
IF (IND(S4,'=(') EQ 0) EXE *+8         ;Ask for object module(s), since list was not given.
SET VAL S4=SUB(S4,IND(S4,'(')+1)
SET VAL S4=SUB(S4,1,IND(S4,')')-1)||',';End of object module name list.
IF (SIZ(S4) NE 0) SET VAL S5=SUB(S4,1,IND(S4,',')-1)
   THEN END INCLUDE &S5
   THEN SET VAL S4=SUB(S4,IND(S4,',')+1)
   THEN EXE *-3
EXE *+4
REA UPP STR S5 PRO 'Enter object module name: ' ATTN=L-2
IF (S5 NE '') END INCLUDE &S5
   THEN EXE *-2
END INCLUDE PASCAL.SYSLIN
                                       ;See if DEBUG option given.
IF (IND(S1,'DEBUG') NE 0) END INCLUDE @GG.&PAS.LOADLIB(PDPXUR)
   ELSE END INCLUDE @GG.&PAS.LOADLIB(PXUR)
IF (IND(S1,'NAM') EQ 0) EXE *+4        ;If NAME was not specified, then prompt for name.
SET VAL S9=SUB(S1,IND(S1,'NAM'))
SET VAL S9=SUB(S9,IND(S9,'=')+1)
IF (IND(S9,',') NE 0) SET VAL S9=SUB(S9,1,IND(S9,',')-1)
IF (S9 NE '') EXE *+2
REA UPP STR S9 PRO 'Enter name: ' ATTN=L-2
IF (SIZ(S9) GT 8) EXE PREV
IF (S9 EQ '') COMM Pascal program must have a name.
   THEN EXE *-3
END ENTRY &S9
END NAME &S9(R)
COMM Linking...
LINK ACT CLR

SET ESC &S0
CLR ACT
CLR VAL
CLR EXE
