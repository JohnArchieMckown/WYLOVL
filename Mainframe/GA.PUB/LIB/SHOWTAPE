comment Please use the SHOW TAPES command instead.  Type HELP SHOWW TAPES for more
comment information on the command.
comment
if (fetype eq 'VIRTUAL') comment %%%COND=999
clear execute
;WYL.GA.PUB.LIB#SHOWTAPE -- created 08/10/80 by Lincoln Ong, CIT Techical Support Services
;          latest modification 12/26/85  Version 1.17
;This EXEC file searches the TLMS tape ownership data base for all tapes
;associated with a particular account and displays the status for each
;tape.
;
;Associated data sets:
;     TLMS tape ownership data base
;     SYS2.TLMS.TAPES                        -- SYSA & SYSD
;     SYS2.BTLMS.TAPES                       -- RLG & SYSE
;
;          Variables used:
;   PARM = [ACC=gg.uuu][,INTERNAL][,SORT][,LIBRARIAN]
;
;     N0 = account tape count
;
;     S0 = original ESC setting
;     S1 = PARM
;     S2 = TLMS data base data set name
;     S3 = logged on account or account setting with password
;     S4 = ALL or INTERNAL display (ALL is default)
;     S5 = working variable
;
;     W0 = original DELTA
;     W1 = last line of original active file
;     W2 = current line pointer of search results
;     W3 = start of tape information display
;
CLR VAL
SET VAL S0=ESC
SET ESC &
SET VAL W0=DEL
SET DEL=1
SET VAL W1=END
SET VAL W2=W1
SET VAL S1=UPP(PARM)
IF (IND('SYSA.SYSD.',MAC||'.') NE 0) SET VAL S2='SYS2.TLMS.TAPES'
   ELSE SET VAL S2='SYS2.BTLMS.TAPES'
SET VAL S4='ALL'
IF (IND(S1,'INTERNAL') NE 0) SET VAL S4='INTERNAL'
WRI '' ATTN=NEX
WRI 'SHOWTAPE  Revised 26 November 1985  Version 1.17' ATTN=NEX
WRI '' ATTN=NEX

IF (IND(S1,'LIBRARIAN') NE 0) EXEC *+15  ;If LIBRARIAN is specified, prompt for account.
SET VAL S3=GRO||USE
DUM ACC                                ;Verify account.
REA STR S5 USI L DEL
IF (SUB(ACC,5,2)||SUB(ACC,1,3) EQ S3) EXE *+18
IF (IND(S5,'&(GRO).&(USE)') NE 0) IF (IND(UPP(S5),'(PASSWORD)') NE 0) EXE *+17
COMM TLMS information for account &(GRO).&(USE) cannot be searched without changing
COMM your account with the PASSWORD option to account &(GRO).&(USE).
COMM
COMM To execute this EXEC file with the correct setup, type:
COMM      SET ACCOUNT gg.uuu PASSWORD
COMM      [where gg.uuu is the account you want the tape information from]
COMM      EXEC FROM LIB#SHOWTAPE PUBLIC
COMM
EXE L-2

IF (IND(S1,'ACC=') NE 0) SET VAL S3=SUB(S1,IND(S1,'C=')+2,6)  ;If ACC specified, skip account prompt.
   ELSE REA UPP STR S3 PRO 'Account (gg.uuu) <CR>=own, <ATTN>=quit? ' ATTN=L-2
IF (S3 EQ '') SET VAL S3=SUB(ACC,5,2)||SUB(ACC,1,3)
   THEN EXE *+3
IF (IND(S3,'.') NE 3) EXE *-3
   ELSE SET VAL S3=SUB(S3,1,2)||SUB(S3,4,3)

COU FRO &S2 QUI                        ;Check if the data set is empty.
IF (COU EQ 0) WRI 'TLMS data base empty.  Try searching the TLMS data base tomorrow.' ATTN=*
   THEN EXE L-2
COP '&S3' 1 FRO &S2                    ;Retrieve tape volume numbers from TLMS data base.
IF (END EQ W1) WRI "No tapes found for account &(SUB(S3,1,2)||'.'||SUB(S3,3,3))" ATTN=L-2
   THEN EXE L-2
CH 1/6 TO '' IN &W1/L NOL
ALI &W1/L LEN=1
COU &W1/L QUI
SET VAL N0=COU                         ;Account tape count.
IF (IND(S1,'SORT') NE 0) CAL SORT(LINES=&W1/L) CLR  ;If SORT is specified, sort tape volume numbers in
                                       ;decending order, rather than by dsname (default).
SET VAL W3=END

END Tape Inventory for Account &(SUB(S3,1,2)||'.'||SUB(S3,3,3)) -- &N0 tapes -- Current as of 00:00 &(SUB(DAT,10,9))
END,
REA STR S5 USI &W2 COL 1/6
DUM TAP &S5 &S4
IF (IND(S1,'INTERNAL') EQ 0) END,
SET VAL W2=W2+1
IF (W2 NE W3) EXE *-4
DEL &W1/&(W3-1)
NUM &W1/L

LIS &W1/L UNN ATTN=NEX
WRI '' ATTN=NEX
WRI '*** PRINT tape inventory?  <CR>=Yes, <ATTN>=No, L=list, or K=keep in ACTIVE.' ATTN=NEX
REA UPP STR S5 PRO 'Y/N? ' ATTN=L-4
IF (IND('K.L.N.Y.KEEP.LIST.NO.YES.',S5||'.') EQ 0) EXE *-3
IF (IND(S5,'L') NE 0) EXE *-5
IF (IND(S5,'K') NE 0) IF (F EQ W1) WRI 'The tape inventory listing is in the ACTIVE file.' ATTN=NEX
   THEN EXE L-2
IF (IND(S5,'K') NE 0) WRI 'The tape inventory listing is in lines &W1/LAST of the ACTIVE file.' ATTN=NEX
   THEN EXE L-2
IF (IND(S5,'N') NE 0) EXE *+2          ;No print
PRI &W1/L UNN
DEL &W1/L

SET DEL=&W0
SET ESC &S0
CLR VAL EXE
