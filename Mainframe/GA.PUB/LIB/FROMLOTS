comm FROMLOTS version of 02/04/83
;  bug fix 02/04/83 by Mark Lawrence:
;  was coding OPTCD=Q with RECFM=VB, causing 013 ABEND
;
;
; revised 02/02/83 by Mark Lawrence to eliminate use of the obsolete
; OUTEDIT parameter in IOPROGM; also minor cosmetic things.
;
;
; modified 06/16/81 by Mark Lawrence to allow AS or ES tapes as
; well as X and U type tapes (in case AX tape was copied)
;
;  JCL ERROR WITH OPTCD CORRECTED 04/16/81 BY MARK LAWRENCE (GA.MCL)
;  CORRECTIONS AND CHANGES 4/23/81 MARK LAWRENCE ---------
;
;  1.  DCB for CARD and PRINT formats corrected to add RECFM and OPTCD.
;  2.  DCB for CARD and PRINT formats changed to delete BLKSIZE.
;  3.  EXPLAIN for diskvols changed to display only PUB volumes.
;  4.  disk volume prompt checking changed to permit null response (nonspecific
;      volume request); explanation changed accordingly.
;  5.  Bug corrected wherin extra line of JCL with DCB inserted if user
;      got explanation of format options
;
;This is an EXEC file to write the JCL necessary to
;read a tape written at LOTS.  It will accept
;both labeled and unlabeled tapes.
;This file was originally written by Barry Benight and has been
;modified by Dave Woodruff to handle labeled tapes and direct
;printing.
;
COMM This EXEC file will help you write the JCL to move files
COMM written on a tape at LOTS to a dataset on the disk or to the
COMM printer here at C.I.T.  Reply '?' to any question for help.
COMM
;
;-------------------INITIALIZE--------------
;
CLEAR VARIABLES
SET VAL S9=ESCAPE
SET ESCAPE &
SET VALUE S3='WYL.'||GROUP||'.'||USER||'.'
SET VAL S7=' 1181 9511 1411 1281 BLNK '
DEFINE $GOTO AS "POINT ';" APPEND "'1 EXEC NOLIST" REPLACE
DEFINE $EXPLAIN AS "POINT ';EXPLAIN." APPEND "'1 EXEC NOLIST" REPLACE
SET RETURN NEXT
IF (LAST EQ 0) $GOTO NOACTIVE
COMM Your active file must be empty.  Type 'EXEC' when you are
COMM ready to continue.
EXEC PAUSE
EXEC RETURN
;NOACTIVE
END // JOB ,TIME=(,15),CLASS=B
END /*SETUP T=1 INPUT=<VOLSER>
END /*JOBPARM LINES=<MAXLIN><OUTPUT>
END //* JCL PREPARED BY 'FROMLOTS' EXECFILE
END // EXEC IOPROGM
END //INPUT DD UNIT=(T1600,,DEFER),VOL=(,RETAIN,SER=<VOLSER>),
END //         DISP=(OLD,PASS),
;
;------------------------GET THE INFO----------------
;
;GET VOLSER
SET RETURN NEXT
READ STRING S0 UPPER PROMPT='What is the Volume Serial Number of your tape? '
IF (SIZ(S0) NE 6) $EXPLAIN VOLSER
IF (INDEX(' AX AU EX EU AS ES ED',' '||SUB(S0,1,2)) EQ 0) $EXPLAIN VOLSER
IF (VER(SUB(S0,3,4),'1234567890') NE 0) $EXPLAIN VOLSER
CHANGE "<VOLSER>" TO "&S0" IN ALL NOLIST
;
SET RETURN NEXT
COMM Which program did you use to write the tape?
READ STRING S0 UPPER PROMPT = '(TAPEIO or TOSCIP) '
IF (SIZ(S0) EQ 0) $EXPLAIN WRITER
IF (INDEX(' TAPEIO',' '||S0) NE 0) THEN $GOTO TAPEIO
IF (INDEX(' TOSCIP',' '||S0) NE 0) THEN $GOTO TOSCIP
$EXPLAIN WRITER
;TOSCIP
;FORMAT
END //      DCB=(<DCBTYPE>),LABEL=(<START>,NL)
SET RETURN NEXT
COMM Which SCIP format did you use to write on the tape at LOTS,
READ STRING S0 UPPER PROMPT='(CARD or PRINT)? '
IF (SIZ(S0) EQ 0) $EXPLAIN FORMAT
IF (INDEX(' CARD PRINT',' '||S0) EQ 0) $EXPLAIN FORMAT
IF (INDEX(' CARD',' '||S0) NE 0) CHANGE "<DCBTYPE>" TO "LRECL=80,RECFM=FB,OPTCD=Q" NOLIST
ELSE CHANGE "<DCBTYPE>" TO "LRECL=133,RECFM=FB,OPTCD=Q" NOLIST
$GOTO NFILES
;
;TAPEIO
;LABEL
SET RETURN NEXT
COMM Does your tape have an internal label?
READ STRING S0 UPPER PROMPT='(YES, NO, or ? for help) '
SET VAL S0=SUB(S0,1,1)
IF (S0 NE 'Y') IF (S0 NE 'N') $EXPLAIN LABEL
IF (S0 EQ 'Y') $GOTO INTERN
SET RETURN NEXT
COMM Did you specify any format options?
READ STRING S0 UPPER PROMPT = '(YES, NO, or ? for help) '
SET VAL S0=SUB(S0,1,1)
IF (S0 NE 'Y') IF (S0 NE 'N') THEN $EXPLAIN TPFORM
IF (S0 EQ 'Y') THEN $GOTO FORMSW
END //     DCB=(RECFM=VB,LRECL=30716,BLKSIZE=30720),LABEL=(<START>,NL)
$GOTO NFILES
;
;FORMSW
COMM You will need to specify the LRECL and RECFM implied
COMM by the format you specified.  Enter ? if you need help.
READ STRING S0 UPPER PROMPT='LRECL (? for help) '
IF (VER (S0,'1234567890') NE 0) $EXPLAIN TPFORM
SET VAL N0=S0
READ STRING S0 UPPER PROMPT= 'RECFM (? for help) '
IF (INDEX(' VB VBA FB FBA ',' '||S0||' ') EQ 0) $EXPLAIN TPFORM
END //     DCB=(RECFM=&S0,LRECL=&N0,OPTCD=Q),LABEL=(<START>,NL)
IF (INDEX(' VB',' '||S0) NE 0) CHANGE ',OPTCD=Q' TO '' NOLIST
EXE PAU
$GOTO NFILES
;
;INTERN
END //     LABEL=(<START>,SL)
SET RETURN NEXT
COMM Was your tape on temporary checkout from C.I.T.?
READ STRING S0 UPPER PROMPT='(YES, NO, or ? for help) '
SET VAL S0=SUB(S0,1,1)
IF (S0 NE 'Y') IF (S0 NE 'N') $EXPLAIN CHECKOUT
IF (S0 EQ 'Y') SET VALUE S2=''
IF (S0 EQ 'N') SET VALUE S2='NOVOLCHECK'
;  S2 WILL BE USED LATER IN THE IOPROGM PARMS
;
;NFILES
SET RETURN NEXT
READ STRING S0 UPPER PROMPT='How many files are you transferring (RETURN=1)? '
IF (S0 NE '') IF (VER(S0,'1234567890') NE 0) $EXPLAIN NFILES
IF (S0 EQ '') SET VALUE N0=1
ELSE SET VALUE N0=S0
;
;SEQNO
SET RETURN NEXT
COMM What is the sequence number of the first file that you
READ STRING S0 UPPER PROMPT='want to transfer (RETURN=1)? '
IF (S0 NE '') IF (VER(S0,'1234567890') NE 0) $EXPLAIN SEQNO
IF (S0 EQ '') SET VAL N1=1
ELSE SET VAL N1=S0
CHANGE "<START>" TO "&N1" IN ALL NOLIST
;
;DEST
SET RETURN NEXT
COMM Where do you want your files to go?
READ STRING S0 UPPER PROMPT='(DISK or PRINTER) '
IF (SIZ(S0) EQ 0) $EXPLAIN DEST
IF (INDEX(' DISK PRINTER',' '||S0) EQ 0) $EXPLAIN DEST
IF (INDEX(' PRINTER',' '||S0) EQ 1) $GOTO PRINTER
;
;WE KNOW WE DON'T NEED OUTPUT SPECIFIED IN THE SETUP CARD.
CHANGE '<OUTPUT>' TO '' IN ALL NOLIST
;WE CAN ALSO SET LINE TO 1
CHANGE '<MAXLIN>' TO '1' IN ALL NOLIST
COMM Now you will need to supply a name for each file that you are
COMM transferring.  This is the name that will be associated with
COMM the file when it is stored on the disk.  After you have typed
COMM &n0 names, you may supply the name of the disk volume on which
COMM the files will be stored.
SET VALUE N5=1
;
;NAMES
SET RETURN NEXT
READ STRING S0 UPPER PROMPT="Disk file name for data from tape file #&(N5+N1-1) ? "
IF (S0 EQ '') $GOTO NAMES
IF (S0 EQ '?') $EXPLAIN NAMES
IF (VERIFY(S0,'()ABCDEFGHIJKLMNOPQRSTUVWXYZ.1234567890') NE 0 ) $EXPLAIN NAMES
IF (SUBSTR(S0,1,4) NE 'WYL.') SET VALUE S0=S3||S0
END //OUTPUT&N5 DD UNIT=DISK,VOL=SER=<DVOLSER>,DISP=(,CATLG),
END //    SPACE=(6233,(6,3),RLSE),DSN=&S0
SET VALUE N5=N5+1
IF (N5 LE N0) $GOTO NAMES
;
;DISKVOL
SET RETURN NEXT
READ STRING S0 UPPER PROMPT='What disk volume do you want the files saved on(RETURN=any PUB volume)? '
IF (S0 EQ '') $GOTO DISKDD
IF (SIZ(S0) NE 6) $EXPLAIN DISKVOL
IF (INDEX(' ENC PUB SYS SCR ',' '||SUBSTR(S0,1,3)||' ') EQ 0) $EXPLAIN DISKVOL
IF (VERIFY(SUBSTR(S0,4,3),'0123456789') NE 0) $EXPLAIN DISKVOL
;DISKDD
CHANGE '<DVOLSER>' TO '&S0' IN ALL NOLIST
IF (SUBSTR(S0,1,3) EQ 'SCR') CHANGE 'UNIT=DISK' TO 'UNIT=SYSDA' NOL
;
;--------------------SETUP IOPROGM INPUT-------------------------
;
END //SYSIN DD *
END COPY OUTFORMAT=EDIT &S2
$GOTO WRAPUP
;
;PRINTER
END //SYSIN DD *
END PRINT EJECT NFILES=&N0 &S2
SET RETURN NEXT
READ STRING S0 UPPER PROMPT = 'Which forms do you want? '
IF (INDEX(S7,' '||S0||' ') EQ 0) $EXPLAIN FORMS
CHANGE '<OUTPUT>' TO ',FORMS=&S0' IN ALL NOLIST
;
;MAXLINES
SET RETURN NEXT
COMM What is the maximum number of lines you want to print?
READ STRING S0 UPPER PROMPT '(Return=5000) '
IF (S0 NE '') IF (VER(S0,'1234567890') NE 0) $EXPLAIN MAXLINES
IF (S0 EQ '') SET VAL N1=5000
ELSE SET VAL N1=S0
SET VAL N1=N1/1000
CHANGE '<MAXLIN>' TO &N1 IN ALL NOLIST
;
;WRAPUP
COMM The JCL is in your active file.  List it to check for
COMM 'typos' and then give the 'RUN' command.
COMM
COMM If you are copying files to disk, note that your new
COMM files will be stored in WYLBUR edit format and can not always be
COMM accessed directly by batch programs.  See Consulting if you need
COMM additional help.
SET ESCAPE &S9
undefine $goto
undefine $explain
clear variables
CLEAR EXEC

;----------------------------ALL THE EXPLAINS-------------------
;EXPLAIN.VOLSER
COMM
COMM When you submitted your tape at the INFORMATION DESK at Forsythe
COMM Hall you filled out a form describing the tape.  At that
COMM time your tape was assigned a 'VOLUME SERIAL NUMBER'.  You
COMM can find this number in the top right hand corner of your
COMM copy of the tape submittal form.  It is composed of the
COMM letters 'AX' or 'AU' followed by four numbers e.g. AX1234.
COMM
EXEC RETURN
;
;EXPLAIN.FORMAT
COMM
COMM When you wrote the information on the tape at LOTS, using
COMM TOSCIP, you should have given the command 'SCIP' followed by
COMM either CARD or PRINT.  If you did not use one of these
COMM standard LOTS-SCIP formats, this execfile will not work for
COMM you.  If you did use one of these formats just enter CARD or
COMM PRINT.
COMM
EXEC RETURN
;
;EXPLAIN.NFILES
COMM
COMM You can transfer several files from tape if they appear on the
COMM tape immediately following each other.  If you added six files
COMM on the end of the tape at LOTS and would like to transfer just
COMM those six you would answer '6'.
COMM
EXEC RETURN
;
;EXPLAIN.SEQNO
COMM
COMM Every file on a tape is identified by its sequence number, 1
COMM being the first file on the tape, 2 being the second, etc.
COMM If your tape was empty when you wrote on it at LOTS you
COMM wrote into the first file on the tape (if you had written
COMM more than one file, you would also have written the second,
COMM and so on).  You need to give the sequence number of the
COMM first file on the tape that you would like to transfer.
COMM This will then start at that file and transfer the number of
COMM files that you specified above.
COMM
EXEC RETURN
;
;EXPLAIN.NAMES
COMM
COMM At C.I.T., just as on LOTS every file that is stored online
COMM (on a disk pack) must have a name so you can refer to it.
COMM These names are not put on the tape you wrote the files on
COMM at LOTS, so you must supply them.  So that two different
COMM people can have files called the same thing, the full name
COMM of the file begins with WYL.GG.UUU.name  where GG is the
COMM file owner's group code and UUU is the file owner's user
COMM code.  The WYL stands for WYLBUR and tells what file system
COMM the file is in.  You do not normally need to refer to the
COMM file by its full name.
COMM
COMM The 'name' that you specify should consist of 8 characters or
COMM fewer, starting with a letter.
COMM
EXEC RETURN
;
;EXPLAIN.DISKVOL
COMM
COMM There are many disk volumes available to save files (another
COMM name is data sets) on.  You do not have to specify a volume;
COMM if you don't, each data set will be saved on any available storage
COMM volume.  The location of the data sets will be recorded in the
COMM system catalog, so you don't need to know which volume is used.
COMM WYLBUR will automatically look in the catalog when you refer to
COMM the data set.
COMM
COMM If you want to specify a volume, choose one of the PUB series volumes
COMM (storage volumes) listed below:
SET VAL W8 END
DUMP VOLS
D ~'PUB0' IN &W8/L NOL
COMM
L &W8/L UNN
D &W8/L
COMM
EXEC RETURN
;
;EXPLAIN.DEST
COMM
COMM If you want your files printed immediately without storing
COMM them on disk, type "PRINTER".  If you want to transfer the
COMM files from tape to disk, type "DISK".
COMM
;
;EXPLAIN.FORMS
COMM
COMM If you are going to print your files directly, you must
COMM specify the forms you want used.  The possibilities are:
COMM 9511:  White paper, no stripes, 8 and 1/2 by 11 inches.
COMM 1181:  Blue and white stripes, 11 by 8 and 1/2 inches.
COMM 1411:  Blue and white stripes, 15 by 11 inches
COMM 1281:  White paper, no stripes, 12 by 8 and 1/2 inches.
COMM BLNK:  White paper, no stripes, 15 by 11 inches
COMM
EXEC RETURN
;
;EXPLAIN.LABEL
COMM
COMM A labeled tape has information written at its beginning
COMM identifying the owner, density, and format of the
COMM material on the tape.  This information is created by
COMM initializing the tape at C.I.T. or by running the TAPELABEL
COMM program at LOTS.
COMM
COMM If your tape was written with TOSCIP it doesn't have
COMM an internal label.  If you wrote your tape with
COMM TAPEIO or some other utility and had previously used
COMM TAPELABEL to create an internal label at the beginning of
COMM the tape then you should answer yes.
COMM
EXEC RETURN
;
;EXPLAIN.CHECKOUT
COMM
COMM If your tape was initialized and checked in at C.I.T. and
COMM if you did not check the tape out permanently you should
COMM answer 'yes'.  Otherwise, you should answer 'no'.
COMM
EXEC RETURN
;
;EXPLAIN.MAXLINES
COMM
COMM If you are going to print a large file (over 5000 lines), it
COMM will be necessary to specify a maximum number of lines to be
COMM printed.  If your file is not more than 5000 lines long, you
COMM should type a carriage return.
COMM
EXEC RETURN
;
;EXPLAIN.WRITER
COMM
COMM If you wrote the tape at LOTS, GSB or SCORE you should
COMM have written the tape using TAPEIO (or you might have used
COMM the older program TOSCIP -- SCIP is the old name for
COMM C.I.T.).  If you used DUMPER, the tape cannot be read at
COMM C.I.T.  If you used some other industry compatible utility
COMM to write the tape, you should use the IOPROGM program
COMM here at C.I.T.
COMM
EXEC RETURN
;
;EXPLAIN.TPFORM
COMM
COMM When you wrote the tape you may have specified one of the
COMM following formats, or may have specified LRECL, BLKSIZE, or
COMM RECFM explicitly:
COMM
COMM Name       LRECL         BLKSIZE     RECFM
COMM
COMM VCARD      30716         30720       VB
COMM VPRINT     30716         30720       VBA
COMM FCARD         80         30720       FB
COMM FPRINT        80         30720       FBA
COMM TCARD         80          8000       FB
COMM TPRINT       132          7920       FBA
COMM
COMM The default is VPRINT.
COMM
EXEC RETURN
;
