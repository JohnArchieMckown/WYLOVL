   comm LPLOT - SAS/GRAPH local plotting EXEC file
   comm

   def $goto   as  "point ';" append " '1(1) exe nol " rep

   prton  = s'27'||'[5i'              ;seq to turn printer port on
   prtoff = s'27'||'[4i'              ;seq to turn printer port off

   pstart  = s'27'||'.('              ;turn plotter on
   pstop = s'27'||'.)'||s'0d'        ; turn plotter off

   ;checkact
   if (last le 0) $goto pause
   p x'274b'(1) n                     ;look for esc-dot
   if (* gt 0) $goto haveplot
   comm Active file does not contain plot data.
   comm

   ;pause
   comm Place plot output in your active file and then type 'EXEC' to continue.
   comm
   exec pause
   $goto checkact

   ;haveplot
   oldesc = escape
   set escape &

   didplot = false
   plotting = false
   attn = false
   papersize = ''
   penmount = ''

   if (sub(lin(f),1,1) eq s'27')
       then read str s0 pro 'Press RETURN to start plotting: '

   p x'2f'(1) n
   if (* gt 0)
     then comm Type 'pause' at any prompt to pause plotting and temporarily return to WYLBUR.
     then comm Press BREAK to terminate plotting.
     then comm

   p'&(prtoff)'(1) nolist
   if (* lt 0)
       then ch '&(pstop)' to '&(pstop)&(prtoff)' nolist

   ptr = first

   ;loop

   if (sub(lin(ptr),1,1) eq s'27') $goto plot
   pr = lin(ptr)
   if (siz(pr) gt 3)
    then if (sub(pr,siz(pr)-2) eq s'2f0d25')
     then pr = sub(pr,1,siz(pr)-2)

   if (ind(pr,'SET THE PAPER SIZE') gt 0)
    then s0 = papersize
    then papersize = pr
    then if (s0 eq pr) $goto nextline

   if (ind(pr,'MOUNT THE FOLLOWING PENS') gt 0)
    then s0 = penmount
    then penmount = pr
    then if (s0 eq pr) $goto nextline

   write pr nonl nomodel attn=*+2
   exec *+2
   $goto attn

   if (ind(pr,s'2f') le 0) $goto nextline
   read str s0 attn=*+2 prompt ' '
   exec *+2
   $goto attn
   if (s0 eq '') $goto nextline
   Comm Type EXEC to continue.
   exec pause
   $goto nextline

   ;plot
   p '&(pstop)'(1) in &(ptr)/last n
   if (* lt 0)
       then comm Missing plot data.
       then $goto done

   plotting = true
   write prton nonl nomod attn=*+2    ;data to plotter
   exec *+2
   $goto attn
   list &(ptr)/* unn nonl nomodel nopause clean attn=*+2  ;plot data
   exec *+2
   $goto attn
   write prtoff nonl nomod attn=*+2   ;turn off printer port
   exec *+2
   $goto attn
   plotting = false
   didplot = true
   ptr = *

   ;nextline

   if (ptr lt last)
       then set * &(ptr)
       then ptr = next
       then $goto loop

   ;attn
   if (plotting) write prtoff nonl nomodel
   attn = true

   ;done

   comm
   if (attn) comm Plotting terminated.
    else if (~didplot) comm No plots generated.
     else comm Plotting complete.

   set escape &(oldesc)
