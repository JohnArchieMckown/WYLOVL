;WYL.GA.PUB.LIB#NEWLOGON
;This program is executed as the first action taken on all new accounts
;  (except certain bulk accounts like those limited to SOCRATES).
;
;  Last modified 7-Sep-84, Niz
; changed to exit after set autos to prevent new logon stuff for user
;    accounts; kab 11/19/97

SET AUTO EXEC QUIET  ; Reset NEWEXEC as quickly as possible
SET AUTO NOEXEC NOMAIL NOPTIME NOLTIME NORECOVERY QUIET
cle exe
;
CLEAR VALUES
DEFINE $GOTO AS "POINT ';" APPEND " ' 1 (1) EXEC NOL " REP
COMM
COMM Welcome to the New User setup
COMM
;SET PARAMETERS:
SET ESC &
SET MAIL
COMM To read received mail, type SHOW MAIL.
DUMP CAT
P 'LIB '1 N
IF (* LE 0) ALLOCATE LIB TRACKS=2 BLOCKS=15 QUIET
    THEN EXE *+4
DUMP DIRECTORY
P 'LOGON '1 N
IF (* GT 0) THEN $GOTO ***** CHANGE PASSWORD
SAVE LIB#LOGON LINES L-6/L EXEC QUIET
SET VAL S0 'NEWLOGON'
CLEAR ACTIVE

;***** CHANGE PASSWORD
COMM
COMM The password used to start this session is temporary.
COMM You will now be asked to reenter this password,
COMM and then a new one - from 5 to 8 characters long.
COMM
SET PASSWORD
COMM
COMM Use this password in future sessions.
COMM It should be changed periodically - the system will remind
COMM you when the age of your password reaches 14 days.
COMM

;SET NAME?
IF (NAME EQ '') THEN $GOTO ***** NO NAME
COMM
COMM The name setup to identify you during terminal sessions is:
COMM
COMM              &(NAME)
COMM
COMM You may change it at any time with a SET NAME command.
EXEC SAVE
READ STR S1 UPPER PRO 'Do you want to change it now (Yes/No)? '  ATTN=*

;***** CHECK RESPONSE
IF (SUBSTR(S1,1,1) EQ 'N') THEN $GOTO ***** EXIT
IF (SUBSTR(S1,1,1) NE 'Y') THEN EXE RET
COMM
EXEC SAVE
READ STR S1       PRO 'Enter a new name now (20 character maximum): ' ATTN *
SET NAME '&S1'
$GOTO ***** EXIT

;***** NO NAME
COMM No name or nickname has been set to identify you during terminal
COMM sessions.  It is useful but not necessary to have one.
EXEC SAVE
READ STR S1 UPPER PRO 'Do you want to set one up now (Yes/No)? ' ATTN=*
$GOTO ***** CHECK RESPONSE

;***** EXIT
COMM
COMM To assist you in getting information on how to use the
COMM computer, type:
COMM                 HELP topic
COMM
COMM where topic is the desired subject.  There is a topic
COMM called Newuser, which you may wish to try now.  If so,
COMM type HELP NEWUSER  now.   Good luck.
COMM
SET ESC
IF (S0 EQ 'NEWLOGON') EXEC FROM LIB#LOGON NOPARM CLEAR
  ELSE CLEAR EXEC

;These commands are executed automatically upon logon to your account:
SET TERMINAL HEIGHT=20
SET MODE NOATTN WARN=1
SET JES NOTIFY
COMMENT
IF (IND(PARM,'NEWMAIL') NE 0) SHOW MAIL SUMMARY
CLEAR EXEC
