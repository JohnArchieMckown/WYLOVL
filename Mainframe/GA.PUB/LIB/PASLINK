;PASCCG--created 08/02/80 by Eagle Berns and Lincoln Ong
;          latest modification 04/27/81  Version 1.03
;This EXEC file COMPILES, LOADS, and GO (executes) an IBM Pascal program interactively.
;
;          Variables used:
;     N0 = file type flag (WYLBUR or ORVYL)
;     N1 = file flag
;          0 -- default ACTIVE file
;          1 -- default ORVYL file = PASCAL.SYSIN
;          2 -- WYLBUR (O.S.) data set (name given in S3)
;          3 -- ORVYL file (name given in S3)
;     N2 = index for S3
;     N9 = condition code (returned from Pascal step)
;
;     S0 = original ESC setting
;     S1 = PARM PLUS THE DEBUG OPTION
;     S2 = DEBUG option
;     S3 = data set name
;     S4 = reserved
;     S5 = reserved
;     S9 = program name (returned from the LOAD phase)
;
CLR VAL COR
SET VAL S0=ESC
SET ESC &
SET VAL N9=255                         ;Non zero so it does not mess up the TRANSLATE phase if it dies.
SET VAL S1=UPP(PARM) || ',MARGINS(1,100),DEBUG'
SET VAL N0=IND(S1,'WYL')               ;Go get Pascal source in ACTIVE (if necessary).
IF (N0 NE 0) EXE *+3
SET VAL N0=IND(S1,'ORV')
IF (N0 NE 0) SET VAL N1=1
IF (N0 EQ 0) EXE *+9
SET VAL S3=SUB(S1,N0)
SET VAL N2=IND(S3,',')
IF (N2 EQ 0) SET VAL N2=SIZ(S3)+1
SET VAL S3=SUB(S3,1,N2-1)
SET VAL N2=IND(S3,'=')
IF (N2 EQ 0) EXE *+3
SET VAL S3=SUB(S3,N2+1)
IF (SIZ(S3) NE 0) SET VAL N1=N1+2
IF (N1 EQ 0) SET VAL S3=''
IF (N1 EQ 1) SET VAL S3='GET PASCAL.SYSIN'
IF (N1 EQ 2) SET VAL S3='USE '||'&S3'
IF (N1 EQ 3) SET VAL S3='GET '||'&S3'
&S3
IF (LINES LE 0) REA STR S3 PRO 'Enter command to put Pascal source (no JCL) in ACTIVE: ' UPP ATTN=L-1
THEN EXE *-2
PUT PASCAL.SYSIN REP QUI               ;Put away the source.
;
;Pascal COMPILE step
;
COMM Compiling...
CALL PCALL'&S1' PUB CLR       ;LOAD phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')"
THEN EXE L-1
CALL PCALO'&S1' PUB CLR       ;LOAD phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')"
THEN EXE L-1
CALL PCALT'&S1' PUB CLR       ;TRANSLATE phase
IF (N9 NE 0) WRI "&(S'2F')CONDITION CODE &N9&(S'2F')"
THEN EXE L-1
;
;Pascal LOAD step
;
COMM Linking...
DUMP BLOCK
REA STR S6 LAST DELETE
SET VAL S6 SUB(S6,SIZ(S6)-2,3)
set val s8 '09600'
;IF('&S6' NE 'DAY') EXE *+3
set val s7 '(Time out interval : 1 Second)'
;exe *+3
;set val s8 '12c00'
;set val s7 '(Time out interval : 2 Seconds)'
IF (IND(S1,'DEBUG') NE 0) SET VAL S2='DEBUG/'  ;Check if DEBUG option given.
THEN GET CSD.LINK.USER.DEBUG ACC PAS$GG CLR  ;IF true, link with DEBUG modules.
ELSE GET PCAL.LINK.USER ACC PAS$GG CLR
CH '@@@@@@@@' TO '&S9' NOL
ch '%%%%%' to '&s8' nol
LINK ACT CLR
GET PASCAL.SYSIN CLR
SET ESC &S0
CLR VAL EXE
