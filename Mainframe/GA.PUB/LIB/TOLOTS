;   TOLOTS
;
;  This EXEC file constructs the JCL to write a tape which will be
;  read at LOTS.
;
;  Report problems to:             Mark Lawrence (GA.MCL)
;                                  C.I.T. Technical Support Services
;                                  497-4976
;
;  Revised 02/25/83 to replace OUTEDIT option (obsolete) with OUTFORMAT
;
;
;  VARIABLE USAGE:
;
;  S0    VARIOUS PROMPTS
;        DSNAME FOR INPUT DD STATEMENT
;  S1    USER NAME FOR JOB STATEMENT
;        VOLUME SERIAL FOR INPUT DD STATEMENT
;  S3    DSNAME PREFIX AS WYL.GG.UUU.
;  S9    USER'S ESCAPE CHARACTER
;  W9    USER'S END LINE NUMBER
;  S4    VOLUME SERIAL OF TAPE
;  N2    STARTING FILE SEQUENCE NUMBER
;  N5    DD SEQUENCE NUMBER
COMM
;
;-------------------INITIALIZE--------------
;
CLEAR VARIABLES
SET VAL W9 END                     ;  USER'S END LINE
SET VAL S9=ESCAPE
SET ESCAPE &
SET VAL S1 SUBSTR(NAME,1,20)
IF (S1 EQ '') SET VAL S1 'LOTS FILE TRANSFER'
SET VALUE S3='WYL.'||GROUP||'.'||USER||'.'
DEFINE $GOTO AS "POINT ';" APPEND "'1 EXEC NOLIST" REPLACE
DEFINE $EXPLAIN AS "POINT ';EXPLAIN." APPEND "'1 EXEC NOLIST" REPLACE
DEFINE $ERROR AS "POINT ';ERROR." APPEND "'1 EXEC NOLIST" REPLACE
SET RETURN NEXT
COMM TOLOTS version of 02/25/83
COMM For help, reply with ? to any prompt.
REA STR S0 UPP PRO 'Do you need instructions? '
IF (S0 EQ '') EXE 45
IF (S0 EQ 'NO') EXE 45
SET RETURN 45
$EXPLAIN GENERAL
;
;------------------------GET THE INFO----------------
;
;GET VOLSER
SET RETURN NEXT
COMM
READ STRING S4 UPP PRO='What is the Volume Serial Number of your tape? '
IF (S4 EQ '?') $EXPLAIN VOLSER
IF (SIZ(S4) NE 6) $ERROR VOLSER
IF (VER(SUB(S4,1,2),'AXEXAUEU') NE 0) $ERROR VOLSER
IF (VER(SUB(S4,3,4),'1234567890') NE 0) $ERROR VOLSER
;SEQNO
SET RETURN NEXT
COMM What position on the tape do you want to start
READ STRING S0 UPP PRO='copying files to (RETURN=1)? '
IF (S0 NE '') IF (VER(S0,'1234567890') NE 0) $EXPLAIN SEQNO
IF (S0 EQ '') SET VAL N2=1
ELSE SET VAL N2=S0
END //  JOB  ,'&S1',TIME=(,15)
END /*SETUP T=1 OUTPUT=&S4,T1600
END //* JCL PREPARED BY 'TOLOTS' EXECFILE
END // EXEC IOPROGM,PARM='OUTFORMAT=NOEDIT'
END //OUTPUT DD UNIT=(T1600,,DEFER),VOL=(,RETAIN,SER=&S4),
END //    DCB=(DEN=3,BLKSIZE=8000),LABEL=(&N2,SL),DISP=(,KEEP)
;
COMM
COMM Now you will need to give the name of each data set that
COMM you want to transfer and the name of the disk volume it is
COMM stored on.  Press RETURN (only) to terminate prompting.
COMM
SET VALUE N5=1
;
;NAMES
SET RETURN NEXT
COMM
SET VAL S1 ''
READ STRING S0 UPP PRO="Name of data set #&N5 ? "
IF (S0 EQ '') $GOTO FINAL
IF (S0 EQ '?') $EXPLAIN NAMES
IF (VERIFY(S0,'ABCDEFGHIJKLMNOPQRSTUVWXYZ.1234567890(#)') NE 0 ) $ERROR NAMES
IF (SUBSTR(S0,1,1) EQ '#') SET VAL S0 LIBRARY||S0
SET VAL N0 INDEX(S0,'#')
IF (N0 EQ 0) $GOTO NOTLIB
SET VAL S0 SUBSTR(S0,1,N0-1)||'('||SUBSTR(S0,N0+1)||')'
;NOTLIB
IF (SUBSTR(S0,1,4) NE 'WYL.') SET VAL S0=S3||S0
;
;DISKVOL
IF (INDEX(S0,'(') NE 0) $GOTO CAT
SET RETURN NEXT
COMM
READ STRING S1 UPP PRO='Disk volume (RETURN if cataloged)? '
IF (S1 EQ '?') $EXPLAIN DISKVOL
IF (SIZE(S1) NE 6) IF (SIZE(S1) NE 0)  $ERROR DISKVOL
IF (S1 NE '') IF (VER(SUBSTR(S1,1,3),'PUBSCRENCSYS') NE 0) $ERROR DISKVOL
IF (S1 NE '') IF (VER(SUBSTR(S1,4,3),'1234567890') NE 0) $ERROR DISKVOL
;CAT
END //IN&N5 DD UNIT=DISK,VOL=SER=&S1,DISP=OLD,
END //    DSN=&S0
IF (SUBSTR(S1,1,3) EQ 'SCR') CHANGE 'UNIT=DISK' TO 'UNIT=SYSDA'IN LAST-1  NOL
SET VALUE N5=N5+1
$GOTO NAMES
;
;--------------------SETUP IOPROGM INPUT-------------------------
;
;FINAL
SET VAL N9 W9
COMM
COMM The JCL is in lines &N9/last of your active file.  List it to check
COMM for errors and then give the command
IF (N9 EQ 1) COMM          RUN
ELSE COMM          RUN &N9/last
COMM You should also SAVE the jcl in case you need to re-run the job
COMM or see Consulting.
SET ESCAPE &S9
UNDEFINE $GOTO
UNDEFINE $EXPLAIN
UNDEFINE $ERROR
CLEAR EXEC

;----------------------------ALL THE EXPLAINS-------------------
;
;ERROR.VOLSER
COMM &S4:  INVALID TAPE NUMBER
EXE RETURN
;EXPLAIN.VOLSER
COMM
COMM When you submitted your tape at the Information Desk at Forsythe
COMM Hall you filled out a form describing the tape.  At that
COMM time your tape was assigned a 'VOLUME SERIAL NUMBER'.  You
COMM can find this number in the top right hand corner of your
COMM copy of the tape submittal form.  It is composed of the letters
COMM AX, AU, EX, or EU followed by four numbers e.g. AX1234.
comm
COMM The tape must have been initialized as standard label (SL),
COMM either at CIT or elsewhere using the CIT tape number as the
COMM volume serial.  If the tape was ititialized elsewhere and has
COMM a volume serial number different from the label, you will have
COMM to modify the JCL created by TOLOTS; see Importing and Exporting Tapes.
EXEC RETURN
;
;EXPLAIN.SEQNO
COMM
COMM Every file on a tape is identified by its sequence number,
COMM 1 being the first file on the tape, 2 being the second, etc.
COMM If your tape is empty or if it has no data on it that you
COMM want to keep, you would want to copy the files to position
COMM one on the tape.  Since whenever you write anything on a
COMM tape you destroy all the data that comes after that
COMM position, if you already have four files on the tape that
COMM you want to keep, you would answer '5'.  You should not
COMM give a number that is more than one beyond the last file
COMM currently on the tape.
COMM
EXEC RETURN
;
;ERROR.NAMES
COMM &S0:  INVALID DATA SET NAME.
EXE RETURN
;
;EXPLAIN.NAMES
COMM
COMM This question wants to know the name of the data sets
COMM (files) here that you want copied to the tape to
COMM transfer to LOTS.  You need not give the fully qualified
COMM name, e.g. you do not have to give the filename in the
COMM in the form &s3name .  Enter as many names ay you want;
COMM when you are done, just press RETURN in response to the prompt.
COMM
COMM You can only transfer WYLBUR-type data sets, that is, data sets
COMM that you can USE or SAVE with WYLBUR.  Also, you cannot transfer
COMM a library, although you can transfer individual members.
COMM Data sets that cannot be transferred include the following:
COMM *  Load module libraries (transfer the source program instead)
COMM *  SPSS SYSTEM FILES (use WRITE CASES and WRITE FILEINFO to
COMM                       create a compatable file)
COMM *  SAS DATA BASES
COMM *  DIRECT DATA SETS (DSORG=DA)
COMM
EXEC RETURN
;
;ERROR.DISKVOL
COMM &S1:  INVALID VOLUME.
EXE RETURN
;EXPLAIN.DISKVOL
COMM
COMM This question justs wants to know which volume the data
COMM set you named is on.  You should answer one of these:
COMM
SHO VOLS
COMM
COMM or just (RETURN) if the data set is cataloged.
EXEC RETURN
;
;EXPLAIN.GENERAL
;
COMM This EXEC file will construct the Job Control Language (JCL) to
COMM write a tape which can then be taken to LOTS.  The tape will be
COMM written in EBCDIC at 1600 BPI and will have standard labels.
COMM
COMM You can also use it to write tapes for export to installations other
COMM than LOTS.  In this case you may wish to change the LABEL parameter
COMM in the JCL to specify "NL" (for no labels) or "AL" (for ASCII
COMM labels) instead of "SL" (IBM standard labels).  You must have the
COMM tape initialized with the label type you want; ask the Information
COMM Desk for assistance if you need to re-initialize the tape.  If you
COMM want an unlabeled ASCII tape, you must add the subparameter OPTCD=Q
COMM to the DCB parameter in the JCL.  For more information, refer to
COMM "Importing and Exporting Tapes", available at Document Sales.
COMM
COMM The IOPROGM data set utility program will be used to write the tape;
COMM refer to the "IOPROGM User's Guide" for more information on IOPROGM.
COMM
EXEC RETURN
