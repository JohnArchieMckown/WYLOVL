;  last revision 06/29/90 by Mark Lawrence
;
;  Revised 06/29/90:  Call FORMCHEK to test form code
;  Revised 04/07/89:  handling of long dsns
;  Revised 04/06/89:  Correct handling of default forms
;
;  Revised 02/21/89:  New variables; OPEN new active file; use
;                     DSNFORMAT function; forms/chars/pformat fixes;
;                     remove 9511, impact printer forms.
;
;  Bug fix 01/22/87:  ATTN exit from FORMS prompt caused error
;                     because previous dump dir output not cleared
;
;  Bug fix 05/09/86:  ATTN exit from "do all members..." prompt
;                     caused loop
;
;  Revised 02/14/86:  Remove "Do you want a punched deck?" prompt
;                     Install new "LIBLIST OPTIONS?" prompt
;                     Misc. HELP editing
;  Bug fix 09/10/85:  Failed if title of member had quote in it (in
;                     PROMPT mode)
;  Bug fix 01/24/85:  was clobbering mem list if noprint
;  BUG FIX 8/02/84:  correct code that recognizes "SYSn." data sets
; revised 02/28/84:  Install 4 modes of operation (all, list,
; prompt, dir)
;
;  revised 02/10/84:
;        remove UPLOW stuff
;        general cleanup/renumbering
;        remove SET DELTA
;        default is page eject
;
;  revised 09/28/83 to increase line limit, remove old dump dir
;  format, fix bug in prompt code
;  minor corrections 06/10/83 by Mark Lawrence
;  Revised 06/07/83 to handle either new or old DUMP DIR format
;  Revised 05/20/83 by Mark Lawrence:
;
;  Remove VOLUME code--always assume cataloged
;  Remove SET LENGTH stuff
;
;  Bug fix 22 Jul 80 by Mark Lawrence
;
; Variable usage
;
; S0           misc. prompts
; S1           DSN, SHO DIR options, FORMS, CHARS
; S2           misc.
; PDSN         DSN for member prompt
; EPARM        PARM for EXEC statement
; JPARM        JOBPARM options
; LIBNAME      Library name
; OLDESC       User's ESC character
;
; W0           misc. line pointers
; XEND         END pointer for DUMP DIR
; OLDACT       Old active file number, 0 if none
;
; N0           misc. index pointers
; N1           more misc. index pointers
; N2           used in line-number formatting for RUN prompt
; NCOPIES      COPIES value
; DSNFLAG      flag for DSN from PARM (1=yes)
CLEAR VARIABLES
DECLARE STRING  LDSN DOPT FORMS CHARS PDSN EPARM JPARM LIBNAME OLDESC
DECLARE STRING FORM_TEST
DECLARE NUM  XEND NCOPIES PMODE OLDACT
DECLARE BOOLEAN  DSNFLAG
OLDACT = ACTNO
IF (LINES EQ 0) OLDACT = 0
OPEN LIBLIST TITLE 'JCL generated by UTIL LIBLIST'
OLDESC = ESC
SET ESC &
LIBNAME = STRIP(UPPER(PARM))       ; SEE IF NAME SUPPLIED AS PARM
DSNFLAG = FALSE                    ; DSN FROM PARM
IF (LIBNAME NE '') DSNFLAG = TRUE
COMM
COMM LIBLIST version of 06/29/90
COMM
IF (DSNFLAG) EXE 100 ; GOT NAME, SKIP INTRO
REA STR S0 UPP PRO 'Do you want instructions? ' ATTN=900
IF (IND('YES OK ?',S0) NE 0) LIST 1001/1049 EXE UNN ATTN=100
   ELSE WRI ' '
;
;  LIBRARY NAME
;
LDSN = DSNFORMAT(LIBRARY)
IF(LIBNAME EQ '') REA STR LIBNAME UPP PRO 'Enter library name (RETURN=&(LDSN))? ' ATTN=900
IF (LIBNAME EQ '') EXE 155               ;  USE DEFAULT
; Scan out leading blanks
LIBNAME = STRIP(LIBNAME)

IF (LIBNAME EQ '?') LIST 1101/1149 EXE UNN ATTN=NEXT ; HELP DSN
   THEN LIBNAME = ''
   THEN EXE 100
TRY LIBNAME=DSNFORMAT(LIBNAME)
IF (TRYERR) WRI 'Invalid dsname. '
   THEN LIBNAME=''
   THEN EXEC 100

; Got a dsname
LDSN = LIBNAME                      ;  COPY QUALIFIED NAME
IF (DSNFLAG) WRI 'Library name is '||LIBNAME
   THEN WRI ' '

LIBNAME = LDSN                      ;  SAVE DSN
PDSN = LDSN                      ;  COPY DSN FOR MEMBER PROMPT
;  LIBLIST Options:  PUNCH, UPDTE, NOPRINT, EJECT
EPARM = '' ; no options yet
WRI ' '
REA STR S0 UPP PRO 'LIBLIST options (RETURN=EJECT): ' ATTN=290
IF (IND(S0,'?') NE 0) LIST 1201/1299 EXE UNN ATTN=200 ; HELP OPTIONS
   THEN EXE 200
IF (IND(S0,'UPDT') NE 0 AND IND(S0,'NOUPDT') EQ 0) EPARM = ',PUNCH,UPDTE'
   THEN EXE 210
IF (IND(S0,'PUNCH') NE 0 AND IND(S0,'NOPUN') EQ 0) EPARM = ',PUNCH'
   THEN EXE 210
IF (IND(S0,'DECK') NE 0 AND IND(S0,'NODEC') EQ 0) EPARM = ',PUNCH'
;
IF (IND(S0,'NOPRI') NE 0) EPARM = EPARM||',NOPRINT'

IF (IND(S0,'NOEJE') NE 0) EPARM = EPARM||',ECOL'
   THEN EXE 220
IF (IND(S0,'ECOL') NE 0 AND IND(S0,'NOECOL') EQ 0) EPARM = EPARM||',ECOL'


; options cleanup
IF (IND(EPARM,'ECOL') EQ 0) EPARM = EPARM||',NOECOL' ; default if no opt

EXE 300
; ATTN EXIT FROM OPTIONS PROMPT
LIBNAME = ''  ; FORGET DSN
DSNFLAG = FALSE
EXE 100

; Put in a new END line so member list ends in right place
PUTLINE 9 //SYSIN DD *

;  MODE:  ALL, LIST, OR PROMPT
;
COMM
WRI 'Do ALL members, PROMPT with names, ' ATTN=200
REA STR S0 UPP PRO 'Do DIRECTORY subset, or use LIST (RETURN=ALL)? ' ATTN=200
IF (S0 EQ '') PMODE = 1 ;
   THEN EXE 600 ; RETURN=ALL
IF (S0 EQ 'ALL') PMODE = 1 ;
   THEN EXE 600
IF (IND(S0,'?') NE 0) LIST 2101/2120 EXE UNN ATTN=300 ; HELP MODE
   THEN EXE 300
IF (SUB(S0,1,3) EQ 'PRO') PMODE = 2
   THEN EXE 500                    ; PROMPT
IF (SUB(S0,1,3) EQ 'DIR') PMODE = 3
   THEN EXEC 500                   ; SAME AS "PROMPT" SO FAR
IF (SUB(S0,1,3) EQ 'LIS') PMODE = 4
   THEN EXE 400                    ; LIST
; INVALID RESPONSE
WRI S0||':  Invalid.  Please reply ALL, PROMPT, DIRECTORY, or LIST. ' ATTN=300
WRI 'For more information, type ?' ATTN=300
EXE 300
;
;  MEMBER LIST
;
COMM
REA STR S0 UPP PRO 'Enter member names, separated by commas: '  ATTN=300
IF (IND(S0,'?') NE 0) LIST 1501/1549 EXE UNN ATTN=NEXT
   THEN EXE 400
IF(S0 EQ '') EXE 600

; HERE WITH A MEMBER NAME
IF(SIZE(S0) LE 63)EXE *+6          ;  FITS ON ONE LINE
N1 = IND(S0,',')
S2 = SUBSTR(S0,1,N1-1)       ;  FIRST NAME FROM LIST
PUTEND MEMBER=&S2
S0 = SUBSTR(S0,N1+1)
IF(SIZE(S0) GT 63)EXE *-4
PUTEND MEMBER=(&S0)
CH ' ' TO '' IN L NOL
COMM
REA STR S0 UPP PRO'Any more members (RETURN=NO)? '  ATTN=600
IF (S0 EQ 'ATTN') EXE 600
IF (IND(S0,'?') NE 0) LIST 1551/1599 EXE UNN ATTN=NEXT
   THEN EXE 421
IF(IND('YESOK',S0) GE 1)EXE 400    ;  PROMPT FOR MORE
IF(IND('NO',S0) GE 1) EXE 600      ; "NO"
IF(SIZE(S0) NE 0) EXE 410          ;  USER REPLIED WITH NAMES
EXE 600
;  PROMPT MODE OR DIRECTORY MODE
;  S0="PROmpt [options]" or "DIRectory [options]"
XEND = 10   ; LEAVE ROOM FOR JCL
N0 = IND(S0,'PRO') ; -->"PROMPT"
IF (PMODE EQ 3) N0 = IND(S0,'DIR')
S0 = SUB(S0,N0)  ; STRIP ANY LEADING BLANK
N0 = IND(S0,' ') ; FIND A BLANK
IF (N0 EQ 0) S0 = ''  ; NO BLANK, FORGET IT
   THEN EXE 520
S0 = SUB(S0,N0+1) ; OPTIONS IF ANY
IF (S0 EQ '') EXE 520 ; NONE
IF (S0 EQ ' ') S0 = '' ; NONE
   THEN EXE 520
; NOW S0=OPTIONS

IF (S0 EQ '') REA STR DOPT PRO 'Enter SHOW DIRECTORY options (RETURN=no options): ' ATTN=300
   ELSE REA STR DOPT PRO 'Enter SHOW DIRECTORY options (RETURN=&S0): ' ATTN=300
SET RETURN 500
IF (IND(DOPT,'?') NE 0) LIST 1601/1649 EXE UNN ATTN=NEXT
   THEN EXE 520
IF (DOPT EQ '') DOPT = S0 ; USE DEFAULT
TRY SHOW DIR IN &(PDSN) &(DOPT) COLLECT &(XEND) BY 1
IF (TRYERR) EXE 520
DEL &(XEND)(3) ; HEADER LINES FROM DUMP DIR
IF (LAST LT XEND) WRI 'No members &(DOPT) found.'
   THEN EXE 300
POI NEXT NOLIST  ; POINT TO FIRST MEMBER LINE
W0 = *                       ; point to names
IF (PMODE EQ 3) EXE 540               ; DIR mode, no questions asked
;
COMM List of members follows.  For each member, type YES or LIST to list,
COMM type NO or press RETURN to skip.  Press BREAK to stop prompting.
EXE 550

; DIR MODE:  LIST WHAT WE FOUND
WRI ' '                                          ATTN=550
WRI 'The following members will be printed:'     ATTN=550
WRI ' '                                          ATTN=550
LIST &W0/LAST UNN ATTN=550


; LOOP
REA STR S1 USI &W0
IF (PMODE EQ 3) EXE 570 ; DIR mode, just do it

WRI 'List ' NONL ATTN=590
LIST &W0 NONL UNN  ATTN=590
REA STR S0 UPP PRO '? ' ATTN=590
IF (IND(S0,'?') NE 0) LIST 1701/1749 EXE UNN ATTN=NEXT
   THEN EXEC 555
IF (INDEX('YES.LIST.OK',S0) NE 0) EXE 570 ; DO IT
IF (S0 EQ '') DEL &W0  ; RETURN=NO
   THEN EXE 580
IF (S0 EQ 'NO') DEL &W0
   THEN EXE 580
IF (S0 EQ 'N') DEL &W0
   THEN EXE 580
WRI S0||':  Invalid.  Please answer YES, or LIST, or NO to skip.'
WRI 'Press BREAK to terminate prompting.'
EXE 555

; DO THIS MEMBER
CH 9/235 TO '' IN &W0 NOL
CH ' '(1) TO ')' IN &W0 NOL
CH 1 TO 'MEMBER=(' IN &W0 NOL
; next member
POI &W0+.001(1) NOL
W0 = *
IF (W0 GE 0) EXE 551   ; WILL BE -1 IF NO MORE LINES
;
EXEC 599
; WE QUIT PROMPTING.
DEL &W0/LAST
;
;  FORMS AND CHARACTER SET
;
IF (INDEX(EPARM,'NOPRINT') NE 0) EXE 700  ;  IF NOPRINT, NO PRINT OPTIONS
COMM
DUMP OUTPUT
REA STR S0 USI LAST DELETE
N0 = IND(S0,'FORMS=')
S0 = SUBSTR(S0,N0+6,4)       ; FORM CODE FOR ACCT.
S2 = STRIP(S0)
IF (S2 EQ '') S2 = '3LAN'    ;  FOR PROMPT
REA STR FORMS UPP PRO 'On what forms do you want the output printed (RETURN=&S2)? '  ATTN=630
IF (IND(FORMS,'?') NE 0) LIST 1801/1849 EXE UNN ATTN=NEXT
   THEN EXEC 600
FORMS=STRIP(FORMS)
IF (FORMS EQ '') FORMS=S2

XCALL COMMANDS#FORMCHEK PUBLIC  (FORMS,FORM_TEST)
;  Result from FORMCHEK:  One of INVALID, XEROX, 3800, IMPACT, UNKNOWN

IF (FORM_TEST EQ 'INVALID') WRI FORMS||': Invalid; form names must be 4 character alphanumeric.'
   THEN LIST 1801/1849 EXE UNN ATTN=611
   THEN EXEC 611 ; try again
IF (FORM_TEST EQ '3800')  EXE 640 ; 3800
IF (FORM_TEST EQ 'XEROX') EXE 660 ; 4090
IF (FORM_TEST NE 'IMPACT') EXE 620 ; not impact: unknown
; impact printer
WRI FORMS||' is an impact printer form.'
IF (YESNO('OK? ') EQ 'NO') EXE 611
CHARS = 'TN'
EXE 700
; unknown form code
WRI FORMS||':  Unknown FORMS code.  Specify 1181 for the 3800 printer,'
WRI 'BOND or 3HOL for the 4090 printer or local laser printers.'
EXE 611

;  ATTN EXIT FROM FORMS PROMPT
IF (LAST GE XEND) DEL &(XEND)/LAST
EXE 300

;  HERE IF 3800

S0 = 'TN15' ;  Default
REA STR CHARS UPP PRO 'Which character set do you want to use (RETURN=&(S0))? '  ATTN=600
CHARS=STRIP(CHARS)
IF (IND(CHARS,'?') NE 0) LIST 1851/1899 EXE UNN ATTN=NEXT
   THEN EXEC 640
IF (CHARS EQ '') CHARS = S0
   THEN EXE 654 ; Use default, don't check it
IF (SIZE(CHARS) GT 4) WRI CHARS||':  Invalid, name must be 4 characters or less.'
   THEN EXE 640
; Check CHARS
; *** do something here***
;
JPARM = JPARM||',CHARS=&(CHARS)'

EXE 700 ; do COPIES check
; 4090 printing options --------------------------------------------
S0='PFORMAT=L150'
REA STR CHARS UPP PRO 'Printing options (RETURN=&(S0)) ? ' ATTN=600
CHARS=STRIP(CHARS)
IF (CHARS EQ '') CHARS=S0 ; default
   THEN EXE 690
IF (CHARS EQ '?') LIST 2851/2899 EXE UNN ATTN=660
   THEN EXE 660 ; ask again
IF (SIZE(CHARS) NE 4) EXE 680 ; must not be CHARS value
S0 = SUB(CHARS,1,1)
S1 = SUB(CHARS,3,2)
IF (VER(S0,'ABCDEFGHIJKLMNOPQRSTUVWXYZ') EQ 0 AND VER(S1,'0123456789') EQ 0)
   THEN CHARS='CHARS=&(CHARS)'
   THEN EXEC 690

; not a CHARS value

; assume it's just JOBPARM options e.g. PFORMAT, DUPLEX, etc.

; 4090 CHARS wrapup
CHARS=SQUASH(CHARS)
JPARM=JPARM||','||CHARS


;  COPIES  ---------------------------------------------------------

COMM
NCOPIES = 1 ; COPIES
REA STR S0 UPP PRO'How many copies do you want (RETURN=1)? '  ATTN=600
IF (IND(S0,'?') NE 0) LIST 1901/1949 EXE UNN ATTN=NEXT
   THEN EXE 700
IF (S0 NE '') TRY NCOPIES = NCONVERT(S0)
   IF (TRYERR) EXE 700

;  BASIC JCL  -------------------------------------------------------

S1 = LIBNAME ; DSN
IF(SIZE(S1) LE 20) EXE 760          ;  DSN FITS IN NAME FIELD
IF (SUBSTR(S1,1,4) EQ 'WYL.') S1 = SUBSTR(S1,12) ; STRIP PREFIX IF NAME TOO BIG
ELSE IF (SUBSTR(S1,1,3) EQ 'SYS') S1 = SUBSTR(S1,5) ; SAME FOR SYSn.
N1=SIZE(S1)
N0=N1-20+1
IF (N1 GT 20) S1 = SUBSTR(S1,N0,20); JOB TITLE

JPARM=STRIP(JPARM,'B',',') ; remove extra commas
JPARM=SQUASH(JPARM)
PUTLINE  1 // JOB ,'&S1',TIME=(,15)
PUTLINE  2 /*JOBPARM LINES=100
PUTLINE  3 /*JOBPARM CARDS=20000,FORMS=&(FORMS)
IF (JPARM EQ '') EXE 780
PUTLINE 3.5 /*JOBPARM &(JPARM)
CHA ', ' TO ',' IN 3.5 NOLIST
CHA 'DUPLEX ' TO 'DUPLEX=YES ' IN 3.5 NOLIST
CHA ' ' TO ',' IN 3.5 NOLIST
CHA ', ' TO '' IN 3.5 NOLIST ; trailing comma
CHA ',' 10 TO ' ' IN 3.5 NOLIST

IF (EPARM NE '') EPARM = ',PARM=('||SUB(EPARM,2)||')'
PUTLINE  4 //LIBLIST  EXEC  LIBLIST&(EPARM)
IF (NCOPIES GT 1) PUTLINE 5 //SYSPRINT DD COPIES=&(NCOPIES)
PUTLINE  6 //LIB      DD DSN=&(LIBNAME),DISP=SHR
;  WRAP-UP
COMM
REA STR S0 UPP PRO 'RUN? '  ATTN=900
IF (IND(S0,'?') NE 0) LIST 2001/2049 EXE UNN ATTN=NEXT
   THEN EXE 825
IF (INDEX('YESOK',S0) NE 0) EXE 850  ; YES
IF (S0 EQ '') EXE 850                ; DEFAULT=YES
;
WRI 'The JCL is in your active file.  Type RUN to run the job.'
IF (OLDACT NE 0) WRI 'Type PICK &(OLDACT) to restore previous active file.'
EXE 910
;  HERE TO RUN
RUN
IF (OLDACT NE 0) PICK &(OLDACT)
EXE 910

; Abort
IF (OLDACT NE 0) PICK &(OLDACT)

; Cleanup and exit, all cases
SET ESC &(OLDESC)
COMM LIBLIST done
CLR EXE
--General help--
LIBLIST will submit a batch job to print a WYLBUR library.
Every member will be printed unless you indicate otherwise.
You will have a chance to either specify certain members to be
printed, or to have LIBLIST prompt you with the names (and titles)
of members to print.

You can obtain an explanation for any prompt by typing a question
mark (?) in response to the prompt.

--help dsn--
Enter the name of the library you want to print.  You can enter
either a simple name, such as LIB, or a fully qualified name,
such as WYL.GA.PUB.LIB or SYS4.PROCLIB.

--HELP OPTIONS--
If you just press RETURN in response to this prompt, each member
will be printed beginning on a new page.  If you respond NOEJECT,
members will be separated by a few blank lines.

Other options that you can specify are:  PUNCH  UPDTE  NOPRINT

The PUNCH option causes LIBLIST to "unload" a sequential copy of the
library to SYSPUNCH.  Originally intended for punching onto cards,
this option is useful when you want to make a sequential copy of a
library; you can RUN HOLD when the job is ready to run, and FETCH the
PUNCH output and SAVE it.

Punching cards is expensive; refer to DOC#RATES PUBLIC for current
prices.

UPDTE (which implies PUNCH) causes each member to be
preceded (in the SYSPUNCH data set) by a record
of the form

./  ADD NAME=name

A data set (or card deck) in such form may be used to build a new
library using the IBM utility program IEBUPDTE.  Refer to
"MVS Utilities" for information on IEBUPDTE.

NOPRINT (useful only with PUNCH) prevents printing a copy of the
members on SYSPRINT.


--HELP MEMBER--
You can enter the name of a member in your library that you want
listed.  If you do, you will then be prompted for another name.
You can enter as many names as you like; if (after entering a name)
you just press RETURN, LIBLIST will stop prompting for names.

If you just press RETURN in response to this prompt when it is
first issued then all members will be listed.

--HELP MORE MEMBERS--
If you reply YES or OK, you will be prompted again for a member
name.  If you reply NO, prompting will cease.  If you reply with
anything else, it will be taken as a member name to be printed,
and the prompt will be reissued.

--HELP DIR OPTIONS--
You can enter options that would be valid on a SHOW DIRECTORY
command, such as FROM, TO, or LIKE.  These options will reduce
the number of prompts you will have to answer.

If you enter invalid options, you may get the error message
EXEC BREAK.  In this case, enter the command EXEC RETURN to get
another chance to enter options.

--HELP LIST PROMPT--
If you answer YES, OK, or LIST, the member will be printed.  If
you reply NO or just press RETURN, the member will not be listed.
If you press BREAK, prompting will cease.

The prompt will include any TITLE that was specified when you
saved the member.

--HELP FORMS--
Indicate the forms code that describes the forms (paper) you want
your library printed on.  Standard forms are BOND or 3HOL (for the
4090), and 1181 (for the 3800).  Use DOC#FORMS PUBLIC for more
information on forms.

--HELP CHARS--
Specify the character set you want used to print your library.  If
you just press RETURN, you will get the default character set for
the form you requested.  Note that some combinations of forms and
character sets may result in part of your output going off the page.

--HELP COPIES--
Indicate the number of copies you want printed.  Default is one
copy.

--HELP RUN--
If you reply YES, the LIBLIST job will be run for you and then
deleted from your active file, leaving your active file as it was
before you invoked LIBLIST.

If you reply NO, the job control language for LIBLIST will be
left in your active file and will not be run.  You will then need
to issue a RUN command to submit the job.  Use this option if you
want to modify the JCL or use special RUN options.

--HELP MODE--

If you reply ALL or just press RETURN, all members of the library
will be printed.

If you reply PROMPT, LIBLIST will display each member name (and
title, if present), and ask if you want it printed.  In this case you
may also specify any valid SHOW DIRECTORY options (such as FROM, TO,
or LIKE) to limit the search.

If you reply DIRECTORY, you may specify any valid SHOW DIRECTORY
options (such as FROM, TO, or LIKE) to limit the search.  All members
meeting the directory search criteria will be printed.  Unlike PROMPT
mode, you will not be prompted with each name; however, the found
names will be listed.

If you reply LIST, you will then be prompted to enter a list of the
member names you want printed.

; HELP 4090 options (also other laser printers)
You can specify a character set name, such as TN12; in this case
the output will be printed in "portrait" mode (long side vertical).
Note that in most cases this will cause some of your output to fall
off the page.

You can specify a PFORMAT, such as PFORMAT=L132, which will define
both the character set and the page orientation.  PFORMAT names
beginning with L define "landscape" mode printing (long side
horizontal).  The default, L150, will allow all LIBLIST messages and
almost all data to fit on the page.  Other PFORMATs you could specify
include:

   L120  120-column (12 pitch) print
   L132  132-column (13.6 pitch) print
   L150  150-column (15 pitch) print
   L200  200-column (20 pitch) print

Refer to "Using the Xerox 4090 Printer" for more information on
PFORMAT codes.  When your output is directed to other printers (such
as Laserjets) the closest possible simulation of these will be used.

When output is to the 4090, you can specify DUPLEX in addition to
other print options; this will cause the output to be printed on
both sides of the paper.

You can specify a DEST code to route output to a printer in your
office.

When you specify multiple print options, they must be separated by
commas without intervening blanks; for example,

   PFORMAT=L132,DUPLEX
