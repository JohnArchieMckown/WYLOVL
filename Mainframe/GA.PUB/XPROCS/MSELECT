;
; select mail items
;
; this exec will select any mail items that contain a specified
; string.  for example say you want to find all the items that
; may have the word 'samson' (in either upper or lower case),
; this exec will do it.  the word can be anywhere in the mail
; message.  first you must use your mail file into the active
; file.  and then call this exec file:
;
; xcall xprocs#mselect / <range specification>
; eg.  xcall xprocs#mselect pub / c'samson'
;
; vars used:
;    i = count of items found containing given string
;    item = first line of mail item  (-1 if none)
;    next_itme = first line of next mail item (-1 if none)
;    item_end = last line of mail item (may not actually exist)
;
xproc () begin

   i = 0
   point '*N???????'1 (1) nolist
   item = current

   while (item ne -1) begin
      point '*N??????' (1) in &(item+.001)/last nolist
      next_item = current
      if (next_item eq -1) item_end = last
      else item_end = next_item-.001
      point &(parm_string) in &(item)/&(item_end) nolist
      if (current eq -1) delete &(item)/&(item_end)
      else i = i+1
      item = next_item
   end

   if (i eq 0) comment 0 items found.
   else begin
      comment &(i) items found:
      list '*N???????'1 unnumber   col 11/88
   end
end
