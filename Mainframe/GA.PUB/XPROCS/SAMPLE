;
;  Sample exec file with a little error handling.
;    This is saved as XPROCS#SAMPLE PUBLIC.
;
;  Written 3/88, Niz
;

xproc () begin
   on error cmd 'pcall errorexit'  ; set up error handling
   on attn  cmd 'pcall attnexit'   ; set up attention handling

   x.origact  = actno              ; save original Active file number
   x.maint    = 'gg.jdn'           ; List of maintenance accounts
   x.pgm      = 'SAMPLE'           ; Program name to be displayed in
;                                    error messages.

   open workact  ; start with a new Active file
   putend Sample exec called.
   putend Account = &(account)
   putend Name = &(name)
   putend ----
   cmd = strip(parm_string)
   if (cmd ne '') begin
      putend parms = &(cmd)
   end
   try mail to &(x.maint) title 'Sample'
   if (tryerror) write 'EEK!  The mail command failed.'
   close

   pcall cleanup
end



;<.page>

;  errorexit -- Called when an error occurs while executing a
;    command.  The "errmsg" and "errinfo" variables have information
;    about the failure.  Not much we can do except complain.
;

proc errorexit() begin
   write 'An error occurred in the ' || x.pgm || ' command.'
   write 'The programming staff will be notified.'
   write ''
   write 'Debugging information:'
   write '   errmsg: '||errmsg
   write '  errinfo: '||errinfo

;  Mail debugging information to maintenance people.

   open errors
   putend An error occurred in the &(x.pgm) command.
   putend
   putend Debugging information:
   putend
   dump xdate
   dump account
   putend command = &(x.origparm)
   putend errmsg = &(errmsg)
   putend errinfo = &(errinfo)
   putend
   putend ---
   putend
   putend >>> show vars type ses
   dump vars type ses
   putend
   putend >>> show vars type x
   dump vars type x
   putend
   putend >>> show vars type error  ; local vars at time of error
   dump vars type error
   putend
   putend End of dump.

   try quietly mail to (&(x.maint)) title '&(x.pgm) error'
   close

   pcall cleanup
end



;<.page>

;  attnrexit -- Called when the user interrupts a command with
;    the ATTN key.
;
proc attnexit() begin
   try write 'BREAK interrupted command processing.'
   pcall cleanup
end


proc cleanup() begin
   if (actno ne x.origact) pick &(x.origact)
   xreturn
end
