         MACRO                                                                  
&L       POP   &PTR=                                                            
.*       RE-DONE 4/77, JDN;  Last modified, 11/85, Niz                          
         GBLA  &$UPTR,&$UR#STK(0),&$REG#,&$COUNT                                
         GBLB  &$OK                                                             
         GBLC  &$USTK(0),&$ULOC                                                 
         LCLA  &I                                                               
         AIF   ('&SYSLIST(1)' NE 'PRINT' AND '&SYSLIST(1)' NE 'USING'  *        
               AND '&SYSLIST(1)' NE 'DSECTS').PSTK                              
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
         AIF   ('&SYSLIST(&I)' NE 'USING').DO                                   
         AIF   (&$UPTR LE 0).NOPOP                                              
         AIF   (&$UR#STK(&$UPTR) GE 100).ULOOP                                  
.NOPOP   MNOTE 4,'WARNING - POP USING DONE WITH NO PRECEDING PUSH'              
         DROP                                                                   
         AGO   .LOOP                                                            
.ULOOP   ANOP                                                                   
&J       SETA  &$UR#STK(&$UPTR)-100                                             
&$UPTR   SETA  &$UPTR-1                                                         
         AIF   (&J LT 0).ULAST                                                  
         USING &$USTK(&$UPTR+1),&J                                              
         AGO   .ULOOP                                                           
.ULAST   ANOP                                                                   
&J       SETA  0-(200+&J)                                                       
         USING &$USTK(&$UPTR+1),&J                                              
         AGO   .LOOP                                                            
.DO    $$POP   PRINT                                                            
         AIF   ('&SYSLIST(&I)' NE 'DSECTS').LOOP                                
         AIF   ('&$ULOC' EQ '').DOSP                                            
&$ULOC   LOCTR                                                                  
&$ULOC   SETC  ''                                                               
.DOSP    ANOP                                                                   
*POP                                                                            
         AGO   .LOOP                                                            
.*                                                                              
.PSTK    AIF   (T'SPR NE '#').ERRNSPR                                           
&SYM     SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&LEN     SETC  '0'                                                              
.*                                                                              
         AIF   ('&PTR' EQ '').SUBLEN                                            
&L       LR    SPR,&PTR                                                         
         AGO   .POPS                                                            
.SUBLEN  ANOP                                                                   
&L       SH    SPR,=AL2(&SYM)                                                   
.POPS    ANOP                                                                   
.*                                                                              
&I       SETA  N'&SYSLIST+1                                                     
.PLP     AIF   (&I LE 1).PFIN                                                   
&I       SETA  &I-1                                                             
         AGO   (N'&SYSLIST(&I)+1).PLP,.P1,.P2                                   
&NOP     SETA  N'&SYSLIST(&I)                                                   
         MNOTE 8,'&SYSLIST(&I) has wrong no. of operands (NOP=&NOP)'            
         AGO   .PLP                                                             
.*                                                                              
.P1      AIF   ('&SYSLIST(&I)' NE '&SYSLIST(&I,1)').ERRMR                       
        $RGLKUP &SYSLIST(&I)                                                    
         AIF   (NOT &$OK).PLEN                                                  
&LEN     SETC  '&LEN+4'                                                         
         L     &SYSLIST(&I),(&SYM-(&LEN))(,SPR)                                 
         AGO   .PLP                                                             
.PLEN    ANOP                                                                   
&LEN     SETC  '&LEN+((&SYSLIST(&I)+3)/4*4)'                                    
         AGO   .PLP                                                             
.*                                                                              
.P2     $RGLKUP &SYSLIST(&I,1)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&R1      SETA  &$REG#                                                           
        $RGLKUP &SYSLIST(&I,2)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&N       SETA  (&$REG#-&R1+1)*4                                                 
         AIF   (&N GT 0).HVL                                                    
&N       SETA  (16-&R1+&$REG#+1)*4                                              
.HVL     ANOP                                                                   
&LEN     SETC  '&LEN+&N'                                                        
         LM  &SYSLIST(&I,1),&SYSLIST(&I,2),(&SYM-(&LEN))(SPR)                   
         AGO   .PLP                                                             
.*                                                                              
.PREG    AIF   (N'&SYSLIST EQ 1).PREGDO                                         
         MNOTE 4,'ONLY "&SYSLIST(1)" ACCEPTED'                                  
.PREGDO $RGLKUP &SYSLIST(1,1)                                                   
         AIF   (NOT &$OK).ERRNR,(&$REG# EQ 0).ERRR0                             
&L       SH    SPR,=AL2(3+(&SYSLIST(1,1)))                                      
         N     SPR,=A(X'FFFFFFFC')                                              
         MEXIT                                                                  
.*                                                                              
.PFIN    AIF   ('&LEN' EQ '0').ERRNOP                                           
&SYM     EQU   &LEN                                                             
.EXIT    MEXIT                                                                  
.*                                                                              
.ERRNSPR MNOTE 8,'Stack pointer register (SPR) must be defined.'                
         MEXIT                                                                  
.*                                                                              
.ERRMR   MNOTE 8,'"(REG)" must be the first and only operand.'                  
         MEXIT                                                                  
.*                                                                              
.ERRNR1  MNOTE 8,'&SYSLIST(1,1) is not a register.'                             
         MEXIT                                                                  
.*                                                                              
.ERRRO   MNOTE 8,'Register 0 can''t be used.'                                   
         MEXIT                                                                  
.*                                                                              
.ERRNR   MNOTE 8,'&SYSLIST(&I) is not a register.'                              
         MEXIT                                                                  
.*                                                                              
.ERRNOP  MNOTE 8,'Required operands missing.'                                   
         MEND                                                                   
