         MACRO                                                                  
&L       GEN   &STR                                                             
.*       MODIFIED 12/78, JDN                                                    
         GBLA  &$COUNT                                                          
         LCLA  &K                                                               
         LCLB  &STYP                                                            
         LCLC  &O(8),&LBL(0)                                                    
&LQ      SETC  'L'''                                                            
&M       SETA  0                                                                
         AIF   ('&L' EQ '').NOLBL                                               
&TEMPLBL SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&M       SETA  1                                                                
&LBL(1)  SETC  '&TEMPLBL'                                                       
.NOLBL   ANOP                                                                   
&S       SETC  '&STR'                                                           
         AIF   ('&STR'(1,1) NE '''' OR '&STR'(K'&STR,1) NE '''').QERR           
&S       SETC  '&STR'(2,K'&STR-2)                                               
&J       SETA  1                                                                
.*                                                                              
.NEWSTMT ANOP                                                                   
&O(1)    SETC  '','','','','','','',''                                          
.LOOP    AIF   (&J GT K'&S).EXIT                                                
         AIF   ('&S'(&J,1) NE ' ' AND '&S'(&J,1) NE ';').DOFOP                  
&J       SETA  &J+1                                                             
         AGO   .LOOP                                                            
.*                                                                              
.DOFOP   ANOP                                                                   
&I       SETA  &J                                                               
.FOP     AIF   (&I GT K'&S).GOTOP                                               
         AIF   ('&S'(&I,1) EQ ' ' OR '&S'(&I,1) EQ ';').GOTOP                   
         AIF   ('&S'(&I,1) EQ ':').GOTOP                                        
&I       SETA  &I+1                                                             
         AGO   .FOP                                                             
.*                                                                              
.GOTOP   ANOP                                                                   
&OP      SETC  '&S'(&J,&I-&J)                                                   
.LOOP2   AIF   (&I GT K'&S).GOTOPND                                             
         AIF   ('&S'(&I,1) NE ' ').DOFOPND                                      
&I       SETA  &I+1                                                             
         AGO   .LOOP2                                                           
.*                                                                              
.DOFOPND AIF   ('&S'(&I,1) NE ':').NOTLBL                                       
&M       SETA  &M+1                                                             
&LBL(&M) SETC  '&OP'                                                            
&J       SETA  &I+1                                                             
         AGO   .LOOP                                                            
.NOTLBL  ANOP                                                                   
&H       SETA  &I                                                               
&J       SETA  &I                                                               
&K       SETA  0                                                                
&STYP    SETB  0                                                                
         AGO   .FCHAR                                                           
.NXTOPND ANOP                                                                   
&O(&K+1) SETC  '&O(&K+1)'.'&S'(&H,&I-&H)                                        
&K       SETA  &K+1                                                             
&H       SETA  &I+1                                                             
         AIF   (&I GT K'&S OR '&C' EQ ' ' OR '&C' EQ ';').GOTOPND               
.NXTCHAR ANOP                                                                   
&I       SETA  &I+1                                                             
.FCHAR   AIF   (&I GT K'&S).NXTOPND                                             
&C       SETC  '&S'(&I,1)                                                       
         AIF   ('&C' EQ '''').CHKSTR                                            
         AIF   ('&C' NE '"').CHKSEP                                             
         AIF   ('&S '(&I+1,1) NE '"').CHNGEQ                                    
&O(&K+1) SETC  '&O(&K+1)'.'&S'(&H,&I-&H).'"'                                    
&H       SETA  &I+2                                                             
&I       SETA  &I+1                                                             
         AGO   .NXTCHAR                                                         
.CHNGEQ  ANOP                                                                   
&O(&K+1) SETC  '&O(&K+1)'.'&S'(&H,&I-&H).''''                                   
&H       SETA  &I+1                                                             
.CHKSTR  AIF   (NOT &STYP AND ' &S'(&I,1) EQ 'L').NXTCHAR                       
&STYP    SETB  (NOT &STYP)         FLIP/FLOP                                    
         AGO   .NXTCHAR                                                         
.CHKSEP  AIF   (&STYP).NXTCHAR                                                  
         AIF   ('&C' EQ ',' OR '&C' EQ ';' OR '&C' EQ ' ').NXTOPND              
         AGO   .NXTCHAR                                                         
.*                                                                              
.GOTOPND AIF   (&I GT K'&S).GENLINE,('&S'(&I,1) EQ ';').GENLINE                 
&I       SETA  &I+1                                                             
         AGO   .GOTOPND                                                         
.GENLINE ANOP                                                                   
&J       SETA  &I+1                                                             
         AIF   (&K LE 0 OR &K GT 3).G                                           
         AIF   ('&O(1)X'(1,1) NE '(').G                                         
&FO      SETC  '&O(1)'(2,K'&O(1)-1)                                             
         AIF   ('X&O(&K)'(1+K'&O(&K),1) NE ')').G                               
         AGO   (&K).OP1P,.OP2P,.OP3P                                            
.G       AGO   (&K+1).OP0,.OP1,.OP2,.OP3,.OP4,.OP5,.OP6,.OP7,.OP8               
         MNOTE 4,'ONLY FIRST EIGHT OPERANDS GENERATED'                          
.OP8     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3),&O(4),&O(5),&O(6),&O(7),&O(8)                  
         AGO   .NEXT                                                            
.OP7     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3),&O(4),&O(5),&O(6),&O(7)                        
         AGO   .NEXT                                                            
.OP6     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3),&O(4),&O(5),&O(6)                              
         AGO   .NEXT                                                            
.OP5     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3),&O(4),&O(5)                                    
         AGO   .NEXT                                                            
.OP4     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3),&O(4)                                          
         AGO   .NEXT                                                            
.OP3     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2),&O(3)                                                
         AGO   .NEXT                                                            
.OP2     ANOP                                                                   
&LBL(1)  &OP   &O(1),&O(2)                                                      
         AGO   .NEXT                                                            
.OP1     ANOP                                                                   
&LBL(1)  &OP   &O(1)                                                            
         AGO   .NEXT                                                            
.OP0     ANOP                                                                   
&LBL(1)  &OP                                                                    
         AGO   .NEXT                                                            
.OP3P    AIF   ('X&O(1)'(1+K'&O(1),1) EQ ')').G                                 
         AIF   ('&O(2)X'(1,1) EQ '(' OR 'X&O(2)'(1+K'&O(2),1) EQ ')').G         
         AIF   ('&O(3)X'(1,1) EQ '(').G                                         
&LO      SETC  '&O(3)'(1,K'&O(3)-1)                                             
&LBL(1)  &OP   (&FO,&O(2),&LO)                                                  
         AGO   .NEXT                                                            
.OP2P    AIF   ('X&O(1)'(1+K'&O(1),1) EQ ')' OR '&O(2)X'(1,1) EQ '(').G         
&LO      SETC  '&O(2)'(1,K'&O(2)-1)                                             
&LBL(1)  &OP   (&FO,&LO)                                                        
         AGO   .NEXT                                                            
.OP1P    ANOP                                                                   
&FO      SETC  '&FO'(1,K'&FO-1)                                                 
&LBL(1)  &OP   (&FO)                                                            
.NEXT    ANOP                                                                   
&TYPE    SETC  T'&LBL(1)                                                        
&I       SETA  2                                                                
.LOOPL   AIF   (&I GT &M).NEXTI                                                 
&LBL(&I) EQU   &LBL(1),&LQ&LBL(1),C'&TYPE'                                      
&I       SETA  &I+1                                                             
         AGO   .LOOPL                                                           
.NEXTI   ANOP                                                                   
&M       SETA  0                                                                
&LBL(1)  SETC  ''                                                               
         AGO   .NEWSTMT                                                         
.*                                                                              
.EXIT    AIF   (&M LT 1).END                                                    
&TYPE    SETC  T'&TEMPLBL                                                       
&LBL(1)  EQU   *,0,C'&TYPE'                                                     
&I       SETA  2                                                                
.LOOPE   AIF   (&I GT &M).END                                                   
&LBL(&I) EQU   &LBL(&I),&LQ&LBL(&I),C'&TYPE'                                    
&I       SETA  &I+1                                                             
         AGO   .LOOPE                                                           
.END     AIF   ('&L' EQ '').MEND                                                
&L       EQU   &TEMPLBL,*-&TEMPLBL,C'&TYPE'                                     
         MEXIT                                                                  
.*                                                                              
.QERR    MNOTE 8,'&STR - INVALID (QUOTES REQUIRED FOR INSTRUCTIONS)'            
.MEND    MEND                                                                   
