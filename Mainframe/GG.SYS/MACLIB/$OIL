         MACRO                                                                  
&L      $OIL   &BYTE,&MASK,&REF=,&WREGS=(R15,R0,R1)                             
.*  Move immediate interlocked.                                                 
.*  BYTE, MASK, REF, and WREGS are as described in IBM provided                 
.*   OIL.                                                                       
         LCLC  &XREGS(3)               Use these registers                      
         LCLC  &DEFREGS(3)             Default registers                        
         LCLC  &C                      Unique label prefix                      
         LCLC  &OFF                    Offset of byte from fullword             
&DEFREGS(1) SETC 'R15','R1','R0'                                                
&C       SETC  '&SYSNDX'                                                        
.OIL20   AIF   ('&REF' NE '').OIL30     BRANCH IF REF SPECIFIED                 
         MNOTE 8,'REF Keyword required but not specified'                       
         MEXIT                                                                  
.OIL30   AIF   ('&WREGS(1)' NE '').OIL35 BRANCH IF 1st REG SPECIFIED            
&XREGS(1) SETC '&DEFREG(1)'            USE default                              
         AGO   .OIL40                                                           
.OIL35   ANOP                                                                   
&XREGS(1) SETC '&WREGS(1)'                                                      
.OIL40   AIF   ('&WREGS(2)' NE '').OIL45 BRANCH IF 2nd REG SPECIFIED            
&XREGS(2) SETC '&DEFREG(2)'            USE default                              
         AGO   .OIL50                                                           
.OIL45   ANOP                                                                   
&XREGS(2) SETC '&WREGS(2)'                                                      
.OIL50   AIF   ('&WREGS(3)' NE '').OIL55 BRANCH IF 3rd REG SPECIFIED            
&XREGS(3) SETC '&DEFREG(3)'            USE default                              
         AGO   .OIL60                                                           
.OIL55   ANOP                                                                   
&XREGS(3) SETC '&WREGS(3)'                                                      
.OIL60   ANOP  ,                       All operands specified by now            
&OFF     SETC  '(&BYTE-&REF)'                                                   
&L       L     &XREGS(1),&REF+((&OFF/4)*4)      Load original word              
         LA    &XREGS(3),&MASK                  Load & position mask            
         SLL   &XREGS(3),24-(8*(&OFF-((&OFF/4)*4)))(0)                          
$OLL&C   LR    &XREGS(2),&XREGS(1)     LOAD WORD W/ BYTE TO BE SET              
         OR    &XREGS(2),&XREGS(3)                                              
*                                      Modified fullword                        
         CS    &XREGS(1),&XREGS(2),&REF+((&OFF/4)*4)                            
         BNE   $OLL&C                  DO AGAIN IF WORD WAS MODIFIED            
         MEND                                                                   
