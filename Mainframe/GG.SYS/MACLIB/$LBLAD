         MACRO                                                                  
        $LBLAD &ADDR,&SEP=.                                                     
.*  This macro is designed for working with the LABELED USING                   
.*  feature of the SLAC assembler. It takes one operand as                      
.*  its input, an address operand, and separates the labeled                    
.*  using prefix from the address iteself.                                      
         GBLC  &$LULBL,&$LUADDR                                                 
         LCLA  &N                                                               
&$LULBL  SETC  ''                      Default label                            
&$LUADDR SETC  '&ADDR'                 Default addr                             
         AIF   (K'&ADDR LE 2).LBLDONE                                           
&N       SETA  2                                                                
.SEPLOOK AIF   ('&ADDR'(&N,1) NE '&SEP').NSEP                                   
&$LULBL  SETC  '&ADDR'(1,&N)                                                    
&$LUADDR SETC  '&ADDR'(&N+1,K'&ADDR-&N)                                         
         AGO   .LBLDONE                                                         
.NSEP    ANOP  ,                                                                
&N       SETA  &N+1                                                             
         AIF   (&N LT K'&ADDR).SEPLOOK                                          
.LBLDONE ANOP  ,                                                                
         MEND  ,                                                                
