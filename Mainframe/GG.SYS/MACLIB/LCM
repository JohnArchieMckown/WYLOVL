         MACRO                                                                  
&L       LCM   &REG,&MASK,&OP                                                   
.*       ADDED 4/77, JSL;  MODIFIED 11/79, JDN;  MODIFIED 11/85, JKR            
.*    If the global flag &$ASSOC is set (HIBAL ASSOC=YES), then                 
.*  drops are done on the destination register because this will                
.*  lose all associative usings on the register.                                
.*    For all I can tell, the $$DROP will generate an                           
.*  addressability error message if the &OP has its                             
.*  addressability off of &REG (which has just been cleared).                   
.*                                                                              
         GBLA  &$REG#                                                           
         GBLB  &$OK,&$ASSOC                                                     
         GBLC  &$USINGS(16)                                                     
        $RGLKUP &REG                                                            
         AIF   (&$OK).REG                                                       
&$REG#   SETA  &REG                                                             
.REG     AIF   (&$REG# EQ 0).NODROP,(&$ASSOC).NODROP                            
       $$DROP  &$REG#                                                           
.NODROP  ANOP                                                                   
*JOIN                                                                           
&L       SLR   &$REG#,&$REG#                                                    
         ICM   &REG,&MASK,&OP                                                   
         AIF   (&$REG# EQ 0).EXIT,(&$ASSOC).EXIT                                
       $$USING &$USINGS(&$REG#+1),&$REG#                                        
.EXIT    MEND                                                                   
