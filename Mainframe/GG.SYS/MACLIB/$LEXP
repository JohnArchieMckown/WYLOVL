         MACRO                                                                  
&L      $LEXP  &LEXP,&TRUE,&FALSE,&BTYPE=A                                      
.*       RE-DONE 4/77, JDN;  MODIFIED (ADD BTYPE) 5/81, JDN                     
         GBLA  &$COUNT,&$CCVAL                                                  
         LCLA  &I,&J                                                            
         LCLC  &LAB                                                             
&L       LABEL                                                                  
.LOOP    ANOP                                                                   
&J       SETA  &I                                                               
.SLOOP   AIF   (&I GE N'&LEXP).DONE                                             
&I       SETA  &I+1                                                             
         AIF   ('&LEXP(&I)' NE 'AND' AND '&LEXP(&I)' NE 'OR').SLOOP             
         AIF   (&I-&J EQ 2).MULT                                                
.ERROP   MNOTE 8,'LOGICAL EXPRESSION HAS WRONG NO. OF OPERANDS'                 
         MEXIT                                                                  
.MULT    ANOP                                                                   
&TEMP    SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
         AIF   ('&LEXP(&I)' EQ 'AND').AND                                       
&LAB    $LEXP  &LEXP(&J+1),&TRUE,&TEMP.F,BTYPE=T                                
        $BC    &$CCVAL,&TRUE                                                    
&LAB     SETC  '&TEMP.F'                                                        
         AGO   .LOOP                                                            
.AND     ANOP                                                                   
&LAB    $LEXP  &LEXP(&J+1),&TEMP.T,&FALSE,BTYPE=&BTYPE                          
&TEMP.B  BC    15-&$CCVAL,&FALSE                                                
         BEGIN LAB=&BTYPE&TEMP,BLOCK=NO                                         
&LAB     SETC  '&TEMP.T'                                                        
         AGO   .LOOP                                                            
.*                                                                              
.DONE    AIF   ('&LAB' EQ '').OK                                                
         AIF   (&I-&J NE 1).ERROP                                               
&LAB    $LEXP  &LEXP(&I),&TRUE,&FALSE,BTYPE=&BTYPE                              
         MEXIT                                                                  
.OK      AGO   (&I-&J).OP1,.OP2,.OP3                                            
         MNOTE 8,'LOGICAL RELATION HAS WRONG NO. OF OPERANDS'                   
         MEXIT                                                                  
.OP1    $REL   &LEXP(&J+1)                                                      
         MEXIT                                                                  
.OP2     AIF   ('&LEXP(&J+1)' NE 'NOT').OP2R                                    
        $LEXP  &LEXP(&J+2),&FALSE,&TRUE,BTYPE=&BTYPE                            
&$CCVAL  SETA  15-&$CCVAL                                                       
         MEXIT                                                                  
.OP2R   $REL   &LEXP(&J+1),&LEXP(&J+2)                                          
         MEXIT                                                                  
.OP3    $REL   &LEXP(&J+1),&LEXP(&J+2),&LEXP(&J+3)                              
         MEND                                                                   
