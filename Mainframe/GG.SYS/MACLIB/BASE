         MACRO                                                                  
&L       BASE  &ARG1,&ARG2,&USING=USING,&TRACE=NO,&TRACEID=                     
.*       MODIFIED 5/77, CAF; 9/77, JDN                                          
.*       MODIFIED 6/83, RAL; Modified, 11/85, Niz                               
.*                                                                              
.*  From Niz (11/85):  The "&TRACE=" is only used in the WYLBUR                 
.*             version of this macro.  The definition must be                   
.*             present here to avoid assembly errors by common                  
.*             macros which supply the "TRACE=" option.                         
.*                                                                              
         GBLA  &$REG#                                                           
         GBLB  &$OK                                                             
&ADDR    SETC  '&ARG2'                                                          
         AIF   ('&ARG1&ARG2' EQ '').DEFLT                                       
        $RGLKUP &ARG1                                                           
         AIF   (&$OK).GOTREG                                                    
&ADDR    SETC  '&ARG1'                                                          
        $RGLKUP &ARG2                                                           
         AIF   (&$OK).GOTREG                                                    
         AIF   (N'&SYSLIST GE 2).BADREG                                         
.DEFLT  $RGLKUP BR                                                              
         AIF   (NOT &$OK).BADREG                                                
.GOTREG  AIF   ('&ADDR' EQ '' OR '&ADDR' EQ '*').HERE                           
&L       L     &$REG#,=A(&ADDR)                                                 
        &USING (&ADDR,&$REG#)                                                   
         MEXIT                                                                  
.HERE    ANOP                                                                   
&L       BASR  &$REG#,0                                                         
        &USING (*,&$REG#)                                                       
         MEXIT                                                                  
.BADREG  MNOTE 8,'Missing or invalid register.'                                 
         MEND                                                                   
