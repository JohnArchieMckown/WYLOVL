         MACRO                                                                  
&L      $NIL   &BYTE,&MASK,&REF=,&WREGS=(R15,R0,R1)                             
.*  Move immediate interlocked.                                                 
.*  BYTE, MASK, REF, and WREGS are as described in IBM provided                 
.*   NIL.                                                                       
         LCLC  &XREGS(3)               Use these registers                      
         LCLC  &DEFREGS(3)             Default registers                        
         LCLC  &C                      Unique label prefix                      
         LCLC  &OFF                    Offset of byte from fullword             
&DEFREGS(1) SETC 'R15','R1','R0'                                                
&C       SETC  '&SYSNDX'                                                        
.NIL20   AIF   ('&REF' NE '').NIL30     BRANCH IF REF SPECIFIED                 
         MNOTE 8,'REF Keyword required but not specified'                       
         MEXIT                                                                  
.NIL30   AIF   ('&WREGS(1)' NE '').NIL35 BRANCH IF 1st REG SPECIFIED            
&XREGS(1) SETC '&DEFREG(1)'            USE default                              
         AGO   .NIL40                                                           
.NIL35   ANOP                                                                   
&XREGS(1) SETC '&WREGS(1)'                                                      
.NIL40   AIF   ('&WREGS(2)' NE '').NIL45 BRANCH IF 2nd REG SPECIFIED            
&XREGS(2) SETC '&DEFREG(2)'            USE default                              
         AGO   .NIL50                                                           
.NIL45   ANOP                                                                   
&XREGS(2) SETC '&WREGS(2)'                                                      
.NIL50   AIF   ('&WREGS(3)' NE '').NIL55 BRANCH IF 3rd REG SPECIFIED            
&XREGS(3) SETC '&DEFREG(3)'            USE default                              
         AGO   .NIL60                                                           
.NIL55   ANOP                                                                   
&XREGS(3) SETC '&WREGS(3)'                                                      
.NIL60   ANOP  ,                       All operands specified by now            
         DATA  '$NLM&C:DC AL1(&MASK)'  MASK BYTE                                
&OFF     SETC  '(&BYTE-&REF)'                                                   
&L       LA    &XREGS(1),1             SET UP ICM MASK                          
         LNR   &XREGS(3),&XREGS(1)                                              
         SLL   &XREGS(1),3-(&OFF-((&OFF/4)*4))(0)                               
         EX    &XREGS(1),'ICM &XREGS(3),0,$NLM&C'                               
         L     &XREGS(1),&REF+((&OFF/4)*4)                                      
$NLL&C   LR    &XREGS(2),&XREGS(1)     LOAD WORD W/ BYTE TO BE SET              
         NR    &XREGS(2),&XREGS(3)                                              
*                                      Modified fullword                        
         CS    &XREGS(1),&XREGS(2),&REF+((&OFF/4)*4)                            
         BNE   $NLL&C                  DO AGAIN IF WORD WAS MODIFIED            
         MEND                                                                   
