         MACRO                                                                  
&L       ELSE  &OP,&LEXP,&LABEL                                                 
.*       MODIFIED 12/78, JDN;  BUG FIX 8/85, JKR                                
         GBLA  &$COUNT,&$BPTR,&$BLPTR                                           
         GBLC  &$BSTK(0),&$BLABSTK(0)                                           
&BLK     SETC  '&$BSTK(&$BLPTR)'(2,K'&$BSTK(&$BLPTR)-1)                         
&PFX     SETC  '&$BSTK(&$BLPTR)'(1,1)   STACK LABEL SUFFIX 'I'                  
         AIF   ('&PFX' NE 'I' OR &$BLPTR LE &$BPTR).NESTERR                     
  DC  0S(&BLK.F-*)   CAUSES ERROR IF ELSE DOESN'T FOLLOW THEN CLAUSE            
&ELNDX   SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&FALSE   SETC  '&ELNDX.E'                                                       
         AIF   ('&OP' EQ 'IF' OR '&OP' EQ 'BEGIN').FIXB                         
         AIF   ('&OP'(1,1) EQ '''').FIXB                                        
         AIF   ('&OP' NE 'EXIT').TRYNEXT                                        
&FALSE   SETC  'F'                                                              
         AGO   .GETBLAB                                                         
.TRYNEXT AIF   ('&OP' NE 'NEXT').BR                                             
&FALSE   SETC  'N'                                                              
.GETBLAB ANOP                                                                   
&I       SETA  &$BPTR                                                           
         AIF   ('&LEXP' EQ '').GOTBLAB                                          
.LLOOP   AIF   (&I LE 0).LABERR                                                 
         AIF   ('&LEXP' EQ '&$BLABSTK(&I)').GOTBLAB                             
&I       SETA  &I-1                                                             
         AGO   .LLOOP                                                           
.GOTBLAB ANOP                                                                   
&FALSE   SETC  '&$BSTK(&I)'(2,7).'&FALSE'                                       
         AGO   .FIXB                                                            
.BR      ANOP                                                                   
&FALSE   SETC  '&OP'                                                            
.FIXB    ANOP                                                                   
.*  NOW FIXUP FALSE BRANCH(ES)...                                               
&I       SETA  &$BPTR                                                           
.OLOOP   AIF   (&I+1 GE &$BLPTR).LASTORG                                        
&I       SETA  &I+1                                                             
         AIF   ('&$BSTK(&I)'(1,1) EQ 'U').OLOOP                                 
         AIF   ('&$BSTK(&I)'(1,1) EQ 'T').OLOOP                                 
&TEMP    SETC  '&$BSTK(&I)'(2,K'&$BSTK(&I)-1)                                   
         ORG   &TEMP.B+2                                                        
         DC    S(&FALSE)                                                        
         AGO   .OLOOP                                                           
.LASTORG ORG   &BLK.B+2                                                         
         DC    S(&FALSE)                                                        
         ORG                                                                    
.*                                                                              
&$BLPTR  SETA  0                                                                
         AIF   ('&OP' EQ 'IF').IF,('&OP' EQ 'BEGIN').BEGIN                      
         AIF   ('&OP'(1,1) EQ '''').GEN                                         
         MEXIT                                                                  
.*                                                                              
.BEGIN   B     &ELNDX.F                  BRANCH AROUND ELSE IF TRUE             
&L       LABEL                                                                  
&ELNDX.E BEGIN LAB=I&ELNDX,BLAB=&L                                              
         MEXIT                                                                  
.*                                                                              
.GEN     B     &ELNDX.F                                                         
&L       LABEL                                                                  
&ELNDX.E BEGIN LAB=I&ELNDX,BLAB=&L                                              
         GEN   &OP                                                              
         END                                                                    
         MEXIT                                                                  
.NESTERR MNOTE 8,'ELSE MUST FOLLOW IF-BLOCK'                                    
         MEXIT                                                                  
.*                                                                              
.LABERR  MNOTE 8,'BLOCK &OP NOT FOUND, &OP NOT POSSIBLE'                        
         MEXIT                                                                  
.*                                                                              
.IF      B     &ELNDX.F                  BRANCH AROUND ELSE IF TRUE             
&L       LABEL                                                                  
&ELNDX.E IF    &LEXP,&LABEL,INDX=&ELNDX,BLAB=&L                                 
         MEND                                                                   
