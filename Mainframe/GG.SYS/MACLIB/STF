         MACRO ,                       Store with length check                  
&L       STF   &REG,&FLOC,&LEN=,&ALIGNED=YES     Store field                    
.*       Labeled using support 05/87, JKR                                       
         GBLA  &$REG#,&$STF                                                     
         GBLB  &$HIBAL,&$OK                                                     
         GBLC  &$LULBL,&$LUADDR                                                 
         LCLA  &STLEN                                                           
         LCLC  &OPCD,&EQU,&LOC,&LBL                                             
.*       Take parenthesis off of register                                       
&RV      SETC  '&REG'                                                           
         AIF   ('&REG'(1,1) NE '(' OR '&REG'(K'&REG,1) NE ')').NRR              
&RV      SETC  '&REG'(2,K'&REG-2)                                               
.NRR     AIF   (NOT &$HIBAL).NRH                                                
.*       Change Hibal symbolic register to number                               
        $RGLKUP &RV                                                             
         AIF   (NOT &$OK).NRH                                                   
&RV      SETC  '&$REG#'                                                         
.NRH     ANOP                                                                   
.*  Dissect ready for labeled using operands                                    
        $LBLAD &FLOC                                                            
&LOC     SETC  '&$LUADDR'                                                       
&LBL     SETC  '&$LULBL'                                                        
         AIF   ('&LEN' NE '').LENSET                                            
&STLEN   SETA  L'&LOC                  Implicit length                          
         AGO   .SETOP                  Now go set OP-CODE                       
.LENSET  ANOP  ,                                                                
&$STF    SETA  &$STF+1                                                          
&EQU     SETC  '$$JR&$STF'                                                      
&EQU     EQU   0,&LEN,C'N'                                                      
&STLEN   SETA  L'&EQU                  Explicit length                          
.SETOP   ANOP  ,                                                                
         AIF   (&STLEN NE 1).NOT1                                               
&OPCD    SETC  'STC'                                                            
         AGO   .DOST                                                            
.NOT1    AIF   (&STLEN NE 2).NOT2                                               
         AIF   ('&ALIGNED' NE 'YES').NOT2AL                                     
&OPCD    SETC  'STH'                   2 Byte aligned                           
         AGO   .DOST                                                            
.NOT2AL  ANOP  ,                                                                
&OPCD    SETC  'ST2'                   2 Byte, not aligned                      
         AGO   .DOST                                                            
.NOT2    AIF   (&STLEN NE 3).NOT3                                               
&OPCD    SETC  'ST3'                                                            
         AGO   .DOST                                                            
.NOT3    AIF   ('&ALIGNED' NE 'YES').NOT4AL                                     
&OPCD    SETC  'ST'                    4 Byte aligned                           
         AGO   .DOST                                                            
.NOT4AL  ANOP  ,                                                                
&OPCD    SETC  'ST4'                   4 Byte, not aligned                      
.DOST    ANOP  ,                                                                
&L       &OPCD &RV,&LBL.&LOC                                                    
         MEND  ,                                                                
