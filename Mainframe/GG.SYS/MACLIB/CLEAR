         MACRO                                                                  
&L       CLEAR &REF=,&REGS=(R15,R0,R1),&MODE=                                   
.*       ADDED 3/77, JDN;  MODIFIED 12/78, JDN                                  
.*       INTERLOCK MODIFICATION 4/85 JKR                                        
.*  For non-interlocked sets, the operands are the &SYSLIST parms.              
.*    The interlocked sets use the &SYSLIST parm too, after the                 
.*    mode has been determined by keyword parameters.                           
.*  MODE, REF and REGS and are all for the interlocked set instr                
.*  &REF and &REGS have the same meaning as IBM supplied OIL and                
.*    NIL insructions, while &MODE=LOCKED should be specified to                
.*    indicate an interlocked set.  REF must be specified if                    
.*    MODE=LOCKED.                                                              
         GBLA  &$COUNT,&$REG#,&$BFVAL,&$BFCC                                    
         GBLB  &$OK                                                             
         GBLC  &$BFLOC,&$BFTYP,&$LULBL,&$LUADDR                                 
         LCLC  &XREGS(3)      FOR WREGS VALUES                                  
         LCLC  &DEFREGS(3)                                                      
.*       LCLC  &N                                                               
         LCLA  &I,&LN                                                           
         LCLC  &ZR,&OP,&OPLBL                                                   
&L       LABEL                                                                  
         AIF   ('&SYSLIST(1)' EQ '').ERRMISS                                    
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
&OP      SETC  '&SYSLIST(&I,1)'                                                 
         AGO   (N'&SYSLIST(&I)).TRYREG,.TRYSTOR                                 
         MNOTE 8,'&SYSLIST(&I) - WRONG NUMBER OF OPERANDS'                      
         AGO   .LOOP                                                            
.*                                                                              
.TRYREG $RGLKUP &OP                                                             
         AIF   (NOT &$OK).TRYSTOR                                               
         SR    &OP,&OP                                                          
&ZR      SETC  '&$REG#'                                                         
         AGO   .LOOP                                                            
.*                                                                              
.TRYSTOR AIF   ('&OP'(1,1) NE '.').TRYFLG                                       
&OP      SETC  '&OP'(2,K'&OP-1)                                                 
         AGO   .ADDR                                                            
.TRYFLG $BV    &OP,&SYSLIST(&I,2)                                               
         AIF   (NOT &$OK).ADDR                                                  
&OP      SETC  '&$BFLOC'                                                        
         AIF   ('&$BFTYP' EQ 'C').ZOTIMM                                        
         AIF   ('&MODE' EQ 'LOCKED').DOLOCK                                     
         AIF   ('&MODE' EQ '').NLKOK                                            
         MNOTE 8,'MODE=&MODE is an invalid parameter'                           
.NLKOK   ANOP  ,                                                                
         AIF   (&$BFCC NE 1 AND &$BFCC NE 7).FLGON                              
         NI    &OP,255-&$BFVAL                                                  
         AGO   .LOOP                                                            
.FLGON   AIF   (&$BFCC NE 14 AND &$BFCC NE 8).ERRCOND                           
         OI    &OP,&$BFVAL                                                      
         AGO   .LOOP                                                            
.DOLOCK  AIF   (&$BFCC NE 1 AND &$BFCC NE 7).LOFF O,NZ                          
        $NIL   &$BFLOC,255-&$BFVAL,REF=&REF,WREGS=&REGS                         
         AGO   .LOOP                                                            
.LOFF    AIF   (&$BFCC NE 14 AND &$BFCC NE 8).ERRCOND  NO,Z                     
        $OIL   &$BFLOC,&$BFVAL,REF=&REF,WREGS=&REGS                             
         AGO   .LOOP                                                            
.*                                                                              
.ADDR    ANOP  ,                                                                
.*                                                                              
.*  Look for labeled type of storage op                                         
.*                                                                              
        $LBLAD &OP                                                              
&OPLBL   SETC  '&$LULBL'               Labeled prefix                           
&OP      SETC  '&$LUADDR'              Address sans label                       
.*                                                                              
         AIF   ('&SYSLIST(&I,2)' NE '').ZOTLEN                                  
         AIF   ('&OP'(K'&OP,1) EQ ')').ZOTCORE                                  
         AIF   (NOT D'&OP).ZOTCORE                               JSL            
&LEN     SETA  L'&OP                                                            
         AIF   (&LEN EQ 0).ZOTCORE                                              
         AGO   .ZOTCHEK                                                         
.ZOTLEN  ANOP                                                                   
&TEMP    SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&TEMP    EQU   0,&SYSLIST(&I,2)                                                 
&LEN     SETA  L'&TEMP                                                          
.ZOTCHEK AIF   ('&ZR' EQ '').ZOTXCHK                                            
         AGO   (&LEN).Z1,.Z2,.Z3,.Z4                                            
.ZOTXCHK AIF   (&LEN EQ 1).ZOTIMM                                               
         AIF   (&LEN LE 256).ZOTSING                                            
         XC    &OPLBL.&OP.(256),&OPLBL.&OP                                      
&OP      SETC  '&OP+256'                                                        
&LEN     SETA  &LEN-256                                                         
         AGO   .ZOTCHEK                                                         
.ZOTSING XC    &OPLBL.&OP.(&LEN),&OPLBL.&OP                                     
         AGO   .LOOP                                                            
.ZOTCORE XC    &OPLBL.&OP,&OPLBL.&OP                                            
         AGO   .LOOP                                                            
.ZOTIMM  MVI   &OPLBL.&OP,0                                                     
         AGO   .LOOP                                                            
.Z1      STC   &ZR,&OPLBL.&OP                                                   
         AGO   .LOOP                                                            
.Z2      STH   &ZR,&OPLBL.&OP                                                   
         AGO   .LOOP                                                            
.Z3      STCM  &ZR,7,&OPLBL.&OP                                                 
         AGO   .LOOP                                                            
.Z4      ST    &ZR,&OPLBL.&OP                                                   
         AGO   .LOOP                                                            
.*                                                                              
.ERRMISS MNOTE 8,'OPERAND MISSING'                                              
         MEXIT                                                                  
.*                                                                              
.ERRCOND MNOTE 8,'&OP - INCONSISTENT CONDITION CODE'                            
.EXIT    MEND                                                                   
