         MACRO                                                                  
&L       FLOOR &R,&LIMIT                                                        
.*  Modified 11/89, Niz/Chaz -- lines 10/11                                     
         GBLB  &$OK                                                             
         LCLC  &H                                                               
        $RGLKUP &R                                                              
         AIF   (NOT &$OK).STORE                                                 
         AIF   ('&LIMIT'(1,1) EQ '=').LIT                                       
         AIF   (T'&LIMIT NE 'N' AND T'&LIMIT NE 'U').CELL                       
&L       C     &R,=A(&LIMIT)                                                    
         IF    L,BEGIN                                                          
         L     &R,=A(&LIMIT)                                                    
         END                                                                    
         MEXIT                                                                  
.*                                                                              
.LIT     ANOP                                                                   
&T       SETC  '&LIMIT  '(2,2)                                                  
         AIF   ('&T' EQ 'A(' OR '&T' EQ 'V(' OR '&T' EQ 'F''').FULL             
         AIF   ('&T' EQ 'Y(' OR '&T' EQ 'H''').HALF                             
         MNOTE 8,'&T - LITERAL NOT SUPPORTED'                                   
         MEXIT                                                                  
.*                                                                              
.CELL   $RGLKUP &LIMIT                                                          
         AIF   (&$OK).REG                                                       
&LEN     SETA  L'&LIMIT                                                         
         AGO   (&LEN).BYTE,.HALF,.BYTE,.FULL                                    
.BYTE    MNOTE 8,'&LIMIT HAS BAD LENGTH (LENGTH=&LEN)'                          
         MEXIT                                                                  
.*                                                                              
.HALF    ANOP                                                                   
&H       SETC  'H'                                                              
.FULL    ANOP                                                                   
&L       C&H   &R,&LIMIT                                                        
         BNL   *+8                                                              
         L&H   &R,&LIMIT                                                        
         MEXIT                                                                  
.*                                                                              
.REG     ANOP                                                                   
&L       CR    &R,&LIMIT                                                        
         BNL   *+6                                                              
         LR    &R,&LIMIT                                                        
         MEXIT                                                                  
.*                                                                              
.STORE   AIF   ('&LIMIT'(1,1) EQ '=').STOREX                                    
         AIF   (L'&R EQ L'&LIMIT).STOREX                                        
         MNOTE 8,'&R and &LIMIT have different lengths.'                        
.STOREX  ANOP                                                                   
&L       CLC   &R,&LIMIT                                                        
         BNL   *+10                                                             
         MVC   &R,&LIMIT                                                        
         MEND                                                                   
