         MACRO                                                                  
&L      $L     &R,&P,&TYPE=,&SELECT=,&NULL=0,&ERR=,&OP=L                        
.********************************************************************           
.* TYPE        Determines if the register is to be complemented.                
.*             Can be a list of operands.                                       
.*             IF ((TYPE <> '') AND (SELECT = ''))                              
.*               The register being loaded will be complemented                 
.* SELECT      Determines if TYPE parameter indicates complementing.            
.*             Has no effect if TYPE = ''                                       
.*             Can be a list of operands.                                       
.*             IF one of the members of the SELECT list is in the               
.*               TYPE list, then complementing is set.                          
.*             (Note:  Complementing can be set by                              
.*               a) TYPE <> '' AND SELECT = ''                                  
.*               b) a member of the SELECT list matches a member of             
.*                  TYPE list)                                                  
.* NULL        Determines what should be loaded in the absence of               
.*             an &P parameter.                                                 
.* ERR         An error message to be issued at assemble time if the            
.*             &P parameter is omitted.                                         
.*             (Note: If NULL and ERR are both null, then a label is            
.*                 generated.                                                   
.*                    The default of NULL=0, and ERR='' results in              
.*                 register &R being zeroed.)                                   
.* OP          The op-code to be used to load register &R                       
.*             This will be over-ridden by an explicit specification            
.*             in the &P parameter (ie $L R1,LA:ONE,OP=L will do a              
.*             load address, not a load)                                        
.********************************************************************           
.*       RE-DONE 4/77, JDN;  MODIFIED 4/79, JDN                                 
.*       Labeled using support 04/87, JKR                                       
         GBLB  &$HIBAL,&$OK                                                     
         GBLA  &$REG#                                                           
         GBLC  &$LULBL,&$LUADDR                                                 
         LCLC  &ULBL,&PV2                                                       
         LCLB  &LCR                                                             
         AIF   ('&TYPE' EQ '').GO                                               
&LCR     SETB  1                                                                
         AIF   ('&SELECT' EQ '').GO                                             
&X       SETA  1                                                                
.LUP     AIF   (&X GT N'&TYPE).LUPEND                                           
&Y       SETA  1                                                                
.LUPTOO  AIF   (&Y GT N'&SELECT).LUPGO                                          
         AIF   ('&TYPE(&X)' EQ '&SELECT(&Y)').GO                                
&Y       SETA  &Y+1                                                             
         AGO   .LUPTOO                                                          
.LUPGO   ANOP                                                                   
&X       SETA  &X+1                                                             
         AGO   .LUP                                                             
.LUPEND  ANOP                                                                   
&LCR     SETB  0                                                                
.GO      ANOP                                                                   
.*       Take parenthesis off of retister                                       
&RV      SETC  '&R'                                                             
         AIF   ('&R'(1,1) NE '(' OR '&R'(K'&R,1) NE ')').NRR                    
&RV      SETC  '&R'(2,K'&R-2)                                                   
.NRR     AIF   (NOT &$HIBAL).NRH                                                
.*       Change Hibal symbolic register to number                               
        $RGLKUP &RV                                                             
         AIF   (NOT &$OK).NRH                                                   
&RV      SETC  '&$REG#'                                                         
.NRH     ANOP                                                                   
&PV      SETC  '&P'                                                             
&OPC     SETC  '&OP'                                                            
         AIF   ('&PV' NE '').NBL                                                
         AIF   ('&ERR&NULL' EQ '').LBL                                          
&PV      SETC  '&NULL'                                                          
         AIF   ('&ERR' EQ '').NBL                                               
         MNOTE 12,&ERR                                                          
         AGO   .NBL                                                             
.LBL     ANOP                                                                   
         AIF   ('&L' EQ '').END                                                 
&L       DS    0H                                                               
         MEXIT                                                                  
.NBL     AIF   ('&PV'(1,1) EQ '(' AND '&PV'(K'&PV,1) EQ ')').REG                
         AIF   ('&PV'(1,1) NE '-').NOTMIN                                       
&PV      SETC  '&PV'(2,K'&PV-1)                                                 
         AIF   (NOT &LCR).NOLCR                                                 
         MNOTE 12,'MODE= AND NEGATIVE VALUE INCONSISTENT'                       
.NOLCR   ANOP                                                                   
&LCR     SETB  1                                                                
.NOTMIN  AIF   ('&PV' EQ '0').SR                                                
&LABEL   SETC  '&L'                                                             
.*             Look for supplied op-code (ie LA:, LH: etc)                      
&PT      SETA  1                                                                
.OPL     AIF   ('&PV'(&PT,1) EQ ':').GOTOP                                      
&PT      SETA  &PT+1                                                            
         AIF   (&PT LE K'&PV).OPL                                               
         AGO   .DO                                                              
.GOTOP   ANOP                                                                   
&OPC     SETC  '&PV'(1,&PT-1)                                                   
&PV      SETC  '&PV'(&PT+1,K'&PV-2)                                             
.DO      ANOP                                                                   
         AIF   ('&OPC' NE 'IC').NIC                                             
&OPC     SETC  'LC'                                                             
.NIC     AIF   ('&PV' NE '').GOP                                                
&LABEL   &OPC  &R                                                               
         AGO   .COM                                                             
.GOP  AIF ((NOT &$HIBAL) OR ('&OPC' NE 'LA' AND '&OPC' NE 'L')).NOCHK           
        $RGLKUP &PV                                                             
         AIF   (&$OK).ERR1  ERROR, FOR NOW...                                   
         AIF   ('&OPC' NE 'LA' OR '&P' NE '&PV').NOCHK                          
        $LBLAD &PV                                                              
&PV2     SETC  '&$LUADDR'                                                       
&ULBL    SETC  '&$LULBL'                                                        
         AIF   ('&ULBL' EQ '').NOULBL                                           
&ULBL    SETC  '&ULBL'(1,K'&ULBL-1)    no dot                                   
.NOULBL $USLKUP &PV2,LABEL=&ULBL                                                
         AIF   (NOT &$OK).NOCHK                                                 
&PV      SETC  '&$REG#'                                                         
         AIF   (&LCR).LCR,('&RV' EQ '&PV').LBL                                  
&OPC     SETC  'LR'                                                             
.NOCHK   ANOP                                                                   
&LABEL   &OPC  &R,&PV                                                           
.COM     AIF   (NOT &LCR).END                                                   
         LCR   &R,&R                                                            
         MEXIT                                                                  
.SR      ANOP                                                                   
&L       SR    &R,&R                                                            
         MEXIT                                                                  
.ERR1    MNOTE 8,'&PV IS A REGISTER, USE PARENS TO INDICATE THAT'               
         MEXIT                                                                  
.REG     ANOP                                                                   
&PV      SETC  '&PV'(2,K'&PV-2)                                                 
         AIF   (&LCR).LCR                                                       
         AIF   (NOT &$HIBAL).GOTP                                               
        $RGLKUP &PV                                                             
         AIF   (&$OK).GR                                                        
        $USLKUP &PV                                                             
         AIF   (NOT &$OK).GOTP                                                  
         DS    0S(&PV)                                                          
.GR      ANOP                                                                   
&PV      SETC  '&$REG#'                                                         
.GOTP    AIF   ('&RV' EQ '&PV').LBL                                             
&L       LR    &R,&P                                                            
         MEXIT                                                                  
.LCR     ANOP                                                                   
&L       LCR   &R,&P                                                            
.END     MEND                                                                   
