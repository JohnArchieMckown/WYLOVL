         MACRO                                                                  
&L       SET   &MODE=,&REF=,&REGS=(R15,R0,R1)                                   
.*       ADDED 3/77, JDN;  MODIFIED 5/77, JDN                                   
.*       INTERLOCK MODIFICATION 4/85 JKR                                        
.*  For non-interlocked sets, the operands are the &SYSLIST parms.              
.*    The interlocked sets use the &SYSLIST parm too, after the                 
.*    mode has been determined by keyword parameters.                           
.*  MODE, REF and REGS and are all for the interlocked set instr                
.*  &REF and &REGS have the same meaning as IBM supplied OIL and                
.*    NIL insructions, while &MODE=LOCKED should be specified to                
.*    indicate an interlocked set.  REF must be specified if                    
.*    MODE=LOCKED.                                                              
         GBLA  &$BFVAL,&$BFCC          BinaryFlagValue/ConditionCode            
         GBLB  &$OK                                                             
         GBLC  &$BFLOC,&$BFTYP         BinaryFlagLocation/Type                  
         LCLA  &I                                                               
&I       SETA  0                       SYSLIST index                            
&L       LABEL                                                                  
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT          Look at all parms               
&I       SETA  &I+1                                                             
        $BV    &SYSLIST(&I,1),&SYSLIST(&I,2)    Are flags valid?                
         AIF   (NOT &$OK).ERRBADF               | no, bomb out                  
         AIF   ('&$BFTYP' EQ 'C').MVI           SET type EQ                     
         AIF   ('&MODE' EQ 'LOCKED').DOLOCK     Do interlocked set              
         AIF   ('&MODE' EQ '').NLKOK                                            
         MNOTE 8,'MODE=&MODE is an invalid parameter'                           
.NLKOK   ANOP  ,                                                                
         AIF   (&$BFCC NE 1 AND &$BFCC NE 7).OFF        O,NZ ccs                
         OI    &$BFLOC,&$BFVAL                  Do set                          
         AGO   .LOOP                                                            
.OFF     AIF   (&$BFCC NE 14 AND &$BFCC NE 8).ERRCOND   NO,Z ccs                
         NI    &$BFLOC,255-&$BFVAL              Do set "off"                    
         AGO   .LOOP                                                            
.MVI     AIF   (&$BFCC NE 8).ERRCOND            SET type EQ = MVI               
         MVI   &$BFLOC,&$BFVAL                                                  
         AGO   .LOOP                                                            
.DOLOCK  AIF   (&$BFCC NE 1 AND &$BFCC NE 7).LOFF       O,NZ ccs                
        $OIL   &$BFLOC,&$BFVAL,REF=&REF,WREGS=&REGS                             
         AGO   .LOOP                                                            
.LOFF    AIF   (&$BFCC NE 14 AND &$BFCC NE 8).ERRCOND   NO,Z ccs                
        $NIL   &$BFLOC,255-&$BFVAL,REF=&REF,WREGS=&REGS                         
         AGO   .LOOP                                                            
.*                                                                              
.ERRBADF MNOTE 8,'&SYSLIST(&I) IS A NOT A FLAG'                                 
         AGO   .LOOP                                                            
.*                                                                              
.ERRCOND MNOTE 8,'&SYSLIST(&I) - INCONSISTENT CONDITION CODE'                   
         AGO   .LOOP                                                            
.EXIT    MEND                                                                   
