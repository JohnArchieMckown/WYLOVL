         MACRO                                                                  
&L       PUSH  &PTR=                                                            
.*       RE-DONE 4/77, JDN;  Last modified, 11/85, Niz                          
         GBLA  &$COUNT,&$UPTR,&$UR#STK(0),&$REG#                                
         GBLB  &$OK                                                             
         GBLC  &$USINGS(16),&$USTK(0),&$ULOC                                    
         LCLA  &I                                                               
         AIF   ('&SYSLIST(1)' NE 'PRINT' AND '&SYSLIST(1)' NE 'USING'  *        
               AND '&SYSLIST(1)' NE 'GEN' AND '&SYSLIST(1)' NE 'NOGEN' *        
               AND '&SYSLIST(1)' NE 'ON' AND '&SYSLIST(1)' NE 'OFF'    *        
               AND '&SYSLIST(1)' NE 'DSECTS').PSTK                              
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
         AIF   ('&SYSLIST(&I)' NE 'USING').DO                                   
&UP      SETA  &$UPTR+1                                                         
&J       SETA  1                                                                
.ULOOP   ANOP                                                                   
&$UPTR   SETA  &$UPTR+1                                                         
&$USTK(&$UPTR) SETC '&$USINGS(&J)'                                              
&$UR#STK(&$UPTR) SETA &J-1+100                                                  
&J       SETA  &J+1                                                             
         AIF   (&J LE 16).ULOOP                                                 
&$UR#STK(&UP) SETA 0-&$UR#STK(&UP)                                              
         AGO   .LOOP                                                            
.DO    $$PUSH  PRINT                                                            
         AIF   ('&SYSLIST(&I)' EQ 'PRINT').LOOP                                 
         AIF   ('&SYSLIST(&I)' EQ 'DSECTS').DSECTS                              
         PRINT &SYSLIST(&I)                                                     
         AGO   .LOOP                                                            
.DSECTS  ANOP                                                                   
&$ULOC   SETC  '&SYSLOC'                                                        
         AIF   ('&SYSPARM' EQ 'DSECTS').DOSP                                    
         PRINT OFF                                                              
.DOSP    ANOP                                                                   
*PUSH,GEN                                                                       
         AGO   .LOOP                                                            
.*                                                                              
.PSTK    AIF   (T'SPR NE '#').ERRNSPR                                           
.*   AIF   (N'&SYSLIST EQ 1 AND '&SYSLIST(1)' NE '&SYSLIST(1,1)').PREG          
&LEN     SETC  '0'                                                              
.PLP     AIF   (&I GE N'&SYSLIST).PFIN                                          
&I       SETA  &I+1                                                             
         AGO   (N'&SYSLIST(&I)+1).PLP,.P1,.P2                                   
&NOP     SETA  N'&SYSLIST(&I)                                                   
         MNOTE 8,'&SYSLIST(&I) has wrong no. of operands (NOP=&NOP)'            
         AGO   .PLP                                                             
.*                                                                              
.P1      AIF   ('&SYSLIST(&I)' NE '&SYSLIST(&I,1)').ERRMR                       
        $RGLKUP &SYSLIST(&I)                                                    
         AIF   (NOT &$OK).PLEN                                                  
         ST    &SYSLIST(&I),(&LEN)(,SPR)                                        
&LEN     SETC  '&LEN+4'                                                         
         AGO   .PLP                                                             
.PLEN    ANOP                                                                   
&LEN     SETC  '&LEN+((&SYSLIST(&I)+3)/4*4)'                                    
         AGO   .PLP                                                             
.*                                                                              
.P2     $RGLKUP &SYSLIST(&I,1)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&R1      SETA  &$REG#                                                           
        $RGLKUP &SYSLIST(&I,2)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&N       SETA  (&$REG#-&R1+1)*4                                                 
         AIF   (&N GT 0).HVL                                                    
&N       SETA  (16-&R1+&$REG#+1)*4                                              
.HVL     ANOP                                                                   
         STM   &SYSLIST(&I,1),&SYSLIST(&I,2),(&LEN)(SPR)                        
&LEN     SETC  '&LEN+&N'                                                        
         AGO   .PLP                                                             
.*                                                                              
.PREG    AIF   (N'&SYSLIST EQ 1).PREGDO                                         
         MNOTE 8,'only "&SYSLIST(1)" accepted.'                                 
.PREGDO $RGLKUP &SYSLIST(1,1)                                                   
         AIF   (NOT &$OK).ERRNR1,(&$REG# EQ 0).ERRR0                            
         AR    SPR,&SYSLIST(1,1)                                                
         LA    SPR,3(,SPR)                                                      
         N     SPR,=A(X'FFFFFFFC')                                              
         AGO   .COM                                                             
.*                                                                              
.PFIN    AIF   ('&LEN' EQ '0').ERRNOP                                           
.COM     AIF   ('&PTR' EQ '').NPTR                                              
         LR    &PTR,SPR                                                         
.NPTR    LA    SPR,(&LEN)(,SPR)                                                 
.EXIT    MEXIT                                                                  
.*                                                                              
.ERRNSPR MNOTE 8,'Stack pointer register (SPR) must be defined.'                
         MEXIT                                                                  
.*                                                                              
.ERRMR   MNOTE 8,'"(REG)" must be the first and only operand.'                  
         MEXIT                                                                  
.*                                                                              
.ERRNR1  MNOTE 8,'&SYSLIST(1,1) is not a register.'                             
         MEXIT                                                                  
.*                                                                              
.ERRRO   MNOTE 8,'Register 0 can''t be used.'                                   
         MEXIT                                                                  
.*                                                                              
.ERRNR   MNOTE 8,'&SYSLIST(&I) is not a register.'                              
         MEXIT                                                                  
.*                                                                              
.ERRNOP  MNOTE 8,'Required operands missing.'                                   
         MEND                                                                   
