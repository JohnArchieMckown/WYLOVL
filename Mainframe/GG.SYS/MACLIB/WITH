         MACRO                                                                  
&L       WITH  &LABEL=                                                          
.*       RE-DONE 2/77, JDN;  MODIFIED 4/79, JDN                                 
.*       Labeled usings added 11/85, JKR                                        
         GBLA  &$UPTR,&$UR#STK(0)                                               
         GBLC  &$USTYPE                                                         
         LCLA  &I                                                               
         LCLC  &LBA,&LBB,&LBC,&LBS                                              
         AIF   ('&LABEL' EQ '').NOUPLBL                                         
&LBA     SETC  '$$U&LABEL.A'                                                    
&LBC     SETC  '$$U&LABEL.C'                                                    
&LBS     SETC  '$$U&LABEL.S'                                                    
         GBLA  &(&LBA)(0),&(&LBS)                                               
         GBLC  &(&LBC)(0)                                                       
&UP      SETA  &(&LBS)                 Stack value on this label                
         AGO   .DONOP                                                           
.NOUPLBL ANOP  ,                                                                
&UP      SETA  &$UPTR                                                           
.DONOP   ANOP  ,                                                                
&NOP     SETA  N'&SYSLIST                                                       
 AIF ('&SYSLIST(&NOP)'NE'BEGIN' AND '&SYSLIST(&NOP) '(1,1)NE'''').LOOP          
&NOP     SETA  &NOP-1                                                           
.LOOP    AIF   (&I GE &NOP).DONE                                                
&I       SETA  &I+1                                                             
&LABEL   USING &SYSLIST(&I),PUSH=YES                                            
         AGO   .LOOP                                                            
.*                                                                              
.DONE    AIF   ('&$USTYPE' EQ 'L').LABWITH      Labeled withs                   
         AIF   ('&$USTYPE' EQ 'A').ASSWITH      Associative withs               
         AIF   (&UP GE &$UPTR).NOCHNG                                           
&$UR#STK(&UP+1) SETA 0-&$UR#STK(&UP+1)                                          
.*                                                                              
.*  If the number of WITH operands is not equal to N'&SYSLIST then              
.*  the MACRO is block type                                                     
.*                                                                              
         AIF   (&NOP NE N'&SYSLIST).BLOCK                                       
&L       BEGIN LAB=U,INVERT=YES,BLOCK=NO                                        
         MEXIT                                                                  
.*                                                                              
.BLOCK   ANOP                                                                   
&L       BEGIN LAB=W                                                            
         AIF   ('&SYSLIST(&NOP+1)'(1,1) NE '''').EXIT                           
         GEN   &SYSLIST(&NOP+1)                                                 
         END                                                                    
         MEXIT                                                                  
.*                                                                              
.NOCHNG  AIF   (&NOP EQ N'&SYSLIST).EXIT                                        
         BEGIN LAB=Z               DUMMY BLOCK TO KEEP THINGS COOL              
         AIF   ('&SYSLIST(&NOP+1)'(1,1) NE '''').EXIT                           
         GEN   &SYSLIST(&NOP+1)                                                 
         END                                                                    
         MEXIT                                                                  
.*                                                                              
.*  Labeled with code                                                           
.*                                                                              
.LABWITH AIF   (&UP GE &(&LBS)).NOCHNGL                                         
&(&LBA)(&UP+1) SETA 0-&(&LBA)(&UP+1)                                            
.*                                                                              
.*  If the number of WITH operands is not equal to N'&SYSLIST then              
.*  the MACRO is block type                                                     
.*                                                                              
         AIF   (&NOP NE N'&SYSLIST).BLOCKL                                      
&L       BEGIN LAB=UL$&LABEL,INVERT=YES,BLOCK=NO                                
         MEXIT                                                                  
.*                                                                              
.BLOCKL  ANOP                                                                   
&L       BEGIN LAB=WL$&LABEL                                                    
         AIF   ('&SYSLIST(&NOP+1)'(1,1) NE '''').EXIT                           
         GEN   &SYSLIST(&NOP+1)                                                 
         END                                                                    
         MEXIT                                                                  
.*                                                                              
.NOCHNGL AIF   (&NOP EQ N'&SYSLIST).EXIT                                        
         BEGIN LAB=Z               DUMMY BLOCK TO KEEP THINGS COOL              
         AIF   ('&SYSLIST(&NOP+1)'(1,1) NE '''').EXIT                           
         GEN   &SYSLIST(&NOP+1)                                                 
         END                                                                    
         MEXIT                                                                  
.*                                                                              
.*  Associative with code                                                       
.*                                                                              
.ASSWITH ANOP  ,                                                                
.*                                                                              
.*  If the number of WITH operands is not equal to N'&SYSLIST then              
.*  the MACRO is block type                                                     
.*                                                                              
         AIF   (&NOP EQ N'&SYSLIST).EXIT                                        
         BEGIN LAB=Z               DUMMY BLOCK TO KEEP THINGS COOL              
         AIF   ('&SYSLIST(&NOP+1)'(1,1) NE '''').EXIT                           
         GEN   &SYSLIST(&NOP+1)                                                 
         END                                                                    
.EXIT    MEND                                                                   
