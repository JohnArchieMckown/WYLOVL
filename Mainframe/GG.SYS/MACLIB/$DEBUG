         MACRO                                                                  
        $DEBUG                                                                  
.*       MODIFIED 2/80, JDN                                                     
.*       Labeled using debug support 11/85, JKR                                 
         GBLA  &$BPTR,&$BLPTR,&$PPTR,&$NEST,&$UPTR,&$UR#STK(0)                  
         GBLA  &$LUPTR                                                          
         GBLB  &$HIBAL                                                          
         GBLC  &$BSTK(0),&$BLABSTK(0)                                           
         GBLC  &$PSTK(0),&$PLENSTK(0),&$PFRSTK(0),&$PLRSTK(0)                   
         GBLC  &$USINGS(16),&$USTK(0),&$LUSING(0)                               
         LCLA  &I,&J,&J1                                                        
         LCLC  &LBL,&LBA,&LBB,&LBC,&LBS,&LBN                                    
.*                                                                              
&STK     SETC  'ALL'                                                            
         AIF   ('&SYSLIST(1)' EQ '').STK                                        
.*                                                                              
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
&STK     SETC  '&SYSLIST(&I)'                                                   
.*                                                                              
         AIF   ('&STK  '(1,2) NE 'L:').TYP                                      
&L       SETC  '&STK'(3,K'&STK-2)                                               
&LV      SETA  L'&L                                                             
         MNOTE *,'SYMBOL &L HAS LENGTH &LV'                                     
         AGO   .LOOP                                                            
.*                                                                              
.TYP     AIF   ('&STK  '(1,2) NE 'T:').DEF                                      
&T       SETC  '&STK'(3,K'&STK-2)                                               
&TV      SETC  T'&T                                                             
&TEMP    SETC  'C''&TV'''                                                       
&BIN     SETA  &TEMP                                                            
         MNOTE *,'SYMBOL &T HAS TYPE "&TV" (VALUE=&BIN)'                        
         AGO   .LOOP                                                            
.*                                                                              
.DEF     AIF   ('&STK  '(1,2) NE 'D:').SECT                                     
&L       SETC  '&STK'(3,K'&STK-2)                                               
&DV      SETB  (D'&L)                                                           
         MNOTE *,'SYMBOL &L HAS D-QUOTE OF &DV'                                 
         AGO   .LOOP                                                            
.*                                                                              
.SECT    AIF   ('&STK' NE 'ALL' AND '&STK' NE 'SECT').STK                       
         MNOTE *,'CURRENT CSECT/DSECT: &SYSECT, LOCTR: &SYSLOC'                 
.*                                                                              
.STK     AIF   (&$HIBAL).STKDO                                                  
         MNOTE 4,'NO "HIBAL" STATEMENT -- &&HIBAL NOT SET'                      
*                                                                               
.STKDO   AIF   ('&STK' NE 'ALL' AND '&STK' NE 'BSTK').PSTK                      
         MNOTE *,'BSTK STATUS -- BLPTR=&$BLPTR, BPTR=&$BPTR'                    
&J       SETA  &$BPTR                                                           
.BLP     AIF   (&J LE 0).PSTK                                                   
         MNOTE *,'BLOCK #&J, &$BSTK(&J), &$BLABSTK(&J)'                         
&J       SETA  &J-1                                                             
         AGO   .BLP                                                             
.*                                                                              
.PSTK    AIF   ('&STK' NE 'ALL' AND '&STK' NE 'PSTK').USTK                      
         MNOTE *,'PSTK STATUS -- PPTR=&$PPTR'                                   
&J       SETA  &$PPTR                                                           
.PLP     AIF   (&J LE 0).USTK                                                   
         MNOTE *,'PUSH #&J, &$PSTK(&J), (&$PFRSTK(&J),&$PLRSTK(&J)), &$*        
               PLENSTK(&J)'                                                     
         ANOP                                                                   
&J       SETA  &J-1                                                             
         AGO   .PLP                                                             
.*                                                                              
.USTK    AIF   ('&STK' NE 'ALL' AND '&STK' NE 'USINGS').LUSTK                   
         MNOTE *,'CURRENT USINGS...'                                            
&J       SETA  15                                                               
.USLP    AIF   ('&$USINGS(&J+1)' EQ '@R&J').USSKP                               
         MNOTE *,'R&J = &$USINGS(&J+1)'                                         
.USSKP   ANOP                                                                   
&J       SETA  &J-1                                                             
         AIF   (&J GE 0).USLP                                                   
         MNOTE *,'USTK STATUS -- UPTR=&$UPTR'                                   
&J       SETA  &$UPTR                                                           
.ULP     AIF   (&J LE 0).LOOP                                                   
         AIF   (&$UR#STK(&J) LT 0).USNEG                                        
         MNOTE *,'USING #&J: &$USTK(&J), &$UR#STK(&J)'                          
&J       SETA  &J-1                                                             
         AGO   .ULP                                                             
.USNEG   MNOTE *,'USING #&J: &$USTK(&J), -&$UR#STK(&J)'                         
&J       SETA  &J-1                                                             
         AGO   .ULP                                                             
.*                                                                              
.*  Labeled usings                                                              
.*                                                                              
.LUSTK   AIF   ('&STK' NE 'ALL' AND '&STK' NE 'LUSINGS').LOOP                   
         MNOTE *,'CURRENT LABELED USINGS...'                                    
&J       SETA  1                                                                
.LUS1    ANOP  ,                                                                
&LBL     SETC  '&$LUSING(&J)'                                                   
&LBA     SETC  '$$U&LBL.A'                                                      
&LBB     SETC  '$$U&LBL.B'                                                      
&LBC     SETC  '$$U&LBL.C'                                                      
&LBS     SETC  '$$U&LBL.S'                                                      
&LBN     SETC  '$$U&LBL.N'                                                      
         GBLA  &(&LBA)(0),&(&LBS),&(&LBN)                                       
         GBLB  &(&LBB)                                                          
         GBLC  &(&LBC)(0)                                                       
         MNOTE *,'The active level for &LBL is &(&LBN)'                         
&J1      SETA  &(&LBS)                                                          
.LUS2    ANOP  ,                                                                
&MSG     SETC  ' has no labeled using at this level'                            
         AIF   (&J1 LE 0).LUINACT                                               
         AIF   ('&(&LBC)(&J1)' EQ '').LUINACT                                   
&MSG     SETC  '.&(&LBC)(&J1) with R&(&LBA)(&J1)'                               
.LUINACT MNOTE *,'&LBL&MSG (Level=&J1)'                                         
&J1      SETA  &J1-1                                                            
         AIF   (&J1 GT 0).LUS2                                                  
&J       SETA  &J+1                                                             
         AIF   (&J LE &$LUPTR).LUS1                                             
         AGO   .LOOP                                                            
.EXIT    MEND                                                                   
