         MACRO                                                                  
&L       HIBAL &R0,&R1,&R2,&R3,&R4,&R5,&R6,&R7,&R8,&R9,&R10,&R11,&R12,&*        
               R13,&R14,&R15,&XA=YES,&ASSOC=NO                                  
.*       Modified 1/88, Niz                                                     
.*                                                                              
.* The HIBAL macro system was originally written by Richard Carr of             
.* the Stanford Systems Group.  The second version of HIBAL was                 
.* written by Jim Nisbet also of the Stanford Systems Group.                    
.*                                                                              
.* Other contributors are:  Carol Farlow-Lerche, Parker Gillespie,              
.* Charley Hoyt, Bruce Hunt, Rich Levitt, Jim Lipkis, Jimmse Mandl,             
.* John Montague, Jim Nisbet, Mike Powell, Jonathan Ross, Len                   
.* Shustek, and Jim Stosick.                                                    
.*                                                                              
.* There is a relatively current Hibal Manual written by Vicki                  
.* Griffeth now available.  Call me (Niz) at 415-723-4206 if you                
.* want a copy.                                                                 
.*                                                                              
.* Hibal 5 is really just the introduction of a new ASMEDIT                     
.* (version 3).  Hibal 5 will work fine with the old ASMEDIT 2.                 
.* The new ASMEDIT statements "*BOX" and "*-" provide better                    
.* formatting.  In general the output from Asmedit 3 is quite a                 
.* bit different than with Asmedit 2.  If after a week you don't                
.* like it, then send me mail GG.JDN@FORSYTHE.STANFORD.EDU.                     
.* (The new ASMEDIT is written in Hibal.)                                       
.*                                                                              
.*   The &XA keyword parameter indicates that the XA branch and                 
.*   save (BAS,BASR) instructions can be used.                                  
.*                                                                              
.*   The &ASSOC keyword parameter indicates that associative USINGs             
.*   will be used.                                                              
.*                                                                              
         GBLA  &$MAP(256)                                                       
         GBLC  &$HEX(256)                                                       
&$MAP(C'A') SETA 1,1,1,1,1,1,1,1,1                                              
&$MAP(C'J') SETA 1,1,1,1,1,1,1,1,1                                              
&$MAP(C'S') SETA 1,1,1,1,1,1,1,1                                                
&$MAP(C'@') SETA 1                                                              
&$MAP(C'$') SETA 1                                                              
&$MAP(C'#') SETA 1                                                              
&$MAP(C'0') SETA 2,2,2,2,2,2,2,2,2,2                                            
&$HEX(1)    SETC '00','01','02','03','04','05','06','07'                        
&$HEX(9)    SETC '08','09','0A','0B','0C','0D','0E','0F'                        
&$HEX(17)   SETC '10','11','12','13','14','15','16','17'                        
&$HEX(25)   SETC '18','19','1A','1B','1C','1D','1E','1F'                        
&$HEX(33)   SETC '20','21','22','23','24','25','26','27'                        
&$HEX(41)   SETC '28','29','2A','2B','2C','2D','2E','2F'                        
&$HEX(49)   SETC '30','31','32','33','34','35','36','37'                        
&$HEX(57)   SETC '38','39','3A','3B','3C','3D','3E','3F'                        
&$HEX(65)   SETC '40','41','42','43','44','45','46','47'                        
&$HEX(73)   SETC '48','49','4A','4B','4C','4D','4E','4F'                        
&$HEX(81)   SETC '50','51','52','53','54','55','56','57'                        
&$HEX(89)   SETC '58','59','5A','5B','5C','5D','5E','5F'                        
&$HEX(97)   SETC '60','61','62','63','64','65','66','67'                        
&$HEX(105)  SETC '68','69','6A','6B','6C','6D','6E','6F'                        
&$HEX(113)  SETC '70','71','72','73','74','75','76','77'                        
&$HEX(121)  SETC '78','79','7A','7B','7C','7D','7E','7F'                        
&$HEX(129)  SETC '80','81','82','83','84','85','86','87'                        
&$HEX(137)  SETC '88','89','8A','8B','8C','8D','8E','8F'                        
&$HEX(145)  SETC '90','91','92','93','94','95','96','97'                        
&$HEX(153)  SETC '98','99','9A','9B','9C','9D','9E','9F'                        
&$HEX(161)  SETC 'A0','A1','A2','A3','A4','A5','A6','A7'                        
&$HEX(169)  SETC 'A8','A9','AA','AB','AC','AD','AE','AF'                        
&$HEX(177)  SETC 'B0','B1','B2','B3','B4','B5','B6','B7'                        
&$HEX(185)  SETC 'B8','B9','BA','BB','BC','BD','BE','BF'                        
&$HEX(193)  SETC 'C0','C1','C2','C3','C4','C5','C6','C7'                        
&$HEX(201)  SETC 'C8','C9','CA','CB','CC','CD','CE','CF'                        
&$HEX(209)  SETC 'D0','D1','D2','D3','D4','D5','D6','D7'                        
&$HEX(217)  SETC 'D8','D9','DA','DB','DC','DD','DE','DF'                        
&$HEX(225)  SETC 'E0','E1','E2','E3','E4','E5','E6','E7'                        
&$HEX(233)  SETC 'E8','E9','EA','EB','EC','ED','EE','EF'                        
&$HEX(241)  SETC 'F0','F1','F2','F3','F4','F5','F6','F7'                        
&$HEX(249)  SETC 'F8','F9','FA','FB','FC','FD','FE','FF'                        
.*                                                                              
         GBLB  &$HIBAL                                                          
         AIF   (&$HIBAL).NOMULT                                                 
&$HIBAL  SETB  1                                                                
*VERSION:: Hibal 5                                                              
.*                                                                              
&SECT    SETC  '&SYSECT'                                                        
         AIF   ('&SECT' EQ '').OK                                               
         MNOTE 4,'HIBAL macro should come before CSECT/DSECT''s'                
.OK      ANOP                                                                   
         MNOTE *,'HIBAL/4 Macro System. Class I. Stanford Proprietary'          
         PUSH  PRINT                                                            
         PRINT OFF                                                              
.*                                                                              
$$START  OPSYN START                                                            
         COPY  START                                                            
$$CSECT  OPSYN CSECT                                                            
         COPY  CSECT                                                            
$$USING  OPSYN USING                                                            
         COPY  USING                                                            
$$DROP   OPSYN DROP                                                             
         COPY  DROP                                                             
$$PRINT  OPSYN PRINT                                                            
         COPY  PRINT                                                            
$$PUSH   OPSYN PUSH                                                             
         COPY  PUSH                                                             
$$POP    OPSYN POP                                                              
         COPY  POP                                                              
$$END    OPSYN END                                                              
         COPY  END                                                              
$$LTORG  OPSYN LTORG                                                            
         COPY  LTORG                                                            
$$EX     OPSYN EX                                                               
         COPY  EX                                                               
.*                                                                              
         AIF   ('&XA' EQ 'YES').BASOK                                           
BAS      OPSYN BAL                                                              
BASR     OPSYN BALR                                                             
.BASOK   ANOP                                                                   
.*                                                                              
         GBLB  &$ASSOC                                                          
&$ASSOC  SETB  0                                                                
         AIF   ('&ASSOC' EQ 'NO').NOASSOC                                       
&$ASSOC  SETB  1                                                                
.NOASSOC ANOP                                                                   
.*                                                                              
         DEFCC (EQ,E,EQU,EQUAL),8,TYPE=C                                        
         DEFCC (OFF,Z,ZERO),8,TYPE=TM                                           
         DEFCC (NE,NEQ),7,TYPE=C                                                
         DEFCC (NZ,NZER),7,TYPE=TM                                              
         DEFCC (P,POS,PLUS),2                                                   
         DEFCC (GT,H,HI,HIGH),2,TYPE=C                                          
         DEFCC (LGT),2,TYPE=CL                                                  
         DEFCC (CARRY),3,TYPE=CL                                                
         DEFCC (NP,NPOS,NH,NHI),13                                              
         DEFCC (NG),13,TYPE=C                                                   
         DEFCC (LNG),13,TYPE=CL                                                 
         DEFCC (LE),12,TYPE=C                                                   
         DEFCC (LLE),12,TYPE=CL                                                 
         DEFCC (L,LOW,N,NEG,MIN,MINUS),4                                        
         DEFCC (MIX,M,MIXED),4,TYPE=TM                                          
         DEFCC (LT),4,TYPE=C                                                    
         DEFCC (LLT),4,TYPE=CL                                                  
         DEFCC (NLOW,NN,NNEG,NMIN),11                                           
         DEFCC (NL),11,TYPE=C                                                   
         DEFCC (LNL),11,TYPE=CL                                                 
         DEFCC (NM,NMIX,NMIXED),11,TYPE=TM                                      
         DEFCC (GE),10,TYPE=C                                                   
         DEFCC (LGE),10,TYPE=CL                                                 
         DEFCC (ONES,ONE,ON,O),1,TYPE=TM                                        
         DEFCC (OFL,OFLO),1                                                     
         DEFCC (NO,NONE),14,TYPE=TM                                             
         DEFCC (NOFL),14                                                        
         DEFCC (TRUE),15                                                        
         DEFCC (FALSE),0                                                        
.*                                                                              
         AIF   ('&L' EQ '').RSECT                                               
&L       CSECT                                                                  
         AGO   .REGS                                                            
.*                                                                              
.RSECT   AIF   ('&SECT' EQ '').NOSECT                                           
&SECT    LOCTR                                                                  
         AGO   .REGS                                                            
.*                                                                              
.NOSECT  ANOP                                                                   
HIBAL    DSECT                                                                  
.*                                                                              
.REGS    REGS  R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,R10,R11,R12,R13,R14,R15            
         PRINT ON                                                               
         REGS  &R0,&R1,&R2,&R3,&R4,&R5,&R6,&R7,&R8,&R9,&R10,&R11,&R12,&*        
               R13,&R14,&R15                                                    
         PRINT OFF                                                              
         DROP                                                                   
.*                                                                              
HIBAL-4  IDENT CODE=NO                                                          
.*                                                                              
         POP   PRINT                                                            
.*                                                                              
.*       Check for SYSPARM(PCODE) and do some additional setup.                 
.*                                             Added, 5/89, Niz                 
.*                                                                              
         GBLB  &$PCODE,&$PRALL                                                  
&$PCODE  SETB 0                                                                 
         AIF   ('&SYSPARM' NE 'PCODE').NPCODE                                   
&$PCODE  SETB  1                                                                
       $$PRINT ON,GEN,DATA                                                      
&$PRALL  SETB  1                   Force printing ON,GEN,DATA                   
.NPCODE  ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
.NOMULT  MNOTE 4,'Multiple HIBAL statements ignored.'                           
         MEND                                                                   
