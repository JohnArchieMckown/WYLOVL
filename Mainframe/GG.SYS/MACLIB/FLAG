         MACRO                                                                  
&L       FLAG  &INIT=0                                                          
.*       ADDED 3/77, JDN;  MODIFIED 12/78, JDN                                  
         GBLA  &$COUNT,&$FLAGV,&$BFVAL,&$BFCC                                   
         GBLB  &$OK                                                             
         GBLC  &$FLAGNM,&$FLAGIN,&$BFLOC,&$BFTYP,&$BFMSG,&$HEX(256)             
         LCLA  &I                                                               
*PUSH,NOGEN                                                                     
         AIF   ('&L' EQ '').LOOP                                                
         AIF   ('&INIT' EQ '0').NOINIT                                          
        $BV    &INIT                                                            
&TMP     SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&TMP     DS    0X                                                               
         ORG   &$FLAGNM                                                         
         DC    AL1(&$BFVAL)                                                     
         ORG   &TMP                                                             
.NOINIT  ANOP                                                                   
&$FLAGNM SETC  '&L'                                                             
&L       DC    AL1(&INIT)                                                       
&$FLAGV  SETA  256                                                              
         AIF (N'&SYSLIST LE 2 AND '&SYSLIST(1)&SYSLIST(2)' EQ '').EXIT          
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
         AGO   (N'&SYSLIST(&I)+1).OP1,.OP1,.OPM,.OPM                            
         MNOTE 8,'&SYSLIST(&I) - BAD FLAG DEFINITION'                           
         AGO   .LOOP                                                            
.OP1     ANOP                                                                   
&$BFLOC  SETC  '&$FLAGNM'                                                       
&MSG     SETC  '&SYSLIST(&I)'                                                   
&$FLAGV  SETA  &$FLAGV/2                                                        
         AIF   (&$FLAGV EQ 0).ERRVAL,('&SYSLIST(&I)' EQ '').LOOP                
&$BFVAL  SETA  &$FLAGV                                                          
&$BFTYP  SETC  'TM'                                                             
&$BFCC   SETA  1                                                                
&$BFMSG  SETC  'ONES'                                                           
&CC      SETA  &$BFCC                                                           
         AGO   .NOLOC                                                           
.OPM     AIF   ('&SYSLIST(&I,2)' NE '').OPBV                                    
&$FLAG   SETA  &$FLAGV/2                                                        
         AIF   (&$FLAGV EQ 0).ERRVAL                                            
        $BV    &SYSLIST(&I,2),&$FLAGV,&$FLAGNM                                  
         AGO   .GOTBV                                                           
.OPBV   $BV    &SYSLIST(&I,2),&SYSLIST(&I,3),&$FLAGNM                           
.GOTBV   AIF   (NOT &$OK).ERRBV                                                 
&MSG     SETC  '&SYSLIST(&I,1)'                                                 
&CC      SETA  &$BFCC                                                           
         AIF   ('&$BFTYP' EQ 'TM').CCOK                                         
&CC      SETA  16+&CC                                                           
.CCOK    AIF   ('&$BFLOC' NE '').NOLOC                                          
&$BFLOC  SETC  '&$FLAGNM'                                                       
.NOLOC   AIF   ('&$BFMSG' NE '').NOMSG                                          
&$BFMSG  SETC  'ONES'                                                           
.NOMSG   ANOP                                                                   
&SYSLIST(&I,1) EQU &$BFLOC,&$BFVAL,&CC                                          
.MSG     ANOP                                                                   
&MSG     SETC  '&$BFLOC..&MSG              '(1,17)                              
         MNOTE *,'::&MSG &$HEX(&$BFVAL+1) &$BFMSG'                              
         AGO   .LOOP                                                            
.*                                                                              
.ERRBV   MNOTE 8,'&SYSLIST(&I,2),&SYSLIST(&I,3) IS NOT A FLAG, &SYSLIST*        
               (&I,1) SKIPPED'                                                  
         AGO   .EXIT                                                            
.*                                                                              
.ERRVAL  MNOTE 8,'ATTEMPT TO DEFINE OVER 8 BITS IN &$FLAGNM'                    
.EXIT    ANOP                                                                   
*POP                                                                            
         MEND                                                                   
