         MACRO                                                                  
        $REL   &OP1P,&OP2,&OP3                                                  
.*       MODIFIED 7/77, JDN                                                     
         GBLA  &$CCVAL,&$BFVAL,&$BFCC                                           
         GBLB  &$OK                                                             
         GBLC  &$CCTYP,&$BFLOC,&$BFTYP                                          
         LCLC  &OP1LBL,&OP1                                                     
         LCLA  &N                                                               
&OP1LBL  SETC  ''                                                               
&OP1     SETC  '&OP1P'                                                          
.*                                                                              
         AGO   (N'&SYSLIST).TYPEA,.TYPEB,.TYPEC                                 
&I       SETA  N'&SYSLIST                                                       
  MNOTE 8,'WRONG NO. OF OPERANDS IN RELATION (NOP=&I), FALSE ASSUMED'           
&$CCVAL  SETA  0                                                                
         MEXIT                                                                  
.*                                                                              
.* TYPE A RELATION - ONE TOKEN                                                  
.*       COULD BE: 1. CONDITION CODE                                            
.*                 2. FLAG EXPRESSION                                           
.*                                                                              
.TYPEA  $CCLKUP &OP1                                                            
         AIF   (&$OK).EXIT                                                      
        $BV    &OP1                                                             
         AIF   (&$OK).TFLAG                                                     
&ERR     SETC  '&OP1'                                                           
         AGO   .BVERR                                                           
.*                                                                              
.* TYPE B RELATION - 2 TOKENS                                                   
.*       COULD BE: 1. (REG,CC)                                                  
.*                 2. (SYMBOL,CC) CC RESTRICTED TO ZERO OR NOTZERO              
.*                 2. (FLAG,CC)                                                 
.*                 3. ('ANY INSTRUCTION',CC)                                    
.*                                                                              
.TYPEB  $CCLKUP &OP2                                                            
         AIF   (NOT &$OK).CCERR                                                 
         AIF   ('&OP1'(1,1) EQ '''').TB4  GENERAL TEST                          
        $RGLKUP &OP1                  REGISTER TEST?                            
         AIF   (NOT &$OK).TB2                                                   
*JOIN                                                                           
         LTR   &OP1,&OP1                                                        
         AGO   .EXIT                                                            
.*                                                                              
.TB2    $BV    &OP1,&OP2                                                        
         AIF   (&$OK).TFLAG                                                     
.*                                                                              
.TB3     AIF   (&$CCVAL NE 7 AND &$CCVAL NE 8).CCERR                            
.*                                                                              
.*  Search for labels of labeled usings                                         
.*                                                                              
         AIF   (K'&OP1P LE 2).LBLD1                                             
&N       SETA  2                                                                
.NDLK1   AIF   ('&OP1P'(&N,1) NE '.').NDLBL1                                    
&OP1LBL  SETC  '&OP1P'(1,&N)                                                    
&OP1     SETC  '&OP1P'(&N+1,K'&OP1P-&N)                                         
         AGO   .LBLD1                                                           
.NDLBL1  ANOP  ,                                                                
&N       SETA  &N+1                                                             
         AIF   (&N LT K'&OP1P).NDLK1                                            
.LBLD1   ANOP  ,                                                                
.*                                                                              
         AIF   (L'&OP1 LE 1).CLIZERO                                            
&LEN     SETA  L'&OP1                                                           
         AIF   (&LEN GT 256).LENERR                                             
         OC    &OP1LBL.&OP1.(&LEN),&OP1LBL.&OP1                                 
         AGO   .EXIT                                                            
.*                                                                              
.CLIZERO CLI   &OP1LBL.&OP1,0                                                   
         AGO   .EXIT                                                            
.*                                                                              
.TB4     GEN   &OP1                                                             
         AGO   .EXIT                                                            
.*                                                                              
.*                                                                              
.* TYPE C RELATION - THREE TOKENS                                               
.*       COULD BE: 1. (REG,CC,REG)                                              
.*                 2. (REG,CC,SYMBOL)                                           
.*                 3. (SYMBOL,CC,REG)                                           
.*                 4. (SYMBOL,CC,SYMBOL)                                        
.*                 5. (SYMBOL,CC,SELF-DEFINING-TERM)                            
.*                                                                              
.TYPEC  $CCLKUP &OP2                                                            
         AIF   (NOT &$OK).CCERR                                                 
         AIF   ('&$CCTYP' NE 'C' AND '&$CCTYP' NE 'CL').NOTCMP                  
        $RGLKUP &OP1                  LEFT OPERAND A REGISTER?                  
         AIF   (&$OK).TCR                                                       
        $RGLKUP &OP3                  RIGHT OPERAND A REGISTER?                 
         AIF   (&$OK).TCSR             SYMBOL,REG                               
        $CSS   &OP1,&OP3,&$CCTYP                                                
         AGO   .EXIT                                                            
.TCSR    ANOP  ,                       INVERT TO REG-SYMBOL                     
        $CRS   &OP3,&OP1,&$CCTYP                                                
.* THESE STATEMENTS INVERT THE CONDITION CODE IN ORDER TO                       
.* SWITCH THE ORDER OF THE OPERANDS                                             
&V       SETA  &$CCVAL                                                          
         AIF   (&V LE 1 OR &V GE 14 OR &V EQ 8 OR &V EQ 7).EXIT                 
         AIF   (&V EQ 11 OR &V EQ 10 OR &V EQ 3 OR &V EQ 2).ADD2                
     AGO .SUB2 (&V EQ 13 OR &V EQ 12 OR &V EQ 5 OR &V EQ 4)                     
.ADD2    ANOP                                                                   
&$CCVAL  SETA  &$CCVAL+2                                                        
         AGO   .EXIT                                                            
.SUB2    ANOP                                                                   
&$CCVAL  SETA  &$CCVAL-2                                                        
         AGO   .EXIT                                                            
.*                                                                              
.TCRS   $CRS   &OP1,&OP3,&$CCTYP                                                
         AGO   .EXIT                                                            
.*                                                                              
.TCR    $RGLKUP &OP3                                                            
         AIF   (NOT &$OK).TCRS                                                  
*JOIN                                                                           
         &$CCTYP.R &OP1,&OP3                                                    
         AGO   .EXIT                                                            
.*                                                                              
.NOTCMP $BV    &OP1..&OP3,&OP2                                                  
&ERR     SETC  '&OP1..&OP3,&OP2'                                                
         AIF   (NOT &$OK).BVERR                                                 
&$BFLOC  SETC  '&OP1'                                                           
.TFLAG   ANOP                                                                   
&$CCVAL  SETA  &$BFCC                                                           
         AIF   ('&$BFTYP' NE 'TM').CLI                                          
         TM    &$BFLOC,&$BFVAL                                                  
         MEXIT                                                                  
.*                                                                              
.CLI     CLI   &$BFLOC,&$BFVAL                                                  
         MEXIT                                                                  
.*                                                                              
.CCERR   MNOTE 8,'&OP2: INVALID RELATIONAL OPERATOR'                            
         MEXIT                                                                  
.*                                                                              
.BVERR   MNOTE 8,'&ERR IS NOT A FLAG, "FALSE" ASSUMED'                          
&CCVAL   SETA  0                                                                
.EXIT    MEND                                                                   
