         MACRO                                                                  
&L       IDENT &LEVEL,&CMNT,&CODE=YES,&SKIP=NO                                  
.*       MODIFIED 12/78, JDN                                                    
         GBLA  &$COUNT                                                          
         GBLB  &$HIBAL                                                          
         GBLC  &$MOD,&$DATE,&$TIME                                              
         LCLC  &COMM,&PFX,&TEMP,&LAB                                            
         AIF   ('&$DATE' NE '').GOTDATE                                         
&$DATE   SETC  '&SYSDATE'                                                       
&$TIME   SETC  '&SYSTIME'(1,2).':'.'&SYSTIME'(4,2)                              
         AIF   ('&$DATE'(3,1) NE '/').ASMG                                      
&X1      SETC  '&$DATE'(4,2)                                                    
&X2      SETC  '&$DATE'(1,2)                                                    
&I       SETA  &X2*3-2                                                          
&X2      SETC  'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'(&I,3)                     
&X3      SETC  '&$DATE'(7,2)                                                    
         AGO   .DATE                                                            
.ASMG    ANOP                                                                   
&X3      SETC  '&$DATE'(1,2)                                                    
&X2      SETC  '&$DATE'(3,3)                                                    
&X1      SETC  '&$DATE '(6,2)                                                   
         AIF   ('&X1'(2,1) NE ' ').DATE                                         
&X1      SETC  '0'.'&X1'(1,1)                                                   
.DATE    ANOP                                                                   
&$DATE   SETC  '&X1&X2&X3'                                                      
.GOTDATE AIF   ('&CODE' EQ 'NO').GOTIT,('&LEVEL' EQ '').GOTMOD                  
&$MOD    SETC  '&LEVEL'                                                         
         AIF   ('&$MOD'(1,1) NE '.').GOTMOD                                     
&$MOD    SETC  '&$MOD'(2,K'&$MOD-1)                                             
.GOTMOD  ANOP                                                                   
&PFX     SETC  '&L&$MOD'                                                        
         AIF   ('&L' NE '' OR '&LEVEL '(1,1) EQ '.').GOTIT                      
&PFX     SETC  '&SYSECT&$MOD'                                                   
         AIF   ('&$MOD' EQ '').GOTIT                                            
         AIF   (K'&SYSECT LE 4).GOTIT                                           
&PFX     SETC  '&SYSECT'(1,4).'&$MOD'                                           
.GOTIT   AIF   ('&CMNT' EQ '').NOCMNT                                           
         AIF   ('&CMNT'(1,1) NE '''').NOQC                                      
&COMM    SETC  '&CMNT'(2,K'&CMNT-2)                                             
         AGO   .NOCMNT                                                          
.NOQC    ANOP                                                                   
&COMM    SETC  '&CMNT'                                                          
.NOCMNT  AIF   ('&PFX' EQ '').NOPFX                                             
&TEMP    SETC  '(&PFX) '                                                        
.NOPFX   MNOTE *,'TITLE:: &TEMP.(&$TIME) (&$DATE) &COMM'                        
         AIF   ('&CODE' EQ 'NO').EXIT                                           
         AIF   (NOT &$HIBAL).NPUSH                                              
*PUSH,GEN                                                                       
.NPUSH   AIF   ('&CODE' EQ 'SAVE').SAVE,('&SKIP' EQ 'NO').NOBR                  
&TMPLBL  SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
*PUSH,NOGEN                                                                     
         B     &TMPLBL                                                          
*POP                                                                            
.NOBR    AIF   ('&LEVEL' EQ '' OR '&LEVEL '(1,1) EQ '.').NOENT                  
         AIF   ('&PFX' EQ '&SYSECT').NOENT                                      
&LAB     SETC  '&PFX'                                                           
*                                                                               
         ENTRY &LAB                                                             
*                                                                               
.NOENT   ANOP                                                                   
&I       SETA  K'&PFX+2+K'&$DATE+K'&$TIME                                       
&LAB     DC    AL1(&I),C'&PFX,&$DATE,&$TIME'                                    
*                                                                               
         AIF   ('&SKIP' EQ 'NO').MREST                                          
*PUSH,NOGEN                                                                     
&TMPLBL  DS    0H                                                               
*POP                                                                            
         AGO   .MREST                                                           
.SAVE    ANOP                                                                   
&PFX&$MOD SAVE (14,12),,&PFX&$MOD-&$DATE-&$TIME                                 
*                                                                               
.MREST   AIF   (NOT &$HIBAL).EXIT                                               
*POP                                                                            
.EXIT    MEND                                                                   
