         MACRO                                                                  
&L      $CSS   &S1P,&S2P,&C            STORAGE-STORAGE COMPARE                  
.*       MODIFIED 10/78, JDN                                                    
.*       Labeled using support 12/85, JKR                                       
         GBLA  &$CCVAL,&$LPTR,&$LTZLEN                                          
         GBLC  &$LULBL,&$LUADDR                                                 
         LCLA  &LEN,&V,&S1L,&S2L                                                
         LCLB  &S1X,&S2X                                                        
         LCLC  &S1C,&S2C,&S1T,&S2T,&T                                           
         LCLC  &S1LBL,&S2LBL                                                    
&S1C     SETC  '&S1P'                                                           
&S2C     SETC  '&S2P'                                                           
&L       LABEL                                                                  
.*                                                                              
.* DECODE TYPELESS OPERANDS                                                     
.*                                                                              
&S1L     SETA  0                   DEFAULT FOR SDT                              
&T       SETC  '&S1C'(1,1)                                                      
         AIF   ('&T' NE '.').NODOT1                                             
&S1C     SETC  '&S1C'(2,K'&S1C-1)  REMOVE DOT                                   
&S1L     SETA  0                   DOT TERM                                     
&S1T     SETC  'G'                                                              
         AGO   .CHK1                                                            
.NODOT1  AIF   ('&T' NE '''').NOQT1                                             
&S1C     SETC  'C&S1C'             SIMPLE STRING                                
.C1      ANOP                                                                   
&S1T     SETC  'N'                 C'...'                                       
&S1L     SETA  K'&S1C-3                                                         
         AIF   (&S1L NE 2).CHK1                                                 
         AIF  ('&S1C' NE 'C''''''''' AND '&S1C' NE 'C''&&&&''').CHK1            
&S1L     SETA  1                                                                
         AGO   .CHK1                                                            
.NOQT1   AIF   ('&T' NE 'L').NOL1                                               
         AIF   ('&S1C'(1,2) NE 'L''').NOL1                                      
&S1T     SETC  'N'                 L'...                                        
&S1X     SETB  1                   USE LENGTH OF OTHER OPERAND                  
         AGO   .CHK1                                                            
.NOL1    AIF   ('&T' NE 'C').NOC1                                               
         AIF   ('&S1C'(1,2) EQ 'C''').C1                                        
         AGO   .TYP1                                                            
.NOC1    AIF   ('&T' NE 'X').NOX1                                               
         AIF   ('&S1C'(1,2) NE 'X''').NOX1                                      
&S1T     SETC  'N'                                                              
&S1L     SETA  (K'&S1C-2)/2                                                     
         AGO   .CHK1                                                            
.NOX1    AIF   ('&T' NE 'B').NOB1                                               
         AIF   ('&S1C'(1,2) NE 'B''').NOB1                                      
&S1T     SETC  'N'                                                              
&S1L     SETA  (K'&S1C+4)/8                                                     
         AGO   .CHK1                                                            
.NOB1    ANOP                                                                   
&S1X     SETB  1                                                                
      AIF ('&T' NE '@' AND '&S1C'(K'&S1C,1) NE ')').TYP1  @REG/X(REG)           
&S1T     SETC  'G'                                                              
         AGO   .CHK1                                                            
.TYP1    AIF   ('&T' EQ '=').CHK1     LITERAL: TYPE='', LEN=0                   
.*                                                                              
.*  Look for labeled type of storage op                                         
.*                                                                              
        $LBLAD &S1P                                                             
&S1LBL   SETC  '&$LULBL'               Labeled prefix                           
&S1C     SETC  '&$LUADDR'              Address sans label                       
.*                                                                              
&S1T     SETC  T'&S1C                                                           
         AIF   (('&T' GE '0' AND '&T' LE '9') OR ('&T' EQ '-')).CHKN1           
&S1L     SETA  L'&S1C                                                           
         AIF   ('&S1T' NE 'U').CHK1                                             
.CHKN1   ANOP                                                                   
&S1T     SETC  'N'                 (EQUATES ARE T' = U)                         
.CHK1    ANOP                                                                   
.*                                                                              
&S2L     SETA  0                   DEFAULT FOR SDT                              
&T       SETC  '&S2C'(1,1)                                                      
         AIF   ('&T' NE '.').NODOT2                                             
&S2C     SETC  '&S2C'(2,K'&S2C-1)  REMOVE DOT                                   
&S2L     SETA  0                   DOT TERM                                     
&S2T     SETC  'G'                                                              
         AGO   .CHK2                                                            
.NODOT2  AIF   ('&T' NE '''').NOQT2                                             
&S2C     SETC  'C&S2C'             SIMPLE STRING                                
.C2      ANOP                                                                   
&S2T     SETC  'N'                 C'...'                                       
&S2L     SETA  K'&S2C-3                                                         
         AIF   (&S2L NE 2).CHK2                                                 
         AIF  ('&S2C' NE 'C''''''''' AND '&S2C' NE 'C''&&&&''').CHK2            
&S2L     SETA  1                                                                
         AGO   .CHK2                                                            
.NOQT2   AIF   ('&T' NE 'L').NOL2                                               
         AIF   ('&S2C'(1,2) NE 'L''').NOL2                                      
&S2T     SETC  'N'                 L'...                                        
&S2X     SETB  1                   USE LENGTH OF OTHER OPERAND                  
         AGO   .CHK2                                                            
.NOL2    AIF   ('&T' NE 'C').NOC2                                               
         AIF   ('&S2C'(1,2) EQ 'C''').C2                                        
         AGO   .TYP2                                                            
.NOC2    AIF   ('&T' NE 'X').NOX2                                               
         AIF   ('&S2C'(1,2) NE 'X''').NOX2                                      
&S2T     SETC  'N'                                                              
&S2L     SETA  (K'&S2C-2)/2                                                     
         AGO   .CHK2                                                            
.NOX2    AIF   ('&T' NE 'B').NOB2                                               
         AIF   ('&S2C'(1,2) NE 'B''').NOB2                                      
&S2T     SETC  'N'                                                              
&S2L     SETA  (K'&S2C+4)/8                                                     
         AGO   .CHK2                                                            
.NOB2    ANOP                                                                   
&S2X     SETB  1                                                                
      AIF ('&T' NE '@' AND '&S1C'(K'&S1C,1) NE ')').TYP2  @REG/X(REG)           
&S2T     SETC  'G'                                                              
         AGO   .CHK2                                                            
.TYP2    AIF   ('&T' EQ '=').CHK2  LITERAL: TYPE='', LEN=0                      
.*                                                                              
.*  Look for labeled type of storage op                                         
.*                                                                              
        $LBLAD &S2P                                                             
&S2LBL   SETC  '&$LULBL'               Labeled prefix                           
&S2C     SETC  '&$LUADDR'              Address sans label                       
.*                                                                              
&S2T     SETC  T'&S2C                                                           
         AIF   (('&T' GE '0' AND '&T' LE '9') OR ('&T' EQ '-')).CHKN2           
&S2L     SETA  L'&S2C                                                           
         AIF   ('&S2T' NE 'U').CHK2                                             
.CHKN2   ANOP                                                                   
&S2T     SETC  'N'                                                              
.CHK2    ANOP                                                                   
.*                                                                              
.*                                                                              
.* CHECK FOR ONE (AND ONLY ONE) SELF DEFINING TERM.                             
.*                                                                              
         AIF   ('&S1T' NE 'N' AND '&S2T' NE 'N').NOSDT                          
         AIF   ('&S1T' EQ 'N' AND '&S2T' EQ 'N').TWOSDTS                        
         AIF   ('&S1T' NE 'N').S2ISSD                                           
.*                                                                              
.* S1 IS SELF-DEFINING                                                          
         AIF   ((&S1X) AND (&S1L LE 1)).SDTA1                                   
         AIF   (&S1L LE 1).CLIREV                                               
&S1C     SETC  '=&S1C'             CONVERT TO =X'...' OR =C'...'                
         AGO   .CLC                                                             
.SDTA1   AIF   (&S2L LE 1).CLIREV                                               
         AIF   ('&S1C' NE '0').SNZ1                                             
&S1C     SETC  '$$$Z&$LPTR.(&S2L)'                                              
         AIF   (&S2L LE &$LTZLEN).CLC                                           
&$LTZLEN SETA  &S2L                                                             
         AGO   .CLC                                                             
.SNZ1    ANOP                                                                   
&S1C     SETC  '=AL&S2L.(&S1C)'                                                 
         AGO   .CLC                                                             
.*                                                                              
.* S2 IS SELF-DEFINING                                                          
.S2ISSD  AIF   ((&S2X) AND (&S2L LE 1)).SDTA2                                   
         AIF   (&S2L LE 1).CLI                                                  
&S2C     SETC  '=&S2C'             CONVERT TO =X'...' OR =C'...'                
         AGO   .CLCREV                                                          
.SDTA2   AIF   (&S1L LE 1).CLI                                                  
         AIF   ('&S2C' NE '0').SNZ2                                             
&S2C     SETC  '$$$Z&$LPTR.(&S1L)'                                              
         AIF   (&S1L LE &$LTZLEN).CLCREV                                        
&$LTZLEN SETA  &S1L                                                             
         AGO   .CLCREV                                                          
.SNZ2    ANOP                                                                   
&S2C     SETC  '=AL&S1L.(&S2C)'                                                 
         AGO   .CLCREV                                                          
.*                                                                              
.* NORMAL CLI                                                                   
.CLI     CLI   &S1LBL.&S1C,&S2LBL.&S2C                                          
         MEXIT                                                                  
.*                                                                              
.* INVERTED OPERAND CLI                                                         
.CLIREV  ANOP                                                                   
         CLI   &S2LBL.&S2C,&S1LBL.&S1C                                          
.CCREV   ANOP                                                                   
.* THESE STATEMENTS INVERT THE CONDITION CODE...                                
&V       SETA  &$CCVAL                                                          
         AIF   (&V LE 1 OR &V GE 14 OR &V EQ 8 OR &V EQ 7).EXIT                 
         AIF   (&V EQ 11 OR &V EQ 10 OR &V EQ 3 OR &V EQ 2).ADD2                
.*             (&V EQ 13 OR &V EQ 12 OR &V EQ 5 OR &V EQ 4)                     
&$CCVAL  SETA  &$CCVAL-2                                                        
         MEXIT                                                                  
.ADD2    ANOP                                                                   
&$CCVAL  SETA  &$CCVAL+2                                                        
.EXIT    MEXIT                                                                  
.*                                                                              
.CLCREV  CLC   &S2LBL.&S2C,&S1LBL.&S1C                                          
         AGO   .CCREV                                                           
.*                                                                              
.* NO SDTS : CHECK FOR ZERO-LENGTH SYMBOLS                                      
.*                                                                              
.NOSDT   AIF   ('&S1C'(1,1) EQ '=').CLC                                         
         AIF   ('&S2C'(1,1) EQ '=').CLCREV                                      
         AIF   (&S1L NE 0).CLC                                                  
         AIF   (&S2L NE 0).CLCREV                                               
         MNOTE *,'COMPARING TWO ZERO LENGTH SYMBOLS'                            
.S1SDLIT ANOP                                                                   
.CLC     CLC   &S1LBL.&S1C,&S2LBL.&S2C                                          
         MEXIT                                                                  
.TWOSDTS MNOTE 8,'INVALID RELATION - TWO SELF DEFINING TERMS'                   
         MEXIT                                                                  
.BADSDT  MNOTE 8,'INVALID USE OF SELF DEFINING TERMS'                           
         MEND                                                                   
