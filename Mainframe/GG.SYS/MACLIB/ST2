         MACRO                                                                  
&L       ST2   &R,&A                                                            
.********************************************************************           
.* Store a halfword, not aligned.                                               
.*   All this garbage is to scan off an index register /jkr                     
.********************************************************************           
         LCLC  &INDX,&ADDR             Index reg, Base Disp Addr                
         LCLA  &CNT,&ILEN,&ISTRT       CouNTer, Index Len, Index St             
&ADDR    SETC  '&A'                                                             
&INDX    SETC  '0'                                                              
&ILEN    SETA  0                                                                
&ISTRT   SETA  0                                                                
&CNT     SETA  1                                                                
.LPLOOK  AIF   (&CNT GE K'&A).ENDLOOK  Look for left par                        
&CNT     SETA  &CNT+1                                                           
         AIF   ('&A'(&CNT,1) NE '(').LPLOOK                                     
&ISTRT   SETA  &CNT+1                                                           
.IXLOOK  AIF   (&CNT GE K'&A).ENDLOOK  Look for index spec                      
&CNT     SETA  &CNT+1                                                           
         AIF  (('&A'(&CNT,1) NE ',') AND ('&A'(&CNT,1) NE ')')).IXLOOK          
&ILEN    SETA  &CNT-&ISTRT                                                      
.ENDLOOK AIF   (&ISTRT LE 1).DOST      No parens... do store                    
         AIF   (&ILEN LE 0).NILIX      No index, leave it 0                     
&INDX    SETC  '&A'(&ISTRT,&ILEN)      Set index                                
.NILIX   ANOP  ,                                                                
&ADDR    SETC  '&A'(1,&ISTRT-1)        Set start of address field               
.*                                     Check base reg field                     
&BASCHK  SETC  '&A'(&ISTRT+&ILEN,K'&A-(&ISTRT+&ILEN-1))                         
         AIF   ('&BASCHK'(1,1) NE ',').NOCOM    Take out IX comma               
&BASCHK  SETC  '&BASCHK'(2,K'&BASCHK-1)                                         
.NOCOM   ANOP  ,                                                                
         AIF   ('&BASCHK' EQ ')').NILBAS                                        
&ADDR    SETC  '&ADDR&BASCHK'          Set the rest of BASE,DISP fld            
         AGO   .DOST                                                            
.NILBAS  ANOP  ,                                                                
&ADDR    SETC  '&ADDR'(1,K'&ADDR-1)    If no BASE in paren, remove              
.DOST    ANOP  ,                                                                
.*       MNOTE *,'INDEX = &INDX'                                                
.*       MNOTE *,'ADDR  = &ADDR'                                                
&L       DC    0H'0',X'40',AL.4(&R),AL.4(&INDX),S(&ADDR)                        
         MEND                                                                   
