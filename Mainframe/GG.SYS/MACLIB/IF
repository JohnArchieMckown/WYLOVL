         MACRO                                                                  
&L       IF    &LEXP,&LABEL,&XLABEL,&INDX=,&SLAB=,&BLAB=                        
.*       RE-DONE 2/77, JDN;  MODIFIED 6/81, JDN                                 
         GBLA  &$COUNT,&$BPTR,&$CCVAL                                           
         GBLC  &$BSTK(0),&$BLABSTK(0)                                           
&L       LABEL                                                                  
         AIF   (N'&SYSLIST GT 3).ERROP                                          
&TNDX    SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
         AIF   ('&INDX' EQ '').NOINDX                                           
&TNDX    SETC  '&INDX'                                                          
.NOINDX  ANOP                                                                   
&LAB     SETC  '&L'                                                             
         AIF   ('&BLAB' EQ '').NOBLAB                                           
&LAB     SETC  '&BLAB'                                                          
.NOBLAB  ANOP                                                                   
&TRUE    SETC  '&TNDX.T'           TRUE BRANCH FOR "OR"                         
&FALSE   SETC  '&TNDX.F'           FALSE BRANCH FOR "AND"                       
         AIF   ('&LABEL' EQ 'NEXT').NEXT,('&LABEL' NE 'EXIT').BEGCHK            
&TRUE    SETC  'F'                 (EXIT BRANCH LABEL)                          
         AGO   .GETBLAB                                                         
.NEXT    ANOP                                                                   
&TRUE    SETC  'S'                 (NEXT BRANCH LABEL)                          
.GETBLAB ANOP                                                                   
&I       SETA  &$BPTR                                                           
         AIF   ('&XLABEL' EQ '').GOTBLAB                                        
.LLOOP   AIF   (&I LE 0).LABERR                                                 
         AIF   ('&XLABEL' EQ '&$BLABSTK(&I)').GOTBLAB                           
&I       SETA  &I-1                                                             
         AGO   .LLOOP                                                           
.GOTBLAB ANOP                                                                   
&TRUE    SETC  '&$BSTK(&I)'(2,7).'&TRUE'                                        
         AGO   .NL                                                              
.*                                                                              
.BEGCHK  AIF   ('&XLABEL' NE '').ERROP,('&LABEL' EQ '').ERRLAB                  
         AIF   ('&LABEL' EQ 'BEGIN' OR '&LABEL'(1,1) EQ '''').NL                
.*  IF NOT ONE OF THE ABOVE RESERVED WORDS, IT IS A TRUE BRANCH LABEL.          
&TRUE    SETC  '&LABEL'                                                         
.NL      ANOP                                                                   
&SLAB   $LEXP  &LEXP,&TRUE,&FALSE                                               
         AIF   ('&LABEL' EQ 'BEGIN' OR '&LABEL'(1,1) EQ '''').NL2               
        $BC    &$CCVAL,&TRUE                                                    
&FALSE   BEGIN LAB=X&TNDX,BLOCK=NO  DUMMY BLOCK TO FORCE AND'S OUT              
         END                                                                    
         MEXIT                                                                  
.NL2     ANOP                                                                   
&TNDX.B  BC    15-&$CCVAL,&FALSE                                                
&TRUE    BEGIN LAB=I&TNDX,BLAB=&LAB                                             
         AIF   ('&LABEL' EQ 'BEGIN').EXIT                                       
         GEN   &LABEL                                                           
         END                                                                    
.EXIT    MEXIT                                                                  
.*                                                                              
.ERROP   ANOP                                                                   
&I       SETA  N'&SYSLIST                                                       
         MNOTE 8,'WRONG NO. OF OPERANDS FOR IF (NOP=&I)'                        
         MEXIT                                                                  
.*                                                                              
.ERRLAB  MNOTE 8,'SECOND OPERAND FOR IF IS MISSING'                             
         MEXIT                                                                  
.*                                                                              
.LABERR  MNOTE 8,'BLOCK &XLABEL NOT FOUND FOR "&LABEL"'                         
         MEND                                                                   
