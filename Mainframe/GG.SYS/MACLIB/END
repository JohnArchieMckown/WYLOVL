         MACRO                                                                  
&L       END   &OP,&OP1,&OP2,&TYPE=                                             
.*       RE-DONE 2/77, JDN;  MODIFIED 5/81, JDN                                 
.*       Labeled using support, 11/85, JKR                                      
         GBLC  &$BSTK(0),&$BLABSTK(0),&$USTK(0)                                 
         GBLA  &$BPTR,&$BLPTR,&$NEST,&$UR#STK(0),&$UPTR                         
         LCLC  &LBA,&LBB,&LBC,&LBS,&ULABEL                                      
         AIF   ('&OP' EQ '.').ENDALL                                            
&L       LABEL                                                                  
.EL      AIF   (&$BPTR LE 0).ERRLAB                                             
&PFX     SETC  '&$BSTK(&$BPTR)'(1,1)                                            
         AIF   ('&PFX' EQ 'P' AND '&TYPE' NE 'P').ERRENDP                       
&INDX    SETC  '&$BSTK(&$BPTR)'(2,K'&$BSTK(&$BPTR)-1)                           
&LABEL   SETC  '&$BLABSTK(&$BPTR)'                                              
&$BLPTR  SETA  &$BPTR                                                           
.ULOOP   ANOP                                                                   
.ALOOP   ANOP                                                                   
&$BPTR   SETA  &$BPTR-1                                                         
         AIF   (&$BPTR LE 0).BLZERO                                             
         AIF   ('&$BSTK(&$BPTR)'(1,1) NE 'U').NOTUSNG                           
&ULABEL  SETC  '&$BSTK(&$BPTR)'                                                 
         AGO   .WITH                                                            
.NOTUSNG AIF   ('&$BSTK(&$BPTR)'(1,1) EQ 'A').ALOOP                             
         AIF   ('&$BSTK(&$BPTR)'(1,1) EQ 'T').ALOOP                             
.BLZERO  AIF   ('&PFX' EQ 'X').DONE,('&PFX' EQ 'P').CODE                        
         AIF   ('&TYPE' NE 'P').BLDOWN                                          
         MNOTE 4,'INCOMPLETE BLOCK ENDED BY PEND (TYPE=&PFX)'                   
.BLDOWN  MNOTE *,'<&$NEST'                                                      
&$NEST   SETA  &$NEST-1                                                         
         AIF   ('&PFX' EQ 'Z').DONE    Dummy like 'X' but unnests               
         AIF   ('&PFX' EQ 'L').LOOP                                             
         AIF   ('&PFX' NE 'W').NOTWITH                                          
&ULABEL  SETC  '&$BSTK(&$BLPTR)'                                                
         AGO   .WITH                                                            
.NOTWITH AIF   ('&PFX' EQ 'I' OR '&PFX' EQ 'C').CODE                            
         AIF   ('&PFX' EQ 'D').DATA,('&PFX' EQ 'R').RECORD                      
         MNOTE 8,'HIBAL STRUCTURE ERROR (BLOCK TYPE &PFX UNKNOWN)'              
         MEXIT                                                                  
.*                                                                              
.LOOP    B     &INDX.S             BRANCH TO LOOP START                         
.CODE    ANOP                                                                   
&INDX.F  DS    0H                                                               
         AGO   .DONE                                                            
.*                                                                              
.WITH    AIF   (K'&ULABEL EQ 1).WITH2,('&ULABEL'(2,1) EQ 'L').LBLWTH            
.WITH2   AIF   (&$UR#STK(&$UPTR) LT 0).WITHL                                    
         USING &$USTK(&$UPTR),&$UR#STK(&$UPTR)                                  
&$UPTR   SETA  &$UPTR-1                                                         
         AGO   .WITH2                                                           
.WITHL   ANOP                                                                   
&I       SETA  0-&$UR#STK(&$UPTR)                                               
         USING &$USTK(&$UPTR),&I                                                
&$UPTR   SETA  &$UPTR-1                                                         
         AIF   (&$BPTR LE 0).DONE                                               
         AIF   ('&$BSTK(&$BPTR)'(1,1) EQ 'U').ULOOP                             
         AGO   .DONE                                                            
.*                                                                              
.*  End of LABELED WITH block                                                   
.*                                                                              
.LBLWTH  ANOP  ,                                                                
&ULABEL  SETC  '&ULABEL'(4,K'&ULABEL-3)   using label                           
&LBA     SETC  '$$U&ULABEL.A'                                                   
&LBS     SETC  '$$U&ULABEL.S'                                                   
&LBC     SETC  '$$U&ULABEL.C'                                                   
         GBLA  &(&LBA)(0),&(&LBS)                                               
         GBLC  &(&LBC)(0)                                                       
.LBLWTH2 ANOP  ,                                                                
         AIF   ('&(&LBC)(&(&LBS)-1)' NE '').DOLWITH                             
.*                                                                              
.*  None to pop, so do drop                                                     
.*                                                                              
         DROP  &ULABEL                                                          
         AGO   .DOLLOOP                                                         
.DOLWITH ANOP  ,                                                                
&(&LBS)  SETA  &(&LBS)-1                                                        
         AIF   (&(&LBA)(&(&LBS)+1) LT 0).LWITHL                                 
&ULABEL  USING &(&LBC)(&(&LBS)),&(&LBA)(&(&LBS))                                
         AGO   .LBLWTH2                                                         
.LWITHL  ANOP                                                                   
&I       SETA  0-&(&LBA)(&(&LBS))                                               
&ULABEL  USING &(&LBC)(&(&LBS)),&I                                              
.DOLLOOP AIF   (&$BPTR LE 0).DONE                                               
         AIF   ('&$BSTK(&$BPTR)'(1,1) EQ 'U').ULOOP                             
         AGO   .DONE                                                            
.*                                                                              
.RECORD  ANOP                                                                   
*POP                                                                            
*PUSH,NOGEN                                                                     
         ORG                                                                    
         AGO   .DR                                                              
.DATA    ANOP                                                                   
*PUSH,NOGEN                                                                     
.DR      AIF   ('&LABEL' EQ '').DATANL                                          
&TEMP    SETC  '$$$&PFX&LABEL'                                                  
&TEMP    SETC  T'&TEMP                                                          
&LABEL   EQU   $$$&PFX&LABEL,*-$$$&PFX&LABEL,C'&TEMP'                           
.DATANL  ANOP                                                                   
&INDX    LOCTR                                                                  
*POP                                                                            
.*                                                                              
.DONE    AIF   ('&OP' EQ 'ELSE' OR '&OP' EQ 'ELSEIF').MACRO                     
         AIF   ('&TYPE' NE '' AND '&TYPE' NE '&PFX').EL                         
         AIF   ('&OP' NE '' AND '&OP' NE '&LABEL').EL                           
         MEXIT                                                                  
.*                                                                              
.MACRO   &OP   &OP1,&OP2                                                        
         MEXIT                                                                  
.*                                                                              
.ENDALL  LTORG                                                                  
         AIF   ('&L' NE '').ENDAL                                               
       $$END                                                                    
         MEXIT                                                                  
.*                                                                              
.ENDAL $$END   &L                                                               
         MEXIT                                                                  
.*                                                                              
.ERRLAB  AIF   ('&TYPE&OP' EQ '').ERREND                                        
&MSG     SETC  'TYPE=&TYPE "&OP"'                                               
         AIF   ('&TYPE' NE '' AND '&OP' NE '').ERRLAB1                          
&MSG     SETC  'TYPE=&TYPE'                                                     
         AIF   ('&OP' EQ '').ERRLAB1                                            
&MSG     SETC  '"&OP"'                                                          
.ERRLAB1 MNOTE 8,'BLOCK &MSG NOT FOUND--ALL BLOCKS ENDED'                       
         MEXIT                                                                  
.*                                                                              
.ERREND  MNOTE 8,'BAD HIBAL END STATEMENT (NOT CURRENTLY IN A BLOCK)'           
         MEXIT                                                                  
.*                                                                              
.ERRENDP MNOTE 8,'BAD HIBAL END STATEMENT (CURRENTLY IN PROC BLOCK)'            
.EXIT    MEND                                                                   
