         MACRO                                                                  
&L      $CRS   &R,&SP,&C               REGISTER-STORAGE COMPARE                 
.*       MODIFIED 5/77, JDN                                                     
.*       Labeled using support 12/85, JKR                                       
         GBLC  &$LULBL,&$LUADDR                                                 
         LCLC  &SLBL,&S                                                         
&S       SETC  '&SP'                                                            
&L       LABEL                                                                  
         AIF   ('&S'(1,1) NE '=').NONLIT                                        
&TMP     SETC  '&S'(2,2)                                                        
         AIF   ('&TMP' EQ 'A(' OR '&TMP' EQ 'F''' OR '&TMP' EQ 'V(').CF         
         AIF   ('&TMP' EQ 'Y(' OR '&TMP' EQ 'H''').CH                           
         AIF   ('&TMP' EQ 'X''').HEX                                            
         MNOTE 8,'&S - LITERAL NOT SUPPORTED'                                   
         MEXIT                                                                  
.HEX     ANOP                                                                   
&LEN     SETA  (K'&S-2)/2                                                       
         AGO   .DOLEN                                                           
.NONLIT  AIF   ('&S'(1,1) EQ '-').SDFT                                          
.*  Look for label prefix                                                       
        $LBLAD &SP                                                              
&SLBL    SETC  '&$LULBL'               Labeled prefix                           
&S       SETC  '&$LUADDR'              Address sans label                       
         AIF   (T'&S EQ '@').CF                                                 
         AIF   (T'&S EQ 'N' OR T'&S EQ 'U' OR '&S '(1,2) EQ 'L''').SDFT         
&LEN     SETA  L'&S                                                             
.DOLEN   AGO   (&LEN).C1,.CH,.C3,.CF                                            
         MNOTE 8,'&S HAS LENGTH &LEN - INVALID, LENGTH=4 ASSUMED'               
.CF      &C    &R,&SLBL.&S                                                      
         MEXIT                                                                  
.C1      CLM   &R,B'0001',&SLBL.&S                                              
         MEXIT                                                                  
.CH      AIF   ('&C' EQ 'CL').C2                                                
         CH    &R,&SLBL.&S                                                      
         MEXIT                                                                  
.C2      CLM   &R,B'0011',&SLBL.&S                                              
         MEXIT                                                                  
.C3      CLM   &R,B'0111',&SLBL.&S                                              
         MEXIT                                                                  
.SDFT    &C    &R,=A(&S)                                                        
         MEND                                                                   
