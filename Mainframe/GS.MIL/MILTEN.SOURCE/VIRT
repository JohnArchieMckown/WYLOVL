VIRT     TITLE 'MILTEN''s Virtual Terminal Interface'                           
*******************************************************************             
*                                                                 *             
*     MILTEN/370 - Release VI - Class I - Stanford Proprietary    *             
*                                                                 *             
*******************************************************************             
         HIBAL                                                                  
*                                                                               
         SYSDEFN                                                                
*                                                                               
MILVIRT  CSECT                                                                  
VIRT     IDENT 2047                10:16:25 02/16/02  (SLP)                     
         REGS  ,,FSR,,,,,,BR,SCBR,PIBR,(FEBR,LSR),CVR,SPR,RAR                   
*                                                                               
         PUSH  DSECTS                                                           
         TITLE 'DSECTS'                                                         
CVDSECT  DSECT                                                                  
         COPY  CV                                                               
         EJECT                                                                  
SCB      RECORD BEGIN                                                           
         COPY  SCB                                                              
         END                                                                    
         EJECT                                                                  
PDB      RECORD 'PDB'                                                           
         EJECT                                                                  
         COPY  FDB                                                              
         EJECT                                                                  
SCANCB   RECORD 'SCANCB'                                                        
         EJECT                                                                  
SEGCB    RECORD 'SEGCB'                                                         
         EJECT                                                                  
PATHD    RECORD BEGIN                                                           
         PATH  VERSION=2                                                        
         END                                                                    
         EJECT                                                                  
         CVT   DSECT=YES                                                        
*                                                                               
         POP   DSECTS                                                           
         EJECT                                                                  
         COPY  SUBPOOLS                                                         
         EJECT                                                                  
*box                                                                            
*                                                                               
*  RQST -- Front-end Request Block                                              
*                                                                               
RQST     RECORD 'RQST'                                                          
         EJECT                                                                  
FEB      RECORD BEGIN                                                           
         COPY  FEB                                                              
*****  device dependent line                                                    
FEBFLAG  FLAG                                                                   
         FLAG  FEBFINIT            - Connection established                     
         FLAG  FEBFEXT             - Extended mode (sends EOS)                  
*                                                                               
FEBASCBP DS    A                   Partner's ASCB ptr                           
FEBTAG   DS    CL8                 Path "tag" name                              
FEBACCT  DS    CL5                 Account (uuugg)                              
*                                                                               
FEBINMX  DS    H                   Maximum input length                         
FEBOUMX  DS    H                   Maximum output length                        
*                                                                               
FEBPATHX PATH  PFX=FEBP,ECB=0,FLAG=FEBSFLAG,MYTAG=*,YOURTAG=*,PASS=*,  X        
               VERSION=2                                                        
FEBPATH  EQU   FEBPATHX,*-FEBPATHX,C'X'                                         
*                                                                               
FEBSFLAG DS    X                   Suzan post flag byte                         
*                                                                               
FEBPNO   DS    H                   Current no. of active ports                  
*                                                                               
FEBNICLK DS    D                   Clock at last new-info post                  
         END                                                                    
         EJECT                                                                  
PIB      RECORD BEGIN                                                           
         COPY  PIB                                                              
*****  device dependent line                                                    
         END                                                                    
         EJECT                                                                  
VTRC#    EQU   25                  No. of trace entries                         
VTRCP#   EQU   5                   No. of PIBtrc entries                        
         SPACE 2                                                                
         COPY  VIRTTRC                                                          
         TITLE 'Virtual Terminal Interface'                                     
*box                                                                            
*                                                                               
*  VIRTDEF -- Routine to process virtual FEB definition.                        
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - ok; R1 - new FEB ptr                                                 
*                                                                               
VIRTDEF  XPROC                                                                  
         LA    R0,L'FEB                                                         
         VCALL GETFEB                                                           
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
         MVI   FEBTYPE,FEBTVIRT                                                 
         MVC   FEBNAME,=CL8'VIRTUAL'                                            
         SET   FEBFPS                                                           
*                                                                               
         LA    R1,FEB                                                           
         CLEAR R15                                                              
         PEND                                                                   
         SPACE 2                                                                
*box                                                                            
*                                                                               
*  VIRTPORT -- Dummy routine.                                                   
*                                                                               
VIRTPORT XPROC                                                                  
         FAIL  'VIRTPORT ERROR'                                                 
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTFIN -- Routine to finish building virtual front-end.                     
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*                                                                               
VIRTFIN  XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
         FAIL  (FEB,NE,'FEB'),'BAD FEB PTR'                                     
         MVC   FEBPATH,MODLPATH                                                 
         IF    CVFTEST,'MVI FEBPPASS,C"T"'                                      
         LA    R1,CVECB                                                         
         ST    R1,FEBPECB          Save ecb ptr                                 
         LA    R1,FEBSFLAG                                                      
         ST    R1,FEBPECBF         Save flag ptr                                
         LA    R1,FEBPATH                                                       
         ST    R1,CVWORK                                                        
         OI    CVWORK,X'80'                                                     
         SCOM  OPEN,CVWORK        Open                                          
         FAIL  (R15,NZ),'VIRTUAL PATH/SUZAN ERROR'                              
*                                                                               
         SET   FEBFOPEN                                                         
         PEND                                                                   
         EJECT                                                                  
MODLPATH PATH  PFX=MODL,MYTAG=MILTEN,PASS=PVFE,                        *        
               BUFSIZE=CVBUFLEN,ECB=*-*,FLAG=*-*,VERSION=2                      
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTXCLS -- Special routine to mark the front-end as closing                 
*    if it needs full service.                                                  
*                                                                               
*    This routine is called by SIFT when ORVYL or WYLBUR dies                   
*    so that the path will close.                                               
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*                                                                               
VIRTXCLS XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
*                                                                               
         FAIL  (FEB,NE,'FEB'),'Bad FEB ptr.'                                    
*-                                                                              
*-       Only terminate this front-end if it needs full service.                
*-                                                                              
         IF    ((FEBTAG,EQ,'VAM'),OR,(FEBTAG,EQ,'VFE')),BEGIN                   
         SET   FEBSFLAG.FEBPFCLS,MODE=LOCKED,REF=FEB                            
         END                                                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTCLS -- Routine to close virtual front-end.                               
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*                                                                               
VIRTCLS  PROC                                                                   
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
*                                                                               
         FAIL  (FEB,NE,'FEB'),'BAD FEB PTR'                                     
*                                                                               
         IF    (FEBPMGC,NZ),BEGIN                                               
         SCOM  CLOSE,0,FEBPMGC     Close path                                   
         CLEAR FEBPMGC                                                          
         END                                                                    
*                                                                               
         CLEAR FEBSFLAG                                                         
*                                                                               
         LT    R7,FEBPATP                                                       
         IF    NZ,BEGIN            Zot current sessions...                      
         CLEAR R6                                                               
         WHILE (R6,LT,FEBPMAX),BEGIN                                            
         LR    R5,R6                                                            
         SLL   R5,2                Times 4 for PAT offset                       
         LA    R6,@R6+1                                                         
         LT    PIBR,@R7(R5)                                                     
         IF    Z,NEXT              Go get next PIB ptr                          
         WITH  PIB                                                              
         IF    ('LT R1,PIBSCBP',NZ),'VCALL DISCSESS'  Disconnect                
         LR    R1,PIBR                                                          
         VCALL FREEPIB             Release port                                 
         CLEAR PIBR                                                             
*                                                                               
         DECR  R15,FEBPNO                                                       
         END                                                                    
         END                                                                    
*                                                                               
         IF    (FEBSCLCK,NZ),'DECR R15,CVMCRVIR'  NO. OF ACTIVE VFE'S           
*                                                                               
         LA    R1,FEB                                                           
         VCALL FREEFEB             Release front-end                            
*                                                                               
         VCALL STOPCHK             Shutdown now if stop pending                 
*                                                                               
         CLEAR R15                                                              
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTCNTL -- Routine to perform virtual control operation.                    
*                                                                               
*    On entry:                                                                  
*      R0 - control number                                                      
*      R1 - FEB ptr                                                             
*      R15- PDB ptr                                                             
*                                                                               
*    On exit, R15 (and CC):                                                     
*      -8 - bad control number                                                  
*      -4 - bad format                                                          
*       0 - ok (PDB has return data)                                            
*      12 - error                                                               
*                                                                               
VIRTCNTL XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
         FAIL  (FEB,NE,'FEB'),'BAD FEB PTR'                                     
         LR    R7,R15                                                           
         WITH  (PDB,R7)                                                         
*                                                                               
         LA    R6,PDBTEXT                                                       
         WITH  (FDB,R6)                                                         
*                                                                               
         LR    RAR,R0                                                           
         IF    (RAR,GE,100),BEGIN  priv'd control...                            
         IF    (RAR,GT,VCTLPMAX),'LA RAR,100'                                   
         SH    RAR,=Y(100)                                                      
         SLL   RAR,2                                                            
         L     RAR,VCTLPTBL(RAR)   Processing routine                           
         BR    RAR                                                              
         END                                                                    
*                                                                               
         IF    (RAR,LGT,VCTLMAX),'CLEAR RAR'                                    
         SLL   RAR,2                                                            
         L     RAR,VCTLTBL(RAR)    Processing routine                           
         BR    RAR                                                              
         EJECT                                                                  
VCTLTBL  DC    A(VCTL0)                                                         
         DC    A(VCTL1)            1:   sense virtual front-end info            
         DC    A(VCTL2)            2:   sense virtual port info                 
VCTLMAX  EQU   (*-VCTLTBL-4)/4,,C'N'                                            
*                                                                               
         DS    0S(VCTLMAX-FDBCMAX,FDBCMAX-VCTLMAX)                              
         SPACE 2                                                                
VCTLPTBL DC    A(VCTL0)                                                         
         DC    A(VCTL101)          101: set virtual front-end                   
         DC    A(VCTL102)          102: set virtual port                        
VCTLPMAX EQU   (*-VCTLPTBL-4)/4+100,,C'N'                                       
*                                                                               
         DS    0S(VCTLPMAX-FDBPMAX,FDBPMAX-VCTLPMAX)                            
         SPACE 4                                                                
*****  Control 0:   Bad control number                                          
         SPACE 2                                                                
VCTL0    LH    R15,=H'-8'                                                       
         EXIT                                                                   
*                                                                               
VCTLBF   LH    R15,=H'-4'                                                       
         EXIT                                                                   
*                                                                               
VCTLRC12 LA    R15,12                                                           
         EXIT                                                                   
*                                                                               
VCTLOK   CLEAR R15                                                              
         EXIT                                                                   
         EJECT                                                                  
*****  Control 1:   Sense virtual front-end info                                
         SPACE 2                                                                
VCTL1    IF    (PDBWC,NE,L'FDBT1),VCTLBF  bad parms                             
         CLEAR FDBF1                                                            
         MVC   FDBFFTNO,=Y(VTRC#)  No. of entries                               
         MVC   FDBFFLEN,=Y(L'VTRC)  size of an entry                            
         LA    R2,FDBFFTRC                                                      
         LM    R3,R5,FEBTRCP+4     R3=next,r4=first,r5=end                      
         LOOP  BEGIN                                                            
         S     R3,FEBTRCP          Back up                                      
         IF    (R3,LT,R4),'LR R3,R5; S R3,FEBTRCP'  last                        
         MVC   @R2(L'VTRC),@R3     Copy trace entry                             
         LA    R2,@R2+L'VTRC       Next entry                                   
         UNTIL ('C R3,FEBTRCP+4',EQ),END                                        
         MVC   PDBLENG,=Y(L'PDBHDR+L'FDBF1+VTRC#*L'VTRC)                        
         B     VCTLOK                                                           
         EJECT                                                                  
*****  Control 2:   Sense virtual port info                                     
         SPACE 2                                                                
VCTL2    IF    (PDBWC,NE,L'FDBT2),VCTLBF  bad parms                             
         LH    R15,FDBT2P          Port no.                                     
         IF    (R15,LGE,FEBPMAX),VCTLRC12  out of bounds                        
         SLL   R15,2                                                            
         L     PIBR,FEBPATP                                                     
         LT    PIBR,@PIBR(R15)     PIB ptr                                      
         BZ    VCTLRC12                                                         
         CLEAR FDBF2                                                            
         WITH  PIB,BEGIN                                                        
         MVC   FDBFPTNO,=Y(VTRCP#)  no. of trace entries                        
         MVC   FDBFPLEN,=Y(L'VTRCP)  length of a trace entry                    
*                                                                               
         LA    R2,FDBFPTRC                                                      
         LM    R3,R5,PIBTRCP+4     R3=next,r4=first,r5=end                      
         LOOP  BEGIN                                                            
         S     R3,PIBTRCP          Back up                                      
         IF    (R3,LT,R4),'LR R3,R5; S R3,PIBTRCP'  last                        
         MVC   @R2(L'VTRCP),@R3    Copy trace entry                             
         LA    R2,@R2+L'VTRCP      Next entry                                   
         UNTIL ('C R3,PIBTRCP+4',EQ),END                                        
         END                                                                    
         MVC   PDBLENG,=Y(L'PDBHDR+L'FDBF2+VTRCP#*L'VTRCP)                      
         B     VCTLOK                                                           
         EJECT                                                                  
*****  Control 101: Set virtual front-end                                       
         SPACE 2                                                                
VCTL101  IF    (PDBWC,NE,L'FDBT101),VCTLBF  bad parms                           
         IF    FDBTFOPN,BEGIN      Open...                                      
* temp..  what to do?????                                                       
         END                                                                    
*                                                                               
         IF    FDBTFCLS,BEGIN      Close...                                     
         LA    R1,FEB                                                           
         ACALL VIRTCLS                                                          
*                                                                               
         CLEAR PDBHDR              (virtcls may have used this area)            
         MVC   PDBLENG,=Y(L'PDBHDR)                                             
         MVI   PDBCMD,MILCNTL                                                   
         B     VCTLOK                                                           
         END                                                                    
*                                                                               
         B     VCTLBF              Bad command code                             
         EJECT                                                                  
*****  Control 102: Set virtual port                                            
         SPACE 2                                                                
VCTL102  IF    (PDBWC,NE,L'FDBT102),VCTLBF  bad parms                           
         B     VCTLBF              Temporary                                    
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTUPD -- Routine to check SCB updates.                                     
*                                                                               
*    On entry:                                                                  
*      R1 - SCB ptr                                                             
*                                                                               
VIRTUPD  XPROC                                                                  
         LR    SCBR,R1                                                          
         WITH  SCB                                                              
         L     PIBR,SCBPIBP                                                     
         WITH  PIB                                                              
         L     FEBR,PIBFEBP                                                     
         WITH  FEB                                                              
*                                                                               
         SET   SCBFNTIM            Insist on notime                             
         CLEAR SCBHEIP             Ignore page/pause                            
*                                                                               
         IF    (FEBINMX,NZ),'CEIL SCBINMX,FEBINMX'                              
         IF    (FEBOUMX,NZ),'CEIL SCBOUMX,FEBOUMX'                              
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTDO -- Routine to do virtual terminal I/O.                                
*                                                                               
*    On entry:                                                                  
*      R1 - SCB ptr                                                             
*      R15- PDB ptr                                                             
*                                                                               
VIRTDO   XPROC                                                                  
         LR    SCBR,R1                                                          
         WITH  SCB                                                              
         L     PIBR,SCBPIBP                                                     
         WITH  PIB                                                              
         L     FEBR,PIBFEBP                                                     
         WITH  FEB                                                              
         LR    R7,R15                                                           
         WITH  (PDB,R7)                                                         
*                                                                               
         XTRACE FEBTRCP,VIRTDO                                                  
         WITH  (VTRC,R15),BEGIN                                                 
         MVC   VTRCPNO,PIBNO                                                    
         MVC   VTRCSID,SCBID                                                    
         MVC   VTRCSACC,SCBACCT                                                 
         MVC   VTRCCMD,PDBCMD                                                   
         MVC   VTRCTLEN,PDBLENG                                                 
         MVC   VTRCTEXT,PDBTEXT                                                 
         END                                                                    
         EJECT                                                                  
         IF    (PDBCMD,EQ,TMILEOS),BEGIN  end of session...                     
         FAIL  (SCBR,NE,PIBSCBP),'BAD PIB SCB PTR'                              
         LR    R1,SCBR                                                          
         CLEAR SCBR,PIBSCBP                                                     
         VCALL FREESCB                                                          
         LH    R2,PIBNO                                                         
         SLL   R2,2                                                             
         A     R2,FEBPATP                                                       
         FAIL  ('C PIBR,@R2',NE),'BAD PAT PIB PTR'                              
         IF    (PIBTRCP,NZ),'LA R15,PIBTRCP; VCALL FREETRC'                     
         CLEAR (@R2,4)             Zero pib ptr in pat                          
         LR    R1,PIBR                                                          
         CLEAR PIBR                                                             
         VCALL FREEPIB                                                          
         DECR  R15,FEBPNO                                                       
*-                                                                              
*-   Last terminal has logged off; now send a global normal shutdown            
*-       request (if extended mode) and then close the path.                    
*-                                                                              
         IF    (FEBPNO,Z),BEGIN    No more terminals...                         
         IF    FEBFEXT,BEGIN       Extended mode...                             
         L     R6,CVWKBUFP                                                      
         WITH  (RQST,R6)                                                        
         CLEAR RQSTHDR                                                          
         MVC   RQSTLEN,=AL2(L'RQSTHDR)                                          
         MVC   RQSTPNO,=H'-1'      Set port number of -1 for Global             
         MVI   RQSTCMD,FMILSTOP    Global stop request                          
*                                                                               
         LA    R1,FEBPATH                                                       
         LA    R15,RQST                                                         
         ACALL PATHSEND            Send global end of session                   
         END                                                                    
*                                                                               
         LR    R1,FEBR                                                          
         ACALL VIRTCLS             Close virtual path                           
         END                                                                    
*                                                                               
         EXIT  VIRTDO                                                           
         END                                                                    
*                                                                               
*                                                                               
         IF    ((PDBCMD,EQ,TMILWRIT),OR,(PDBCMD,EQ,TMILREAD)),BEGIN             
         INCR  R15,PIBNIO          Kick port i/o count                          
         END                                                                    
*                                                                               
         L     R6,CVWKBUFP                                                      
         WITH  (RQST,R6)                                                        
         CLEAR RQSTHDR                                                          
*                                                                               
         MVC   RQSTPNO,PIBNO       Port number                                  
         MVC   RQSTCMD,PDBCMD      Command code                                 
         MVC   RQSTMODB(2),PDBMODB  flags                                       
         MVC   RQSTINCT,PDBINCT    Input count                                  
         LH    R2,PDBOUTCT                                                      
         STH   R2,RQSTOUCT                                                      
         LA    R14,RQSTTEXT                                                     
         IF    (RQSTCMD,EQ,TMILREAD),BEGIN  counts (compatability)...           
         STH   R2,@R14             Write count                                  
         CLEAR (@R14+2,2)          Prompt count                                 
         LA    R15,@R2+4           Outct includes other counts                  
         STH   R15,RQSTOUCT                                                     
         LA    R14,@R14+4                                                       
         END                                                                    
         MOVEL (R14),PDBTEXT,(R2)                                               
         LA    R15,L'RQSTHDR                                                    
         AH    R15,RQSTOUCT                                                     
         STH   R15,RQSTLEN         Total length                                 
*                                                                               
         XTRACE FEBTRCP,RQSTTO                                                  
         WITH  (VTRC,R15),BEGIN                                                 
         MVC   VTRCPNO,PIBNO                                                    
         MVC   VTRCRQST,RQST                                                    
         END                                                                    
*                                                                               
         XTRACE PIBTRCP                                                         
         WITH  (VTRCP,R15),BEGIN                                                
         MVI   VTRCPTYP,C'T'       To virtual front-end                         
         MVC   VTRCPRQS,RQST                                                    
         END                                                                    
*                                                                               
         LA    R1,FEBPATH                                                       
         LA    R15,RQST            Send to virtual front-end                    
         ACALL PATHSEND                                                         
         IF    NZ,BEGIN            Path closing...                              
         MVC   PDBLENG,=Y(L'PDBHDR)                                             
         CLEAR PDBOPT                                                           
         MVI   PDBCMD,FMILASYN     Asynchronous                                 
         IF    SCBTFIO,'MVC PDBCMD,SCBCMD'                                      
         SET   PDBCPLB.CPLBLGF     Logoff pending                               
         LA    R1,SCB                                                           
         LA    R15,PDB                                                          
         VCALL SUBSEND             Tell subsystem                               
         END                                                                    
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTPOST -- Routine to process posts from virtual front-end.                 
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*                                                                               
VIRTPOST XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
*                                                                               
         IF    FEBSFLAG.FEBPFOPN,BEGIN  path opened...                          
         CLEAR FEBSFLAG.FEBPFOPN,MODE=LOCKED,REF=FEB                            
*                                                                               
         PUSH  8+20+64,PTR=R7      Arglist + info + misc                        
         CLEAR (@R7,8+20+64)       Tidy                                         
         LA    R15,@R7+8                                                        
         ST    R15,@R7             Pcw addr                                     
         OI    @R7,X'80'                                                        
         MVC   @R7+4(4),=F'20'     Length of area                               
         SCOM  INFO,(R7),FEBPMGC   Sense partner info                           
         IF    (R15,NZ),BEGIN                                                   
         POP   8+20+64             Cleanup                                      
         LCALL PROCCLS             Error!                                       
         EXIT  VIRTPOST                                                         
         END                                                                    
         MVC   FEBASCBP,@R7+8      Save ASCB addr                               
         MVC   FEBTAG,@R7+8+8      Save path name                               
         HASPSERV JOBNAME,@R7+28,ASCB=@R7+8                                     
         MVC   FEBNAME,@R7+28      Make jobname = front-end name                
*                                                                               
         IF    (R15,NZ),BEGIN                                                   
         QSNAP 'HASPSERV JOBNAME failed in VIRT:'                               
         QSNAP (R15)                                                            
         END                                                                    
*                                                                               
         HASPSERV JOBACCT,@R7+28,ASCB=@R7+8                                     
         MVC   FEBACCT,@R7+28      Save account                                 
*                                                                               
         IF    (R15,NZ),BEGIN                                                   
         QSNAP 'HASPSERV JOBACCT failed in VIRT:'                               
         QSNAP (R15)                                                            
         END                                                                    
*                                                                               
         HASPSERV JOBINFO,@R7+28,ASCB=@R7+8  get job-number                     
         MVC   FEBINFO,=CL12'Job ????'                                          
         IF    (R15,Z),'MVC FEBINFO+4(4),@R7+28'  add jobno                     
         POP   8+20+64                                                          
*                                                                               
         VCALL LOCALTOD            Clock at start                               
         STM   R0,R1,FEBSCLCK                                                   
         MVC   FEBNICLK,FEBSCLCK   Start new-info clock timer                   
         INCR  R15,CVMCRVIR        Kick active vfe count                        
*                                                                               
         LA    R0,L'VTRC           Length of trace entry                        
         LA    R1,VTRC#            No. of entries                               
         LA    R15,FEBTRCP                                                      
         VCALL GETTRC              Get trace table                              
*                                                                               
         ACALL VIRTDEF             Get a spare virtual path                     
         IF    Z,BEGIN                                                          
         L     R15,CVFEBQT                                                      
         WITH  (FEB,R15),'ST R1,FEBLINK; ST R1,CVFEBQT'  Link up                
         ACALL VIRTFIN                                                          
         END                                                                    
*                                                                               
         IF    (FEBTAG+7,EQ,'X'),'SET FEBFEXT'  Extended mode                   
*                                                                               
         IF    (FEBTAG,EQ,'RLGPATH'),'SET FEBFRLIN'  RLIN front-end             
*                                                                               
         IF    (FEBTAG,EQ,'VFEPATH'),EXIT                                       
         IF    (FEBTAG,EQ,'VAMPATH'),EXIT                                       
         IF    (FEBTAG,EQ,'WYLPATH'),EXIT                                       
         IF    (FEBTAG,EQ,'REALPAT'),EXIT                                       
         IF    (FEBTAG,EQ,'RLGPATH'),EXIT                                       
         LCALL PROCCLSZ            Bad path name                                
         EXIT  VIRTPOST                                                         
         END                                                                    
*-                                                                              
*-     The following code is a work-around for a timing problem with            
*-     MP systems and the Suzan post flag byte.  This code will set             
*-     the new-info bit every 5 seconds, if there has been no                   
*-     activity.                                                                
*-                                                                              
*-     Additional note (04/21/98) - This might no longer be                     
*-     necessary since Rich has extensively reworked Suzan. Check               
*-     out the possibility of eliminating this code. - MPD                      
*-                                                                              
         IF    ^FEBSFLAG.FEBPFNI,BEGIN  New info...                             
         IF    (FEBNICLK,Z),EXIT   No new info timer, scram                     
*                                                                               
         VCALL LOCALTOD            Current time/date                            
         STM   R0,R1,@R13                                                       
         SDL   R0,FEBNICLK         Time since last activity                     
         IF    (R0,LGT,5),BEGIN                                                 
         SET   FEBSFLAG.FEBPFNI,MODE=LOCKED,REF=FEB                             
         MVC FEBNICLK,@R13                                                      
         END                                                                    
         END                                                                    
*-                                                                              
*-       End of work-around code.                                               
*-                                                                              
         IF    FEBSFLAG.FEBPFNI,'LCALL PROCRQST'  Process new info              
*                                                                               
         IF    FEBSFLAG.FEBPFCLS,'LA R1,FEB; ACALL VIRTCLS'  Close              
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PROCRQST -- Local routine to get request from vfe and process it.            
*                                                                               
PROCRQST PROC                                                                   
         WITH  FEB                 (Entry assumption)                           
*                                                                               
PROCLOOP CLEAR FEBSFLAG.FEBPFNI,MODE=LOCKED,REF=FEB                             
         L     R7,CVWKBUFP                                                      
         WITH  (RQST,R7)                                                        
         LA    R15,4(,R7)                                                       
         ST    R15,@R13                                                         
         SET   @R13.X'80'                                                       
         MVC   @R13+4(4),=A(CVBUFLEN-4)                                         
         SCOM  GET,(R13),FEBPMGC                                                
         IF    (R15,NZ),BEGIN      Bad return code...                           
         FAIL  ((R15,LE,PATHRPMW),OR,(R15,GT,PATHRNOP)),               X        
               'VIRT path read failed'                                          
         LCALL PROCCLS                                                          
         EXIT  PROCRQST                                                         
         END                                                                    
         LTR   R1,R1                                                            
         IF    Z,EXIT              Nothing                                      
         LA    R1,4(,R1)                                                        
         STH   R1,RQSTLEN                                                       
         CLEAR (RQST+2,2)          Reset rdw flags                              
*                                                                               
         SET   FEBSFLAG.FEBPFNI,MODE=LOCKED,REF=FEB                             
*                                                                               
         INCR  R15,FEBNIO          Total i/o count                              
         EJECT                                                                  
         IF    ^FEBFINIT,BEGIN     Expecting "init"...                          
         IF    (RQSTCMD,NE,TMILINIT),'LCALL PROCCLSZ; EXIT PROCRQST'            
*-                                                                              
*-    The following check for VAMPATH is to make sure that ORVYL/VAM            
*-         does not get lines longer than 168 characters (for                   
*-         compatability with previous versions of MILTEN).                     
*-                                                                              
         IF    (FEBTAG,EQ,'VAM'),BEGIN                                          
         CLEAR RQSTINCT                                                         
         MVC   RQSTOUCT,=H'168'    Maximum output length                        
         END                                                                    
*                                                                               
*                                                                               
         MVC   FEBINMX,RQSTINCT    Maximum input len (if any)                   
         MVC   FEBOUMX,RQSTOUCT    Maximum output len (if any)                  
         LC    R0,RQSTCMFL         No. of ports (-1) wanted                     
         AH    R0,=H'1'                                                         
         STH   R0,FEBPMAX                                                       
         SLL   R0,2                Times 4 for pat size                         
         LA    R1,VPATSUBP                                                      
         PUSH  R2                                                               
         LR    R2,RAR                                                           
         VCALL NGETCORE                                                         
         POP   R2                                                               
         ST    R1,FEBPATP                                                       
         FLIP  (R0,R1)                                                          
         CLEAR R15                                                              
         MVCL  R0,R14              Zero pat                                     
         SET   FEBFINIT            Connection established                       
*                                                                               
         L     R6,FEBPATP                                                       
         CLEAR R5                                                               
         WHILE (R5,LT,FEBPMAX),BEGIN                                            
         LA    R0,L'PIB                                                         
         VCALL GETPIB                                                           
         LR    PIBR,R1                                                          
         WITH  PIB                                                              
         INCR  R15,FEBPNO          Kick active port count                       
         STH   R5,PIBNO                                                         
         SET   PIBFPS                                                           
         IF    (FEBTAG,NE,'REAL'),'MVC PIBDFACT,FEBACCT'  default               
         MVC   PIBNAME,CVBLANKS                                                 
         BTD   PIBNAME,0,(R5)      Port number                                  
         MVC   PIBLOC,CVBLANKS                                                  
         MVC   PIBLOC(12),FEBINFO Use jobname as "port location"                
         SET   PIBFPTID            terminal id can't be changed                 
*                                                                               
         LA    R0,L'VTRCP          Length of a trace entry                      
         LA    R1,VTRCP#           Number of trace entries                      
         LA    R15,PIBTRCP                                                      
         VCALL GETTRC              Get trace table                              
*                                                                               
         LR    R15,R5                                                           
         SLL   R15,2               Times 4 for pat offset                       
         ST    PIBR,@R6(R15)       Save pib ptr in pat                          
         ST    FEBR,PIBFEBP        Save feb ptr in pib                          
         LA    R1,PIB                                                           
         LA    R0,L'SCB                                                         
         VCALL GETSCB              Get a session                                
         LR    SCBR,R1                                                          
         WITH  SCB                                                              
*                                                                               
         MVC   SCBASCBP,FEBASCBP   Save jobs ASCB addr in SCB                   
         MVC   SCBINMX,FEBINMX     Maximum input len                            
         MVC   SCBOUMX,FEBOUMX     Maximum output len                           
*                                                                               
         IF    ((FEBTAG,EQ,'VAM'),OR,(FEBTAG,EQ,'VFE'),OR,             *        
               (FEBTAG,EQ,'WYL')),BEGIN                                         
         SET   SCBSFNAT+SCBSFNAR+SCBSFPS  No auto stuff, pses                   
         SET   SCBFTRC             Make session tracing the default             
         MVC   SCBTTYPE,=CL8'VIRTUAL'                                           
         MVC   SCBTID,=CL8'VIR'    Terminal id                                  
         MVC   SCBTINFO,=X'8188'   Ttype info (for compatibility)               
         END                                                                    
*                                                                               
         IF    (FEBTAG,EQ,'RLG'),BEGIN  RLG monitor...                          
         SET   SCBSFNAT+SCBSFNAR+SCBSFPS  No auto stuff, pses                   
         SET   SCBFTRC             Make session tracing the default             
         MVC   SCBTTYPE,=CL8'VIRTUAL'                                           
         MVC   SCBTID,=CL8'VIR'    Terminal id                                  
         MVC   SCBTINFO,=X'81C0'   "RLG" ttype info                             
         END                                                                    
*                                                                               
         IF    (FEBTAG,EQ,'REAL'),BEGIN  Pseudo-real...                         
         CLEAR PIBFPTID            Allow terminal id to be changed              
         SET   SCBTASYN            Assume async                                 
         END                                                                    
*                                                                               
         L     R15,CVPDBP                                                       
         WITH  (PDB,R15)                                                        
         CLEAR PDBHDR                                                           
         MVC   PDBLENG,=AL2(L'PDBHDR)                                           
         LA    R1,SCB                                                           
         VCALL SUBSTART                                                         
         LA    R5,@R5+1                                                         
         END                                                                    
         B     PROCLOOP                                                         
         END                                                                    
         EJECT                                                                  
         LH    R15,RQSTPNO         Port number                                  
         IF    (R15,LGE,FEBPMAX),'LCALL PROCCLSZ; EXIT PROCRQST'                
         SLL   R15,2               Pat offset                                   
         L     R1,FEBPATP                                                       
         LT    PIBR,@R1(R15)       PIB ptr                                      
         IF    Z,'LCALL PROCCLSZ; EXIT PROCRQST'                                
         WITH  PIB                                                              
         L     SCBR,PIBSCBP                                                     
         WITH  SCB                                                              
*                                                                               
         XTRACE FEBTRCP,RQSTFROM                                                
         WITH  (VTRC,R15),BEGIN                                                 
         MVC   VTRCPNO,PIBNO                                                    
         MVC   VTRCRQST,RQST                                                    
         END                                                                    
*                                                                               
         XTRACE PIBTRCP                                                         
         WITH  (VTRCP,R15),BEGIN                                                
         MVI   VTRCPTYP,C'F'       From virtual front-end                       
         MVC   VTRCPRQS,RQST                                                    
         END                                                                    
*                                                                               
         IF    (RQSTLEN,LT,L'RQSTHDR),'LCALL PROCCLSZ; EXIT PROCRQST'           
         IF    (RQSTCMD,NE,FMILASYN),BEGIN  sync i/o completion...              
         IF    (SCBTFIO+SCBTFAIO,Z),'LCALL PROCCLSZ; EXIT PROCRQST'             
*batwyl  if    (rqstcmd,ne,scbcmd),'lcall procclsz; exit procrqst'              
         MVC   RQSTCMD,SCBCMD      *batwyl                                      
         END                                                                    
*                                                                               
         L     R6,CVPDBP                                                        
         WITH  (PDB,R6)                                                         
         CLEAR PDBHDR                                                           
         MVC   PDBLIN#,SCBNO       Session number                               
         MVC   PDBCMD,RQSTCMD      Command code                                 
         MVC   PDBCPL,RQSTCPL      Completion flags                             
         LH    R2,RQSTOUCT                                                      
         IF    (R2,LGT,4096),'LCALL PROCCLSZ; EXIT PROCRQST'                    
         LH    R15,RQSTLEN                                                      
         SH    R15,=Y(L'RQSTHDR)                                                
         CEIL  R2,R15              Chop to len actually sent                    
         MOVEL PDBTEXT,RQSTTEXT,(R2)                                            
         LA    R15,L'PDBHDR+@R2                                                 
         STH   R15,PDBLENG                                                      
*                                                                               
         XTRACE FEBTRCP,VIRTPOST                                                
         WITH  (VTRC,R15),BEGIN                                                 
         MVC   VTRCPNO,PIBNO                                                    
         MVC   VTRCSID,SCBID                                                    
         MVC   VTRCSACC,SCBACCT                                                 
         MVC   VTRCCMD,PDBCMD                                                   
         MVC   VTRCCPL,PDBCPL                                                   
         MVC   VTRCTLEN,PDBLENG                                                 
         MVC   VTRCTEXT,PDBTEXT                                                 
         END                                                                    
*                                                                               
         SET   SCBMODB.MODBUPLO,(SCBMODB.MODBUPPR,OFF)  no upper xl             
         LA    R1,SCB                                                           
         LA    R15,PDB                                                          
         VCALL SUBSEND                                                          
         IF    NZ,EXIT                                                          
         B     PROCLOOP                                                         
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PATHSEND -- Routine to send request accoss a SUZAN path.                     
*                                                                               
*    On entry:                                                                  
*      R1 - PATH ptr                                                            
*      R15- request (starting with RDW)                                         
*                                                                               
*    On exit, R15 (and CC):                                                     
*       -4 - died                                                               
*        0 - sent                                                               
*                                                                               
PATHSEND PROC                                                                   
         LR    R6,R1                                                            
         LR    R2,R15              Save PDB pointer                             
         CLEAR (@R2+2,2)           Clear rdw flags                              
         WITH  (PATHD,R6)                                                       
         L     R5,PATHECBF                                                      
PATHLOOP ST    R2,@R13                                                          
         CLEAR (@R13+4,4)                                                       
         SET   @R13.X'80'                                                       
         CLEAR @R5.PATHFFQE                                                     
         LT    R0,PATHMGC                                                       
         BZ    PATHDOWN            Path is closing                              
         SCOM  SEND,(R13),PATHMGC                                               
         IF    (R15,Z),EXIT                                                     
         IF    (R15,EQ,PATHRBAF),BEGIN  overflow...                             
         IF    @R5.PATHFCLS,PATHDOWN  closing, scram                            
*-                                                                              
*- If full queues happened more often, the following code would have            
*-          to be smarter.                                                      
*-                                                                              
         QSNAP 'Virtual terminal fullQ',MODE=WTO                                
         SET   @R5.PATHFCLS        Closing, closing, closing                    
         B     PATHDOWN            Scram                                        
         END                                                                    
         IF    (R15,EQ,PATHRNOP),PATHDOWN                                       
         FAIL  'VIRT - Unxepected SCOM return code in PATHSEND'                 
*                                                                               
PATHDOWN LH    R15,=H'-4'                                                       
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  PROCCLS/PROCCLSZ -- Local routine to close vfe path.                         
*                                                                               
PROCCLSZ CLEAR R15                                                              
PROCCLS  PROC                                                                   
         WITH  (RQST,R7),FEB       (Entry assumptions)                          
         QSNAP 'Virtual terminal path error:',MODE=WTO                          
         IF    (R15,NZ),'QSNAP (R15)'                                           
         QSNAP RQSTHDR                                                          
         QSNAP FEBPATH                                                          
         S     RAR,=A(MILVIRT)                                                  
         LA    RAR,@RAR                                                         
         QSNAP (RAR)               Module offset                                
         SET   FEBSFLAG.FEBPFCLS,MODE=LOCKED,REF=FEB                            
         PEND                                                                   
         QLTORG                                                                 
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTERR -- Routine to do error recovery.                                     
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*                                                                               
*    On exit, R15 (and CC):                                                     
*      0 - attempt retry                                                        
*      4 - no retry, shutdown MILTEN                                            
*                                                                               
VIRTERR  XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
*                                                                               
         CLEAR CVCUR                                                            
*                                                                               
         LA    R1,FEB                                                           
         ACALL VIRTCLS             Release front-end                            
         CLEAR R15                 Retry rc                                     
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTDIE -- Routine to do final cleanup.  It is entered when                  
*    MILTEN is terminating.                                                     
*                                                                               
*    On entry:                                                                  
*      R0 - 0: normal, 4: error                                                 
*      R1 - FEB ptr                                                             
*                                                                               
VIRTDIE  XPROC                                                                  
*-                                                                              
*-       Nothing to do, simply return.                                          
*-                                                                              
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTCMD -- Routine to process virtual commands; the scanner                  
*    is all set up to scan the command.                                         
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*      R15- SEGCB ptr                                                           
*                                                                               
VIRTCMD  XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
         LR    R7,R15              Segcb ptr                                    
         WITH  (SEGCB,R7)                                                       
         SEGDEF (R7)                                                            
*                                                                               
         SEGHX (FEBR),8                                                         
         SEGB  '  virtual'                                                      
         SEGTB FEBNAME                                                          
         SEGDC LH:FEBPMAX                                                       
         SEGB  ' ports '                                                        
         IF    ^FEBFOPEN,'SEGB "** Not-open **"'                                
         IF    (FEB,NE,'FEB'),'SEGB "** Invalid **"'                            
         SEGWR                                                                  
*                                                                               
         PEND                                                                   
         EJECT                                                                  
*box                                                                            
*                                                                               
*  VIRTFMT -- Routine to do control block formatting.                           
*                                                                               
*    On entry:                                                                  
*      R1 - FEB ptr                                                             
*      R15- SEGCB ptr                                                           
*                                                                               
VIRTFMT  XPROC                                                                  
         LR    FEBR,R1                                                          
         WITH  FEB                                                              
         LR    R7,R15              Segcb ptr                                    
         WITH  (SEGCB,R7)                                                       
         SEGDEF (R7)                                                            
*                                                                               
         SEGHX (FEBR),8                                                         
         SEGB  '  VIRTUAL'                                                      
         SEGTB FEBNAME                                                          
         SEGDC LH:FEBPMAX                                                       
         SEGB  ' PORTS '                                                        
         IF    ^FEBFOPEN,'SEGB "** NOT-OPEN **"'                                
         IF    (FEB,NE,'FEB'),'SEGB "** INVALID **"'                            
         SEGWR                                                                  
         IF    (FEBPATP,Z),EXIT                                                 
*                                                                               
         L     R5,FEBPATP          Start                                        
         LH    R6,FEBPMAX                                                       
         SLL   R6,2                                                             
         AR    R6,R5               End                                          
         WHILE (R5,LT,R6),BEGIN                                                 
         IF    ('LT PIBR,@R5',NZ),BEGIN                                         
         WITH  PIB                                                              
         SEGCOL 3                                                               
         SEGHX (PIBR),6                                                         
         SEG   ' PORT#'                                                         
         LR    R15,R5                                                           
         S     R15,FEBPATP                                                      
         SRL   R15,2                                                            
         SEGDC (R15)                                                            
         SEGB                                                                   
         IF    ('LT SCBR,PIBSCBP',NZ),BEGIN                                     
         WITH  SCB                                                              
         SEGB  'SESSION:'                                                       
         SEG   SCBGRP                                                           
         SEG   '.'                                                              
         SEG   SCBUSER                                                          
         IF    ('L2 R2,SCBANO',NZ),'SEG "#"; SEGDC (R2)'                        
         END                                                                    
         SEGWR                                                                  
         END                                                                    
         LA    R5,@R5+4                                                         
         END                                                                    
         SEGWR CVBLANKS,1                                                       
         SEGWR CVBLANKS,1                                                       
*                                                                               
*  display trace table...                                                       
         SEGWR 'TRACE:'                                                         
         MVC   SEGCBINDF,=A(25)                                                 
         LM    R4,R6,FEBTRCP+4     R4=next, r5=first, r6=end                    
         LOOP  BEGIN                                                            
         S     R4,FEBTRCP          Back up                                      
         IF    (R4,LT,R5),'LR R4,R6; S R4,FEBTRCP'  last                        
         WITH  (VTRC,R4)                                                        
         IF    (VTRCCLCK,Z),EXIT   All done                                     
         LM    R0,R1,VTRCCLCK                                                   
         PUSH  11,PTR=R15          l'hh:mm:ss.hh                                
         VCALL FMTTIME                                                          
         SEGB  (R1),(R0)                                                        
         POP   11                                                               
         SEGB  VTRCNAME                                                         
*                                                                               
         IF    (VTRCNAME,EQ,'VIRT'),BEGIN                                       
         SEGDC LH:VTRCPNO,3                                                     
         SEGB                                                                   
         IF    (VTRCSACC,Z),'SEG "-----"'                                       
         ELSE  BEGIN                                                            
         SEG   VTRCSACC+3,2                                                     
         SEG   '.'                                                              
         SEG   VTRCSACC,3                                                       
         END                                                                    
         SEG   ','                                                              
         SEGDC L:VTRCSID                                                        
         SEGB                                                                   
         IF    (VTRCCMD,EQ,TMILWRIT),'SEG "WRITE:"'                             
         ELSEIF (VTRCCMD,EQ,TMILREAD),'SEG "READ:"'                             
         ELSE  'SEG "CMD="; SEGHX IC:VTRCCMD,2'                                 
         SEGB                                                                   
         LH    R2,VTRCTLEN                                                      
         SH    R2,=Y(L'PDBHDR)                                                  
         IF    ^NEG,BEGIN                                                       
         SEG   'LENGTH='                                                        
         SEGDC (R2)                                                             
         SEG   ', TEXT='                                                        
         CEIL  R2,L'VTRCTEXT                                                    
         SEGT  VTRCTEXT,(R2)                                                    
         END                                                                    
         END                                                                    
*                                                                               
         SEGWR                                                                  
*                                                                               
         UNTIL ('C R4,FEBTRCP+4',EQ),END                                        
         CLEAR SEGCBINDF           Reset indentation                            
*                                                                               
         PEND                                                                   
*                                                                               
         QLTORG                                                                 
*                                                                               
         END   .                                                                
