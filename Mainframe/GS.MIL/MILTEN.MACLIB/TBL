         MACRO                                                                  
&L       TBL   &VAL,&RTN,&ELSE=0                                                
.*  Added 5/79, Niz; modified, 10/84, Niz                                       
         GBLA  &TBLCTR,&TBL(256)                                                
         GBLC  &TBLLAB,&TBLRTN(0)                                               
         LCLA  &I,&J,&K                                                         
         AIF   ('&L&TBLLAB' EQ '').ERRNLAB                                      
         AIF   ('&L' EQ '').NOLAB                                               
         AIF   ('&TBLLAB' NE '').ERRDLAB                                        
&TBLLAB  SETC  '&L'                                                             
.NOLAB   AIF   ('&VAL' EQ '').END                                               
.*                                                                              
         AIF   ('&ELSE' EQ '0').NOELSE                                          
         MNOTE 8,'ELSE=&ELSE can only be specified on last TBL.'                
.NOELSE  ANOP                                                                   
.*                                                                              
.DUPLP   AIF   (&I GE &TBLCTR).NOTDUP                                           
&I       SETA  &I+1                                                             
         AIF   ('&RTN' NE '&TBLRTN(&I)').DUPLP                                  
         AGO   .LP                                                              
.NOTDUP  ANOP                                                                   
&I       SETA  &I+1                                                             
&TBLCTR  SETA  &I                                                               
&TBLRTN(&TBLCTR)  SETC  '&RTN'                                                  
.LP      AIF   (&J GE N'&VAL).EXIT                                              
&J       SETA  &J+1                                                             
&TBL(&VAL(&J)+1)  SETA  &I*4                                                    
         AGO   .LP                                                              
.*                                                                              
.END     ANOP                                                                   
         PUSH  NOGEN                                                            
&TBLLAB.TBL  DS  0F                                                             
.ELP     DC    AL1(&TBL(&K+1),&TBL(&K+2),&TBL(&K+3),&TBL(&K+4))                 
&TBL(&K+1)  SETA  0,0,0,0                                                       
&K       SETA  &K+4                                                             
         AIF   (&K LT 256).ELP                                                  
&K       SETA  0                                                                
&TBLLAB.VECT  DC  A(&ELSE)                                                      
.VLP     AIF   (&K GE &TBLCTR).ENDV                                             
&K       SETA  &K+1                                                             
         DC    A(&TBLRTN(&K))                                                   
&TBLRTN(&K)  SETC  ''                                                           
         AGO   .VLP                                                             
.ENDV    ANOP                                                                   
&TBLCTR  SETA  0                                                                
&TBLLAB  SETC  ''                                                               
         POP   PRINT                                                            
.EXIT    MEXIT                                                                  
.*                                                                              
.ERRNLAB MNOTE 8,'First TBL line must have a label.'                            
         MEXIT                                                                  
.*                                                                              
.ERRDLAB MNOTE 8,'Label &L conflicts with &TBLLAB'                              
         MEND                                                                   
