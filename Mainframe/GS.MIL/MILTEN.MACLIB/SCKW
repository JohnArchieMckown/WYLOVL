         MACRO                                                                  
&L       SCKW  &KP,&XP,&TP,&MP                                                  
.*       modified 11/79, JDN                                                    
         LCLA  &X,&PV,&TV,&AV,&LV,&LV1                                          
&RP      SETC  '&XP'                                                            
&A       SETC  'A'                                                              
         AIF   ('&RP  '(1,2) NE 'V(').NOV                                       
&A       SETC  'V'                                                              
&RP      SETC  '&RP'(3,K'&RP-3)                                                 
.NOV     AIF   ('&RP' NE '').UNZ                                                
&RP      SETC  '0'                                                              
.UNZ     AIF   ('&KP'.'&TP' EQ '').UNREC                                        
.LOOP    ANOP                                                                   
&X       SETA  &X+1                                                             
         AIF   (&X GT N'&TP).DONE                                               
         AIF   ('&TP(&X)' EQ 'P').P                                             
         AIF   ('&TP(&X)' EQ 'A').A                                             
         AIF   ('&TP(&X)' EQ 'PI').PI                                           
         AIF   ('&TP(&X)' EQ 'I').I                                             
         AIF   ('&TP(&X)' EQ 'LN').LN                                           
         AIF   ('&TP(&X)' EQ 'S').S                                             
         AIF   ('&TP(&X)' EQ 'POP').POP                                         
         AIF   ('&TP(&X)' EQ 'PUSH').PUSH                                       
.ILLTYPE MNOTE 12,'***** "&TP(&X)" IS ILLEGAL'                                  
         MEXIT                                                                  
.P       ANOP                                                                   
&PV      SETA  1                                                                
         AGO   .LOOP                                                            
.S       ANOP                                                                   
&TV      SETA  1                                                                
         AGO   .LOOP                                                            
.PI      ANOP                                                                   
&TV      SETA  2                                                                
.ICOM    AIF   ('&MP' EQ '').LOOP                                               
&TV      SETA  &TV+1                                                            
         AGO   .LOOP                                                            
.I       ANOP                                                                   
&TV      SETA  4                                                                
         AGO   .ICOM                                                            
.LN      ANOP                                                                   
&TV      SETA  6                                                                
         AGO   .LOOP                                                            
.A       ANOP                                                                   
&AV      SETA  1                                                                
         AGO   .LOOP                                                            
.DONE    ANOP                                                                   
&X       SETA  &PV*128+&TV*16+&AV*8                                             
         AIF   ('&KP' EQ '').NOKW                                               
&LV      SETA  K'&KP-1                                                          
         AIF   ('&KP'(1,1) NE '''').NOQ                                         
&LV      SETA  &LV-2                                                            
.NOQ     ANOP                                                                   
         AIF   (&LV LT 8).LVSET                                                 
&LV      SETA  7                                                                
.LVSET   ANOP                                                                   
&LV1     SETA  &LV+1         true character count                               
&X       SETA  &X+&LV        final flag value                                   
         AIF   ('&KP' EQ '').NOKPQ                                              
         AIF   ('&KP'(1,1) NE '''').NOKPQ                                       
&L       DC    AL1(&X),&A.L3(&RP),CL&LV1&KP                                     
         AGO   .LIM                                                             
.NOKPQ   ANOP                                                                   
&L       DC    AL1(&X),&A.L3(&RP),CL&LV1'&KP'                                   
         AGO   .LIM                                                             
.NOKW    AIF   (&PV EQ 1 OR &TV LT 2).ILLKW                                     
&L       DC    AL1(&X),&A.L3(&RP),C' '                                          
.LIM     ANOP                                                                   
         AIF   (&TV LT 2 OR &TV GT 5).END                                       
         AIF   ('&MP' EQ '').END                                                
         DC    AL4(&MP)                                                         
         MEXIT                                                                  
.ILLKW   MNOTE 8,'**** KEYWORD REQUIRED'                                        
         MEXIT                                                                  
.UNREC   ANOP                                                                   
&L       DC    X'FF',&A.L3(&RP)                                                 
         MEXIT                                                                  
.POP     ANOP                                                                   
&L       DC    X'F9'                                                            
         MEXIT                                                                  
.PUSH    ANOP                                                                   
&L       DC    X'F8',&A.L3(&RP)                                                 
.END     MEND                                                                   
