         MACRO                                                                  
&L       PUSH  &PTR=,&ID=,&BASE=NO                                              
.*       RE-DONE 4/77, JDN;  MODIFIED 8/79, JDN                                 
.*       MODIFIED 6/83, RAL                                                     
         GBLA  &$COUNT,&$UPTR,&$UR#STK(0),&$REG#                                
         GBLB  &$OK                                                             
         GBLC  &$USINGS(16),&$USTK(0),&$ULOC                                    
         LCLA  &I                                                               
         AIF   ('&SYSLIST(1)' NE 'PRINT' AND '&SYSLIST(1)' NE 'USING'  *        
               AND '&SYSLIST(1)' NE 'GEN' AND '&SYSLIST(1)' NE 'NOGEN' *        
               AND '&SYSLIST(1)' NE 'ON' AND '&SYSLIST(1)' NE 'OFF'    *        
               AND '&SYSLIST(1)' NE 'DSECTS').PSTK                              
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
         AIF   ('&SYSLIST(&I)' NE 'USING').DO                                   
&UP      SETA  &$UPTR+1                                                         
&J       SETA  1                                                                
.ULOOP   ANOP                                                                   
&$UPTR   SETA  &$UPTR+1                                                         
&$USTK(&$UPTR) SETC '&$USINGS(&J)'                                              
&$UR#STK(&$UPTR) SETA &J-1+100                                                  
&J       SETA  &J+1                                                             
         AIF   (&J LE 16).ULOOP                                                 
&$UR#STK(&UP) SETA 0-&$UR#STK(&UP)                                              
         AGO   .LOOP                                                            
.DO    $$PUSH  PRINT                                                            
         AIF   ('&SYSLIST(&I)' EQ 'PRINT').LOOP                                 
         AIF   ('&SYSLIST(&I)' EQ 'DSECTS').DSECTS                              
         PRINT &SYSLIST(&I)                                                     
         AGO   .LOOP                                                            
.DSECTS  ANOP                                                                   
&$ULOC   SETC  '&SYSLOC'                                                        
         AIF   ('&SYSPARM' EQ 'DSECTS').DOSP                                    
         PRINT OFF                                                              
.DOSP    ANOP                                                                   
*PUSH,GEN                                                                       
         AGO   .LOOP                                                            
.*                                                                              
.PSTK    AIF   (T'SPR NE '#').ERRNSPR                                           
.*   AIF   (N'&SYSLIST EQ 1 AND '&SYSLIST(1)' NE '&SYSLIST(1,1)').PREG          
&LEN     SETC  '0'                                                              
.PLP     AIF   (&I GE N'&SYSLIST).PFIN                                          
&I       SETA  &I+1                                                             
         AGO   (N'&SYSLIST(&I)+1).PLP,.P1,.P2                                   
&NOP     SETA  N'&SYSLIST(&I)                                                   
         MNOTE 8,'&SYSLIST(&I) HAS WRONG NO. OF OPERANDS (NOP=&NOP)'            
         AGO   .PLP                                                             
.*                                                                              
.P1      AIF   ('&SYSLIST(&I)' NE '&SYSLIST(&I,1)').ERRMR                       
        $RGLKUP &SYSLIST(&I)                                                    
         AIF   (NOT &$OK).PLEN                                                  
&LEN     SETC  '&LEN+4'                                                         
         ST    &SYSLIST(&I),@NSP-@SPR-(&LEN)(,SPR)                              
         AGO   .PLP                                                             
.PLEN    ANOP                                                                   
&LEN     SETC  '&LEN+((&SYSLIST(&I)+3)/4*4)'                                    
         AGO   .PLP                                                             
.*                                                                              
.P2     $RGLKUP &SYSLIST(&I,1)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&R1      SETA  &$REG#                                                           
        $RGLKUP &SYSLIST(&I,2)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&N       SETA  (&$REG#-&R1+1)*4                                                 
         AIF   (&N GT 0).HVL                                                    
&N       SETA  (16-&R1+&$REG#+1)*4                                              
.HVL     ANOP                                                                   
&LEN     SETC  '&LEN+&N'                                                        
         STM   &SYSLIST(&I,1),&SYSLIST(&I,2),@NSP-@SPR-(&LEN)(SPR)              
         AGO   .PLP                                                             
.*                                                                              
.PREG    AIF   (N'&SYSLIST EQ 1).PREGDO                                         
         MNOTE 4,'ONLY "&SYSLIST(1)" ACCEPTED'                                  
.PREGDO $RGLKUP &SYSLIST(1,1)                                                   
         AIF   (NOT &$OK).ERRNR1,(&$REG# EQ 0).ERRR0                            
         AIF   ('&BASE' EQ 'NO').NB1                                            
         BASR  BR,0                                                             
         WITH  (*,BR)                                                           
.NB1     SR    SPR,&SYSLIST(1,1)                                                
         SH    SPR,=Y(4)                                                        
         ST3   SPR,@NSP-@SPR+1+4(&SYSLIST(1,1),SPR)                             
         N     SPR,=A(X'FFFFFC')                                                
         AGO   .COM                                                             
.*                                                                              
.PFIN    AIF   ('&LEN' EQ '0').ERRNOP                                           
         AIF   ('&BASE' EQ 'NO').NB                                             
         BASR  BR,0                                                             
         WITH  (*,BR)                                                           
.NB      SH    SPR,=Y(&LEN+4)                                                   
         ST3   SPR,@NSP-@SPR+(&LEN+4)+1(SPR)                                    
.COM     ST    SPR,@NSP-@SPR(,SPR)                                              
         AIF   ('&ID' EQ '').NID                                                
         MVI   @NSP-@SPR(SPR),&ID                                               
.NID     AIF   ('&PTR' EQ '').EXIT                                              
         LA    &PTR,@WA-@SPR(,SPR)                                              
.EXIT    MEXIT                                                                  
.*                                                                              
.ERRNSPR MNOTE 8,'STACK POINTER REGISTER (SPR) MUST BE DEFINED'                 
         MEXIT                                                                  
.*                                                                              
.ERRMR   MNOTE 8,'"(REG)" MUST BE THE FIRST AND ONLY OPERAND'                   
         MEXIT                                                                  
.*                                                                              
.ERRNR1  MNOTE 8,'&SYSLIST(1,1) IS NOT A REGISTER'                              
         MEXIT                                                                  
.*                                                                              
.ERRRO   MNOTE 8,'REGISTER 0 CAN''T BE USED'                                    
         MEXIT                                                                  
.*                                                                              
.ERRNR   MNOTE 8,'&SYSLIST(&I) IS NOT A REGISTER'                               
         MEXIT                                                                  
.*                                                                              
.ERRNOP  MNOTE 8,'REQUIRED OPERANDS MISSING'                                    
         MEND                                                                   
