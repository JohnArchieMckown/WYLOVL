         MACRO                                                                  
&L       POP                                                                    
.*       RE-DONE 4/77, JDN;  MODIFIED 8/79, JDN                                 
         GBLA  &$UPTR,&$UR#STK(0),&$REG#,&$COUNT                                
         GBLB  &$OK                                                             
         GBLC  &$USTK(0),&$ULOC                                                 
         LCLA  &I                                                               
         AIF   ('&SYSLIST(1)' NE 'PRINT' AND '&SYSLIST(1)' NE 'USING'  *        
               AND '&SYSLIST(1)' NE 'DSECTS').PSTK                              
.LOOP    AIF   (&I GE N'&SYSLIST).EXIT                                          
&I       SETA  &I+1                                                             
         AIF   ('&SYSLIST(&I)' NE 'USING').DO                                   
         AIF   (&$UPTR LE 0).NOPOP                                              
         AIF   (&$UR#STK(&$UPTR) GE 100).ULOOP                                  
.NOPOP   MNOTE 4,'WARNING - POP USING DONE WITH NO PRECEDING PUSH'              
         DROP                                                                   
         AGO   .LOOP                                                            
.ULOOP   ANOP                                                                   
&J       SETA  &$UR#STK(&$UPTR)-100                                             
&$UPTR   SETA  &$UPTR-1                                                         
         AIF   (&J LT 0).ULAST                                                  
         USING &$USTK(&$UPTR+1),&J                                              
         AGO   .ULOOP                                                           
.ULAST   ANOP                                                                   
&J       SETA  0-(200+&J)                                                       
         USING &$USTK(&$UPTR+1),&J                                              
         AGO   .LOOP                                                            
.DO    $$POP   PRINT                                                            
         AIF   ('&SYSLIST(&I)' NE 'DSECTS').LOOP                                
         AIF   ('&$ULOC' EQ '').DOSP                                            
&$ULOC   LOCTR                                                                  
&$ULOC   SETC  ''                                                               
.DOSP    ANOP                                                                   
*POP                                                                            
         AGO   .LOOP                                                            
.*                                                                              
.PSTK    AIF   (T'SPR NE '#').ERRNSPR                                           
.*    AIF  (N'&SYSLIST EQ 1 AND '&SYSLIST(1)' NE '&SYSLIST(1,1)').PREG          
&SYM     SETC  '$$$&$COUNT'                                                     
&$COUNT  SETA  &$COUNT+1                                                        
&LEN     SETC  '0'                                                              
&L       LA    SPR,&SYM+4(,SPR)                                                 
&I       SETA  N'&SYSLIST+1                                                     
.PLP     AIF   (&I LE 1).PFIN                                                   
&I       SETA  &I-1                                                             
         AGO   (N'&SYSLIST(&I)+1).PLP,.P1,.P2                                   
&NOP     SETA  N'&SYSLIST(&I)                                                   
         MNOTE 8,'&SYSLIST(&I) HAS WRONG NO. OF OPERANDS (NOP=&NOP)'            
         AGO   .PLP                                                             
.*                                                                              
.P1      AIF   ('&SYSLIST(&I)' NE '&SYSLIST(&I,1)').ERRMR                       
        $RGLKUP &SYSLIST(&I)                                                    
         AIF   (NOT &$OK).PLEN                                                  
         L     &SYSLIST(&I),@NSP-@SPR-(&SYM-(&LEN))(,SPR)                       
&LEN     SETC  '&LEN+4'                                                         
         AGO   .PLP                                                             
.PLEN    ANOP                                                                   
&LEN     SETC  '&LEN+((&SYSLIST(&I)+3)/4*4)'                                    
         AGO   .PLP                                                             
.*                                                                              
.P2     $RGLKUP &SYSLIST(&I,1)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&R1      SETA  &$REG#                                                           
        $RGLKUP &SYSLIST(&I,2)                                                  
         AIF   (NOT &$OK).ERRNR                                                 
&N       SETA  (&$REG#-&R1+1)*4                                                 
         AIF   (&N GT 0).HVL                                                    
&N       SETA  (16-&R1+&$REG#+1)*4                                              
.HVL     ANOP                                                                   
         LM  &SYSLIST(&I,1),&SYSLIST(&I,2),@NSP-@SPR-(&SYM-(&LEN))(SPR)         
&LEN     SETC  '&LEN+&N'                                                        
         AGO   .PLP                                                             
.*                                                                              
.PREG    AIF   (N'&SYSLIST EQ 1).PREGDO                                         
         MNOTE 4,'ONLY "&SYSLIST(1)" ACCEPTED'                                  
.PREGDO $RGLKUP &SYSLIST(1,1)                                                   
         AIF   (NOT &$OK).ERRNR,(&$REG# EQ 0).ERRR0                             
&L       LA    SPR,4+3(&SYSLIST(1,1),SPR)                                       
         N     SPR,=A(X'FFFFFC')                                                
         AGO   .COM                                                             
.*                                                                              
.PFIN    AIF   ('&LEN' EQ '0').ERRNOP                                           
&SYM     EQU   &LEN                                                             
.COM     ST    SPR,@NSP-@SPR(,SPR)                                              
.EXIT    MEXIT                                                                  
.*                                                                              
.ERRNSPR MNOTE 8,'STACK POINTER REGISTER (SPR) MUST BE DEFINED'                 
         MEXIT                                                                  
.*                                                                              
.ERRMR   MNOTE 8,'"(REG)" MUST BE THE FIRST AND ONLY OPERAND'                   
         MEXIT                                                                  
.*                                                                              
.ERRNR1  MNOTE 8,'&SYSLIST(1,1) IS NOT A REGISTER'                              
         MEXIT                                                                  
.*                                                                              
.ERRRO   MNOTE 8,'REGISTER 0 CAN''T BE USED'                                    
         MEXIT                                                                  
.*                                                                              
.ERRNR   MNOTE 8,'&SYSLIST(&I) IS NOT A REGISTER'                               
         MEXIT                                                                  
.*                                                                              
.ERRNOP  MNOTE 8,'REQUIRED OPERANDS MISSING'                                    
         MEND                                                                   
