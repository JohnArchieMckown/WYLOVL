xproc () begin
; UCBLOOK, using new WINGS UNITINFO interface
; WINGS UNITINFO record:
;
;  ....v....1....v....2....v....3....v....4....v....5....v....6
;  0420 A     DISK STR200 3390-3     3340      SMS Shared
;  unit stat  devt volser devtype    cyls      use misc.
;  0802 O     U.R.        3211 Printer         PN   LC06
;                                              ucs  fcb
;
;  parm is unit address or volser

declare string uparm uopt udev uvol ustat umsg

declare num lparm
lparm=size(parm_string)
uparm=upp(strip(parm_string))

if (lparm eq 3 or lparm eq 4) begin              ;  unit address
   if (verify(uparm,'0123456789ABCDEF') ne 0) begin ; invalid digit
      wri 'Invalid unit address '||uparm
      xreturn
      end                                        ;  (invalid)

   uparm='UNIT='||uparm
   end                                           ;  (unit address)

if (lparm eq 6) begin                            ;  volser
   uparm='VOLUME='||uparm
   end

if (lparm lt 3 or lparm eq 5 or lparm gt 6) begin  ; invalid length
   wri 'Invalid option '||uparm
   wri 'Specify unit address or volume serial.'
   xreturn
   end

xl = last
xc wyl.ga.mcl.wings.commands#unitinfo/dump &(uparm)

if (last gt xl) begin              ; got it

   rea str umsg usi las delete     ; get it
   ustat=sub(umsg,6,1)             ; basic status O, F, A
   wri 'Unit '||sub(umsg,1,10)     ; address, status
   udev = sub(umsg,12,4)           ; basic unit type
   uvol = sub(umsg,17,6)           ; volser
   if (udev eq 'DISK') begin       ; disk
      wri udev||' ('||strip(sub(umsg,24,10))||') '||sub(umsg,34,5)||' cyls'
      wri 'Vol='||uvol||' Use='||sub(umsg,45,3)||'   '||sub(umsg,49)
      end
   elseif (udev eq 'TAPE') begin
      wri udev||' ('||strip(sub(umsg,24))||')'
      if(sub(uvol,1,1) eq s'ff') uvol='SCRTCH'
      wri 'Vol='||uvol
      end
   elseif (udev eq 'U.R.') begin
      wri udev||' ('||strip(sub(umsg,24,20))||')'
      wri 'UCS='||sub(umsg,45,4)||'  FCB='||sub(umsg,50,4)
      end
   else begin  ; misc
      wri sub(umsg,12)
      end
   end                             ;  (got it)
end
