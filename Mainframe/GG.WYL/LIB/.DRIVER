//WYLBRMAN JOB ,'WYLBUR Ref. Manual',REGION=512K,TIME=2
/*JOBPARM HOLD=OUTPUT,PURGE=NO
//SCRIPT   EXEC SCRIPT,VERSION=V831,
//            PARM='FFTOP=4,AD=12,CO=10,FNS=1000,2P'
//SYSPRINT DD UNIT=SYSDA,DSN=&TEMP,
//            SPACE=(TRK,(50,50)),DCB=(RECFM=VBA,BLKSIZE=13030),
//            DISP=(,PASS)
//SYSLIB   DD DSN=WYL.GA.PUB.WYLHELP,DISP=SHR
//         DD DSN=WYL.GA.MCL.TSS.SCRIPLIB,DISP=SHR
//SYSIN    DD *                                                         UNN
.cm ****************************************************************************
.cm ****************************************************************************
.cm **                                                                        **
.cm **     WYLBUR Manual Driver File                                          **
.cm **                                                                        **
.cm **     Written by Lincoln Ong & Jim Nisbet, 11/22/80                      **
.cm **                                                                        **
.cm **     Latest modification 08/11/84 by Lincoln Ong, Version 1.57          **
.cm **                                                                        **
.cm **     Address all inquiries to:                                          **
.cm **                                                                        **
.cm **          Lincoln Ong                                                   **
.cm **          Information Technology Services                               **
.cm **          George Forsythe Hall                                          **
.cm **          Stanford University                                           **
.cm **          Stanford, CA  94305                                           **
.cm **          (415) 497-0595                                                **
.cm **                                                                        **
.cm **     MILTEN, ORVYL are trademarks of                                    **
.cm **            the Leland Stanford Junior University                       **
.cm **                                                                        **
.cm **     WYLBUR is a registered trademark of                                **
.cm **            the Leland Stanford Junior University                       **
.cm **                                                                        **
.cm **     WYLBUR Reference Manual                                            **
.cm **     (C) Copyright 1975, 1977, 1981, 1983, 1984 by                      **
.cm **         The Board of Trustees of the                                   **
.cm **         Leland Stanford Junior University                              **
.cm **                                                                        **
.cm **         All rights reserved                                            **
.cm **                                                                        **
.cm ****************************************************************************
.cm ****************************************************************************
.cm
.cm -------------------------------------------------------------- initial setup
.cm ----------------------------------- symbols
.sr wyldriver = 'WYLBUR Manual Driver  11 August 1984  Version 1.57'
.sr site = 'Stanford'
.sr orgtitle = 'Information Technology Services'
.sr orgname = 'ITS'
.sr machine = 'IBM 3081'
.sr cpu = '3081'
.cm ----------------------------------- switches
.cm manual = {'WYLBUR'|'ORVYL'|'ALL'}
.cm version = {'PUBLISH'|ONLINE'|'DRAFT'}
.cm
.sr manual = 'WYLBUR'                  ;.cm Manual selection.
.   .sr manual = &u'&manual
.sr version = 'PUBLISH'                ;.cm Manual format.
.   .sr version = &u'&version
.cm
.cm Condition section assignments:
.cm       1 -- [online]  system omission  (not displayed by online HELP)
.cm       2 -- [online]  omit from manual  (displayed by online HELP)
.cm       3 -- [manual]  WYLBUR command
.cm       4 -- [manual]  ORVYL command
.cm       8 --           Line mode specific HELP
.cm       9 --           Page mode specific HELP
.cm   10-19 -- system and optional manual omission
.cm            for experimental and testing use
.cm ----------
.cs 8 ignore
.cs 9 ignore
.cm ----------
.if &manual eq 'WYLBUR' .th .do        ;.cm WYLBUR manual selected.
.   .cs 4 ignore                       ;.cm Omit ORVYL sections.
.   .sr docnum = '#234'
.   .sr docpubdate = 'September 1984'  ;.cm latest revised edition.
.   .sr doctitle = 'WYLBUR Reference Manual'
.   .sr docedition = 'Sixth Edition'
.do end
.if &manual eq 'ORVYL' .th .do         ;.cm ORVYL manual selected.
.   .cs 3 ignore                       ;.cm Omit WYLBUR sections.
.   .sr docnum = '#???'
.   .sr docpubdate = '???'             ;.cm 10/22/81, no recent revised edition.
.   .sr doctitle = 'ORVYL Reference Manual'
.   .sr docedition = '???'
.do end
.if &manual eq 'ALL' or &manual eq 'STS' .th .do
.   .sr docnum = '#???'
.   .sr docpubdate = '???'
.   .sr doctitle = 'Stanford Timeshare System'
.   .sr docedition = '???'
.do end
.if &version ne 'ONLINE' .cs 2 ignore  ;.cm default
.cm * BUG *    .   .el .cs 1 ignore
.cm ----------
.cm ----------------------------------- environment
.tm 6;.hm 1;.sr pubMYhs = 1
.bm 6;.fm 1;.sr pubMYfs = 1
.rt top all ++++
.rt bottom all ++++
.im boldital chars=U670
.if &chars eq 'U670' .im arrows
.im syspub layout=manual hy=off pl=66 ll=78
.dc rb \
.if &chars eq 'U340' .th .do
.   .tr 4F 16  4A 9E                     ;.cm <-- for U340 character set
.   .tr 79 4B                            ;.cm <-- for .commands of MODIFY and EDIT
.do end
.if &chars eq 'U670' .th .do
.   .tr 4F FA
.   .tr 79 4B                            ;.cm <-- for .commands of MODIFY and EDIT
.do end
.ud on 40 . , : 5E ? ! ' " ( ) [ ] { }
.cm --------------------------------------------------------------------- macros
.cm ----------------------------------- boxon & boxoff
.dm boxon begin
.   .sk 1 a c;.bx new &SYSIN+1 &SYSINR+&SYSCL
.   .in +3 -3;.sk 1 a
.dm
.cm ----------
.dm boxoff /.sk 1 a/.in -3 +3/.bx off/
.cm ----------------------------------- item & itemix
.dm item begin
.   .pa
.   .sr command = '&*1'
.   .if &command(1:1) ne &u'&command(1:1) .th .do
.       .rt top all +&u'&command(1:1)&command(2:&L'&command)++&doctitle+
.   .do end
.       .el .rt top all +&command++&doctitle+
.   .if &version eq 'PUBLISH' .rt bottom all ++- % -++
.       .el .rt bottom all +&*2+- % -+&SYSDATE+
.   .ix '&*1'
.dm
.cm ----------
.dm itemix /.ix &*1/
.cm ----------------------------------- optix
.dm optix begin
.   .ix '&*1 Option' 'on &command'
.dm
.cm ----------------------------------- optbegin & optend
.dm optbegin /.co on/.of +16/
.dm optend /.of &SYSOF/.co off/
.cm ----------------------------------- docbegin & docend
.dm docbegin /.co on/.in +16/
.dm docend /.in -16/.co off/
.cm ----------------------------------------------------------------- title page
.ra &docnum
.ra &docpubdate
.if &version ne 'PUBLISH' .ra &version
.   .el .sp
.titlepage
.ce on
.bs /
.sr trade = 9/F
.bs
.sp 14;WYLBUR&trade Reference Manual
.sp 2;&docedition
.sp 25;Information Technology Services
Stanford University
Stanford, CA  94305
.ce off
.cm ------------------------------------------------------------- copyright page
.prelim
.sp 42
.ti set \
.if &chars eq 'U340' .ti c 8F  r 9F    ;.cm copyright and register symbols
.if &chars eq 'U670' .ti c DC  r 9F
.ce on
\c Copyright 1975, 1977, 1981, 1983, 1984 by The Board of Trustees of the
Leland Stanford Junior University
All rights reserved
Printed in the United States of America
.sp 2
WYLBUR \r is a registered trademark of the Leland Stanford Junior University
.ce off
.ti set
.cm ---------------------------------------------------------- table of contents
.if &version eq 'PUBLISH' .rt bottom all ++- % -++
.   .el .rt bottom all ++- % -+&SYSDATE+
.rt top all +Contents++&doctitle+
.contents 'TABLE OF CONTENTS'
.cm ------------------------------------------------------------ acknowledgments
.im .acknowl
.cm -------------------------------------------------------------------- preface
.im .preface
.im .syntax
.cm --------------------------------------------------------------- introduction
.im .intro
.cm ---------------------------------------------------------- command reference
.im .comref
.fo off
.im .members
.cm ----------------------------------------------------------------- appendices
.rt top all ++++
.if &version eq 'PUBLISH' .rt bottom all  ++- % -++
.   .el .rt bottom all ++- % -+&SYSDATE+
.pa odd                               ;.cm <-- page eject for odd page alignment
.im .appendA                           ;.cm abbreviations
.im .appendB                           ;.cm change count
.im .appendC                           ;.cm EXEC files
.cm ---------------------------------------------------------------------- index
.cm ----------------------------------- additional entries
.ix '(ATTN)' . 'see (BREAK)';.ix 'ATTN key' . 'see BREAK key'
.ix 'ENTER key' . 'see RETURN key'
.cw
.ix 'comment character' . 'see ;'
.ix character comment . 'see ;'
.cw ;
.cm ----------------------------------- page format
.hl 0 INDEX
.pa                                   ;.cm <-- page eject for odd page alignment
.ju off
.rt top all +Index++&doctitle+
.if &version eq 'PUBLISH' .rt bottom all ++- % -++
.   .el .rt bottom all ++- % -+&SYSDATE+
.cm ----------------------------------- format macros
.dm SYSIX0 begin
.   .cw `
.   .in
.   .sk 2
.   .cc 10
.   .bx 6 10
       &1
.   .bx off
.   .sk 2
.dm
.cw ;
.dm SYSIX1 /.in/
.dm SYSIX2 /.in/.in 3/
.dm SYSIX3 /.in/.in 6/
.cm ----------------------------------- format
.sp 5
.ce INDEX
.sp 4
.cd set 2 4
.tb set +
.sr SYSIXREF = ' + '
.tb '.'&SYSCL.r
.ix . dump
.cm ---------------------------- End of Driver ---------------------------------
//*
//PRESS    EXEC IOPROGM,COND=(0,NE),                                    NUM
//            PARM='REBLOCK,OUTFORMAT=EDIT'
//INPUT    DD *                                                         UNN
OPTIONS: UNN CC NOBACK ID=#WMAN FORMS=9511 FCB=6 CHARS=U670
//         DD DSN=&TEMP,DISP=(OLD,DELETE)                               NUM
//OUTPUT   DD DSN=WYL.GA.PUB.WYLMAN,DISP=SHR
//*
//PURGE    EXEC PGM=HCMD,COND=(0,NE),PARM='PURGE * OUTPUT'
