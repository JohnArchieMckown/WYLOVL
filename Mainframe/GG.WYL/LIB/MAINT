;WYL.GG.LSO.LIB#MAINT -- created 12/27/83 by Lincoln Ong, ITS Systems
;         latest modification 09/09/84  Version 1.11
;This EXEC file updates the various tables used by MILTEN and WYLBUR.
;
;Associated data sets:
;     WYLBUR HELP Directory
;             WYL.GA.PUB.HELPDIR         [Production]
;             WYL.GA.PUB.BHELPDIR        [Backup]
;
;     MILTEN Ports Definition Table
;     SYSA -- WYL.GS.MIL.SYSA.PORTS      [Production]
;             WYL.GS.MIL.SYSA.BPORTS     [Backup]
;
;     SYSB -- WYL.GS.MIL.RLG.PORTS       [Production]
;             WYL.GS.MIL.RLG.BPORTS      [Backup]
;
;     SYSC -- WYL.GS.MIL.SYSC.PORTS      [Production]
;             WYL.GS.MIL.SYSC.BPORTS     [Backup]
;
;     MILTEN Character Translation Tables
;             WYL.GS.MIL.TABLE           [Production]
;             WYL.GS.MIL.BTABLE          [Backup]
;
;     WYLBUR Terminal Type Table
;             WYL.GS.WYL.TERMINAL        [Production]
;             WYL.GS.WYL.BTERM           [Backup]
;
;          Variables used:
;
;   PARM = [ACTIVE][,{HELPDIR|PORTS|TABLE|TERMINAL}][,{SYSA|SYSB|SYSC}][,{BACKUP|NOBACKUP}]
;
;     S0 = original ESC setting
;     S1 = PARM
;     S2 = system id
;     S3 = table to be updated
;     S4 = table dsname
;     S5 = backup response (YES or NO)
;     S6 = working variable
;     S7 = working variable
;
;     W0 = original DELTA
;     W8 = RETURN line number for system id prompt  * RETURN1 *
;     W9 = RETURN line number for saving the new version  * RETURN2 *
;
IF (IND(PARM,'?') NE 0) WRI 'Usage is: EXEC FRO WYL.GG.WYL.LIB#MAINT' ATTN=*
   THEN WRI '          PARM=([ACTIVE][,{[HELPDIR|PORTS|TABLE|TERMINAL}]' ATTN=*
   THEN WRI '          [,{SYSA|SYSB|SYSC}][,{BACKUP|NOBACKUP}])' ATTN=*
   THEN WRI '' ATTN=*
   THEN CLR EXE
IF (IND(UPP(PARM),'ACTIVE') NE 0) EXE *+2
IF (LINES NE 0) REA PRO 'Enter SAVE for ACTIVE file or press <CR>=Clear, <ATTN>=No: ' ATTN=L
CLR VAL
IF (IND(PARM,'ACTIVE') EQ 0) CLR ACT   ;Use the ACTIVE file.

SET VAL S0=ESC
SET ESC &
END &(PARM)
CH ' ' TO '' IN L NOL
CH ',' TO ' ' IN L NOL                 ;Remove extra commas.
SQU L
CH ' ' TO ',' IN L NOL
REA UPP STR S1 USI L DEL
IF (VER(S1,',') EQ 0) SET VAL S1=''
SET VAL S6=S1
SET VAL S7=S1
SET VAL W0=DEL
SET DEL=1
SET VAL W8=105                         ; * RETURN1 * line number
SET VAL W9=214                         ; * RETURN2 * line number
;Parse the PARM for information.
SET RET=NEX
SET VAL S7=SUB(S6,1,IND(S6||',',','))
SET VAL S6=SUB(S6,IND(S6,',')+1)
IF (IND('A,B,C,ALPHA,BETA,CIT,ITS,RLG,SYSA,SYSB,SYSC,',S7) NE 0) SET VAL S2=SUB(S7,1,IND(S7,',')-1)
    THEN IF (S7 NE '') EXE RET
IF (IND('HELPDIR,PORTS,TABLE,TERMINAL,',S7) NE 0) SET VAL S3=SUB(S7,1,IND(S7,',')-1)
    THEN IF (S7 NE '') EXE RET
IF (IND('BACKUP,',S7) NE 0) SET VAL S5='YES'
    THEN IF (S7 NE '') EXE RET
IF (IND('NOBACKUP,',S7) NE 0) SET VAL S5='NO'
IF (S7 NE '') EXE RET

WRI '' ATTN=NEX
WRI 'MAINT  Revised 09 September 1984  Version 1.11' ATTN=NEX
WRI '' ATTN=NEX
WRI 'Updates the HELPDIR, PORTS, TABLE or TERMINAL table.' ATTN=NEX
WRI '' ATTN=NEX
;
;Retrieve table
;
SET RET=*+2
IF (S3 NE '') EXE *+3
REA UPP STR S3 PRO 'Table (<ATTN>=quit): ' ATTN=L-3
IF (S3 EQ '') EXE PREV
IF (IND('HELPDIR.PORTS.TABLE.TERMINAL.',S3||'.') EQ 0) WRI 'Enter one of the following:' ATTN=RET
   THEN WRI 'HELPDIR   WYLBUR HELP Directory' ATTN=RET
   THEN WRI 'PORTS     MILTEN Ports Definition Table' ATTN=RET
   THEN WRI 'TABLE     MILTEN Character Translation Tables' ATTN=RET
   THEN WRI 'TERMINAL  WYLBUR Terminal Type Table' ATTN=RET
   THEN WRI '' ATTN=RET
   THEN EXE RET

IF (S3 NE 'PORTS') EXE *+15
IF (S2 NE '') EXE *+3
REA UPP STR S2 PRO 'System (<CR>=&(MAC), <ATTN>=restart): ' ATTN=RET  ; * RETURN1 *
IF (S2 EQ '') SET VAL S2=MAC           ;System id.
IF (IND('.A.B.C.ALPHA.BETA.CIT.ITS.RLG.SYSA.SYSB.SYSC.',S2||'.') EQ 0) WRI 'Enter one of the following:' ATTN=&W8
   THEN WRI 'A, ALPHA, or SYSA       3081, System A' ATTN=&W8
   THEN WRI 'B, BETA, RLG, or SYSB   3081, System B' ATTN=&W8
   THEN WRI 'C, or SYSC              3083, System C' ATTN=&W8
   THEN WRI '' ATTN=&W8
   THEN EXE &W8
IF (IND('A.SYSA.CIT.ITS.',S2||'.') NE 0) SET VAL S2='SYSA'
IF (IND('B.BETA.RLG.SYSB.',S2||'.') NE 0) SET VAL S2='RLG'
IF (S2 EQ 'C') SET VAL S2='SYSC'
SET VAL S2=S2||'.'

;Retrieve requested table.
IF (S3 EQ 'HELPDIR') SET VAL S4='WYL.GA.PUB.HELPDIR'
IF (S3 EQ 'PORTS') SET VAL S4='WYL.GS.MIL.&S2PORTS'
IF (S3 EQ 'TABLE') SET VAL S4='WYL.GS.MIL.TABLE'
IF (S3 EQ 'TERMINAL') SET VAL S4='WYL.GS.WYL.TERMINAL'
IF (IND(S1,'ACTIVE,') EQ 0) USE &S4 CLR
IF (LINES EQ 0) WRI 'No ACTIVE file.  Table &S4 retrieved instead.' ATTN=*
   THEN USE &S4 CLR
IF (S1 NE '') THEN WRI 'Table=&S3  Dsname=&S4' ATTN=*  ;Table was specified in the PARM.
WRI '' ATTN=*
WRI 'Make your modifications and type EXEC when ready.' ATTN=*
EXE PAU
;
;Verify updated table
;
                                       ;Check dsname.
IF (S3 EQ 'HELPDIR') IF (IND(LIN(3),';; *  &S4 ') EQ 0) WRI S'2F'||'ERROR: Invalid WYLBUR HELP Directory data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'PORTS') IF (IND(LIN(3),';; *  &S4 ') EQ 0) WRI S'2F'||'ERROR: Invalid MILTEN Ports Definition Table data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'TABLE') IF (IND(LIN(3),';; *  &S4 ') EQ 0) WRI S'2F'||'ERROR: Invalid MILTEN Character Translation Tables data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'TERMINAL') IF (IND(LIN(3),';; *  &S4 ') EQ 0) WRI S'2F'||'ERROR: Invalid WYLBUR Terminal Type data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
                                       ;Verify table version.
POI C'[Production] ' 54 IN F(20) NOL
IF (* EQ -1.000) WRI S'2F'||'ERROR: Invalid table version or missing version name' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
                                       ;Check for "[End of..." line at end of the data set.
IF (S3 EQ 'HELPDIR') IF (LIN(L) NE ';;[End of WYLBUR HELP Directory]') WRI S'2F'||'ERROR: Missing ";;[End of WYLBUR HELP Directory]" line at end of data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'PORTS') IF (LIN(L) NE ';;[End of MILTEN Ports Definition Table]') WRI S'2F'||'ERROR: Missing ";;[End of MILTEN Ports Definition Table]" line at the end of data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'TABLE') IF (LIN(L) NE ';;[End of MILTEN Character Translation Tables]') WRI S'2F'||'ERROR: Missing ";;[End of MILTEN Character Translation Tables]" line at the end of data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
IF (S3 EQ 'TERMINAL') IF (LIN(L) NE ';;[End of WYLBUR Terminal Type Table]') WRI S'2F'||'ERROR: Missing ";;[End of WYLBUR Terminal Type Table]" line at the end of data set' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-3
                                       ;Check for creation date.
POI C';; *  Created on ' 1 NOL
IF (* EQ -1.000) WRI S'2F'||'ERROR: Missing creation date' ATTN=*
   THEN WRI '       Correct and type EXEC when ready.' ATTN=*
   THEN EXE PAU
   THEN EXE *-4
;
;Create a backup copy
;
                                       ;Update the last updated line.
POI C';; *  Updated on ' 1 NOL
IF (* EQ -1.000) POI C';; *  Created on ' 1 NOL
   THEN *+0.01 ;; *  Updated on &(SUB(DAT,10,8)) at &(SUB(DAT,1,8)) by &(SUB(ACC,5,2)).&(SUB(ACC,1,3)) &(SUB('',1,18)) *
   ELSE * ;; *  Updated on &(SUB(DAT,10,8)) at &(SUB(DAT,1,8)) by &(SUB(ACC,5,2)).&(SUB(ACC,1,3)) &(SUB('',1,18)) *
NUM

IF (S5 NE '') EXE *+3
REA UPP STR S5 PRO 'Backup (<CR>=yes, <ATTN>=no): ' ATTN=&W9
IF (S5 EQ '') EXE *+6
IF (IND('N.Y.NO.OK.YES.',S5||'.') EQ 0) WRI 'Answer YES or NO.' ATTN=*
   THEN WRI '' ATTN=*
   THEN EXE *-4
IF (IND('N.NO.',S5||'.') NE 0) EXE &W9

COP FRO * PLU 10000                    ;Copy current version to end.
CH '[Production] ' 54 TO '[Backup]     ' IN 10000(4) NOL
                                       ;Put timestamp on backup version creation.
IF (S3 EQ 'PORTS') IF (S2 EQ 'RLG') POI C';; *  Last RLIN ' 1 IN 10000/L NOL
   ELSE POI C';; *  Updated ' 1 IN 10000/L NOL
*+0.01 ;; * &(SUB('',1,61)) *
*+0.01 ;; *  Backup version created on &(SUB(DAT,10,8)) at &(SUB(DAT,1,8)) by &(SUB(ACC,5,2)).&(SUB(ACC,1,3))     *
NUM 10000/L STA 10001
IF (S3 EQ 'HELPDIR') CH '.HELPDIR ' TO '.BHELPDIR' IN 10000(4) NOL
   THEN SAV WYL.GA.PUB.BHELPDIR LINS=10000/L REP
IF (S3 EQ 'PORTS') CH '.&S2PORTS ' TO '.&S2BPORTS' IN 10000(4) NOL
   THEN SAV WYL.GS.MIL.&S2BPORTS LINS=10000/L REP
IF (S3 EQ 'TABLE') CH '.TABLE ' TO '.BTABLE' IN 10000(4) NOL
   THEN SAV WYL.GS.MIL.BTABLE LINS=10000/L REP
IF (S3 EQ 'TERMINAL') CH '.TERMINAL ' TO '.BTERM    ' IN 10000(4) NOL
   THEN SAV WYL.GS.WYL.BTERM LINS=10000/L REP

SAV &S4 LINS=F/9999.999 REP CLR        ;Save the updated version.  * RETURN2 *
                                       ;Update the table (if needed) on other systems also.
IF (IND('HELPDIR.TERMINAL.',S3||'.') EQ 0) EXE L-3
END //&(SUB(ACC,1,3))MAINT JOB ,'Table Maintenance',CLASS=E
END //*
END //* &S3 Table Maintenance on &(SUB(DAT,10,8)) at &(SUB(DAT,1,8))
END //*
END //BATWYL   EXEC BATWYL
END //SYSIN    DD *
IF (S3 EQ 'HELPDIR') END SET WHELP
IF (S3 EQ 'TERMINAL') END SET WTERM
END //*
END //PURGE    EXEC PGM=HCMD,COND=(0,NE),PARM='PURGE * OUTPUT'
IF (MAC EQ 'SYSA') SET VAL S6='SYSB,SYSC,'
IF (MAC EQ 'RLG') SET VAL S6='SYSA,SYSC,'
IF (MAC EQ 'SYSC') SET VAL S6='SYSA,SYSB,'
SET VAL S7=SUB(S6,1,IND(S6,',')-1)
SET VAL S6=SUB(S6,IND(S6,',')+1)
RUN NOT SYSAFF=&S7
IF (S6 NE '') EXE *-3
CLR ACT

SET DEL=&W0
SET ESC &S0
CLR VAL
CLR EXE
