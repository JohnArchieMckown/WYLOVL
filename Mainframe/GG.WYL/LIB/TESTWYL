; Modified 4/83, "Niz"
SET ESC &
COMM   *   *   *   *   * * * *
COMM    * * * * * * * *  * * *   *** Install TESTWYL ***
COMM     *   *   *   *   * * *
;
SET VAL S7 SUB(ACC,1,3)
COMM **** WILL COPY LOAD MODULE FROM WYL.GG.JDN.LINKLIB
S7 = 'JDN'  ; DO NOT LINK FROM SCH OR OTHER USER LINKLIB
IF (SUB(ACC,1,3) NE 'WYL') EXE 12
REA STR S7 PRO 'User? JDN' UPPER
IF (S7 EQ '') SET VAL S7 'JDN'
IF (SIZ(S7) NE 3) EXE 8
;
SET VAL W0 END
END //WYL$NLNK JOB GS.WYL,'Install TESTWYL',REGION=512K
SET VAL S6 SUB(DAT,1,5)||' '||SUB(DAT,10,5)||': '
READ STR S9 PRO '&S6' UPLOW
SET VAL S0 S9
IF (S0 EQ '') EXE 15
SET VAL S0 S6||S0
IF (SIZ(S0) GT 235)
THEN SET VAL N0 SIZ(S0)-235
THEN COMM MSG &N0 CHARS TOO LONG
THEN EXEC 15
;
END //VERS EXEC PGM=AMASPZAP,REGION=64K
END //SYSPRINT DD SYSOUT=A
END //SYSLIB DD DSN=WYL.GG.&S7.LINKLIB,DISP=SHR
END //SYSIN DD *
END NAME WYLBUR VERSION
END * &(SUB(S0,2,69))
SET VAL N0 0 ; START AT OFFSET 0
SET VAL S0 SUB(S0,1,235)
IF (SIZ(S0) GT 32)
THEN END REP &(SUB(DIS(N0),7)) &(DIS(SUB(S0,1,32)))
THEN SET VAL S0 SUB(S0,33)
THEN SET VAL N0 N0+32
THEN EXE 31
END REP &(SUB(DIS(N0),7)) &(DIS(S0))
;
END,//COPY EXEC PGM=IEBCOPY
END,//SYSPRINT DD SYSOUT=A
END,//TEST   DD DSN=WYL.GG.&S7.LINKLIB,DISP=SHR
END,//NEW    DD DSN=WYL.GS.WYL.LINKLIB,DISP=SHR
END,//SYSIN DD *
END, COPY INDD=TEST,OUTDD=NEW
END, SELECT MEMBER=((WYLBUR,,R))
;
END //$COND  EXEC PGM=IEBCOPY,COND=(0,NE,COPY)
END //SYSPRINT DD SYSOUT=A
END //SYSIN DD DUMMY
END //SYSUT1 DD DSN=WYL.GS.WYL.LINKLIB,DISP=SHR
END //SYSUT2 DD DSN=WYL.GS.WYL.LINKLIB,DISP=SHR
END // EXEC NOLIST
;
RUN &W0/L HOLD NOTIFY
DEL &W0/L
SET ESC
CLR EXEC
