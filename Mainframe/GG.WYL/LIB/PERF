; 3/82 "NIZ", updated 06/17/82 LSO V1.01
COMM WYLBUR Resource Usage Information
COMM
COMM (Intervals less than a few seconds won't be too accurate.)
COMM
IF (LINES NE 0) REA PRO 'Enter <return> if ok to clear... '
COMM Interval starts now...
SET VAL S2 DATE
SET VAL N0 SUB(S2,1,2)*60*60+SUB(S2,4,2)*60+SUB(S2,7,2) ; SECS
DUMP WINFO CLR
P C'CPU TIME =' (1) N
SET VAL N1 SUB(LINE(*),12,2)*60*60+SUB(LINE(*),15,2)*60+SUB(LINE(*),18,2); CPU SECS TOTAL
P C'DISK I/O''S' (1) IN NEXT/L N
SET VAL N2 SUB(LINE(*),1,INDEX(LINE(*),' ')); TOTAL DISK I/O'S
P C'PAGING:'1 (1) IN NEXT/L N
SET VAL S0 LINE(*)
SET VAL S0 SUB(S0,9)
SET VAL N3 SUB(S0,1,INDEX(S0,'I/O')-1); TOTAL PAGING I/O'S
; OLD DATA NOW COLLECTED
;
REA STR S0 PRO 'Type <return> to end interval...'
SET VAL S3 DATE
SET VAL N0 (SUB(S3,1,2)*60*60+SUB(S3,4,2)*60+SUB(S3,7,2))-N0 ; ELAPSED SECS
DUMP WINFO CLR
P C'CPU TIME =' (1) N
SET VAL N1 (SUB(LINE(*),12,2)*60*60+SUB(LINE(*),15,2)*60+SUB(LINE(*),18,2))-N1; CPU SECS USED
P C'DISK I/O''S' (1) N
SET VAL N2 SUB(LINE(*),1,INDEX(LINE(*),' '))-N2; DISK I/O'S
P C'PAGING:'1 (1) IN NEXT/L N
SET VAL S0 LINE(*)
SET VAL S0 SUB(S0,9)
SET VAL N3 SUB(S0,1,INDEX(S0,'I/O')-1)-N3; PAGING I/O'S
;
CLE ACT
COMM
SET ESC &
COMM WYLBUR Statistics on &S3
COMM Interval:  &N0 seconds
COMM CPU time:  &N1 seconds (&((N1*100)/N0)%)
COMM File I/O:  &(WCO(N2)/N0) I/O's
COMM Page I/O:  &(WCO(N3)/N0) I/O's
SET ESC
EXE F PAU
