; Modified 2/85 "Niz"
SET ESC &
COMM *** Install RLG WYLBUR ***
COMM
set val s7 'JDN' ; Copy from my linklib to prod
;
REA STR S0 PRO 'Backup (Y/N)? ' UPPER
IF (S0 EQ '') EXE 10
SET VAL S8 'YES'
IF (IND(S0,'Y')+IND(S0,'OK') EQ 0) SET VAL S8 'NO'
;
write '(RLG machine only!)'
;-- REA STR S0 PRO 'A(lpha) R(lg): ' UPP
SET VAL N1 0;         INDEX (S0,'A') ; install on Alpha machine
SET VAL N2 1;         INDEX (S0,'R') ; install on RLG machine
IF (N1+N2 EQ 0) EXE 13.2
COMM
;
SET VAL W0 END
END //WYL$LINK JOB GS.WYL,'Install WYLBUR',REGION=512K
IF (S8 EQ 'YES') LAST //WYL$BLNK JOB GS.WYL,'Install WYLBUR',REGION=512K
SET VAL S6 SUB(DAT,1,5)||' '||SUB(DAT,10,5)||': '
READ STR S9 PRO '&S6' UPLOW
SET VAL S0 S9
IF (S0 EQ '') EXE 40
SET VAL S0 S6||S0
IF (SIZ(S0) GT 235)
THEN SET VAL N0 SIZ(S0)-235
THEN COMM MSG &N0 CHARS TOO LONG
THEN EXEC 40
END //VERS EXEC PGM=AMASPZAP,REGION=64K
END //SYSPRINT DD SYSOUT=A
END //SYSLIB DD DSN=WYL.GG.&S7.LINKLIB,DISP=SHR
END //SYSIN DD *
END NAME WYLBUR VERSION
END * &(SUB(S0,2,69))
SET VAL N0 0 ; START AT OFFSET 0
SET VAL S0 SUB(S0,1,235)
IF (SIZ(S0) GT 32)
THEN END REP &(SUB(DIS(N0),7)) &(DIS(SUB(S0,1,32)))
THEN SET VAL S0 SUB(S0,33)
THEN SET VAL N0 N0+32
THEN EXE 56
END REP &(SUB(DIS(N0),7)) &(DIS(S0))
IF (S8 NE 'YES') EXEC 61.13
END,//BCOPY EXEC PGM=IEBCOPY
END,//SYSPRINT DD SYSOUT=A
IF (N1 NE 0) END,//OLDSYSA DD DSN=WYL.GS.WYL.SYSA.BLINKLIB,DISP=SHR
IF (N2 NE 0) END,//OLDRLG   DD DSN=WYL.GS.WYL.RLG.BLINKLIB,DISP=SHR
IF (N1 NE 0) END,//SYSA    DD DSN=WYL.GS.WYL.SYSA.LINKLIB,DISP=SHR
IF (N2 NE 0) END,//RLG      DD DSN=WYL.GS.WYL.RLG.LINKLIB,DISP=SHR
END,//SYSIN DD *
IF (N1 NE 0) END, COPY INDD=((SYSA,R)),OUTDD=OLDSYSA
IF (N2 NE 0) END, COPY INDD=((RLG,R)),OUTDD=OLDRLG
END,//COPY EXEC PGM=IEBCOPY
IF (S8 EQ 'YES') LAST,//COPY EXEC PGM=IEBCOPY,COND=(0,NE,BCOPY)
END,//SYSPRINT DD SYSOUT=A
END,//TEST DD DSN=WYL.GG.&S7.LINKLIB,DISP=SHR
IF (N1 NE 0) END,//SYSA    DD DSN=WYL.GS.WYL.SYSA.LINKLIB,DISP=SHR
IF (N2 NE 0) END,//RLG      DD DSN=WYL.GS.WYL.RLG.LINKLIB,DISP=SHR
END,//SYSIN DD *
IF (N1 NE 0) END, COPY INDD=TEST,OUTDD=SYSA
THEN         END, SELECT MEMBER=((WYLBUR,,R))
IF (N2 NE 0) END, COPY INDD=TEST,OUTDD=RLG
THEN         END, SELECT MEMBER=((WYLBUR,,R))
;
IF (S8 NE 'YES') EXE 66.1
IF (N1 NE 0)
THEN END,//$COND1 EXEC PGM=IEBCOPY,COND=(0,NE,COPY)
THEN END,//SYSPRINT DD SYSOUT=A
THEN END,//SYSIN DD DUMMY
THEN END,//SYSUT1 DD DSN=WYL.GS.WYL.SYSA.BLINKLIB,DISP=SHR
THEN END,//SYSUT2 DD DSN=WYL.GS.WYL.SYSA.BLINKLIB,DISP=SHR
IF (N2 NE 0)
THEN END,//$COND2 EXEC PGM=IEBCOPY,COND=(0,NE,COPY)
THEN END,//SYSPRINT DD SYSOUT=A
THEN END,//SYSIN DD DUMMY
THEN END,//SYSUT1 DD DSN=WYL.GS.WYL.RLG.BLINKLIB,DISP=SHR
THEN END,//SYSUT2 DD DSN=WYL.GS.WYL.RLG.BLINKLIB,DISP=SHR
;
IF (N1 NE 0)
THEN END,//$COND3 EXEC PGM=IEBCOPY,COND=(0,NE,COPY)
THEN END,//SYSPRINT DD SYSOUT=A
THEN END,//SYSIN DD DUMMY
THEN END,//SYSUT1 DD DSN=WYL.GS.WYL.SYSA.LINKLIB,DISP=SHR
THEN END,//SYSUT2 DD DSN=WYL.GS.WYL.SYSA.LINKLIB,DISP=SHR
IF (N2 NE 0)
THEN END,//$COND4 EXEC PGM=IEBCOPY,COND=(0,NE,COPY)
THEN END,//SYSPRINT DD SYSOUT=A
THEN END,//SYSIN DD DUMMY
THEN END,//SYSUT1 DD DSN=WYL.GS.WYL.RLG.LINKLIB,DISP=SHR
THEN END,//SYSUT2 DD DSN=WYL.GS.WYL.RLG.LINKLIB,DISP=SHR
END // EXEC NOLIST
RUN &W0/L HOLD NOTIFY
DEL &W0/L
SET ESC
CLR EXEC
