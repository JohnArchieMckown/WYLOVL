*  $MAC(RACAUDIT) COMP(SC1BN) PROD(RACF):                               00013300
*                                                                       00013800
*  DESCRIPTIVE NAME = INTERFACE TO RACF AUDITING FUNCTION               00015300
*                                                                       00015400
* *01* PROPRIETARY STATEMENT=                                         * 00043500
*  **PROPRIETARY_STATEMENT********************************************* 00071600
*                                                                     * 00099700
*                                                                     * 00127800
*   LICENSED MATERIALS - PROPERTY OF IBM                              * 00155900
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                       * 00184000
*   5647-A01 (C) COPYRIGHT IBM CORP. 1989, 2000                       * 00212100
*                                                                     * 00240200
*   STATUS= HBB7703                                                   * 00268300
*                                                                     * 00296400
*  **END_OF_PROPRIETARY_STATEMENT************************************** 00324500
*                                                                    *  00353600
*01* EXTERNAL CLASSIFICATION:                                        *  00357400
*02* NONE:                                                           *  00361200
*01* END OF EXTERNAL CLASSIFICATION:                                 *  00365000
*                                                                    *  00369000
*  FUNCTION:                                                            00637500
*    THE RACAUDIT MACRO PROVIDES A STANDARD INTERFACE TO THE RACF       00675000
*    AUDITING ROUTINE ICHAUD00                                          00712500
*                                                                       00750000
*  INVOCATION:                                                          00787500
*    THE RACF AUDIT ROUTINE IS INVOKED BY THE RACROUTE REQUEST=AUDIT    00825000
*    MACRO.                                                             00862500
*                                                                       00900000
*   REQUIREMENTS FOR USE - USER MUST BE APF AUTHORIZED, SUP STATE       00950000
*                          OR SYSTEM KEY (0-7)                          01000000
*                                                                       01050000
*  MODULE TYPE:                                                         01083300
*    MACRO                                                              01116600
*                                                                       01150000
*  CHANGE ACTIVITY:                                                     01200000
*   $L0= VTAMSLS RACF/190 880803 PDPWB1: VTAM SESSION SECURITY     @L0A 01227700
*   $P1= PGW0043 RACF/190 881102 PDPWB1: PGW0043                   @P1A 01255400
*   $P2= PGW0045 RACF/190 881102 PDPWB1: PGW0045                   @P2A 01283100
*   $P3= PGW0042 RACF/190 881102 PDPWB1: PGW0042                   @P3A 01310800
*   $P4= PGP0408 RACF/190 890721 PDPWB1: PGP0408                   @P4A 01338500
*   $D1= DCR0077 RACF/190 890814 PDJCD1: DCR0077                   @D1A 01366200
*   $P5= PGP0550 RACF/190 890915 PDB_S1: PGP0550                   @P5A 01393900
*   $P6= PI90009 RACF/190 891010 PDB_S1: PI90009                   @P6A 01421900
*   $L1= RELID   RACF/192 900904 PDAHW1: SUPPORT FOR RACF 1.9.2    @L1A 01431200
*   $P7= PJA0029 RACF/192 910524 PDPSB1: PJA0029                   @P7A 01440500
*   $L2= OW04015 RACF/210 930426 PDKAF1: SUPPORT FOR RACF 2.1      @L2A 01443600
*   $L3= OW10709 RACF/220 940509 PDJMV1: SUPPORT FOR RACF 2.2      @L3A 01446700
*   $L4= RELID   RACF/230 960422 PDJMV1: SUPPORT FOR RACF 2.3      @L4A 01448300
*   $P8= PQC1216 RACF/230 960801 PDBCC1: PQC1216                   @P8A 01449100
*   $L5= RELID   RACF/240 960904 PDJMV1: RELEASE ID SUPPORT 2.4    @L5A 01449500
*   $L6= RELID   RACF/260 970728 PDCAM1: RELEASE ID FOR HRF2260    @L6A 01449700
*   $L7= RELID   HRF2608  980624 PDRDS2: RELEASE ID FOR HRF2608    @L7A 01449800
*   $L8= RELID   HRF7703  990429 PDRDS2: RELEASE ID FOR HRF7703    @L8A 01449900
*                                                                       01450000
*  CHANGE DESCRIPTIONS:                                                 01500000
*   A000000-999999 ORIGINAL CODE                                   @L0A 01550000
*   A - START PLIST ON A WORD BOUNDARY                             @P1A 01600000
*   A - START BAL ON A WORD BOUNDARY                               @P2A 01650000
*   A - ASSEMBLER F COMPATIBILITY                                  @P3A 01700000
*   A - GENERALIZED AUDIT FUNCTION, LOGSTR & RESULT KEYWORDS ADDED @D1A 01716600
*   A - ALLOW PARENS AROUND RELEASE                                @P4A 01733200
*   D - DELETE REFERENCES TO ERTN070 FOR ASSEMBLER F COMPATIBILITY @P5A 01741600
*   C - CORRECT THE PARAMETER LIST RESERVED STORAGE                @P6A 01745800
*   C - ALLOW RACF RELEASE 1.9.2 TO BE SPECIFIED                   @L1A 01747200
*   C - STATUS TO CORRECTLY CORRESPOND TO MVS LEVEL                @P7A 01748600
*   C - ALLOW RACF RELEASE 2.1 TO BE SPECIFIED                     @L2A 01749300
*   C - ALLOW RACF RELEASE 2.2 TO BE SPECIFIED                     @L3A 01749600
*   C - ALLOW RACF RELEASE 2.3 TO BE SPECIFIED                     @L4A 01749800
*   C - CDPI CLASSIFICATION NONE                                   @P8A 01749900
*   A - ALLOW RACF RELEASE 2.4 TO BE SPECIFIED                     @L5A 01766600
*   A - ALLOW RACF RELEASE 2.6 TO BE SPECIFIED FOR HRF2260         @L6A 01774900
*   A - ALLOW RACF RELEASE 2608 TO BE SPECIFIED FOR HRF2608        @L7A 01779100
*   A - ALLOW RACF RELEASE 7703 TO BE SPECIFIED FOR HRF7703        @L8A 01781200
*                                                                       01783300
********************************************************************    01800000
         MACRO                                                          01850000
&SYMBOL RACAUDIT &ACEE=,                                           @D1C*01900000
               &CLASS=,                                                *01950000
               &ENTITYX=,                                              *02000000
               &EVENT=,                                                *02050000
               &EVQUAL=,                                               *02100000
               &RELEASE=,                                              *02150000
               &RESULT=,                                               *02166600
               &LOGSTR=,                                               *02183200
               &MF=S                                                    02200000
.*  DEFINE LOCAL VARIABLES                                              02250000
         LCLA  &AUDVERS            USED TO CONTAIN VERSION NUMBER       02300000
         LCLA  &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7          02350000
         LCLA  &BITA0,&BITA1,&BITA2,&BITA3                              02400000
         LCLA  &BITA4,&BITA5,&BITA6,&BITA7                              02450000
         LCLC  &NX                                                 @P3A 02500000
         LCLC  &EVN000                                             @P3A 02550000
         LCLC  &CLS000                                             @P3A 02600000
.*  DEFINE GLOBAL VARIABLES                                             02650000
         GBLC  &ICHL               LABEL TO START RACAUDIT PLIST        02700000
         GBLC  &ICHMF              AN INDICATOR OF THE MACRO            02750000
.*                                 FORMAT - L, E, S OR M                02800000
         GBLA  &ICHCT              ICHCT - A COUNTER USED TO KEEP       02850000
.*                                 TRACK OF WHERE IN THE PARAMETER      02900000
.*                                 LIST THE PARAMETER IS TO BE          02950000
.*                                 PLACED. ICHCT IS INITIALIZED TO      03000000
.*                                 THE FIRST FULLWORD IN THE LIST       03050000
.*                                 TO BE FILLED IN BY ICHMM,            03100000
.*                                 AND INCREMENTED BY THE INNER         03150000
.*                                 MACRO (ICHMM).                       03200000
         GBLB  &ICHLIST            USED TO TELL THE ICHMM MACRO    @L0A 03250000
.*                                 WHETHER LIST OR EXECUTE PASS    @L0A 03300000
.*                                 IS BEING PROCESSED              @L0A 03350000
         GBLB  &ICHRAC             ICHRAC - A FLAG WHICH IS             03400000
.*                                 INITIALIZED IN THE RACROUTE MACRO    03450000
.*                                 TO COMMUNICATE TO RACXTRT THAT IT    03500000
.*                                 IS BEING INVOKED AS AN INNER MACRO   03550000
.*                                 TO THE RACROUTE MACRO.               03600000
.*                                 EXECUTE PASS.                        03650000
         GBLB  &ICHNTFY            ICHNTFY - A GLOBAL INDICATOR         03700000
.*                                 WHICH IS USED TO COMMUNICATE WITH    03750000
.*                                 ICHMM - THE INNER MACRO. IF ICHNTFY  03800000
.*                                 IS ON, THE INNER MACRO WILL NOT      03850000
.*                                 ISSUE ITS OWN MNOTE IF IT FINDS AN   03900000
.*                                 ERROR-IT WILL MERELY TURN ON THE     03950000
.*                                 ICHINER FLAG AS NOTIFICATION TO THE  04000000
.*                                 OUTER MACRO, WHICH WILL TAKE THE     04050000
.*                                 APPROPRIATE ACTION.                  04100000
.*                                 IF ICHNTFY IS OFF, THE INNER MACRO   04150000
.*                                 WILL TAKE ACTION ON ITS OWN,         04200000
.*                                 IE. ISSUE AN MNOTE WITH A SEVERITY   04250000
.*                                 CODE OF ITS CHOOSING.                04300000
         GBLB  &ICHINER            ICHINER - A GLOBAL INDICATOR         04350000
.*                                 WHICH IS USED BY ICHMM - AN INNER    04400000
.*                                 MACRO - TO TELL THE OUTER MACRO IF   04450000
.*                                 AN ERROR HAS BEEN DETECTED IN A      04500000
.*                                 PARAMETER.  IF ICHINER IS ON, AN     04550000
.*                                 ERROR HAS BEEN DETECTED.             04600000
.********************************************************************** 04650000
.*                     INITIALIZE VARIABLES                           * 04700000
.********************************************************************** 04750000
&NX      SETC  '&SYSNDX'                                                04800000
&ICHNTFY SETB  0                   WANT ICHMM TO ISSUE ERROR MSGS       04850000
&ICHINER SETB  0                   NO ERRORS YET FROM ICHMM             04900000
&AUDVERS SETA  0                   VERSION 0 OF AUDIT PLIST             04950000
.*                                 NOTE: THE MAXIMUM PLIST VERSION      05000000
.*                                   SHOULD BE BE INCREMENTED WHEN      05050000
.*                                   A NEW FIELD IS ADDED (I.E. AS      05100000
.*                                   NEW KEYWORDS ARE ADDED TO THE      05150000
.*                                   MACRO). THE VERSION OF THE PLIST   05200000
.*                                   WILL BE DETERMINED BY THE KEYWORDS 05250000
.*                                   SPECIFIED. FOR EXAMPLE IF THE      05300000
.*                                   MAXIMUM CURRENT VERSION OF THE     05350000
.*                                   PLIST IS 1 BUT NO VERSION 1        05400000
.*                                   KEYWORDS ARE CODED THEN A VERSION  05450000
.*                                   0 PLIST WILL BE GENERATED.         05500000
         AIF   (NOT &ICHRAC).ERR010 RACAUDIT MUST BE INVOKED VIA        05550000
.*                                   RACROUTE REQUEST=AUDIT             05600000
.ERRTN10 ANOP                      RETURN POINT FROM ERROR 10      @P3C 05650000
.********************************************************************** 05700000
.*  VALIDATE RELEASE AND MF                                           * 05750000
.********************************************************************** 05800000
.****  ERROR INVALID RELEASE                                       @L2C 05816600
         AIF   ('&RELEASE(1)' NE '1.9' AND '&RELEASE(1)' NE            X05833300
                '1.9.2' AND '&RELEASE(1)' NE '2.1'                     X05855500
                AND '&RELEASE(1)' NE '2.2'                             X05866600
                AND '&RELEASE(1)' NE '2.3'                             X05874900
                AND '&RELEASE(1)' NE '2.4'                             X05883200
                AND '&RELEASE(1)' NE '2.6'                             X05888800
                AND '&RELEASE(1)' NE '2608'                            X05892500
                AND '&RELEASE(1)' NE '7703').ERR015                @L8A 05896200
.ERRTN15 ANOP                                                      @P3C 05900000
         AIF   ('&MF' NE 'L' AND '&MF' NE 'S' AND '&MF(1)' NE 'M' AND  X05950000
               '&MF(1)' NE 'E').ERR020      MF MUST BE S, L, M OR E     06000000
.*                                    NOTE: RACROUTE MACRO WILL CONVERT 06050000
.*                                          MF=M TO MF=E IN &ICHMF      06100000
.*                                          BEFORE CALLING RACAUDIT     06150000
.ERRTN20 ANOP                      RETURN POINT FROM ERROR 20      @P3C 06200000
         AIF   ('&ICHMF' EQ 'E' AND (N'&MF NE 2 OR                     X06250000
               '&MF(2)' EQ '')).ERR030   MF=E MUST HAVE PLIST ADDR PARM 06300000
.ERRTN30 ANOP                      RETURN POINT FROM ERROR 30      @P3C 06350000
         AIF   ('&ICHMF' NE 'S').SKIPCHK SKIP CHECK FOR REQUIRED   @P3C 06400000
.*                                      MF=S PARMS IF NOT MF=S          06450000
.********************************************************************** 06500000
.*        MAKE SURE ALL REQUIRED PARMS SUPPLIED FOR MF=S              * 06550000
.********************************************************************** 06600000
.*                                                                1@P5D 06650000
         AIF   ('&EVENT' EQ '').ERR050    EVENT REQUIRED                06700000
         AIF   ('&EVQUAL' EQ '').ERR060   EVQUAL REQUIRED               06750000
.*                                                                1@P5D 06800000
.ERRTN50 ANOP                             RETURN FROM ERROR 50     @P3C 06900000
.ERRTN60 ANOP                             RETURN FROM ERROR 60     @P3C 06950000
.*                                                                 @D1D 07000000
         CNOP  0,4                        ALIGN BAL TO WORD BNDY   @P2C 07050000
         BAL   1,ICH&NX.A                 BRANCH AROUND PLIST, SET R1   07100000
.SKIPCHK ANOP                                                      @P3C 07150000
.********************************************************************** 07200000
.*        SET UP FOR LITERALS IF ANY SUPPLIED                         * 07250000
.********************************************************************** 07300000
&EVN000  SETC  '&EVENT'                                                 07350000
         AIF   ('&EVENT' EQ '').SKIPLT1          SKIP IF NO EVENT  @P3C 07400000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT1   SKIP NOT LITERAL  @P3C 07450000
&EVN000  SETC  'ICH&NX.E'                 SUBSTITUTE LABEL OF LITERAL   07500000
.*                                         THAT WILL BE GENERATED       07550000
.SKIPLT1 ANOP                                                      @P3C 07600000
&CLS000  SETC  '&CLASS'                                                 07650000
         AIF   ('&CLASS' EQ '').SKIPLT2          SKIP IF NO CLASS  @P3C 07700000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT2   SKIP NOT LITERAL  @P3C 07750000
&CLS000  SETC  'ICH&NX.C'                 SUBSTITUTE LABEL OF LITERAL   07800000
.*                                         THAT WILL BE GENERATED       07850000
.SKIPLT2 ANOP                                                      @P3C 07900000
         AIF   ('&ICHMF' EQ 'E').MFECONS IF MF=E GO GENERATE            07950000
.*                                           LITERAL CONSTANTS          08000000
.********************************************************************** 08050000
.*                                                                    * 08100000
.*             GENERATE LIST PASS FOR MF=L AND MF=S                   * 08150000
.*                                                                    * 08200000
.********************************************************************** 08250000
&ICHLIST SETB  1                          INDICATE TO ICHMM THAT LIST   08300000
.*                                        PASS IS BEING PROCESSED       08350000
.*    ***************************************************************** 08400000
.*    **   SET VERSION AND LENGTH                                    ** 08450000
.*    ***************************************************************** 08500000
&ICHL    DS    0F                         START OF RACAUDIT PLIST  @P1C 08550000
ICH&NX.B DC    AL2(&AUDVERS)              VERSION                       08600000
         DC    AL2(ICH&NX.F-ICH&NX.B)     LENGTH                        08650000
.*    ***************************************************************** 08700000
.*    **   EVENT KEYWORD                                             ** 08750000
.*    ***************************************************************** 08800000
          ICHMM &EVN000                                                 08850000
.*    ***************************************************************** 08900000
.*    **   EVQUAL KEYWORD                                            ** 08950000
.*    ***************************************************************** 09000000
         AIF   ('&EVQUAL' NE '').CHKREG1  NO EVQUAL, SKIP DC(0)    @P3C 09050000
         DC    AL2(0)                     EVENT QUALIFIER               09100000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     09150000
.CHKREG1 ANOP                             SEE IF REGISTER PASSED   @P3C 09200000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM1  IF REGISTER PASSED @P3C 09250000
.*                                        SKIP OVER LITERAL DC          09300000
         DC    AL2(&EVQUAL)               EVENT QUALIFIER               09350000
         AGO   .RESFLD                    GO DECLARE RESERVED FIELD     09400000
.REGFRM1 ANOP                             REGISTER FORM USED       @P3C 09450000
         AIF   ('&ICHMF' EQ 'L').ERR080   REGISTER ILLEGAL FOR MF=S     09500000
.ERRTN80 ANOP                             RETURN FROM ERROR 75     @P3C 09550000
         DC    AL2(0)                     EVENT QUALIFIER               09600000
.RESFLD  ANOP                                                           09650000
         DC    AL2(0)                     RESERVED                      09700000
.*    ***************************************************************** 09750000
.*    **   CLASS KEYWORD                                             ** 09800000
.*    ***************************************************************** 09850000
         ICHMM &CLS000                                                  09900000
.*    ***************************************************************** 09950000
.*    **   ENTITYX KEYWORD                                           ** 10000000
.*    ***************************************************************** 10050000
         ICHMM &ENTITYX                                                 10100000
.*    ***************************************************************** 10150000
.*    **   ACEE KEYWORD                                              ** 10200000
.*    ***************************************************************** 10250000
         ICHMM &ACEE                                                    10300000
.*    ***************************************************************** 10302200
.*    **   LOGSTR KEYWORD                                        @D1A** 10304400
.*    ***************************************************************** 10306600
         ICHMM &LOGSTR                                           @D1A   10308800
.*    ***************************************************************** 10311000
.*    **   VALIDATE AND GENERATE RESULT BYTE                     @D1A** 10313200
.*    ***************************************************************** 10315400
         AIF   ('&RESULT' EQ '' OR '&RESULT' EQ 'SUCCESS').NORES @D1A   10317600
         AIF   ('&RESULT' NE 'FAILURE').RESERR                   @D1A   10319800
.*                                                                      10322000
         DC    AL1(1)            RESULT OF FAILURE SPECIFIED     @D1A   10324200
         DC    AL3(0)            RESERVED                        @D1A   10326400
         AGO   .CONT1                                            @D1A   10328600
.RESERR  ANOP                                                    @D1A   10330800
         AGO   .ERR090                                           @D1A   10333000
.ERRTN90 ANOP                                                    @D1A   10335200
.NORES   ANOP                                                    @D1A   10337400
         DC    AL1(0)            RESULT BYTE                     @D1A   10339600
         DC    AL3(0)            RESERVED                        @D1A   10341800
         AGO   .CONT1                                            @D1A   10344000
.CONT1   ANOP                                                    @D1A   10346200
         DC    4F'0'                      RESERVED               @P6C   10350000
ICH&NX.F DS    0H                         END OF RACAUDIT PLIST         10400000
         AGO   .MFLCONS           GO GENERATE CONSTANTS FOR MF=L,S @P3C 10450000
.********************************************************************** 10500000
.*        GENERATE LITERAL CONSTANTS IF NEEDED                        * 10550000
.********************************************************************** 10600000
.MFECONS ANOP                        MF=E CONSTANT GENERATION           10650000
         B     ICH&NX.A                   BRANCH AROUND CONSTANTS       10700000
.MFLCONS ANOP                        MF=L,S CONSTANT GENERATION    @P3C 10750000
         AIF   ('&EVENT' EQ '').SKIPLT3          SKIP IF NO EVENT  @P3C 10800000
         AIF   ('&EVENT'(1,1) NE '''').SKIPLT3   SKIP NOT LITERAL  @P3C 10850000
ICH&NX.E DC    CL8&EVENT                  EVENT NAME                    10900000
.SKIPLT3 ANOP                                                      @P3C 10950000
         AIF   ('&CLASS' EQ '').SKIPLT4          SKIP IF NO CLASS  @P3C 11000000
         AIF   ('&CLASS'(1,1) NE '''').SKIPLT4   SKIP IF LITERAL   @P3C 11050000
ICH&NX.C DC    CL8&CLASS                  CLASS NAME                    11100000
.SKIPLT4 ANOP                                                      @P3C 11150000
         AIF   ('&ICHMF' EQ 'L').MEND     FINISHED IF IF MF=L           11200000
.********************************************************************** 11250000
.*                                                                    * 11300000
.*               GENERATE EXECUTE PASS FOR FOR MF=S,M,E               * 11350000
.*                                                                    * 11400000
.********************************************************************** 11450000
         AIF   ('&ICHMF' NE 'S').LOADADR IF MF NOT S GO LOAD ADDR  @P3C 11500000
.*                                       OF PLIST INTO R1               11550000
ICH&NX.A LA    1,0(1)                    CLEAR HIGH BYTE/BIT OF REG 1   11600000
         AGO   .SKPADDR                SKIP LOADING ADDR OF PLIST  @P3C 11650000
.LOADADR ANOP                                                      @P3C 11700000
ICH&NX.A A     1,36(1)                   ADDRESS OF AUDIT PLIST IN R1   11750000
.SKPADDR ANOP                                                      @P3C 11800000
&ICHLIST SETB  0                     INDICATE TO ICHMM THAT EXECUTE     11850000
.*                                   OR MODIFY IS BEING PROCESSED       11900000
&ICHCT   SETA  1                     INDICATE TO ICHMM THAT NEXT        11950000
.*                                   FIELD IS 1 WORD OFF OF R1          12000000
.*    ***************************************************************** 12050000
.*    **   EVENT KEYWORD                                             ** 12100000
.*    ***************************************************************** 12150000
         ICHMM &EVN000                                                  12200000
.*    ***************************************************************** 12250000
.*    **   EVQUAL KEYWORD                                            ** 12300000
.*    ***************************************************************** 12350000
         AIF   ('&EVQUAL' EQ '').CLSKEY   NO EVQUAL, TRY CLASS     @P3C 12400000
         AIF   ('&EVQUAL'(1,1) EQ '(').REGFRM2  IF REGISTER PASSED @P3C 12450000
.*                                        SKIP OVER LITERAL STORE       12500000
         LA    15,&EVQUAL                 EVENT QUALIFIER               12550000
         STH   15,8(1)                    EVENT QUALIFIER               12600000
         AGO   .CLSKEY                    GO PROCESS CLASS KEY     @P3C 12650000
.REGFRM2 ANOP                             REGISTER FORM USED       @P3C 12700000
         STH   &EVQUAL(1),8(1)            EVENT QUALIFIER               12750000
.CLSKEY  ANOP                                                      @P3C 12800000
&ICHCT   SETA  &ICHCT+1                  BUMP ICHMM TO NEXT WORD OFFSET 12850000
.*    ***************************************************************** 12900000
.*    **   CLASS KEYWORD                                             ** 12950000
.*    ***************************************************************** 13000000
         ICHMM &CLS000                                                  13050000
.*    ***************************************************************** 13100000
.*    **   ENTITYX KEYWORD                                           ** 13150000
.*    ***************************************************************** 13200000
         ICHMM &ENTITYX                                                 13250000
.*    ***************************************************************** 13300000
.*    **   ACEE KEYWORD                                              ** 13350000
.*    ***************************************************************** 13400000
         ICHMM &ACEE                                                    13450000
.*    ***************************************************************** 13452200
.*    **   LOGSTR KEYWORD                                        @D1A** 13454400
.*    ***************************************************************** 13456600
         ICHMM &LOGSTR                                                  13458800
.*                                                                      13461000
         AIF   ('&ICHMF' EQ 'S').MEND                            @D1A   13463200
.*                                                                      13465400
         AIF   ('&RESULT' EQ '').CONT1S                          @D1A   13467600
         AIF   ('&RESULT' EQ 'SUCCESS').NORES1                   @D1A   13469800
         AIF   ('&RESULT' NE 'FAILURE').RESERR1                  @D1A   13472000
.*                                                                      13474200
         LA    15,1              RESULT OF FAILURE SPECIFIED     @D1A   13476400
         STC   15,28(1)          RESERVED                        @D1A   13478600
         AGO   .CONT1S                                           @D1A   13480800
.RESERR1 ANOP                                                    @D1A   13483000
         AGO   .ERR095                                           @D1A   13485200
.ERRTN95 ANOP                                                    @D1A   13487400
.NORES1  ANOP                                                    @D1A   13489600
         LA    15,0              RESULT BYTE                     @D1A   13491800
         STC   15,28(1)          RESERVED                        @D1A   13494000
.CONT1S  ANOP                                                    @D1A   13496200
         AGO   .MEND                      FINISHED WITH EXECUTE PATH    13500000
.********************************************************************** 13550000
.*        ERROR PROCESSING                                            * 13600000
.********************************************************************** 13650000
.ERR010  ANOP                                                           13700000
         MNOTE 12,'*** RACAUDIT MUST BE INVOKED VIA RACROUTE'           13750000
         AGO   .ERRTN10                                            @P3C 13800000
.ERR015  ANOP                                                           13850000
         MNOTE 12,'*** INVALID RELEASE SPECIFIED FOR KEYWORDS'          13900000
         AGO   .ERRTN15                                            @P3C 13950000
.ERR020  ANOP                                                           14000000
         MNOTE 12,'*** MF MUST BE S, L, E OR M'                         14050000
         AGO   .MEND                                                    14100000
.ERR030  ANOP                                                           14150000
         MNOTE 12,'*** INVALID PLIST ADDR SUPPLIED FOR MF'              14200000
         AGO   .ERRTN30                                           3@P5D 14250000
.ERR050  ANOP                                                           14450000
         MNOTE 12,'*** EVENT REQUIRED ON MF=S'                          14500000
         AGO   .ERRTN50                                            @P3C 14550000
.ERR060  ANOP                                                           14600000
         MNOTE 12,'*** EVQUAL REQUIRED ON MF=S'                         14650000
         AGO   .ERRTN60                                           4@P5D 14700000
.ERR080  ANOP                                                           14900000
         MNOTE 12,'*** REGISTER NOT ALLOWED ON LIST FORM'               14950000
         AGO   .ERRTN80                                            @P3C 15000000
.ERR090  ANOP                                                      @D1A 15005500
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX15011000
               SS, OR FAILURE'                                     @D1A 15016500
         AGO   .ERRTN90                                            @D1A 15022000
.ERR095  ANOP                                                      @D1A 15027500
         MNOTE 12,'*** IF RESULT IS SPECIFIED, VALID OPERANDS ARE SUCCEX15033000
               SS, OR FAILURE'                                     @D1A 15038500
         AGO   .ERRTN95                                            @D1A 15044000
.MEND    MEND                               EL FIN                      15050000
