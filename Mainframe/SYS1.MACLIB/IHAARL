*%;   /* PLAS MACRO COMMENTS ARE NOT PRINTED UNLESS MSOURCE USED.       00200000
         MACRO                                                          00400000
&NAME    IHAARL &PREFIX=ARL,       BEGINNING OF EACH SYMBOL            *00600000
               &DSECT=YES,         WHETHER FIRST NAME IS ON DSECT      *00800000
               &DESCR=NO           WHETHER TO EXPAND DESCRIPTION        01000000
         AIF   ('&DESCR' EQ 'YES').DESCRIBE GO IF DESCRIPTION WANTED    01200000
         AIF   ('&DESCR' EQ 'NO').DATA      GO IF NO DESCRIPTION WANTED 01400000
         MNOTE 4,'VALUE OF DESCR= MUST BE ''YES'' OR ''NO''.'           01600000
.DESCRIBE ANOP              END THIS PLAS COMMENT       */              01800000
*/********************************************************************* 02000000
* MACRO NAME = IHAARL                                                   02200000
.*PROPRIETARY V3 STATEMENT                                              02225000
.*LICENSED MATERIALS - PROPERTY OF IBM                                  02250000
.*"RESTRICTED MATERIALS OF IBM"                                         02275000
.*5695-DF1                                                              02300000
.*(C) COPYRIGHT 1987  IBM CORP.                                         02325000
.*END PROPRIETARY V3 STATEMENT                                          02362500
* DESCRIPTION = MAPPING OF ALLOCATION RETRIEVAL LIST.                   02400000
* FUNCTION = THIS AREA IS SUPPLIED BY A CALLER OF THE RDJFCB MACRO.     02600000
*      THE CALLER STORES ITS ADDRESS IN A DCB EXIT LIST ENTRY THAT      02800000
*      HAS A CODE OF X'13' OR X'93'.                                    03000000
* INVOCATION =                                                          03200000
*      ASSEMBLER -                                                      03400000
*           PREFIX=XXX.  A CHARACTER STRING THAT ALL GENERATED SYMBOLS  03600000
*                ARE TO START WITH (OTHER THAN THE OPTIONAL SYMBOL      03800000
*                IN THE LABEL FIELD ON THE MACRO CALL AND THE REASON    04000000
*                CODES).  THE REASON CODES ARE DEFINED ONLY IN THE      04200000
*                FIRST INVOCATION AND THEY ALWAYS START WITH 'ARL'.     04400000
*                DO NOT SPECIFY A DELIMITER SUCH AS A QUOTE MARK.       04600000
*                IT IS RECOMMENDED THAT THIS PARAMETER EITHER BE        04800000
*                SPECIFIED AS THREE CHARACTERS OR BE OMITTED.  THE      05000000
*                DEFAULT IS ARL.                                        05200000
*                     THE PURPOSE OF PREFIX= IS TO ALLOW THIS MACRO TO  05400000
*                BE CALLED MORE THAN ONCE PER ASSEMBLY.                 05600000
*           DSECT=YES OR NO.  SPECIFIES WHETHER THE FIRST SYMBOL IS TO  05800000
*                BE ON A DSECT OR DC INSTRUCTION.  THE DEFAULT IS YES.  06000000
*           DESCR=YES OR NO.  SPECIFIES WHETHER THIS DESCRIPTION IS TO  06200000
*                BE GENERATED.  THE DEFAULT IS NO.                      06400000
*           (SYMBOL IN LABEL FIELD OF MACRO CALL).  IF THIS IS CODED,   06600000
*                IT BECOMES THE FIRST SYMBOL GENERATED.  THE DEFAULT    06800000
*                IS THE VALUE THAT IS SPECIFIED OR DEFAULTED FOR        07000000
*                PREFIX=.                                               07200000
*      PLAS OR PLS -                                                    07400000
*           %INCLUDE SYSLIB(IHAARL)   (END THIS LINE AND THE OTHER      07600000
*                PLAS LINES WITH A SEMICOLON).  THERE ARE TWO           07800000
*                ASSIGNMENT STATEMENTS THAT CAN BE USED BEFORE          08000000
*                THIS LINE TO CONTROL THE EXPANSION.                    08200000
*           %ARLNAME=.  THIS VARIABLE IS TO CHANGE THE NAME ON          08400000
*                LEVEL 1 OF THE STRUCTURE.  IF NULL OR NOT SPECIFIED,   08600000
*                'ARL' IS USED.  AN EXAMPLE FOLLOWS.                    08800000
*                %ARLNAME = 'MYJFCBLIST'                                09000000
*                     THE PURPOSE OF ARLNAME= IS TO ALLOW IHAARL        09200000
*                TO BE CALLED MORE THAN ONCE PER COMPILATION.  IT CAN   09400000
*                BE USED IN PLAS ONLY, NOT IN PLS.                      09600000
*           %ARLBASE=.  THIS VARIABLE CONTROLS THE BASING EXPRESSION.   09800000
*                IF IT IS NULL OR NOT SET, THE STRUCTURE WILL BE BASED  10000000
*                AND HAVE NO BASING EXPRESSION, THEREBY REQUIRING LATER 10200000
*                CODE TO USE AN IMPLICIT OR EXPLICIT BASING             10400000
*                EXPRESSION.  EXAMPLES FOLLOW.                          10600000
*                %ARLBASE = 'BASED(ARLPTR)'                             10800000
*                %ARLBASE = 'BASED(EXITARL)'                            11000000
*                %ARLBASE = 'BASED'            (THE DEFAULT)            11200000
*                %ARLBASE = 'DEFINED(DXCCW1)'  (ON TOP OF DXCCW1)       11400000
*                %ARLBASE = 'AUTOMATIC'                                 11600000
*                %ARLBASE = 'STATIC'           (NOT REENTRANT)          11800000
* OUTPUT = SUCCESSFUL EXECUTION OF RDJFCB RESULTS IN THE RETURN OF      12000000
*      AN AREA CALLED THE ALLOCATION RETRIEVAL AREA.  THIS AREA HAS     12200000
*      THE FOLLOWING INFORMATION REPEATED FOR DATA SET.                 12400000
*           DISP LEN                                                    12600000
*           +0   2   LENGTH OF INFO FOR THIS DATA SET, INCLUDING THIS   12800000
*                    FIELD AND SOME PADDING.                            13000000
*           +2   2   RESERVED.                                          13200000
*           +4   176 JFCB, JOB FILE CONTROL BLOCK.                      13400000
*           +180 6   VOLUME SERIAL NUMBERS FOR SIXTH AND SUBSEQUENT     13600000
*                    VOLUMES IF THERE ARE ANY.  THAT NUMBER IS          13800000
*                    DETERMINED BY JFCBNVOL.  SOME NUMBERS, INCLUDING   14000000
*                    THOSE IN THE JFCB, WILL CONTAIN ALL BLANKS IF      14200000
*                    THE NUMBER OF SERIAL NUMBERS IS LESS THAN THE      14400000
*                    SPECIFIED VOLUME COUNT.  IF THE FIRST BYTE OF A    14600000
*                    SERIAL NUMBER IS X'FF', VOL=REF WAS SPECIFIED AND  14800000
*                    THE SERIAL NUMBER NOT BEEN DETERMINED.             15000000
* NOTES = AN ASSEMBLER GLOBAL VARIABLE SYMBOL IS USED TO ALLOW THIS     15200000
*      MACRO TO BE CALLED MULTIPLE TIMES WITHOUT DUPLICATE SYMBOLS.     15400000
* CHANGE ACTIVITY =                                                     15600000
*$L0=SWA,HDP2230,850821,STLWER: SWA ABOVE THE LINE                 @L0A 15800000
*********************************************************************** 16000000
         AGO   .DATA     AVOID GENERATING STRANGE ASSEMBLER COMMENT  */ 16200000
*%;   /* COMMENTS ON PLAS MACRO LINES NOT PRINTED UNLESS MSOURCE USED.  16400000
.DATA    GBLB  &IHAARL   THIS MACRO HAS BEEN CALLED BEFORE IN ASSEMBLY  16600000
         LCLC  &P        THE PREFIX VALUE (SHORT SYMBOL FOR ALIGNMENT)  16800000
         LCLC  &FRSTSYM  FIRST SYMBOL BEING GENERATED                   17000000
&P       SETC  '&PREFIX' GET THE SPECIFIED OR DEFAULTED PREFIX          17200000
         AIF   ('&P' NE '').GOTPREF  GO IF WE HAVE A VALUE              17400000
&P       SETC  'ARL'     OVERRIDE USER'S NULL VALUE                     17600000
.GOTPREF ANOP                                                           17800000
.*                                                                      18000000
&FRSTSYM SETC  '&NAME'   TRY TO GET FIRST SYMBOL SPECIFIED BY CALLER    18200000
         AIF   ('&NAME' NE '').TESTDSE                                  18400000
&FRSTSYM SETC  '&P'    DEFAULT 1ST SYMBOL TO SPECIFIED/DEFAULTED PREFIX 18600000
.*                                                                      18800000
.TESTDSE AIF ('&DSECT' EQ 'NO').NODSECT                                 19000000
         AIF   ('&DSECT' EQ 'YES').DSECT                                19200000
         MNOTE 4,'VALUE OF DSECT= MUST BE ''YES'' OR ''NO''.'           19400000
.DSECT   ANOP            GENERATE THE DSECT STATEMENT                   19600000
&FRSTSYM DSECT           ALLOCATION RETRIEVAL LIST                      19800000
         AGO   .EXPAND                                                  20000000
.NODSECT ANOP                                                           20200000
&FRSTSYM DC    0F'0'     BEGINNING OF ALLOCATION RETRIEVAL LIST         20400000
.EXPAND  ANOP                                                           20600000
*   THE FOLLOWING FIELDS ARE SET BY THE CALLER OF THE RDJFCB MACRO.     20800000
&P.LEN   DC    Y(&P.END-*) LENGTH OF THIS AREA.  MUST BE AT LEAST 36.   21000000
&P.IDENT DC    C'AR'     IDENTIFICATION OF THIS AREA. MUST BE 'AR'.     21200000
&P.OPT1  DC    B'0'      OPTION BYTE                                    21400000
&P.LANY  EQU   X'80'     ALLOCATION RETRIEVAL AREA MAY BE ABOVE 16M     21600000
***      EQU   X'7F'     RESERVED, SHOULD BE ZERO                       21800000
&P.RSVD1 DC    XL7'0'    RESERVED, SHOULD BE ZERO                       22000000
&P.RETRV DC    H'0'      NUMBER OF DATA SETS FOR WHICH INFORMATION IS  *22200000
                         TO BE RETRIEVED.  IF ZERO, ALL CONCATENATED   *22400000
                         WITH THE DD NAME ARE RETRIEVED.                22600000
&P.FIRST DC    H'0'      NUMBER OF THE FIRST DATA SET IN CONCATENATION *22800000
                         FOR WHICH TO RETRIEVE INFORMATION.  ZERO OR   *23000000
                         ONE MEANS FIRST.  TWO MEANS SECOND, ETC.       23200000
*   THE FOLLOWING FIELDS ARE SET BY RDJFCB.                             23400000
&P.AREA  DC    A(0)      ADDRESS OF ALLOCATION RETRIEVAL AREA           23600000
&P.POOL  DC    HL1'0'    SUBPOOL TO PASS TO FREEMAIN FOR ALLOCATION    *23800000
                         RETRIEVAL AREA.  0 FOR CALLER IN USER KEY (8- *24000000
                         15) OR 230 FOR CALLER IN SYSTEM KEY (0-7).     24200000
&P.RLEN  DC    FL3'0'    LENGTH OF ALLOCATION RETRIEVAL AREA            24400000
&P.RTRVD DC    H'0'      NUMBER OF DATA SETS FOR WHICH INFORMATION WAS *24600000
                         RETRIEVED.  THEY ARE CONCATENATED.             24800000
&P.CONC  DC    H'0'      NUMBER OF DATA SETS CONCATENATED TOGETHER.  IF*25000000
                         NO CONCATENATION, THIS IS ONE.                 25200000
&P.RCODE DC    HL1'0'    REASON CODE.                                   25400000
         AIF   (&IHAARL).CALLED  GO IF ALREADY CALLED ONCE IN ASSEMBLY  25600000
.*  THE RETURN CODES ARE DEFINED ONLY ONCE PER ASSEMBLY.                25800000
&IHAARL  SETB  1         TELL NEXT IHAARL INVOCATION OF ME              26000000
ARLALLOK EQU   0         REQUESTED INFORMATION READ.  ARLRTRVD MAY BE  *26200000
                         LESS THAN ARLRETRV.                            26400000
*   THE FOLLOWING REASON CODES ARE ASSOCIATED WITH RDJFCB RETURN CODE 8 26600000
ARLTBIG  EQU   4         ARLFIRST GREATER THAN ARLCONC                  26800000
ARLNSTOR EQU   8         INSUFFICIENT STORAGE TO RETRIEVE INFO. ARLPOOL*27000000
                         & ARLRLEN DESCRIBE WHAT COULD NOT BE GOTTEN    27200000
         AGO   .CONT1    SKIP AROUND COMMENT                            27400000
.CALLED  MNOTE 'REASON CODES ARE DEFINED ONLY IN THE FIRST INVOCATION'  27600000
.CONT1   ANOP                                                           27800000
&P.RSVD2 DC    XL7'0'    RESERVED, WILL BE SET BY RDJFCB                28000000
&P.END   EQU   *         END OF ALLOCATION RETRIEVAL AREA               28200000
         SPACE 1                                                        28400000
         MEND      END LONG PLAS MACRO COMMENT */                       28600000
    %IF ARLNAME = '' %THEN     /* ARLNAME DEFAULTS TO CHAR INTERNAL   */28800000
      %ARLNAME = 'ARL';        /* SUPPLY DEFAULT FOR STRUCTURE NAME   */29000000
    %IF ARLBASE = '' %THEN     /* ARLBASE DEFAULTS TO CHAR INTERNAL   */29200000
      %ARLBASE = 'BASED';      /* SUPPLY DEFAULT FOR BASE EXPRESSION  */29400000
 DECLARE                                                                29600000
   1 ARLNAME  ARLBASE,         /* ALLOCATION RETRIEVAL LIST           */29800000
 /* THE FOLLOWING FIELDS ARE SET BY THE CALLER OF THE RDJFCB MACRO.   */30000000
     2 ARLLEN   FIXED(15),     /* LENGTH OF THIS AREA.  AT LEAST 36.  */30200000
     2 ARLIDENT CHAR(2),       /* AREA IDENTIFICATION.  MUST BE 'AR'. */30400000
     2 ARLOPT1  BIT(8),        /* OPTION BYTE                         */30600000
       3 ARLLANY  BIT(1),      /* ALLOCATION RETRIEVAL AREA MAY BE      30800000
                                  ABOVE 16M LINE                      */31000000
       3 *        BIT(7),      /* RESERVED, SHOULD BE ZERO            */31200000
     2 *        CHAR(7),       /* RESERVED, SHOULD BE ZERO            */31400000
     2 ARLRETRV FIXED(15),     /* NUMBER OF DATA SETS FOR WHICH         31600000
                                  INFORMATION IS TO BE RETRIEVED.       31800000
                                  IF ZERO, ALL CONCATENATED WITH        32000000
                                  THE DD NAME ARE RETRIEVED.          */32200000
     2 ARLFIRST FIXED(15),     /* NUMBER OF THE FIRST DATA SET IN       32400000
                                  CONCATENATION FOR WHICH TO RETRIEVE   32600000
                                  INFORMATION.  ZERO OR ONE MEANS       32800000
                                  FIRST.  TWO MEANS SECOND, ETC.      */33000000
 /* THE FOLLOWING FIELDS ARE SET BY RDJFCB.                           */33200000
     2 ARLAREA  POINTER,       /* ADDRESS OF ALLOCATION RETRIEVAL AREA*/33400000
     2 ARLPOOL  FIXED(8),      /* SUBPOOL OF STORAGE CONTAINING         33600000
                                  ALLOCATION RETRIEVAL AREA.  252 FOR   33800000
                                  SYSTEM KEY CALLERS, 0 FOR OTHERS.   */34000000
     2 ARLRLEN  FIXED(24),     /* LENGTH OF ALLOCATION RETRIEVAL AREA */34200000
     2 ARLRTRVD FIXED(15),     /* NUMBER OF DATA SETS FOR WHICH         34400000
                                  INFORMATION WAS RETRIEVED.  THEY      34600000
                                  ARE CONCATENATED.                   */34800000
     2 ARLCONC  FIXED(15),     /* NUMBER OF DATA SETS CONCATENATED      35000000
                                  TOGETHER. IF NO CONCATENATION,        35200000
                                  THIS IS ONE.                        */35400000
     2 ARLRCODE FIXED(8),      /* REASON CODE. SEE DEFINITIONS BELOW  */35600000
     2 ARLRSVD2 CHAR(7);       /* RESERVED, WILL BE SET BY RDJFCB     */35800000
 DECLARE                       /* REASON CODE VALUES FOR ARLRCODE     */36000000
     ARLALLOK CONSTANT(0)      /* REQUESTED INFORMATION READ.         */36200000
                FIXED(8),      /* ARLRTRVD MAY BE LESS THAN ARLRETRV. */36400000
  /* THE FOLLOWING REASON CODES ARE ASSOCIATED WITH RDJFCB RETURN     */36600000
  /* CODE 8.                                                          */36800000
     ARLTBIG  CONSTANT(4)      /* ARLFIRST GREATER THAN ARLCONC       */37000000
                FIXED(8),                                               37200000
     ARLNSTOR CONSTANT(8)      /* INSUFFICIENT STORAGE TO RETRIEVE    */37400000
                FIXED(8);      /* INFO.  ARLPOOL & ARLRLEN DESCRIBE WHAT37600000
                                  COULD NOT BE GOTTEN.                */37800000
