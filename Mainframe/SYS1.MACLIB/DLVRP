         MACRO                                                          00050000
&NAME    DLVRP &TYPE=,&MF=I,&SHRPOOL=,&MODE=24                     @L9C 00050500
.*                                                                      00051000
.* $MAC(DLVRP) COMP(SC1DE):                                             00051500
.*                                                                      00052000
.*PROPRIETARY V3 STATEMENT                                              00052300
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00052600
.*"RESTRICTED MATERIALS OF IBM"                                         00052900
.*5695-DF1                                                              00053200
.*(C) COPYRIGHT 1983,1987  IBM CORP.                                    00053500
.*END PROPRIETARY V3 STATEMENT                                          00054200
.*                                                                      00055000
.* STATUS = MVS/XA DFP RELEASE 2.3                                      00055500
.*                                                                      00056000
.* $L3=DFP,JDP1110,,STLJRB: MVS/XA DFP RELEASE 1.1 VSAM 31-BIT     @L3A 00056500
.* $L9=DFP,HDP2230,,STLJRB: MVS/XA DFP RELEASE 2.3   VSAM 31-BIT   @L9A 00056600
.*                                                                      00057000
.*                                                             @AZ84070 00057200
.*                                                             @AZ90259 00057300
.* $LX=DFSMS,HDZ11D0,09/16/96,SJPLJRB: SYSTEM MANAGED BUFFERS      @LXA 00057400
         LCLA  &ORCODE                  CODES TO BE TURNED ON IN LIST   00057500
         LCLA  &ANDCODE                 CODES TO BE TURNED OFF IN LIST  00058000
         LCLA  &I                       WORK VARIABLE                   00058500
         LCLA  &DISP                    DISPL. TO ACB          @AZ84070 00058700
         LCLB  &MFE                     EXECUTE FORM INDICATOR          00059000
         LCLB  &ERR                     ERROR INDICATOR                 00059500
         LCLC  &EREG                    EXECUTE FORM LIST ADDR REG      00060000
         LCLC  &LAB                     ACB LABEL              @AZ84070 00060200
         LCLC  &AMODE                   ADDRESSING MODE            @L9A 00060300
&ORCODE  SETA  128+32                   INDICATE DB/DC AND DLVRP        00060500
&ANDCODE SETA  255-64                   RESET BLDVRP                    00061000
&DISP    SETA  4                        DEFAULT TO 4           @AZ84070 00061100
&AMODE   SETC  '&MODE'                  SET AMODE                  @L9A 00061200
         AIF   ('&MODE' NE '').MODECHK                             @L9A 00061300
&AMODE   SETC  '24'                     SET AMODE DEFAULT TO 24    @L9A 00061400
         AGO   .MODEOK                                             @L9A 00061500
.MODECHK AIF   ('&AMODE' EQ '24' OR '&AMODE' EQ '31' OR T'&MODE EQ     X00061600
               'O').MODEOK                                         @L9A 00061700
         IDAERMAC 3,MODE,&MODE          VALID VALUES ARE 24 AND 31 @L9A 00061800
.MODEOK  ANOP                                                      @L9A 00061900
         AIF   ('&AMODE' EQ '24').GO24  DISP=4 FOR MODE=24         @L9A 00062000
&DISP    SETA  8                        SET ACB DISPL TO 8         @L9A 00062100
.GO24    ANOP                                                      @L9A 00062200
&LAB     SETC  'IDAB&SYSNDX'            GENERATE ACB LABEL     @AZ84070 00062300
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS    00062400
.POSLOOP ANOP                                                           00062500
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND       00062600
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE             00063000
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE POS OPERANDS      00063500
.NOPOS   ANOP                                                           00064000
         AIF   ('&MF' EQ 'I').MFB       GO IF STANDARD FORM             00064500
         AIF   (N'&MF LT 2).MFERR       GO IF NOT ENOUGH PARMS          00065000
         AIF   ('&MF(1)' NE 'E').MFERR  GO IF NOT EXECUTE FORM          00065500
&MFE     SETB  1                        INDICATE EXECUTE FORM           00066000
         AGO   .MFB                     GO AROUND ERROR                 00066500
.MFERR   ANOP                                                           00067000
         IDAERMAC 3,MF,&MF              ISSUE ERROR MNOTE               00067500
&ERR     SETB  1                        INDICATE ERROR                  00068000
.MFB     ANOP                                                           00068500
         AIF   ('&TYPE' NE '').TOKY     GO IF TYPE IS SPECIFIED         00069000
         AIF   (&MFE).TYPEB             GO IF EXECUTE FORM              00069500
         AGO   .TLSR                    GO TAKE DEFAULT                 00070000
.TOKY    ANOP                                                           00070500
         AIF   ('&TYPE' NE 'LSR').TLSRB  GO IF NOT LOCALLY SHARED       00071000
.TLSR    ANOP                                                           00071500
&ORCODE  SETA  &ORCODE+16               INDICATE LOCALLY SHARED         00072000
&ANDCODE SETA  &ANDCODE-8               RESET GLOBALLY SHARED           00072500
         AGO   .TYPEB                   GO AROUND TYPE ANALYSIS         00073000
.TLSRB   ANOP                                                           00073500
         AIF   ('&TYPE' NE 'GSR').TGSRB  GO IF NOT GLOBALLY SHARED      00074000
&ORCODE  SETA  &ORCODE+8                INDICATE GLOBALLY SHARED        00074500
&ANDCODE SETA  &ANDCODE-16              RESET LOCALLY SHARED            00075000
         AGO   .TYPEB                   GO AROUND TYPE ANALYSIS         00075500
.TGSRB   ANOP                                                           00076000
         IDAERMAC 3,TYPE,&TYPE          ISSUE ERROR MNOTE               00076500
&ERR     SETB  1                        INDICATE ERROR                  00077000
.TYPEB   ANOP                                                           00077500
         AIF   (T'&SHRPOOL EQ 'O').TYPEB1                          @L3A 00077600
         AIF   ('&TYPE' NE 'GSR').TYPEB1                           @L3A 00077700
         MNOTE 4,'***IDA011 SHRPOOL IS NOT A VALID KEYWORD WITH THE TYPX00077800
               E=GSR KEYWORD - IGNORED'                            @L3A 00077900
.TYPEB1  AIF   (&ERR).END               GO IF ANY ERRORS                00078000
         AIF   ('&NAME' EQ '').NAMEB    GO IF NO NAME                   00078500
&NAME    DS    0H                                                       00079000
.NAMEB   ANOP                                                           00079500
         AIF   (&MFE).EXEC              GO IF EXECUTE FORM              00080000
         CNOP  0,4                      ALIGN ON A WORD BOUNDARY        00080500
.NOP2    AIF   ('&AMODE' EQ '24').MODE24 CHECK FOR 31-BIT MODE     @L9A 00080600
         BAS   1,*+64                   PLACE LIST ADDRESS IN R1   @LXA 00080800
         AGO   .REST                                               @L9A 00081100
.MODE24  BAL   1,*+60                   PLACE LIST ADDRESS IN 1         00081200
.REST    ANOP                                                      @L9A 00081400
         OPEN  (&LAB),MF=L,MODE=&AMODE  GENERATE OPEN PARM LST     @L9C 00081600
.*                          ADD LABEL TO ACB IN FOLLOWING LINE @AZ84070 00082000
&LAB     DC    X'A011'                  ID AND SUBTYPE                  00082500
         DC    AL2(0)                   RESERVED                        00083000
         DC    A(0)                     BUFFER LIST ADDRESS             00083500
         DC    AL1(0)                   KEY LENGTH                      00084000
         DC    AL1(0)                   STRING NUMBER                   00084500
         DC    AL1(&ORCODE)             VRP CODES                       00085000
         DC    AL1(0)                   RESERVED                        00085500
         DC    3A(0)                    RESERVED                        00086000
         DC    XL3'0'                   RESERVED                        00086500
         DC    X'08'                    DSORG                           00087000
         AIF   (T'&SHRPOOL EQ 'O').NOPOL1                          @L3A 00087200
         AIF   ((T'&SHRPOOL EQ 'O') OR ('&TYPE' EQ 'GSR')).NOPOL1  @L3A 00087400
         DC    AL1(&SHRPOOL)            SHARED RESOURCE POOL ID    @L3A 00087600
         AGO   .NOPOL2                                                  00087800
.NOPOL1  DC    AL1(0)                   SHARED RESOURCE POOL ID    @L3A 00088000
.NOPOL2  DC    AL3(0)                   RESERVED                   @L3A 00088200
         DC    4A(0)                    RESERVED                   @L3C 00088400
         DC    X'02'                    OFLAGS                          00089000
         DC    XL3'0'                   RESERVED                        00089500
         AGO   .ISUSVX                  GO TO ISSUE OPEN SVC       @L9C 00090000
.EXEC    ANOP                                                           00090500
         AIF   ('&MF(2)'(1,1) EQ '(').EREG  GO IF LIST ADDRESS IN REG   00091000
         LA    1,&MF(2)                 LOAD LIST ADDRESS               00091500
         AGO   .AEREG                   GO AROUND REG CODE              00092000
.EREG    ANOP                                                           00092500
         AIF   ('&MF(2)' EQ '(1)').AEREG  GO IF LIST ADDR ALREADY IN 1  00093000
&I       SETA  K'&MF(2)-2               CALC LENGTH OF REG NAME         00093500
&EREG    SETC  '&MF(2)'(2,&I)           PICK UP REG NAME                00094000
         LR    1,&EREG                  LOAD LIST ADDRESS               00094500
.AEREG   ANOP                                                           00095000
         AIF   ('&AMODE' NE '31').AEM2                             @L9A 00095100
.AEREG2  NI    18(1),&ANDCODE           REMOVE OLD VRP CODES       @L9A 00095300
         OI    18(1),&ORCODE            INSERT NEW VRP CODES       @L9A 00095500
         AGO   .AEM3                                               @L9A 00095600
.AEM2    ANOP                                                      @L9A 00095700
         NI    14(1),&ANDCODE           REMOVE OLD VRP CODES            00095800
         OI    14(1),&ORCODE            INSERT NEW VRP CODES            00096000
.AEM3    AIF   (T'&SHRPOOL EQ 'O').ISUSVC                          @L3A 00096100
         AIF   ('&AMODE' NE '31').AEM4                             @L9A 00096200
         MVI   36(1),&SHRPOOL           SET SHARED POOL ID NUMBER  @L9C 00096300
         AGO   .ISUSVC                                             @L9A 00096400
.AEM4    ANOP                                                      @L9A 00096500
         MVI   32(1),&SHRPOOL           SET SHARED POOL ID NUMBER  @L9C 00096700
.ISUSVC  ANOP                                                           00097000
         OPEN  (&DISP.(1)),MF=(E,(1)),MODE=&AMODE ISSUE OPEN SVC   @L9C 00097200
         AGO   .END                                                @L9A 00097400
.ISUSVX  ANOP                                                           00097600
         OPEN  ,MF=(E,(1)),MODE=&AMODE           ISSUE OPEN SVC    @L9C 00097800
.END     ANOP                                                           00098000
         MEND                                                           00098500
