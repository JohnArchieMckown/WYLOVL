* /* START OF SPECIFICATIONS ****************************************** 00050000
*                                                                     * 00100000
*01*  MACRO NAME = NMLDEF                                             * 00150000
*                                                                     * 00200000
*01*  MACRO DATE = 11/27/89                                           * 00250000
*                                                                     * 00300000
*01*  DESCRIPTIVE NAME - NUCLEUS MODULE LIST DEFINITION               * 00350000
*                                                                     * 00400000
*01*  PROPRIETARY STATEMENT:                                          * 00450000
*     LICENSED MATERIALS - PROPERTY OF IBM                            * 00500000
*     THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                     * 00550000
*     5695-047 (C) COPYRIGHT IBM CORP. 1990                           * 00600000
*     SEE COPYRIGHT INSTRUCTIONS                                      * 00650000
*                                                                     * 00700000
*     STATUS = HBB4410                                                * 00750000
*                                                                     * 00800000
*01*  FUNCTION =  CREATES A NUCLEUS MODULE LIST.  ALL MODULES         * 00850000
*                 IN THE LIST MUST RESIDE IN SYS1.NUCLEUS.            * 00900000
*                 THE MODULES ARE LOADED AS PART OF THE MVS           * 00950000
*                 SYSTEM NUCLEUS.                                     * 01000000
*                                                                     * 01050000
*01*  MODULE-TYPE = MACRO                                             * 01100000
*                                                                     * 01150000
*01*  PROCESSOR = ASSEMBLER                                           * 01200000
*                                                                     * 01250000
*01*  MACROS = NONE                                                   * 01300000
*                                                                     * 01350000
*01*  CHANGE ACTIVITY =  NEW FOR OS/VS2 HBB4410                       * 01400000
*      $L0=GNLDR    HBB4410  890301  PD81LY:  Generalized Nucleus     * 01450000
*                                             Loader                  * 01500000
*      $P1=PEO3711  HBB4410  900226  PD16HA:  Add version number      * 01525000
*                                                                     * 01550000
*01*  MNOTES =                                                        * 01600000
*              CSECT NAME OMITTED. MACRO NAME FIELD BLANK.            * 01650000
*              AT LEAST ONE MODULE MUST BE LISTED.                    * 01700000
*              IN THE NUCL LIST, THE MODULE NAME nnnnnnnn EXCEEDS     * 01750000
*                    8 CHARACTERS.                                    * 01800000
*                                                                     * 01850000
*****   END OF SPECIFICATIONS ***************************************** 01900000
         MACRO                                                          01950000
&LABEL   NMLDEF &NUCL=                 Required list of module names    02000000
.********************************************************************** 02050000
.*  Declare local variables                                             02100000
.********************************************************************** 02150000
         LCLA  &I                      Index for sub operands           02200000
         LCLA  &ERROR                  Error flag                       02250000
         LCLA  &NUCNUM                 Number of nucleus modules        02300000
         LCLA  &NUCLN                  Nucleus module name length       02350000
.********************************************************************** 02400000
.*  Initialize local variables                                          02450000
.********************************************************************** 02500000
&ERROR   SETA  0                       Initialize error flag            02550000
&NUCLN   SETA  8                       Nucleus module name length       02600000
&NUCNUM  SETA  N'&NUCL                 Number of nucleus modules        02650000
.********************************************************************** 02700000
.*  Put out MACDATE comment line                                        02750000
.********************************************************************** 02800000
*              MACDATE = 11/27/89                                       02850000
.********************************************************************** 02900000
.*  Verify that a label was specified on the macro invocation           02950000
.********************************************************************** 03000000
         AIF   (K'&LABEL NE 0).OKCSECT  Verify CSECT was specified      03050000
         MNOTE 8,'CSECT NAME OMITTED. MACRO NAME FIELD BLANK.'          03100000
&ERROR   SETA  1                       Set error flag                   03150000
.OKCSECT ANOP                          Continue                         03200000
.********************************************************************** 03250000
.*  Verify that at least one module is in the nucleus module list       03300000
.********************************************************************** 03350000
         AIF   (&NUCNUM NE 0).OKCOUNT  Verify at least 1 module in list 03400000
         MNOTE 8,'AT LEAST ONE MODULE MUST BE LISTED.'                  03450000
&ERROR   SETA  1                       Set error flag                   03500000
.OKCOUNT ANOP                          Continue                         03550000
.********************************************************************** 03600000
.*  Check the Nucleus module list                                       03650000
.********************************************************************** 03700000
&I       SETA  1                       Initialize loop counter to 1     03750000
.NUCLOOP ANOP                          Top of loop for NUCL             03800000
         AIF   (&I GT N'&NUCL).ENDNUC  Loop exit condition              03850000
         AIF   (K'&NUCL(&I) LE &NUCLN).OKNUC                            03900000
         MNOTE 8,'IN THE NUCL LIST, THE MODULE NAME &NUCL(&I) EXCEEDS 8+03950000
                CHARACTERS.'                                            04000000
&ERROR   SETA  1                       Set error flag                   04050000
.OKNUC   ANOP                                                           04100000
&I       SETA  &I+1                    Increment loop counter           04150000
         AGO   .NUCLOOP                Continue loop                    04200000
.ENDNUC  ANOP                                                           04250000
.********************************************************************** 04300000
.*  If an error was detected, skip over code generation                 04350000
.********************************************************************** 04400000
          AIF  (&ERROR GT 0).EXIT      If an error was detected, skip   04450000
.*                                      generation of code              04500000
.********************************************************************** 04550000
.*  Generate the CSECT                                                  04600000
.********************************************************************** 04650000
&LABEL   CSECT                         NML CSECT                        04700000
         DC    CL4'NML '               NML CB Identifier                04750000
         DC    A(0)                    Chain pointer                    04800000
         DC    CL8'&LABEL'             CSECT name                       04850000
         DC    CL8'&SYSDATE'           Date of assembly (mm/dd/yy)      04900000
         DC    F'1'                    NMLDEF version number       @P1C 04950000
         DC    A(NUCLIST)              Address of Nucleus list          05000000
         DC    F'&NUCNUM'              Number of Nucleus modules        05050000
         DC    3F'0'                   Reserved                    @P1C 05100000
.********************************************************************** 05150000
.*  Build the Nucleus module list                                       05200000
.********************************************************************** 05250000
NUCLIST  DS    0CL1                    Start of Nucleus List            05300000
&I       SETA  1                       Initialize loop counter to 1     05350000
.BLDNUC  ANOP                          Top of loop for NUCL             05400000
         AIF   (&I GT N'&NUCL).ENDN2   Loop exit condition              05450000
         DC    CL8'&NUCL(&I)'          Declaration for list entry       05500000
&I       SETA  &I+1                    Increment loop counter           05550000
         AGO   .BLDNUC                 Continue loop                    05600000
.ENDN2   ANOP                          End loop for NUCL                05650000
         DC    4CL8' '                 Patch area                       05700000
         END   &LABEL                                                   05750000
.EXIT    ANOP                          Exit on error                    05800000
         MEND                                                           05850000
