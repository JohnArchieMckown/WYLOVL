*%/* IF PLS, EXCLUDE THE BAL FORM OF THE MACRO                          00100000
         MACRO                                                          00150000
&NAME    SYMREC &SR=,&MF=I                                              00200000
*        MACDATE = 06/12/85                                             00250000
.********************************************************************** 00300000
.*                                                                    * 00350000
.*  Macro: SYMREC                                                     * 00400000
.*                                                                    * 00450000
.*  Copyright:                                                        * 00500000
.*      5740-XC6                                                      * 00550000
.*      Contains Restricted Materials of IBM                          * 00600000
.*      (C) Copyright IBM Corp. 1986                                  * 00650000
.*      Licensed Materials - Property of IBM,                         * 00700000
.*      Refer to copyright instructions                               * 00750000
.*      form number G120-2083                                         * 00800000
.*                                                                    * 00850000
.*  Status:  OS/VS2 JBB2220                                           * 00900000
.*                                                                    * 00950000
.*  Function: The SYMREC macro generates code which interfaces via PC * 01000000
.*             with the symptom record service routine, ASRSERVP.     * 01050000
.*                                                                    * 01100000
.*  Component: SCASR ( Symptom Record )                               * 01107100
.*                                                                    * 01114200
.*  System library:  SYS1.MACLIB                                      * 01121300
.*                                                                    * 01128400
.*  DLIB:            AMACLIB                                          * 01135500
.*                                                                    * 01142600
.*  Syntax:                                                           * 01150000
.*    <name>  SYMREC   SR=(sraddr)                                    * 01200000
.*                     <,MF=(L|E,listaddr)>                           * 01250000
.*                                                                    * 01300000
.*         sraddr     = the address of a symptom record               * 01350000
.*                       an A-type address or register if MF=standard * 01360000
.*                       an optional A-type address if MF=L           * 01370000
.*                       an optional RX-type address or register      * 01380000
.*                        if MF=E                                     * 01390000
.*         listaddr   = the address of a remote control program       * 01400000
.*                       parameter list on MF=E form only             * 01433300
.*                         an RX-type address or register             * 01466600
.*                                                                    * 01500000
.*  Macros: IHBINNRA                                                  * 01550000
.*                                                                    * 01600000
.*  External macro variables : None                                   * 01650000
.*                                                                    * 01700000
.*  Change Activity:                                                  * 01750000
.*                                                                    * 01800000
.* FLAG  REASON   RELEASE   DATE    ORIGIN                            * 01833300
.* $L0=  SYMREC   JBB2220   850601  PD1C:  SYMPTOM RECORD SUPPORT     * 01866600
.* $P1=  PA20281  JBB2220   850601  PD1C:  MNOTE FOR SR SUBPARAMETERS * 01883300
.*                                                                    * 01900000
.********************************************************************** 01950000
         LCLB  &SRATYPE                                                 01975000
         LCLC  &VERLVL                                                  02000000
         LCLC  &LISTLEN                                                 02006200
         LCLC  &RSVLEN1                                                 02012400
         LCLC  &RSVLEN2                                                 02018600
         LCLC  &LABEL             Labels for the generated parmlist     02024800
         LCLC  &LABELL                                                  02031000
         LCLC  &LABELO                                                  02037200
         LCLC  &LABELR                                                  02043400
         LCLC  &LABELS                                                  02050000
         LCLC  &LABELX                                                  02150000
&SRATYPE SETB  1                  Assume that SR is an A-type address   02158300
&LABEL   SETC  'ASR&SYSNDX'                                             02166600
&LABELL  SETC  'ASR&SYSNDX.L'                                           02174900
&LABELO  SETC  'ASR&SYSNDX.O'                                           02183200
&LABELR  SETC  'ASR&SYSNDX.R'                                           02191500
&LABELS  SETC  'ASR&SYSNDX.S'                                           02200000
&LABELX  SETC  'ASR&SYSNDX.X'                                           02300000
&RSVLEN1 SETC  '3'                length of first reserved area         02320000
&RSVLEN2 SETC  '8'                length of first reserved area         02340000
&VERLVL  SETC  '01'               level 01 reflects JBB2220 changes     02360000
&LISTLEN SETC  '16'               parmlist is 16 bytes long             02380000
.********************************************************************** 02408300
.*       Validity check parameters for standard form.                 * 02436600
.*        1-SR keyword must be specified and can be an A-type address * 02464900
.*          or a register.                                            * 02493200
.*        2-SR keyword can have 1 and only 1 option/subparameter. @P1A* 02507300
.********************************************************************** 02521500
.*       Validity check for SR parameter on standard form.              02550000
         AIF   ('&MF(1)' NE 'I').EFORM                                  02600000
         AIF   ('&SR' EQ '').ERR1                                       02650000
         AIF   ('&SR'(1,1) NE '(').STDFORM                              02687500
         AIF   (N'&SR GT 1).ERR6                                   @P1A 02692500
         AIF   ('&SR(1)' EQ '').ERR1                               @P1A 02697500
&SRATYPE SETB  0                  SR is a register not an A-type addr   02702500
.********************************************************************** 02717500
.*       If SR is a numeric register, it must be in 2-12 range.         02732500
.********************************************************************** 02747500
         AIF   (T'&SR(1) NE 'N').STDFORM                                02762500
         AIF   (&SR(1) LT 2 OR &SR(1) GT 12).ERR2                       02800000
.********************************************************************** 02850000
.*       Generate first segment of standard form.                     * 02900000
.********************************************************************** 02950000
.STDFORM ANOP                                                           03000000
.*       Generate name if it is present on macro request.               03050000
         AIF   ('&NAME' EQ '').LBL0110                                  03100000
&NAME    DS    0H                                                       03150000
.LBL0110 ANOP                                                           03200000
.********************************************************************** 03225000
.*       If sraddr is in a register, store it in generated list.        03250000
.********************************************************************** 03275000
         AIF   ('&SR'(1,1) NE '(').LBL0120                              03300000
         ST    &SR(1),&LABELS     STORE ADDRESS OF SYMPTOM RECORD       03350000
.LBL0120 ANOP                                                           03400000
.********************************************************************** 03425000
.*       Align the in-line parameter list to word boundary.             03450000
.*       Branch around the in-line parameter list.                      03550000
.********************************************************************** 03553200
         CNOP  0,4                ALIGN IN-LINE LIST TO WORD BOUNDARY   03556400
         BAS   1,&LABELX          BRANCH AROUND THE IN-LINE LIST        03559600
&LABEL   DS    0CL&LISTLEN                                              03569200
.********************************************************************** 03571100
.*       Goto GENLIST which is common segment that builds the parmlist  03573000
.*       for both the list and standard forms.                          03574900
.********************************************************************** 03576800
         AGO   .GENLIST                                                 03578800
.EFORM   ANOP                                                           03588400
         AIF   ('&MF(1)' NE 'E').LFORM                                  03626800
.********************************************************************** 03628100
.*       Validity check the keywords on the execute form.             * 03629400
.*        1-MF must specify 2 and only 2 positional values.           * 03630700
.*        2-SR is optional and can be an RX-type address or a register* 03632000
.*          If SR specifies a register, it must be in 2-12 range.     * 03633300
.*        3-SR keyword can have 1 and only 1 option/subparameter. @P1A* 03633900
.********************************************************************** 03634600
         AIF   (N'&MF NE 2).ERR3                                        03636400
         AIF   ('&SR' EQ '').LBL0410                                    03646000
         AIF   ('&SR'(1,1) NE '(').LBL0410                              03655600
         AIF   (N'&SR GT 1).ERR6                                   @P1A 03658800
         AIF   ('&SR(1)' EQ '').ERR7                               @P1A 03662000
         AIF   (T'&SR(1) NE 'N').LBL0410                                03665200
         AIF   (&SR(1) LT 2 OR  &SR(1) GT 12).ERR2                      03674800
.LBL0410 ANOP                                                           03684400
.********************************************************************** 03694000
.*       Generate first segment of code for MF=E request.             * 03703600
.*       IHBINNRA macro initializes reg1 with address of parmlist.    * 03710000
.********************************************************************** 03716400
&NAME    IHBINNRA &MF(2)                                                03722800
         AIF   ('&SR' EQ '').INITLST                                    03732400
         AIF   ('&SR'(1,1) NE '(').LBL0420                              03742000
         ST    &SR(1),4(0,1)      STORE ADDRESS OF SYMPTOM RECORD       03751600
         AGO   .INITLST                                                 03761200
.LBL0420 ANOP                                                           03770800
         LA    15,&SR             STORE SYMPTOM RECORD ADDRESS          03780400
         ST    15,4(0,1)          INTO THE PARAMETER LIST               03790000
.INITLST ANOP                                                           03799600
.********************************************************************** 03801200
.*       INITLST puts defaults in the parmlist fields that belong to  * 03802800
.*        the SYMREC service and are not accessable through some      * 03804400
.*        external/user interface.                                    * 03806000
.********************************************************************** 03807600
         MVI   0(1),X'&VERLVL'    INITIALIZE LEVEL OF SYMREC MACRO      03809200
         XC    1(&RSVLEN1,1),1(1) CLEAR FIRST RESERVE FIELDS            03818800
         XC    8(&RSVLEN2,1),8(1) CLEAR SECOND RESERVE FIELDS           03828400
.********************************************************************** 03830300
.*       Goto INVOKE to use the common segment that generates the code* 03832200
.*        that issues the PC to the service routine.                  * 03834100
.********************************************************************** 03836000
         AGO   .INVOKE                                                  03838000
.LFORM   ANOP                                                           03847600
         AIF   ('&MF(1)' NE 'L').ERR4                                   03857200
.********************************************************************** 03871600
.*       Validity checks for the list form.                             03886000
.*        1- One and only one value on the MF keyword.                  03888400
.*        2- SR is optional but it must be an A-type addr if specified. 03890800
.*        3- SR keyword can have 1 and only 1 option/subparameter. @P1A 03892000
.********************************************************************** 03893200
         AIF   (N'&MF NE 1).ERR3                                        03895600
         AIF   ('&SR' EQ '').NOSR                                       03905200
         AIF   (N'&SR GT 1).ERR6                                   @P1A 03908400
         AIF   ('&SR(1)' EQ '').ERR7                               @P1A 03911600
         AIF   ('&SR'(1,1) EQ '(').ERR5                                 03914800
         AGO   .SRISOK                                                  03924400
.NOSR    ANOP                                                           03934000
&SRATYPE SETB  0                                                        03943600
.SRISOK  ANOP                                                           03953200
.********************************************************************** 03962800
.*       Build and initialize the list form on word boundary.         * 03972400
.********************************************************************** 03982000
         DS    0F                 ALIGN THE LIST TO WORD BOUNDARY       03991600
&NAME    DS    0CL&LISTLEN                                              04001200
.GENLIST ANOP                                                           04010800
.********************************************************************** 04012100
.*       GENLIST is common to the List and Standard forms.            * 04013400
.*       It generates the parameter list in the static area.          * 04014700
.*       It also uses constants to initialize the fields that the     * 04016000
.*       SYMREC services own or have a reserve classification.        * 04017300
.********************************************************************** 04018600
&LABELL  DC    XL1'&VERLVL'       LEVEL AND VERSION OF SYMREC MACRO     04020400
&LABELO  DC    XL3'000000'        RESERVED                              04030000
.*       If sraddr not an A-type address, zero it in generated list.    04039600
         AIF   (&SRATYPE).ATYPE                                         04049200
&LABELS  DC    A(0)               ADDRESS OF SYMPTOM RECORD             04058800
         AGO   .RESERVE                                                 04068400
.ATYPE   ANOP                                                           04078000
&LABELS  DC    A(&SR)             ADDRESS OF SYMPTOM RECORD             04087600
.RESERVE ANOP                                                           04097200
&LABELR  DC    XL8'0000000000000000' RESERVED                           04106800
.********************************************************************** 04108700
.*       If generating code for MF=L, then the processing is all done.* 04110600
.*       Exit from the macro.                                         * 04112500
.********************************************************************** 04114400
         AIF   ('&MF' NE 'L').GENSTD                                    04116400
         MEXIT                                                          04126000
.GENSTD  ANOP                                                           04135600
&LABELX  DS    0H                                                       04150000
.********************************************************************** 04200000
.*       INVOKE is the code that invokes the PC to the service routine* 04250000
.*       It is common to the standard and execute form.               * 04300000
.********************************************************************** 04350000
.INVOKE  ANOP                                                           04400000
         ESAR  15                 GET CALLER'S SASID                    04450000
         STM   15,12,16(13)       SAVE CALLER'S REGISTERS AND SASID     04500000
         L     2,16(0,0)          ACCESS THE CVT                        04550000
         L     2,772(0,2)         ACCESS THE SFT                        04600000
         L     2,148(0,2)         OBTAIN THE PC NUMBER FOR SYMREC       04650000
         PC    0(2)               ISSUE THE PC                          04700000
         L     2,16(0,13)         LOAD THE SAVED SASID                  04750000
         SSAR  2                  RESTORE CALLER'S SASID                04800000
         LM    2,12,28(13)        RESTORE REGISTERS 2-12                04850000
         MEXIT                                                          04900000
.********************************************************************** 05000000
.*       DIAGNOSTICS                                                  * 07050000
.********************************************************************** 07100000
.ERR1    ANOP                                                           07150000
         MNOTE 8,'THE SR PARAMETER IS MISSING OR INCOMPLETE. IT IS REQUX07183300
               IRED'                                               @P1A 19750000
         MEXIT                                                          19800000
.ERR2    ANOP                                                           19850000
         MNOTE 8,'REGISTER VALUE FOR SR PARAMETER IS NOT 2-12'          19900000
         MEXIT                                                          19950000
.ERR3    ANOP                                                           20000000
         MNOTE 8,'INCORRECT NUMBER OF SUBPARAMETERS ON MF KEYWORD'      20050000
         MEXIT                                                          20100000
.ERR4    ANOP                                                           20150000
         MNOTE 8,'MF PARAMETER SPECIFIED AN INVALID MACRO FORM'         20200000
         MEXIT                                                          20250000
.ERR5    ANOP                                                           20300000
         MNOTE 8,'SR CANNOT BE A REGISTER ON MF=L MACRO FORM'           20350000
         MEXIT                                                          20400000
.ERR6    ANOP                                                      @P1A 20450000
         MNOTE 8,'ONLY ONE ARGUMENT IS ALLOWED FOR SR KEYWORD'     @P1A 20500000
         MEXIT                                                     @P1A 20550000
.ERR7    ANOP                                                      @P1A 20600000
         MNOTE 8,'THE SR PARAMETER IS INCOMPLETE.'                 @P1A 20650000
         MEXIT                                                     @P1A 20700000
         MEND                                                           20750000
* END OF BAL SYMREC MACRO                                            */ 20800000
* SYMREC: /***********************************************************/ 20850000
*         /* INVOKE ?SYMRECP - PL/S VERSION OF THE SYMREC MACRO      */ 20900000
*         /***********************************************************/ 20950000
* MACRO                            /* ?SYMREC INVOKES ?SYMRECP       */ 21000000
*   KEYS(SR,                       /* SYMPTOM RECORD ADDRESS         */ 21050000
*        MF,                       /* MACRO FORM                     */ 21100000
*        RETCODE,                  /* RETURN CODE VARIABLE NAME      */ 21150000
*        REASON);                  /* REASON CODE VARIABLE NAME      */ 21200000
* ANS( '?' || MACLABEL || 'SYMRECP ' || MACLIST || MACKEYS || ';' );/*  21250000
*                                     INVOKE ?SYMRECP                */ 21300000
*%END SYMREC;                                                           21350000
