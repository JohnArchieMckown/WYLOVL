*/**** START OF SPECIFICATIONS ****************************************         
***********************************************************************         
*                                                                     *         
*01* MACRO NAME:  IKJFREE                           Macro Date: 08/91 *         
*                                                                     *         
*01* DESCRIPTIVE NAME:  Dynamic Storage Return Macro                  *         
*                                                                     *         
*****PROPRIETARY_STATEMENT********************************************          
* *01* PROPRIETARY STATEMENT=                                        *          
*                                                                    *          
*                                                                    *          
*   LICENSED MATERIALS - PROPERTY OF IBM                             *          
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      *          
*   5685-025 (C) COPYRIGHT IBM CORP. 1989, 1991                      *          
*   SEE COPYRIGHT INSTRUCTIONS                                       *          
*                                                                    *          
*   STATUS= HTE22D2                                                  *          
*                                                                    *          
*  **END_OF_PROPRIETARY_STATEMENT*************************************          
*                                                                     *         
*01* FUNCTION:                                                        *         
*      The Free Storage Macro will return storage to the storage      *         
*      pool (if given) or a FREEMAIN is used to return the            *         
*      storage.                                                       *         
*                                                                     *         
*01* INVOCATION REQUIREMENTS:                                         *         
*                                                                     *         
*      Restrictions:     This macro is intended only for use in TSO/E *         
*                        modules.  The storage pool is not an         *         
*                        intended programming interface.              *         
*                                                                     *         
*01* INVOCATION FORMAT:                                               *         
*                                                                     *         
*          IKJFREE   LV=(  ),                                         *         
*                    A=(  ),                                          *         
*                    SP=(   ),                                        *         
*                    SAVEREGS=YES                                     *         
*                                                                     *         
*      Where:                                                         *         
*                                                                     *         
*                                                                     *         
*       A                                                             *         
*          is the parameter that represents a register to contain     *         
*          the address of the storage returned.  All registers are    *         
*          valid except register 2-4.                                 *         
*                                                                     *         
*                                                                     *         
*       LV                                                            *         
*          is the parameter that represents a register containing     *         
*          the length of storage to obtain.   All registers are       *         
*          valid except register 2-4.                                 *         
*                                                                     *         
*                                                                     *         
*       SP                                                            *         
*          is the parameter that represents the register containing   *         
*          the subpool to obtain the storage in. All registers are    *         
*          valid except register 2-4.  This is ignored if the storage *         
*          is returned to the storage pool.                           *         
*                                                                     *         
*       SAVEREGS=YES                                                  *         
*          indicates to save and restore registers 2-4 which are used *         
*          as work registers by the macro.  Use of this parameter     *         
*          depends on a savearea pointed to by register 13.           *         
*                                                                     *         
*  NOTE:  Registers 2-4 cannot be used on any of the above parameters *         
*         since they are used as work registers by this macro.        *         
*                                                                     *         
*                                                                     *         
*01* RETURN CODES:                                                    *         
*                                                                     *         
*            0.   The storage release was successful. This will       *         
*                 be the case if the storage is released to the       *         
*                 pool or via FREEMAIN.        Since an unconditional *         
*                 FREEMAIN will be issued, no other return code will  *         
*                 be returned.                                        *         
*                                                                     *         
*01* MODULE TYPE:  MACRO                                              *         
*                                                                     *         
*02*   PROCESSOR:  ASSEMBLER                                          *         
*                                                                     *         
*01* COMPONENT:  28502                                                *         
*                                                                     *         
*01* DISTRIBUTION LIBRARY: ADISTLIB                                   *         
*                                                                     *         
*01* CHANGE-ACTIVITY:                                                 *         
*A00000000-99999999 Created for APAR OY46651                  @YA46651*         
*                                                                     *         
*    OY46969 - Remove the serialization on the free side.             *         
*              If the storage falls within the stack then             *         
*              this task (current) is the originating task            *         
*              and owns the stack. Then this task can manipulate      *         
*              the stack without any other task interfering.          *         
*              If the storage falls outside the stack then it         *         
*              will be freed via FREEMAIN.                    @YA46969*         
*                                                                     *         
****** END OF SPECIFICATIONS ******************************************         
         MACRO                                                                  
&LABEL   IKJFREE &LV=,&SP=,&A=,&SAVEREGS=                                       
.*                                                                              
.*                                                                              
         AIF   ('&SAVEREGS' NE 'YES').NOSAVE                                    
         STM   2,4,28(13)                                                       
.NOSAVE  L     4,548                   POINT TO THE ASCB    (PSAAOLD)           
         L     4,108(,4)               POINT TO THE ASXB    (ASCBASXB)          
         L     4,20(,4)                POINT TO LWA         (ASXBLWA)           
         LTR   4,4                     IS IT NON-ZERO                           
         BZ    IKJ1&SYSNDX             NO - DO FREEMAIN                         
         CLI   67(4),X'3C'             IS IT AT TSO/E 2.2 OR HIGHER?            
         BL    IKJ1&SYSNDX             NO - DO FREEMAIN                         
         L     4,544(4)                POINT TO STORAGE pool                    
         LTR   4,4                     ENSURE IT EXISTS                         
         BZ    IKJ1&SYSNDX             NO pool - DO FREEMAIN                    
         LA    2,36(4)                 GET ADDR OF START OF pool                
         CR    &A,2                    Is the Specified Address lower           
*                                      than the start of the pool               
         BL    IKJ1&SYSNDX             YES - DO FREEMAIN       @YA46969         
         L     2,548                   POINT TO THE ASCB    (PSAAOLD)           
         L     2,108(,2)               POINT TO THE ASXB    (ASCBASXB)          
         L     2,20(,2)                POINT TO LWA         (ASXBLWA)           
         C     &A,548(2)               Is the specified address higher          
*                                      than the end of the pool?                
         BH    IKJ1&SYSNDX             YES - DO FREEMAIN       @YA46969         
         LR    3,&A                    Calculate the end address of             
         AR    3,&LV                   the storage to be freed                  
         BCTR  3,0                                                              
         C     3,548(2)               Does the specified storage extend         
*                                     past the end of the pool?                 
         BH    IKJ4&SYSNDX             Yes, Error Encountered  @YA46969         
         L     3,20(4)                 R3 = pool start ptr                      
         C     3,548(2)                Is pool start ptr higher                 
*                                      than the end of the pool?                
         BH    IKJ4&SYSNDX             IF YES, pool in error   @YA46969         
         LA    2,36(4)                 R2 = start addr of storage area          
         CR    3,2                     Is pool ptr below starting               
*                                      addr of storage area?                    
         BL    IKJ4&SYSNDX             IF YES, pool in error   @YA46969         
         LR    3,&A                    Calculate the end address of             
         AR    3,&LV                   the storage to be freed                  
         C     3,20(4)                 Is the end address + 1 equal             
*                                      to the pool start ptr?                   
         BNE   IKJ4&SYSNDX             No, Error Encountered   @YA46969         
         ST    &A,20(4)                Update pool start to start               
*                                      address of specified area                
         B     IKJ4&SYSNDX                                     @YA46969         
IKJ1&SYSNDX DS 0H                                                               
         AIF   ('&SP' NE '').SPFREE                                             
         FREEMAIN RU,A=&A,LV=&LV                                                
         AGO   .EXITF                                                           
.SPFREE  FREEMAIN RU,A=&A,LV=&LV,SP=&SP                                         
.EXITF   ANOP                                                                   
IKJ4&SYSNDX DS 0H                                                               
         LM    2,4,28(13)              RESTORE REGISTERS 2-4                    
         MEND                                                                   
