         MACRO                                                          00050000
.********************************************************************** 00100000
.*                                                                    * 00150000
.* $MAC(ARCHRCAL) COMP(SERVICE) PROD(DFHSM):                          * 00200000
.* MACRO NAME = ARCHRCAL                                              * 00250000
.* FUNC = SERVICE                                                     * 00300000
.* SUBFUNC = MWE                                                      * 00350000
.*                                                                    * 00400000
.* DESCRIPTIVE NAME = REQUEST DFHSM RECALL A MIGRATED DATA SET.       * 00450000
.*                                                                    * 00500000
.********************************************************************** 00537500
.*PROPRIETARY V3 STATEMENT                                            * 00575000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00612500
.*"RESTRICTED MATERIALS OF IBM"                                       * 00650000
.*5695-DF1                                                            * 00678100
.*(C) COPYRIGHT 1989  IBM CORP.                                       * 00706200
.*END PROPRIETARY V3 STATEMENT                                        * 00734300
.********************************************************************** 00762500
.*                                                                    * 00800000
.* STATUS = VERSION 2, RELEASE 6, LEVEL 0                             * 00850000
.*                                                                    * 00900000
.* FUNCTION = PERFORM DFHSM SVC 109 TO RECALL THE SPECIFIED           * 00950000
.*            MIGRATED DATA SET.                                      * 01000000
.*                                                                    * 01050000
.* NOTES = SEE BELOW                                                  * 01100000
.*                                                                    * 01150000
.*   DEPENDENCIES =                                                   * 01200000
.*            IF ASYNCHRONOUS PROCESSING IS REQUESTED, APPLICATION    * 01250000
.*            IS RESPONSIBLE FOR FREEING THE MWE STORAGE IN CSA.      * 01300000
.*            USE THE ARCFMWE MACRO TO FREE THE MWE AFTER THE ECB     * 01350000
.*            IN THE MWE HAS BEEN POSTED.                             * 01400000
.*                                                                    * 01450000
.*   RESTRICTIONS = THE APPLICATION MUST BE APF AUTHORIZED TO USE     * 01500000
.*            ASYNCHRONOUS PROCESSING.                                * 01550000
.*                                                                    * 01600000
.*   REGISTER CONVENTIONS =                                           * 01650000
.*                                                                    * 01700000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 01750000
.*            WHEN MACRO IS ISSUED.                                   * 01800000
.*                                                                    * 01850000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 01900000
.*                                                                    * 01950000
.*   PATCH LABEL = NONE                                               * 02000000
.*                                                                    * 02050000
.* MODULE TYPE = MACRO                                                * 02100000
.*                                                                    * 02150000
.*   PROCESSOR = BAL                                                  * 02200000
.*                                                                    * 02250000
.* INPUT =                                                            * 02300000
.*          DSN      (REQUIRED) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 02350000
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  * 02400000
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       * 02450000
.*                                                                    * 02500000
.*               NOTE:     TOVOL AND UNIT MUST BOTH BE SPECIFIED      * 02550000
.*                         IF THE RECALL IS DIRECTED TO A SPECIFIC    * 02600000
.*                         VOLUME.                                    * 02650000
.*                                                                    * 02700000
.*          TOVOL    SPECIFY THE ADDRESS OF A 6 CHARACTER VOLUME      * 02750000
.*                   SERIAL OF THE VOLUME CONTAINING THE DATA SET,    * 02800000
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           * 02850000
.*                                                                    * 02900000
.*          UNIT     SPECIFY THE ADDRESS OF A 4 CHARACTER UCB DEVICE  * 02950000
.*                   TYPE OF THE DASD VOLUME CONTAINING THE DATA SET. * 03000000
.*                                                                    * 03050000
.*          WAIT     (OPTIONAL) SPECIFY YES|NO WHETHER APPLICATION    * 03100000
.*                   DESIRES TO WAIT UNTIL DFHSM HAS PROCESSED THIS   * 03150000
.*                   REQUEST.  DEFAULT FOR THIS PARAMETER IS NO.      * 03200000
.*                                                                    * 03250000
.*          ASYNC    (OPTIONAL) SPECIFY YES|NO WHETHER ASYNCHRONOUS   * 03300000
.*                   RECALL PROCESSING IS REQUESTED.  ASYNC=YES       * 03350000
.*                   CAUSES THE MWE TO BE BUILT IN CSA AND ITS ECB    * 03400000
.*                   ADDRESS WILL BE RETURNED IN REGISTER 1.          * 03450000
.*                   DEFAULT FOR THIS PARAMETER IS NO.                * 03500000
.*                                                                    * 03550000
.*              NOTE: ASYNC=YES REQUIRES WAIT=YES ALSO BE SPECIFIED.  * 03600000
.*                                                                    * 03650000
.*          TEST     (OPTIONAL) SPECIFY YES|NO WHETHER MACRO IN TEST  * 03700000
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD BE   * 03750000
.*                   BUILT BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  * 03800000
.*                   FOR THIS PARAMETER IS NO.                        * 03850000
.*                                                                    * 03900000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 03950000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 04000000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 04050000
.*                                                                    * 04100000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 04150000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 04200000
.*                   THIS MACRO.                                      * 04250000
.*                                                                    * 04300000
.*          DAOPT    (OPTIONAL) SPECIFY SAMETRK|RELTRK|RELBLK     @01A* 04350000
.*                   DEPENDING ON THE VOLUME SELECTION AND DATA   @01A* 04400000
.*                   ACCESSING REQUIRED FOR THE RECALL OF A DA    @01A* 04450000
.*                   DATA SET                                         * 04500000
.*                                                                    * 04550000
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 04600000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 04650000
.*            REGISTER 1  WILL CONTAIN ADDRESS OF ECB IN CSA AREA     * 04700000
.*                        OR ZERO IF ASYNC NOT SPECIFIED              * 04750000
.*                                                                    * 04800000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 04850000
.*            THE FOLLOWING RETURN CODES:                             * 04900000
.*                                                                    * 04950000
.*            402 - LOCATE ERROR OCCURRED FOR SPECIFIED DATA SET      * 05000000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 05050000
.*                                                                    * 05100000
.* CHANGE ACTIVITY =                                                  * 05150000
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA* 05200000
.*                            PROCESSOR                           @KDA* 05250000
.* $01=OY40118,260,020591,THA4: ROLLIN APAR OY39056 - DA DATA     @01A* 05300000
.*                              SET IMPROVEMENTS SPE              @01A* 05350000
.*                                                                    * 05400000
.********************************************************************** 05450000
&NAME    ARCHRCAL &DSN=,&UNIT=,&ASYNC=NO,&WAIT=NO,&TEST=NO,            X05500000
               &WKAREA=,&TOVOL=,&SNAPDCB=,&DAOPT=                 @01C  05550000
         LCLA  &A                                                       05600000
         LCLB  &ERROR                                                   05650000
         LCLC  &H                                                       05700000
         LCLC  &L1                                                      05750000
         LCLC  &L2                                                      05800000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      05850000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      05900000
.*                                                                      05950000
.* CHECK THE PARAMETERS SPECIFIED WITH THE HRECALL FUNCTION             06000000
.*                                                                      06050000
.CHKRCL  ANOP                                                           06100000
         AIF   ('&DSN' NE '').RCL1                                      06150000
         MNOTE 8,'REQUIRED PARAMETER, DSN, NOT SPECIFIED'               06200000
&ERROR   SETB  1                                                        06250000
.RCL1    ANOP                                                           06300000
         AIF   ('&WKAREA' NE '').RCL2                                   06350000
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         06400000
&ERROR   SETB  1                                                        06450000
.RCL2    ANOP                                                           06500000
         AIF   ('&UNIT' EQ '' AND '&TOVOL' EQ '').RCL3                  06550000
         AIF   ('&UNIT' NE '' AND '&TOVOL' NE '').RCL3                  06600000
         MNOTE 8,'UNIT AND TOVOL MUST EITHER BOTH BE SPECIFIED OR NEITHX06650000
               ER SPECIFIED'                                            06700000
&ERROR   SETB  1                                                        06750000
.RCL3    ANOP                                                           06800000
         AIF   ('&ASYNC' NE 'YES').RCL4                                 06850000
         AIF   ('&WAIT' EQ 'YES').RCL4                                  06900000
         MNOTE 8,'ASYNC=YES REQUIRES THAT WAIT=YES ALSO BE SPECIFIED'   06950000
&ERROR   SETB  1                                                        07000000
.RCL4    ANOP                                                     @01A  07050000
         AIF   ('&DAOPT' EQ '').RCL5                              @01A  07100000
         AIF   ('&DAOPT' EQ 'SAMETRK').RCL5                       @01A  07150000
         AIF   ('&DAOPT' EQ 'RELTRK').RCL5                        @01A  07200000
         AIF   ('&DAOPT' EQ 'RELBLK').RCL5                        @01A  07250000
         MNOTE 8,'DAOPT SUBPARAMETER SPECIFIED INCORRECTLY'       @01A  07300000
&ERROR   SETB  1                                                  @01A  07350000
.RCL5    AIF   (NOT &ERROR).BUILD                                 @01C  07400000
         MEXIT                                                          07450000
.*                                                                      07500000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       07550000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         07600000
.*                                                                      07650000
.BUILD   ANOP                                                           07700000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             07750000
&NAME    LA    15,&WKAREA                                               07800000
         AGO   .CLRWKA                                                  07850000
.WREG    ANOP                                                           07900000
&A       SETA  K'&WKAREA-2                                              07950000
&H       SETC  '&WKAREA'(2,&A)                                          08000000
&NAME    LR    15,&H                                                    08050000
.CLRWKA  ANOP                                                           08100000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  08150000
         B     *+12                BRANCH AROUND IDENTIFIER             08200000
         DC    CL8'DFHSMREQ'                                            08250000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   08300000
         B     *+12                BRANCH AROUND FUNCTION NAME          08350000
         DC    CL8'HRECALL '       FUNCTION NAME                        08400000
         MVC   8(8,15),*-8                                              08450000
.XSYNC   AIF   ('&ASYNC' NE 'YES').XWAIT                                08500000
         OI    16(15),X'80'        ASYNC WAS SPECIFIED                  08550000
.XWAIT   AIF   ('&WAIT' NE 'YES').XTEST                                 08600000
         OI    16(15),X'40'        WAIT WAS SPECIFIED                   08650000
.XTEST   AIF   ('&TEST' NE 'YES').XDSN                                  08700000
         OI    16(15),X'02'        TEST WAS SPECIFIED                   08750000
.XDSN    ANOP                                                           08800000
         AIF   ('&DSN'(1,1) EQ '(').DREG                                08850000
         LA    1,&DSN              GET ADDR OF DSN                      08900000
         ST    1,20(15)            STORE IN PARM LIST                   08950000
         AGO   .XSNAP                                                   09000000
.DREG    ANOP                                                           09050000
&A       SETA  K'&DSN-2                                                 09100000
&H       SETC  '&DSN'(2,&A)                                             09150000
         ST    &H,20(15)           STORE ADDR OF DSN                    09200000
.XSNAP   ANOP                                                           09250000
         AIF   ('&SNAPDCB' EQ '').XUNIT                                 09300000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            09350000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              09400000
         ST    1,188(15)           STORE IN PARM LIST                   09450000
         AGO   .XUNIT                                                   09500000
.SREG    ANOP                                                           09550000
&A       SETA  K'&SNAPDCB-2                                             09600000
&H       SETC  '&SNAPDCB'(2,&A)                                         09650000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       09700000
.XUNIT   ANOP                                                           09750000
         AIF   ('&UNIT' EQ '').XTOVOL                                   09800000
         AIF   ('&UNIT'(1,1) EQ '(').UREG                               09850000
         LA    1,&UNIT             GET ADDR OF UNIT                     09900000
         ST    1,24(15)            STORE IN PARM LIST                   09950000
         AGO   .XTOVOL                                                  10000000
.UREG    ANOP                                                           10050000
&A       SETA  K'&UNIT-2                                                10100000
&H       SETC  '&UNIT'(2,&A)                                            10150000
         ST    &H,24(15)           STORE ADDR OF UNIT PARM              10200000
.XTOVOL  ANOP                                                           10250000
         AIF   ('&TOVOL' EQ '').XDAOPT                            @01C  10300000
         AIF   ('&TOVOL'(1,1) EQ '(').TOREG                             10350000
         LA    1,&TOVOL            GET ADDR OF TOVOL                    10400000
         ST    1,52(15)            STORE IN PARM LIST                   10450000
         AGO   .XDAOPT                                            @01C  10500000
.TOREG   ANOP                                                           10550000
&A       SETA  K'&TOVOL-2                                               10600000
&H       SETC  '&TOVOL'(2,&A)                                           10650000
         ST    &H,52(15)           STORE ADDR OF TOVOL PARM             10700000
.XDAOPT  ANOP                                                     @01A  10750000
         AIF   ('&DAOPT' EQ '').GIVER                             @01A  10800000
         AIF   ('&DAOPT' NE 'SAMETRK').DAOPT2                     @01A  10850000
         OI    17(15),X'40'        DAOPT=SAMETRK WAS SPECIFIED    @01A  10900000
         AGO   .GIVER                                             @01A  10950000
.DAOPT2  AIF   ('&DAOPT' NE 'RELTRK').DAOPT3                      @01A  11000000
         OI    17(15),X'80'        DAOPT=RELTRK WAS SPECIFIED     @01A  11050000
         AGO   .GIVER                                             @01A  11100000
.DAOPT3  AIF   ('&DAOPT' NE 'RELBLK').GIVER                       @01A  11150000
         OI    17(15),X'C0'        DAOPT=RELBLK WAS SPECIFIED     @01A  11200000
.GIVER   ANOP                                                           11250000
         LR    1,15                PARAMETER ADDRESS TO REG 1           11300000
         LINK  EP=ARCGIVER,ERRET=&L1                                    11350000
         L     15,192(1)           LOAD RETURN CODE                     11400000
         L     0,196(1)            LOAD REASON CODE                     11450000
         L     1,72(1)             ADDRESS OF ECB IF ASYNC SPECIFIED    11500000
         B     &L2                                                      11550000
.NOMWE   ANOP                                                           11600000
&L1      LA    15,806              INDICATE LINK FAILED                 11650000
&L2      EQU   *                                                        11700000
         MEXIT                                                          11750000
         MEND                                                           11800000
