         MACRO                                                          00050000
&NAME    CALLDISP &BRANCH=NO,&FIXED=YES,&FRRSTK=NOSAVE                  00100000
.*                                                                      00150000
.********************************************************************** 00200000
.*                                                                    * 00250000
.*01* MACRO-NAME = CALLDISP                                           * 00300000
.*                                                                    * 00350000
.*01* DESCRIPTIVE-NAME = CALL DISPATCHER                              * 00400000
.*                                                                    * 00450000
.*01* PROPRIETARY STATEMENT =                                         * 00491600
.*       LICENSED MATERIALS - PROPERTY OF IBM                         * 00533200
.*       THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                  * 00574800
.*       5695-047 (C) COPYRIGHT IBM CORP. 1977, 1992                  * 00616400
.*       SEE COPYRIGHT INSTRUCTIONS                                   * 00658000
.*                                                                    * 00700000
.*01* STATUS = HBB4430                                                * 00750000
.*                                                                    * 00800000
.*01* FUNCTION = CAUSES THE EXECUTING TASK TO EXIT TO THE DISPATCHER  * 00850000
.*                                                                    * 00900000
.*01* MODULE-TYPE = MACRO                                             * 00950000
.*                                                                    * 01000000
.*02* PROCESSOR = ASSEMBLER                                           * 01050000
.*                                                                    * 01100000
.*01* MACRO PARAMETERS =                                              * 01250000
.*         BRANCH= NO  - GENERATE AN SVC AND DO NOT USE THE           * 01300000
.*                       BRANCH ENTRY SERVICE ROUTINES                * 01350000
.*                 YES - GENERATE A BALR TO THE BRANCH ENTRY          * 01400000
.*                       SERVICE ROUTINES (CALLER MUST BE SUPERVISOR  * 01450000
.*                       STATE, KEY 0)                                * 01500000
.*         FIXED=  YES - CODE INVOKING CALLDISP IS IN FIXED STORAGE   * 01550000
.*                 NO  - CODE INVOKING CALLDISP IS IN PAGEABLE STORAGE* 01600000
.*         FRRSTK= NOSAVE - REQUESTS THAT THE CURRENT FRR             * 01650000
.*                          STACK IS NOT TO BE SAVED AND              * 01700000
.*                          RESTORED ACROSS CALLDISP SERVICE          * 01750000
.*                 SAVE   - REQUESTS THAT THE FRRS ON THE             * 01800000
.*                          NORMAL FRR STACK BE SAVED AND             * 01850000
.*                          RESTORED ACROSS CALLDISP SERVICE          * 01900000
.*                                                                    * 01950000
.*01* MACROS = IHBERMAC AND IHAPSA (NEEDED TO USE MACRO)              * 02000000
.*                                                                    * 02050000
.*01* CHANGE-ACTIVITY = G50DPXH,ZA30340,                              * 02100000
.*                      @G38JPXY - CROSS MEMORY SUPPORT               * 02150000
.*                      @ZMB0960 - REMOVE SVT MACRO DEPENDENCY        * 02175000
.*  $P1=PKB3453  HBB4430  920617  PD16H5:  ALLOW AR MODE CALLERS      * 02187500
.*  $01=OY65852  HBB4430  930802  PD16EI:  TEST FOR SYSTEM LEVEL AND  * 02191600
.*                                         SKIP AR CODE IF XA LEVEL   * 02195700
.*                                                                    * 02200000
.*01* MNOTES = IHB002 INVALID BRANCH OPERAND SPECIFIED - XXX          * 02250000
.*             IHB002 INVALID FIXED OPERAND SPECIFIED - XXX           * 02300000
.*             IHB002 INVALID FRRSTK OPERAND SPECIFIED - XXX          * 02350000
.*             IHB280 FIXED=XXX OPTION INVALID WITH BRANCH=NO OPTION  * 02450000
.*             IHB280 FRRSTK=XXX OPTION INVALID WITH BRANCH=NO OPTION * 02500000
.*                                                                    * 02550000
.********************************************************************** 02600000
.* A-000000-999999                                             @G38JPXY 02650000
.*                                                                      02700000
*        MACDATE = 08/03/93                                        @01C 02750000
.*                                                                      02800000
.*                                                                      02850000
         LCLC  &BRLIT             BRANCH LITERAL                        02900000
         LCLC  &FFLIT             FIXED/FRRSTK LITERAL                  02950000
         LCLA  &SVCDSPD           CALLDISP DISABLED EPA        @ZMB0960 02960000
         LCLA  &SVCDSPE           CALLDISP ENABLED EPA         @ZMB0960 02970000
         GBLC  &SYSSPLV           DEFINE GLOBAL SYMBOL             @01A 02975000
&SVCDSPD SETA  X'C4'              INIT TO SVTDSPD OFFSET       @ZMB0960 02980000
&SVCDSPE SETA  X'C0'              INIT TO SVTDSPE OFFSET       @ZMB0960 02990000
.*                                                                      03000000
.*                                                                      03050000
         AIF   ('&BRANCH' EQ 'YES').BRANCH   IF BRANCH OPTION 'YES' IS  03550000
.*                                     SPECIFIED GO HANDLE              03600000
         AIF   ('&BRANCH' EQ 'NO').NOBRNCH   IF BRANCH OPTION 'NO' IS   03650000
.*                                     SPECIFIED GO HANDLE              03700000
.*                                                                      03750000
.*   IHB002 INVALID BRANCH OPERAND SPECIFIED - XXX                      03800000
         IHBERMAC 1001,BRANCH,&BRANCH                                   03850000
         MEXIT                                                          03900000
.*                                                                      03950000
.NOBRNCH ANOP                          CHECK OTHER OPERANDS             04000000
         AIF   ('&FIXED' EQ 'YES').NOBRTST  BR IF &FIXED OK             04050000
.*                                                                      04100000
.*                                                                      04150000
.*   IHB280 FIXED OPTION INVALID WITH BRANCH=NO OPTION                  04200000
&FFLIT   SETC  'FIXED=&FIXED. OPTION'  SET ERROR MESSAGE                04250000
&BRLIT   SETC  'BRANCH=NO OPTION'      LITERALS FOR IHBERMAC            04300000
         IHBERMAC 1020,&FFLIT,&BRLIT   ISSUE ERROR                      04350000
         MEXIT                         EXIT MACRO                       04400000
.*                                                                      04450000
.*                                                                      04500000
.NOBRTST ANOP                                                           04550000
         AIF   ('&FRRSTK' EQ 'NOSAVE').NOBRGEN  BR IF FRRSTK OK         04600000
.*                                                                      04650000
.*                                                                      04700000
.*   IHB280 FRRSTK OPTION INVALID WITH BRANCH=NO OPTION                 04750000
&FFLIT   SETC  'FRRSTK=&FRRSTK. OPTION'  SET ERROR MESSAGE              04800000
&BRLIT   SETC  'BRANCH=NO OPTION'      LITERALS FOR IHBERMAC            04850000
         IHBERMAC 1020,&FFLIT,&BRLIT   ISSUE ERROR                      04900000
         MEXIT                         EXIT MACRO                       04950000
.*                                                                      05000000
.*                                                                      05050000
.NOBRGEN ANOP                          GEN CODE FOR NO BRANCH           05100000
&NAME    SLR   15,15                   .CALLDISP ESR CODE ZERO          05150000
         SVC   137                     .EXTENDED SVC ROUTER - TYPE 6    05200000
         MEXIT                                                          05250000
.*                                                                      05300000
.*                                                                      05350000
.*                                                                      05400000
.*                                                                      05450000
.*                                                                      05500000
.BRANCH  ANOP                          VALIDITY CHECK FRRSTK OPTION     05550000
         AIF   ('&FRRSTK' EQ 'NOSAVE' OR                               -05600000
               '&FRRSTK' EQ 'SAVE').FIXTST   BRANCH IF VALID            05650000
.*                                                                      05700000
.*                                                                      05750000
.*   IHB002 INVALID FRRSTK OPERAND SPECIFIED - XXX                      05800000
         IHBERMAC 1001,FRRSTK,&FRRSTK                                   05850000
         MEXIT                                                          05900000
.*                                                                      05950000
.*                                                                      06000000
.FIXTST  ANOP                          VALIDITY CHECK FIXED OPTION      06050000
         AIF   ('&FIXED' EQ 'YES').FIXED   IF FIXED OPTION 'YES' IS     06100000
.*                                     SPECIFIED GO HANDLE              06150000
         AIF   ('&FIXED' EQ 'NO').NOFIXED   IF FIXED OPTION 'NO' IS     06200000
.*                                     SPECIFIED GO HANDLE              06250000
.*                                                                      06300000
.*   IHB002 INVALID FIXED OPERAND SPECIFIED - XXX                       06350000
         IHBERMAC 1001,FIXED,&FIXED                                     06400000
         MEXIT                                                          06450000
.*                                                                      06500000
.FIXED   ANOP                          GEN CODE FOR FIXED OPTION        06550000
&NAME    STNSM PSASTNSM-PSA(0),X'FC'   .DISABLE                         06600000
         STM   14,1,PSACDSAV-PSA(0)    .SAVE REGS 14 THRU 1             06650000
         SPLEVEL TEST                  IF GLOBAL SYMBOL HAS NO     @01A 06655000
.*                                     VALUE, SET TO THE DEFAULT.       06660000
         AIF   ('&SYSSPLV' LT '3').XAF IF SYSTEM LEVEL XA, SKIP    @01A 06665000
.*                                     AR CODE FOR FIXED OPTION 'YES'   06670000
         SLR   15,15                   CLEAR REG 15                @P1A 06675000
         SAR   15,15                   SET UP AR                   @P1A 06700000
.*                                                                      06706200
.XAF     ANOP                          SKIP AR CODE FOR FIXED      @01A 06712400
.*                                     OPTION IS 'YES'                  06718600
         L     15,PSASVT-PSA(0)        .SVT ADDRESS                     06725000
         L     15,&SVCDSPD.(,15)       .CALLDISP DISABLED EPA--FROM    X06750000
                                       SVTCDSPD                @ZMB0960 06775000
         AGO   .GENFRRS                GO GEN CODE FOR &FRRSTK          06800000
.*                                                                      06850000
.NOFIXED ANOP                          GEN CODE FOR THE NO FIXED OPTION 06900000
         SPLEVEL TEST                  IF GLOBAL SYMBOL HAS NO     @01A 06905000
.*                                     VALUE, SET TO THE DEFAULT.       06910000
         AIF   ('&SYSSPLV' LT '3').XANF  IF SYSTEM LEVEL XA, SKIP  @01A 06915000
.*                                     AR CODE FOR FIXED OPTION 'NO'    06920000
&NAME    SLR   15,15                   CLEAR REG 15                @P1A 06925000
         SAR   15,15                   SET UP AR                   @P1A 06950000
         L     15,PSASVT-PSA(0)        .SVT ADDRESS                @P1C 06975000
         L     15,&SVCDSPE.(,15)       .CALLDISP ENABLED EPA--FROM     X07000000
                                       SVTCDSPE                @ZMB0960 07025000
         AGO   .GENFRRS                GO FINISH GEN FOR FRRS      @01A 07028100
.*                                                                      07031200
.XANF    ANOP                          SKIP AR CODE FOR FIXED      @01A 07034300
.*                                     OPTION IS 'NO'                   07037400
&NAME    L     15,PSASVT-PSA(0)        .SVT ADDRESS                @01A 07040500
         L     15,&SVCDSPE.(,15)       .CALLDISP ENABLED EPA--FROM     X07043600
                                       SVTCDSPE                    @01A 07046700
.*                                                                      07050000
.*                                                                      07100000
.GENFRRS ANOP                          GEN CODE FOR &FRRSTK             07150000
         AIF   ('&FRRSTK' EQ 'SAVE').SAVE  GO GEN FOR SAVE FRR          07200000
.*                                                                      07250000
.NOSAVE  ANOP                          GEN CODE FOR NOSAVE              07300000
         SLR   0,0                     .INDICATE FRRSTK=NOSAVE OPTION   07350000
         AGO   .GENCOM                 GO FINISH GEN OF CODE            07400000
.*                                                                      07450000
.SAVE    ANOP                          GEN CODE FOR SAVE                07500000
         LA    0,4                     .INDICATE FRRSTK=SAVE OPTION     07550000
.*                                                                      07600000
.*                                                                      07650000
.GENCOM  ANOP                          FINISH CODE GEN                  07700000
         SLR   1,1                     .CLEAR REGISTER 1                07750000
         BALR  14,15                   .ENTER THE CALLDISP ROUTINE      07800000
         MEXIT                                                          07850000
.*                                                                      07900000
         MEND                                                           07950000
