*/**** START OF SPECIFICATIONS ****************************************         
***********************************************************************         
*                                                                     *         
*01* MACRO NAME:  IKJGET                            Macro Date: 08/91 *         
*                                                                     *         
*01* DESCRIPTIVE NAME:  Dynamic Storage Obtain Macro                  *         
*                                                                     *         
*****PROPRIETARY_STATEMENT********************************************          
* *01* PROPRIETARY STATEMENT=                                        *          
*                                                                    *          
*                                                                    *          
*   LICENSED MATERIALS - PROPERTY OF IBM                             *          
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                      *          
*   5685-025 (C) COPYRIGHT IBM CORP. 1989, 1991                      *          
*   SEE COPYRIGHT INSTRUCTIONS                                       *          
*                                                                    *          
*   STATUS= HTE22D2                                                  *          
*                                                                    *          
*  **END_OF_PROPRIETARY_STATEMENT*************************************          
*                                                                     *         
*01* FUNCTION:                                                        *         
*      The Obtain Storage macro will obtain storage from the storage  *         
*      pool (if given) or a GETMAIN is used to obtain the storage.    *         
*                                                                     *         
*01* INVOCATION REQUIREMENTS:                                         *         
*                                                                     *         
*      Restrictions:     This macro is intended only for use in TSO/E *         
*                        modules.  The storage pool is not an         *         
*                        intended programming interface.              *         
*                                                                     *         
*01* INVOCATION FORMAT:                                               *         
*                                                                     *         
*          IKJGET    LV=(  ),                                         *         
*                    SP=(   ),                                        *         
*                    SAVEREGS=YES                                     *         
*                                                                     *         
*      Where:                                                         *         
*                                                                     *         
*                                                                     *         
*       LV                                                            *         
*          is the parameter that represents the register containing   *         
*          the length of storage to obtain.  Registers 2-4 are not    *         
*          valid for this parameter.                                  *         
*                                                                     *         
*       SP                                                            *         
*          is the parameter that represents the register containing   *         
*          the subpool to obtain the storage in.  Registers 2-4 are   *         
*          not valid for this parameter.  This parameter is ignored   *         
*          when the storage is obtained from the storage pool.        *         
*                                                                     *         
*       SAVEREGS=YES                                                  *         
*          indicates to save and restore registers 2-4 which are used *         
*          as work registers by the macro.  This parameter depends on *         
*          a savearea pointed to from register 13.                    *         
*                                                                     *         
*  NOTE:  Registers 2-4 cannot be used on any of the above parameters *         
*         since they are used as work registers by this macro.        *         
*                                                                     *         
*                                                                     *         
*01* RETURN CODES:                                                    *         
*                                                                     *         
*            0.   The storage allocation was successful. This will    *         
*                 be the case if the storage comes either from the    *         
*                 pool or from GETMAIN.   Since an unconditional      *         
*                 GETMAIN will be issued, no other return code will   *         
*                 be returned.                                        *         
*                                                                     *         
*01* MODULE TYPE:  MACRO                                              *         
*                                                                     *         
*02*   PROCESSOR:  ASSEMBLER                                          *         
*                                                                     *         
*01* COMPONENT:  28502                                                *         
*                                                                     *         
*01* DISTRIBUTION LIBRARY: ADISTLIB                                   *         
*                                                                     *         
*01* CHANGE-ACTIVITY:                                                 *         
*A00000000-99999999 Created for APAR OY46651                  @YA46651*         
*                                                                     *         
****** END OF SPECIFICATIONS ******************************************         
         MACRO                                                                  
&LABEL   IKJGET &LV=,&SP=,&SAVEREGS=                                            
.*                                                                              
.*                                                                              
         AIF   ('&SAVEREGS' NE 'YES').NOSAVE                                    
         STM   2,4,28(13)                                                       
.NOSAVE  L     4,548                   POINT TO THE ASCB    (PSAAOLD)           
         L     4,108(,4)               POINT TO THE ASXB    (ASCBASXB)          
         L     4,20(,4)                POINT TO LWA         (ASXBLWA)           
         LTR   4,4                     Does it exist                            
         BZ    IKJ1&SYSNDX             NO - DO GETMAIN                          
         CLI   67(4),X'3C'             IS IT AT TSO/E 2.2 OR HIGHER?            
         BL    IKJ1&SYSNDX             NO - DO GETMAIN                          
         L     4,544(4)                POINT TO STORAGE pool DATA AREA          
         LTR   4,4                     ENSURE IT EXISTS                         
         BZ    IKJ1&SYSNDX             NO pool - DO GETMAIN                     
         SR    2,2                     CLEAR R2                                 
         IPK                           PUT OUR KEY IN R2                        
         AIF   ('&SYSECT' EQ 'IKJEFT4A').KEY0 IKJEFT4A RECEIVES                 
.*                                     CONTROL IN KEY 0.                        
         AIF   ('&SYSECT' EQ 'IKJEFT54').KEY0 IKJEFT54 RECEIVES                 
.*                                     CONTROL IN KEY 0.                        
         LA    3,128                   R3 = KEY 8                               
         CR    2,3                     ARE WE KEY 8 ?                           
         BNE   IKJ1&SYSNDX             NOT KEY 8 - DO GETMAIN                   
         AGO   .CONT                                                            
.KEY0    LA    3,0                     R3 = KEY 0                               
         CR    2,3                     ARE WE KEY 0 ?                           
         BNE   IKJ1&SYSNDX             NOT KEY 0 - DO GETMAIN                   
.CONT    SR    2,2                     Zero Register 2                          
         L     3,536                   POINT TO CURRENT TCB  (PSATOLD)          
         CS    2,3,16(4)               SEE IF NOT IN USE                        
         BNE   IKJ1&SYSNDX             IF CS FAILED - DO GETMAIN                
         LA    2,36(4)                 R2 = ADDR OF start of storage            
         C     2,20(4)                 Is the pool Top pointing to the          
*                                      start of the storage area?               
         BNE   IKJ2&SYSNDX             NO, Continue processing                  
         ST    3,12(4)                 YES, Set the originator TCB addr         
*                                      with the current TCB addr                
IKJ2&SYSNDX DS 0H                                                               
         C     3,12(4)                 Is Originating TCB and the               
*                                      current TCB the same?                    
         BNE   IKJ4&SYSNDX             IF NOT, Deserialize & do GETMAIN         
         L     3,20(4)                 R3 = pool top ptr                        
         LA    2,36(4)                 R2 = Start addr of storage area          
         CR    3,2                     Is pool ptr below starting addr          
*                                      of storage area?                         
         BL    IKJ4&SYSNDX             IF YES, Deserialize & do GETMAIN         
         L     2,548                   POINT TO THE ASCB    (PSAAOLD)           
         L     2,108(,2)               POINT TO THE ASXB    (ASCBASXB)          
         L     2,20(,2)                POINT TO LWA         (ASXBLWA)           
         C     3,548(2)                Is pool top addr higher than             
*                                      the END ADDR of pool AREA?               
         BH    IKJ4&SYSNDX             IF YES, Deserialize & GETMAIN            
         AR    3,&LV                   ADD length of current request            
*                                      to pool top ptr                          
         BCTR  3,0                     Subtract one to get end address          
         C     3,548(2)                Is enough storage available?             
         BH    IKJ4&SYSNDX             IF NOT, Deserialize & do GETMAIN         
         L     1,20(4)                 Put current pool top address             
*                                      in Register 1 for return                 
         LA    3,1(3)                  Add one to get new pool top              
         ST    3,20(4)                 Update pool top ptr                      
         B     IKJ3&SYSNDX             Deserialize and Exit                     
IKJ4&SYSNDX DS 0H                                                               
         SR    3,3                     Clear register 3                         
         ST    3,16(4)                 Unserialize pool Data Area               
IKJ1&SYSNDX DS 0H                                                               
         AIF   ('&SP' NE '').SPGET                                              
         GETMAIN RU,LV=&LV,LOC=BELOW                                            
         AGO   .EXITG                                                           
.SPGET   GETMAIN RU,LV=&LV,LOC=BELOW,SP=&SP                                     
.EXITG   ANOP                                                                   
         B    IKJ5&SYSNDX                                                       
IKJ3&SYSNDX DS 0H                                                               
         SR    3,3                     Clear register 3                         
         ST    3,16(4)                 Unserialize pool Data Area               
IKJ5&SYSNDX DS 0H                                                               
         LM    2,4,28(13)              RESTORE REGISTERS 2-4                    
         MEND                                                                   
