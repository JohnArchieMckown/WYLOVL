         MACRO                                                          00010002
&NAME    PICTURE &ID=,&GATHER=,&REPORT=,&MAXRBS=,&RBLEN=,&RPTTYP=,     X00020002
               &TITLE=,&DGTEXT=,&DRTEXT=,&PFK=,&FBLEN=0,&IOML=0,       X00030002
               &HELP=,&WLMMODE=0,&STYP=                            @H1A 00040002
.*   $N1=MON2,HRM3102,,FLS: RMF 3.1 SMF RECORD RESTRUCTURE         @N1A 00050002
.*   $P1=IOMLSP,HRM3302,,WEY: I/O MEASUREMENT LEVEL SUPPORT        @P1A 00060002
.*   $ZA=SPEV4,@WA05515,,HOL: USABILITY ENHANCEMENTS - ISPF        @ZAA 00070002
.*   $C1=R510,HRM5512,,GBO: USE ALIAS NAME                         @C1A 00071002
.*   $ZB=SPEV4,@WA05515,,HOL: USABILITY ENHANCEMENTS V5 - ISPF     @ZBA 00072002
.*   $H1=HFS,HRM6607,,SHR: HFS support                             @H1A 00072102
         LCLC  &RBNUM,&IDX,&DESC,&GTXT,&RTXT,&PFKNUM               @LBA 00072202
&IDX     SETC  '&SYSNDX'                                                00072302
         AIF   ('&RPTTYP' EQ 'T').TYPOK                                 00072402
         AIF   ('&RPTTYP' EQ 'R').TYPOK                                 00072502
         AGO   .TYPNG                                                   00072602
.TYPOK   ANOP                                                           00072702
&STYPV   SETA  00                Default all sessions              @H1A 00072802
         AIF   ('&STYP' EQ '').ALLSES                              @H1A 00072902
         AIF   ('&STYP' EQ 'ALL').ALLSES                           @H1A 00073002
         AIF   ('&STYP' EQ 'IO').IONLY                             @H1A 00074002
         AGO   .BADSES           Bad session typ                   @H1A 00075002
.IONLY   ANOP                                                      @H1A 00076002
&STYPV   SETA  80                ISPF session only                 @H1A 00077002
.ALLSES  ANOP                    All sessions is default           @H1A 00078002
         AIF   ('&ID' EQ '').MSNGID                                     00079002
         AIF   ('&GATHER' EQ '').NOGATH                                 00080002
         AIF   ('&REPORT' EQ '').NOREPT                                 00090002
         AIF   ('&MAXRBS' NE '').MAXRBDF                                00100002
         AIF   ('&RPTTYP' EQ 'R').ROWRPT                                00110002
&RBNUM   SETC  '0'               TABLE REPORT - LET RMF SET DEFAULT     00120002
         AGO   .RBDONE                                                  00130002
.ROWRPT  ANOP                                                           00140002
&RBNUM   SETC  '1'               ROW REPORT - DEFAULT MAX RB'S TO ONE   00150002
         AGO   .RBDONE                                                  00160002
.MAXRBDF ANOP                                                           00170002
&RBNUM   SETC  '&MAXRBS'                                                00180002
.RBDONE  ANOP                                                           00190002
         AIF   ('&RBLEN' EQ '').NORBLEN                                 00200002
         AIF   ('&TITLE' NE '').SETDESC                                 00210002
&DESC    SETC  ''' '''                                                  00220002
         AGO   .TITLEOK                                                 00230002
.SETDESC ANOP                                                           00240002
&DESC    SETC  '&TITLE'                                                 00250002
.TITLEOK ANOP                                                           00260002
         AIF   ('&PFK' NE '').SETPFK  PFK NUMBER - DEFAULT IS 0    @LBA 00270002
&PFKNUM  SETC  '0'                SET DEFAULT PFK NUMBER TO 0      @LBA 00280002
         AGO   .PFKDONE                                            @ZAC 00290002
.SETPFK  ANOP                                                      @LBA 00300002
&PFKNUM  SETC  '&PFK'             ASSIGN PFK NUMBER                @LBA 00310002
.PFKDONE ANOP                                                      @LBA 00320002
         AIF   ('&HELP' NE '').SETHELP   HELP PANEL NAME SPECIFIED @ZAA 00330002
&HELPP   SETC  '*'                       SET DEFAULT PANEL TO '*'  @ZAA 00340002
         AGO   .HELPDON                                            @ZAA 00350002
.SETHELP ANOP                                                      @ZAA 00360002
&HELPP   SETC  '&HELP'                                             @ZAA 00370002
.HELPDON ANOP                                                      @ZAA 00380002
&GTXT    SETC  '&DGTEXT'                                                00390002
&RTXT    SETC  '&DRTEXT'                                                00400002
&NAME    DC    AL2(ERBL&IDX-*)    LENGTH OF THIS MENU ENTRY        @ZAC 00410002
         DC    CL8'&ID'           MENU ID FOR PICTURE                   00420002
         DC    CL8'&GATHER'       DATA GATHERER MODULE NAME             00430002
         DC    CL8'&REPORT'       DATA REPORTER MODULE NAME             00440002
         DC    AL2(&RBNUM)        MAX NUMBER OF RELOCATE BLOCKS         00450002
         DC    AL2(&RBLEN)        LENGTH OF EACH RELOCATE BLOCK         00460002
         DC    AL2(&FBLEN)        LENGTH OF DATA CONTROL SECTION   @N1A 00470002
         DC    AL2(&PFKNUM)       PFK NUMBER                       @LBA 00480002
         DC    C'&RPTTYP'         T - TABLE REPORT,R - ROW REPORT       00490002
         DC    AL1(&IOML)         I/O MESUREMENT LEVEL             @P1C 00500002
         DC    AL1(&WLMMODE)      WORKLOAD MANAGER MODE            @ZBA 00510002
*                                 0 = COMPAT MODE                  @ZBA 00520002
*                                 1 = GOAL MODE                    @ZBA 00530002
         DC    XL1'&STYPV'        Session Type                     @H1A 00540002
*                                 00 = All sessions                @H1A 00541002
*                                 80 = ISPF session only           @H1A 00542002
         DC    AL2(ERB@&IDX-*-2)  PICTURE TITLE LENGTH                  00543002
         DC    C&DESC             PICTURE TITLE                         00544002
ERB@&IDX EQU   *                                                        00545002
         DC    AL2(ERB#&IDX-*-2)  DATA GATHERER TEXT LENGTH             00546002
         AIF   ('&GTXT' EQ '').NOGTXT                                   00547002
         DC    C&GTXT             DATA GATHERER COMMAND TEXT            00548002
.NOGTXT  ANOP                                                           00549002
ERB#&IDX EQU   *                                                        00550002
         DC    AL2(ERB$&IDX-*-2)  DATA REPORTER TEXT LENGTH             00560002
         AIF   ('&RTXT' EQ '').NORTXT                                   00570002
         DC    C&RTXT             DATA REPORTER TEXT                    00580002
.NORTXT  ANOP                                                           00590002
ERB$&IDX EQU   *                                                        00600002
         DC    CL8'&HELPP'        HELP PANEL FOR M-II ISPF SESSION @ZAA 00610002
ERBL&IDX EQU   *                  LABEL TO BUILD TOTAL LENGTH      @ZAA 00620002
         MEXIT                                                          00630002
.MSNGID  ANOP                                                           00640002
         MNOTE 8,'PICTURE ID MUST BE SPECIFIED'                         00650002
         MEXIT                                                          00660002
.NOGATH  ANOP                                                           00670002
         MNOTE 8,'DATA GATHERER MODULE NAME MUST BE SPECIFIED'          00680002
         MEXIT                                                          00690002
.NOREPT  ANOP                                                           00700002
         MNOTE 8,'DATA REPORTER MODULE NAME MUST BE SPECIFIED'          00710002
         MEXIT                                                          00720002
.TYPNG   ANOP                                                           00730002
         MNOTE 8,'REPORT TYPE &RPTTYP NOT DEFINED'                      00740002
         MEXIT                                                          00750002
.NORBLEN ANOP                                                           00760002
         MNOTE 8,'RELOCATE BLOCK LENGTH NOT DEFINED'                    00770002
         MEXIT                                                 @PQQ0020 00780002
.BADPFK  ANOP                                                           00790002
         MNOTE 8,'PFK KEYWORD &PFK NOT RECOGNIZED'                      00800002
         MEXIT                                                     @H1A 00810002
.BADSTYP ANOP                                                      @H1A 00820002
         MNOTE 8,'STYP KEYWORD &STYP NOT RECOGNIZED'               @H1A 00830002
         MEND                                                           00840002
                                                                        00850002
