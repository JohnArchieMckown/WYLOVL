         MACRO                                                          00050000
         IST6MCRF &AREA=,&AREALEN=,&USERFLD=,&BRANCH=,&OPTCD=,&ECB=,   X00100000
               &EXIT=,&CONMODE=,&CONXMOD=,&RTSRTRN=,&INVALP1=,         X00150000
               &INVALP2=,&QUALIFY=,&VTRINA=,&VTRINL=,&CD=,             X00183300
               &FILL=,&VTROUTA=,&VTROUTL=                @L1C @D1C @D2C 00216600
.*                                                                      00250000
.* /***** START OF SPECIFICATIONS ************************************/ 00300000
.* /*                                                                */ 00350000
.* /*  MACRO NAME: IST6MCRF                                          */ 00400000
.* /*                                                                */ 00450000
.* /*  DESCRIPTIVE NAME: CONTROL = RCVFMH5 PROCESSOR                 */ 00500000
.* /*                                                                */ 00550000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00600000
.* /*                                                                */ 00650000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00700000
.* /*                                                                */ 00750000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */ 00800000
.* /*             ALL RIGHTS RESERVED.                               */ 00850000
.* /*                                                                */ 00900000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00950000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01000000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01050000
.* /*                                                                */ 01100000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01150000
.* /*                                                                */ 01200000
.* /*                                                                */ 01250000
.* /*  FUNCTION:                                                     */ 01300000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01350000
.* /*      CONTROL=RCVFMH5 PARAMETER ON THE APPCCMD MACRO.           */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*                                                                */ 01550000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01600000
.* /*                                                                */ 01650000
.* /*  CREATED BY:                                                   */ 01700000
.* /*                                                                */ 01750000
.* /*  INTERNAL MACROS:                                              */ 01800000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI                       */ 01850000
.* /*                                                                */ 01900000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01950000
.* /*                                                                */ 02000000
.* /*  CHANGE ACTIVITY:                                              */ 02050000
.* /*    05/27/86 - ZERO OUT THE QUALIFY FIELD               @P030472*/ 02100000
.* /*                                                                */ 02150000
.* /*  $MAC(IST6MCRF),COMP(API),PROD(VTAM): CONTROL=RCVFMH5 PROCESSOR*/ 02200000
.* /*                                                                */ 02250000
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */ 02300000
.* /*  ---- -------- ------- ------ ------  ---------------------    */ 02350000
.* /*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION         */ 02400000
.* /*  $T1= P100610  NQNAME2 920228 879315: ASSEMBLY ERROR ON ASMXF  */ 02450000
.* /*  $I1= FF@@@MST APPCFDX 911114 879315: APPC Full-Duplex support */ 02500000
.* /*  $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services    */ 02550000
.* /*  $L2= KA@DR006 APPCRR  940713 576146: Vector Format            */ 02600000
.* /*  $Y1= P115530  FREEDOM 940801 879315: Eliminate duplicate MVI  */ 02650000
.* /*  $D1= IA@@@MST V51APPC 941025 941213: Queued RCVFMH5 support   */ 02675000
.* /*  $D2= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC   */ 02687500
.* /*                                                                */ 02700000
.* /***** END OF SPECIFICATIONS **************************************/ 02750000
.*                                                                      02800000
.* A-000000-999999                                             @R495810 02850000
.*                                                                      02900000
         GBLB  &ISTGEN                                                  02950000
         GBLB  &ISTRF                                                   03000000
         AIF   (&ISTRF EQ 1).RFINVKW                                    03050000
*                                                                       03100000
*  IST6MCRF - ACF/VTAM                                                  03150000
*                                                                       03200000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03250000
*                                                                       03300000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03350000
*                                                                       03400000
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1993.              03450000
*             ALL RIGHTS RESERVED.                                      03500000
*                                                                       03550000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03600000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03650000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03700000
*                                                                       03750000
*             SEE COPYRIGHT INSTRUCTIONS.                               03800000
*                                                                       03850000
&ISTRF   SETB  1                                                        03900000
.RFINVKW ANOP                                                           03950000
         IST6MCUC RCVFMH5,CONTROL,INVALKW=&INVALP1                 @T1C 04000000
         IST6MCUC RCVFMH5,CONTROL,INVALKW=&INVALP2                 @T1A 04050000
.*                                              CHECK INVALID APPCCMD   04100000
.RFAREA  ANOP                                                           04150000
         AIF   ('&AREA' EQ '').RFALEN                                   04200000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X04250000
               KW=AREA_ADDRESS                                          04300000
.*                                              PUT AREA ADDRESS IN RPL 04350000
.RFALEN  ANOP                                                           04400000
         AIF   ('&AREALEN' EQ '').RFUFLD                                04450000
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X04500000
               KW=AREA_LENGTH                                           04550000
.*                                               PUT AREA LENGTH IN RPL 04600000
.RFUFLD  ANOP                                                           04650000
         AIF   ('&USERFLD' EQ '').RFVECT                           @L1C 04700000
         IST6MCUI KW=USERFLD,KWVAL=&USERFLD,LEN=4,                     X04750000
               LOC=RPL6USR-ISTRPL6X,BASEREG=14                          04800000
.*                                             PUT USERFLD IN APPC AREA 04850000
.RFVECT  ANOP                          ENSURE BOTH PARMS ARE CODED      04900000
.*                                     OR BOTH ARE NOT CODED       @L1A 04950000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).RFVOUT    @D2C 05000000
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).RFERVI     @D2C 05050000
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_ADDRESS,KWVAL=&VTRINA,        X05100000
               LOC=RPL6VAIA-ISTRPL6X,BASEREG=14                    @D2C 05150000
.*                                     STORE VECTOR AREA INPUT ADDRESS  05250000
.*                                                                 @L1A 05300000
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_LENGTH,KWVAL=&VTRINL,LEN=2,   X05350000
               LOC=RPL6VAIL-ISTRPL6X,BASEREG=14                    @D2C 05400000
.*                                     STORE VECTOR AREA INPUT LENGTH   05450000
.*                                                                 @L1A 05500000
         AGO   .RFVOUT                                             @D2C 05518400
.RFERVI  ANOP                                                      @L1A 05536800
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D2C 05555200
.RFVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED      05573600
.*                                     OR BOTH ARE NOT CODED       @D2A 05592000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).RFQFY   @D2A 05610400
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).RFERVO   @D2A 05628800
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X05647200
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D2A 05665600
.*                                     PUT VECTOR AREA INPUT ADDRESS    05684000
.*                                     IN APPC AREA                @D2A 05702400
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X05720800
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D2A 05739200
.*                                     PUT VECTOR AREA INPUT LENGTH     05757600
.*                                     IN APPC AREA                @D2A 05776000
         AGO   .RFQFY                                                   05794400
.RFERVO  ANOP                                                      @D2A 05812800
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D2A 05831200
.RFQFY   ANOP                       ** QUALIFY= PROCESSING **      @L1A 05850000
         AIF   ('&QUALIFY' EQ '').RFNQUAL                          @Y1C 05900000
         AIF   ('&QUALIFY' EQ 'NULL').RFNQUAL                      @L1A 05950000
         AIF   ('&QUALIFY' NE 'DATAQUE').RFQFY1                    @D1A 05959300
         AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 05968600
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DQUE                      @D1A 05977900
*                                      SET QUALIFY TO DATAQUE      @D1A 05987200
         AGO   .RFFILL                                             @D1A 05996500
.RFQFY1  ANOP                                                      @D1A 06005800
         AIF   ('&QUALIFY' NE 'QUEUE').RFQFYER                     @D1A 06015100
         IST6MCUC QUEUE,QUALIFY,INVALKW=(FILL=&FILL,CD=&CD)        @D1A 06024400
         AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 06033700
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6QUE                       @D1A 06043000
*                                      SET QUALIFY TO QUEUE        @D1A 06052300
         AGO   .RFCOMKW                                            @D1A 06061600
.RFQFYER ANOP                                                      @D1A 06070900
         IST6MCER 5,QUALIFY,&QUALIFY                               @D1A 06080200
         AGO   .RFCOMKW                                            @D1A 06089500
.RFNQUAL ANOP                                                      @L1A 06100000
         IST6MCUC NULL,QUALIFY,INVALKW=(FILL=&FILL,CD=&CD)         @D1A 06125000
         AIF   (NOT &ISTGEN).RFEXIT                                @L1A 06150000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6NQUA                      @L1A 06200000
.*                                     SET QUALIFY TO NULL         @L1A 06250000
         AGO   .RFCOMKW                                            @D1A 06251600
.RFFILL  ANOP                       ** FILL= PROCESSING **         @D1A 06253200
         AIF   ('&FILL' EQ '').RFCD                                @D1A 06254800
         AIF   ('&FILL' NE 'LL').RFFIL1                            @D1A 06256400
         AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 06258000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LL  SET FILL TO LL        @D1A 06259600
         AGO   .RFCD                                               @D1A 06261200
.RFFIL1  ANOP                                                      @D1A 06262800
         AIF   ('&FILL' NE 'BUFF').RFFILER                         @D1A 06264400
         AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 06266000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LL                  @D1A 06267600
*                                            SET FILL TO BUFF      @D1A 06269200
         AGO   .RFCD                                               @D1A 06270800
.RFFILER ANOP                                                      @D1A 06272400
         IST6MCER 5,FILL,&FILL                                     @D1A 06274000
.RFCD    ANOP                       ** CD= PROCESSING **           @D1A 06275600
         AIF   ('&CD' EQ '').RFCOMKW                               @D1A 06277200
         AIF   ('&CD' NE 'IMMED').RFCD1                            @D1A 06278800
.RFCD0   AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 06280400
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6CDDE                @D1A 06282000
*                                            SET CD TO IMMED       @D1A 06283600
         AGO   .RFCOMKW                                            @D1A 06285200
.RFCD1   ANOP                                                      @D1A 06286800
         AIF   ('&CD' NE 'DEFER').RFCD2                            @D1A 06288400
         AIF   (NOT &ISTGEN).RFCOMKW                               @D1A 06290000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6CDDE                      @D1A 06291600
*                                            SET CD TO DEFER       @D1A 06293200
         AGO   .RFCOMKW                                            @D1A 06294800
.RFCD2   ANOP                                                      @D1A 06296400
         IST6MCER 5,CD,&CD                                         @D1A 06298000
.RFCOMKW ANOP                                                           06300000
         AIF   (NOT &ISTGEN).RFEXIT                                     06350000
         IST6MCUP EXIT=&EXIT,BRANCH=&BRANCH,OPTCD=&OPTCD,ECB=&ECB,     X06400000
               CONMODE=&CONMODE,CONXMOD=&CONXMOD,                      X06450000
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X06500000
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX06550000
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID     @I1C 06600000
.*                                 OPTCD PARAMETER                      06650000
         AIF   ('&RTSRTRN' EQ '').RFREQ                            @I1A 06700000
         AIF   ('&RTSRTRN' NE 'BOTH').RFRTSRE                      @I1A 06750000
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6RTSX   RESET FLAG   @I1A 06800000
         AGO   .RFREQ                                              @I1A 06850000
.RFRTSRE ANOP                                                      @I1A 06900000
         AIF   ('&RTSRTRN' NE 'EXPD').RFRTSER                      @I1A 06950000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6RTSX         SET FLAG     @I1A 07000000
         AGO   .RFREQ                                              @I1A 07050000
.RFRTSER ANOP                                                      @I1A 07100000
         IST6MCER 5,RTSRTRN,&RTSRTRN         REPORT BAD VALUE      @I1A 07150000
.RFREQ   ANOP                                                           07200000
         AIF   (NOT &ISTGEN).RFEXIT                                     07250000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RFH5                            07300000
*                              PUT RCVFMH5 REQUEST CODE IN APPC AREA    07350000
.RFEXIT  MEND                                                           07400000
