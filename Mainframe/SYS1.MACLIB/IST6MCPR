         MACRO                                                          00050000
         IST6MCPR &CONVID=,&QUALIFY=,&AREA=,&RECLEN=,&CRYPT=,&LOCKS=,  X00100000
               &ECB=,&EXIT=,&OPTCD=,&BRANCH=,&CONMODE=,&CONXMOD=,      X00150000
               &INVALP1=,&INVALP2=,&VTRINA=,&VTRINL=,&VTROUTA=,        X00183300
               &VTROUTL=                                           @D1C 00216600
.*                                                                      00250000
.* /***** START OF SPECIFICATIONS ************************************/ 00300000
.* /*                                                                */ 00350000
.* /*  MACRO NAME: IST6MCPR                                          */ 00400000
.* /*                                                                */ 00450000
.* /*  DESCRIPTIVE NAME: CONTROL = PREPRCV PROCESSOR                 */ 00500000
.* /*                                                                */ 00550000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00600000
.* /*                                                                */ 00650000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00700000
.* /*                                                                */ 00750000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */ 00800000
.* /*             ALL RIGHTS RESERVED.                               */ 00850000
.* /*                                                                */ 00900000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00950000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01000000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01050000
.* /*                                                                */ 01100000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01150000
.* /*                                                                */ 01200000
.* /*                                                                */ 01250000
.* /*  FUNCTION:                                                     */ 01300000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01350000
.* /*      CONTROL=PREPRCV PARAMETER ON THE APPCCMD MACRO.           */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*                                                                */ 01550000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01600000
.* /*                                                                */ 01650000
.* /*  CREATED BY:                                                   */ 01700000
.* /*                                                                */ 01750000
.* /*  INTERNAL MACROS:                                              */ 01800000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */ 01850000
.* /*                                                                */ 01900000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01950000
.* /*                                                                */ 02000000
.* /*  CHANGE ACTIVITY:                                              */ 02050000
.* /*                                                                */ 02100000
.* /* $MAC(IST6MCPR),COMP(API),PROD(VTAM): CONTROL=PREPRCV PROCESSOR */ 02150000
.* /*                                                                */ 02200000
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */ 02250000
.* /* ---- -------- ------- ------ ------  ---------------------     */ 02300000
.* /* $BA= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */ 02350000
.* /* $B1= AE@@@AMS HVT3401 890707 460025: SELECTIVE DATA ENCRYPTION */ 02400000
.* /* $I1= FF@@@MST APPCFDX 911118 879315: APPC Full-Duplex support  */ 02450000
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */ 02500000
.* /* $Y1= P114781  APPCRR  940518 576146: DCE Security Services     */ 02550000
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */ 02600000
.* /* $D1= ID@@@MST FPAPI   950308 143576: Fast Path API for APPC    */ 02625000
.* /*                                                                */ 02650000
.* /***** END OF SPECIFICATIONS **************************************/ 02700000
.*                                                                      02750000
.* A-000000-999999                                             @R495810 02800000
.*                                                                      02850000
         GBLB  &ISTGEN                                                  02900000
         GBLB  &ISTPR                                                   02950000
         AIF   (&ISTPR EQ 1).PRINVKW                                    03000000
*                                                                       03050000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03200000
*                                                                       03250000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03300000
*                                                                       03350000
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.              03400000
*             ALL RIGHTS RESERVED.                                      03450000
*                                                                       03500000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03550000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03600000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03650000
*                                                                       03700000
*             SEE COPYRIGHT INSTRUCTIONS.                               03750000
*                                                                       03800000
&ISTPR   SETB  1                                                        03850000
.PRINVKW ANOP                                                           03900000
         IST6MCUC PREPRCV,CONTROL,INVALKW=&INVALP1                 @I1C 03950000
         IST6MCUC PREPRCV,CONTROL,INVALKW=&INVALP2                 @I1A 04000000
.*                                           CHECK INVALID APPCCMD      04050000
.PRID    ANOP                                                           04100000
         AIF   ('&CONVID' EQ '').PRQFY                                  04150000
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X04200000
               KW=CONVID                                                04250000
.*                                           PUT CONVID IN APPC AREA    04300000
.PRQFY   ANOP                                                           04350000
         AIF   ('&QUALIFY' EQ '').PRLOCK                                04400000
.PRQFY1  ANOP                                                           04450000
         AIF   ('&QUALIFY' NE 'FLUSH').PRQFY2                           04500000
         IST6MCUC FLUSH,QUALIFY,                                       X04550000
               INVALKW=(AREA=&AREA,RECLEN=&RECLEN,LOCKS=&LOCKS,CRYPT=&CX04600000
               RYPT)                                               @B1C 04650000
         AIF   (NOT &ISTGEN).PRCMKW2                                    04700000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6FLSH                           04750000
*                                       PUT FLUSH CODE IN APPC AREA     04800000
         AGO   .PRCMKW2                                                 04850000
.PRQFY2  ANOP                                                           04900000
         AIF   ('&QUALIFY' NE 'CONFIRM').PRQFY3                         04950000
         IST6MCUC CONFIRM,QUALIFY,INVALKW=(AREA=&AREA,RECLEN=&RECLEN,CRX05000000
               YPT=&CRYPT)                                         @B1C 05050000
         AIF   (NOT &ISTGEN).PRLOCK                                     05100000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CFRM                           05150000
*                                       PUT CONFIRM CODE IN APPC AREA   05200000
         AGO   .PRLOCK                                                  05250000
.PRQFY3  ANOP                                                           05300000
         AIF   ('&QUALIFY' NE 'DATAFLU').PRQFY4                         05350000
         IST6MCUC DATAFLU,QUALIFY,INVALKW=(LOCKS=&LOCKS)                05400000
         AIF   (NOT &ISTGEN).PRCMKW1                                    05450000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DFLU                           05500000
*                                       PUT DATAFLU CODE IN APPC AREA   05550000
         AGO   .PRAREA                                                  05600000
.PRQFY4  ANOP                                                           05650000
         AIF   ('&QUALIFY' NE 'DATACON').PRQFY5                         05700000
         AIF   (NOT &ISTGEN).PRLOCK                                     05750000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DCON                           05800000
*                                       PUT DATACON CODE IN APPC AREA   05850000
         AGO   .PRLOCK                                                  05900000
.PRQFY5  ANOP                                                           05950000
         IST6MCER 5,QUALIFY,&QUALIFY                                    06000000
.PRLOCK  ANOP                                                           06050000
         AIF   ('&LOCKS' EQ '').PRCKPT                                  06100000
.PRLOCK1 ANOP                                                           06150000
         AIF   ('&LOCKS' NE 'SHORT').PRLOCK2                            06200000
         AIF   (NOT &ISTGEN).PRCKPT                                     06250000
         OI    RPL6FLG3-ISTRPL6X(14),RPL6SHRT SET LOCKS TO SHORT        06300000
         AGO   .PRCKPT                                                  06350000
.PRLOCK2 ANOP                                                           06400000
         AIF   ('&LOCKS' NE 'LONG').PRLOCK3                             06450000
         AIF   (NOT &ISTGEN).PRCKPT                                     06500000
         NI    RPL6FLG3-ISTRPL6X(14),X'FF'-RPL6SHRT                     06550000
*                                            SET LOCKS TO LONG          06600000
         AGO   .PRCKPT                                                  06650000
.PRLOCK3 ANOP                                                           06700000
         IST6MCER 5,LOCKS,&LOCKS                                        06750000
.PRCKPT  ANOP                                                           06800000
         AIF   ('&QUALIFY' EQ 'CONFIRM').PRCMKW2                        06850000
.PRAREA  ANOP                                                           06900000
         AIF   ('&AREA' EQ '').PRDLEN                                   06950000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X07000000
               KW=DATA_ADDRESS                                          07050000
.*                                           PUT DATA ADDRESS IN RPL    07100000
.PRDLEN  ANOP                                                           07150000
         AIF   ('&RECLEN' EQ '').PRCRYPT                                07200000
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X07250000
               KW=DATA_LENGTH                                           07300000
.*                                           PUT DATA LENGTH IN RPL     07350000
.PRCRYPT ANOP                                                    @B1A   07400000
         AIF   ('&CRYPT' EQ '').PRCMKW1 CRYPT PARAMETER?         @B1A   07450000
         AIF   ('&CRYPT'(1,3) EQ 'YES').PRCRYES CRYPT REQUEST?   @B1A   07500000
         AIF   ('&CRYPT'(1,2) EQ 'NO').PRCRYNO CRYPT NON-REQ.?   @B1A   07550000
         MNOTE  4,'CRYPT=&CRYPT INVALID-IGNORED'                 @B1A   07600000
         AGO   .PRCMKW1                                          @B1A   07650000
.PRCRYES ANOP                                                    @B1A   07700000
         OI    RPLEXTD1-IFGRPL(1),X'08'    SET CRYPT REQUEST ON  @B1A   07750000
         AGO   .PRCMKW1                                          @B1A   07800000
.PRCRYNO ANOP                                                    @B1A   07850000
         NI    RPLEXTD1-IFGRPL(1),X'F7'  SET CRYPT OFF           @B1A   07900000
.*   QUALIFY=FLUSH OR CONFIRM BRANCHES TO PRCMKW2, THE REST OF          07950000
.*   QUALIFIES AND UNSPECIFIED QUALIFY BRANCH TO PRCMKW1                08000000
.*                                                                      08050000
.PRCMKW1 ANOP                                                           08100000
         IST6MCUP CONMODE=&CONMODE,OPTCD=&OPTCD,ECB=&ECB,              X08194000
               EXIT=&EXIT,BRANCH=&BRANCH,CONXMOD=&CONXMOD,             X08200000
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X08250000
               CKSYN=YES, ASY/SYN IS VALID FOR OPTCD PARAMETER         X08300000
               CKBUFF=XBUF      XBUFLST/BUFFLST/NBUFFLST IS VALID  @D1C 08333300
.*                              OPCTD PARAMETER                    @D1A 08366600
         AGO   .PRVECT                                             @Y1C 08400000
.PRCMKW2 ANOP                                                           08450000
         IST6MCUP CONMODE=&CONMODE,OPTCD=&OPTCD,ECB=&ECB,              X08500000
               EXIT=&EXIT,BRANCH=&BRANCH,CONXMOD=&CONXMOD,             X08550000
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X08600000
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD          X08650000
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID     @I1C 08700000
.*                                 OPTCD PARAMETER                      08750000
.PRVECT  ANOP                      ENSURE BOTH PARMS CODED OR BOTH ARE  08800000
.*                                 NOT CODED                  @L1A @Y1M 08850000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).PRVOUT    @D1C 08900000
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).PRERVI     @D1C 08950000
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X09050000
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C 09100000
.*                                     STORE VECTOR AREA INPUT ADDRESS  09150000
.*                                                                 @L1A 09200000
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X09250000
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C 09300000
.*                                     STORE VECTOR AREA INPUT LENGTH   09350000
.*                                                                 @L1A 09400000
         AGO   .PRVOUT                                             @L1A 09419400
.PRERVI  ANOP                                                 @L1A @Y1M 09438800
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C 09458200
.PRVOUT  ANOP                      ENSURE BOTH PARMS CODED OR BOTH ARE  09477600
.*                                 NOT CODED                       @D1A 09497000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).PRREQ   @D1A 09516400
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).PRERVO   @D1A 09535800
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X09555200
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A 09574600
.*                                     PUT VECTOR AREA INPUT ADDRESS    09594000
.*                                     IN APPC AREA                @D1A 09613400
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X09632800
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A 09652200
.*                                     PUT VECTOR AREA INPUT LENGTH     09671600
         AGO   .PRREQ                                              @D1A 09691000
.PRERVO  ANOP                                                      @D1A 09710400
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A 09729800
.PRREQ   ANOP                                                           09750000
         AIF   (NOT &ISTGEN).PREND                                      09800000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6PREC                            09850000
*                              PUT PREPRCV REQUEST CODE IN APPC AREA    09900000
.PREND   MEND                                                           09950000
