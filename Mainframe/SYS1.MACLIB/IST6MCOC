         MACRO                                                          00050000
         IST6MCOC &QUALIFY=,&LUNAME=,&LOGMODE=,&AREA=,&ECB=,&EXIT=,    X00100000
               &OPTCD=,&BRANCH=,&RECLEN=,&AREALEN=,&INVALKW=,&ARG=,    X00150000
               &CONFTXT=,&USERFLD=,&LIST=,&NETID=,&NAMEUSE=,&VTRINA=,  X00200000
               &VTRINL=,&VTROUTA=,&VTROUTL=                        @D1C 00250000
.*                                                                      00300000
.* /***** START OF SPECIFICATIONS ************************************/ 00350000
.* /*                                                                */ 00400000
.* /*  MACRO NAME: IST6MCOC                                          */ 00450000
.* /*                                                                */ 00500000
.* /*  DESCRIPTIVE NAME: CONTROL = OPRCNTL PROCESSOR                 */ 00550000
.* /*                                                                */ 00600000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00650000
.* /*                                                                */ 00700000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00750000
.* /*                                                                */ 00800000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */ 00850000
.* /*             ALL RIGHTS RESERVED.                               */ 00900000
.* /*                                                                */ 00950000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 01000000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01050000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01100000
.* /*                                                                */ 01150000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01200000
.* /*                                                                */ 01250000
.* /*  FUNCTION:                                                     */ 01300000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01350000
.* /*      CONTROL=OPRCNTL PARAMETER ON THE APPCCMD MACRO.           */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*                                                                */ 01550000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01600000
.* /*                                                                */ 01650000
.* /*  CREATED BY:                                                   */ 01700000
.* /*                                                                */ 01750000
.* /*  INTERNAL MACROS:                                              */ 01800000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI,IST6MCER              */ 01850000
.* /*                                                                */ 01900000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01950000
.* /*                                                                */ 02000000
.* /*  CHANGE ACTIVITY:                                              */ 02050000
.* /*    09/03/86 - DP0053 ALLOW USERFLD ON OPRCNTL          @P041406*/ 02100000
.* /*    11/19/87 - ALLOW RECLEN ON ACTSESS                  @P051842*/ 02150000
.* /*                                                                */ 02200000
.* /* $MAC(IST6MCOC),COMP(API),PROD(VTAM): CONTROL=OPRCNTL PROCESSOR */ 02250000
.* /*                                                                */ 02300000
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */ 02350000
.* /* ---- -------- ------- ------ ------  ---------------------     */ 02400000
.* /* $B1= AP@@@CLD HVT3401 890508 496698: PERSISTENT LU-LU SESSIONS */ 02450000
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */ 02500000
.* /* $KF= NQNSTG2  NQNAME2 910605 000000: ENHANCED NQN SUPPORT (2)  */ 02550000
.* /* $01= OY43143  VM340   910731 617408: UPDATE RPL6USR WITH       */ 02600000
.* /*                                      PROVIDED USRFLD VALUE     */ 02650000
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */ 02700000
.* /* $U2= P110887  GENRES  931024 879315: Generic Resource support  */ 02750000
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */ 02800000
.* /* $D1= ID@@@MST FPAPI   950309 143576: Fast Path API for APPC    */ 02825000
.* /***** END OF SPECIFICATIONS **************************************/ 02850000
.*                                                                      02900000
.* A-000000-999999                                             @R495810 02950000
.*                                                                      03000000
         GBLB  &ISTGEN                                                  03050000
         GBLB  &ISTOC                                                   03100000
         AIF   (&ISTOC EQ 1).OCINVKW                                    03150000
*                                                                       03200000
*  IST6MCOC - ACF/VTAM                                                  03250000
*                                                                       03300000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03350000
*                                                                       03400000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03450000
*                                                                       03500000
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1993.              03550000
*             ALL RIGHTS RESERVED.                                      03600000
*                                                                       03650000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03700000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03750000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03800000
*                                                                       03850000
*             SEE COPYRIGHT INSTRUCTIONS.                               03900000
*                                                                       03950000
&ISTOC   SETB  1                                                        04000000
.OCINVKW ANOP                                                           04050000
         IST6MCUC OPRCNTL,CONTROL,INVALKW=&INVALKW                      04100000
.*                                         CHECK INVALID APPCCMD        04150000
.OCQFY   ANOP                                                           04200000
         AIF   ('&QUALIFY' EQ '').OCCONF1                               04250000
.OCQFY1  ANOP                                                           04300000
         AIF   ('&QUALIFY' NE 'ACTSESS').OCQFY2                         04350000
         IST6MCUC ACTSESS,QUALIFY,INVALKW=(AREALEN=&AREALEN,LOGMODE=&LOX04400000
               GMODE,LUNAME=&LUNAME,LIST=&LIST,NETID=&NETID,NAMEUSE=&NAX04450000
               MEUSE)                                              @U2C 04500000
         AIF   (NOT &ISTGEN).OCCONF1                                    04550000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ACT                            04600000
*                                         PUT ACTSESS CODE IN APPC AREA 04650000
.OCRLEN1 ANOP                                                  @P051842 04700000
         AIF   ('&RECLEN' EQ '').OCAREA1                       @P051842 04750000
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X04800000
               KW=DATA_LENGTH                                  @P051842 04850000
.OCAREA1 ANOP                                                           04900000
         AIF   ('&AREA' EQ '').OCCONF1                                  04950000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X05000000
               KW=AREA_ADDRESS                                          05050000
         AGO   .OCCONF1                                                 05100000
.OCQFY2  ANOP                                                           05150000
         AIF   ('&QUALIFY' NE 'DACTSESS').OCQFY3                        05200000
         IST6MCUC DACTSESS,QUALIFY,INVALKW=(AREA=&AREA,AREALEN=&AREALENX05250000
               ,LOGMODE=&LOGMODE,LUNAME=&LUNAME,RECLEN=&RECLEN,CONFTXT=X05300000
               &CONFTXT,LIST=&LIST,NETID=&NETID,NAMEUSE=&NAMEUSE)  @U2C 05350000
         AIF   (NOT &ISTGEN).OCARG                                      05400000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DACT                           05450000
*                                        PUT DACTSESS CODE IN APPC AREA 05500000
         AGO   .OCARG                                                   05550000
.OCQFY3  ANOP                                                           05600000
         AIF   ('&QUALIFY' NE 'DISPLAY').OCQFY4                         05650000
         IST6MCUC DISPLAY,QUALIFY,INVALKW=(RECLEN=&RECLEN,ARG=&ARG,CONFX05700000
               TXT=&CONFTXT,LIST=&LIST,NAMEUSE=&NAMEUSE)           @U2C 05750000
         AIF   (NOT &ISTGEN).OCLUNAM                                    05800000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DSPY                           05850000
*                                         PUT DISPLAY CODE IN APPC AREA 05900000
.OCALEN1 ANOP                                                           05950000
         AIF   ('&AREALEN' EQ '').OCAREA2                               06000000
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X06050000
               KW=AREA_LENGTH                                           06100000
         AGO   .OCAREA2                                                 06150000
.OCQFY4  ANOP                                                           06200000
         AIF   ('&QUALIFY' NE 'CNOS').OCQFY5                            06250000
         IST6MCUC CNOS,QUALIFY,INVALKW=(AREALEN=&AREALEN,ARG=&ARG,CONFTX06300000
               XT=&CONFTXT,LIST=&LIST)                             @B1C 06350000
         AIF   (NOT &ISTGEN).OCLUNAM                                    06400000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CNOS                           06450000
*                                          PUT CNOS CODE IN APPC AREA   06500000
         AGO   .OCRLEN                                                  06550000
.OCQFY5  ANOP                                                           06600000
         AIF   ('&QUALIFY' NE 'DEFINE').OCQFY6                          06650000
         IST6MCUC DEFINE,QUALIFY,INVALKW=(AREALEN=&AREALEN,ARG=&ARG,CONX06700000
               FTXT=&CONFTXT,LIST=&LIST,NAMEUSE=&NAMEUSE)          @U2C 06750000
         AIF   (NOT &ISTGEN).OCLUNAM                                    06800000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6DFIN                           06850000
*                                         PUT DEFINE CODE IN APPC AREA  06900000
         AGO   .OCRLEN                                                  06950000
.OCQFY6  ANOP                                                           07000000
         AIF   ('&QUALIFY' NE 'RESTORE').OCQFY7                    @B1A 07050000
         IST6MCUC RESTORE,QUALIFY,INVALKW=(RECLEN=&RECLEN,ARG=&ARG,CONFX07100000
               TXT=&CONFTXT,NAMEUSE=&NAMEUSE)                      @U2C 07150000
         AIF   (NOT &ISTGEN).OCLUNAM                               @B1A 07200000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6REST                      @B1A 07250000
*                                         PUT RESTORE CODE IN APPC AREA 07300000
         AIF   ('&LIST' NE '').OCLINA     LIST= SPECIFIED?         @B1A 07350000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS-RPL6LIAL       @B1A 07400000
*                                         DEFAULT LIST TO NONE     @B1A 07450000
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A 07500000
.OCLINA  ANOP                                                      @B1A 07550000
         AIF   ('&LIST' NE 'ALL').OCLINO  SPECIFY LIST=ALL IN RPL  @B1A 07600000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS                @B1A 07650000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LIAL                      @B1A 07700000
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A 07750000
.OCLINO  ANOP                                                      @B1A 07800000
         AIF   ('&LIST' NE 'NONE').OCLINS SPECIFY LIST=NONE IN RPL @B1A 07850000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LINS-RPL6LIAL       @B1A 07900000
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A 07950000
.OCLINS  ANOP                                                      @B1A 08000000
         AIF   ('&LIST' NE 'NOSESS').OCLIER   SPECIFY LIST=NOSESS  @B1A 08050000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LIAL                @B1A 08100000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LINS                      @B1A 08150000
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A 08200000
.OCLIER  ANOP                                                      @B1A 08250000
         IST6MCER 5,LIST,&LIST                                     @B1A 08300000
         AGO   .OCALEN1                   CHECK LENGTH KEYWORD     @B1A 08350000
.OCQFY7  ANOP                                                      @B1A 08400000
         IST6MCER 5,QUALIFY,&QUALIFY                                    08450000
.OCCONF1 ANOP                                                           08500000
         AIF   ('&CONFTXT' EQ '').OCARG                                 08550000
         AIF   ('&CONFTXT' NE 'YES').OCCONF2                            08600000
         AIF   (NOT &ISTGEN).OCARG                                      08650000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6CFT SET CONFTXT TO YES         08700000
         AGO   .OCARG                                                   08750000
.OCCONF2 ANOP                                                           08800000
         AIF   ('&CONFTXT' NE 'NO').OCCONF3                             08850000
         AIF   (NOT &ISTGEN).OCARG                                      08900000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6CFT                      08950000
*                                            SET CONFTXT TO NO          09000000
         AGO   .OCARG                                                   09050000
.OCCONF3 ANOP                                                           09100000
         IST6MCER 5,CONFTXT,&CONFTXT                                    09150000
.OCARG   ANOP                                                           09200000
         AIF   ('&ARG' EQ '').OCCKPT                                    09250000
         IST6MCUA KWVAL=&ARG,LOC=RPLARG-IFGRPL,BASEREG=1,KW=CID         09300000
.OCCKPT  ANOP                                                           09350000
         AIF   ('&QUALIFY' EQ 'ACTSESS').OCURFLD                   @01A 09400000
         AIF   ('&QUALIFY' EQ 'DACTSESS').OCURFLD                  @01A 09450000
.OCALEN2 ANOP                                                           09500000
         AIF   ('&AREALEN' EQ '').OCRLEN                                09550000
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X09600000
               KW=AREA_LENGTH                                           09650000
.OCRLEN  ANOP                                                           09700000
         AIF   ('&RECLEN' EQ '').OCAREA2                                09750000
         IST6MCUA KWVAL=&RECLEN,LOC=RPLRLEN-IFGRPL,BASEREG=1,          X09800000
               KW=DATA_LENGTH                                           09850000
.OCAREA2 ANOP                                                           09900000
         AIF   ('&AREA' EQ '').OCLUNAM                                  09950000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X10000000
               KW=AREA_ADDRESS                                          10050000
.OCLUNAM ANOP                                                           10100000
         AIF   ('&LUNAME' EQ '').OCNETID                           @KFC 10150000
         IST6MCUI KW=LUNAME,KWVAL=&LUNAME,LEN=8,                       X10200000
               LOC=RPL6LU-ISTRPL6X,BASEREG=14                           10250000
.*                                      PUT LUNAME IN APPC AREA         10300000
.OCNETID ANOP                                                      @KFA 10350000
         AIF   ('&NETID' EQ '').OCLOGM                             @KFA 10400000
         IST6MCUI KW=NETID,KWVAL=&NETID,LEN=8,                         X10450000
               LOC=RPL6NET-ISTRPL6X,BASEREG=14                     @KFA 10500000
.*                                      PUT NETID IN APPC AREA          10550000
.OCLOGM  ANOP                                                           10600000
         AIF   ('&LOGMODE' EQ '').OCURFLD                      @P041406 10650000
         IST6MCUI KW=LOGMODE,KWVAL=&LOGMODE,LEN=8,                     X10700000
               LOC=RPL6MODE-ISTRPL6X,BASEREG=14                         10750000
.*                                     PUT LOGMODE IN APPC AREA         10800000
.OCURFLD ANOP                                                  @P041406 10850000
         AIF   ('&USERFLD' EQ '').OCNMUSE                          @U2C 10900000
         IST6MCUI KW=USERFLD,KWVAL=&USERFLD,LEN=4,                     X10950000
               LOC=RPL6USR-ISTRPL6X,BASEREG=14                 @P041406 11000000
.*                                   PUT USERFLD IN APPC AREA  @P041406 11050000
.OCNMUSE ANOP                                                      @U2A 11100000
         AIF   ('&NAMEUSE' EQ '').OCVECT    NAMEUSE OMITTED?  @U2A @L1C 11150000
         AIF   ('&NAMEUSE' NE 'APNAME').OCNMU1     NAMEUSE=APNAME? @U2A 11200000
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS  @U2A 11250000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6ANAM  SET VALUE TO APNAME @U2A 11300000
         AGO   .OCVECT                                        @U2A @L1C 11350000
.OCNMU1  ANOP                                                      @U2A 11400000
         AIF   ('&NAMEUSE' NE 'GNAME').OCNMUER     NAMEUSE=GNAME?  @U2A 11450000
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6NAMU    RESET BITS  @U2A 11500000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6GNAM  SET VALUE TO GNAME  @U2A 11550000
         AGO   .OCVECT                                        @U2A @L1C 11600000
.OCNMUER ANOP                                                      @U2A 11650000
         IST6MCER 5,NAMEUSE,&NAMEUSE                               @U2A 11700000
.OCVECT  ANOP                        ENSURE BOTH PARMS ARE CODED   @L1A 11750000
.*                                   OR BOTH NOT CODED             @L1A 11800000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).OCVOUT    @D1C 11850000
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).OCERVI     @D1C 11900000
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_ADDRESS,KWVAL=&VTRINA,        X11950000
               LOC=RPL6VAIA-ISTRPL6X,BASEREG=14                    @D1C 12000000
.*                                     STORE VECTOR AREA INPUT ADDRESS  12100000
.*                                                                 @L1A 12150000
         IST6MCUA KW=VTAM_TO_APPL_VECTOR_LENGTH,KWVAL=&VTRINL,LEN=2,   X12200000
               LOC=RPL6VAIL-ISTRPL6X,BASEREG=14                    @L1A 12250000
.*                                     STORE VECTOR AREA INPUT LENGTH   12300000
.*                                                                 @L1A 12350000
         AGO   .OCVOUT                                             @D1C 12368400
.OCERVI  ANOP                                                      @L1A 12386800
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C 12405200
.OCVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED      12423600
.*                                     OR BOTH ARE NOT CODED       @D1A 12442000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).OCCOMKW @D1A 12460400
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).PAERVO   @D1A 12478800
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X12497200
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A 12515600
.*                                     PUT VECTOR AREA INPUT ADDRESS    12534000
.*                                     IN APPC AREA                @D1A 12552400
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X12570800
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A 12589200
.*                                     PUT VECTOR AREA INPUT LENGTH     12607600
.*                                     IN APPC AREA                @D1A 12626000
         AGO   .OCCOMKW                                            @D1A 12644400
.PAERVO  ANOP                                                      @D1A 12662800
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A 12681200
.OCCOMKW ANOP                                                      @L1C 12700000
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,                    X12750000
               BRANCH=&BRANCH,                                         X12800000
               CKCMODE=NO,               DO NOT CHECK CONMODE PARAMETERX12850000
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX12900000
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID FOR      12950000
.*                                 OPTCD PARAMETER                      13000000
.OCREQ   ANOP                                                           13050000
         AIF   (NOT &ISTGEN).OCEXIT                                     13100000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6OPER                            13150000
*                              PUT OPRCNTL REQUEST CODE IN APPC AREA    13200000
.OCEXIT  MEND                                                           13250000
