* %/*  THIS MAKES THE BAL VERSION LOOK LIKE A PL/S MACRO COMMENT        00050000
         MACRO                                                          00100000
.********************************************************************** 00150000
.*                                                                    * 00200000
.*01* MACRO NAME = CONVCON                                            * 00250000
.*                                                                    * 00300000
.*01* DESCRIPTIVE NAME = CONSOLE NAME AND ID CONVERTER MACRO          * 00350000
.*                                                                    * 00400000
.*01* COPYRIGHT =                                                     * 00450000
.*        5695-047                                                    * 00500000
.*        THIS MODULE IS "RESTRICTED MATERIALS OF IBM"                * 00550000
.*        (C) COPYRIGHT IBM CORPORATION 1990                          * 00600000
.*        LICENSED MATERIALS - PROPERTY OF IBM                        * 00650000
.*                                                                    * 00700000
.*01* STATUS =  HBB4410                                               * 00750000
.*                                                                    * 00800000
.*   FUNCTION =                                                       * 00850000
.*                                                                    * 00900000
.*       THE CONVCON MACRO IS USED TO INVOKE THE CONVCON              * 00950000
.*       SERVICE PROCESSOR.  THIS SERVICE IS USED TO OBTAIN           * 01000000
.*       A BINARY CONSOLE ID OR EBCDIC CONSOLE NAME FOR A             * 01050000
.*       SPECIFIED CONSOLE.                                           * 01100000
.*                                                                    * 01150000
.*                                                                    * 01200000
.*   OPERATION =                                                      * 01250000
.*                                                                    * 01300000
.*       THIS MACRO CHECKS ITS INPUT PARAMETER FOR SYNTACTIC          * 01350000
.*       CORRECTNESS, ISSUING ERROR OR WARNING MESSAGES IF            * 01400000
.*       NECESSARY.  IF NO PARAMETER ERROR WAS DETECTED, THE          * 01450000
.*       ADDRESS OF THE PARAMETER IS STORED IN REGISTER ONE.          * 01500000
.*       THE UCM IS TESTED FOR A NON-ZERO CONVCON PROCESSOR           * 01541600
.*       ADDRESS AND EITHER A BRANCH IS TAKEN TO THE SERVICE          * 01583200
.*       PROCESSOR, OR RETURN CODE 16 IS SET IN REGISTER 15,          * 01624800
.*       INDICATING THE CONVCON SERVICE IS NOT INSTALLED.             * 01666400
.*       A SPACE SWITCH PC IS PERFORMED TO THE CONVCON PROCESSOR. @L1A* 01708000
.*                                                                    * 01750000
.*                                                                    * 01800000
.*01* INVOCATION =                                                    * 01850000
.*                                                                    * 01900000
.*       CONVCON IS INVOKED WITH A SINGLE POSITIONAL PARAMETER        * 01950000
.*       SPECIFYING THE ADDRESS OF THE CONV PARAMETER LIST (MAPPED    * 02000000
.*       BY IEZVG200) AND THE ADDRESS OF A STANDARD 18 WORD SAVE      * 02050000
.*       AREA IN REGISTER 13.                                         * 02100000
.*                                                                    * 02150000
.*         FOR BAL PROGRAMS, CONVCON IS INVOKED IN ONE OF THE         * 02200000
.*       FOLLOWING WAYS:                                              * 02250000
.*                                                                    * 02300000
.*               1) CONVCON (X), WHERE X IS A REGISTER,               * 02350000
.*                               OR A SYMBOL EQUATED TO               * 02400000
.*                               A REGISTER;                          * 02450000
.*                                                                    * 02500000
.*                                                                    * 02550000
.*               2) CONVCON LABEL, WHERE LABEL IS THE                 * 02600000
.*                                 SYMBOLIC ADDRESS OF                * 02650000
.*                                 THE CONV PARAMETER LIST.           * 02700000
.*                                                                    * 02750000
.*                                                                    * 02800000
.*       FOR PL/AS OR PLS PROGRAMS, CONVCON IS INVOKED AS             * 02850000
.*         FOLLOWS:                                                   * 02900000
.*                                                                    * 02950000
.*               => ?CONVCON(LABEL), WHERE LABEL IS THE               * 03000000
.*                                   SYMBOLIC ADDRESS OF              * 03050000
.*                                   THE CONV PARAMETER LIST.         * 03100000
.*                                                                    * 03150000
.* DEPENDENCIES =                                                     * 03200000
.*                                                                    * 03250000
.*     THE CALLER OF THE CONVCON MACRO MUST INCLUDE THE CONV          * 03300000
.*     PARAMETER LIST MAPPING MACRO (IEZVG200).                       * 03350000
.*                                                                    * 03400000
.*                                                                    * 03450000
.* RETURN CODES =                                                     * 03500000
.*                                                                    * 03550000
.*     THE CODE GENERATED BY THIS MACRO WILL CHECK THE UCM FOR        * 03600000
.*     A NON-ZERO ADDRESS FOR THE CONVCON SERVICE PROCESSOR,          * 03650000
.*     IEAVG200.  IF A VALID ADDRESS IS FOUND, A SPACE SWITCH PC  @L1C* 03700000
.*     WILL BE DONE TO THE SERVICE PROCESSOR.  THE FOLLOWING      @L1C* 03750000
.*     RETURN CODES ARE POSSIBLE AND RETURNED IN REGISTER 15:     @L1C* 03800000
.*                                                                    * 03850000
.*                                                                    * 03900000
.*            0 - NO ERRORS. IF CALLER REQUESTED VERIFICATION OF  @D1C* 03925000
.*                AN AREA ID, A REASON CODE WILL BE PRESENT IN    @D1A* 03950000
.*                CONVRSN.                                        @D1A* 03975000
.*            4 - CONSOLE IS NOT ACTIVE                               * 04000000
.*            8 - INVALID CONSOLE NAME SPECIFIED IN CONVFLD           * 04050000
.*           12 - CONSOLE ID IS OUT OF THE RANGE DEFINED BY MCS       * 04100000
.*           20 - DATA AREA ADDRESSABILITY PROBLEM.  SERVICE      @L1C* 04150000
.*                 FAILED.                                        @L1C* 04200000
.*           24 - NO PROCESSING FUNCTION WAS SELECTED                 * 04350000
.*                (NOTE: THE CONVFLGS FIELD IS USED TO SELECT         * 04400000
.*                 THE DESIRED PROCESSING FUNCTION)                   * 04450000
.*           28 - MORE THAN ONE PROCESSING FUNCTION WAS SELECTED      * 04500000
.*           32 - PARAMETER LIST DOES NOT CONTAIN CONV ACRONYM        * 04550000
.*                                                                    * 04600000
.*           THE FOLLOWING RETURN CODE IS ISSUED VIA THE CODE         * 04650000
.*           GENERATED BY THE CONVCON MACRO:                          * 04700000
.*                                                                    * 04750000
.*           16 - CONVCON SERVICE IS NOT AVAILABLE                    * 04800000
.*                                                                    * 04850000
.* REASON CODES =                                                 @D1A* 04875000
.*                                                                    * 04900000
.*     IF A REASON CODE IS SPECIFIED, IT WILL BE IN CONVRSN.      @D1A* 04902000
.*     A REASON CODE IS ONLY MEANINGFUL WITH RETURN CODE 0.       @D1A* 04904000
.*                                                                    * 04906000
.*     RETURN CODE 0, REASON CODE 0 = CONSOLE NAME AND SPECIFIED  @D1A* 04908000
.*     AREA ID ARE VALID.                                         @D1A* 04910000
.*                                                                @D1A* 04912000
.*     RETURN CODE 0, REASON CODE 4 = THE CONSOLE NAME OR ID      @D1A* 04914000
.*     SPECIFIED IS VALID, BUT THE AREA ID SPECIFIED IS IN USE    @D1A* 04916000
.*     FOR A MESSAGE OTHER THAN FROM A TRACK COMMAND (E.G. A      @D1A* 04918000
.*     DISPLAY COMMAND RESPONSE).                                 @D1A* 04920000
.*                                                                @D1A* 04922000
.*     RETURN CODE 0, REASON CODE 8 = THE CONSOLE NAME OR ID      @D1A* 04924000
.*     SPECIFIED IS VALID, BUT THE AREA SPECIFIED IS CURRENTLY    @D1A* 04926000
.*     IN USE FOR A TRACK COMMAND.                                @D1A* 04928000
.*                                                                @D1A* 04930000
.*     RETURN CODE 0, REASON CODE 12 = THE CONSOLE NAME OR ID     @D1A* 04932000
.*     SPECIFIED IS VALID, BUT THE AREA ID SPECIFIED IS NOT       @D1A* 04934000
.*     DEFINED TO THE CONSOLE.                                    @D1A* 04936000
.*                                                                    * 04938000
.*     RETURN CODE 0, REASON CODE 16 = THE CONSOLE NAME OR ID     @D1A* 04940000
.*     SPECIFIED IS VALID, BUT THE AREA ID DOES NOT COMPLY WITH   @D1A* 04942000
.*     SYNTAX REQUIREMENTS (E.G. GREATER THAN 1 CHARACTER, NOT A  @D1A* 04944000
.*     VALID CHARACTER, ETC.).                                    @D1A* 04946000
.*                                                                    * 04948000
.*01* CREATED-BY     = N/A                                            * 04950000
.*                                                                    * 05000000
.*01* POINTED-TO-BY  = N/A                                            * 05050000
.*                                                                    * 05100000
.*01* LOCATION       = N/A                                            * 05150000
.*                                                                    * 05200000
.*01* SERIALIZATION  = N/A                                            * 05250000
.*                                                                    * 05300000
.*01* CHANGE ACTIVITY =                                               * 05350000
.*      $L0=CNMG2,    HBB4410, 870319 PDV1: CONSOLE MERGE STAGE II    * 05375000
.*      $L1=TSOOP,    HBB4410, 870901 PDBU: ADD PC INVOCATION         * 05400000
.*      $D1=DCR186,   HBB4410, 890626 PDCM: AREA ID VERIFICATION      * 05425000
.*                                                                    * 05450000
.*01* MESSAGES=                                                       * 05500000
.*       THE CONVCON MACRO WILL ISSUE THE FOLLOWING                   * 05550000
.*       MESSAGES UPON DETECTION OF SYNTAX ERRORS:                    * 05600000
.*                                                                    * 05650000
.*                                                                    * 05700000
.*02*  WARNING = (SEVERITY 4)                                         * 05750000
.*                                                                    * 05800000
.*    "CONVCON(X,Y) HAS TOO MANY ARGUMENTS.  ARGUMENTS IGNORED        * 05850000
.*      AFTER X."                                                     * 05900000
.*                                                                    * 05950000
.*02*  ERROR = (SEVERITY 12)                                          * 06000000
.*                                                                    * 06050000
.*    "NO TEXT GENERATED: CONVCON PARAMETER MISSING."                 * 06100000
.*                                                                    * 06150000
.********************************************************************** 06200000
&LABEL   CONVCON &PARAM                                                 06250000
.********************************************************************** 06300000
.*   MAKE SURE THAT THE PARAMETER WAS NOT BLANK.  IF BLANK, ISSUE     * 06350000
.* AN MNOTE OF SEVERITY 12 AND EXIT THE MACRO.  IF THE PARAMETER WAS  * 06400000
.* NOT BLANK GO ON TO CHECK FOR A USER SPECIFIED LABEL.               * 06450000
.********************************************************************** 06500000
         AIF   ('&PARAM' NE '' AND '&PARAM(1)' NE '' AND               +06550000
                '&PARAM(1,1)' NE '').LABEL                              06600000
         MNOTE 12,'NO TEXT GENERATED: CONVCON PARAMETER MISSING.'       06650000
         AGO   .EXIT                                                    06700000
.********************************************************************** 06750000
.*   SEE IF THE USER SPECIFIED A LABEL ON THE MACRO INVOCATION.  IF   * 06800000
.* SO, INSERT THE LABEL WITH AN EQU INTO THE SOURCE STREAM;  OTHERWISE* 06850000
.* JUST CONTINUE.                                                     * 06900000
.********************************************************************** 06950000
.LABEL   ANOP                                                           07000000
         AIF   ('&LABEL' EQ '').NOLABEL                                 07050000
&LABEL   EQU   *                                                        07100000
.********************************************************************** 07150000
.*   SEE IF THE PARAMETER STARTED WITH A PARENTHESIS.  IF IT DID,     * 07200000
.* THEN GO HANDLE THE ENCLOSED ARGUMENT.  IF NOT, THE USER SPECIFIED  * 07250000
.* A LABEL, SO GENERATE INSTRUCTIONS TO PERFORM STANDARD PARAMETER    * 07300000
.* PASSING OF THE CONV PARAMETER LIST.                                * 07350000
.********************************************************************** 07400000
.NOLABEL ANOP                                                           07450000
         AIF   ('&PARAM'(1,1) EQ '(').REG                               07500000
         LA    1,&PARAM       GET ADDRESS OF CONV                       07550000
         ST    1,CONVPTR-CONV(,1) SAVE CONVCON PTR AS PARAMETER FOR THE+07600000
                                CONVCON SERVICE PROCESSOR               07650000
         LA    1,CONVPTR-CONV(,1)   PUT ADDRESS OF PARAMETER IN        +07700000
                                      REGISTER ONE                      07750000
         AGO   .GET@                                                    07800000
.********************************************************************** 07850000
.*   CONVCON WAS INVOKED WITH A REGISTER SPECIFIED AS AN ARGUMENT.    * 07900000
.* GENERATE INSTRUCTIONS TO PERFORM STANDARD PARAMETER PASSING OF     * 07950000
.* THE CONV PARAMETER LIST.                                           * 08000000
.********************************************************************** 08050000
.REG     ANOP                                                           08100000
         ST    &PARAM,CONVPTR-CONV(,&PARAM) SAVE CONV POINTER AS       +08150000
                                   PARAMETER FOR SERVICE PROCESSOR      08200000
         LA    1,CONVPTR-CONV(,&PARAM) PUT ADDRESS OF PARAMETER IN     +08250000
                                      REGISTER ONE                      08300000
.********************************************************************** 08400000
.*   SEE IF MORE THAN ONE ARGUMENT WAS SPECIFIED.  IF SO, ISSUE A     * 08450000
.* WARNING INDICATING THAT THE EXTRA ARGUMENT(S) HAVE BEEN IGNORED;   * 08500000
.* OTHERWISE JUST CONTINUE.                                           * 08550000
.********************************************************************** 08600000
         AIF   (N'&PARAM EQ 1 AND N'&PARAM(1) EQ 1).GET@                08650000
         MNOTE 4,'CONVCON&PARAM HAS TOO MANY ARGUMENTS.  ARGUMENTS IGN +08700000
               ORED AFTER &PARAM(1,1).'                                 08750000
.********************************************************************** 08800000
.*   GENERATE CODE TO LOAD THE ADDRESS OF THE CONVCON PROCESSOR       * 08841600
.* AND BRANCH TO IT IF THE ADDRESS IS NON-ZERO.  IF THE ADDRESS       * 08883200
.* WAS ZERO, THE GENERATED CODE WILL LOAD RETURN CODE 16 INTO         * 08924800
.* REGISTER 15.  IF THE ADDRESS WAS NOT ZERO, GENERATE CODE TO    @L1C* 08966400
.* PERFORM STACKING PC TO CONVCON PROCESSOR.                      @L1A* 09008000
.********************************************************************** 09050000
.GET@    ANOP                                                           09100000
         L     15,16          GET ADDRESS OF CVT FROM PSA               09150000
         L     15,100(,15)    GET ADDRESS OF UCM FROM CVT               09185700
         ICM   15,15,304(15)  GET ADDRESS OF CONVCON PROCESSOR FROM UCM 09221400
         BZ    *+28           DO NOT INVOKE CONVCON PROCESSOR IF ITS   +09257100
                                ADDRESS WAS ZERO                   @L1C 09292800
         L     15,16          GET ADDRESS OF CVT FROM PSA          @L1A 09328500
         L     15,772(,15)    GET ADDRESS OF SFT FROM CVT          @L1A 09364200
         LA    14,2           GET CONVCON INDEX INTO LINKAGE TABLE @L1A 09399900
         AL    14,100(,15)    POINT REG14 TO CONVCON ETD ENTRY     @L1A 09435600
         PC    0(14)          PC TO CONVCON SERVICE                @L1A 09471300
         B     *+8            BRANCH AROUND NEXT INSTRUCTION - RETURN  +09507000
                               CODE WILL BE SET BY CONVCON PROCESSOR    09542700
         LA    15,16          SET RETURN CODE 16 - CONVCON PROCESSOR   +09578400
                               NOT INSTALLED                            09614100
.EXIT    ANOP                                                           09650000
         MEND                                                           09700000
.********************************************************************** 09750000
.*                     PLAS/PLS SECTION                               * 09800000
.********************************************************************** 09850000
**/                                                                     09900000
*CONVCON:  MACRO;                                                       09950000
*            ANS('?'||'CONVCONP'||MACLIST||';'); /* INVOKE PLAS VERSION 10000000
*                                                     OF CONVCON     */ 10050000
*          %END;                                                        10100000
