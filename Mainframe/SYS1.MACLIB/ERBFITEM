 %ERBFITEM:                                                             00010000
     MACRO KEYS(FLOAT               /* Floating point precision      */ 00020000
               ,BOUNDARY    ,BDY    /* Boundary (default byte)       */ 00020000
               );                                                       00020000
 /****  START OF SPECIFICATIONS  *************************************/ 00400000
 /*                                                                  */ 00450000
 /*01* Proprietary Statement:                                        */ 00450000
 /* COPYRIGHT =                                                      */         
 /*             5655-084 (C) COPYRIGHT IBM CORP. 1994, 1995          */         
 /*             THIS MODULE IS "RESTRICTED MATERIALS OF IBM"         */         
 /*             LICENSED MATERIALS-PROPERTY OF IBM                   */         
 /*             SEE COPYRIGHT INSTRUCTIONS                           */         
 /*                                                                  */         
 /* STATUS =                                                         */         
 /*             RMF VERSION 5, RELEASE 2, LEVEL 0                    */         
 /*                                                                  */         
 /*01* DESCRIPTIVE_NAME: FLOATING POINT ITEM DECLARATION             */         
 /*02*   ACRONYM: N/A                                                */         
 /*                                                                  */         
 /*01* MACRO-NAME:  ERBFITEM                                         */         
 /*                                                                  */         
 /*01* DSECT NAME: N/A                                               */         
 /*                                                                  */         
 /*01* COMPONENT:  Resource Measurement Facility (27404)             */         
 /*                                                                  */         
 /*01* EYE-CATCHER:  None.                                           */         
 /*                                                                  */         
 /*01* STORAGE ATTRIBUTES:  N/A                                      */         
 /*                                                                  */         
 /*01* SIZE:  N/A                                                    */         
 /*                                                                  */         
 /*01* CREATED BY:                                                   */         
 /*      Issuing routine                                             */         
 /*                                                                  */         
 /*01* POINTED TO BY:  N/A                                           */         
 /*                                                                  */         
 /*01* SERIALIZATION:  None.                                         */         
 /*                                                                  */         
 /*01* FUNCTION:                                                     */         
 /*02*     Generates a standard PL/X declaration for                 */         
 /*        floating point variables. To be used in                   */ 00700000
 /*        mapping macros only                                       */ 00750000
 /*                                                                  */ 00850000
 /*        FLOAT(24)  is generated as CHAR(4)                        */ 00850000
 /*        FLOAT(56)  is generated as CHAR(8)                        */ 00850000
 /*        FLOAT(112) is generated as CHAR(16)                       */ 00850000
 /*      Notes:                                                      */ 00850000
 /*         Depending on the external macro variable ERBFLTDCL       */ 00900000
 /*         there is an alternative expansion. This expansion is     */ 00950000
 /*         NOT INTENDED FOR CUSTOMER USE.                           */ 01000000
 /*                                                                  */ 01350000
 /*         Begin of part NOT INTENDED FOR CUSTOMER USE              */ 01350000
 /*                                                                  */ 01350000
 /*         The expansion generates the floating point declarations  */ 01050000
 /*         suitable for the floating point macro package using      */ 01100000
 /*         the macro FLTITEM.                                       */ 01150000
 /*         For this expansion the following must be included in     */ 01150000
 /*         the module:                                              */ 01150000
 /*         percent DCL ERBFLTDCL CHAR EXTERNAL semicolon            */ 01150000
 /*         percent ERBFLTDCL = 'YES' semicolon                      */ 01250000
 /*                                                                  */ 01350000
 /*         End of part NOT INTENDED FOR CUSTOMER USE                */ 01350000
 /*                                                                  */ 01350000
 /*01* METHOD OF ACCESS:                                             */         
 /*02*   PL/X:                                                       */         
 /*        ?ERBFITEM(a) FLOAT(b) c semicolon                         */ 01450000
 /*        WHERE:                                                    */ 01600000
 /*             a - Item to be declared. This is the name if the     */ 01650000
 /*                 item optionally preceeded by the PL/X            */ 01700000
 /*                 structure level number.                          */ 01750000
 /*             b - Floating point precision (24, 56, or 112)        */ 01850000
 /*             c - Optional boundary (e.g. BDY(DWORD) )             */ 01850000
 /*                 Default is byte (as opposed to flt macro)        */ 01850000
 /*        EXAMPLE:                                                  */ 01600000
 /*          questionmark ERBFITEM(5 SMF71XYZ) FLOAT(56) semicolon,  */ 01450000
 /*                                                                  */ 02300000
 /*01* STATUS: HRM5520 - RMF Version 5 Release 2.0                   */         
 /*                                                                  */         
 /*01* DELETED BY: N/A                                               */         
 /*                                                                  */         
 /*01* FREQUENCY: N/A                                                */         
 /*                                                                  */         
 /*01* DEPENDENCIES: None                                            */         
 /*                                                                  */         
 /*01* DISTRIBUTION LIBRARY:                                         */         
 /*      N/A                                                         */ 02200000
 /*                                                                  */ 02250000
 /*01* CHANGE ACTIVITY:                                              */ 02300000
 /*      $D0=CAP,HRM5520,,JKE: Floating point mapping support        */ 02350000
 /*                                                                  */ 02550000
 /****  END OF SPECIFICATIONS  ***************************************/ 02500000
 /*                                                                  */ 02550000
     DECLARE ERBFLTDCL CHAR EXTERNAL;                                   00520000
     DECLARE ML     CHAR INT;                                           00540000
     DECLARE FORM   CHAR INT;                                           00540000
     DECLARE XBDY   CHAR INT;                                           00540000
     DECLARE I3GAA  CHAR;                                               00540000
     DECLARE I3GBB  CHAR;                                               00550000
     DECLARE I3GCOL1 FIXED;                                             00560000
     DECLARE I3GCOL3 FIXED;                                             00570000
     DECLARE I3GCOL2 FIXED;                                             00580000
  I3GAA = MACNAME||' '||MACLIST||' '||MACKEYS;                          00630000
  I3GAA = I3GAA||' MACDATE(94.187) ';                                   00640000
  I3GBB = COMMENT(I3GAA);                                               00650000
  I3GCOL1 = 5;   /* BEGIN OF ANSWER TEXT */                             00660000
  I3GCOL3 = 37;  /* BEGIN OF COMMENT TEXT */                            00670000
  I3GCOL2 = 10;  /* BEGIN OF ASSEMBLER OPCODE */                        00680000
                                                                                
  /* Precision                                                    */            
  SELECT (FLOAT(1));                                                            
     WHEN ('24','FLOAT') DO;                                                    
        FORM = 'CHAR(4)';                                                       
        FLT  = '24';                                                            
     END;                                                                       
     WHEN ('56','DFLOAT') DO;                                                   
        FORM = 'CHAR(8)';                                                       
        FLT  = '56';                                                            
     END;                                                                       
     WHEN ('112','QFLOAT') DO;                                                  
        FORM = 'CHAR(16)';                                                      
        FLT  = '112';                                                           
     END;                                                                       
     OTHERWISE ANSWER MSGLEVEL(8)                                               
               MESSAGE('PARM FLOAT('|| FLOAT(1) || ') INVALID. '||              
                       I2GBB);                                                  
  END;                                                                          
                                                                                
  /* Boundary                                                        */         
  XBDY = '';                                                                    
  IF (BDY ^= '') THEN DO;                                                       
     XBDY = XBDY||'BDY'||BDY;                                                   
  END;                                                                          
  IF (BOUNDARY ^= '') THEN DO;                                                  
     XBDY = XBDY||'BOUNDARY'||BOUNDARY;                                         
  END;                                                                          
  IF (XBDY = '') THEN DO;                /* Default Boundary is BYTE */         
     XBDY = 'BOUNDARY(BYTE)';                                                   
  END;                                                                          
                                                                                
  /* Alternate expansion for floating point macro usage              */ 00700000
  IF (ERBFLTDCL = 'YES') THEN DO;                                       00620000
     ANS('?FLTITEM'||MACLIST||' FLOAT('||FLT||') '||XBDY||'; '                  
         ||I3GBB) SKIP COL(I3GCOL1);                                            
  END;                                                                  01430000
  /* Default expansion (not using floating point macros)             */ 00700000
  ELSE DO;                                                                      
     /* Item itself                                                  */         
     IF LENGTH(MACLIST) > 1 THEN DO;                                            
        ML = MACLIST(2::LENGTH(MACLIST)-2);                                     
     END;                                                                       
     ELSE DO;                                                                   
        ML = MACLIST;                                                           
     END;                                                                       
     /* Result                                                       */         
     ANS(ML||' '||FORM||' '||XBDY) SKIP COL(I3GCOL1);                           
  END;                                                                  01430000
 %END ERBFITEM;                                                         01440000
