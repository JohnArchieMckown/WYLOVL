         MACRO                                                                  
&NAME    PUTGET &PARM=,&UPT=,&ECT=,&ECB=,&OUTPUT=,&TERMPUT=,&TERMGET=, *        
               &ENTRY=,&SUBSTACK=,&MF=                        @PBY0239          
.**********************************************************************         
.*                                                                    *         
.*COPYRIGHT:                                                          *         
.*        5685-025 COPYRIGHT IBM CORP 1986, 1990                      *         
.*        THIS PRODUCT CONTAINS RESTRICTED MATERIALS OF IBM,          *         
.*        REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083       *         
.*                                                                    *         
.*STATUS:                                                             *         
.*                                                                    *         
.*        HTE22D2 TSO/E                                               *         
.*                                                                    *         
.*FUNCTION:                                                           *         
.*   THE PUTGET MACRO EXAMINES PARAMETERS SPECIFIED IN THE PUTGET     *         
.*   MACRO INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE *         
.*   PUTGET SERVICE ROUTINE (REFER TO SECTION 3.48.10A.1 FOR THE      *         
.*   PARAMETER BLOCK FORMAT).                                         *         
.*   IN THE LIST FORM, THE PUTGET MACRO GENERATES THE PUTGET          *         
.*   PARAMETER BLOCK CONSISTING OF 2 WORDS OF CONTROL (CONTROL,       *         
.*   TOUTPUT, CONTROL, TINPUT, AND SUBSTACK OPTIONS), THE ADDRESS     *         
.*   OF THE OUTPUT LINE, AND THE ADDRESS OF THE INPUT BUFFER.         *         
.*                                                            @PBY0239*         
.*   IN THE EXECUTE FORM OF THE PUTGET MACRO, CODE IS GENERATED       *         
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *         
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *         
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *         
.*   BE GENERATED TO INVOKE THE PUTGET SERVICE ROUTINE UNLESS AN      *         
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    *         
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              *         
.*                                                                    *         
.*ENTRY POINTS:                                                       *         
.*   PUTGET MACRO CALL - ONLY ENTRY POINT                             *         
.*                                                                    *         
.*INPUT:                                                              *         
.*   PARAMETERS SPECIFIED IN THE PUTLINE MACRO FORMAT(FOR FORMAT      *         
.*   SPECIFICATIONS REFER TO SECTION 3.48.10A.1).                     *         
.*                                                                    *         
.*OUTPUT:                                                             *         
.*   IN THE L-FORM, A 4-WORD PARAMETER BLOCK (REFER TO SECTION        *         
.*   3.48.10A.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO, THE *         
.*   ADDRESS OF THE OUTPUT LINE, AND THE ADDRESS OF THE INPUT BUFFER  *         
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *         
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *         
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *         
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *         
.*                                                                    *         
.*EXTERNAL REFERENCES:                                                *         
.*   LINK MACRO.                                                      *         
.*                                                                    *         
.*EXITS,NORMAL:                                                       *         
.*   IN THE L-FORM, RETURN TO CALLER.                                 *         
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE PUTGET SERVICE    *         
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  *         
.*   SECTION 3.48.1.5).                                               *         
.*                                                                    *         
.*EXITS,ERROR:                                                        *         
.*   INVALID INPUT.                                                   *         
.*                                                                    *         
.*TABLES/WORK AREAS:                                                  *         
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *         
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *         
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *         
.*   TO THE SPECIFIED MACRO PARAMETERS, THE ADDRESS OF THE OUTPUT     *         
.*   LINE, AND THE ADDRESS OF THE INPUT BUFFER.                       *         
.*                                                                    *         
.*ATTRIBUTES:                                                         *         
.*   SERIALLY REUSABLE.                                               *         
.*                                                                    *         
.*NOTES:                                                              *         
.*   NONE                                                             *         
.*                                                                    *         
.*CHANGE ACTIVITY:                                                    *         
.*  @PBY0239   TSO/E RELEASE 3 SUPPORT FOR RUNNING CLIST THROUGH THE  *         
.*             TSO SERVICE FACILITY. THESE CHANGES SUPPORT THE NEW    *         
.*             SUBSTACK OPTION.                                       *         
.*                                                                    *         
.*             SEE THE PUTGET SERVICE ROUTINE FOR FURTHER DESCRIPTION *         
.*             OF THIS NEW OPTION.                            @PBY0239*         
.*                                                                    *         
.*  @PBY0269   THE OUTPUT BUFFER ADDRESS, WHEN SPECIFIED, WAS NOT     *         
.*             BEING STORED INTO THE PUTGET PARAMETER LIST. THE       *         
.*             CODE WAS CHANGED TO BRANCH TO THE PROPER LABEL.        *         
.*                                                            @PBY0269*         
.*                                                                    *         
.*  @PBY0272   THE TERM PROCESSING BIT WAS NOT BEING SET IN THE       *         
.*             EXECUTE FORM WHEN TERM OR ATTN WAS SPECIFIED IN THE    *         
.*             OUTPUT KEYWORD.                                @PBY0272*         
.*                                                                    *         
.*  @PBY0280   THE ATTN BIT WAS NOT GETTING SET DURING EXPANSION      *         
.*             WHEN THE ATTN OPTION WAS SPECIFIED. THE BIT VARIABLE   *         
.*             WAS CHANGED TO THE PROPER ONE (&B6 TO &B9).    @PBY0280*         
.*                                                                    *         
.*  OY10667 - CHANGE L4I TO L4H IN ORDER TO SET THE CORRECT           *         
.*            BITS IN THE PGPB WHEN TERM IS SPECIFIED WITHOUT         *         
.*            THE TERMGET OPERAND IN THE LIST FORMAT.         @YA10667*         
.*                                                                    *         
.*  E22D2DA - TSO/E V2.2.0 - NLS ENABLEMENT                   @E22D2DA*         
.*                                                                    *         
.*  PG10219 - ENABLE TRANS OPTION WITH MODE/TERM/ATTN OPTIONS @PG10219*         
.**********************************************************************         
         LCLA  &A                                                               
.* PUTGET OPTIONS VARIABLES                                   @E22D2DA          
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B7,&B8,&B9,&B10,&B11  @E22D2DA          
.* VARIABLES TO DETECT THAT OPTIONS ARE NOT SPECIFIED TWICE,  @E22D2DA          
.* I.E., TRANS AND NOTRANS ARE SPECIFIED                      @E22D2DA          
         LCLB  &BS1,&BS2,&BS3,&BS4                                              
         LCLC  &C                                                               
.E1      AIF   (T'&MF EQ 'O').ERR1     NO MF PARAMETER                          
         AIF   (N'&SYSLIST EQ 0).MF1   POSITIONAL PARAMETERS INVALID            
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR3                   
.MF1     AIF   ('&MF' EQ 'L').L1       BRANCH TO LIST FORM                      
.**********************************************************************         
.*                                                                    *         
.*                               EXECUTE FORM                         *         
.*                                                                    *         
.**********************************************************************         
         AIF   ('&MF(1)' NE 'E').ERR1                                           
         AIF   (T'&NAME EQ 'O').E2                                              
&NAME    DS    0H                                                               
.E2      AIF   (T'&MF(2) EQ 'O').ERR1                                           
         AIF   ('&MF(2)' EQ '(1)').E3   PARM LIST ADDR IN REG 1                 
         AIF   ('&MF(2)'(1,1) EQ '(').WARN3                                     
.E2A     LA    1,&MF(2)     LOAD PARM LIST ADDR                                 
.E3      AIF   (T'&UPT EQ 'O').E4       UPT OMITTED                             
         AIF   ('&UPT'(1,1) EQ '(').E3A                                         
         LA    14,&UPT        LOAD UPT ADDR                                     
         ST    14,0(0,1)     STORE UPT ADDR IN PARM LIST                        
         AGO   .E4                                                              
.E3A     ANOP                                                                   
&A       SETA  K'&UPT-2                                                         
&C       SETC  '&UPT'(2,&A)                                                     
         ST    &C,0(0,1)     STORE UPT ADDR IN PARM LIST                        
.E4      AIF   (T'&ECT EQ 'O').E5     ECT OMITTED                               
         AIF   ('&ECT'(1,1) EQ '(').E4A LOAD ECT ADDR                           
         LA    14,&ECT                 STORE ECT ADDR IN PARM LIST              
         ST    14,4(0,1)                                                        
         AGO   .E5                                                              
.E4A     ANOP                                                                   
&A       SETA  K'&ECT-2                                                         
&C       SETC  '&ECT'(2,&A)                                                     
         ST    &C,4(0,1)               STORE ECT ADDR IN PARM LIST              
.E5      AIF   (T'&ECB EQ 'O').E6       ECB OMITTED                             
         AIF   ('&ECB'(1,1) EQ '(').E5A                                         
         LA    14,&ECB        LOAD ECB ADDR                                     
         ST    14,8(0,1)     STORE ECB ADDR IN PARM LIST                        
         AGO   .E6                                                              
.E5A     ANOP                                                                   
&A       SETA  K'&ECB-2                                                         
&C       SETC  '&ECB'(2,&A)                                                     
         ST    &C,8(0,1)     STORE ECB ADDR IN PARM LIST                        
.E6      AIF   (T'&PARM EQ 'O').E6B     PARM OMITTED                            
         AIF   ('&PARM'(1,1) EQ '(').E6A                                        
         LA    14,&PARM       LOAD PARM ADDR                                    
         ST    14,12(0,1)    STORE PARM ADDR IN PARM LIST                       
         AGO   .E7                                                              
.E6A     ANOP                                                                   
&A       SETA  K'&PARM-2                                                        
&C       SETC  '&PARM'(2,&A)                                                    
         ST    &C,12(0,1)    STORE PARM ADDR IN PARM LIST                       
         AIF   (N'&OUTPUT EQ 0 AND N'&TERMPUT EQ 0 AND N'&TERMGET      *        
               EQ 0 AND N'&SUBSTACK EQ 0).E11                 @PBY0239          
         LR    14,&C          LOAD PARM ADDR                                    
         AGO   .E7                                                              
.E6B     AIF   (N'&OUTPUT EQ 0   AND N'&TERMPUT EQ 0   AND N'&TERMGET  *        
               EQ 0 AND N'&SUBSTACK EQ 0).E11                 @PBY0239          
         L     14,12(0,1)     LOAD PARM BLOCK ADDR                              
.E7      AIF   (N'&OUTPUT EQ 0 AND N'&TERMPUT EQ 0 AND                 *        
               N'&SUBSTACK EQ 0).E10                          @PBY0239          
&A       SETA  N'&OUTPUT                                                        
         AIF   (&A EQ 0).E7K1      OUTPUT OMITTED             @PBY0239          
         AIF   ('&OUTPUT' EQ '()').E7I       ALL OPERANDS OMITTED               
         AIF   (&A GT 4).ERR6           TOO MANY OPERANDS     @E22D2DA          
         AIF   (&A EQ 1).E7I            OUTPUT LINE ADDR ONLY                   
.E7A     AIF   (T'&OUTPUT(&A) EQ 'O').E7H    OMITTED OPERAND                    
         AIF   ('&OUTPUT(&A)' NE 'PTBYPS').E7B                                  
&B3      SETB  1                                                                
&B5      SETB  1                                                                
         AGO   .E7D                                                             
.E7B     AIF   ('&OUTPUT(&A)' NE 'TERM').E7B0                                   
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                       TERM MUST BE SINGLE   @E22D2DA         
         AGO   .E7B01                                                           
.E7B0    AIF   ('&OUTPUT(&A)' NE 'ATTN').E7B1                                   
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                       ATTN MUST BE SINGLE   @E22D2DA         
&B9      SETB  1                                                                
.E7B01   ANOP                                                                   
&B10     SETB  1                                                                
         AGO   .E7B2                                                            
.E7B1    AIF   ('&OUTPUT(&A)' NE 'MODE').E7C                                    
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                      MODE MUST BE SINGLE    @E22D2DA         
.E7B2    ANOP                                                                   
&B4      SETB  1                                                                
         AGO   .E7D                                                             
.E7C     AIF   ('&OUTPUT(&A)' NE 'PROMPT').E7E                                  
&B3      SETB  1                                                                
.E7D     AIF   (&BS1).ERR6              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .E7H                                                             
.E7E     AIF   ('&OUTPUT(&A)' NE 'MULTLVL').E7F                                 
         AIF   ('&OUTPUT(2)' EQ 'MODE' OR '&OUTPUT(3)' EQ 'MODE' OR    *        
               '&OUTPUT(4)' EQ 'MODE').ERR5                    @E22D2DA         
         AIF   ('&OUTPUT(2)' EQ 'ATTN' OR '&OUTPUT(3)' EQ 'ATTN' OR    *        
               '&OUTPUT(4)' EQ 'ATTN').ERR5                    @E22D2DA         
         AIF   ('&OUTPUT(2)' EQ 'TERM' OR '&OUTPUT(3)' EQ 'TERM' OR    *        
               '&OUTPUT(4)' EQ 'TERM').ERR5                    @E22D2DA         
.*                                      MULTLVL MUST BE PROMPT @E22D2DA         
&B2      SETB  1                                                                
         AGO   .E7G                                                             
.E7F     AIF   ('&OUTPUT(&A)' NE 'SINGLE').E7G1                @E22D2DA         
&B1      SETB  1                                                                
.E7G     AIF   (&BS2).ERR6              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
         AGO   .E7H                                            @E22D2DA         
.E7G1    AIF   ('&OUTPUT(&A)' NE 'TRANS').E7G2                 @E22D2DA         
&B11     SETB  1                                               @E22D2DA         
         AGO   .E7G3                                           @E22D2DA         
.E7G2    AIF   ('&OUTPUT(&A)' NE 'NOTRANS').ERR6               @E22D2DA         
&B11     SETB  0                                               @E22D2DA         
.E7G3    AIF   (&BS3).ERR6         MUTUALLY EXCLUSIVE OPERANDS @E22D2DA         
&BS3     SETB  1                                               @E22D2DA         
.E7H     ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 1).E7A                                                    
.E7I     AIF   (&BS1).E7J               SET DEFAULT VALUES                      
&B3      SETB  1                                                                
.E7J     AIF   (&BS2).E7J1              SET DEFAULT VALUES    @E22D2DA          
&B1      SETB  1                                                                
.E7J1    AIF   (&BS3).E7K               SET DEFAULT VALUES    @E22D2DA          
&B11     SETB  0                                              @E22D2DA          
.E7K     MVI   0(14),B'000&B1.0&B2.0&B3'          SET CONTROL FIELD             
.*                                                            @PBY0239          
.*                                                            @PBY0239          
.* SINCE THE BIT USED TO SET THE SUBSTACK OPTION IS CONTAINED @PBY0239          
.* WITHIN THE OUTPUT CONTROL FIELDS, IT WAS NECESSARY TO      @PBY0239          
.* PLACE THE SUBSTACK OPERAND CHECK WITHIN THE OUTPUT CHECK   @PBY0239          
.* FOR EFFICIENT CODE GENERATION.                             @PBY0239          
.**********************************************************************         
.*                      SUBSTACK PARAMETER                    @PBY0239*         
.**********************************************************************         
.E7K1    ANOP                                                 @PBY0239          
         AIF   (T'&SUBSTACK EQ 'O').E7K2      IF OMITTED, DON'T CHANGE          
.*                                                            @PBY0239          
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT1   USE DEFAULT     @PBY0239          
         AIF   ('&SUBSTACK' NE 'YES').ERR9    INVALID OPERAND @PBY0239          
&B7      SETB  1                         INDICATE SUBSTACK OPTION               
.*                                                            @PBY0239          
.DEFLT1  ANOP                                                 @PBY0239          
         AIF   (N'&OUTPUT NE 0).E7K3     IS OUTPUT PARM BEING RESET?            
.*                                                            @PBY0239          
         NI    1(14),B'11111011'         RESET SUBSTACK OPTION BIT              
.*                                                            @PBY0239          
         OI    1(14),B'00000&B7.00'      SET SUBSTACK OPTION BIT                
.*                                                            @PBY0239          
         AGO   .E9                                            @PBY0239          
.E7K2    ANOP                                                 @PBY0239          
         AIF   (N'&OUTPUT EQ 0).E9       IS OUTPUT PARM BEING RESET?            
.*                                                            @PBY0239          
         NI    1(14),B'00000100'         CLEAR OUTPUT CONTROL FIELD             
*                                        WITHOUT CLEARING SUBSTACK BIT          
.*                                                            @PBY0239          
         OI    1(14),B'&B4.00&B5.&B9.0&B11.0'                                   
.*                                   SET OUTPUT CONTROL FIELD @E22D2DA          
         AGO   .E8                                            @PBY0269          
.E7K3    ANOP                                                 @PBY0239          
         MVI   1(14),B'&B4.00&B5.&B9.&B7.&B11.0'                                
*                    SET OUTPUT CONTROL FIELD AND SUBSTACK OPTION BIT           
.*                                                            @E22D2DA          
.E8      MVI   8(14),B'000&B10.0000'              SET INPUT CONTROL             
.*                                                            @PBY0272          
         AIF   (T'&OUTPUT(1) EQ 'O' OR '&OUTPUT' EQ '()').WARN1                 
         AIF   ('&OUTPUT(1)'(1,1) EQ '(').E8A                                   
         LA    0,&OUTPUT(1)        LOAD ADDR OF OUTPUT                          
         ST    0,4(0,14)          STORE ADDR OF OUTPUT                          
         AGO   .E8B                                                             
.E8A     ANOP                                                                   
&A       SETA  K'&OUTPUT(1)-2                                                   
&C       SETC  '&OUTPUT(1)'(2,&A)                                               
         ST    &C,4(0,14)         STORE ADDR OF OUTPUT                          
.E8B     ANOP                                                                   
&B3      SETB  0                                                                
&B4      SETB  0                                                                
&B5      SETB  0                                                                
&BS1     SETB  0                                                                
&BS2     SETB  0                                                                
&BS3     SETB  0                                               @E22D2DA         
.E9      ANOP                                                                   
&A       SETA  N'&TERMPUT                                                       
         AIF   (&A EQ 0).E10            NO TERMPUT OPERANDS                     
         AIF   ('&TERMPUT' EQ '()').E9O      ALL OPERANDS OMITTED               
         AIF   (&A GT 4).ERR7           TOO MANY OPERANDS                       
.E9A     AIF   (T'&TERMPUT(&A) EQ 'O').E9N   OMITTED OPERAND                    
         AIF   ('&TERMPUT(&A)' NE 'BREAKIN').E9B                                
&B5      SETB  1                                                                
         AGO   .E9C                                                             
.E9B     AIF   ('&TERMPUT(&A)' NE 'NOBREAK').E9D                                
.E9C     AIF   (&BS1).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .E9N                                                             
.E9D     AIF   ('&TERMPUT(&A)' NE 'HOLD').E9E                                   
&B4      SETB  1                                                                
         AGO   .E9F                                                             
.E9E     AIF   ('&TERMPUT(&A)' NE 'NOHOLD').E9G                                 
.E9F     AIF   (&BS2).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
         AGO   .E9N                                                             
.E9G     AIF   ('&TERMPUT(&A)' NE 'NOWAIT').E9H                                 
&B3      SETB  1                                                                
         AGO   .E9I                                                             
.E9H     AIF   ('&TERMPUT(&A)' NE 'WAIT').E9J                                   
.E9I     AIF   (&BS3).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS3     SETB  1                                                                
         AGO   .E9N                                                             
.E9J     AIF   ('&TERMPUT(&A)' NE 'CONTROL').E9K                                
&B6      SETB  1                                                                
         AGO   .E9M                                                             
.E9K     AIF   ('&TERMPUT(&A)' NE 'ASIS').E9L                                   
&B7      SETB  1                                                                
         AGO   .E9M                                                             
.E9L     AIF   ('&TERMPUT(&A)' NE 'EDIT').ERR7                                  
.E9M     AIF   (&BS4).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS4     SETB  1                                                                
.E9N     ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 0).E9A                                                    
.E9O     ANOP                                                                   
         MVI   2(14),B'000&B3.&B4.&B5.&B6.&B7' SET TOUTPUT OPTION FIELD         
&B5      SETB  0                                                                
&B6      SETB  0                                                                
&BS1     SETB  0                                                                
&BS2     SETB  0                                                                
.E10     ANOP                                                                   
&A       SETA  N'&TERMGET                                                       
         AIF   (&A EQ 0).E11            NO TERMGET OPERANDS                     
         AIF   ('&TERMGET' EQ '()').E10H     ALL OPERANDS OMITTED               
         AIF   (&A GT 2).ERR8           TOO MANY OPERANDS                       
.E10A    AIF   (T'&TERMGET(&A) EQ 'O').E10G       OMITTED OPERAND               
         AIF   ('&TERMGET(&A)' NE 'EDIT').E10B                                  
         AGO   .E10C                                                            
.E10B    AIF   ('&TERMGET(&A)' NE 'ASIS').E10D                                  
&B6      SETB  1                                                                
.E10C    AIF   (&BS1).ERR8              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .E10G                                                            
.E10D    AIF   ('&TERMGET(&A)' NE 'WAIT').E10E                                  
         AGO   .E10F                                                            
.E10E    AIF   ('&TERMGET(&A)' NE 'NOWAIT').ERR8                                
&B5      SETB  1                                                                
.E10F    AIF   (&BS2).ERR8              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
.E10G    ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 0).E10A                                                   
.E10H    MVI   10(14),B'100&B5.000&B6'  SET TINPUT OPTIONS FIELD                
.E11     AIF   (T'&ENTRY EQ 'O').E12      ENTRY POINT OMITTED                   
         AIF   ('&ENTRY' EQ '(15)').E11A     EP IN REG 15                       
         AIF   ('&ENTRY'(1,1) EQ '(').WARN4                                     
.E11A1   LA    15,&ENTRY      LOAD EP ADDR                                      
.E11A    BALR  14,15          BRANCH TO SERVICE ROUTINE                         
         MEXIT                                                                  
.E12     L     15,16(0,0)          LOAD CVT POINTER                             
         TM    464(15),B'10000000'   IS ROUTINE LOADED ?                        
         BNO   IKJ@&SYSNDX           NO- BRANCH TO LINK                         
         L     15,464(0,15)          YES - BALR TO PUTGET                       
         BALR  14,15                                                            
         B     IKJ$&SYSNDX                                                      
IKJ@&SYSNDX LINK  EP=IKJPTGT        LINK TO PUTGET                              
IKJ$&SYSNDX DS    0H                                                            
         MEXIT                                                                  
.**********************************************************************         
.*                                                                    *         
.*                                 LIST FORM                          *         
.*                                                                    *         
.**********************************************************************         
.L1      DS    0F                                                               
         AIF   (N'&OUTPUT EQ 0   AND N'&TERMPUT EQ 0                   *        
               AND N'&SUBSTACK EQ 0).L2O                      @PBY0239          
&A       SETA  N'&OUTPUT                                                        
         AIF   (&A EQ 0).L1K            NO OUTPUT OPERANDS    @PBY0239          
         AIF   ('&OUTPUT' EQ '()').L1I       ALL OPERANDS OMITTED               
         AIF   (&A GT 4).ERR6           TOO MANY OPERANDS     @E22D2DA          
         AIF   (&A EQ 1).L1I            OUTPUT ADDR ONLY                        
.L1A     AIF   (T'&OUTPUT(&A) EQ 'O').L1H         OMITTED OPERAND               
         AIF   ('&OUTPUT(&A)' NE 'PTBYPS').L1B                                  
&B3      SETB  1                                                                
&B5      SETB  1                                                                
         AGO   .L1D                                                             
.L1B     AIF   ('&OUTPUT(&A)' NE 'TERM').L1B0                                   
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                         TERM MUST BE SINGLE @E22D2DA         
         AGO   .L1B01                                                           
.L1B0    AIF   ('&OUTPUT(&A)' NE 'ATTN').L1B1                                   
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                         ATTN MUST BE SINGLE @E22D2DA         
&B9      SETB  1                                                                
.L1B01   ANOP                                                                   
&B10     SETB  1                                                                
         AGO   .L1B2                                                            
.L1B1    AIF   ('&OUTPUT(&A)' NE 'MODE').L1C                                    
         AIF   ('&OUTPUT(2)' EQ 'MULTLVL' OR '&OUTPUT(3)' EQ           *        
               'MULTLVL' OR '&OUTPUT(4)' EQ 'MULTLVL').ERR2                     
.*                                         MODE MUST BE SINGLE @E22D2DA         
.L1B2    ANOP                                                                   
&B4      SETB  1                                                                
         AGO   .L1D                                                             
.L1C     AIF   ('&OUTPUT(&A)' NE 'PROMPT').L1E                                  
&B3      SETB  1                                                                
.L1D     AIF   (&BS1).ERR6              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .L1H                                                             
.L1E     AIF   ('&OUTPUT(&A)' NE 'MULTLVL').L1F                                 
         AIF   ('&OUTPUT(2)' EQ 'MODE' OR '&OUTPUT(3)' EQ 'MODE' OR    *        
               '&OUTPUT(4)' EQ 'MODE').ERR5                    @E22D2DA         
         AIF   ('&OUTPUT(2)' EQ 'ATTN' OR '&OUTPUT(3)' EQ 'ATTN' OR    *        
               '&OUTPUT(4)' EQ 'ATTN').ERR5                    @E22D2DA         
         AIF   ('&OUTPUT(2)' EQ 'TERM' OR '&OUTPUT(3)' EQ 'TERM' OR    *        
               '&OUTPUT(4)' EQ 'TERM').ERR5                    @E22D2DA         
.*                                      MULTLVL MUST BE PROMPT @E22D2DA         
&B2      SETB  1                                                                
         AGO   .L1G                                                             
.L1F     AIF   ('&OUTPUT(&A)' NE 'SINGLE').L1G1                @E22D2DA         
&B1      SETB  1                                                                
.L1G     AIF   (&BS2).ERR6              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
         AGO   .L1H                                            @E22D2DA         
.L1G1    AIF   ('&OUTPUT(&A)' NE 'TRANS').L1G2                 @E22D2DA         
&B11     SETB  1                                               @E22D2DA         
         AGO   .L1G3                                           @E22D2DA         
.L1G2    AIF   ('&OUTPUT(&A)' NE 'NOTRANS').ERR6               @E22D2DA         
&B11     SETB  0                                               @E22D2DA         
.L1G3    AIF   (&BS3).ERR6         MUTUALLY EXCLUSIVE OPERANDS @E22D2DA         
&BS3     SETB  1                                               @E22D2DA         
.L1H     ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 1).L1A                                                    
.L1I     AIF   (&BS1).L1J               SET DEFAULT VALUES                      
&B3      SETB  1                                                                
.L1J     AIF   (&BS2).L1J1              SET DEFAULT VALUES     @E22D2DA         
&B1      SETB  1                                                                
.L1J1    AIF   (&BS3).L1K               SET DEFAULT VALUES     @E22D2DA         
&B11     SETB  0                                               @E22D2DA         
.L1K     ANOP                                                                   
&NAME    DC    B'000&B1.0&B2.0&B3'           SET OUTPUT CONTROL FIELD           
.**********************************************************************         
.*                      SUBSTACK PARAMETER                    @PBY0239*         
.**********************************************************************         
.SUBSTCK ANOP                                                 @PBY0239          
         AIF   (T'&SUBSTACK EQ 'O').DEFLT2    IF OMITTED, USE DEFAULT           
.*                                                            @PBY0239          
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT2   USE DEFAULT     @PBY0239          
         AIF   ('&SUBSTACK' NE 'YES').ERR9    INVALID OPERAND @PBY0239          
&B7      SETB  1                         INDICATE SUBSTACK OPTION               
.*                                                            @PBY0239          
.DEFLT2 ANOP                                                  @PBY0239          
         DC    B'&B4.00&B5.&B9.&B7.&B11.0' SET OUTPUT CONTROL FIELD             
*                                          AND SUBSTACK OPTION                  
.*                                                            @E22D2DA          
&B4      SETB  0                                                                
&B5      SETB  0                                                                
&B7      SETB  0                                              @PBY0239          
&BS1     SETB  0                                                                
&BS2     SETB  0                                                                
&BS3     SETB  0                                              @E22D2DA          
.L2      ANOP                                                                   
&A       SETA  N'&TERMPUT                                                       
         AIF   (&A EQ 0).L2P            NO TERMPUT OPERANDS                     
         AIF   ('&TERMPUT' EQ '()').L2NN     ALL OPERANDS OMITTED               
         AIF   (&A GT 4).ERR7           TOO MANY OPERANDS                       
.L2A     AIF   (T'&TERMPUT(&A) EQ 'O').L2N        OMITTED OPERAND               
         AIF   ('&TERMPUT(&A)' NE 'BREAKIN').L2B                                
&B6      SETB  1                                                                
         AGO   .L2C                                                             
.L2B     AIF   ('&TERMPUT(&A)' NE 'NOBREAK').L2D                                
.L2C     AIF   (&BS1).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .L2N                                                             
.L2D     AIF   ('&TERMPUT(&A)' NE 'HOLD').L2E                                   
&B5      SETB  1                                                                
         AGO   .L2F                                                             
.L2E     AIF   ('&TERMPUT(&A)' NE 'NOHOLD').L2G                                 
.L2F     AIF   (&BS2).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
         AGO   .L2N                                                             
.L2G     AIF   ('&TERMPUT(&A)' NE 'NOWAIT').L2H                                 
&B4      SETB  1                                                                
         AGO   .L2I                                                             
.L2H     AIF   ('&TERMPUT(&A)' NE 'WAIT').L2J                                   
.L2I     AIF   (&BS3).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS3     SETB  1                                                                
         AGO   .L2N                                                             
.L2J     AIF   ('&TERMPUT(&A)' NE 'CONTROL').L2K                                
&B7      SETB  1                                                                
         AGO   .L2M                                                             
.L2K     AIF   ('&TERMPUT(&A)' NE 'ASIS').L2L                                   
&B8      SETB  1                                                                
         AGO   .L2M                                                             
.L2L     AIF   ('&TERMPUT(&A)' NE 'EDIT').ERR7                                  
.L2M     AIF   (&BS4).ERR7              MUTUALLY EXCLUSIVE OPERANDS             
&BS4     SETB  1                                                                
.L2N     ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 0).L2A                                                    
.L2NN    DC    B'000&B4.&B5.&B6.&B7.&B8'     SET TOUTPUT OPTIONS FIELD          
         DC    X'00'                         SET TOUTPUT OPTIONS FIELD          
         AGO   .L3                                                              
.L2O     ANOP                                                                   
&NAME    DC    X'00000000'         SET CONTROL AND TOUTPUT FIELDS               
         DC    A(0)      ADDR OF OUTPUT                                         
         AGO   .L4                                                              
.L2P     DC    X'0000'        SET TOUTPUT FIELD                                 
.L3      AIF   (N'&OUTPUT EQ 0).L3A     NO OUTPUT OPERANDS                      
         AIF   (T'&OUTPUT(1) EQ 'O' OR '&OUTPUT' EQ '()').WARN2                 
         DC    A(&OUTPUT(1))       ADDR OF OUTPUT                               
         AGO   .L3B                                                             
.L3A     DC    A(0)      ADDR OF OUTPUT                                         
.L3B     ANOP                                                                   
&B1      SETB  0                                                                
&B2      SETB  0                                                                
&BS1     SETB  0                                                                
&BS2     SETB  0                                                                
.L4      ANOP                                                                   
&A       SETA  N'&TERMGET                                                       
         AIF   (&A EQ 0).L4H             NO TERMGET OPERANDS   @YA10667         
         AIF   ('&TERMGET' EQ '()').L4H      ALL OPERANDS OMITTED               
         AIF   (&A GT 2).ERR8           TOO MANY OPERANDS                       
.L4A     AIF   (T'&TERMGET(&A) EQ 'O').L4G        OMITTED OPERAND               
         AIF   ('&TERMGET(&A)' NE 'EDIT').L4B                                   
         AGO   .L4C                                                             
.L4B     AIF   ('&TERMGET(&A)' NE 'ASIS').L4D                                   
&B2      SETB  1                                                                
.L4C     AIF   (&BS1).ERR8              MUTUALLY EXCLUSIVE OPERANDS             
&BS1     SETB  1                                                                
         AGO   .L4G                                                             
.L4D     AIF   ('&TERMGET(&A)' NE 'WAIT').L4E                                   
         AGO   .L4F                                                             
.L4E     AIF   ('&TERMGET(&A)' NE 'NOWAIT').ERR8                                
&B1      SETB  1                                                                
.L4F     AIF   (&BS2).ERR8              MUTUALLY EXCLUSIVE OPERANDS             
&BS2     SETB  1                                                                
.L4G     ANOP                                                                   
&A       SETA  &A-1                                                             
         AIF   (&A NE 0).L4A                                                    
.L4H     DC    B'000&B10.0000'     SET INPUT CONTROL FIELD                      
         DC    X'00'               SET INPUT CONTROL FIELD                      
         DC    B'100&B1.000&B2'    SET TINPUT OPTIONS FIELD                     
         DC    X'00'               SET TINPUT OPTIONS FIELD                     
         DC    A(0)      INPUT BUFFER ADDR                                      
         MEXIT                                                                  
.WARN1   MNOTE 0,'OUTPUT ADDR IS ASSUMED TO BE IN PARM BLOCK'                   
         AGO   .E8B                                                             
.WARN2   MNOTE 0,'FIRST OUTPUT OPERAND HAS BEEN OMITTED'                        
         AGO   .L3A                                                             
.WARN3   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'          
         AGO   .E2A                                                             
.WARN4   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'         
         AGO   .E11A1                                                           
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'                              
         MEXIT                                                                  
.ERR2    MNOTE 8,'MODE MESSAGES MUST BE SINGLE LINE OUTPUT'                     
         MEXIT                                                                  
.ERR3    MNOTE 8,'POSITIONAL PARAMETERS ARE INVALID'                            
         MEXIT                                                                  
.ERR5    MNOTE 8,'MULTLVL MESSAGES MUST BE PROMPT MESSAGES'                     
         MEXIT                                                                  
.ERR6    MNOTE 8,'INVALID OPERAND IN OUTPUT PARAMETER SUBLIST'                  
         MEXIT                                                                  
.ERR7    MNOTE 8,'INVALID OPERAND IN TERMPUT PARAMETER SUBLIST'                 
         MEXIT                                                                  
.ERR8    MNOTE 8,'INVALID OPERAND IN TERMGET PARAMETER SUBLIST'                 
         MEXIT                                                                  
.ERR9    MNOTE 8,'INVALID OPERAND IN THE SUBSTACK PARAMETER - USE YES O*        
               R NO ONLY'                                                       
         MEXIT                                                                  
.*                                                             @PBY0239         
         MEND                                                                   
