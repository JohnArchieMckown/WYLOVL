         MACRO -- IAZHTXT - ADD REASON CODE/TEXT TO TABLES              00100000
&SYMNAM  IAZHTXT &RC=,&TEXT=                                            00200000
.* **PROPRIETARY-STATEMENT************************************          00210000
.*                                                           *          00220000
.*   LICENSED MATERIALS-PROPERTY OF IBM                      *          00230000
.*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"             *          00240000
.*   5645-001 (C) COPYRIGHT IBM CORP 1990, 1997              *          00300000
.*                                                           *          00500000
.*   STATUS:  HBB6603                                        *          00600000
.*                                                           *          00700000
.*01* EXTERNAL CLASSIFICATION:  NONE                         *          00710000
.*01* END OF EXTERNAL CLASSIFICATION:                        *          00720000
.*                                                           *          00730000
.* **END-OF-PROPRIETARY-STATEMENT*****************************          01000000
.*************************************************************          01100000
.*                                                           *          01200000
.* FUNCTION:                                                 *          01300000
.*                                                           *          01400000
.*       THE IAZHTXT MACRO IS INVOKED BY THE IAZOHLD MACRO   *          01500000
.*       TO ADD A REASON CODE (&RC) AND ITS ASSOCIATED       *          01600000
.*       REASON TEXT (&TEXT) TO THE REASON CODE TABLES       *          01700000
.*       DESCRIBED IN THAT MACRO, AND TO GENERATE THE EQUATE *          01800000
.*       FOR THE REASON CODE'S SYMBOLIC NAME, WHICH IS       *          01900000
.*       PASSED AS &SYMNAM. BEFORE PROCESSING THE REASON     *          02000000
.*       CODE, HOWEVER, THIS MACRO CHECKS TO MAKE SURE THAT  *          02100000
.*       IT SHOULD BE GENERATED GIVEN THE SCOPE REQUESTED    *          02200000
.*       BY THE INVOKER OF THE IAZOHLD MACRO, OTHERWISE      *          02300000
.*       IT IS IGNORED.                                      *          02400000
.*                                                           *          02500000
.* PARAMETERS: (KEYWORD/POSITIONAL)                          *          02600000
.*                                                           *          02700000
.*       &RC        - REASON CODE TO BE ADDED (MUST BE       *          02800000
.*                    BETWEEN 0 AND 255)                     *          02900000
.*       &TEXT      - REASON TEXT TO BE ASSOCIATED WITH      *          03000000
.*                    REASON CODE ENCLOSED IN SINGLE QUOTES  *          03100000
.*       &SYMNAM    - SYMBOLIC NAME FOR THIS REASON CODE     *          03200000
.*                                                           *          03300000
.* PARAMETERS: (PASSED AS GLOBAL ASSEMBLER SYMBOLS)          *          03400000
.*                                                           *          03500000
.*       &OHLDSCP   - SCOPE PARAMETER FROM IAZOHLD           *          03600000
.*       &OHLDTEXT  - REASON TEXT ARRAY                      *          03700000
.*       &OHLDRCT   - REASON COUNT (SIZE OF TEXT TABLE)      *          03800000
.*       &OHLDINDX  - REASON INDEX ARRAY                     *          03900000
.*       &OHLDLEN   - MAXIMUM LENGTH FOR REASON TEXT         *          04000000
.*       &OHLDCMS   - COMMON RANGE START                     *          04100000
.*       &OHLDCME   - COMMON RANGE END                       *          04200000
.*       &OHLDJ2S   - JES2 RANGE START                       *          04300000
.*       &OHLDJ2E   - JES2 RANGE END                         *          04400000
.*       &OHLDJ3S   - JES3 RANGE START                       *          04500000
.*       &OHLDJ3E   - JES3 RANGE END                         *          04600000
.*       &OHLDUDS   - USER DEFINED RANGE START               *          04700000
.*       &OHLDUDE   - USER DEFINED RANGE END                 *          04800000
.*       &OHLDRDS   - RESERVED FOR DEVELOPMENT RANGE START   *          04900000
.*       &OHLDRDE   - RESERVED FOR DEVELOPMENT RANGE END     *          05000000
.*                                                           *          05100000
.* NOTES:                                                    *          05200000
.*                                                           *          05300000
.*       THE DESCRIPTION OF THE IAZOHLD MACRO SHOULD BE READ *          05400000
.*       FIRST.                                              *          05500000
.*                                                           *          05600000
.* ENVIRONMENT:                                              *          05700000
.*                                                           *          05800000
.*       THIS MACRO IS ONLY INVOKED WITHIN THE IAZOHLD MACRO *          05900000
.*       TO BUILD THE ARRAYS USED IN THAT MACRO.             *          06000000
.*                                                           *          06100000
.* REGISTER USAGE (ENTRY/EXIT):                              *          06200000
.*                                                           *          06300000
.*       N/A                                                 *          06400000
.*                                                           *          06500000
.* RETURN CODES (R15 ON EXIT):                               *          06600000
.*                                                           *          06700000
.*       N/A                                                 *          06800000
.*                                                           *          06900000
.* OTHER CONSIDERATIONS:                                     *          07000000
.*                                                           *          07100000
.*       NONE                                                *          07200000
.*                                                           *          07300000
.* CHANGE ACTIVITY:                                          *          07400000
.*                                                           *          07500000
.*                                                           *          07600000
.* A000000-999999    CREATED FOR MVS  4.1.0               @410          07620000
.*************************************************************          07700000
.*       DECLARE MACRO VARIABLES.                            *          07800000
.*       (SEE IAZOHLD MACRO FOR DESCRIPTIONS OF GLOBALS)     *          07900000
.*************************************************************          08000000
         GBLC  &OHLDTXT(256),&OHLDSCP                                   08100000
         GBLA  &OHLDRCT,&OHLDINDX(256)                                  08200000
         GBLA  &OHLDCMS,&OHLDCME,&OHLDJ2S,&OHLDJ2E                      08300000
         GBLA  &OHLDJ3S,&OHLDJ3E,&OHLDLEN                               08400000
         GBLA  &OHLDUDS,&OHLDUDE,&OHLDRDS,&OHLDRDE                      08500000
         LCLA  &ERRORS                                                  08600000
         LCLC  &TEMPC,&STEXT                                            08700000
.*************************************************************          08800000
.*       INITIALIZE PARAMETER ERROR COUNTER.                 *          08900000
.*************************************************************          09000000
&ERRORS  SETA  0                                                        09100000
.*************************************************************          09200000
.*       VALIDATE REASON CODE. IF INVALID, ISSUE MESSAGE,    *          09300000
.*       INCREMENT ERROR COUNT, AND SKIP CODE DEFINED CHECK  *          09400000
.*       (WHICH WOULD GIVE AN ARRAY BOUNDS ERROR).           *          09500000
.*************************************************************          09600000
         AIF   (&RC GE 0 AND &RC LE 255).HTXTRCV                        09700000
         MNOTE 4,'IAZHTXT: &RC IS AN INVALID REASON CODE.'              09800000
         MNOTE 4,'  REASON CODES MUST BE BETWEEN 0 AND 255.'            09900000
         MNOTE 4,'  REASON CODE &RC AND ITS TEXT IGNORED.'              10000000
&ERRORS  SETA  &ERRORS+1                                                10100000
         AGO   .HTXTRCU            SKIP DEFINED CHECK                   10200000
.HTXTRCV ANOP                      REASON CODE VALID                    10300000
.*************************************************************          10400000
.*       MAKE SURE REASON CODE HAS NOT BEEN DEFINED BEFORE.  *          10500000
.*       IF IT HAS, ISSUE MESSAGE AND INCREMENT ERROR COUNT. *          10600000
.*************************************************************          10700000
         AIF   (&OHLDINDX(&RC+1) EQ 0).HTXTRCU                          10800000
         MNOTE 4,'IAZHTXT: REASON CODE &RC HAS ALREADY BEEN'            10900000
         MNOTE 4,'  DEFINED AS &OHLDTXT(&OHLDINDX(&RC+1)+1).'           11000000
         MNOTE 4,'  NEW TEXT FOR REASON CODE &RC IGNORED.'              11100000
&ERRORS  SETA  &ERRORS+1                                                11200000
.HTXTRCU ANOP                      REASON CODE UNDEFINED                11300000
.*************************************************************          11400000
.*       MAKE SURE &TEXT IS IN SINGLE QUOTES. IF NOT, ISSUE  *          11500000
.*       MESSAGE AND INCREMENT ERROR COUNT.                  *          11600000
.*************************************************************          11700000
         AIF   ('&TEXT'(1,1) EQ '''' AND                               C11800000
                '&TEXT'(K'&TEXT,1) EQ '''').HTXTTOK                     11900000
         MNOTE 4,'IAZHTXT: TEXT FOR REASON CODE &RC IS NOT'             12000000
         MNOTE 4,'  ENCLOSED IN SINGLE QUOTES AS REQUIRED.'             12100000
         MNOTE 4,'  REASON CODE &RC AND ITS TEXT IGNORED.'              12200000
&ERRORS  SETA  &ERRORS+1                                                12300000
.HTXTTOK ANOP                      REASON TEXT OK                       12400000
.*************************************************************          12500000
.*       STRIP THE SINGLE QUOTES OFF THE REASON TEXT AND     *          12600000
.*       PUT IT IN &STEXT (THE SINGLE QUOTES ALLOW COMMAS TO *          12700000
.*       BE IN THE REASON TEXT).                             *          12800000
.*************************************************************          12900000
&STEXT   SETC  '&TEXT'(2,K'&TEXT-2)                                     13000000
.*************************************************************          13100000
.*       MAKE SURE THE LENGTH OF THE TEXT IS NOT GREATER     *          13200000
.*       THAN THE LIMIT (&OHLDLEN). IF THE TEXT IS TOO LONG, *          13300000
.*       ISSUE A MESSAGE AND TRUNCATE THE TEXT.              *          13400000
.*************************************************************          13500000
         AIF   (K'&STEXT LE &OHLDLEN).HTXTLOK                           13600000
         MNOTE 4,'IAZHTXT: TEXT FOR REASON CODE &RC TOO LONG.'          13700000
         MNOTE 4,'  MUST NOT EXCEED &OHLDLEN CHARACTERS.'               13800000
         MNOTE 4,'  TEXT TRUNCATED TO &OHLDLEN CHARACTERS.'             13900000
&STEXT   SETC  '&STEXT'(1,&OHLDLEN)                                     14000000
.HTXTLOK ANOP                      LENGTH OK                            14100000
.*************************************************************          14200000
.*       IF THERE WERE ANY ERRORS, IGNORE THIS REASON CODE   *          14300000
.*************************************************************          14400000
         AIF   (&ERRORS GT 0).HTXTEND                                   14500000
.*************************************************************          14600000
.*       ONLY PROCESS THIS REASON CODE IF:                   *          14700000
.*         1) SCOPE = "BOTH"                                 *          14800000
.*         2) SCOPE = "JES2" AND REASON CODE IS NOT IN       *          14900000
.*            THE JES3 RANGE                                 *          15000000
.*         3) SCOPE = "JES3" AND REASON CODE IS NOT IN       *          15100000
.*            THE JES2 RANGE                                 *          15200000
.*************************************************************          15300000
         AIF   ('&OHLDSCP' EQ 'BOTH').HTXTSOK                           15400000
         AIF   ('&OHLDSCP' EQ 'JES2' AND                               C15500000
                &RC GE &OHLDJ3S AND                                    C15600000
                &RC LE &OHLDJ3E).HTXTEND                                15700000
         AIF   ('&OHLDSCP' EQ 'JES3' AND                               C15800000
                 &RC GE &OHLDJ2S AND                                   C15900000
                 &RC LE &OHLDJ2E).HTXTEND                               16000000
.HTXTSOK ANOP                      REASON CODE OK WITHIN SCOPE          16100000
.*************************************************************          16200000
.*       ADD THIS REASON TO THE TABLE AS FOLLOWS:            *          16300000
.*         1) INCREMENT THE NUMBER OF REASONS IN THE TEXT    *          16400000
.*            ARRAY (&OHLDRCT).                              *          16500000
.*         2) STORE REASON TEXT AT END OF TEXT ARRAY         *          16600000
.*            (I.E. IN &OHLDTXT(&OHLDRCT)).                  *          16700000
.*         3) THE INDEX TO BE GENERATED FOR THIS REASON IS   *          16800000
.*            THE INDEX OF THE REASON TEXT IN THE TEXT ARRAY *          16900000
.*            (I.E. &OHLDRCT) MINUS ONE. STORE THIS IN THE   *          17000000
.*            INDEX ARRAY IN THE SLOT RESERVED FOR THIS      *          17100000
.*            REASON CODE (INDEX &RC+1).                     *          17200000
.*************************************************************          17300000
&OHLDRCT SETA  &OHLDRCT+1                                               17400000
&OHLDTXT(&OHLDRCT) SETC '&STEXT'                                        17500000
&OHLDINDX(&RC+1) SETA &OHLDRCT-1                                        17600000
.*************************************************************          17700000
.*       EQUATE THE SYMBOLIC NAME OF THE REASON CODE WITH ITS*          17800000
.*       NUMERIC VALUE AND COMMENT THIS WITH THE REASON TEXT.*          17900000
.*************************************************************          18000000
&TEMPC   SETC  '&RC'.'                 '.'&STEXT'(1,36)                 18100000
&SYMNAM  EQU   &TEMPC                                                   18200000
.HTXTEND ANOP                      END OF IAZHTXT                       18300000
         MEND                                                           99999999
