*     MACCOMP(SC109) MACDATE(80212) MACSTAT(HDP1102)                    00050000
         MACRO                                                          00100000
&NAME    CHKPT7 &DCB,&ID,&LNG,&MF=S                                @L1C 00150000
.**** START OF SPECIFICATIONS ***************************************** 00200000
.*                                                                    * 00250000
.*$MAC(CHKPT7): CHECKPOINT INVOCATION MACRO.                          * 00300000
.*                                                                    * 00350000
.*01* MACRO NAME = CHKPT7                                             * 00400000
.*                                                                    * 00450000
.*01* DESCRIPTIVE-NAME = CHECKPOINT INVOCATION MACRO COMPATABLE WITH  * 00500000
.*                     MVS/XA                                         * 00550000
.*                                                                    * 00600000
.********************************************************************** 00650000
.*                                                                    * 00700000
.*PROPRIETARY V3 STATEMENT                                              00703800
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00707600
.*"RESTRICTED MATERIALS OF IBM"                                         00711400
.*5695-DF1                                                              00715200
.*(C) COPYRIGHT 1982  IBM CORP.                                         00719000
.*END PROPRIETARY V3 STATEMENT                                          00725000
.*                                                                    * 00731000
.********************************************************************** 00737200
.*                                                                    * 00743400
.*01* STATUS = MVS / 3.8                                              * 00750000
.*                                                                    * 00800000
.*01* FUNCTION = THIS MACRO WILL SET UP THE CHECKPOINT PARAMETER      * 00850000
.*     LIST TO ALLOW THE CALLER TO INVOCATE CHECKPOINT TO CREATE A    * 00900000
.*     CHECKPOINT RECORD ON A CHECKPOINT DATA SET.                    * 00950000
.*                                                                    * 01000000
.*02* KEYWORDS =                                                      * 01050000
.*                                                                    * 01100000
.*     THE 'MF' KEYWORD DETERMINES WHETHER THE CHECKPOINT PARAMETER   * 01150000
.*     LIST IS EXPANDED AND/OR EXECUTABLE INSTRUCTIONS ARE GENERATED  * 01200000
.*     TO INVOKE CHECKPOINT.                                          * 01250000
.*                                                                    * 01300000
.*     THE SPECIFICATION OF MF=(E,ADDR), WILL RESULT IN THE           * 01350000
.*     MODIFICATION OF AN EXISTING CHECKPOINT PARAMETER LIST. THE     * 01400000
.*     ADDRESS OF THE PARAMETER LIST IS PROVIDED VIA A SECOND         * 01450000
.*     SUBPARAMETER OF THE 'MF' KEYWORD.  IT ALSO GENERATES CODE      * 01500000
.*     NECESSARY TO INVOKE CHECKPOINT. THIS IS CALLED THE             * 01550000
.*     EXECUTE-FORM OF THE MACRO.                                     * 01600000
.*                                                                    * 01650000
.*     WITH A SPECIFICATION OF MF=S, OR NOT SPECIFYING THE 'MF'       * 01700000
.*     KEYWORD AT ALL, THIS MACRO WILL EXPAND THE CHECKPOINT PARAMETER* 01750000
.*     LIST AND INITIALIZE IT ACCORDING TO THE OPTIONS SELECTED       * 01800000
.*     VIA THE ALLOWABLE KEYWORDS.  IT ALSO GENERATES CODE            * 01850000
.*     NECESSARY TO ISSUE SVC 63 TO INVOKE CHECKPOINT. THIS IS        * 01900000
.*     CALLED THE STANDARD-FORM (NORMAL-FORM) OF THE MACRO.           * 01950000
.*                                                                    * 02000000
.*     THE SPECIFICATION OF MF=L WILL RESULT IN THE EXPANSION OF THE  * 02050000
.*     PARAMETER LIST AND INITIALIZE IT ACCORDING TO THE SPECIFIED    * 02100000
.*     KEYWORDS. HOWEVER IT DOES NOT GENERATE THE CODE TO INVOKE      * 02150000
.*     CHECKPOINT. THIS IS CALLED THE LIST-FORM OF THE MACRO.         * 02200000
.*                                                                    * 02250000
.*     POSITIONAL OPERANDS ARE:                                       * 02300000
.*                                                                    * 02350000
.*        DCB - WILL PUT THE ADDRESS OF THE DCB INTO THE CHECKPOINT   * 02400000
.*              PARAMETER LIST. THIS OPERAND MAY SPECIFY A REGISTER   * 02450000
.*              WHICH POINTS TO THE LOCATION OF THE DCB, THE LABEL OF * 02500000
.*              THE LOCATION OF A DCB, OR "CANCEL". THIS FIELD IS     * 02550000
.*              REQUIRED FOR THE STANDARD FORM.                       * 02600000
.*                                                                    * 02650000
.*              VALID ENTRIES:  (RX)  ANY REGISTER EXCEPT REG 1 OR THE* 02700000
.*                                    REGISTER USED FOR MF= (IF THE   * 02750000
.*                                    REGISTER FORM IS USED).         * 02800000
.*                                    ASSEMBLER ONLY                  * 02850000
.*                              DCBADDR                               * 02900000
.*                              CANCEL                                * 02950000
.*                                                                    * 03000000
.*        ID - WILL PUT THE ADDRESS OF THE FIELD CONTAINING (OR TO    * 03050000
.*             RECEIVE) THE CHECKPOINT ID INTO THE CHECKPOINT         * 03100000
.*             PARAMETER LIST. THIS OPERAND MAY SPECIFY A REGISTER    * 03150000
.*             WHICH POINTS TO THE CHECKPOINT ID FIELD OR THE LABEL OF* 03200000
.*             THE CHECKPOINT ID FIELD. THIS OPERAND IS OPTIONAL.     * 03250000
.*                                                                    * 03300000
.*             VALID ENTRIES: (RX)   ANY REGISTER EXCEPT REG 1 OR THE * 03350000
.*                                   REGISTER USED FOR MF=, DCB, OR   * 03400000
.*                                   DDNADDR= (IF THE REGISTER FORM OF* 03450000
.*                                   ANY OF THESE OPERANDS IS USED).  * 03500000
.*                                   ASSEMBLER ONLY                   * 03550000
.*                            CKIDADDR                                * 03600000
.*                                                                    * 03650000
.*        LNG - WILL PUT THE LENGTH OF THE CHECKPOINT ID FIELD INTO   * 03700000
.*              THE CHECKPOINT PARAMETER LIST. THIS OPERAND MAY       * 03750000
.*              SPECIFY A REGISTER WHICH CONTAINS THE LENGTH OR A BASE* 03800000
.*              DISPLACEMENT TYPE VALUE. THIS OPERAND IS OPTIONAL.    * 03850000
.*                                                                    * 03900000
.*              FOR ASSEMBLY: IF LNG IS OMMITTED AND ID IS SPECIFIED, * 03950000
.*              THIS VALUE WILL DEFAULT TO 8.                         * 04000000
.*                                                                    * 04050000
.*              VALID ENTRIES: (RX)   ANY REGISTER EXCEPT REG 1 OR THE* 04100000
.*                                    REGISTER USED FOR MF=, DCB,     * 04150000
.*                                    ID (IF THE REGESTER FORM OF ANY * 04200000
.*                                    OF THESE OPERANDS IS USED).     * 04250000
.*                             L'CKIDADDR                             * 04300000
.*                             CKIDLNTH                               * 04350000
.*                                                                    * 04400000
.*       MF      - SEE THE OPENING PARAGRAPHS ABOVE.  IF THE MF       * 04450000
.*                 KEYWORD IS NOT SPECIFIED, THE DEFAULT, MF=S, WILL  * 04500000
.*                 BE IN EFFECT.                                      * 04550000
.*                                                                    * 04600000
.*               - FOR THE STANDARD-FORM (MF=S), THE                  * 04650000
.*                 FOLLOWING VALIDITY CHECK WILL BE MADE:             * 04700000
.*                      POSITIONAL OPERAND DCB IS REQUIRED.           * 04750000
.*                                                                    * 04800000
.*               - FOR ALL FORMS OF THE MACRO, THE FOLLOWING          * 04850000
.*                 VALIDITY CHECKS WILL BE MADE:                      * 04900000
.*                   1. VALID KEYWORD OPERANDS.                       * 04950000
.*                   2. FOR LNG, ID IS REQUIRED.                      * 05000000
.*                                                                    * 05050000
.*                 VALID ENTRIES:  MF=S             DEFAULT           * 05100000
.*                                 MF=L                               * 05150000
.*                                 MF=(E,(RX))                        * 05200000
.*                                 MF=(E,ADDR)                        * 05250000
.*                                                                    * 05300000
.*01* NOTES = N/A                                                     * 05350000
.*                                                                    * 05400000
.*01* MODULE-TYPE = MACRO                                             * 05450000
.*                                                                    * 05500000
.*02* PROCESSOR = ASSEMBLER.                                          * 05550000
.*                                                                    * 05600000
.*02* ATTRIBUTES = EXECUTABLE MACRO                                   * 05650000
.*                                                                    * 05700000
.*01* ENTRY-POINT = MACRO STATEMENT.                                  * 05750000
.*                                                                    * 05800000
.*02* INPUT = SEE DESCRIPTION OF OPERANDS AND KEYWORDS UNDER          * 05850000
.*           "FUNCTION".                                              * 05900000
.*                                                                    * 05950000
.*02* OUTPUT = CHECKPOINT PARAMETER LIST WITH THE FLAGS AND THE       * 06000000
.*           ADDRESSES SET DEPENDING UPON THE OPERANDS AND  KEYWORDS  * 06050000
.*           SPECIFIED AS DEFINED UNDER "OPERANDS" AND "KEYWORDS"     * 06100000
.*           ABOVE.                                                   * 06150000
.*                                                                    * 06200000
.*          FORMAT:                                                   * 06250000
.*                                                                    * 06300000
.*          BYTES  BIT  EXPLANATION                                   * 06350000
.*             0    0   THE ADDRESS OF THE CHECKPOINT ID FIELD WAS    * 06400000
.*                      NOT SUPPLIED.                                 * 06450000
.*                  1   RESERVED.                                     * 06500000
.*                  2   RESERVED.                                     * 06550000
.*                  3   RESERVED.                                     * 06600000
.*                  4   RESERVED.                                     * 06650000
.*                  5   RESERVED.                                     * 06700000
.*                  6   RESERVED.                                     * 06750000
.*                  7   RESERVED.                                     * 06800000
.*           1-3        ADDRESS OF DCB                                * 06850000
.*             4        LENGTH OF CHECKPOINT ID FIELD (IF 0,0 = 0).   * 06900000
.*           5-7        ADDRESS OF CHECKPOINT ID FIELD (IF 0,0 = 0).  * 06950000
.*                                                                    * 07000000
.*01* EXIT-NORMAL = MEND STATEMENT.                                   * 07050000
.*                                                                    * 07100000
.*01* EXIT-ERROR =                                                    * 07150000
.*               MNOTE AND MEND STATEMENT.  MNOTES WILL BE GENERATED  * 07200000
.*               FOR ANY OF THE FOLLOWING ERRORS ENCOUNTERED:         * 07250000
.*                                                                    * 07300000
.*                 FOR THE MF=S FORM OF THE MACRO:                    * 07350000
.*                   1. DCB NOT SUPPLIED.                             * 07400000
.*                                                                    * 07450000
.*                 FOR ALL FORMS OF THE MACRO:                        * 07500000
.*                   1. INVALID KEYWORD OPERAND.                      * 07550000
.*                   2. FOR LNG, ID IS REQUIRED.                      * 07600000
.*                                                                    * 07650000
.*01* EXTERNAL-REFERENCES = NONE                                      * 07700000
.*                                                                    * 07750000
.*01* TABLES = NONE                                                   * 07800000
.*                                                                    * 07850000
.*01* MACROS = NONE                                                   * 07900000
.*                                                                    * 07950000
.*01* CHANGE ACTIVITY = AS FOLLOWS:                                   * 08000000
.*                                                                    * 08050000
.*$L1=CKPT21,HDP1102,,STLJAK: SUPPORT TO PROVIDE DOWNWARD             * 08100000
.*              COMPATABILITY FROM MVS/XA                             * 08150000
.*$P1=KC11252,HDP3330,,SJLERM: INCORRECT CLASS IN COPYRIGHT MACRO @P1A* 08175000
.*                                                                    * 08200000
.**** END OF SPECIFICATIONS ******************************************* 08250000
         LCLA  &A                                                       08300000
&A       SETA  N'&DCB+N'&ID+N'&LNG                                      08350000
         AIF   (T'&DCB EQ 'O').E   CANCEL OPND CHECK- 1ST OPND, S FORM  08400000
         AIF   ('&DCB' EQ 'CANCEL' AND '&MF' NE 'S').ERR5               08450000
         AIF   ('&DCB' EQ 'CANCEL' AND &A GT 1).ERR5                    08500000
.E       AIF   ('&MF' EQ 'S').S1   STANDARD FORM                        08550000
         AIF   ('&MF' EQ 'L').L1        LIST FORM                       08600000
         AIF   ('&MF(1)' NE 'E' OR N'&MF NE 2).ERR1                     08650000
&NAME    IHBINNRA &MF(2)                EXECUTE FORM                    08700000
         AIF   (T'&DCB EQ 'O').E2                                       08750000
         AIF   (&A NE 1).E1A    E FORM, MORE THAN 1 OPND                08800000
         IC    15,0(0,1)                                                08850000
.E1A     AIF   ('&DCB'(1,1) EQ '(').E1                                  08900000
         LA    14,&DCB             LOAD DCB ADDRESS                     08950000
         ST    14,0(0,1)           STORE INTO PARAM. LIST               09000000
         AGO   .E1B                                                     09050000
.E1      ST    &DCB,0(0,1)         STORE DCB ADDR INTO PARAM LIST       09100000
.E1B     AIF   (&A NE 1).E2                                             09150000
         STC   15,0(0,1)                                                09200000
.E2      AIF   (T'&ID EQ 'O').E6                                        09250000
         AIF   (T'&DCB NE 'O').E2A                                      09300000
         MVI   0(1),0                                                   09350000
.E2A     AIF   (T'&LNG NE 'O').E3                                       09400000
         IC    15,4(0,1)           SAVE LENGTH                          09450000
.E3      AIF   ('&ID'(1,1) EQ '(').E4                                   09500000
         LA    14,&ID              LOAD CHECKID ADDRESS                 09550000
         ST    14,4(0,1)           STORE INTO PARAM. LIST               09600000
         AGO   .E5                                                      09650000
.E4      ST    &ID,4(0,1)          STORE CHECKID ADDR INTO PARAM LIST   09700000
.E5      AIF   (T'&LNG NE 'O').S13                                      09750000
         STC   15,4(0,1)           RESTORE LENGTH                       09800000
         AGO   .E7                                                      09850000
.E6      AIF   (T'&LNG NE 'O').S13                                      09900000
.E7      SVC   63                                                       09950000
         MEXIT                                                          10000000
.L1      AIF   (T'&DCB EQ 'O').L2                                       10050000
         AIF   ('&DCB'(1,1) EQ '(').ERR2     L FORM, REG OPND           10100000
.L2      AIF   (T'&ID  EQ 'O').L3                                       10150000
         AIF   ('&ID'(1,1) EQ '(').ERR2      L FORM, REG OPND           10200000
.L3      AIF   (T'&LNG EQ 'O').L4                                       10250000
         AIF   ('&LNG'(1,1) EQ '(').ERR2     L FORM, REG OPND           10300000
.L4      ANOP                                                           10350000
&NAME    DS    0F                                                       10400000
         AIF   (&A GT 1 OR T'&DCB EQ 'O').S5                            10450000
         DC    X'80'               INDICATE END OF LIST                 10500000
         DC    AL3(&DCB)           DCB ADDRESS                          10550000
         DC    A(0)                CHECKID ADDRESS                      10600000
         MEXIT                                                          10650000
.S1      AIF   (T'&DCB EQ 'O').ERR3   S FORM, DCB OPND CANT BE OMITTED  10700000
         AIF   (&A GT 1).S4                                             10750000
         AIF   ('&DCB' NE 'CANCEL').S2                                  10800000
&NAME    XR    1,1                                                      10850000
         AGO   .S3                                                      10900000
.S2      CNOP  0,4                                                      10950000
&NAME    BAL   1,*+12                                                   11000000
         AIF   ('&DCB'(1,1) EQ '(').S22                                 11050000
         DC    X'80'               INDICATE END OF LIST                 11100000
         DC    AL3(&DCB)           DCB ADDRESS                          11150000
         DC    A(0)                CHECKID ADDRESS                      11200000
.S3      SVC   63                                                       11250000
         MEXIT                                                          11300000
.S22     DC    A(0)                                                     11350000
         DC    A(0)                                                     11400000
         ST    &DCB,0(0,1)                                              11450000
         MVI   0(1),X'80'                                               11500000
         AGO   .S3                                                      11550000
.S4      AIF   (T'&ID  EQ 'O').ERR4     2ND OPND OMITTED                11600000
         CNOP  0,4                                                      11650000
&NAME    BAL   1,*+12                                                   11700000
.S5      AIF   (T'&DCB EQ 'O').S6       LIST FORM                       11750000
         AIF   ('&DCB'(1,1) EQ '(').S6  STANDARD FORM                   11800000
         DC    A(&DCB)             DCB ADDRESS                          11850000
         AGO   .S6A                                                     11900000
.S6      DC    A(0)                DCB ADDRESS                          11950000
.S6A     AIF   ('&MF' EQ 'L').S7A                                       12000000
.S7      AIF   ('&ID'(1,1) EQ '(').S12       S FORM, REG OPND           12050000
.S7A     AIF   (T'&LNG EQ 'O').S8                                       12100000
         AIF   ('&LNG' EQ '''S''').S10                                  12150000
         AIF   ('&LNG'(1,1) EQ '(').S9                                  12200000
         DC    AL1(&LNG)           CHECKID LENGTH                       12250000
         AGO   .S11                                                     12300000
.S8      DC    AL1(8)              CHECKID LENGTH                       12350000
         AGO   .S11                                                     12400000
.S9      DC    A(&ID)              CHECKID ADDRESS                      12450000
         AGO   .S15                GENERATE STC INSTR FOR REG FORM      12500000
.S10     DC    AL1(255)            CHECKID LENGTH                       12550000
.S11     AIF   (T'&ID EQ 'O').S11A      L FORM                          12600000
         DC    AL3(&ID)            CHECKID ADDRESS                      12650000
         AIF   ('&MF' EQ 'S').S18       S FORM                          12700000
         MEXIT                                                          12750000
.S11A    DC    AL3(0)              CHECKID ADDRESS                      12800000
         MEXIT                                                          12850000
.S12     DC    A(0)                CHECKID ADDRESS                      12900000
         ST    &ID,4(0,1)          STORE CHECKID ADDRESS                12950000
         AIF   (T'&LNG EQ 'O').S14                                      13000000
.S13     AIF   ('&LNG'(1,1) EQ '(').S15      REGISTER FORM              13050000
         AIF   ('&LNG' EQ '''S''').S16                                  13100000
         MVI   4(1),&LNG           CHECKID LENGTH                       13150000
         AGO   .S17                                                     13200000
.S14     MVI   4(1),8              CHECKID LENGTH                       13250000
         AGO   .S17                                                     13300000
.S15     STC   &LNG,4(0,1)         CHECKID LENGTH                       13350000
         AGO   .S17                                                     13400000
.S16     MVI   4(1),255            CHECKID LENGTH                       13450000
.S17     AIF   ('&MF(1)' EQ 'E').S19                                    13500000
.S18     AIF   ('&DCB'(1,1) NE '(').S19                                 13550000
         ST    &DCB,0(0,1)         DCB ADDRESS                          13600000
.S19     SVC   63                                                       13650000
         MEXIT                                                          13700000
.ERR1    IHBERMAC   35,,&MF   INVALID MF OPND SPECIFIED                 13750000
         MEXIT                                                          13800000
.ERR2    IHBERMAC   69        INVALID REGISTER NOTATION WITH MF=L       13850000
         MEXIT                                                          13900000
.ERR3    IHBERMAC   06        DCB OPRND NOT SPECIFIED                   13950000
         MEXIT                                                          14000000
.ERR4    IHBERMAC   03        SECOND OPRND NOT SPECIFIED (CHECKID)      14050000
         MEXIT                                                          14100000
.ERR5    IHBERMAC   36,,&DCB       INVALID FIRST OPERAND                14150000
         MEXIT                                                          14200000
         MEND                                                           14250000
