         MACRO                                                          00050000
&NAME    FESTAE7 &CNCL,&WRKREG=,&EXITADR=,&RBADDR=,&PARAM=,&TCBADDR=,  X00150000
               &PURGE=,&ASYNCH=,&TERM=,&RECORD=,&XCTL=,&ERRET= @G860P1C 00200000
*   MACDATE 09/08/95   S/370 LEVEL OF FESTAE                   @ZMC3023 00225000
.* A-000000-999999                                             @G860P1C 00250000
.*  MOVED PROMT COMMENT TO LINE JUST ABOVE                     @ZMC3023 00275000
.********************************************************************** 00300000
.*                                                                      00333300
.*  COPYRIGHT =                                                    @P2C 00366600
.*  5645-001                                                            00399900
.*  THIS MODULE IS "RESTRICTED MATERIALS OF IBM"                        00433200
.*  (C) COPYRIGHT IBM CORP. 1980, 1996                                  00466500
.*  LICENSED MATERIALS - PROPERTY OF IBM                                00499800
.*                                                                      00533100
.*  STATUS = HBB6601                                                    00566400
.*                                                             @G860P1C 00600000
.*01* EXTERNAL CLASSIFICATION:  GUPI                               @P1A 00610000
.*                                                                 @P1A 00620000
.*01* END OF EXTERNAL CLASSIFICATION:                              @P1A 00630000
.*                                                                 @P1A 00640000
.*  MACMEAN FAST ESTAE -SYSTEM 370 COMPATIBLE LEVEL            @G860P1C 00650000
.*  MACCOMP SCRTM                                                  @P1C 00700000
.*  MACSTAT OSVS2 HBB6601                                          @P1C 00750000
.*                                                             @G860P1C 00800000
.*  CHANGE ACTIVITY                                            @G860P1C 00850000
.*          G860P1C - MVS/SYSTEM PRODUCT HBB2102               @G860P1C 00900000
.* $P1= PR20011  HBB6601 950908 PDKD: SC1CM COMPID SPLIT                00925000
.* $P2= PR20013  HBB6601 950908 PDKD: FIX UP COPYRIGHT                  00937500
*********************************************************************** 00950000
         LCLA  &FLG1                                                    01000000
         LCLA  &FLG2                                                    01050000
         LCLA  &FLG3                                                    01100000
         LCLB  &ESTA                                                    01150000
         LCLB  &ASY                                                     01200000
         LCLB  &PNONE                                                   01250000
         LCLB  &HALT                                                    01300000
         LCLB  &XCTLB                                                   01350000
         LCLB  &KEY0                                                    01400000
         LCLB  &SUPER                                                   01450000
         LCLB  &TERMB                                                   01500000
         LCLB  &REC                                                     01550000
         LCLB  &BRNTR                                                   01600000
         LCLB  &ERRBIT                                                  01650000
         LCLC  &L1                                                      01700000
&L1      SETC  '&SYSNDX'                                                01750000
         AIF   ('&CNCL' NE '').CANCEL                                   01800000
&ESTA    SETB  1                                                        01850000
&ASY     SETB  ('&ASYNCH' EQ 'YES' OR '&ASYNCH' EQ '')                  01900000
&PNONE   SETB  ('&PURGE' EQ 'NONE' OR '&PURGE' EQ '')                   01950000
&HALT    SETB  ('&PURGE' EQ 'HALT')                                     02000000
&XCTLB   SETB  ('&XCTL' EQ 'YES')                                       02050000
&KEY0    SETB  1                                                        02100000
&SUPER   SETB  1                                                        02150000
&TERMB   SETB  ('&TERM' EQ 'YES')                                       02200000
&REC     SETB  ('&RECORD' EQ 'YES')                                     02250000
&BRNTR   SETB  1                                                        02300000
         AIF   ('&XCTL' NE 'YES' AND '&XCTL' NE 'NO' AND '&XCTL' NE '')X02350000
               .XCTLERR                                                 02400000
.PURCK   AIF   ('&PURGE' NE 'QUIESCE' AND '&PURGE' NE 'NONE' AND       X02450000
               '&PURGE' NE 'HALT' AND '&PURGE' NE '').PURERR            02500000
.ASYCK   AIF   ('&ASYNCH' NE 'YES' AND '&ASYNCH' NE 'NO' AND '&ASYNCH' X02550000
               NE '').ASYERR                                            02600000
.TERCK   AIF   ('&TERM' NE 'YES' AND '&TERM' NE 'NO' AND '&TERM' NE '')X02650000
               .TERERR                                                  02700000
.RECCK   AIF   ('&RECORD' NE 'YES' AND '&RECORD' NE 'NO' AND '&RECORD' X02750000
               NE '').RECERR                                            02800000
.CKERR   AIF   ('&ERRBIT' EQ '0').NOERR                                 02850000
         MEXIT                                                          02900000
.NOERR   ANOP                                                           02950000
&FLG1    SETA  16*&ESTA+4*&ASY+2*&PNONE+&HALT                           03000000
&FLG2    SETA  64*&XCTLB+2*&KEY0+&SUPER                                 03050000
&FLG3    SETA  64*&TERMB+32*&REC+4*&BRNTR                               03100000
         AIF   ('&EXITADR' EQ '').CANERR                                03150000
         AIF   ('&EXITADR'(1,1) NE '(').EXNREG                          03200000
         AIF   ('&RBADDR' EQ '').NRBA                                   03250000
         AIF   ('&RBADDR'(1,1) NE '(').RBNREG                           03300000
&NAME    L     15,RBSEXIT-RBSECT+RBPRFXLN(0,&RBADDR(1)) OBTAIN CURRENT  03350000
*                                            RB SCBEXIT VALUE           03400000
         LTR   15,15                         IF ZERO - SCB AVAILABLE    03450000
         BZ    IHB1&L1                       CONTINUE PROCESSING        03500000
         LA    15,8(0,0)                     INDICATE PREVIOUS EXIT     03550000
*                                            IN THIS RB                 03600000
         AIF   ('&ERRET' EQ '').NERRET                                  03650000
         B     &ERRET                                                   03700000
         AGO   .NERRET1                                                 03750000
.NERRET  ANOP                                                           03800000
         B     IHB2&L1                       CONTINUE USERS EXECUTION   03850000
.NERRET1 ANOP                                                           03900000
IHB1&L1  DS    0H                                                       03950000
         ST    &EXITADR(1),RBSEXIT-RBSECT+RBPRFXLN(0,&RBADDR(1)) STORE  04000000
*                                            EXIT ADDR IN SCB           04050000
         AIF   ('&PARAM' EQ '').GETPARM                                 04100000
         AIF   ('&PARAM'(1,1) NE '(').PARERR                            04150000
         ST    &PARAM(1),RBSPARM-RBSECT+RBPRFXLN(0,&RBADDR(1))  STORE   04200000
*                                            PARM ADDR IN SCB           04250000
         AGO   .CONT                                                    04300000
.GETPARM ANOP                                                           04350000
         LA    &EXITADR(1),RBFEPARM-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET   04400000
*                                            DEFAULT PARAMETER AREA     04450000
         ST    &EXITADR(1),RBSPARM-RBSECT+RBPRFXLN(0,&RBADDR(1)) STORE  04500000
*                                            IT IN SCB                  04550000
.CONT    ANOP                                                           04600000
         MVI   RBSFLGS1-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG1 INIT FLAGS    04650000
         MVI   RBSFLGS2-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG2 INIT FLAGS    04700000
         MVI   RBSFLG3-RBSECT+RBPRFXLN(&RBADDR(1)),&FLG3  INIT FLAGS    04750000
         AIF   ('&TCBADDR' EQ '').NTCB                                  04800000
         AIF   ('&TCBADDR'(1,1) NE '(').TCBERR                          04850000
         L     &EXITADR(1),TCBSTAB-TCB(0,&TCBADDR(1)) GET ADDR PREVIOUS 04900000
*                                            SCB                        04950000
         LA    &EXITADR(1),0(0,&EXITADR(1))   CLEAR HIGH BYTE           05000000
         ST    &EXITADR(1),RBSCHAIN-RBSECT+RBPRFXLN(0,&RBADDR(1)) CHAIN 05050000
*                                            TO CURRENT SCB             05100000
         LA    &EXITADR(1),RBSCBB-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET     05150000
*                                            ADDR CURRENT SCB           05200000
         STCM  &EXITADR(1),7,TCBSTABB-TCB(&TCBADDR(1)) MAKE IT CURRENT  05250000
         AGO   .CONT1                                                   05300000
.NTCB    ANOP                                                           05350000
         L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    05400000
         L     &EXITADR(1),TCBSTAB-TCB(0,15)  GET ADDRESS PREVIOUS SCB  05450000
         LA    &EXITADR(1),0(0,&EXITADR(1))   CLEAR HIGH BYTE           05500000
         ST    &EXITADR(1),RBSCHAIN-RBSECT+RBPRFXLN(0,&RBADDR(1)) CHAIN 05550000
*                                            TO CURRENT SCB             05600000
         LA    &EXITADR(1),RBSCBB-RBSECT+RBPRFXLN(0,&RBADDR(1)) GET     05650000
*                                            ADDR CURRENT SCB           05700000
         STCM  &EXITADR(1),7,TCBSTABB-TCB(15) MAKE IT CURRENT           05750000
         SLR   15,15                                                    05800000
.CONT1   ANOP                                                           05850000
         OI    RBFLAGS1-RBPRFX(&RBADDR(1)),X'02'  MARK RB AS OWNING SCB 05900000
IHB2&L1  DS    0H                                                       05950000
         MEXIT                                                          06000000
.XCTLERR MNOTE 4,'***  XCTL   INVALID VALUE SPECIFIED - &XCTL'          06050000
&ERRBIT  SETB  1                                                        06100000
         AGO   .PURCK                                                   06150000
.PURERR  MNOTE 4,'***  PURGE  INVALID VALUE SPECIFIED - &PURGE'         06200000
&ERRBIT  SETB  1                                                        06250000
         AGO   .ASYCK                                                   06300000
.ASYERR  MNOTE 4,'***  ASYNCH INVALID VALUE SPECIFIED - &ASYNCH'        06350000
&ERRBIT  SETB  1                                                        06400000
         AGO   .TERCK                                                   06450000
.TERERR  MNOTE 4,'***  TERM   INVALID VALUE SPECIFIED - &TERM'          06500000
&ERRBIT  SETB  1                                                        06550000
         AGO   .RECCK                                                   06600000
.RECERR  MNOTE 4,'***  RECORD INVALID VALUE SPECIFIED - &RECORD'        06650000
&ERRBIT  SETB  1                                                        06700000
         AGO   .CKERR                                                   06750000
.EXNREG  MNOTE 12,'*** EXITADR SPECIFIED - NOT IN REGISTER'             06800000
         MEXIT                                                          06850000
.NRBA    MNOTE 12,'***RBADDR MUST BE SPECIFIED WITH EXITADR'            06900000
         MEXIT                                                          06950000
.RBNREG  MNOTE 12,'*** RBADDR NOT IN A REGISTER'                        07000000
         MEXIT                                                          07050000
.PARERR  MNOTE 8,'*** PARAM SPECIFIED - NOT IN REGISTER'                07100000
         MEXIT                                                          07150000
.TCBERR  MNOTE 8,'***TCBADDR SPECIFIED - NOT IN REGISTER'               07200000
         MEXIT                                                          07250000
.CANCEL  ANOP                                                           07300000
         AIF   ('&CNCL' NE '0').CANERR                                  07350000
         AIF   ('&WRKREG' EQ '').WRKERR                                 07400000
         AIF   ('&WRKREG'(1,1) NE '(').WRKNREG                          07450000
         AIF   ('&EXITADR' NE '' OR '&RBADDR' NE '' OR '&PARAM' NE ''  X07500000
               OR '&PURGE' NE '' OR '&ASYNCH' NE '' OR '&TERM' NE ''   X07550000
               OR '&RECORD' NE '' OR '&XCTL' NE '').EXCESS              07600000
.CONT3   AIF   ('&TCBADDR' NE '').CKTCB                                 07650000
&NAME    L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    07700000
         ICM   &WRKREG(1),7,TCBSTABB-TCB(15)  GET CURRENT SCB ADDRESS   07750000
         BZ    IHB3&L1                       ZERO IF NO SCB FOR TCB     07800000
         CLC   TCBRBP-TCB+1(3,15),SCBOWNRA-SCB(&WRKREG(1)) IS SCB FOR   07850000
*                                            CURRENT RB                 07900000
         BNE   IHB3&L1                                                  07950000
         TM    SCBFLGS3-SCB(&WRKREG(1)),X'04' WAS SCB CREATED BY        08000000
*                                            FESTAE OR BRANCH ENTRY SVC 08050000
         BO    IHB5&L1                       CONTINUE CHECKING          08100000
IHB3&L1  DS    0H                                                       08150000
         LA    15,12(0,0)                    INDICATE CURRENT SCB NOT   08200000
*                                            CREATED BY FESTAE          08250000
         AIF   ('&ERRET' EQ '').NERRE                                   08300000
         B     &ERRET                                                   08350000
         AGO   .NERRE1                                                  08400000
.NERRE   ANOP                                                           08450000
         B     IHB8&L1                       EXIT                       08500000
.NERRE1  ANOP                                                           08550000
IHB5&L1  DS    0H                                                       08600000
         SLR   15,15                         GET ZEROS                  08650000
         ST    15,SCBEXIT-SCB(0,&WRKREG(1))   MAKE SCB AVAILABLE        08700000
         L     15,PSATOLD-PSA(0,0)           GET CURRENT TCB ADDRESS    08750000
         L     &WRKREG(1),SCBCHAIN-SCB(0,&WRKREG(1)) GET PREVIOUS SCB   08800000
         STCM  &WRKREG(1),7,TCBSTABB-TCB(15)  MAKE IT CURRENT           08850000
         LTR   &WRKREG(1),&WRKREG(1)         IS THERE AN SCB            08900000
         BZ    IHB6&L1                       GO INDICATE NO MORE SCBS   08950000
         CLC   TCBRBP-TCB+1(3,15),SCBOWNRA-SCB(&WRKREG(1)) PREVIOUS FOR 09000000
*                                            CURRENT RB                 09050000
         BE    IHB7&L1                       SET RETURN CODE AND EXIT   09100000
IHB6&L1  DS    0H                                                       09150000
         L     15,TCBRBP-TCB(0,15)            GET CURRENT RB ADDRESS    09200000
         LA     &WRKREG(1),RBPRFXLN           GET LENGTH OF PREFIX      09250000
         SLR    15,&WRKREG(1)                 CALC PREFIX ADDRESS       09300000
         NI    RBFLAGS1-RBPRFX(15),X'FD'      INDICATE  NO MORE SCB FOR 09350000
*                                             THIS RB                   09400000
IHB7&L1  DS    0H                                                       09450000
         SLR   15,15                                                    09500000
IHB8&L1  DS    0H                                                       09550000
         MEXIT                                                          09600000
.CKTCB   ANOP                                                           09650000
         AIF   ('&TCBADDR'(1,1) NE '(').TCBERR                          09700000
&NAME    ICM   &WRKREG(1),7,TCBSTABB-TCB(&TCBADDR(1)) GET CURRENT SCB   09750000
         BZ    IHB3&L1                       ZERO IF NO SCB FOR TCB     09800000
         CLC   TCBRBP-TCB+1(3,&TCBADDR(1)),SCBOWNRA-SCB(&WRKREG(1)) IS  09850000
*                                            SCB FOR CURRENT RB         09900000
         BNE   IHB3&L1                       CONTINUE CHECKING          09950000
*                                            NOT OWNED BY CURRENT RB    10000000
         TM    SCBFLGS3-SCB(&WRKREG(1)),X'04'    WAS SCB CREATED BY     10050000
*                                            FESTAE OR BRANCH ENTRY SVC 10100000
         BO    IHB5&L1                       CONTINUE CHECKING          10150000
IHB3&L1  DS    0H                                                       10200000
         LA    15,12(0,0)                    INDICATE CURRENT SCB NOT   10250000
*                                            CREATED BY FESTAE          10300000
         AIF   ('&ERRET' EQ '').NERET                                   10350000
         B     &ERRET                                                   10400000
         AGO   .NERET1                                                  10450000
.NERET   ANOP                                                           10500000
         B     IHB8&L1                       EXIT                       10550000
.NERET1  ANOP                                                           10600000
IHB5&L1  DS    0H                                                       10650000
         L     15,SCBCHAIN-SCB(0,&WRKREG(1))  GET PREVIOUS SCB ADDRESS  10700000
         STCM  15,7,TCBSTABB-TCB(&TCBADDR(1))    MAKE IT CURRENT        10750000
         LTR   15,15                         IS THERE AN SCB            10800000
         BZ    IHB6&L1                                                  10850000
         CLC   TCBRBP-TCB+1(3,&TCBADDR(1)),SCBOWNRA-SCB(15) IS PREVIOUS 10900000
*                                            FOR CURRENT RB             10950000
         BE    IHB7&L1                                                  11000000
IHB6&L1  DS    0H                                                       11050000
         LA    15,RBPRFXLN                    GET LENGTH OF PREFIX      11100000
         LNR   15,15                          NEGATE FOR SUBTRACTION    11150000
         AL    15,TCBRBP-TCB(0,&TCBADDR(1))   CALC PREFIX ADDRESS       11200000
         NI    RBFLAGS1-RBPRFX(15),X'FD'      INDICATE  NO MORE SCB FOR 11250000
*                                            THIS RB                    11300000
IHB7&L1  DS    0H                                                       11350000
         SLR   15,15                GET ZEROS                           11400000
         ST    15,SCBEXIT-SCB(0,&WRKREG(1))   MAKE SCB AVAILABLE        11450000
IHB8&L1  DS    0H                                                       11500000
          MEXIT                                                         11550000
.CANERR  MNOTE 12,'*** EXITADR/0 OPERAND MISSING'                       11600000
         MEXIT                                                          11650000
.WRKERR  MNOTE 12,'*** WRKREG MUST BE SPECIFIED WITH CANCEL REQUEST'    11700000
         MEXIT                                                          11750000
.WRKNREG MNOTE 8,'*** WRKREG SPECIFIED - NOT IN REGISTER'               11800000
         MEXIT                                                          11850000
.EXCESS  MNOTE *,'***EXCESSIVE PARAMETERS W/CANCEL REQUEST IGNORED'     11900000
         AGO   .CONT3                                                   11950000
         MEND                                                           12000000
