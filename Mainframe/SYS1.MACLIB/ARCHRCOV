         MACRO                                                          00050000
.********************************************************************** 00100000
.*                                                                    * 00150000
.* $MAC(ARCHRCOV) COMP(SERVICE) PROD(DFHSM):                          * 00200000
.* MACRO NAME = ARCHRCOV                                              * 00250000
.* FUNC = SERVICE                                                     * 00300000
.* SUBFUNC = MWE                                                      * 00350000
.*                                                                    * 00400000
.* DESCRIPTIVE NAME = REQUEST DFHSM RECOVER A DATA SET.               * 00450000
.*                                                                    * 00500000
.********************************************************************** 00537500
.*PROPRIETARY V3 STATEMENT                                            * 00575000
.*LICENSED MATERIALS - PROPERTY OF IBM                                * 00612500
.*"RESTRICTED MATERIALS OF IBM"                                       * 00650000
.*5695-DF1                                                            * 00678100
.*(C) COPYRIGHT 1989,1995  IBM CORP.                                  * 00706200
.*END PROPRIETARY V3 STATEMENT                                        * 00734300
.********************************************************************** 00762500
.*                                                                    * 00800000
.* STATUS = VERSION 2, RELEASE 6, LEVEL 0                             * 00850000
.*                                                                    * 00900000
.* FUNCTION = PERFORM DFHSM SVC 109 TO RECOVER THE SPECIFIED          * 00950000
.*            DATA SET FROM THE LATEST BACKUP VERSION OR DUMP COPY.   * 01000000
.*                                                                    * 01050000
.* NOTES = SEE BELOW                                                  * 01100000
.*                                                                    * 01150000
.*   DEPENDENCIES = NONE                                              * 01200000
.*                                                                    * 01250000
.*   RESTRICTIONS = NONE                                              * 01300000
.*                                                                    * 01350000
.*   REGISTER CONVENTIONS =                                           * 01400000
.*                                                                    * 01450000
.*            REGISTER 13 MUST POINT TO A STANDARD SAVE AREA          * 01500000
.*            WHEN MACRO IS ISSUED.                                   * 01550000
.*                                                                    * 01600000
.*            REGISTERS 0, 1, 14 AND 15 WILL BE DESTROYED.            * 01650000
.*                                                                    * 01700000
.*   PATCH LABEL = NONE                                               * 01750000
.*                                                                    * 01800000
.* MODULE TYPE = MACRO                                                * 01850000
.*                                                                    * 01900000
.*   PROCESSOR = BAL                                                  * 01950000
.*                                                                    * 02000000
.* INPUT =                                                            * 02050000
.*          DSN      (REQUIRED) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 02100000
.*                   CONTAINING THE NAME OF THE DATA SET TO BE ACTED  * 02150000
.*                   ON, LEFT JUSTIFIED AND PADDED WITH BLANKS.       * 02200000
.*                                                                    * 02250000
.*          TOVOL    (OPTIONAL) SPECIFY THE ADDRESS OF A 6 CHARACTER  * 02300000
.*                   SERIAL OF THE VOLUME TO RECEIVE THE DATA SET,    * 02350000
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           * 02400000
.*                                                                    * 02450000
.*               NOTE:     IF TOVOL IS SPECIFIED, UNIT IS REQUIRED.   * 02500000
.*                                                                    * 02550000
.*          UNIT     SPECIFY THE ADDRESS OF A 4 CHARACTER UCB DEVICE  * 02600000
.*                   TYPE OF THE DASD VOLUME TO CONTAIN THE DATA SET  * 02650000
.*                   (VOLUME SPECIFIED BY THE TOVOL PARAMETER).       * 02700000
.*                                                                    * 02750000
.*               NOTE:     SPECIFY THE FROMVOL PARAMETER ONLY IF THE  * 02800000
.*                         DATA SET WAS UNCATALOGED WHEN BACKED UP.   * 02850000
.*                                                                    * 02900000
.*          FROMVOL  SPECIFY THE ADDRESS OF A 6 CHARACTER VOLUME      * 02950000
.*                   SERIAL OF THE VOLUME THAT CONTAINED THE DATA     * 03000000
.*                   SET AT THE TIME THE DATA SET WAS BACKED UP,      * 03050000
.*                   LEFT JUSTIFIED AND PADDED WITH BLANKS.           * 03100000
.*                                                                    * 03150000
.*          WAIT     (OPTIONAL) SPECIFY YES|NO WHETHER APPLICATION    * 03200000
.*                   DESIRES TO WAIT UNTIL DFHSM HAS PROCESSED THIS   * 03250000
.*                   REQUEST.  DEFAULT FOR THIS PARAMETER IS NO.      * 03300000
.*                                                                    * 03350000
.*               NOTE:     GEN, DATE AND VER ARE MUTUALLY         @SHC* 03387500
.*                         EXCLUSIVE PARAMETERS, ONLY ONE MAY     @SHC* 03425000
.*                         BE SPECIFIED.                          @SHC* 03462500
.*                                                                    * 03500000
.*          GEN      (OPTIONAL) SPECIFY THE ADDRESS OF A 2 BYTE       * 03550000
.*                   GENERATION NUMBER OF THE BACKUP GENERATION TO    * 03600000
.*                   BE RECOVERED.                                    * 03650000
.*                                                                    * 03700000
.*          DATE     (OPTIONAL) SPECIFY THE ADDRESS OF A 7 CHARACTER  * 03750000
.*                   DATE IN THE FORMAT YYYYDDD.  THE BACKUP VERSION  * 03800000
.*                   RECOVERED IS THE NEWEST ONE CREATED ON OR BEFORE * 03850000
.*                   THE DATE SPECIFIED.                              * 03900000
.*                                                                    * 03910000
.*          VER      (OPTIONAL) SPECIFY THE ADDRESS OF A 2 BYTE   @SHA* 03920000
.*                   VERSION NUMBER OF THE BACKUP VERSION TO BE   @SHA* 03930000
.*                   RECOVERED.                                   @SHA* 03940000
.*                                                                    * 03950000
.*          NEWNAME  (OPTIONAL) SPECIFY THE ADDRESS OF A 44-BYTE AREA * 04000000
.*                   CONTAINING THE NEW NAME FOR THE DATA SET TO BE   * 04050000
.*                   RECOVERED, LEFT JUSTIFIED AND PADDED WITH BLANKS.* 04100000
.*                                                                    * 04150000
.*          REPLACE  (OPTIONAL) SPECIFY YES|NO WHETHER AN EXISTING    * 04200000
.*                   DATA SET WITH THE SAME NAME IS TO BE REPLACED.   * 04250000
.*                   DEFAULT FOR THIS PARAMETER IS NO.                * 04300000
.*                                                                    * 04350000
.*          TEST     (OPTIONAL) SPECIFY YES|NO WHETHER MACRO IN TEST  * 04400000
.*                   MODE.  TEST=YES DENOTES THAT THE MWE SHOULD BE   * 04450000
.*                   BUILT BUT THAT NO DFHSM SVC IS ISSUED.  DEFAULT  * 04500000
.*                   FOR THIS PARAMETER IS NO.                        * 04550000
.*                                                                    * 04600000
.*          SNAPDCB  (OPTIONAL) SPECIFY THE ADDRESS OF AN OPEN DCB    * 04650000
.*                   USED FOR SNAP PROCESSING OF THE MWE AREA UPON    * 04700000
.*                   RETURN FROM DFHSM SVC PROCESSING.                * 04750000
.*                                                                    * 04800000
.*          WKAREA   (REQUIRED) SPECIFY THE ADDRESS OF A 200 BYTE     * 04850000
.*                   WORK AREA TO CONTAIN INFORMATION SPECIFIED IN    * 04900000
.*                   THIS MACRO.                                      * 04950000
.*                                                                    * 05000000
.*          DAOPT    (OPTIONAL) SPECIFY SAMETRK|RELTRK|RELBLK     @01A* 05050000
.*                   DEPENDING ON THE VOLUME SELECTION AND DATA   @01A* 05100000
.*                   ACCESSING REQUIRED FOR THE RECALL OF A DA    @01A* 05150000
.*                   DATA SET                                         * 05200000
.*                                                                    * 05208300
.*          RCVRQD   (OPTIONAL) SPECIFY YES|NO WHETHER RECOVERY   @THA* 05216600
.*                   REQUIRED INDICATOR SHOULD BE TURNED ON IN    @THA* 05224900
.*                   TARGET DATA SET.  DEFAULT FOR THIS PARAMETER @THA* 05233200
.*                   IS NO                                        @THA* 05241500
.*                                                                    * 05250000
.* OUTPUT =   REGISTER 15 WILL CONTAIN MWE RETURN CODE                * 05300000
.*            REGISTER 0  WILL CONTAIN MWE REASON CODE                * 05350000
.*                                                                    * 05400000
.*            INVALID REQUESTS NOT SENT TO DFHSM WILL HAVE            * 05450000
.*            THE FOLLOWING RETURN CODES:                             * 05500000
.*                                                                    * 05550000
.*            403 - AN ATTEMPT WAS MADE TO SPECIFY A VOLUME       @01A* 05600000
.*                  RECOVER BY SPECIFYING DSN='*' OR DSN=' '      @01A* 05650000
.*            806 - LINK FAILURE - UNABLE TO LINK TO ARCGIVER         * 05700000
.*                                                                    * 05750000
.* CHANGE ACTIVITY =                                                  * 05800000
.* $K0=HSMKD,250,090888,THP5: PROGRAMMABLE DFHSM REQUEST          @KDA* 05850000
.*                            PROCESSOR                           @KDA* 05900000
.* $01=OY40118,260,020591,THA4: ROLLIN APAR OY39056 - DA DATA     @01A* 05950000
.*                              SET IMPROVEMENTS SPE              @01A* 06000000
.* $SH=HSMSH,130,071994,THWG: RLS1 SUPPORT                        @SHA* 06025000
.* $TH=HSMTH,130,102494,THWG: RLS1B SUPPORT                       @THA* 06037500
.*                                                                    * 06050000
.********************************************************************** 06100000
&NAME    ARCHRCOV &DSN=,&UNIT=,                                        X06150000
               &WAIT=NO,&FROMVOL=,                                     X06200000
               &GEN=,&DATE=,&VER=,&NEWNAME=,&REPLACE=NO,&TOVOL=,       X06250000
               &WKAREA=,&SNAPDCB=,&TEST=NO,&DAOPT=,&RCVRQD=NO     @THC  06300000
         LCLA  &A                                                       06350000
         LCLB  &ERROR                                                   06400000
         LCLC  &H                                                       06450000
         LCLC  &L1                                                      06500000
         LCLC  &L2                                                      06550000
&L1      SETC  'ARC'.'&SYSNDX'.'A'                                      06600000
&L2      SETC  'ARC'.'&SYSNDX'.'B'                                      06650000
.*                                                                      06700000
.* CHECK THE PARAMETERS SPECIFIED WITH THE HRECOVER FUNCTION            06750000
.*                                                                      06800000
.CHKRECV ANOP                                                           06850000
         AIF   ('&DSN' NE '').REC1                                      06900000
         MNOTE 8,'REQUIRED PARAMETER, DSN, NOT SPECIFIED'               06950000
&ERROR   SETB  1                                                        07000000
.REC1    ANOP                                                           07050000
         AIF   ('&UNIT' EQ '' AND '&TOVOL' EQ '').REC2                  07100000
         AIF   ('&UNIT' NE '' AND '&TOVOL' NE '').REC2                  07150000
         MNOTE 8,'EITHER BOTH UNIT AND TOVOL MUST BE SPECIFIED OR NEITHX07200000
               ER SHOULD BE SPECIFIED'                                  07250000
&ERROR   SETB  1                                                        07300000
.REC2    ANOP                                                           07350000
         AIF   ('&GEN' EQ '' AND '&DATE' EQ '' AND '&VER' EQ '').REC3   07391600
         AIF   ('&GEN' EQ '' AND ('&DATE' EQ '' OR '&VER' EQ '')).REC3  07433200
         AIF   ('&DATE' EQ '' AND ('&GEN' EQ '' OR '&VER' EQ '')).REC3  07474800
         MNOTE 8,'GEN, DATE AND VER ARE MUTUALLY EXCLUSIVE, ONLY ONE SHX07516400
               OULD BE SPECIFIED'                                 5@SHC 07558000
&ERROR   SETB  1                                                        07600000
.REC3    ANOP                                                           07650000
         AIF   ('&WKAREA' NE '').REC4                                   07700000
         MNOTE 8,'REQUIRED PARAMETER, WKAREA, IS NOT SPECIFIED'         07750000
&ERROR   SETB  1                                                        07800000
.REC4    ANOP                                                     @01A  07850000
         AIF   ('&DAOPT' EQ '').REC5                              @01A  07900000
         AIF   ('&DAOPT' EQ 'SAMETRK').REC5                       @01A  07950000
         AIF   ('&DAOPT' EQ 'RELTRK').REC5                        @01A  08000000
         AIF   ('&DAOPT' EQ 'RELBLK').REC5                        @01A  08050000
         MNOTE 8,'DAOPT SUBPARAMETER SPECIFIED INCORRECTLY'       @01A  08100000
&ERROR   SETB  1                                                  @01A  08150000
.REC5    ANOP                                                     @01C  08200000
         AIF   (NOT &ERROR).BUILD                                       08250000
         MEXIT                                                          08300000
.*                                                                      08350000
.* AT THIS POINT ALL THE PARAMETERS HAVE BEEN PROPERLY SPECIFIED.       08400000
.* NOW BUILD THE CONTROL BLOCK AND CALL THE PROCESSING ROUTINE.         08450000
.*                                                                      08500000
.BUILD   ANOP                                                           08550000
         AIF   ('&WKAREA'(1,1) EQ '(').WREG                             08600000
&NAME    LA    15,&WKAREA                                               08650000
         AGO   .CLRWKA                                                  08700000
.WREG    ANOP                                                           08750000
&A       SETA  K'&WKAREA-2                                              08800000
&H       SETC  '&WKAREA'(2,&A)                                          08850000
&NAME    LR    15,&H                                                    08900000
.CLRWKA  ANOP                                                           08950000
         XC    0(200,15),0(15)     CLEAR THE WORK AREA                  09000000
         B     *+12                BRANCH AROUND IDENTIFIER             09050000
         DC    CL8'DFHSMREQ'                                            09100000
         MVC   0(8,15),*-8         SET THE IDENTIFIER                   09150000
         B     *+12                BRANCH AROUND FUNCTION NAME          09200000
         DC    CL8'HRECOVER'       FUNCTION NAME                        09250000
         MVC   8(8,15),*-8         SET THE FUNCTION NAME                09300000
.XWAIT   AIF   ('&WAIT' NE 'YES').XREP                                  09350000
         OI    16(15),X'40'        WAIT WAS SPECIFIED                   09400000
.XREP    AIF   ('&REPLACE' NE 'YES').XTEST                              09450000
         OI    16(15),X'08'        REPLACE WAS SPECIFIED                09500000
.XTEST   AIF   ('&TEST' NE 'YES').XDSN                                  09550000
         OI    16(15),X'02'        TEST WAS SPECIFIED                   09600000
.XDSN    AIF   ('&DSN'(1,1) EQ '(').DREG                                09650000
         LA    1,&DSN              GET ADDR OF DSN                      09700000
         ST    1,20(15)            STORE IN PARM LIST                   09750000
         AGO   .XSNAP                                                   09800000
.DREG    ANOP                                                           09850000
&A       SETA  K'&DSN-2                                                 09900000
&H       SETC  '&DSN'(2,&A)                                             09950000
         ST    &H,20(15)           STORE ADDR OF DSN                    10000000
.XSNAP   ANOP                                                           10050000
         AIF   ('&SNAPDCB' EQ '').XUNIT                                 10100000
         AIF   ('&SNAPDCB'(1,1) EQ '(').SREG                            10150000
         LA    1,&SNAPDCB          GET ADDRESS OF SNAP DCB              10200000
         ST    1,188(15)           STORE IN PARM LIST                   10250000
         AGO   .XUNIT                                                   10300000
.SREG    ANOP                                                           10350000
&A       SETA  K'&SNAPDCB-2                                             10400000
&H       SETC  '&SNAPDCB'(2,&A)                                         10450000
         ST    &H,188(15)          STORE DCB ADDRESS IN PARM LIST       10500000
.XUNIT   ANOP                                                           10550000
         AIF   ('&UNIT' EQ '').XFROM                                    10600000
         AIF   ('&UNIT'(1,1) EQ '(').UREG                               10650000
         LA    1,&UNIT             GET ADDR OF UNIT                     10700000
         ST    1,24(15)            STORE IN PARM LIST                   10750000
         AGO   .XFROM                                                   10800000
.UREG    ANOP                                                           10850000
&A       SETA  K'&UNIT-2                                                10900000
&H       SETC  '&UNIT'(2,&A)                                            10950000
         ST    &H,24(15)           STORE ADDR OF UNIT PARM              11000000
.XFROM   ANOP                                                           11050000
         AIF   ('&FROMVOL' EQ '').XGEN                                  11100000
         AIF   ('&FROMVOL'(1,1) EQ '(').FREG                            11150000
         LA    1,&FROMVOL          GET ADDR OF FROM VOLUME              11200000
         ST    1,32(15)            STORE IN PARM LIST                   11250000
         AGO   .XGEN                                                    11300000
.FREG    ANOP                                                           11350000
&A       SETA  K'&FROMVOL-2                                             11400000
&H       SETC  '&FROMVOL'(2,&A)                                         11450000
         ST    &H,32(15)           STORE ADDR OF FROM VOLUME PARM       11500000
.XGEN    ANOP                                                           11550000
         AIF   ('&GEN' EQ '').XDATE                                     11600000
         AIF   ('&GEN'(1,1) EQ '(').GREG                                11650000
         LA    1,&GEN              GET ADDR OF GENERATIONS              11700000
         ST    1,40(15)            STORE IN PARM LIST                   11750000
         AGO   .XDATE                                                   11800000
.GREG    ANOP                                                           11850000
&A       SETA  K'&GEN-2                                                 11900000
&H       SETC  '&GEN'(2,&A)                                             11950000
         ST    &H,40(15)           STORE ADDR OF GENERATIONS PARM       12000000
.XDATE   ANOP                                                           12050000
         AIF   ('&DATE' EQ '').XVER                               @SHC  12100000
         AIF   ('&DATE'(1,1) EQ '(').DATEREG                            12150000
         LA    1,&DATE             GET ADDR OF DATE                     12200000
         ST    1,44(15)            STORE IN PARM LIST                   12250000
         AGO   .XVER                                              @SHC  12300000
.DATEREG ANOP                                                           12350000
&A       SETA  K'&DATE-2                                                12400000
&H       SETC  '&DATE'(2,&A)                                            12450000
         ST    &H,44(15)           STORE ADDR OF DATE PARM              12500000
.XVER    ANOP                                                     @SHA  12504500
         AIF   ('&VER' EQ '').XNEW                                @SHA  12509000
         AIF   ('&VER'(1,1) EQ '(').VREG                          @SHA  12513500
         LA    1,&VER              GET ADDR OF VERSION            @SHA  12518000
         ST    1,68(15)            STORE IN PARM LIST             @SHA  12522500
         AGO   .XNEW                                              @SHA  12527000
.VREG    ANOP                                                     @SHA  12531500
&A       SETA  K'&VER-2                                           @SHA  12536000
&H       SETC  '&VER'(2,&A)                                       @SHA  12540500
         ST    &H,68(15)           STORE ADDR OF VERSION PARM     @SHA  12545000
.XNEW    ANOP                                                           12550000
         AIF   ('&NEWNAME' EQ '').XTOVOL                                12600000
         AIF   ('&NEWNAME'(1,1) EQ '(').NEWREG                          12650000
         LA    1,&NEWNAME          GET ADDR OF NEWNAME                  12700000
         ST    1,48(15)            STORE IN PARM LIST                   12750000
         AGO   .XTOVOL                                                  12800000
.NEWREG  ANOP                                                           12850000
&A       SETA  K'&NEWNAME-2                                             12900000
&H       SETC  '&NEWNAME'(2,&A)                                         12950000
         ST    &H,48(15)           STORE ADDR OF NEWNAME PARM           13000000
.XTOVOL  ANOP                                                           13050000
         AIF   ('&TOVOL' EQ '').XDAOPT                            @01C  13100000
         AIF   ('&TOVOL'(1,1) EQ '(').TOREG                             13150000
         LA    1,&TOVOL            GET ADDR OF TOVOL                    13200000
         ST    1,52(15)            STORE IN PARM LIST                   13250000
         AGO   .XDAOPT                                            @01C  13300000
.TOREG   ANOP                                                           13350000
&A       SETA  K'&TOVOL-2                                               13400000
&H       SETC  '&TOVOL'(2,&A)                                           13450000
         ST    &H,52(15)           STORE ADDR OF TOVOL PARM             13500000
.XDAOPT  ANOP                                                     @01A  13550000
         AIF   ('&DAOPT' EQ '').XRCVRQD                           @THC  13600000
         AIF   ('&DAOPT' NE 'SAMETRK').DAOPT2                     @01A  13650000
         OI    17(15),X'40'        DAOPT=SAMETRK WAS SPECIFIED    @01A  13700000
         AGO   .XRCVRQD                                           @THC  13750000
.DAOPT2  AIF   ('&DAOPT' NE 'RELTRK').DAOPT3                      @01A  13800000
         OI    17(15),X'80'        DAOPT=RELTRK WAS SPECIFIED     @01A  13850000
         AGO   .XRCVRQD                                           @THC  13900000
.DAOPT3  AIF   ('&DAOPT' NE 'RELBLK').XRCVRQD                     @THC  13950000
         OI    17(15),X'C0'        DAOPT=RELBLK WAS SPECIFIED     @01A  14000000
.XRCVRQD AIF   ('&RCVRQD' NE 'YES').GIVER                         @THA  14016600
         OI    17(15),X'20'        RCVRQD WAS SPECIFIED           @THA  14033200
.GIVER   ANOP                                                           14050000
         LR    1,15                PARAMETER ADDRESS TO REG 1           14100000
         LINK  EP=ARCGIVER,ERRET=&L1                                    14150000
         L     15,192(1)           LOAD RETURN CODE                     14200000
         L     0,196(1)            LOAD REASON CODE                     14250000
         B     &L2                                                      14300000
&L1      LA    15,806              INDICATE LINK FAILED                 14350000
&L2      EQU   *                                                        14400000
         MEXIT                                                          14450000
         MEND                                                           14500000
