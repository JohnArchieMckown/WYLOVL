* %/*                                                                   00050000
         MACRO                                                          00100000
&LABEL   PGSER &FORMAT,&FUNC,&LA=,&A=,&EA=,&TCB=,&ECB=,&KEEPREL=,      X00150000
               &ASCB=,&RELEASE=,&LONG=,&ANYWHER=,&BRANCH=N,&BACKOUT=,  X00200000
               &RELATED=                                                00250000
.*                                                                      00300000
.*                                                    03/30/92          00335700
.* PROPRIETARY STATEMENT =                                              00385700
.*       THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                    00442800
.*       5647-A01  (C) COPYRIGHT IBM CORP. 1981, 2000                   00466600
.*       LICENSED MATERIALS - PROPERTY OF IBM                           00490400
.*                                                                      00550000
.*   STATUS = HBB7703                                                   00600000
.*                                                                      00625000
.* EXTERNAL CLASSIFICATION: PI                                          00650000
.* END OF EXTERNAL CLASSIFICATION                                       00675000
.*                                                                      00700000
.*       CHANGE-ACTIVITY =                                              00712500
.*          $L1=PROD310,HBB3310,860219,PD48: Base Support               00725000
.*          $D1=DHB1019,HBB4410,890206,PDK1: RELEASE CLEAR(Y|N)         00731200
.*          $01=OY37322 ,HBB4410,901115,PD00A8: Use literals for CVT    00734300
.*   $L2=RSMDVP04,JBB4422,900824,PD00A8: PROTECT/UNPROTECT Support      00735900
.*   $P1=PKB2145 ,HBB4430,920411,PD00A8: Fix Branch=Y problem           00736700
.*   $L3=64BIT   ,JBB7703,990730,PD00SE: LIMIT Support                  00737100
.*   $P2=PXD0443 ,HBB7703,990928,PD00SE: Copyright Errors               00737300
.*                                                                      00737500
.*  A-000000-999999                                         @G860P23    00750000
         LCLB  &LFORMAT,&BRANCHE,&NOEA,&YKPRL,&YRELS,&YANYW,&YLONG,    X00800000
               &YBACK,&DFLTDEA,&IGNORET,&ZEROTCB,&LALA,&LAA             00850000
         LCLC  &FUNCODE,&BITS,&SQ,&LABELX,&FREG,&EAREG                  00900000
&SQ      SETC  '&SYSNDX' SET SEQUENCE NUMBER FOR UNIQUE LABELS IN       00950000
.*                            GENERATED CODE                            01000000
         AIF   ('&LABEL' EQ '').DFLTLBL                                 01050000
&LABELX  SETC  '&LABEL'                                                 01100000
         AGO   .SVCOBRE                                                 01150000
.DFLTLBL ANOP                                                           01200000
&LABELX  SETC  'IHB&SQ.A'      GENERATE OUR OWN LABEL,CALLER LACKED ONE 01250000
.SVCOBRE AIF   ('&BRANCH' EQ 'N').TCBCHK       SVC ENTRY ?              01300000
         AIF   ('&BRANCH' NE 'Y' AND '&BRANCH' NE 'SPECIAL').BADBR      01350000
&BRANCHE SETB  1               INDICATE BRANCH ENTRY                    01400000
         AGO   .GETFORM                                                 01450000
.TCBCHK  AIF   (T'&TCB NE 'O').TCBNO  CANT SPECIFY TCB FOR SVC ENTRY    01500000
.GETFORM AIF   (T'&FORMAT EQ 'O').FRMOMIT                               01550000
         AIF   ('&FORMAT' EQ 'L').LFMCHK L-FORMAT CALL                  01600000
         AIF   ('&FORMAT' NE 'R').BADFORM                               01650000
         AIF   (T'&LA NE 'O').RLA        R-FORMAT CALL                  01700000
         AIF   (T'&A EQ 'O' OR '&A' EQ '').AOMIT                        01750000
         AIF   ('&A'(1,1) EQ'(').EATYPCK                                01800000
&LAA     SETB  1               MACRO MUST LOAD START ADDRESS            01850000
.EATYPCK AIF   (&BRANCHE).EAR2                                          01900000
&EAREG   SETC  '15'             'EA' GOES IN R15 FOR SVC ENTRY          01950000
         AGO   .EACHK                                                   02000000
.EAR2    ANOP                                                           02050000
&EAREG   SETC  '2'              'EA' GOES IN R2 FOR BRANCH ENTRY        02100000
.EACHK   AIF   (T'&EA EQ 'O').NOEA                                      02150000
         AIF   ('&EA' EQ '').NULLEA                                     02200000
         AGO   .GETFUNC                                                 02250000
.NOEA    ANOP                  TOOK THE DEFAULT END ADDRESS(=START      02300000
&DFLTDEA SETB  1                 ADDRESS)                               02350000
         AGO   .GETFUNC                                                 02400000
.LFMCHK  AIF   (T'&A NE 'O').LA        L-FORM CALL                      02450000
         AIF   (T'&EA NE 'O').LEA                                       02500000
         AIF   (T'&LA EQ 'O' OR '&LA' EQ '').NOLA                       02550000
&LFORMAT SETB  1                                                        02600000
         AIF   ('&LA'(1,1) EQ '(').GETFUNC                              02650000
&LALA    SETB  1               MACRO MUST LOAD THE LIST ADDRESS         02700000
         AGO   .GETFUNC                                                 02750000
.GETFUNC AIF   (T'&FUNC EQ 'O').NOFUNC                                  02800000
         AIF   ('&BRANCH' EQ 'SPECIAL').FAST  SEE IF SPECIAL CODE REQ'D 02850000
         AIF   (T'&ASCB NE 'O').XASCB   ERROR FOR ASCB IF NOT SPECIAL   02900000
         AIF   ('&FUNC' EQ 'FIX'    ).FFIX                              02950000
         AIF   ('&FUNC' EQ 'FREE'   ).FFREE                             03000000
         AIF   ('&FUNC' EQ 'ANYWHER').FANYW                             03050000
         AIF   ('&FUNC' EQ 'LOAD'   ).FLOAD                             03100000
         AIF   ('&FUNC' EQ 'OUT'    ).FOUT                              03150000
         AIF   ('&FUNC' EQ 'PROTECT').FPROTECT                     @L2A 03166600
         AIF   ('&FUNC' EQ 'UNPROTECT').FUNPROTECT                 @L2A 03183200
         AIF   ('&FUNC' NE 'RELEASE').BADFUNC                           03200000
&IGNORET SETB  1               WILL IGNORE TCB IF SPECIFIED             03250000
&FUNCODE SETC  '06'            CODE FOR RELEASE                         03300000
         AIF   (T'&KEEPREL NE 'O').XKPRL    CHECK OPTIONS               03350000
         AIF   (T'&RELEASE NE 'O').XRELS                                03400000
         AIF   (T'&LONG    NE 'O').XLONG                                03450000
         AIF   (T'&ANYWHER NE 'O').XANYW                                03500000
         AIF   (T'&BACKOUT NE 'O').XBACK                                03550000
         AIF   (T'&ECB     NE 'O').XECB                                 03600000
         AIF   (T'&TCB EQ 'O' OR '&TCB' EQ '').OPTIONS                  03650000
         MNOTE 0,'''TCB='' PARAMETER NOT APPLICABLE TO THIS PAGE SERVICX03700000
               E. IT IS IGNORED.'                                       03750000
         AGO   .OPTIONS                                                 03800000
.FPROTECT ANOP                                                     @L2A 03803100
&FUNCODE SETC  '07'            CODE FOR PROTECT                    @L2A 03806200
         AGO   .PRO1                                               @L2A 03809300
.FUNPROTECT ANOP                                                   @L2A 03812400
&FUNCODE SETC  '08'            CODE FOR UNPROTECT                  @L2A 03815500
.PRO1    ANOP                                                      @L2A 03818600
&IGNORET SETB  1               WILL IGNORE TCB IF SPECIFIED        @P1A 03820100
         AIF   (T'&RELEASE NE 'O').XRELS    CHECK OPTIONS          @L2A 03821700
         AIF   (T'&LONG    NE 'O').XLONG                           @L2A 03824800
         AIF   (T'&ANYWHER NE 'O').XANYW                           @L2A 03827900
         AIF   (T'&BACKOUT NE 'O').XBACK                           @L2A 03831000
         AIF   (T'&ECB     NE 'O').XECB                            @L2A 03834100
         AIF   (T'&TCB EQ 'O' OR '&TCB' EQ '').OPTIONS             @L2A 03837200
         MNOTE 0,'''TCB='' PARAMETER NOT APPLICABLE TO THIS PAGE SERVICX03840300
               E. IT IS IGNORED.'                                  @L2A 03843400
         AGO   .OPTIONS                                            @L2A 03846500
.FOUT    ANOP                                                           03850000
&FUNCODE SETC  '05'            CODE FOR OUT                             03900000
         AIF   (T'&RELEASE NE 'O').XRELS    CHECK OPTIONS               03950000
         AIF   (T'&LONG    NE 'O').XLONG                                04000000
         AIF   (T'&ANYWHER NE 'O').XANYW                                04050000
         AIF   (T'&BACKOUT NE 'O').XBACK                                04100000
         AIF   (T'&ECB     NE 'O').XECB                                 04150000
         AIF   (&BRANCHE AND (T'&TCB EQ 'O' OR '&TCB' EQ '')).ZTCB      04200000
         AGO   .OPTIONS                                                 04250000
.FLOAD   ANOP                                                           04300000
&FUNCODE SETC  '04'            CODE FOR LOAD                            04350000
         AIF   (T'&KEEPREL NE 'O').XKPRL    CHECK OPTIONS               04400000
         AIF   (T'&LONG    NE 'O').XLONG                                04450000
         AIF   (T'&ANYWHER NE 'O').XANYW                                04500000
         AIF   (T'&BACKOUT NE 'O').XBACK                                04550000
         AIF   (&BRANCHE AND (T'&TCB EQ 'O' OR '&TCB' EQ '')).ZTCB      04600000
         AGO   .OPTIONS                                                 04650000
.FANYW   ANOP                                                           04700000
&IGNORET SETB  1               WILL IGNORE TCB IF SPECIFIED             04750000
&FUNCODE SETC  '03'            CODE FOR ANYWHER                         04800000
         AIF   (T'&KEEPREL NE 'O').XKPRL    CHECK OPTIONS               04850000
         AIF   (T'&RELEASE NE 'O').XRELS                                04900000
         AIF   (T'&LONG    NE 'O').XLONG                                04950000
         AIF   (T'&ANYWHER NE 'O').XANYW                                05000000
         AIF   (T'&BACKOUT NE 'O').XBACK                                05050000
         AIF   (T'&ECB     NE 'O').XECB                                 05100000
         AIF   (T'&TCB EQ 'O' OR '&TCB' EQ '').OPTIONS                  05150000
         MNOTE 0,'''TCB='' PARAMETER NOT APPLICABLE TO THIS PAGE SERVICX05200000
               E. IT IS IGNORED.'                                       05250000
         AGO   .OPTIONS                                                 05300000
.FFREE   ANOP                                                           05350000
&FUNCODE SETC  '02'            CODE FOR FREE                            05400000
         AIF   (T'&KEEPREL NE 'O').XKPRL    CHECK OPTIONS               05450000
         AIF   (T'&LONG    NE 'O').XLONG                                05500000
         AIF   (T'&BACKOUT NE 'O').XBACK                                05550000
         AIF   (&BRANCHE AND (T'&TCB EQ 'O' OR '&TCB' EQ '')).ZTCB      05600000
         AGO   .OPTIONS                                                 05650000
.FFIX    ANOP                                                           05700000
&FUNCODE SETC  '01'            CODE FOR FIX                             05750000
         AIF   (T'&KEEPREL NE 'O').XKPRL    CHECK OPTIONS               05800000
         AIF   (T'&ANYWHER NE 'O').XANYW                                05850000
         AIF   (T'&ECB     EQ 'O' OR '&ECB' EQ '').NOECB                05900000
         AIF   ('&ECB' NE '0' AND '&BACKOUT' EQ 'N').NOIMP              05950000
         AIF   (&BRANCHE AND (T'&TCB EQ 'O' OR '&TCB' EQ '')).ZTCB      06000000
         AGO   .OPTIONS                                                 06050000
.ZTCB    ANOP                                                           06100000
&ZEROTCB SETB  1               INDICATE TOOK DEFAULT ZERO-TCB           06150000
.OPTIONS AIF   (T'&KEEPREL EQ 'O' OR '&KEEPREL' EQ 'N').ORELS           06200000
         AIF   ('&KEEPREL' NE 'Y').BDKPRL                               06250000
&YKPRL   SETB  1               KEEPREL=Y INDICATED                      06300000
.ORELS   AIF   (T'&RELEASE EQ 'O' OR '&RELEASE' EQ 'N').OLONG           06350000
         AIF   ('&RELEASE' NE 'Y').BDRELS                               06400000
&YRELS   SETB  1               RELEASE=Y INDICATED                      06450000
.OLONG   AIF   (T'&LONG EQ 'O' AND '&FUNC' EQ 'FIX').YLONG              06500000
         AIF   (T'&LONG EQ 'O' OR '&LONG' EQ 'N').OANYW                 06550000
         AIF   ('&LONG' NE 'Y').BDLONG                                  06600000
.YLONG   ANOP                                                           06650000
&YLONG   SETB  1               LONG=Y INDICATED (OR DEFAULTED FOR FIX)  06700000
.OANYW   AIF   (T'&ANYWHER EQ 'O' OR '&ANYWHER' EQ 'N').OBACK           06750000
         AIF   ('&ANYWHER' NE 'Y').BDANYW                               06800000
&YANYW   SETB  1               ANYWHER=Y INDICATED                      06850000
.OBACK   AIF   (T'&BACKOUT EQ 'O' AND '&FUNC' EQ 'FIX').YBACK           06900000
         AIF   ('&BACKOUT' EQ 'N' OR T'&BACKOUT EQ 'O').OTCB            06950000
         AIF   ('&BACKOUT' NE 'Y').BDBACK                               07000000
.YBACK   ANOP                                                           07050000
&YBACK   SETB  1               FIX GETS DEFAULT OF BACKOUT=Y            07100000
.OTCB    AIF   (&IGNORET).GENCODE   SHOULD WE IGNORE THIS TCB INFO      07150000
         AIF   (T'&TCB      EQ 'O' OR '&TCB' EQ '').GENCODE             07200000
         AIF   ('&TCB'      NE '0'  ).GENCODE                           07250000
&ZEROTCB SETB  1              USER WANTS A 'ZERO TCB' (NO FOE) FIX/FREE 07300000
.GENCODE AIF   (NOT &DFLTDEA).SETBITS                                   07350000
         MNOTE *,'''EA='' PARAMETER OMITTED FOR R-FORM REQUEST, DEFAULTX07400000
               ED BY MACRO EXPANSION TO VALUE OF ''A=''.'               07450000
.SETBITS ANOP                                                           07500000
&BITS    SETC  'X''&FUNCODE.00''+B''0&YRELS.&YKPRL.&YANYW.&YLONG.&YBACKX07550000
               .00'''                                                   07600000
         AIF   ((NOT &LFORMAT) AND &LAA).NODCS                          07650000
         B     &LABELX          GENERATE BRANCH AROUND CONSTANT         07700000
         AIF   (NOT &LFORMAT).REGDC                                     07750000
IHB&SQ.Y DC    A(X'80000000')   CONSTANT TO TURN ON BIT0 IN A REG       07800000
         AGO   .COLLECT                                                 07850000
.REGDC   ANOP                                                           07900000
IHB&SQ.Z DC    A(X'7FFFFFFF')   CONSTANT TO TURN OFF BIT0 IN A REG      07950000
.COLLECT AIF   ('&LABEL' EQ '').GENLAB                                  08000000
&LABEL   DS    0H       USER SUPPLIED LABEL FOR CODE GENERATED BY MACRO 08050000
         AGO   .MISCLDS                                                 08100000
.GENLAB  ANOP                                                           08150000
&LABELX  DS    0H               LABEL FOR TARGET OF BRANCH AROUND      X08200000
                                  CONSTANT                              08250000
         AGO   .MISCLDS                                                 08300000
.NODCS   AIF   ('&LABEL' EQ '').MISCLDS                                 08350000
&LABEL   DS    0H       USER SUPPLIED LABEL FOR CODE GENERATED BY MACRO 08400000
.MISCLDS AIF   (T'&ECB EQ 'O' OR '&ECB' EQ '' OR '&ECB' EQ '0').ECB0    08450000
         AIF   ('&ECB' EQ '(0)').LACK                                   08500000
         AIF   ('&ECB'(1,1) EQ '(').REGECB                              08550000
         LA    0,&ECB           LOAD ECB ADDRESS                        08600000
         AGO   .LACK                                                    08650000
.REGECB  LR    0,&ECB           LOAD ECB ADDRESS INTO PARAMETER REG     08700000
         AGO   .LACK                                                    08750000
.ECB0    SLR   0,0              NO ECB --- FORCE REG 0 TO EQUAL ZERO    08800000
.LACK    AIF   (NOT &LFORMAT).LDA                                       08850000
         AIF   (NOT &LALA).REGLA                                        08900000
         LA    1,&LA            LOAD ADDRESS OF START OF PSL LIST       08950000
         AGO   .MSKLA                                                   09000000
.REGLA   AIF   ('&LA' EQ '(1)').MSKLA                                   09050000
         LR    1,&LA            LOAD POINTER TO START OF PSL LIST      X09100000
                                  INTO PARAMETER REG                    09150000
.MSKLA   O     1,IHB&SQ.Y       FORCE BIT0 TO ONE                       09200000
         AGO   .TCBEXAM                                                 09250000
.LDA     AIF   (NOT &LAA).REGA                                          09300000
         LA    1,&A             LOAD START ADDRESS                      09350000
         AGO   .EACK                                                    09400000
.REGA    AIF   ('&A' EQ '(1)').MSKA                                     09450000
         LR    1,&A             LOAD POINTER TO START OF VIRTUAL       X09500000
                                  ADDRESS RANGE INTO PARAMETER REG      09550000
.MSKA    N     1,IHB&SQ.Z       FORCE BIT0 TO ZERO                      09600000
.EACK    AIF   (NOT &DFLTDEA).LDEA                                      09650000
         LR    &EAREG,1         GENERATE DEFAULT END ADDRESS            09700000
         AGO   .TCBEXAM                                                 09750000
.LDEA    AIF   ('&EA' EQ '(&EAREG)').TCBEXAM                            09800000
         AIF   ('&EA'(1,1) EQ '(').REGEA                                09850000
         LA    &EAREG,&EA       LOAD END ADDRESS                        09900000
         AGO   .TCBEXAM                                                 09950000
.REGEA   LR    &EAREG,&EA       LOAD POINTER TO END ADDRESS OF VIRTUAL X10000000
                                  ADDRESS RANGE INTO PARM REG           10050000
.TCBEXAM AIF   ((NOT &BRANCHE) OR &IGNORET OR '&TCB' EQ '(4)').FUNCHK   10100000
         AIF   (NOT &ZEROTCB).LDTCB                                     10150000
         SLR   4,4              'TCB=0' SPECIFIED OR DEFAULTED.SET REG X10200000
                                  4 TO EQUAL ZERO.IF FUNCTION IS FIX ORX10250000
                                  FREE,NO FOE PROCESSING WILL BE DONE.  10300000
         AGO   .FUNCHK                                                  10350000
.LDTCB   AIF   ('&TCB'(1,1) EQ '(').REGTCB                              10400000
         LA    4,&TCB           LOAD TCB ADDRESS                        10450000
         AGO   .FUNCHK                                                  10500000
.REGTCB  LR    4,&TCB           LOAD TCB ADDRESS INTO PARAMETER REG     10550000
.FUNCHK  AIF   (&LFORMAT).INVOKEL                                       10600000
         AIF   (&BRANCHE).R3                                            10650000
&FREG    SETC  '14'             FOR SVC CALLER, R14 GETS FUNC/OPTIONS   10700000
         AGO   .GENFUNC                                                 10750000
.R3      ANOP                                                           10800000
&FREG    SETC  '3'              FOR BRANCH ENTRY, R3 GETS FUNC/OPTIONS  10850000
.GENFUNC LA    &FREG,&BITS.(0,0)   SET FUNCTION CODE AND OPTION FLAGS   10900000
         AGO   .GENLKG                                                  10950000
.INVOKEL MVI   X'A'(1),X'&FUNCODE' SET PSLFUNC PAGE SERVICE FUNCTION   X11000000
                                  REQUEST CODE                          11050000
         MVI   X'B'(1),B'0&YRELS.&YKPRL.&YANYW.&YLONG.&YBACK.00' SET   X11100000
                                  PSLFLAG1 OPTION FLAGS                 11150000
.GENLKG  AIF   (&BRANCHE).GENBR                                         11200000
         SVC   138              INVOKE PAGE SERVICE PROCESSOR SVC       11250000
         MEXIT                                                          11300000
.GENBR   L     15,16(0,0)       POINT TO CVT                       @01C 11350000
         L     15,1116(0,15)    EXTRACT PAGE SERVICES ENTRY POINT  @01C 11400000
         BASSM 14,15            CALL PAGE SERVICES                      11450000
         MEXIT                                                          11500000
.FAST    AIF   ('&FUNC' NE 'FIX' AND '&FUNC' NE 'FREE').BADSPEC         11550000
         AIF   ('&FUNC' EQ 'FREE' AND T'&BACKOUT NE 'O').XBACK          11600000
         AIF   (T'&BACKOUT EQ 'O').SKPBACK                              11650000
         AIF   ('&BACKOUT' NE 'N' AND '&BACKOUT' NE 'Y').BDBACK         11700000
.SKPBACK AIF   (T'&ASCB EQ 'O').SPASCB                                  11750000
         AIF   (T'&ECB NE 'O' AND '&ECB' NE '0').SPECB                  11800000
         AIF   (T'&TCB NE 'O' AND '&TCB' NE '0').SPTCB                  11850000
         AIF   (T'&LONG NE 'O' AND '&LONG' NE 'N').SPLONG               11900000
         AIF   (T'&KEEPREL NE 'O' AND '&KEEPREL' NE 'N').SPKPRL         11950000
         AIF   (T'&RELEASE NE 'O' AND '&RELEASE' NE 'N').SPRELS         12000000
         AIF   (T'&ANYWHER NE 'O' AND '&ANYWHER' NE 'N').SPANYW         12050000
&LABEL   DS    0H               BEGINING OF CODE GENERATED BY MACRO     12100000
         AIF   (&LFORMAT).SPECL                                         12150000
         AIF   (&LAA).SPECLAA                                           12200000
         AIF   ('&A' EQ '(1)').OKREGA                                   12250000
         LR    1,&A             LOAD PARAMETER REG WITH THE VIRTUAL    *12300000
                                   ADDRESS OF THE START OF THE RANGE    12350000
         AGO   .OKREGA                                                  12400000
.SPECLAA LA    1,&A             LOAD PARAMETER REG WITH THE VIRTUAL    *12450000
                                   ADDRESS OF THE START OF THE RANGE    12500000
.OKREGA  AIF   (NOT &DFLTDEA).SPECLEA                                   12550000
         LR    2,1              LOAD DEFAULT END ADDRESS                12600000
         AGO   .LDASCB                                                  12650000
.SPECLEA AIF   ('&EA' EQ '(2)').LDASCB                                  12700000
         AIF   ('&EA'(1,1) NE '(').SPECEA                               12750000
         LR    2,&EA            LOAD PARAMETER REG WITH THE VIRTUAL    *12800000
                                   ADDRESS OF THE END OF THE RANGE      12850000
         AGO   .LDASCB                                                  12900000
.SPECEA  LA    2,&EA            LOAD PARAMETER REG WITH THE VIRTUAL    *12950000
                                   ADDRESS OF THE END OF THE RANGE      13000000
         AGO   .LDASCB                                                  13050000
.SPECL   AIF   ('&LA' EQ '(1)').LDASCB                                  13100000
         AIF   (&LALA).SPECLAL                                          13150000
         LR    1,&LA            LOAD PARAMETER REG WITH THE VIRTUAL    *13200000
                                   ADDRESS OF THE LIST                  13250000
         AGO   .LDASCB                                                  13300000
.SPECLAL LA    1,&LA            LOAD PARAMETER REG WITH THE VIRTUAL    *13350000
                                   ADDRESS OF THE LIST                  13400000
.LDASCB  AIF   ('&ASCB' EQ '(3)').LDPVT                                 13450000
         AIF   ('&ASCB'(1,1) EQ '(').REGASCB                            13500000
         LA    3,&ASCB          LOAD PARAMETER REG WITH THE VIRTUAL    *13550000
                                   ADDRESS OF THE ASCB                  13600000
         AGO   .LDPVT                                                   13650000
.REGASCB LR    3,&ASCB          LOAD PARAMETER REG WITH THE VIRTUAL    *13700000
                                   ADDRESS OF THE ASCB                  13750000
.LDPVT   L     15,16(0,0)         FIND THE CVT                     @01C 13800000
         L     15,356(0,15)       FIND THE PVT                     @01C 13850000
         AIF   ('&FUNC' EQ 'FREE').SPFREE                               13900000
         AIF   (&LFORMAT).SPLFIX                                        13950000
         AIF   (T'&BACKOUT EQ 'O' OR '&BACKOUT' EQ 'N').SPRNBX          14000000
         L     15,PVTPQRB-PVT(0,15)   EXTRACT THE ENTRY POINT           14050000
         AGO   .BASSM                                                   14100000
.SPRNBX  L     15,PVTPQRNB-PVT(0,15)  EXTRACT THE ENTRY POINT           14150000
         AGO   .BASSM                                                   14200000
.SPLFIX  AIF   (T'&BACKOUT EQ 'O' OR '&BACKOUT' EQ 'N').SPLNBX          14250000
         L     15,PVTPQLB-PVT(0,15)   EXTRACT THE ENTRY POINT           14300000
         AGO   .BASSM                                                   14350000
.SPLNBX  L     15,PVTPQLNB-PVT(0,15)  EXTRACT THE ENTRY POINT           14400000
         AGO   .BASSM                                                   14450000
.SPFREE  AIF   (&LFORMAT).SPLFREE                                       14500000
         L     15,PVTPNR-PVT(0,15)    EXTRACT THE ENTRY POINT           14550000
         AGO   .BASSM                                                   14600000
.SPLFREE L     15,PVTPNL-PVT(0,15)    EXTRACT THE ENTRY POINT           14650000
.BASSM   BASSM 14,15             CALL FAST-PATH SERVICE                 14700000
         MEXIT                                                          14750000
.FRMOMIT IHBERMAC 1006,FIRST_POSITIONAL                                 14800000
         AGO   .LBL4ERR                                                 14850000
.BADFORM IHBERMAC 1001,FIRST_POSITIONAL,&FORMAT                         14900000
         AGO   .LBL4ERR                                                 14950000
.RLA     IHBERMAC 1020,''LA='',REGISTER_FORMAT_REQUEST                  15000000
         AGO   .LBL4ERR                                                 15050000
.AOMIT   IHBERMAC 1006,''A''                                            15100000
         AGO   .LBL4ERR                                                 15150000
.LA      IHBERMAC 1020,''A='',LIST_FORMAT_REQUEST                       15200000
         AGO   .LBL4ERR                                                 15250000
.LEA     IHBERMAC 1020,''EA='',LIST_FORMAT_REQUEST                      15300000
         AGO   .LBL4ERR                                                 15350000
.NOLA    IHBERMAC 1006,''LA''                                           15400000
         AGO   .LBL4ERR                                                 15450000
.NULLEA  IHBERMAC 1007,''EA''                                           15500000
         AGO   .LBL4ERR                                                 15550000
.BADBR   IHBERMAC 1007,BRANCH                                           15600000
         AGO   .LBL4ERR                                                 15650000
.TCBNO   IHBERMAC 1020,''TCB='',SVC_ENTRY                               15700000
         AGO   .LBL4ERR                                                 15750000
.NOFUNC  IHBERMAC 1006,SECOND_POSITIONAL                                15800000
         AGO   .LBL4ERR                                                 15850000
.NOIMP   IHBERMAC 1020,''ECB^=0'',''BACKOUT=N''                         15900000
         AGO   .LBL4ERR                                                 15950000
.BADFUNC IHBERMAC 1014,SECOND_POSITIONAL                                16000000
         AGO   .LBL4ERR                                                 16050000
.XASCB   IHBERMAC 1020,''ASCB'',REGULAR_FORM_OF_PGSER                   16100000
         AGO   .LBL4ERR                                                 16150000
.XKPRL   IHBERMAC 1020,''KEEPREL='',&FUNC                               16200000
         AGO   .LBL4ERR                                                 16250000
.XRELS   IHBERMAC 1020,''RELEASE='',&FUNC                               16300000
         AGO   .LBL4ERR                                                 16350000
.XLONG   IHBERMAC 1020,''LONG='',&FUNC                                  16400000
         AGO   .LBL4ERR                                                 16450000
.XANYW   IHBERMAC 1020,''ANYWHER='',&FUNC                               16500000
         AGO   .LBL4ERR                                                 16550000
.XECB    IHBERMAC 1020,''ECB='',&FUNC                                   16600000
         AGO   .LBL4ERR                                                 16650000
.XBACK   IHBERMAC 1020,''BACKOUT='',&FUNC                               16700000
         AGO   .LBL4ERR                                                 16750000
.BADSPEC IHBERMAC 1020,&FUNC,SPECIAL_BRANCH_ENTRY                       16800000
         AGO   .LBL4ERR                                                 16850000
.SPASCB  IHBERMAC 1006,''ASCB=''                                        16900000
         AGO   .LBL4ERR                                                 16950000
.SPECB   IHBERMAC 1020,''ECB^=0'',SPECIAL_BRANCH_ENTRY                  17000000
         AGO   .LBL4ERR                                                 17050000
.SPTCB   IHBERMAC 1020,''TCB^=0'',SPECIAL_BRANCH_ENTRY                  17100000
         AGO   .LBL4ERR                                                 17150000
.SPLONG  IHBERMAC 1020,''LONG^=N'',SPECIAL_BRANCH_ENTRY                 17200000
         AGO   .LBL4ERR                                                 17250000
.SPKPRL  IHBERMAC 1020,''KEEPREL^=N'',SPECIAL_BRANCH_ENTRY              17300000
         AGO   .LBL4ERR                                                 17350000
.SPRELS  IHBERMAC 1020,''RELEASE^=N'',SPECIAL_BRANCH_ENTRY              17400000
         AGO   .LBL4ERR                                                 17450000
.SPANYW  IHBERMAC 1020,''ANYWHER^=N'',SPECIAL_BRANCH_ENTRY              17500000
         AGO   .LBL4ERR                                                 17550000
.NOECB   IHBERMAC 1006,''ECB=''                                         17600000
         AGO   .LBL4ERR                                                 17650000
.BDKPRL  IHBERMAC 1020,&KEEPREL,''KEEPREL=''                            17700000
         AGO   .LBL4ERR                                                 17750000
.BDRELS  IHBERMAC 1020,&RELEASE,''RELEASE=''                            17800000
         AGO   .LBL4ERR                                                 17850000
.BDLONG  IHBERMAC 1020,&LONG,''LONG=''                                  17900000
         AGO   .LBL4ERR                                                 17950000
.BDANYW  IHBERMAC 1020,&ANYWHER,''ANYWHER=''                            18000000
         AGO   .LBL4ERR                                                 18050000
.BDBACK  IHBERMAC 1020,&BACKOUT,''BACKOUT=''                            18100000
.LBL4ERR AIF   ('&LABEL' EQ '').MEND                                    18150000
&LABEL   DS    0H    USER SPECIFIED LABEL FOR CODE GENERATED BY MACRO   18200000
.MEND    MEND                                                           18250000
* */                                                                    18300000
* PGSER: MACRO KEYS(LA,A,EA,ASCB,TCB,ECB,KEEPREL,RELEASE,LONG,ANYWHER,  18350000
*                   BACKOUT,BRANCH,CLEAR,RELATED,LIMIT);   /*    @L3C*/ 18400000
*      ANS('?'||MACLABEL||'PGSERP'||MACLIST||MACKEYS||';');             18450000
*%     END PGSER;                                                       18500000
