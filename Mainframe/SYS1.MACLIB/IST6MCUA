         MACRO                                                          00050000
         IST6MCUA &KWVAL=,&LOC=,&BASEREG=,&KW=,&LEN=               @L1C 00100000
.*                                                                      00150000
.* /***** START OF SPECIFICATIONS ************************************/ 00200000
.* /*                                                                */ 00250000
.* /*  MACRO NAME: IST6MCUA                                          */ 00300000
.* /*                                                                */ 00350000
.* /*  DESCRIPTIVE NAME: ADDRESS-DATA PARAMETER PROCESSOR            */ 00400000
.* /*                                                                */ 00450000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00500000
.* /*                                                                */ 00550000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00600000
.* /*                                                                */ 00650000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */ 00700000
.* /*             ALL RIGHTS RESERVED.                               */ 00750000
.* /*                                                                */ 00800000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00850000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 00900000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 00950000
.* /*                                                                */ 01000000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01050000
.* /*                                                                */ 01100000
.* /*  FUNCTION:                                                     */ 01150000
.* /*      CALLED BY OTHER MACROS TO STORE A PARAMETER VALUE         */ 01200000
.* /*      IN APPROPRIATE LOCATION  IN RPL OR APPC AREA. THE         */ 01250000
.* /*      PARAMETER VALUES MAY BE IN THE FORM OF A SYMBOLIC         */ 01300000
.* /*      NAME OR REGISTER NOTATION.                                */ 01350000
.* /*                                                                */ 01400000
.* /*  NOTES:                                                        */ 01450000
.* /*     KWVAL   -- CONTAIN THE PARAMETER VALUE                     */ 01500000
.* /*     LOC     -- CONTAIN THE LOCATION (OFFSET) IN THE RPL OR     */ 01550000
.* /*                APPC AREA FOR THIS PARAMETER                    */ 01600000
.* /*     BASEREG -- BASE REGISTER FOR RPL OR APPC AREA ADDRESSING   */ 01650000
.* /*     KW      -- CONTAINS A CHARACTER STRING DESCRIBING THE      */ 01668700
.* /*                KEYWORD, USED TO RECOGNIZE SPECIAL HANDLING     */ 01687400
.* /*                REQUIRED BY KEYWORD TYPES AND IN GENERATED      */ 01706100
.* /*                COMMENTS.  LENGTH KEYWORDS (SUCH AS AREALEN,    */ 01724800
.* /*                RECLEN, VTRINL) MUST HAVE A CHARACTER STRING    */ 01743500
.* /*                VALUE ENDING IN "LENGTH" TO BE CORRECTLY        */ 01762200
.* /*                HANDLED IF THEIR SPECIFIED VALUE > X'FFF'.) @Y1C*/ 01780900
.* /*     LEN     -- LENGTH OF FIELD TO BE SET (DEFAULT IS 4)        */ 01800000
.* /*                (VALID LENGTHS ARE 4 AND 2)                 @L1A*/ 01850000
.* /*                                                                */ 01900000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01950000
.* /*                                                                */ 02000000
.* /*  CREATED BY:                                                   */ 02050000
.* /*                                                                */ 02100000
.* /*  INTERNAL MACROS: NONE                                         */ 02150000
.* /*                                                                */ 02200000
.* /*  EXTERNAL REFERENCES: IST6MCER                                 */ 02250000
.* /*                                                                */ 02300000
.* /*  CHANGE ACTIVITY:                                              */ 02350000
.* /*                                                                */ 02400000
.* /*  $MAC(IST6MCUA),COMP(API),PROD(VTAM): ADDRESS-DATA PARAMETER   */ 02450000
.* /*                                       PROCESSOR                */ 02500000
.* /*                                                                */ 02550000
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */ 02600000
.* /*  ---- -------- ------- ------ ------  ----------------         */ 02650000
.* /*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION         */ 02700000
.* /*  $KF= S4N@@CLD HATAPPN 910629 692313: CP-CP Sessions           */ 02750000
.* /*  $L1= KA@DR006 APPCRR  940714 879315: Vector format            */ 02800000
.* /*  $Y1= P118546  FREEDOM 950405 879315: Handle length values     */ 02833300
.* /*                                       larger than X'FFF'       */ 02866600
.* /*                                                                */ 02900000
.* /***** END OF SPECIFICATIONS **************************************/ 02950000
.*                                                                      03000000
.* A-000000-999999                                             @R495810 03050000
.*                                                                      03100000
         GBLB  &ISTGEN                                                  03150000
         GBLB  &ISTUA                                                   03200000
         LCLA  &LN                                                 @L1A 03250000
         LCLA  &KWLN                                               @Y1A 03275000
         AIF   (&ISTUA EQ 1).UASTART                                    03300000
*                                                                       03350000
*  IST6MCUA - ACF/VTAM                                                  03400000
*                                                                       03450000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03500000
*                                                                       03550000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03600000
*                                                                       03650000
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.              03700000
*             ALL RIGHTS RESERVED.                                      03750000
*                                                                       03800000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03850000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03900000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03950000
*                                                                       04000000
*             SEE COPYRIGHT INSTRUCTIONS.                               04050000
*                                                                       04100000
&ISTUA   SETB  1                                                        04150000
.UASTART ANOP                                                           04200000
&KWLN    SETA  K'&KW                                                    04225000
&LN      SETA  4                       DEFAULT FIELD LENGTH        @L1A 04250000
         AIF   ('&LEN' EQ '').UAKW     CHECK FOR LENGTH PASSED     @L1A 04300000
&LN      SETA  &LEN                    USE SPECIFIED FIELD LENGTH  @L1A 04350000
         AIF   ((&LN EQ 2) OR (&LN EQ 4)).UAKW       VALID LENGTHS @L1A 04400000
         IST6MCER 5,LEN,&LEN           ERROR IN INVOKING MACRO     @L1A 04450000
         MEXIT                                                     @L1A 04500000
.UAKW    AIF   ('&KWVAL'(1,1) NE '(').CKKW   BR IF NON REG FORM         04550000
         AIF   (NOT &ISTGEN).UAEND                                      04600000
         AIF   (&LN GT 2).STREG                                    @L1A 04650000
         STCM  &KWVAL(1),3,&LOC.(&BASEREG)                              04700000
         MNOTE '*                                     STORE 2-BYTE &KW' 04750000
         MEXIT                                                     @L1A 04800000
.STREG   ST    &KWVAL(1),&LOC.(&BASEREG)                                04850000
         MNOTE '*                                           STORE &KW'  04900000
         MEXIT                                                          04950000
.CKKW    ANOP                                                           05000000
.*   CHECK FOR KEYWORDS WHOSE VALUES REPRESENT A LENGTH, NOT AN ADDRESS 05010000
         AIF   ((&KWLN GE 6) AND ('&KW'(&KWLN-5,6) EQ 'LENGTH')).LNFLD X05020000
                                                                   @Y1A 05030000
.*   CHECK FOR KEYWORDS HAVING SPECIAL HANDLING                         05040000
         AIF   ('&KW'(1,6) NE 'CONVID').CKKW1                           05050000
         AIF   (NOT &ISTGEN).UAEND                                      05100000
         MVC   &LOC.(4,&BASEREG),&KWVAL                                 05150000
         MNOTE '*                                           STORE &KW'  05200000
         MEXIT                                                          05250000
.CKKW1   ANOP                                                           05300000
         AIF   ('&KW'(1,4) NE 'CGID').CKKW2                        @KFA 05350000
         AIF   (NOT &ISTGEN).UAEND                                      05400000
         MVC   &LOC.(4,&BASEREG),&KWVAL                                 05450000
         MNOTE '*                                           STORE &KW'  05500000
         MEXIT                                                          05550000
.CKKW2   ANOP                                                           05600000
         AIF   ('&KW'(1,3) NE 'CID').CKKW3                              05650000
         IST6MCER 8,ARG                                                 05700000
         MEXIT                                                          05750000
.CKKW3   ANOP                                                           05800000
.*   GENERAL ADDRESS-TYPE FIELD                                         05825000
         AIF   (NOT &ISTGEN).UAEND                                      05850000
         LA    15,&KWVAL            SET VALUE IN REGISTER               05858300
         AGO   .STVAL                                              @Y1A 05866600
.LNFLD   ANOP                                                      @Y1A 05874900
         AIF   (NOT &ISTGEN).UAEND                                 @Y1A 05883200
         CNOP  0,4                  FORCE WORD CONSTANT ALIGNMENT  @Y1A 05891500
         AIF   (&LN GT 2).LDL4                                     @Y1A 05899800
         B     *+8                  BRANCH AROUND VALUE CONSTANT   @Y1A 05908100
         DC    AL4(&KWVAL*65536)    HALFWORD VALUE IN HI-ORDER PART OF +05916400
                 WORD CONSTANT (DONE TO FORCE RANGE-CHECK OF SPECIFIED +05924700
                 VALUE), OVERFLOW ERROR WHEN VALUE EXCEEDS 32767.  @Y1A 05933000
         ICM   15,3,*-4             GET 2-BYTE VALUE IN REGISTER   @Y1A 05941300
         AGO   .STVAL                                              @Y1A 05949600
.LDL4    ANOP                                                      @Y1A 05957900
         B     *+8                  BRANCH AROUND VALUE CONSTANT   @Y1A 05966200
         DC    AL4(&KWVAL)          WORD CONTAINING VALUE          @Y1A 05974500
         L     15,*-4               GET VALUE IN REGISTER          @Y1A 05982800
.STVAL   AIF   (&LN GT 2).STL4                                     @Y1C 05991100
         STCM  15,3,&LOC.(&BASEREG)                                     06000000
         MNOTE '*                                     STORE 2-BYTE &KW' 06050000
         MEXIT                                                     @L1A 06100000
.STL4    ST    15,&LOC.(&BASEREG)                                  @Y1C 06150000
         MNOTE '*                                            STORE &KW' 06200000
.UAEND   MEND                                                           06300000
