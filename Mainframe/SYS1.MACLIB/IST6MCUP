         MACRO                                                          00050000
         IST6MCUP &CONMODE=,&OPTCD=,&ECB=,&EXIT=,&BRANCH=,&CKCMODE=,   X00100000
               &CKSYN=,&CKBUFF=,&CONXMOD=                          @I1C 00150000
.*                                                                      00200000
.* /***** START OF SPECIFICATIONS ************************************/ 00250000
.* /*                                                                */ 00300000
.* /*  MACRO NAME: IST6MCUP                                          */ 00350000
.* /*                                                                */ 00400000
.* /*  DESCRIPTIVE NAME: COMMON PARAMETER PROCESSOR                  */ 00450000
.* /*                                                                */ 00500000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00550000
.* /*                                                                */ 00600000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00650000
.* /*                                                                */ 00700000
.* /*             5655-127 (C) COPYRIGHT IBM CORP. 1987, 1996.       */ 00750000
.* /*             ALL RIGHTS RESERVED.                               */ 00800000
.* /*                                                                */ 00850000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00900000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 00950000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01000000
.* /*                                                                */ 01050000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01100000
.* /*                                                                */ 01150000
.* /*                                                                */ 01200000
.* /*  FUNCTION:                                                     */ 01250000
.* /*      GENERATES IN-LINE CODE FOR PARAMETERS WHICH ARE COMMONLY  */ 01300000
.* /*      USED IN 'CONTROL=' INNER MACROS. THESE PARAMETERS ARE     */ 01350000
.* /*      OPTCD, CONMODE, CONXMOD, ECB, EXIT AND BRANCH.            */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*     CKCMODE -- YES/NO                                          */ 01550000
.* /*        TO INDICATE WHETHER TO CHECK CONMODE & CONXMOD KEYWORDS */ 01600000
.* /*     CKSYN -- YES/NO                                            */ 01650000
.* /*        TO INDICATE WHETHER SYN/ASY IS VALID OPTCD VALUE        */ 01700000
.* /*        OR NOT, ALSO CONTROLS KEEPSRB/NKEEPSRB OPTCD CHECKS @U2C*/ 01750000
.* /*     CKBUFF -- XBUF/OBUF/BBUF/YES/NO                            */ 01761700
.* /*        TO INDICATE WHETHER XBUFLST/BUFFLST/NBUFFLST ARE        */ 01773400
.* /*        VALID OPTCD VALUES AS SHOWN BELOW:                      */ 01785100
.* /*                                                                */ 01796800
.* /*               OPTCD=BUFFLST  XBUFLST  NBUFFLST                 */ 01808500
.* /*                    +--------+--------+--------+                */ 01820200
.* /*        CKBUFF=XBUF | VALID  | VALID  | VALID  |                */ 01831900
.* /*                    +--------+--------+--------+                */ 01843600
.* /*               OBUF | ERROR  | VALID  | VALID  |                */ 01855300
.* /*                    +--------+--------+--------+                */ 01867000
.* /*               BBUF | VALID  | VALID  | ERROR  |                */ 01878700
.* /*                    +--------+--------+--------+                */ 01890400
.* /*               YES  | VALID  | ERROR  | VALID  |                */ 01902100
.* /*                    +--------+--------+--------+                */ 01913800
.* /*               NO   | ERROR  | ERROR  | ERROR  |                */ 01925500
.* /*                    +--------+--------+--------+                */ 01937200
.* /*                                                                */ 01950000
.* /*  PROCESSOR: ASSEMBLER                                          */ 02000000
.* /*                                                                */ 02050000
.* /*  CREATED BY:                                                   */ 02100000
.* /*                                                                */ 02150000
.* /*  INTERNAL MACROS: IST6MCER                                     */ 02200000
.* /*                                                                */ 02250000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 02300000
.* /*                                                                */ 02350000
.* /*  CHANGE ACTIVITY:                                              */ 02400000
.* /*                                                                */ 02450000
.* /* $MAC(IST6MCUP),COMP(API),PROD(VTAM): COMMON PARAMTER PROCESSOR */ 02500000
.* /*                                                                */ 02550000
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */ 02600000
.* /* ---- -------- ------- ------ ------  ----------------          */ 02650000
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */ 02700000
.* /* $I1= FF@@@MST APPCFDX 911118 879315: APPC Full-Duplex support  */ 02750000
.* /* $U2= P109285  KEEPFRR 931104 879315: Implement FZKDR001, allow */ 02800000
.* /*                                      OPTCD=KEEPSRB/NKEEPSRB    */ 02850000
.* /* $D1= ID@@@MST FPAPI   941005 510689: Fast Path API for APPC    */ 02875000
.* /* $Y1= P116831  FREEDOM 941026 879315: Fix ACTR error message    */ 02900000
.* /* $D2= IA@@@MST V51APPC 941213 226653: APPC ENHANCEMENTS         */ 02925000
.* /* $S1= P119840  FPAPI   950921 879315: Skip setting after error  */ 02937500
.* /*                                                                */ 02950000
.* /***** END OF SPECIFICATIONS **************************************/ 03000000
.*                                                                      03050000
.* A-000000-999999                                             @R495810 03100000
.*                                                                      03150000
         GBLB  &ISTGEN                                                  03200000
         GBLB  &ISTUP                                                   03250000
         LCLA  &NUM                                                     03300000
         LCLB  &SYN,&ASY,&BUF,&NBUF,&XBUF                               03350000
         LCLB  &KPSRB,&NKPSRB                                      @U2A 03400000
&SYN     SETB  0                                                        03450000
&ASY     SETB  0                                                        03500000
&BUF     SETB  0                                                        03550000
&NBUF    SETB  0                                                        03600000
&XBUF    SETB  0                                                   @D1A 03625000
&KPSRB   SETB  0                                                   @U2A 03650000
&NKPSRB  SETB  0                                                   @U2A 03700000
         AIF   (&ISTUP EQ 1).UPSTART                                    03750000
*                                                                       03800000
*  IST6MCUP - ACF/VTAM                                                  03850000
*                                                                       03900000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03950000
*                                                                       04000000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       04050000
*                                                                       04100000
*             5655-127 (C) COPYRIGHT IBM CORP. 1987, 1996.              04150000
*             ALL RIGHTS RESERVED.                                      04200000
*                                                                       04250000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 04300000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              04350000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  04400000
*                                                                       04450000
*             SEE COPYRIGHT INSTRUCTIONS.                               04500000
*                                                                       04550000
&ISTUP   SETB  1                                                        04600000
.UPSTART ANOP                                                           04650000
         AIF   ('&CKCMODE' NE 'YES').OPT  BR IF NO CONMODE/CONXMOD @I1C 04700000
         AIF   ('&CONXMOD' EQ '').CONM                             @I1A 04750000
         AIF   ('&CONXMOD' NE 'CA').CONX1                          @I1A 04800000
         AIF   (NOT &ISTGEN).CONM                                  @I1A 04850000
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6CXBT                @I1A 04900000
*                                      TURN OFF CONXMOD BITS       @I1A 04950000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6CACX   SET CONXMOD CA     @I1A 05000000
         AGO   .CONM                                               @I1A 05050000
.CONX1   ANOP                                                      @I1A 05100000
         AIF   ('&CONXMOD' NE 'CS').CONX2                          @I1A 05150000
         AIF   (NOT &ISTGEN).CONM                                  @I1A 05200000
         NI    RPL6FLG2-ISTRPL6X(14),X'FF'-RPL6CXBT                @I1A 05250000
*                                      TURN OFF CONXMOD BITS       @I1A 05300000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6CSCX   SET CONXMOD CS     @I1A 05350000
         AGO   .CONM                                               @I1A 05400000
.CONX2   ANOP                                                      @I1A 05450000
         AIF   ('&CONXMOD' NE 'SAME').CONX3                        @I1A 05500000
         AIF   (NOT &ISTGEN).CONM                                  @I1A 05550000
         OI    RPL6FLG2-ISTRPL6X(14),RPL6SAMX   SET CONXMOD SAME   @I1A 05600000
         AGO   .CONM                                               @I1A 05650000
.CONX3   ANOP                                                      @I1A 05700000
         IST6MCER 5,CONXMOD,&CONXMOD                               @I1A 05750000
.CONM    ANOP                                                      @I1A 05800000
         AIF   ('&CONMODE' EQ '').OPT                                   05850000
         AIF   ('&CONMODE' NE 'LLCA').CONM1                             05900000
         AIF   (NOT &ISTGEN).OPT                                        05950000
         NI    RPL6FLG3-ISTRPL6X(14),X'FF'-RPL6CBIT                     06000000
*                                                TURN OFF CONMODE BITS  06050000
         OI    RPL6FLG3-ISTRPL6X(14),RPL6LLCA    SET CONMODE TO LLCA    06100000
         AGO   .OPT                                                     06150000
.CONM1   ANOP                                                           06200000
         AIF   ('&CONMODE' NE 'BUFFCA').CONM2                           06250000
         AIF   (NOT &ISTGEN).OPT                                        06300000
         NI    RPL6FLG3-ISTRPL6X(14),X'FF'-RPL6CBIT                     06350000
*                                                TURN OFF CONMODE BITS  06400000
         OI    RPL6FLG3-ISTRPL6X(14),RPL6BFCA    SET CONMODE TO BUFFCA  06450000
         AGO   .OPT                                                     06500000
.CONM2   ANOP                                                           06550000
         AIF   ('&CONMODE' NE 'CS').CONM3                               06600000
         AIF   (NOT &ISTGEN).OPT                                        06650000
         NI    RPL6FLG3-ISTRPL6X(14),X'FF'-RPL6CBIT                     06700000
*                                                TURN OFF CONMODE BITS  06750000
         OI    RPL6FLG3-ISTRPL6X(14),RPL6CS      SET CONMODE TO CS      06800000
         AGO   .OPT                                                     06850000
.CONM3   ANOP                                                           06900000
         AIF   ('&CONMODE' NE 'SAME').CONM4                             06950000
         AIF   (NOT &ISTGEN).OPT                                        07000000
         OI    RPL6FLG3-ISTRPL6X(14),RPL6SAME    SET CONMODE TO SAME    07050000
         AGO   .OPT                                                     07100000
.CONM4   ANOP                                                           07150000
         IST6MCER 5,CONMODE,&CONMODE                                    07200000
.*                                                                      07216600
.***** PROCESS ALL OPTCD VALUES SPECIFIED ON RPL *****************      07233200
.OPT     ANOP                                                           07250000
         AIF   ('&OPTCD' EQ '').ECBEXIT   SKIP IF OPTCD NOT SPECIFIED   07272700
&NUM     SETA  N'&OPTCD            SET NUM TO NUMBER OF OPTCD= VALUES   07295400
.*                                                                      07318100
.OPTSCAN ANOP                  LOOP THROUGH OPTCD= SUBLIST ENTRIES @D1A 07340800
.*                                                                      07363500
.***** CHECK FOR OPTCD VALUES BUFFLST|NBUFFLST|XBUFLST **********       07386200
         AIF   ('&CKBUFF' EQ 'NO').BUFSKIP                         @D2A 07408900
         AIF   ('&OPTCD(&NUM)' NE 'XBUFLST').OPT1                  @D1A 07431600
&XBUF    SETB  1                                                   @D1A 07454300
         AGO   .NEXT                                               @D1A 07477000
.OPT1    ANOP                                                           07500000
         AIF   ('&OPTCD(&NUM)' NE 'BUFFLST').OPT2                       07550000
&BUF     SETB  1                                                        07600000
         AGO   .NEXT                                                    07650000
.OPT2    ANOP                                                           07700000
         AIF   ('&OPTCD(&NUM)' NE 'NBUFFLST').OPT3                      07750000
&NBUF    SETB  1                                                        07800000
         AGO   .NEXT                                                    07850000
.OPT3    ANOP                                                           07900000
.BUFSKIP ANOP                       END OF BUFFERLIST VALUES       @D2A 07910000
.*                                                                      07920000
.***** CHECK FOR OPTCD VALUES SYN|ASY ****************************      07930000
         AIF   ('&CKSYN' EQ 'NO').SYNSKIP                          @D2A 07940000
         AIF   ('&OPTCD(&NUM)' NE 'SYN').OPT4                           07950000
&SYN     SETB  1                                                        08000000
         AGO   .NEXT                                                    08050000
.OPT4    ANOP                                                           08100000
         AIF   ('&OPTCD(&NUM)' NE 'ASY').OPT5                           08150000
&ASY     SETB  1                                                        08200000
         AGO   .NEXT                                                    08250000
.OPT5    ANOP                                                           08300000
.SYNSKIP ANOP                       END OF SYN|ASYN VALUES         @D2A 08312500
.*                                                                      08325000
.***** CHECK FOR OPTCD VALUES KEEPSRB|NKEEPSRB *******************      08337500
         AIF   ('&OPTCD(&NUM)' NE 'KEEPSRB').OPT6                       08350000
&KPSRB   SETB  1                                                        08400000
         AGO   .NEXT                                                    08450000
.OPT6    ANOP                                                           08500000
         AIF   ('&OPTCD(&NUM)' NE 'NKEEPSRB').OPT7                 @Y1C 08550000
&NKPSRB  SETB  1                                                        08600000
         AGO   .NEXT                                                    08650000
.OPT7    ANOP                                                           08700000
.*                                                                      08720000
.***** SPECIFIED OPTCD VALUE IS NOT SUPPORTED FOR THIS MACRO *****      08740000
         IST6MCER 5,OPTCD,&OPTCD(&NUM)   ISSUE VALUE NOT VALID MESSAGE  08760000
.*                                                                      08780000
.NEXT    ANOP                                                           08800000
&NUM     SETA  &NUM-1               DECREMENT SUBLIST ENTRY NUMBER      08822200
         AIF   (&NUM NE 0).OPTSCAN   CHECK ANOTHER ENTRY IF ANY    @D2C 08844400
.*                                                                      08866600
.***** CHECK IF MUTUALLY EXCLUSIVE OPTCD VALUES WERE SPECIFIED ***      08888800
.*     ISSUE ERROR MESSAGES IF NEEDED, SET RPL INDICATORS IF OK         08911000
.*                                                                      08933200
.BUFCHK  ANOP                                                      @D2A 08955400
         AIF   (NOT(&XBUF OR &BUF OR &NBUF)).SYNCHK  NO VALUE SET  @D2A 08977600
         AIF   (NOT(&XBUF AND &NBUF)).BUFCHK1                      @D2C 08999800
         IST6MCER 4,OPTCD,XBUFLST,NBUFFLST  WRITE CONFLICT MESSAGE @D1A 09022000
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09033100
.BUFCHK1 ANOP                                                      @D2C 09044200
         AIF   (NOT(&XBUF AND &BUF)).BUFCHK2                       @D2C 09066400
         IST6MCER 4,OPTCD,XBUFLST,BUFFLST   WRITE CONFLICT MESSAGE @D1A 09088600
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09099700
.BUFCHK2 ANOP                                                      @D2C 09110800
         AIF   (NOT(&BUF AND &NBUF)).BUFCHK3                       @D2C 09133000
         IST6MCER 4,OPTCD,BUFFLST,NBUFFLST  WRITE CONFLICT MESSAGE @D2C 09155200
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09166300
.BUFCHK3 ANOP                                                           09177400
         AIF   (NOT(('&CKBUFF' EQ 'YES') AND &XBUF)).BUFCHK4       @D2C 09199600
         IST6MCER 5,OPTCD,XBUFLST          VALUE NOT VALID MESSAGE @D2A 09221800
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09232900
.BUFCHK4 ANOP                                                      @D2C 09244000
         AIF   (NOT(('&CKBUFF' EQ 'BBUF') AND &NBUF)).BUFCHK5      @D2A 09266200
         IST6MCER 5,OPTCD,NBUFFLST         VALUE NOT VALID MESSAGE      09288400
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09299500
.BUFCHK5 ANOP                                                      @D2C 09310600
         AIF   (NOT(('&CKBUFF' EQ 'OBUF') AND &BUF)).BUFCHK6       @D2A 09332800
         IST6MCER 5,OPTCD,BUFFLST          VALUE NOT VALID MESSAGE @D2A 09355000
         AGO   .SYNCHK                    SKIP TO NEXT OPTCD CHECK @S1A 09366100
.BUFCHK6 ANOP                                                      @D2C 09377200
         AIF   (NOT &ISTGEN).SYNCHK   SKIP SETTING IF ANY ERRORS   @D2C 09399400
.BUFSET  ANOP                                                      @D2A 09421600
         AIF   (NOT &XBUF).BUFSET1                                 @D2C 09443800
         OI    RPLOPT6-IFGRPL(1),X'04'       SET TO XBUFLST        @D1A 09466000
*                                                                  @D1A 09488200
         NI    RPLOPT6-IFGRPL(1),X'FD'       TURN OFF BUFFLST      @D1A 09510400
         NI    RPLEXTDS-IFGRPL(1),X'FE'      TURN OFF XSRV         @D1A 09532600
.BUFSET1 ANOP                                                      @D2C 09554800
         AIF   (NOT &BUF).BUFSET2                                  @D2C 09577000
         OI    RPLOPT6-IFGRPL(1),X'02'       SET TO BUFFLST             09600000
*                                                                       09650000
         NI    RPLOPT6-IFGRPL(1),X'FB'       TURN OFF XBUFLST      @D1A 09690600
         NI    RPLEXTDS-IFGRPL(1),X'FE'      TURN OFF XSRV         @D1A 09731200
.BUFSET2 ANOP                                                      @D2C 09771800
         AIF   (NOT &NBUF).BUFSET3                                 @D2C 09812400
         NI    RPLOPT6-IFGRPL(1),X'F9'       TURN OFF BUFFLST      @D1A 09853000
*                                            ... AND XBUFLST       @D1A 09893600
         NI    RPLEXTDS-IFGRPL(1),X'FE'      TURN OFF XSRV         @D1A 09934200
.BUFSET3 ANOP                                                      @D2C 09974800
.SYNCHK  ANOP                                                      @D2C 10015400
         AIF   (NOT(&SYN AND &ASY)).SYNCHK0                        @D2C 10056000
         IST6MCER 4,OPTCD,SYN,ASY           WRITE CONFLICT MESSAGE      10096600
         AGO   .SRBCHK                    SKIP TO NEXT OPTCD CHECK @S1A 10116900
.SYNCHK0 ANOP                                                           10137200
         AIF   (NOT &ISTGEN).SRBCHK     SKIP SETTING IF ANY ERRORS @D2C 10177800
.SYNSET  ANOP                                                      @D2A 10218400
         AIF   (NOT &SYN).SYNSET1                                  @D2C 10259000
         NI    RPLOPT1-IFGRPL(1),X'F7'       TURN OFF ASY BIT           10300000
*                                                                       10350000
.SYNSET1 ANOP                                                           10400000
         AIF   (NOT &ASY).SYNSET2                                  @D2C 10450000
         OI    RPLOPT1-IFGRPL(1),X'08'       SET TO ASY                 10550000
*                                                                       10600000
.SYNSET2 ANOP                                                      @D2A 10644400
.SRBCHK  ANOP                                                      @D2A 10688800
         AIF   (NOT(&KPSRB AND &NKPSRB)).SRBCHK1                   @D2A 10733200
         IST6MCER 4,OPTCD,KEEPSRB,NKEEPSRB  WRITE CONFLICT MESSAGE @U2A 10777600
         AGO   .ECBEXIT                 SKIP TO NEXT KEYWORD CHECK @S1A 10799800
.SRBCHK1 ANOP                                                      @U2A 10822000
         AIF   (NOT &ISTGEN).ECBEXIT   SKIP SETTING IF ANY ERRORS  @U2A 10866400
.SRBSET  ANOP                                                      @D2A 10910800
         AIF   (NOT &KPSRB).SRBSET1                                @D2C 10955200
         OI    RPLOPT11-IFGRPL(1),X'01'       SET TO KEEPSRB       @U2A 11000000
*                                                                       11050000
.SRBSET1 ANOP                                                      @U2A 11100000
         AIF   (NOT &NKPSRB).SRBSET2                               @D2C 11150000
         NI    RPLOPT11-IFGRPL(1),X'FE'       TURN OFF KEEPSRB BIT @U2A 11250000
*                                                                       11300000
.SRBSET2 ANOP                                                      @D2A 11325000
.ECBEXIT ANOP                                                           11350000
         AIF   ('&ECB' EQ '' AND '&EXIT' EQ '').BR  NEITHER SPECIFIED?  11400000
         AIF   ('&ECB' NE '' AND '&EXIT' NE '').ECBMSG  BOTH SPECIFIED? 11450000
         AIF   (NOT &ISTGEN).BR                                         11500000
         AIF   ('&ECB' NE '').ECBGEN         BR IF ECB SPECIFIED        11550000
         AIF   ('&EXIT'(1,1) EQ '(').EXIT1   BR IF REG FORM             11600000
         LA    15,&EXIT                      GET EXIT ROUTINE ADDRESS   11650000
         ST    15,RPLECB-IFGRPL(1)           STORE IN RPL               11700000
         AGO   .FLGSETA                                                 11750000
.EXIT1   ANOP                                                           11800000
         ST    &EXIT(1),RPLECB-IFGRPL(1)     STORE EXIT ROUTINE ADDR    11850000
.FLGSETA ANOP                                                   IN RPL  11900000
         NI    RPLOPT1-IFGRPL(1),X'FE'       SET ECB FLAG OFF           11950000
         NI    RPLEXTDS-IFGRPL(1),X'BF'      SET NOEXIT BIT OFF         12000000
         OI    RPLEXTDS-IFGRPL(1),X'20'      SET EXIT BIT ON            12050000
*                                                                       12100000
         AGO   .BR                                                      12150000
.ECBGEN  ANOP                                                           12200000
         AIF   ('&ECB' EQ 'INTERNAL').FLGSETC  BR IF INTERNAL SPECIFIED 12250000
         AIF   ('&ECB'(1,1) EQ '(').ECBGEN1  BR IF REG FORM             12300000
         LA    15,&ECB                       GET ECB ADDRESS            12350000
         ST    15,RPLECB-IFGRPL(1)           STORE IN RPL               12400000
         AGO   .FLGSETB                                                 12450000
.ECBGEN1 ANOP                                                           12500000
         ST    &ECB(1),RPLECB-IFGRPL(1)      STORE ECB ADDRESS IN RPL   12550000
.FLGSETB ANOP                                                           12600000
         OI    RPLOPT1-IFGRPL(1),X'01'       SET ECB FLAG ON            12650000
         OI    RPLEXTDS-IFGRPL(1),X'40'      SET NOEXIT BIT ON          12700000
         NI    RPLEXTDS-IFGRPL(1),X'DF'      SET EXIT BIT OFF           12750000
*                                                                       12800000
         AGO   .BR                                                      12850000
.ECBMSG  ANOP                                                           12900000
         IST6MCER 9                                                     12950000
         AGO   .BR                      SKIP TO NEXT KEYWORD CHECK @S1A 12975000
.FLGSETC ANOP                                                           13000000
         NI    RPLOPT1-IFGRPL(1),X'FE'       SET ECB FLAG OFF           13050000
         OI    RPLEXTDS-IFGRPL(1),X'40'      SET NOEXIT BIT ON          13100000
         NI    RPLEXTDS-IFGRPL(1),X'DF'      SET EXIT BIT OFF           13150000
*                                                                       13200000
.BR      ANOP                                                           13250000
         AIF   ('&BRANCH' EQ '').BREND                                  13300000
         AIF   ('&BRANCH' NE 'YES').BRNO                                13350000
         AIF   (NOT &ISTGEN).BREND                                      13400000
         OI    RPLEXTDS-IFGRPL(1),X'02'      SET BRANCH ENTRY BIT ON    13450000
*                                                                       13500000
         MEXIT                                                          13550000
.BRNO    ANOP                                                           13600000
         AIF   ('&BRANCH' NE 'NO').BRERROR                              13650000
         AIF   (NOT &ISTGEN).BREND                                      13700000
         NI    RPLEXTDS-IFGRPL(1),X'FD'      SET BRANCH ENTRY BIT OFF   13750000
*                                                                       13800000
         MEXIT                                                          13850000
.BRERROR ANOP                                                           13900000
         IST6MCER 5,BRANCH,&BRANCH                                      13950000
.BREND   MEND                                                           14000000
