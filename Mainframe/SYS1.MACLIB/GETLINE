         MACRO                                                                  
&NAME    GETLINE &PARM=,&UPT=,&ECT=,&ECB=,&INPUT=,&TERMGET=,&ENTRY=,   *        
               &SUBSTACK=,&MF=                                @PBY0239          
.**********************************************************************         
.*                                                                    *         
.*COPYRIGHT:                                                          *         
.*        5685-025 COPYRIGHT IBM CORP 1986,                           *         
.*        THIS PRODUCT CONTAINS RESTRICTED MATERIALS OF IBM,          *         
.*        REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083       *         
.*                                                                    *         
.*STATUS:                                                             *         
.*                                                                    *         
.*        JBB2367  TSO/E                                              *         
.*                                                                    *         
.*FUNCTION:                                                           *         
.*   THE GETLINE MACRO EXAMINES PARAMETERS SPECIFIED IN THE GETLINE   *         
.*   MACRO INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE *         
.*   GETLINE SERVICE ROUTINE (REFER TO SECTION 3.48.9.1 FOR THE       *         
.*   PARAMETER BLOCK FORMAT).                                         *         
.*   IN THE LIST FORM, THE GETLINE MACRO GENERATES THE GETLINE        *         
.*   PARAMETER BLOCK CONSISTING OF ONE WORD OF CONTROL (CONTROL,      *         
.*   TINPUT, AND SUBSTACK OPTIONS) AND THE ADDRESS OF THE INPUT       *         
.*   BUFFER.                                                  @PBY0239*         
.*   IN THE EXECUTE FORM OF THE GETLINE MACRO, CODE IS GENERATED      *         
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *         
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *         
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *         
.*   BE GENERATED TO INVOKE THE GETLINE SERVICE ROUTINE UNLESS AN     *         
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    *         
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              *         
.*                                                                    *         
.*ENTRY POINTS:                                                       *         
.*   GETLINE MACRO CALL - ONLY ENTRY POINT.                           *         
.*                                                                    *         
.*INPUT:                                                              *         
.*   PARAMETERS SPECIFIED IN THE GETLINE MACRO FORMAT (FOR FORMAT     *         
.*   SPECIFICATIONS REFER TO SECTION 3.48.9.1).                       *         
.*                                                                    *         
.*OUTPUT:                                                             *         
.*   IN THE L-FORM, A 2-WORD PARAMETER BLOCK (REFER TO SECTION        *         
.*   3.48.9.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO AND    *         
.*   ADDRESS OF THE INPUT BUFFER.                                     *         
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *         
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *         
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *         
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *         
.*                                                                    *         
.*EXTERNAL REFERENCES:                                                *         
.*   LINK MACRO.                                                      *         
.*                                                                    *         
.*EXITS,NORMAL:                                                       *         
.*   IN THE L-FORM, RETURN TO CALLER.                                 *         
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE GETLINE SERVICE   *         
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  *         
.*   SECTION 3.48.1.5).                                               *         
.*                                                                    *         
.*EXITS,ERROR:                                                        *         
.*   INVALID INPUT.                                                   *         
.*                                                                    *         
.*TABLES/WORK AREAS:                                                  *         
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *         
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *         
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *         
.*   TO THE SPECIFIED MACRO PARAMETERS AND THE ADDRESS OF THE INPUT   *         
.*   BUFFER.                                                          *         
.*                                                                    *         
.*ATTRIBUTES:                                                         *         
.*   SERIALLY REUSABLE.                                               *         
.*                                                                    *         
.*NOTES:                                                              *         
.*   DATE OF CURRENT RELEASE :                                        *         
.*      09/01/86                                              @PBY0239*         
.*   PREVIOUS RELEASE DATES :                                         *         
.*      01/22/70                                              @PBY0239*         
.*      12/12/69                                                      *         
.*      10/22/69                                                      *         
.*                                                                    *         
.*CHANGE ACTIVITY:                                                    *         
.*  @PBY0239   TSO/E RELEASE 3 SUPPORT FOR RUNNING CLIST THROUGH THE  *         
.*             TSO SERVICE FACILITY. THESE CHANGES SUPPORT THE NEW    *         
.*             SUBSTACK OPTION.                                       *         
.*             THE INPUT AND TERMGET OPTION SECTIONS OF THE EXECUTE   *         
.*             FORM WERE REWRITTEN TO MAKE THE MACRO MORE READABLE    *         
.*             AND THE SUBSTACK OPTION WAS ADDED TO THE EXECUTE FORM. *         
.*             THE LIST FORM WAS COMPLETELY REWRITTEN TO MAKE IT MORE *         
.*             READABLE AND TO ADD THE SUBSTACK OPTION.               *         
.*                                                                    *         
.*             SEE THE GETLINE SERVICE ROUTINE FOR FURTHER DESCRIPTION*         
.*             OF THIS NEW OPTION.                            @PBY0239*         
.*                                                                    *         
.**********************************************************************         
         LCLA  &COUNT                    LOOP COUNTER         @PBY0239          
         LCLB  &B1,&B2,&CHECK1,&CHECK2,&NOLOAD,&REGLOAD       @PBY0239          
.*                                                                              
         AIF   (T'&MF EQ 'O').ERR1       NO MF PARAMETER                        
         AIF   (N'&SYSLIST EQ 0).MF1     POSITIONAL PARAMETERS INVALID          
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR7                   
.MF1     AIF   ('&MF' EQ 'L').LSTFORM    LIST FORM            @PBY0239          
.**********************************************************************         
.*                                                                    *         
.*                         EXECUTE FORM                               *         
.*                                                                    *         
.**********************************************************************         
         AIF   ('&MF(1)' NE 'E').ERR1                                           
         AIF   (T'&NAME EQ 'O').E2                                              
&NAME    DS    0H                                                               
.**********************************************************************         
.*                         MF PARAMETER                       @PBY0239*         
.**********************************************************************         
.E2      AIF   (T'&MF(2) EQ 'O').ERR1                                           
         AIF   ('&MF(2)' EQ '(1)').E3                                           
         AIF   ('&MF(2)'(1,1) EQ '(').WARN1                   @PBY0239          
.E2A     LA    1,&MF(2)                  LOAD PARM LIST ADDR                    
.**********************************************************************         
.*                         UPT PARAMETER                      @PBY0239*         
.**********************************************************************         
.E3      AIF   (T'&UPT EQ 'O').E4        UPT OMITTED                            
         AIF   ('&UPT'(1,1) EQ '(').E3A                                         
         LA    14,&UPT                   LOAD UPT ADDR                          
         ST    14,0(0,1)                 STORE UPT ADDR IN PARM LIST            
         AGO   .E4                                                              
.E3A     ANOP                                                                   
         ST    &UPT(1),0(0,1)            STORE UPT ADDR IN PARM LIST            
.*                                                            @PBY0239          
.**********************************************************************         
.*                         ECT PARAMETER                      @PBY0239*         
.**********************************************************************         
.E4      AIF   (T'&ECT EQ 'O').E5        ECT OMITTED                            
         AIF   ('&ECT'(1,1) EQ '(').E4A                                         
         LA    14,&ECT                   LOAD ECT ADDR                          
         ST    14,4(0,1)                 STORE ECT ADDR IN PARM LIST            
         AGO   .E5                                                              
.E4A     ANOP                                                                   
         ST    &ECT(1),4(0,1)            STORE ECT ADDR IN PARM LIST            
.**********************************************************************         
.*                         ECB PARAMETER                      @PBY0239*         
.**********************************************************************         
.E5      AIF   (T'&ECB EQ 'O').E6        ECB OMITTED                            
         AIF   ('&ECB'(1,1) EQ '(').E5A                                         
         LA    14,&ECB                   LOAD ECB ADDR                          
         ST    14,8(0,1)                 STORE ECB ADDR IN PARM LIST            
         AGO   .E6                                                              
.E5A     ANOP                                                                   
         ST    &ECB(1),8(0,1)            STORE ECB ADDR IN PARM LIST            
.*                                                            @PBY0239          
.**********************************************************************         
.*                         PARM PARAMETER                     @PBY0239*         
.**********************************************************************         
.E6      AIF   (T'&PARM EQ 'O').E7       PARM OMITTED                           
         AIF   ('&PARM'(1,1) EQ '(').E6A                                        
         LA    14,&PARM                  LOAD PARM ADDR                         
         ST    14,12(0,1)                STORE PARM ADDR IN PARM LIST           
&NOLOAD  SETB  1                         SET FLAG TO INDICATE THAT REG          
.*                                       14 CONTAINS THE PARM ADDRESS           
.*                                                            @PBY0239          
         AGO   .E7                                                              
.E6A     ANOP                                                                   
         ST    &PARM(1),12(0,1)          STORE PARM ADDR IN PARM LIST           
.*                                                            @PBY0239          
&REGLOAD SETB  1                         SET FLAG TO INDICATE THAT A            
.*                                       REGISTER CONTAINS THE PARM             
.*                                       ADDRESS              @PBY0239          
.**********************************************************************         
.*           INPUT, SUBSTACK, AND TERMGET PARAMETERS          @PBY0239*         
.**********************************************************************         
.E7      ANOP                                                 @PBY0239          
         AIF   (N'&INPUT EQ 0 AND N'&SUBSTACK EQ 0                     *        
               AND N'&TERMGET EQ 0).E11  DON'T UPDATE ANYTHING                  
.*                                                            @PBY0239          
         AIF   (&NOLOAD).INPUT           LOAD PARM BLOCK?                       
.*                                                            @PBY0239          
         AIF   (&REGLOAD).LOADREG        LOAD PARM BLOCK REGISTER?              
.*                                                            @PBY0239          
         L     14,12(0,1)                LOAD PARM BLOCK ADDRESS                
.*                                                            @PBY0239          
         AGO   .INPUT                                                           
.*                                                            @PBY0239          
.LOADREG LR    14,&PARM(1)               LOAD PARM BLOCK ADDRESS                
.*                                                            @PBY0239          
.**********************************************************************         
.*                      INPUT PARAMETER                       @PBY0239*         
.**********************************************************************         
.INPUT   ANOP                                                 @PBY0239          
.*   IF THE OPERANDS ARE OMITTED DON'T UPDATE THEM.           @PBY0239          
         AIF   (N'&INPUT EQ 0).E8                             @PBY0239          
         AIF   (N'&INPUT GT 2).ERR2      TOO MANY OPERANDS    @PBY0239          
&COUNT   SETA  N'&INPUT                  INITIALIZE LOOP COUNTER                
.*                                                            @PBY0239          
.LOOP1   ANOP                                                 @PBY0239          
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239          
         AIF   (T'&INPUT(&COUNT) EQ 'O').DEC1                 @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'ISTACK').DEFLT2  USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'TERM').SET1      DON'T USE DEFAULT         
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'LOGICAL').DEFLT1 USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' NE 'PHYSICAL').ERR3  INVALID OPERAND           
.*                                                            @PBY0239          
&B1      SETB  1                   INDICATE "PHYSICAL" OPTION @PBY0239          
.DEFLT1  ANOP                                                 @PBY0239          
         AIF   (&CHECK1).ERR3      CHECK FOR "PHYSICAL" AND "LOGICAL"           
.*                                                            @PBY0239          
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
         AGO   .DEC1                                          @PBY0239          
.SET1    ANOP                      INDICATE "TERM" OPTION     @PBY0239          
&B2      SETB  1                                              @PBY0239          
.DEFLT2  ANOP                                                 @PBY0239          
         AIF   (&CHECK2).ERR3      CHECK FOR "TERM" AND "ISTACK"                
.*                                                            @PBY0239          
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
.DEC1    ANOP                                                 @PBY0239          
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER                 
.*                                                            @PBY0239          
         AIF   (&COUNT NE 0).LOOP1                            @PBY0239          
         MVI   0(14),B'00&B1.&B2.0000'   SET CONTROL FIELD                      
.*                                                            @PBY0239          
&B1      SETB  0                                              @PBY0239          
&B2      SETB  0                                              @PBY0239          
&CHECK1  SETB  0                                              @PBY0239          
&CHECK2  SETB  0                                              @PBY0239          
.**********************************************************************         
.*                      SUBSTACK PARAMETER                    @PBY0239*         
.**********************************************************************         
.E8      ANOP                                                 @PBY0239          
         AIF   (T'&SUBSTACK EQ 'O').E9       IF OMITTED, USE DEFAULT            
.*                                                            @PBY0239          
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT3  USE DEFAULT      @PBY0239          
         AIF   ('&SUBSTACK' NE 'YES').ERR4   INVALID OPERAND  @PBY0239          
&B1      SETB  1                             INDICATE "SUBSTACK" OPTION         
.*                                                            @PBY0239          
.DEFLT3  ANOP                                                                   
         MVI   1(14),B'&B1.0000000'      SET SUBSTACK OPTION BIT                
.*                                                            @PBY0239          
&B1      SETB  0                                              @PBY0239          
.**********************************************************************         
.*                      TERMGET PARAMETER                     @PBY0239*         
.**********************************************************************         
.E9      ANOP                                                 @PBY0239          
.*   IF THE OPERAND IS OMITTED DON'T UPDATE IT.               @PBY0239          
         AIF   (N'&TERMGET EQ 0).E11                          @PBY0239          
         AIF   (N'&TERMGET GT 2).ERR5    TOO MANY OPERANDS    @PBY0239          
&COUNT   SETA  N'&TERMGET                INITIALIZE LOOP COUNTER                
.*                                                            @PBY0239          
.LOOP2   ANOP                                                 @PBY0239          
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239          
         AIF   (T'&TERMGET(&COUNT) EQ 'O').DEC2               @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'EDIT').DEFLT5 USE DEFAULT                
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'ASIS').SET2   DON'T USE DEFAULT          
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'WAIT').DEFLT4 USE DEFAULT                
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' NE 'NOWAIT').ERR6 INVALID OPERAND            
.*                                                            @PBY0239          
&B1      SETB  1                   INDICATE "NOWAIT" OPTION   @PBY0239          
.DEFLT4  ANOP                                                 @PBY0239          
         AIF   (&CHECK1).ERR6      CHECK FOR "NOWAIT" AND "WAIT"                
.*                                                            @PBY0239          
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
         AGO   .DEC2                                          @PBY0239          
.SET2    ANOP                      INDICATE "ASIS" OPTION     @PBY0239          
&B2      SETB  1                                              @PBY0239          
.DEFLT5  ANOP                                                 @PBY0239          
         AIF   (&CHECK2).ERR6      CHECK FOR "EDIT" AND "ASIS"                  
.*                                                            @PBY0239          
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
.DEC2    ANOP                                                 @PBY0239          
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER                 
.*                                                            @PBY0239          
         AIF   (&COUNT NE 0).LOOP2                            @PBY0239          
         MVI   2(14),B'100&B1.000&B2'    SET TINPUT OPTION FIELDS               
.*                                                            @PBY0239          
.**********************************************************************         
.*                      ENTRY PARAMETER                       @PBY0239*         
.**********************************************************************         
.E11     AIF   (T'&ENTRY EQ 'O').E12                                            
         AIF   ('&ENTRY' EQ '(15)').E11B                                        
         AIF   ('&ENTRY'(1,1) EQ '(').WARN2                   @PBY0239          
.E11A    LA    15,&ENTRY                 LOAD EP ADDR         @PBY0239          
.E11B    BALR  14,15                     BRANCH TO SERVICE ROUTINE              
.*                                                            @PBY0239          
         MEXIT                                                                  
.**********************************************************************         
.*                      STANDARD LINKAGE                      @PBY0239*         
.**********************************************************************         
.E12     L     15,16(0,0)                LOAD CVT POINTER                       
         TM    348(15),B'10000000'       IS GETLINE LOADED ?                    
         BNO   IKJ@&SYSNDX               NO - BRANCH TO LINK                    
         L     15,348(0,15)              YES - BALR TO GETLINE                  
         BALR  14,15                                                            
         B     IKJ$&SYSNDX                                                      
IKJ@&SYSNDX LINK  EP=IKJGETL                                                    
IKJ$&SYSNDX DS    0H                                                            
         MEXIT                                                                  
.**********************************************************************         
.*                                                                    *         
.*                             LIST FORM                              *         
.*                                                                    *         
.**********************************************************************         
.LSTFORM DS    0F                                             @PBY0239          
.**********************************************************************         
.*                      INPUT PARAMETER                       @PBY0239*         
.**********************************************************************         
.*       AIF   (N'&INPUT GT 2).ERR2      TOO MANY OPERANDS    @PBY0239          
.*   IF THE OPERANDS ARE OMITTED USE THE DEFAULT VALUES.      @PBY0239          
&COUNT   SETA  N'&INPUT                  INITIALIZE LOOP COUNTER                
.*                                                            @PBY0239          
         AGO   .DECIDE3                                       @PBY0239          
.LOOP3   ANOP                                                 @PBY0239          
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239          
         AIF   (T'&INPUT(&COUNT) EQ 'O').DEC3                 @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'ISTACK').DEFLT7  USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'TERM').SET3      DON'T USE DEFAULT         
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' EQ 'LOGICAL').DEFLT6 USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&INPUT(&COUNT)' NE 'PHYSICAL').ERR3  INVALID OPERAND           
.*                                                            @PBY0239          
&B1      SETB  1                   INDICATE "PHYSICAL" OPTION @PBY0239          
.DEFLT6  ANOP                                                 @PBY0239          
         AIF   (&CHECK1).ERR3      CHECK FOR "PHYSICAL" AND "LOGICAL"           
.*                                                            @PBY0239          
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
         AGO   .DEC3                                          @PBY0239          
.SET3    ANOP                      INDICATE "TERM" OPTION     @PBY0239          
&B2      SETB  1                                              @PBY0239          
.DEFLT7  ANOP                                                 @PBY0239          
         AIF   (&CHECK2).ERR3      CHECK FOR "TERM" AND "ISTACK"                
.*                                                            @PBY0239          
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
.DEC3    ANOP                                                 @PBY0239          
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER                 
.*                                                            @PBY0239          
.DECIDE3 AIF   (&COUNT NE 0).LOOP3                            @PBY0239          
&NAME    DC    B'00&B1.&B2.0000'         SET CONTROL FIELD    @PBY0239          
&B1      SETB  0                                              @PBY0239          
&B2      SETB  0                                              @PBY0239          
&CHECK1  SETB  0                                              @PBY0239          
&CHECK2  SETB  0                                              @PBY0239          
.**********************************************************************         
.*                      SUBSTACK PARAMETER                    @PBY0239*         
.**********************************************************************         
         AIF   (T'&SUBSTACK EQ 'O').DEFLT8   IF OMITTED, USE DEFAULT            
.*                                                            @PBY0239          
         AIF   ('&SUBSTACK' EQ 'NO').DEFLT8  USE DEFAULT      @PBY0239          
         AIF   ('&SUBSTACK' NE 'YES').ERR4   INVALID OPERAND  @PBY0239          
&B1      SETB  1                         INDICATE "SUBSTACK" OPTION             
.*                                                            @PBY0239          
.DEFLT8  ANOP                                                 @PBY0239          
         DC    B'&B1.0000000'            SET SUBSTACK OPTION BIT                
.*                                                            @PBY0239          
&B1      SETB  0                                              @PBY0239          
.**********************************************************************         
.*                      TERMGET PARAMETER                     @PBY0239*         
.**********************************************************************         
         AIF   (N'&TERMGET GT 2).ERR5    TOO MANY OPERANDS    @PBY0239          
.*   IF THE OPERAND IS OMITTED USE THE DEFAULT VALUES.        @PBY0239          
&COUNT   SETA  N'&TERMGET                INITIALIZE LOOP COUNTER                
.*                                                            @PBY0239          
         AGO   .DECIDE4                                       @PBY0239          
.LOOP4   ANOP                                                 @PBY0239          
.*   IF ONE OF THE OPERANDS IS OMITTED USE THE DEFAULT VALUE. @PBY0239          
         AIF   (T'&TERMGET(&COUNT) EQ 'O').DEC4               @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'EDIT').DEFLT10 USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'ASIS').SET4    DON'T USE DEFAULT         
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' EQ 'WAIT').DEFLT9  USE DEFAULT               
.*                                                            @PBY0239          
         AIF   ('&TERMGET(&COUNT)' NE 'NOWAIT').ERR6  INVALID OPERAND           
.*                                                            @PBY0239          
&B1      SETB  1                   INDICATE "NOWAIT" OPTION   @PBY0239          
.DEFLT9  ANOP                                                 @PBY0239          
         AIF   (&CHECK1).ERR6      CHECK FOR "NOWAIT" AND "WAIT"                
.*                                                            @PBY0239          
&CHECK1  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
         AGO   .DEC4                                          @PBY0239          
.SET4    ANOP                      INDICATE "ASIS" OPTION     @PBY0239          
&B2      SETB  1                                              @PBY0239          
.DEFLT10 ANOP                                                 @PBY0239          
         AIF   (&CHECK2).ERR6      CHECK FOR "EDIT" AND "ASIS"                  
.*                                                            @PBY0239          
&CHECK2  SETB  1                   BEING USED TOGETHER - CAN'T BE               
.*                                                            @PBY0239          
.DEC4    ANOP                                                 @PBY0239          
&COUNT   SETA  &COUNT-1                  DECREMENT LOOP COUNTER                 
.*                                                            @PBY0239          
.DECIDE4 AIF   (&COUNT NE 0).LOOP4                            @PBY0239          
         DC    B'100&B1.000&B2'          SET TINPUT OPTION FIELDS               
.*                                                            @PBY0239          
         DC    B'00000000'               SET TINPUT OPTION FIELDS               
.*                                                            @PBY0239          
         DC    A(0)                      INPUT BUFFER ADDRESS                   
.*                                                            @PBY0239          
         MEXIT                                                                  
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'                              
         MEXIT                                                                  
.ERR2    MNOTE 8,'INVALID NUMBER OF OPERANDS FOR INPUT KEYWORD'                 
         MEXIT                                                                  
.*                                                            @PBY0239          
.ERR3    MNOTE 8,'INVALID OPERAND USED FOR INPUT KEYWORD'                       
         MEXIT                                                                  
.*                                                            @PBY0239          
.ERR4    MNOTE 8,'INVALID OPERAND USED FOR SUBSTACK KEYWORD - USE YES O*        
               R NO ONLY'                                                       
         MEXIT                                                                  
.*                                                            @PBY0239          
.ERR5    MNOTE 8,'INVALID NUMBER OF OPERANDS FOR TERMGET KEYWORD'               
         MEXIT                                                                  
.*                                                            @PBY0239          
.ERR6    MNOTE 8,'INVALID OPERAND USED FOR TERMGET KEYWORD'                     
         MEXIT                                                                  
.*                                                            @PBY0239          
.ERR7    MNOTE 8,'POSITIONAL PARAMETERS ARE NOT ALLOWED'                        
         MEXIT                                                                  
.*                                                            @PBY0239          
.WARN1   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'          
         AGO   .E2A                                                             
.*                                                            @PBY0239          
.WARN2   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'         
         AGO   .E11A                                                            
.*                                                            @PBY0239          
         MEND                                                                   
