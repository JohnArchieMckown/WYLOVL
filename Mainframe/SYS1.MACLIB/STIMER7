*********************************************************************** 00050000
*                                                                     * 00100000
* MACRO NAME - STIMER7                                                * 00150000
*                                                                     * 00200000
* COPYRIGHT -                                                         * 00250000
*                                                                     * 00300000
*   5740-XC6 COPYRIGHT IBM CORP 1981,                                 * 00340000
*   LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM,                       * 00380000
*   REFER TO COPYRIGHT INSTRUCTION FORM NUMBER                        * 00420000
*   G120-2083.                                                        * 00460000
*                                                                     * 00500000
* STATUS - OS/VS2 HBB2102                                             * 00550000
*                                                                     * 00600000
* FUNCTION -                                                          * 00650000
*                                                                     * 00700000
*    THE STIMER7 MACRO IS THE MVS/370 FORMAT OF THE STIMER MACRO.     * 00750000
*    THE STIMER MACRO ALLOWS THE ISSUER TO SET A TIMER TO A SPECIFIED * 00800000
*    INTERVAL(LESS THAN 24 HOURS) OR TO AN INTERVAL THAT WILL EXPIRE  * 00850000
*    AT A SPECIFIED TIME OF DAY. WHEN THE TIME INTERVAL EXPIRES, THE  * 00900000
*    EXIT ROUTINE, IF SPECIFIED, WILL BE GIVEN CONTROL ASYNCHRONOUSLY.* 00950000
*    AN ERROR EXIT MAY ALSO BE SPECIFIED WHICH WILL BE GIVEN CONTROL  * 01000000
*    IF THE STIMER FUNCTION CANNOT BE PERFORMED.                      * 01050000
*                                                                     * 01100000
*    THE TYPE OF TIME INTERVALS THAT MAY BE SPECIFIED ARE:            * 01150000
*                                                                     * 01200000
*    REAL - MEASURED CONTINUOUSLY IN REAL-TIME                        * 01250000
*    TASK - MEASURED WHEN THE TASK IS ACTIVE                          * 01300000
*    WAIT - MEASURED CONTINUOUSLY IN REAL-TIME. THE TASK IS PLACED IN * 01350000
*           A WAIT UNTIL THE INTERVAL EXPIRES.                        * 01400000
*                                                                     * 01450000
*    THE ASYNCHRONOUS EXIT ROUTINE MAY BE SPECIFIED ONLY FOR REAL OR  * 01500000
*    TASK TIME INTERVALS. THE EXIT ROUTINE ADDRESS MAY EITHER BE AN   * 01550000
*    RX-TYPE ADDRESS OR REGISTER (0),(2)-(12), OR (15).               * 01600000
*                                                                     * 01750000
*    THE TIMER INTERVAL MAY BE SPECIFIED IN ONE OF SEVERAL WAYS:      * 01800000
*         (ONE OF THE FOLLOWING MUST BE SPECIFIED)                    * 01850000
*         BINTVL=ADDR - ADDRESS OF A 32-BIT UNSIGNED BINARY NUMBER    * 01900000
*                       CONTAINING THE TIME INTERVAL VALUE. THE       * 01950000
*                       VALUE OF THE LOW-ORDER BIT IS .01 SECONDS.    * 02000000
*         DINTVL=ADDR - ADDRESS OF A DOUBLE WORD ON A DOUBLE WORD     * 02050000
*                       BOUNDARY CONTAINING THE TIME INTERVAL VALUE.  * 02100000
*                       THE TIME INTERVAL IS PRESENTED AS UNPACKED    * 02150000
*                       DECIMAL DIGITS OF THE FORM: HHMMSSTH.         * 02200000
*         GMT=ADDR    - ADDRESS OF AN 8-BYTE AREA CONTAINING THE      * 02250000
*                       GREENWICH MEAN TIME AT WHICH THE INTERVAL     * 02300000
*                       IS TO COMPLETE. PRESENTED IN THE SAME FORM AS * 02350000
*                       DINTVL(HHMMSSTH).                             * 02400000
*         MICVL=ADDR  - ADDRESS OF A DOUBLE WORD ON A DOUBLE WORD     * 02450000
*                       BOUNDARY CONTAINING THE TIME INTERVAL VALUE.  * 02500000
*                       THE TIME INTERVAL IS PRESENTED AS AN          * 02550000
*                       UNSIGNED 64-BIT BINARY NUMBER. BIT 51 IS THE  * 02600000
*                       LOW ORDER BIT OF THE INTERVAL VALUE AND       * 02650000
*                       EQUIVALENT TO 1 MICROSECOND.                  * 02700000
*         TOD=ADDR    - ADDRESS OF A DOUBLE WORD ON A DOUBLE WORD     * 02750000
*                       BOUNDARY CONTAINING THE TIME OF DAY AT WHICH  * 02800000
*                       THE INTERVAL IS TO COMPLETE. PRESENTED IN THE * 02850000
*                       SAME FORM AS DINTVL(HHMMSSTH).                * 02900000
*         TUINTVL=ADDR- ADDRESS OF A FULLWORD ON A FULLWORD BOUNDARY  * 02950000
*                       CONTAINING THE TIME INTERVAL VALUE PRESENTED  * 03000000
*                       AS AN UNSIGNED 32-BIT BINARY NUMBER, THE LOW- * 03050000
*                       ORDER BIT OF WHICH HAS A VALUE OF 1 TIMER     * 03100000
*                       UNIT (APPROXIMATELY 26.04166 MICROSECONDS).   * 03150000
*         NOTE: GMT AND TOD MAY NOT BE SPECIFIED FOR TASK TIME        * 03200000
*                       INTERVALS.                                    * 03250000
*                                                                     * 03300000
*         THE ADDRESS SPECIFIED FOR THE TIME INTERVAL VALUE MAY BE    * 03350000
*         EITHER AN RX-TYPE ADDRESS OF REGISTER (1) OR (2)-(12).      * 03400000
*                                                                     * 03450000
*    AN ERROR EXIT MAY BE SPECIFIED (VIA THE ERRET=ADDR OPTION) WHICH * 03500000
*    WILL BE GIVEN CONTROL IS THE STIMER FUNCTION CANNOT BE PERFORMED.* 03550000
*    THE ADDRESS OF THE ERROR EXIT MAY BE EITHER AN RX-TYPE ADDRESS   * 03600000
*    OR REGISTER (2) - (12)                                           * 03650000
*                                                                     * 03700000
*********************************************************************** 03750000
         MACRO                                                          03850000
&NAME    STIMER7 &TYPE,&EXIT,&DINTVL=,&BINTVL=,&TUINTVL=,&TOD=,&MICVL=,*03900000
               &GMT=,&ERRET=                                            03950000
.* A 000000-999999                                             @G860PXK 03975000
* /* MACDATE Y-1 72277                                               */ 04000000
         LCLB  &B(9)                                                    04050000
         LCLA  &SUM                                                     04100000
         AIF   ('&TYPE' EQ '' OR '&DINTVL&BINTVL&TUINTVL&TOD&MICVL&GMT'*04150000
                EQ '').E2                                               04200000
         AIF   ('&EXIT' NE '' AND '&TYPE' EQ 'WAIT').NMNOTE      A26596 04250000
&B(1)    SETB  ('&TYPE' EQ 'TASK')                                      04300000
&B(2)    SETB  ('&TYPE' EQ 'REAL')                                      04350000
&B(3)    SETB  ('&TYPE' EQ 'WAIT')                                      04400000
&B(4)    SETB  ('&DINTVL' NE '')                                        04450000
&B(5)    SETB  ('&BINTVL' NE '')                                        04500000
&B(6)    SETB  ('&TUINTVL' NE '')                                       04550000
&B(7)    SETB  ('&TOD' NE '')                                           04600000
&B(8)    SETB  ('&MICVL' NE '')                                         04650000
&B(9)    SETB  ('&GMT' NE '')                                           04700000
         AIF   (&B(1)+&B(2)+&B(3) NE 1).E1                              04750000
         AIF   (&B(1) AND (&B(7) OR &B(9))).E3                          04800000
&NAME    IHBINNRA &DINTVL&BINTVL&TUINTVL&TOD&MICVL&GMT,&EXIT            04850000
         AIF   ('&ERRET' EQ '').NORET                                   04900000
&SUM     SETA  8                                  RETURN CODE FLAG      04950000
.NORET   AIF   ('&EXIT' NE '').CONTA                                    05000000
         AIF   (&B(1) AND &B(6)).SRSVC                                  05050000
&SUM     SETA  &SUM+&B(3)+3*&B(2)+16*&B(5)+48*&B(4)+112*&B(7)+32*&B(8)+*05100000
               96*&B(9)                                                 05150000
.LC      LA    0,&SUM.(0,0)                       LOAD FLAG BYTE        05200000
.ZSHIFT  SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE 05250000
         AGO   .SVC                                                     05300000
.SRSVC   AIF   (&SUM NE 0).LC                     TEST FOR RC REQUESTED 05350000
         SR    0,0                                INDICATE TASK,TUINTVL 05400000
         AGO   .SVC                                                     05450000
.SPZRT   LA    0,48+&SUM.(0,0)                    LOAD FLAG BYTE        05500000
         AGO   .ZSHIFT                                                  05550000
.CONTA   AIF   (&B(1) AND &B(6)).RETCK                                  05600000
&SUM     SETA  &SUM+3*&B(2)+16*&B(5)+48*&B(4)+112*&B(7)+32*&B(8)+96*&B(*05650000
               9)                                                       05700000
.LCEX    LA    14,&SUM.(0,0)                                            05750000
.SHIFT   SLL   14,24(0)                          SHIFT TO HI-ORDER BYTE 05800000
         OR    0,14                              AND PACK WITHEXIT ADDR 05850000
         AGO   .SVC                                                     05900000
.RETCK   AIF   (&SUM NE 0).LCEX                   TEST FOR RC REQUESTED 05950000
.SVC     SVC   47                                ISSUE STIMER SVC       06000000
         AIF   ('&ERRET' EQ '').DONE                                    06050000
         LTR   15,15                   ANY RETURN CODE                  06100000
         AIF   ('&ERRET'(1,1) EQ '(').BCR                               06150000
         BNZ   &ERRET                  YES. BR TO USER EXIT             06200000
         AGO   .DONE                                                    06250000
.BCR     BCR   7,&ERRET(1)                                              06300000
.DONE    MEXIT                                                          06350000
.SPECRT  LA    14,48+&SUM.(0,0)                   LOAD FLAG BYTE        06400000
         AGO   .SHIFT                                                   06450000
.NMNOTE  ANOP                                                    A26596 06500000
         MNOTE 12,'WAIT AND EXIT ADDRESS INVALID'                A26596 06550000
         MEXIT                                                          06600000
.E3      ANOP                                                           06650000
         MNOTE 16,'TASK WITH GMT OR TOD INVALID'                        06700000
         MEXIT                                                          06750000
.E1      IHBERMAC 36,,&TYPE,,,0                                         06800000
         MEXIT                                                          06850000
.E2      IHBERMAC 01                                                    06900000
         MEXIT                                                          06950000
         MEND                                                           07000000
