******** START OF SPECIFICATIONS ************************************** 00050000
*                                                                     * 00100000
*01* MACRO-NAME = STAX7                                               * 00150000
*                                                                     * 00200000
*01* COPYRIGHT = 5740-XC6 COPYRIGHT IBM CORP. 1981,                   * 00250000
*                LICENSED MATERIAL-PROGRAM, PROPERTY OF IBM,          * 00300000
*                REFER TO COPYRIGHT INSTRUCTIONS FORM G120-2-83       * 00350000
*                                                                     * 00400000
*01* STATUS = OS/VS2 HBB2102                                          * 00450000
*                                                                     * 00500000
*01* FUNCTION = DOWNWARD COMPATIBLE VERSION OF THE STAX MACRO         * 00550000
*               (EQUIVALENT TO THE JBB1326 LEVEL).                    * 00600000
*                                                                     * 00650000
*01* CHANGE ACTIVITY =                                                * 00700000
*        @G860PXZ - HBB2102 SUPPORT                                   * 00750000
*                                                                     * 00800000
******** END OF SPECIFICATIONS **************************************** 00850000
         MACRO                                                          00900000
&NAME    STAX7 &EXADDR,&OBUF=,&IBUF=,&USADDR=,&REPLACE=,&DEFER=,&MF=I   00950000
*        MACDATE 06/29/81                                               01000000
.*A000000-999999                                               @G860PXZ 01050000
         LCLA  &A                                                       01100000
         LCLC  &GNAME,&H                                                01150000
         LCLA  &REPLSW,&DEFRSW                                          01200000
         LCLA  &REPLYES,&REPLNO,&CLEAR                           YM4019 01250000
&REPLYES SETA  X'30'                                             YM4019 01300000
&REPLNO  SETA  X'40'                                             YM4019 01350000
&CLEAR   SETA  X'40'                                             YM4019 01400000
&GNAME   SETC  'IHB'.'&SYSNDX'                                          01450000
         AIF   ('&MF' EQ 'L' OR '&MF(1)' EQ 'E').DEFER4          YM4019 01500000
         AIF   ('&EXADDR' NE '').DEFER4                          YM4019 01550000
         AIF   ('&OBUF' NE '' OR '&IBUF' NE '').ERROR1           YM4019 01600000
         AIF   ('&REPLACE' NE '' OR '&USADDR' NE '').ERROR1      YM4019 01650000
         AIF   ('&MF' NE 'I').ERROR2                             YM4019 01700000
         AIF   ('&EXADDR' EQ '' AND '&DEFER' EQ '').CANCEL       YM4019 01750000
.DEFER4  AIF   ('&REPLACE' EQ '').DEFER5                                01800000
         AIF   ('&REPLACE' EQ 'YES').DEFER5                             01850000
         AIF   ('&REPLACE' NE 'NO').ERROR1                       YM4019 01900000
&REPLSW  SETA  X'40'                                                    01950000
.DEFER5  ANOP                                                           02000000
         AIF   ('&NAME' EQ '' OR '&MF' EQ 'L').CONTA                    02050000
&NAME    DS    0H                                                       02100000
         AGO   .CONTA                                                   02150000
.ERROR   MNOTE 8,'INVALID REGISTER SPECIFICATION'                YM4019 02200000
         MEXIT                                                          02250000
.ERROR1  MNOTE 8,'INVALID OPTION SPECIFIED'                      YM4019 02300000
         MEXIT                                                          02350000
.CONTA   ANOP                                                           02400000
         AIF   ('&DEFER' EQ '').DEFER1       IS DEFER REQUESTED         02450000
         AIF   ('&DEFER' EQ 'YES').DEFER2    SHOULD DEFER ATTN          02500000
         AIF   ('&DEFER' EQ 'NO').DEFER3     SHOULD CANCEL DEFER ATTN   02550000
         AGO   .ERROR1        INVALID OPERAND HAS BEEN SPECIFIED        02600000
.DEFER2  ANOP                 SET DEFER SWITCH EQUAL TO                 02650000
&DEFRSW  SETA  X'20'          DEFER/YES OR X'20'                        02700000
         AGO   .DEFER1                                                  02750000
.DEFER3  ANOP                 SET DEFER SWITCH EQUAL TO                 02800000
&DEFRSW  SETA  X'10'          DEFER/NO OR X'10'                         02850000
.DEFER1  ANOP                                                           02900000
         AIF   ('&MF' EQ 'L').LROUT      IS THIS A LIST REQUEST         02950000
         AIF   ('&MF' EQ 'I').IROUT      IS THIS A STANDARD REQUEST     03000000
         AIF   ('&MF(1)' EQ 'E').EROUT     IS THIS A EXECUTE REQUEST    03050000
.ERROR2  MNOTE 8,'INVALID MF SPECIFICATION'                      YM4019 03100000
         MEXIT                                                          03150000
.EROUT   ANOP                                                           03200000
         AIF   ('&MF'(4,1) EQ '(').ISAREG                               03250000
         LA    1,&MF(2)      ADDRESS OF PARAMETER LIST INTO REG 1       03300000
         AGO   .CONTB                                                   03350000
.ISAREG  AIF   ('&MF(2)' EQ '(1)').CONTB                                03400000
&A       SETA  K'&MF-6                                                  03450000
&H       SETC  '&MF'(5,&A)                                              03500000
         LR    1,&H          ADDRESS OF PARAMETER LIST INTO REG 1       03550000
.CONTB   ANOP                                                           03600000
         AIF   ('&EXADDR' EQ '').CONTC                                  03650000
         AIF   ('&EXADDR'(1,1) EQ '(').ISAREG1                          03700000
         LA    14,&EXADDR    ADDRESS OF EXIT ROUTINE                    03750000
         ST    14,0(1,0)     INTO PARAMETER LIST                        03800000
         AGO   .CONTC                                                   03850000
.ISAREG1 ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST     03900000
.CONTC   AIF   ('&USADDR' EQ '').CONTD                                  03950000
         AIF   ('&USADDR'(1,1) EQ '(').ISAREG2                          04000000
         IC    15,16(1,0)    SAVE OPTION BYTE                   @YM2296 04050000
         LA    14,&USADDR    ADDRESS OF USER PARAMETERS                 04100000
         ST    14,16(1,0)    INTO LIST                                  04150000
         STC   15,16(1,0)    RESTORE OPTION BYTE                @YM2296 04200000
         AGO   .CONTD                                                   04250000
.ISAREG2 IC    15,16(1,0)         SAVE OPTION BYTE              @YM2296 04300000
         ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS INTO       04350000
*                                 LIST                                  04400000
         STC   15,16(1,0)         RESTORE OPTION BYTE           @YM2296 04450000
.CONTD   AIF   ('&OBUF(1)' EQ '').CNTSUB1                               04500000
         AIF   ('&OBUF(1)'(1,1) EQ '(').IRGSUB1                         04550000
         LA    14,&OBUF(1)   ADDRESS OF OUTPUT BUFFERS                  04600000
         ST    14,8(1,0)     INTO LIST                                  04650000
         AGO   .CNTSUB1                                                 04700000
.IRGSUB1 ST   &OBUF(1),8(1,0)  ADDRESS OF OUTPUT BUFFERS INTO LIST      04750000
.CNTSUB1 AIF  ('&OBUF(2)' EQ '').CONTE                                  04800000
         AIF   ('&OBUF(2)'(1,1) EQ '(').IRGSUB2                         04850000
         LA    14,&OBUF(2)(0,0) LENGTH OF OUTPUT BUFFERS        @YM2296 04900000
         STH   14,6(1,0)     INTO LIST                          @YM2296 04950000
         AGO   .CONTE                                                   05000000
.IRGSUB2 STH  &OBUF(2),6(1,0) LENGTH OF OUTPUT BUFFERS IN LIST  @YM2296 05050000
.CONTE   AIF   ('&IBUF(1)' EQ '').CNTSUB2                               05100000
         AIF   ('&IBUF(1)'(1,1) EQ '(').IRGSUB3                         05150000
         LA    14,&IBUF(1)    ADDRESS OF INPUT BUFFERS                  05200000
         ST    14,12(1,0)    INTO LIST                                  05250000
         AGO   .CNTSUB2                                                 05300000
.IRGSUB3 ST   &IBUF(1),12(1,0)    ADDRESS OF INPUT BUFFERS INTO LIST    05350000
.CNTSUB2 AIF  ('&IBUF(2)' EQ '').CONTG                                  05400000
         AIF   ('&IBUF(2)'(1,1) EQ '(').IRGSUB4                         05450000
         LA    14,&IBUF(2)(0,0) LENGTH OF INPUT BUFFERS         @YM2296 05500000
         STH   14,4(1,0)     INTO LIST                          @YM2296 05550000
         AGO   .CONTG                                                   05600000
.IRGSUB4 STH  &IBUF(2),4(1,0) LENGTH OF INPUT BUFFERS IN LIST   @YM2296 05650000
.CONTG   AIF   ('&REPLACE' EQ '').CNTSUB5                               05700000
         AIF   ('&REPLACE' EQ 'YES').CNTSUB6                    @YM1829 05750000
         OI    16(1),&REPLNO      SET REPLACE NO                 YM4019 05800000
         AGO   .CNTSUB5                                          YM4019 05850000
.CNTSUB6 NI    16(1),&REPLYES     INDICATE REPLACE PREVIOUS STAX YM4019 05900000
.CNTSUB5 AIF   ('&DEFER' EQ '').CNTSUB7                          YM4019 05950000
         NI    16(1),&CLEAR       CLEAR DEFER SWITCHES           YM4019 06000000
         OI    16(1),&DEFRSW     SET DEFER SWITCHES              YM4019 06050000
.CNTSUB7 AGO   .SVC                                                     06100000
.LROUT   ANOP                                                           06150000
&NAME    DS    0F                                                       06200000
.CONTL4  AIF   ('&EXADDR' EQ '').DEFER6                                 06250000
         AIF   ('&EXADDR'(1,1) EQ '(').ERROR                            06300000
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE                    06350000
         AGO   .DEFER7                                                  06400000
.DEFER6  DC    AL4(0)         ADDRESS OF EXIT ROUTINE                   06450000
.DEFER7  ANOP                                                           06500000
.CNTL6   AIF   ('&IBUF(2)' EQ '').CONTL1                                06550000
         AIF   ('&IBUF(2)'(1,1) EQ '(').ERROR                           06600000
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS                    06650000
         AGO   .CONTL2                                                  06700000
.CONTL1  DC    AL2(0)        LENGTH OF INPUT BUFFERS                    06750000
.CONTL2  AIF   ('&OBUF(2)' EQ '').CONTL3                                06800000
         AIF   ('&OBUF(2)'(1,1) EQ '(').ERROR                           06850000
         DC    AL2(&OBUF(2)) LENGTH OF OUTPUT BUFFERS                   06900000
         AGO   .CONTLA                                                  06950000
.CONTL3  DC    AL2(0)        LENGTH OF OUTPUT BUFFERS                   07000000
.CONTLA  AIF   ('&OBUF(1)' EQ '').CONTLB                                07050000
         AIF   ('&OBUF(1)'(1,1) EQ '(').ERROR                           07100000
         DC    AL4(&OBUF(1))  ADDRESS OF OUTPUT BUFFERS                 07150000
         AGO   .CONTL8                                                  07200000
.CONTLB  DC    A(0)          ADDRESS OF OUTPUT BUFFERS                  07250000
.CONTL8  AIF   ('&IBUF(1)' EQ '').CONTL9                                07300000
         AIF   ('&IBUF(1)'(1,1) EQ '(').ERROR                           07350000
         DC    AL4(&IBUF(1))    ADDRESS OF INPUT BUFFERS                07400000
         AGO   .CONTL0                                                  07450000
.CONTL9  DC    A(0)          ADDRESS OF INPUT BUFFERS                   07500000
.CONTL0  DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND 07550000
.CONTL6  AIF   ('&USADDR' EQ '').CONTL7                                 07600000
         AIF   ('&USADDR'(1,1) EQ '(').ERROR                            07650000
         DC    AL3(&USADDR)   ADDRESS OF USER PARAMETERS                07700000
         AGO   .ENDLIST                                                 07750000
.CONTL7  DC    AL3(0)        ADDRESS OF USER PARAMETERS                 07800000
.ENDLIST  ANOP                                                          07850000
         MEXIT                                                          07900000
.IROUT   ANOP                                                           07950000
         LA    1,&GNAME      ADDRESS OF PARAMETER LIST                  08000000
         AIF   ('&IBUF(2)' EQ '').IROUT1                                08050000
         AIF   ('&IBUF(2)'(1,1) NE '(').IROUT1                          08100000
         STH   &IBUF(2),4(1,0) LENGTH OF INPUT BUFFERS          @YM2296 08150000
.IROUT1  AIF   ('&OBUF(2)' EQ '').IROUT2                                08200000
         AIF   ('&OBUF(2)'(1,1) NE '(').IROUT2                          08250000
         STH   &OBUF(2),6(1,0) LENGTH OF OUTPUT BUFFERS         @YM2296 08300000
.IROUT2  AIF   ('&EXADDR' EQ '').IROUT3                                 08350000
         AIF   ('&EXADDR'(1,1) NE '(').IROUT3                           08400000
         ST    &EXADDR(1),0(1,0)  ADDRESS OF EXIT ROUTINE INTO LIST     08450000
.IROUT3  AIF   ('&USADDR' EQ '').IROUT4                                 08500000
         AIF   ('&USADDR'(1,1) NE '(').IROUT4                           08550000
         IC    15,16(1,0)         GET OPTIONS BYTE              @YM2296 08600000
         ST    &USADDR(1),16(1,0) ADDRESS OF USER PARAMETERS IN LIST    08650000
         STC   15,16(1,0)         PRESERVE OPTIONS BYTE         @YM2296 08700000
.IROUT4  AIF   ('&IBUF' EQ '').IROUT5                                   08750000
         AIF   ('&IBUF(1)'(1,1) NE '(').IROUT5                          08800000
         ST    &IBUF(1),12(1,0)   ADDRESS OF INPUT BUFFERS INTO LIST    08850000
.IROUT5  AIF   ('&OBUF' EQ '').IROUT6                                   08900000
         AIF   ('&OBUF(1)'(1,1) NE '(').IROUT6                          08950000
         ST    &OBUF(1),8(1,0)    ADDRESS OF OUTPUT BUFFERS INTO LIST   09000000
.IROUT6  ANOP                                                           09050000
         B     20(0,1)                                                  09100000
&GNAME   DS    0F                                                       09150000
.CNTD    AIF   ('&EXADDR' EQ '').CNTE                                   09200000
         AIF   ('&EXADDR'(1,1) EQ '(').CNTE                             09250000
         DC    AL4(&EXADDR)  ADDRESS OF EXIT ROUTINE                    09300000
         AGO  .CONTF2                                                   09350000
.CNTE    DC    AL4(0)   ADDRESS OF EXIT ROUTINE                         09400000
.CONTF2  AIF   ('&IBUF(2)' EQ '').CNTA                                  09450000
         AIF   ('&IBUF(2)'(1,1) EQ '(').CNTA                            09500000
         DC    AL2(&IBUF(2)) LENGTH OF INPUT BUFFERS                    09550000
         AGO   .CNTB                                                    09600000
.CNTA    DC    AL2(0)        LENGTH OF INPUT BUFFERS                    09650000
.CNTB    AIF   ('&OBUF(2)' EQ '').CNTC                                  09700000
         AIF   ('&OBUF(2)'(1,1) EQ '(').CNTC                            09750000
         DC    AL2(&OBUF(2))  LENGTH OF OUTPUT BUFFERS                  09800000
         AGO   .CNTK                                                    09850000
.CNTC    DC    AL2(0)        LENGTH OF OUTPUT BUFFERS                   09900000
.CNTK    AIF   ('&OBUF(1)' EQ '').CNTL                                  09950000
         AIF   ('&OBUF(1)'(1,1) EQ '(').CNTL                            10000000
         DC    AL4(&OBUF(1)) ADDRESS OF OUTPUT BUFFERS                  10050000
         AGO   .CNTI                                                    10100000
.CNTL    DC    A(0)          ADDRESS OF OUPTUT BUFFERS                  10150000
.CNTI    AIF   ('&IBUF(1)' EQ '').CNTJ                                  10200000
         AIF   ('&IBUF(1)'(1,1) EQ '(').CNTJ                            10250000
         DC    AL4(&IBUF(1)) ADDRESS OF INPUT BUFFERS                   10300000
         AGO   .CNTM                                                    10350000
.CNTJ    DC    A(0)          ADDRESS OF INPUT BUFFERS                   10400000
.CNTM    DC    AL1(&REPLSW+&DEFRSW)    REPLACE/NO REPLACE, DEFERRAL IND 10450000
.CNTF    AIF   ('&USADDR' EQ '').CNTH                                   10500000
         AIF   ('&USADDR'(1,1) EQ '(').CNTH                             10550000
         DC    AL3(&USADDR)  ADDRESS OF USER PARAMETERS                 10600000
         AGO   .SVC                                                     10650000
.CNTH    DC    AL3(0)        ADDRESS OF USER PARAMETERS                 10700000
         AGO   .SVC                                                     10750000
.CANCEL  ANOP                                                           10800000
&NAME    DS    0H                                                       10850000
         SR    1,1           CANCEL PREVIOUS STAX                       10900000
.SVC     SVC   96                                                       10950000
         MEND                                                           11000000
