         MACRO                                                          00000500
         GIMDFUT &USERMOD=,        USERMOD SYSMODID                    X00001000
               &PRE=,              PREREQUISITES FOR USERMOD           X00001500
               &REQ=,              REQUISITES FOR USERMOD              X00002000
               &SUP=,              SUPERCEDED SYSMODS FOR USERMOD      X00002500
               &PGM=,              USER SPECIFIED PROGRAMS             X00003000
               &DEFAULT=NO         DO NOT GENERATE SMP/E DEFAULTS       00003500
.*A000000-999999                                              @Z21SP00* 00004000
.********************************************************************** 00004500
.*                                                                    * 00005000
.* START_OF_SPECIFICATIONS ******************************************** 00005500
.*                                                                    * 00006000
.*  MACRO_NAME = GIMDFUT                                              * 00006500
.*                                                                    * 00007000
.*  DESCRIPTIVE_NAME = SMP/E VALID UTILTITY TABLE GENERATOR           * 00007500
.*                                                                    * 00008000
.********************************************************************** 00008500
.*                                                                    * 00009000
.* COPYRIGHT = 5647-A01 (C) COPYRIGHT IBM CORPORATION 1982, 1999      * 00009500
.*             LICENSED MATERIALS - PROPERTY OF IBM                   * 00010000
.*                                                                    * 00010500
.********************************************************************** 00011000
.*                                                                    * 00011500
.*  FUNCTION = GENERATE GIMUTTBL, SMP/E VALID UTILITY TABLE.          * 00012000
.*                                                                    * 00012500
.*  OPERATION = GENERATE THE ENTRIES FOR GIMUTTBL AS DEFINED BY       * 00013000
.*      THE INPUT PARAMETER &PGM AND/OR &DEFAULT IN THE FORM OF       * 00013500
.*      A USERMOD TO SMP/E.  DO ALL VALIDITY CHECKING OF INPUT        * 00014000
.*      OPERAND VALUES PRIOR TO GENERATION OF USERMOD AND ISSUE       * 00014500
.*      MNOTES IF ERRORS DETECTED.  IF NO ERRORS, GENERATE USERMOD.   * 00015000
.*      THE SPECIFIC CHECKS MADE ARE THE FOLLOWING:                   * 00015500
.*                                                                    * 00016000
.*      1. ENSURE ONLY ONE VALUE IS SUPPLIED FOR THE &USERMOD         * 00016500
.*         OPERAND.  IN CASE OF ERROR ISSUE MNOTE -                   * 00017000
.*            ONLY ONE VALUE CAN BE SPECIFIED FOR USERMOD             * 00017500
.*                                                                    * 00018000
.*      2. ENSURE THE VALUE SUPPLIED FOR THE &USERMOD OPERAND IS      * 00018500
.*         7 CHARACTERS IN LENGTH.  IN CASE OF ERROR ISSUE MNOTE -    * 00019000
.*            USERMOD VALUE USERMODID IS NOT A VALID SYSMOD NAME      * 00019500
.*                                                                    * 00020000
.*      3. ENSURE THAT EITHER &PGM OR &DEFAULT WITH A VALUE OF "YES"  * 00020500
.*         OR BOTH ARE SPECIFIED.  IN CASE OF ERROR ISSUE MNOTE -     * 00021000
.*            PGM AND/OR DEFAULT=YES MUST BE SPECIFIED                * 00021500
.*                                                                    * 00022000
.*      4. IF &DEFAULT IS SPECIFIED, ENSURE THAT ITS VALUE IS VALID   * 00022500
.*         (EITHER "YES" OR "NO").  IN CASE OF ERROR ISSUE MNOTE -    * 00023000
.*            VALUE FOR DEFAULT OPERAND IS INVALID (IT MUST BE        * 00023500
.*            EITHER YES OR NO)                                       * 00024000
.*                                                                    * 00024500
.*      5. IF &PGM IS SPECIFIED, ENSURE THAT THE VALUES SPECIFIED ARE * 00025000
.*         NOT NULL (A COMMA FOLLOWED IMMEDIATELY BY ANOTHER COMMA).  * 00025500
.*         IN CASE OF ERROR ISSUE MNOTE -                             * 00026000
.*            NULL PROGRAM NAME SPECIFIED                             * 00026500
.*                                                                    * 00027000
.*      6. IF &PGM IS SPECIFIED, ENSURE THAT THE VALUES SPECIFIED ARE * 00027500
.*         1 TO 8 CHARACTERS IN LENGTH.  IN CASE OF ERROR ISSUE       * 00028000
.*         MNOTE -                                                    * 00028500
.*            PGMNAME IS AN INVALID PROGRAM NAME                      * 00029000
.*                                                                    * 00029500
.*      7. IF &SUP IS SPECIFIED, ENSURE THAT ALL VALUES HAVE A LENGTH * 00030000
.*         OF 7 CHARACTERS.  IN CASE OF ERROR ISSUE MNOTE -           * 00030500
.*            SUP VALUE SUPERCEDED-ID IS NOT A VALID SYSMOD NAME      * 00031000
.*                                                                    * 00031500
.*      8. IF &REQ IS SPECIFIED, ENSURE THAT ALL VALUES HAVE A LENGTH * 00032000
.*         OF 7 CHARACTERS.  IN CASE OF ERROR ISSUE MNOTE -           * 00032500
.*            REQ VALUE REQUISITE-ID IS NOT A VALID SYSMOD NAME       * 00033000
.*                                                                    * 00033500
.*      9. IF &PRE IS SPECIFIED, ENSURE THAT ALL VALUES HAVE A LENGTH * 00034000
.*         OF 7 CHARACTERS.  IN CASE OF ERROR ISSUE MNOTE -           * 00034500
.*            PRE VALUE PREREQUISITE-ID IS NOT A VALID SYSMOD NAME    * 00035000
.*                                                                    * 00035500
.*  INPUT =                                                           * 00036000
.*      &USERMOD, NAME OF USERMOD (REQUIRED)                          * 00036500
.*      &PRE, PRE'S FOR USERMOD (OPTIONAL)                            * 00037000
.*      &REQ, REQ'S FOR USERMOD (OPTIONAL)                            * 00037500
.*      &SUP, SUP'S FOR USERMOD (OPTIONAL)                            * 00038000
.*      &PGM, ANY NUMBER OF SUBLIST ENTRIES CAN BE GENERATED          * 00038500
.*            WHICH ARE TO BE CONSIDERED VALID UTILITIES FOR SMP/E.   * 00039000
.*            REQUIRED IF &DEFAULT OPERAND NOT SPECIFIED; OPTIONAL    * 00039500
.*            IF &DEFAULT OPERAND SPECIFIED.                          * 00040000
.*      &DEFAULT, VALUE OF YES OR NO - "YES" INDICATES SMP/E DEFAULT  * 00040500
.*            UTILITY NAMES ARE TO PLACED INTO TABLE IN ADDITION      * 00041000
.*            TO USER SUPPLIED VALUES FROM THE &PGM OPERAND.  "NO"    * 00041500
.*            INDICATES THAT SMP/E DEFAULT UTILITIES ARE NOT TO BE    * 00042000
.*            PLACED IN THE TABLE; ONLY USER SUPPLIED VALUES FROM     * 00042500
.*            THE &PGM OPERAND ARE TO BE USED.  "NO" IS THE DEFAULT   * 00043000
.*            VALUE OF THIS OPERAND.  REQUIRED TO HAVE A VALUE OF     * 00043500
.*            "YES" IF &PGM OPERAND IS NOT SPECIFIED.  THE DEFAULT    * 00044000
.*            SMP/E UTILITY NAMES ARE AS FOLLOWS:                     * 00044500
.*                                                                    * 00045000
.*               1. ASMA90   - ASSEMBLER UTILITY                  @D1C* 00045500
.*               2. IDCAMS   - AMS UTILITY                            * 00046000
.*               3. IEBCOPY  - COMPRESS UTILITY                       * 00046500
.*                           - COPY UTILITY                           * 00047000
.*                           - RETRY COMPRESS UTILITY                 * 00047500
.*               4. IEBUPDTE - UPDATE UTILITY                         * 00048000
.*               5. IEHIOSUP - IOSUP UTILITY                          * 00048500
.*               6. IEWL     - LINK EDIT UTILITY                      * 00049000
.*               7. IEWBLINK - LINK EDIT UTILITY              @DCR1761* 00049500
.*               8. IMASPZAP - SUPERZAP UTILITY               @DCR1761* 00050000
.*               9. BPXCOPY  - HFS COPY UTILITY                   @L1A* 00050500
.*                                                                    * 00051000
.*  OUTPUT =                                                          * 00051500
.*      USERMOD TO REPLACE GIMUTTBL OR                                * 00052000
.*      MNOTE(S) DESCRIBING ERRORS IN MACRO SPECIFICATION             * 00052500
.*                                                                    * 00053000
.*  EXIT_NORMAL =                                                     * 00053500
.*                                                                    * 00054000
.*      CONDITIONS = NO ERRORS IN MACRO SPECIFICATION                 * 00054500
.*                                                                    * 00055000
.*      OUTPUT = USERMOD TO REPLACE GIMUTTBL                          * 00055500
.*                                                                    * 00056000
.*  EXIT_ERROR =                                                      * 00056500
.*                                                                    * 00057000
.*      CONDITIONS = ERROR(S) IN MACRO SPECIFICATION                  * 00057500
.*                                                                    * 00058000
.*      OUTPUT = MNOTE(S) DESCRIBING ERROR(S) ASSOCIATED WITH A       * 00058500
.*               RETURN CODE OF 12                                    * 00059000
.*                                                                    * 00059500
.*  MNOTES =                                                          * 00060000
.*            ONLY ONE VALUE CAN BE SPECIFIED FOR USERMOD             * 00060500
.*                                                                    * 00061000
.*            USERMOD VALUE USERMODID IS NOT A VALID SYSMOD NAME      * 00061500
.*                                                                    * 00062000
.*            PGM AND/OR DEFAULT=YES MUST BE SPECIFIED                * 00062500
.*                                                                    * 00063000
.*            VALUE FOR DEFAULT OPERAND IS INVALID (IT MUST BE        * 00063500
.*            EITHER YES OR NO)                                       * 00064000
.*                                                                    * 00064500
.*            NULL PROGRAM NAME SPECIFIED                             * 00065000
.*                                                                    * 00065500
.*            PGMNAME IS AN INVALID PROGRAM NAME                      * 00066000
.*                                                                    * 00066500
.*            SUP VALUE SUPERCEDED-ID IS NOT A VALID SYSMOD NAME      * 00067000
.*                                                                    * 00067500
.*            REQ VALUE REQUISITE-ID IS NOT A VALID SYSMOD NAME       * 00068000
.*                                                                    * 00068500
.*            PRE VALUE PREREQUISITE-ID IS NOT A VALID SYSMOD NAME    * 00069000
.*                                                                    * 00069500
.*  CHANGE_ACTIVITY                                                   * 00070000
.*                                                                    * 00070500
.*      11.00 - Z21SP00, INITIAL RELEASE OF GIMSMP            @Z21SP00* 00071000
.*      12.00 - Z22SP00, SMP/E VERSION 1 RELEASE 2            @Z22SP00* 00071500
.*      13.00 - HMP1300, SMP/E VERSION 1 RELEASE 3            @HMP1300* 00072000
.*      14.00 - IR71858, SMP/E VERSION 1 RELEASE 4            @IR71858* 00072500
.*      14.05 - IR71858, NEVER CHANGED FOR RELEASE 4.         @IR71858* 00073000
.*      15.00 - HMP1500, SMP/E VERSION 1 RELEASE 5            @HMP1500* 00073500
.*            - PER0194, NEVER CHANGED FOR RELEASE 5          @PER0194* 00074000
.*    15.1.00 - HMP1502, SMP/E VERSION 1 RELEASE 5.1          @HMP1502* 00074500
.*      16.00 - HMP1600, SMP/E VERSION 1 RELEASE 6            @HMP1600* 00075000
.*      16.04 - CHANGE ASSEMBLER DEFAULT - ASMBLR TO IEV90    @IR93967* 00075500
.*      17.00 - HMP1700, SMP/E VERSION 1 RELEASE 7            @HMP1700* 00076000
.*            - DCR1761, ADD IEWBLINK AND IGWSPZAP AS DEFAULT @DCR1761* 00076500
.*            - DCR1764, IGWSPZAP NOT EXTERNALIZED            @DCR1764* 00077000
.*                                                                    * 00077500
.*                                                                    * 00078000
.* ------------------------------------------------------------------ * 00078500
.* FLAG REASON  RELEASE   DATE   ORG  DESCRIPTION                     * 00079000
.* ---- ------- ------- -------- ---  ------------------------------- * 00079500
.* $L1= HFS1800  18.00  03/13/92 KMH: OPEN MVS #2                     * 00080000
.* $P1= PPA0038 18.1.00 04/01/94 KAP: CHANGE FMID TO HMP1802          * 00080500
.* $S1= HMP1900  19.00  08/08/94 KAP: PRE RELEASE PROCESSING          * 00081000
.* $D1= DCR1909  19.00  11/17/95 MUD: MAKE ASMA90 DEFAULT ASSEMBLER   * 00081500
.* $S2= HMP1A00  1A.00  03/28/96 KAP: PRE RELEASE PROCESSING          * 00082000
.* $S3= HMP1B00  1B.00  10/11/96 KAP: PRE RELEASE PROCESSING          * 00082500
.* $S4= HMP1C00  27.00  11/11/97 KAP: PRE RELEASE PROCESSING          * 00083000
.*                                                                    * 00083500
.********************************************************************** 00084000
.*   CHANGE FLAG KEY: M@PNC                                           * 00084500
.*   WHERE:                                                           * 00085000
.*                                                                    * 00085500
.* M= MULTIPLE    P= DCRS = D,E,F,G    N= 0-9,A-Z     C= A = ADD      * 00086000
.*    FOR DELETE     HDWE = H,I,J,K                      C = CHANGE   * 00086500
.*    FLAGS ONLY     LINE = L,M,N,O                      P = COPY     * 00087000
.*                   PTMS = P,Q,R,S                      M = MOVE     * 00087500
.*                   APARS = 0-9                         D = DELETE   * 00088000
.*                                                                    * 00088500
.* END_OF_SPECIFICATIONS ********************************************** 00089000
.*                                                                    * 00089500
.********************************************************************** 00090000
         LCLA  &INDEX                                                   00090500
         LCLA  &ERROR                                                   00091000
         LCLA  &GENDONE                                                 00091500
         LCLA  &X070                                                    00092000
&ERROR   SETA  0                                                        00092500
&X070    SETA  0                                                        00093000
.CHECK1  ANOP                                                           00093500
         AIF   (N'&USERMOD NE 1).MNOTE1                                 00094000
.CHECK2  ANOP                                                           00094500
         AIF   (K'&USERMOD NE 7).MNOTE2                                 00095000
.CHECK2A ANOP                                                           00095500
         AIF   ('&DEFAULT' EQ 'YES').CHECK3                             00096000
         AIF   (N'&PGM EQ 0).MNOTE8                                     00096500
.CHECK2B ANOP                                                           00097000
         AIF   ('&DEFAULT' NE 'NO').MNOTE9                              00097500
.CHECK3  ANOP                                                           00098000
&INDEX   SETA  0                                                        00098500
.PRELOOP ANOP                                                           00099000
         AIF   (&INDEX EQ N'&PRE).CHECK4                                00099500
&INDEX   SETA  &INDEX+1                                                 00100000
         AIF   (K'&PRE(&INDEX) NE 7).MNOTE3                             00100500
         AGO   .PRELOOP                                                 00101000
.CHECK4  ANOP                                                           00101500
&INDEX   SETA  0                                                        00102000
.REQLOOP ANOP                                                           00102500
         AIF   (&INDEX EQ N'&REQ).CHECK5                                00103000
&INDEX   SETA  &INDEX+1                                                 00103500
         AIF   (K'&REQ(&INDEX) NE 7).MNOTE4                             00104000
         AGO   .REQLOOP                                                 00104500
.CHECK5  ANOP                                                           00105000
&INDEX   SETA  0                                                        00105500
.SUPLOOP ANOP                                                           00106000
         AIF   (&INDEX EQ N'&SUP).CHECK6                                00106500
&INDEX   SETA  &INDEX+1                                                 00107000
         AIF   (K'&SUP(&INDEX) NE 7).MNOTE5                             00107500
         AGO   .SUPLOOP                                                 00108000
.CHECK6  ANOP                                                           00108500
&INDEX   SETA  0                                                        00109000
.P01LOOP ANOP                                                           00109500
         AIF   (&INDEX EQ N'&PGM).CHECK7                                00110000
&INDEX   SETA  &INDEX+1                                                 00110500
         AIF   (K'&PGM(&INDEX) GT 8).MNOTE6                             00111000
         AIF   (K'&PGM(&INDEX) LT 1).MNOTE7                             00111500
         AGO   .P01LOOP                                                 00112000
.CHECK7  ANOP                                                           00112500
         AIF   (&ERROR EQ 0).GENTBL                                     00113000
         MEXIT                                                          00113500
.MNOTE1  ANOP                                                           00114000
         MNOTE 12,'ONLY ONE VALUE CAN BE SPECIFIED FOR USERMOD'         00114500
&ERROR   SETA  1                                                        00115000
         AGO   .CHECK3                                                  00115500
.MNOTE2  ANOP                                                           00116000
         MNOTE 12,'USERMOD VALUE &USERMOD IS NOT A VALID SYSMOD NAME'   00116500
&ERROR   SETA  1                                                        00117000
         AGO   .CHECK3                                                  00117500
.MNOTE3  ANOP                                                           00118000
         MNOTE 12,'PRE VALUE &PRE(&INDEX) IS NOT A VALID SYSMOD NAME'   00118500
&ERROR   SETA  1                                                        00119000
         AGO   .PRELOOP                                                 00119500
.MNOTE4  ANOP                                                           00120000
         MNOTE 12,'REQ VALUE &REQ(&INDEX) IS NOT A VALID SYSMOD NAME'   00120500
&ERROR   SETA  1                                                        00121000
         AGO   .REQLOOP                                                 00121500
.MNOTE5  ANOP                                                           00122000
         MNOTE 12,'SUP VALUE &SUP(&INDEX) IS NOT A VALID SYSMOD NAME'   00122500
&ERROR   SETA  1                                                        00123000
         AGO   .SUPLOOP                                                 00123500
.MNOTE6  ANOP                                                           00124000
         MNOTE 12,'&PGM(&INDEX) IS AN INVALID PROGRAM NAME'             00124500
&ERROR   SETA  1                                                        00125000
         AGO   .P01LOOP                                                 00125500
.MNOTE7  ANOP                                                           00126000
         MNOTE 12,'NULL PROGRAM NAME SPECIFIED'                         00126500
&ERROR   SETA  1                                                        00127000
         AGO   .P01LOOP                                                 00127500
.MNOTE8  ANOP                                                           00128000
         MNOTE 12,'PGM AND/OR DEFAULT=YES MUST BE SPECIFIED'            00128500
&ERROR   SETA  1                                                        00129000
         AGO   .CHECK2B                                                 00129500
.MNOTE9  ANOP                                                           00130000
         MNOTE 12,'VALUE FOR DEFAULT OPERAND INVALID (IT MUST BE YES  OX00130500
               R NO)'                                                   00131000
         AGO   .CHECK3                                                  00131500
&ERROR   SETA  1                                                        00132000
         AGO   .CHECK2B                                                 00132500
.GENTBL  ANOP                                                           00133000
         PUNCH '++USERMOD(&USERMOD) .'                                  00133500
         PUNCH '++VER(Z038) FMID(HMP1C00) '                        @S4C 00134000
.DOPRE   ANOP                                                           00134500
&INDEX   SETA  0                                                        00135000
&GENDONE SETA  0                                                        00135500
         AIF   (&INDEX EQ N'&PRE).PREGEND                               00136000
         PUNCH '     PRE('                                              00136500
&GENDONE SETA  1                                                        00137000
.PREGEN  ANOP                                                           00137500
         AIF   (&INDEX EQ N'&PRE).PREGEND                               00138000
&INDEX   SETA  &INDEX+1                                                 00138500
         PUNCH '         &PRE(&INDEX)'                                  00139000
         AGO   .PREGEN                                                  00139500
.PREGEND ANOP                                                           00140000
         AIF   (&GENDONE NE 1).DOREQ                                    00140500
         PUNCH '                )'                                      00141000
.DOREQ   ANOP                                                           00141500
&INDEX   SETA  0                                                        00142000
&GENDONE SETA  0                                                        00142500
         AIF   (&INDEX EQ N'&REQ).REQGEND                               00143000
         PUNCH '     REQ('                                              00143500
&GENDONE SETA  1                                                        00144000
.REQGEN  ANOP                                                           00144500
         AIF   (&INDEX EQ N'&REQ).REQGEND                               00145000
&INDEX   SETA  &INDEX+1                                                 00145500
         PUNCH '         &REQ(&INDEX)'                                  00146000
         AGO   .REQGEN                                                  00146500
.REQGEND ANOP                                                           00147000
         AIF   (&GENDONE NE 1).DOSUP                                    00147500
         PUNCH '                )'                                      00148000
.DOSUP   ANOP                                                           00148500
&INDEX   SETA  0                                                        00149000
&GENDONE SETA  0                                                        00149500
         AIF   (&INDEX EQ N'&SUP).SUPGEND                               00150000
         PUNCH '     SUP('                                              00150500
&GENDONE SETA  1                                                        00151000
.SUPGEN  ANOP                                                           00151500
         AIF   (&INDEX EQ N'&SUP).SUPGEND                               00152000
&INDEX   SETA  &INDEX+1                                                 00152500
         PUNCH '         &SUP(&INDEX)'                                  00153000
         AGO   .SUPGEN                                                  00153500
.SUPGEND ANOP                                                           00154000
         AIF   (&GENDONE NE 1).DOMOD                           @HMP1300 00154500
         PUNCH '                )'                                      00155000
.DOMOD   ANOP                                                           00155500
         PUNCH '                 .'                                     00156000
         PUNCH '++MOD(GIMUTTBL) DISTLIB(AOS12).'                        00156500
GIMUTTBL CSECT                                                          00157000
         TITLE 'GIMUTTBL, SMP/E VALID UTILITY TABLE'                    00157500
*/*A000000-999999                                            @Z21SP00*/ 00158000
*/********************************************************************/ 00158500
*/*                                                                  */ 00159000
*/* START_OF_SPECIFICATIONS ******************************************/ 00159500
*/*                                                                  */ 00160000
*/* MODULE_NAME = GIMUTTBL                                           */ 00160500
*/*                                                                  */ 00161000
*/* DESCRIPTIVE_NAME = SMP/E VALID UTILITY TABLE                     */ 00161500
*/*                                                                  */ 00162000
*/********************************************************************/ 00162500
*/*                                                                  */ 00163000
*/* COPYRIGHT = 5647-A01 (C) COPYRIGHT IBM CORPORATION 1982, 1998    */ 00163500
*/*            LICENSED MATERIALS - PROPERTY OF IBM                  */ 00164000
*/*                                                                  */ 00164500
*/********************************************************************/ 00165000
*/*                                                                  */ 00165500
*/* FUNCTION = PROVIDE A TABLE OF PROGRAM NAMES WHICH ARE CONSIDERED */ 00166000
*/*     VALID FOR SMP/E TO USE AS UTILITY FUNCTIONS.                 */ 00166500
*/*                                                                  */ 00167000
*/*     OPERATION =                                                  */ 00167500
*/*                                                                  */ 00168000
*/*        THIS MODULE IS A TABLE AND IS NON-EXECUTABLE.  IT IS LINK */ 00168500
*/*        EDITTED AS ONLY LOADABLE.                                 */ 00169000
*/*                                                                  */ 00169500
*/*        THE FORMAT OF THE TABLE IS AS FOLLOWS:                    */ 00170000
*/*                                                                  */ 00170500
*/*           OFFSET   LENGTH   DESCRIPTION                          */ 00171000
*/*            0 (0)      8     PROGRAM NAME - LEFT JUSTIFIED AND    */ 00171500
*/*                             PADDED WITH BLANKS                   */ 00172000
*/*            8 (8)      2     FLAG BYTES - CURRENTLY UNUSED        */ 00172500
*/*                                                                  */ 00173000
*/*        THE END OF THE TABLE IS INDICATED BY A 10 BYTE ENTRY      */ 00173500
*/*        OF X'FFFFFFFFFFFFFFFFFFFF'.                               */ 00174000
*/*                                                                  */ 00174500
*/*        THE TABLE SHIPPED WITH SMP/E IS A NULL LIST OR TABLE.     */ 00175000
*/*        I.E., THE FIRST ENTRY IS THE END OF TABLE INDICATOR.      */ 00175500
*/*        THE TABLE IN THIS MODULE BEGINS AT OFFSET 0.              */ 00176000
*/*                                                                  */ 00176500
*/*     PATCH_LABEL = PATCHTBL                                       */ 00177000
*/*                                                                  */ 00177500
*/* MODULE_TYPE = TABLE                                              */ 00178000
*/*                                                                  */ 00178500
*/*     PROCESSOR = BAL                                              */ 00179000
*/*                                                                  */ 00179500
*/*     ATTRIBUTES = OL                                              */ 00180000
*/*                                                                  */ 00180500
*/* ENTRY_POINT = GIMUTTBL                                           */ 00181000
*/*                                                                  */ 00181500
*/*                                                                  */ 00182000
*/* CHANGE_ACTIVITY                                                  */ 00182500
*/*                                                                  */ 00183000
*/*     11.00 - Z21SP00, INITIAL RELEASE OF GIMSMP           @Z21SP00*/ 00183500
*/*     12.00 - Z22SP00, SMP/E VERSION 1 RELEASE 2           @Z22SP00*/ 00184000
*/*     13.00 - HMP1300, SMP/E VERSION 1 RELEASE 3           @HMP1300*/ 00184500
*/*     14.00 - IR71858, SMP/E VERSION 1 RELEASE 4           @IR71858*/ 00185000
*/*     14.05 - IR71858, NEVER CHANGED FOR RELEASE 4         @IR71858*/ 00185500
*/*     15.00 - HMP1500, SMP/E VERSION 1 RELEASE 5           @HMP1500*/ 00186000
*/*           - PER0194, NEVER CHANGED FOR RELEASE 5         @PER0194*/ 00186500
*/*   15.1.00 - HMP1502, SMP/E VERSION 1 RELEASE 5.1         @HMP1502*/ 00187000
*/*     16.00 - HMP1600, SMP/E VERSION 1 RELEASE 6           @HMP1600*/ 00187500
*/*     16.04 - CHANGE ASSEMBLER DEFAULT - ASMBLR TO IEV90   @IR93967*/ 00188000
*/*     17.00 - HMP1700, SMP/E VERSION 1 RELEASE 7           @HMP1700*/ 00188500
*/*           - DCR1761,ADD IEWBLINK AND IGWSPZAP AS DEFAULT @DCR1761*/ 00189000
*/*           - DCR1764,IGWSPZAP NOT EXTERNALIZED            @DCR1764*/ 00189500
*/*                                                                  */ 00190000
*/* ---------------------------------------------------------------- */ 00190500
*/* FLAG REASON  RELEASE   DATE   ORG  DESCRIPTION                   */ 00191000
*/* ---- ------- ------- -------- ---  ----------------------------- */ 00191500
*/* $L1= HFS1800  18.00  03/13/93 KMH: OPEN MVS #2                   */ 00192000
*/* $D1= DCR1909  19.00  11/17/95 MUD: MAKE ASMA90 DEFAULT ASSEMBLER */ 00192500
*/*                                                                  */ 00193000
*/********************************************************************/ 00193500
*/*   CHANGE FLAG KEY: M@PNC                                         */ 00194000
*/*   WHERE:                                                         */ 00194500
*/*                                                                  */ 00195000
*/* M= MULTIPLE    P= DCRS = D,E,F,G    N= 0-9,A-Z     C= A = ADD    */ 00195500
*/*    FOR DELETE     HDWE = H,I,J,K                      C = CHANGE */ 00196000
*/*    FLAGS ONLY     LINE = L,M,N,O                      P = COPY   */ 00196500
*/*                   PTMS = P,Q,R,S                      M = MOVE   */ 00197000
*/*                   APARS = 0-9                         D = DELETE */ 00197500
*/*                                                                  */ 00198000
         MNOTE '*/*           - &USERMOD, USERMOD LEVEL'                00198500
*/*                                                                  */ 00199000
*/* END_OF_SPECIFICATIONS ********************************************/ 00199500
*/*                                                                  */ 00200000
*/********************************************************************/ 00200500
         EJECT                                                          00201000
         AIF   ('&DEFAULT' EQ 'NO').USERVAL                             00201500
*/********************************************************************/ 00202000
*/*                                                                  */ 00202500
*/* SMP/E DEFAULT UTILITY PROGRAMS                                   */ 00203000
*/*                                                                  */ 00203500
*/********************************************************************/ 00204000
         SPACE                                                          00204500
         DC    CL8'ASMA90'         ASSEMBLER UTILITY PROGRAM     @D1C   00205000
         DC    XL2'00'             RESERVED FLAG BYTES                  00205500
         SPACE                                                          00206000
         DC    CL8'IDCAMS'         AMS UTILITY PROGRAM                  00206500
         DC    XL2'00'             RESERVED FLAG BYTES                  00207000
         SPACE                                                          00207500
         DC    CL8'IEBCOPY'        COMPRESS/COPY UTILITY PROGRAM        00208000
         DC    XL2'00'             RESERVED FLAG BYTES                  00208500
         SPACE                                                          00209000
         DC    CL8'IEBUPDTE'       UPDATE UTILITY PROGRAM               00209500
         DC    XL2'00'             RESERVED FLAG BYTES                  00210000
         SPACE                                                          00210500
         DC    CL8'IEHIOSUP'       IOSUP UTILITY PROGRAM                00211000
         DC    XL2'00'             RESERVED FLAG BYTES                  00211500
         SPACE                                                          00212000
         DC    CL8'IEWL'           LINK EDIT UTILITY PROGRAM            00212500
         DC    XL2'00'             RESERVED FLAG BYTES                  00213000
         SPACE                                                          00213500
         DC    CL8'IEWBLINK'       LINK EDIT UTILITY PROGRAM @DCR1761   00214000
         DC    XL2'00'             RESERVED FLAG BYTES       @DCR1761   00214500
         SPACE                                                          00215000
         DC    CL8'IMASPZAP'       SUPERZAP UTILITY PROGRAM             00215500
         DC    XL2'00'             RESERVED FLAG BYTES                  00216000
         SPACE                                                          00216500
         DC    CL8'BPXCOPY'        HFSCOPY UTILITY PROGRAM       @L1A   00217000
         DC    XL2'00'             RESERVED FLAG BYTES           @L1A   00217500
         EJECT                                                          00218000
.USERVAL ANOP                                                           00218500
*/********************************************************************/ 00219000
*/*                                                                  */ 00219500
*/* USER DEFINED UTILITY PROGRAMS                                    */ 00220000
*/*                                                                  */ 00220500
*/********************************************************************/ 00221000
&INDEX   SETA  0                                                        00221500
.LOOP    ANOP                                                           00222000
         AIF   (&INDEX EQ N'&PGM).DONE                                  00222500
&INDEX   SETA  &INDEX+1                                                 00223000
         SPACE                                                          00223500
         DC    CL8'&PGM(&INDEX)'   UTILITY PROGRAM NAME                 00224000
         DC    XL2'00'             RESERVED FLAG BYTES                  00224500
         ORG                                                            00225000
         AGO   .LOOP                                                    00225500
.DONE    ANOP                                                           00226000
         SPACE                                                          00226500
*/* THIS ENTRY CANNOT BE USED; IT MARKS THE END OF THE TABLE         */ 00227000
GIMUTEND DC    X'FFFFFFFFFFFFFFFFFFFF'  END OF TABLE INDICATOR          00227500
         SPACE                                                          00228000
PATCH    DC    XL80'00'                 PATCH AREA                      00228500
         SPACE                                                          00229000
         DC    CL8'GIMUTTBL'       MODULE NAME                          00229500
         DC    CL7'&USERMOD'       MODIFICATION NUMBER                  00230000
         DC    CL8'&SYSDATE'       DATE                                 00230500
         DC    CL5'&SYSTIME'       TIME                                 00231000
         END   GIMUTTBL                                                 00231500
         MEND                                                           00232000
