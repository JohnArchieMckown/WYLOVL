         MACRO                                                          01000000
&LBL     MODCB &ACB=,&EXLST=,&RPL=,&MF=,&BUFND=,&BUFNI=,               -02000000
               &BUFSP=,&DDNAME=,&MACRF=,&PASSWD=,&STRNO=,              -03000000
               &AREA=,&AREALEN=,&ARG=,&ECB=,&KEYLEN=,&MSGAREA=,        -04000000
               &MSGLEN=,&NXTRPL=,&OPTCD=,&RECLEN=,&CATALOG=,&EODAD=,   -05000000
               &JRNAD=,&LERAD=,&SYNAD=,&TRANSID=,&BSTRNO=,      X04SVHS-06000000
               &CRA=,&MAREA=,&MLEN=,&JFCB=,                    @XM01140-06010000
               &USERPTR=,&SHRPOOL=,                                @L1C-06010300
               &AM=,&APPLID=,&LOGON=,        *** VTAM KEYWORDS***X03004-06010600
               &LOSTERM=,&RELREQ=,&ATTN=,                      @Z40BHUC-06010900
               &TPEND=,&AAREA=,&AAREALN=,&ARECLEN=,&EXIT=,       X03004-06011200
               &NSEXIT=,                                       @G40AKCM-06011500
               &NIB=,&BRANCH=,&NAME=,                           Y30ASHS-06011800
               &USERFLD=,&MODE=,&LISTEND=,&CONDN=,&PROC=,        X03004-06012100
               &CHNGDIR=,&STYPE=,&RTYPE=,&CHAIN=,&POST=,        X3004BS-06012400
               &RESPOND=,&CONTROL=,&OBSQAC=,&IBSQAC=,           X3004BS-06012700
               &OBSQVAL=,&IBSQVAL=,&SEQNO=,&SSENSEO=,           X3004BS-06013000
               &USENSEO=,&RESPLIM=,&SDT=,&SCIP=,&DFASY=,        X3004BS-06013300
               &RESP=,&BRACKET=,&SSENSMO=,                     @Z40BHUC-06013600
               &LOGMODE=,&BNDAREA=,&SIGDATA=,&CODESEL=,        @Z40BHUC-06013900
               &CRYPT=,&ENCR=,                                 @G40AKSL-06014100
               &RMODE31=                                           @LPA 06014300
.*                                                                      06014500
.* $MAC(MODCB):                                                         06014800
.*                                                                      06015100
.*PROPRIETARY V3 STATEMENT                                              06015300
.*LICENSED MATERIALS - PROPERTY OF IBM                                  06015500
.*"RESTRICTED MATERIALS OF IBM"                                         06015700
.*5695-DF1                                                              06015900
.*(C) COPYRIGHT 1983  IBM CORP.                                         06016100
.*END PROPRIETARY V3 STATEMENT                                          06016500
.*                                                                      06016900
.* STATUS = MVS/XA DFP RELEASE 1.1                                      06017200
.*                                                                      06017500
.* $L3=DFP,JDP1110,,STLJRB: MVS/XA DFP RELEASE 1.1 VSAM 31-BIT     @L3A 06017800
.* $LP=DFP,HDP2230,,STLJTP: MVS/XA DFP RELEASE 2.3 31/24 BIT       @LPA 06017900
.* APAR TO SHIP 31/24 BIT CODE                                 @YA04964 06018000
.* CHANGE CONTINUATION CHARACTER TO '-'                        @YA21786 06018100
.*                                                                      06018200
.* GLOBALS                                                              06018400
         GBLB  &IDACB01                ERROR INDICATOR                  06018700
         GBLA  &IDACB02                NUMBER OF FIELDS IN HEADER ACE   06019000
         GBLA  &IDACB03                LENGTH OF HEADER ACE             06019300
         GBLA  &IDACB04(5)             HEADER TYPE ARRAY                06019600
         GBLC  &IDACB05(5)             HEADER VALUE ARRAY               06019900
         GBLA  &IDACB06(5)             HEADER OFFSET ARRAY              06020200
         GBLA  &IDACB07(5)             HEADER FIELD LENGTH ARRAY        06020500
         GBLA  &IDACB11                OPERAND TYPE - SET BY IDACB1     06020800
         GBLC  &IDACB13                OPERAND VALUE - SET BY IDACB1    06021100
.* LOCALS                                                               06021400
         LCLA  &RS,&BA,&I              ARRAY INDICES                    06021700
         LCLC  &BLKTYPE                TYPE OF BLOCK TO BE MODIFIED     06022000
.* INITIALIZATION                                                       06022300
&IDACB01 SETB  0                       CLEAR ERROR INDICATOR            06022600
&IDACB02 SETA  2                       NUMBER OF HEADER FIELDS          06022900
&IDACB03 SETA  8                       HEADER LENGTH                    06023200
&RS      SETA  1                       RESERVED FIELD INDEX             06023500
&IDACB04(3) SETA 0                     CLEAR UNUSED PORTION             06023800
&IDACB04(4) SETA 0                       OF HEADER TYPE ARRAY           06024100
&BA      SETA  2                       BLOCK ADDRESS INDEX              06024400
.* TEST FOR POSITIONAL PARAMETERS                                       06024700
&I       SETA  N'&SYSLIST+1                                             06025000
.L10     ANOP                                                           06025300
&I       SETA  &I-1                                                     06025600
         AIF   (&I LE 0).L20                                            06025900
         IDAERMAC 1,&SYSLIST(&I)                                        06026200
         AGO   .L10                                                     06026500
.* VALIDITY CHECK AND TRANSFORM REQUEST KEYWORDS                        06026800
.L20     ANOP                          *** BLOCK TYPE AND ADDRESS ***   06027100
&IDACB04(&BA) SETA 0                                                    06027400
&IDACB06(&BA) SETA 4                                                    06027700
&IDACB07(&BA) SETA 4                                                    06028000
         AIF   ('&RPL' NE '').L30           CHECKING MUST BE DONE       06028300
         AIF   ('&ACB' NE '').L40           IN THIS ORDER BECAUSE       06028600
         AIF   ('&NIB' NE '').L60           OF USE OF BLOCK NAMESX03004 06028900
         AIF   ('&EXLST' NE '').L50         AS KEYWORDS                 06029200
         AIF   ('&MF(1)' EQ 'E').L80                                    06029500
         IDAERMAC 2,BLOCK-TYPE                                          06029800
&IDACB01 SETB  1                                                        06030100
         AGO   .L80                                                     06030400
.L30     IDACB1 ARG=&RPL,ARGNAME=RPL,MF=&MF                             06030700
&BLKTYPE SETC  'RPL'                                                    06031000
         AGO   .L70                                                     06031300
.L40     IDACB1 ARG=&ACB,ARGNAME=ACB,MF=&MF                             06031600
&BLKTYPE SETC  'ACB'                                                    06031900
         AGO   .L70                                                     06032200
.L50     IDACB1 ARG=&EXLST,ARGNAME=EXLST,MF=&MF                         06032500
&BLKTYPE SETC  'EXLST'                                                  06032800
         AGO   .L70                                              X03004 06033100
.L60     IDACB1 ARG=&NIB,ARGNAME=NIB,MF=&MF                      X03004 06033400
&BLKTYPE SETC  'NIB'                                             X03004 06033700
.L70     ANOP                                                           06034000
&IDACB04(&BA) SETA &IDACB11                                             06034300
&IDACB05(&BA) SETC '&IDACB13'                                           06034600
.L80     ANOP                          *** RESERVED FIELD ***           06034900
&IDACB04(&RS) SETA 0                                                    06035200
&IDACB06(&RS) SETA 2                                                    06035500
&IDACB07(&RS) SETA 2                                                    06035800
.L100    IDACB2 CALLER=MOD,BLKTYPE=&BLKTYPE,LABEL=&LBL,MF=&MF,         -06036100
               BUFND=&BUFND,BUFNI=&BUFNI,BUFSP=&BUFSP,DDNAME=&DDNAME,  -06036400
               EXLST=&EXLST,MACRF=&MACRF,PASSWD=&PASSWD,STRNO=&STRNO,  -06036700
               ACB=&ACB,AREA=&AREA,AREALEN=&AREALEN,                   -06037000
               ARG=&ARG,ECB=&ECB,KEYLEN=&KEYLEN,MSGAREA=&MSGAREA,      -06037300
               MSGLEN=&MSGLEN,NXTRPL=&NXTRPL,OPTCD=&OPTCD,             -06037600
               RECLEN=&RECLEN,CATALOG=&CATALOG,EODAD=&EODAD,           -06037900
               JRNAD=&JRNAD,LERAD=&LERAD,SYNAD=&SYNAD,           X03004-06038200
               USERPTR=&USERPTR,JFCB=&JFCB,SHRPOOL=&SHRPOOL,       @L3C-06038500
               APPLID=&APPLID,LOGON=&LOGON, *** VTAM KEYWORDS ***X03004-06038800
               LOSTERM=&LOSTERM,RELREQ=&RELREQ,ATTN=&ATTN,     @Z40BHUC-06039100
               TPEND=&TPEND,                                   @Z40BHUC-06039400
               NSEXIT=&NSEXIT,                                 @G40AKCM-06039700
               AAREA=&AAREA,AAREALN=&AAREALN,ARECLEN=&ARECLEN,   X03004-06040000
               EXIT=&EXIT,                                      Y30ASHS-06040300
               NIB=&NIB,BRANCH=&BRANCH,NAME=&NAME,               X03004-06040600
               USERFLD=&USERFLD,MODE=&MODE,LISTEND=&LISTEND,     X03004-06040900
               CONDN=&CONDN,PROC=&PROC,AM=&AM,                   X03004-06041200
               CHNGDIR=&CHNGDIR,STYPE=&STYPE,RTYPE=&RTYPE,      X3004BS-06041500
               CHAIN=&CHAIN,POST=&POST,RESPOND=&RESPOND,        X3004BS-06041800
               CONTROL=&CONTROL,OBSQAC=&OBSQAC,IBSQAC=&IBSQAC,  X3004BS-06042100
               OBSQVAL=&OBSQVAL,IBSQVAL=&IBSQVAL,SEQNO=&SEQNO,  X3004BS-06042400
               SSENSEO=&SSENSEO,USENSEO=&USENSEO,               X3004BS-06042700
               RESPLIM=&RESPLIM,SDT=&SDT,SCIP=&SCIP,            X3004BS-06043000
               DFASY=&DFASY,RESP=&RESP,                        @Z40BHUC-06043300
               BRACKET=&BRACKET,SSENSMO=&SSENSMO,               X04SVHS-06043600
               TRANSID=&TRANSID,CRA=&CRA,BSTRNO=&BSTRNO,        X04SVHS-06043900
               MAREA=&MAREA,MLEN=&MLEN,                         X04SVHS-06044200
               LOGMODE=&LOGMODE,BNDAREA=&BNDAREA,              @Z40BHUC-06044500
               SIGDATA=&SIGDATA,CODESEL=&CODESEL,              @Z40BHUC-06044800
               CRYPT=&CRYPT,ENCR=&ENCR,                        @G40AKSL-06045000
               RMODE31=&RMODE31                                    @LPA 06045200
         MEND                                                           06045400
