         MACRO                                                          00050000
         IST6MCRE &AREA=,&AREALEN=,&QUALIFY=,&CD=,&CONVID=,&CONMODE=,  X00100000
               &BRANCH=,&ECB=,&EXIT=,&OPTCD=,&FILL=,&CONXMOD=,         X00150000
               &INVALP1=,&INVALP2=,&VTRINA=,&VTRINL=,&VTROUTA=,        X00183300
               &VTROUTL=                                           @D1C 00216600
.*                                                                      00250000
.* /***** START OF SPECIFICATIONS ************************************/ 00300000
.* /*                                                                */ 00350000
.* /*  MACRO NAME: IST6MCRE                                          */ 00400000
.* /*                                                                */ 00450000
.* /*  DESCRIPTIVE NAME: CONTROL = RECEIVE PROCESSOR                 */ 00500000
.* /*                                                                */ 00550000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00600000
.* /*                                                                */ 00650000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00700000
.* /*                                                                */ 00750000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1994.       */ 00800000
.* /*             ALL RIGHTS RESERVED.                               */ 00850000
.* /*                                                                */ 00900000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00950000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01000000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01050000
.* /*                                                                */ 01100000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01150000
.* /*                                                                */ 01200000
.* /*                                                                */ 01250000
.* /*  FUNCTION:                                                     */ 01300000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01350000
.* /*      CONTROL=RECEIVE PARAMETER ON THE APPCCMD MACRO.           */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*                                                                */ 01550000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01600000
.* /*                                                                */ 01650000
.* /*  CREATED BY:                                                   */ 01700000
.* /*                                                                */ 01750000
.* /*  INTERNAL MACROS:                                              */ 01800000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */ 01850000
.* /*                                                                */ 01900000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01950000
.* /*                                                                */ 02000000
.* /*  CHANGE ACTIVITY:                                              */ 02050000
.* /*                                                                */ 02100000
.* /*    07/11/88 - ADD CD= KEYWORD OPERAND FOR RECEIVE      @P058398*/ 02150000
.* /*    07/29/88 - FIX CD= FOR DEFER SETTING                @P058899*/ 02200000
.* /*                                                                */ 02250000
.* /*  $MAC(IST6MCRE),COMP(API),PROD(VTAM): CONTROL=RECEIVE PROCESSOR*/ 02300000
.* /*                                                                */ 02350000
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */ 02400000
.* /*  ---- -------- ------- ------ ------  ---------------------    */ 02450000
.* /*  $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION         */ 02500000
.* /*  $I1= BR@@@MST APPCE01 911108 510689: APPC Receive Immediate   */ 02550000
.* /*  $I2= FF@@@MST APPCFDX 911118 879315: APPC Full-Duplex support */ 02600000
.* /*  $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services    */ 02650000
.* /*  $U2= P111572  GENRES  931029 879315: Split INVALKW keyword    */ 02700000
.* /*  $L2= KA@DR006 APPCRR  940713 576146: Vector Format            */ 02750000
.* /*  $D1= ID@@@MST FPAPI   941005 510689: Fast Path API for APPC   */ 02775000
.* /*                                                                */ 02800000
.* /***** END OF SPECIFICATIONS **************************************/ 02850000
.*                                                                      02900000
.* A-000000-999999                                             @R495810 02950000
.*                                                                      03000000
         GBLB  &ISTGEN                                                  03050000
         GBLB  &ISTRE                                                   03100000
         AIF   (&ISTRE EQ 1).REINVKW                                    03150000
*                                                                       03200000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03350000
*                                                                       03400000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03450000
*                                                                       03500000
*             5695-117 (C) COPYRIGHT IBM CORP. 1987, 1993.              03550000
*             ALL RIGHTS RESERVED.                                      03600000
*                                                                       03650000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03700000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03750000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03800000
*                                                                       03850000
*             SEE COPYRIGHT INSTRUCTIONS.                               03900000
*                                                                       03950000
&ISTRE   SETB  1                                                        04000000
.REINVKW ANOP                                                           04050000
         IST6MCUC RECEIVE,CONTROL,INVALKW=&INVALP1                 @U2C 04100000
         IST6MCUC RECEIVE,CONTROL,INVALKW=&INVALP2                 @U2A 04150000
.*                                             CHECK INVALID APPCCMD    04200000
.REAREA  ANOP                       ** AREA= PROCESSING **              04250000
         AIF   ('&AREA' EQ '').REALEN                                   04300000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X04350000
               KW=AREA_ADDRESS                                          04400000
.*                                             PUT AREA ADDRESS IN RPL  04450000
.REALEN  ANOP                       ** AREALEN= PROCESSING **           04500000
         AIF   ('&AREALEN' EQ '').REQFY                                 04550000
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X04600000
               KW=AREA_LENGTH                                           04650000
.*                                             PUT AREA LENGTH IN RPL   04700000
.REQFY   ANOP                       ** QUALIFY= PROCESSING **           04750000
         AIF   ('&QUALIFY' EQ '').REID                                  04800000
.REQFY1  ANOP                                                           04850000
         AIF   ('&QUALIFY' NE 'ANY').REQFY2                             04900000
         IST6MCUC ANY,QUALIFY,INVALKW=(FILL=&FILL,CONVID=&CONVID)       04950000
         AIF   (NOT &ISTGEN).RECOMKW                                    05000000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ANY                            05050000
*                                         PUT ANY CODE IN APPC AREA     05100000
         AGO   .RECOMKW                                                 05150000
.REQFY2  ANOP                                                           05200000
         AIF   ('&QUALIFY' NE 'IANY').REQFY3                       @I1A 05250000
         IST6MCUC IANY,QUALIFY,INVALKW=(FILL=&FILL,CONVID=&CONVID) @I1A 05300000
         AIF   (NOT &ISTGEN).RECOMKW                               @I1A 05350000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6IANY                      @I1A 05400000
*                                      PUT IANY CODE IN APPC AREA  @I1A 05450000
         AGO   .RECOMKW                                            @I1A 05500000
.REQFY3  ANOP                                                      @I1A 05550000
         AIF   ('&QUALIFY' NE 'SPEC').REQFY4                       @I1C 05600000
         AIF   (NOT &ISTGEN).REFILL                                     05650000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SPEC                           05700000
*                                         PUT SPEC CODE IN APPC AREA    05750000
         AGO   .REID                                                    05800000
.REQFY4  ANOP                                                      @I1A 05850000
         AIF   ('&QUALIFY' NE 'ISPEC').REQFYER                     @I1A 05900000
         AIF   (NOT &ISTGEN).REFILL                                @I1A 05950000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ISPC                      @I1A 06000000
*                                      PUT ISPEC CODE IN APPC AREA @I1A 06050000
         AGO   .REID                                               @I1A 06100000
.REQFYER ANOP                                                      @I1A 06150000
         IST6MCER 5,QUALIFY,&QUALIFY                                    06200000
.REID    ANOP                       ** CONVID= PROCESSING **            06250000
         AIF   ('&CONVID' EQ '').REFILL                                 06300000
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X06350000
               KW=CONVID                                                06400000
.*                                              PUT CONVID IN APPC AREA 06450000
.REFILL  ANOP                       ** FILL= PROCESSING **              06500000
         AIF   ('&FILL' EQ '').RECOMKW                                  06550000
         AIF   ('&FILL' NE 'LL').REFIL1                                 06600000
         AIF   (NOT &ISTGEN).RECOMKW                                    06650000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6LL  SET FILL TO LL             06700000
         AGO   .RECOMKW                                                 06750000
.REFIL1  ANOP                                                           06800000
         AIF   ('&FILL' NE 'BUFF').REFIL2                               06850000
         AIF   (NOT &ISTGEN).RECOMKW                                    06900000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6LL                       06950000
*                                            SET FILL TO BUFF           07000000
         AGO   .RECOMKW                                                 07050000
.REFIL2  ANOP                                                           07100000
         IST6MCER 5,FILL,&FILL                                          07150000
.RECOMKW ANOP                       ** COMMON KEYWORDS **               07200000
         AIF   ('&CD' EQ '').RECD0                             @P058398 07250000
         AIF   ('&CD' NE 'IMMED').RECD1                        @P058398 07300000
.RECD0   AIF   (NOT &ISTGEN).RECONMD                           @P058398 07350000
         NI    RPL6FLG1-ISTRPL6X(14),X'FF'-RPL6CDDE            @P058899 07400000
*                                            SET CD TO IMMED   @P058398 07450000
         AGO   .RECONMD                                        @P058398 07500000
.RECD1   ANOP                                                  @P058398 07550000
         AIF   ('&CD' NE 'DEFER').RECD2                        @P058398 07600000
         AIF   (NOT &ISTGEN).RECONMD                           @P058398 07650000
         OI    RPL6FLG1-ISTRPL6X(14),RPL6CDDE                           07700000
*                                            SET CD TO DEFER   @P058899 07750000
         AGO   .RECONMD                                        @P058398 07800000
.RECD2   ANOP                                                  @P058398 07850000
         IST6MCER 5,CD,&CD                                     @P058398 07900000
.RECONMD ANOP                       ** CONMODE= PROCESSING **  @P058398 07950000
         IST6MCUP CONMODE=&CONMODE,OPTCD=&OPTCD,ECB=&ECB,              X08000000
               EXIT=&EXIT,BRANCH=&BRANCH,CONXMOD=&CONXMOD,             X08050000
               CKCMODE=YES,        CHECK CONMODE & CONXMOD PARAMETERS  X08100000
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX08150000
               CKBUFF=OBUF         XBUFLST/NBUFFLST IS        @I2C @D1C 08200000
.*                                 VALID OPTCD PARARAMETER              08250000
.*                                     ENSURE BOTH PARMS ARE CODED @L1A 08300000
.*                                     OR BOTH ARE NOT CODED       @L1A 08350000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).REVOUT    @D1C 08400000
         AIF   (('&VTRINA' EQ '') OR  ('&VTRINL' EQ '')).REERVI    @D1C 08450000
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X08600000
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C 08650000
.*                                     STORE VECTOR AREA INPUT ADDRESS  08700000
.*                                                                 @L1A 08750000
.*                                  ** VTRINL = PROCESSING **      @L1A 08800000
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X08850000
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C 08900000
.*                                     STORE VECTOR AREA INPUT LENGTH   08950000
.*                                                                 @L1A 09000000
         AGO   .REVOUT                                             @D1C 09018400
.REERVI  ANOP                                                      @L1A 09036800
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C 09055200
.REVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED      09073600
.*                                     OR BOTH ARE NOT CODED       @D1A 09092000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).REREQ   @D1A 09110400
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).REERVO   @D1A 09128800
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X09147200
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A 09165600
.*                                     PUT VECTOR AREA INPUT ADDRESS    09184000
.*                                     IN APPC AREA                @D1A 09202400
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X09220800
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A 09239200
.*                                     PUT VECTOR AREA INPUT LENGTH     09257600
.*                                     IN APPC AREA                @D1A 09276000
         AGO   .REREQ                                                   09294400
.REERVO  ANOP                                                      @D1A 09312800
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A 09331200
.REREQ   ANOP                       ** MACRO REQUEST TYPE **            09350000
         AIF   (NOT &ISTGEN).REEXIT                                     09400000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RCV                             09450000
*                               PUT RECEIVE REQUEST CODE IN APPC AREA   09500000
.REEXIT  MEND                                                           09550000
