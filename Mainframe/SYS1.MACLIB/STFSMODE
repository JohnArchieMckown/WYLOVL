         MACRO                                                          00050000
&NAME    STFSMODE &A,&INITIAL=NO,&RSHWKEY=64,&NOEDIT=NO,&PARTION=NO     00100000
.********************************************************************** 00150000
.*                                                                    * 00200000
.* STATUS - OS/VS2 TSO/VTAM DYNAMIC RESHOW LEVEL 3.0                  * 00250000
.*                                                                    * 00300000
.* NAME - STFSMODE  (SET FULL SCREEN MODE)                            * 00350000
.*                                                                    * 00355000
.*      COPYRIGHT = LICENSED MATERIALS - PROPERTY OF IBM              * 00360000
.*                                                                    * 00365000
.*                  "RESTRICTED MATERIALS OF IBM"                     * 00370000
.*                                                                    * 00375000
.*                  5695-117                                          * 00380000
.*                                                                    * 00385000
.*                  (C) COPYRIGHT IBM CORP. 1979                      * 00390000
.*                                                                    * 00395000
.*                                                                    * 00400000
.* FUNCTION - THE STFSMODE MACRO IS USED TO SPECIFY WHETHER OR NOT    * 00450000
.*            AN APPLICATION IS PRESENTING DISPLAYS TO A TSO/VTAM     * 00500000
.*            DISPLAY TERMINAL IN FULL SCREEN MODE.                   * 00550000
.*                                                                    * 00600000
.*            WHEN INITIALLY SETTING THE MODE ON, INITIAL=YES WILL    * 00650000
.*            PREVENT PAGING FOR THE FIRST FULL SCREEN TPUT ISSUED BY * 00700000
.*            THE APPLICATION.                                        * 00750000
.*                                                                    * 00800000
.*            RSHWKEY IS USED TO SPECIFY WHICH PFKEY IS TO BE USED    * 00850000
.*            AS THE RESHOW KEY.  E.G., RSHWKEY=3 MEANS THAT PFKEY 3  * 00900000
.*            IS TO BE THE RESHOW KEY.  WHEN STFSMODE IS OFF,         * 00950000
.*            RSHWKEY IS NOT TO BE SPECIFIED.  WHEN STFSMODE IS ON    * 01000000
.*            AND NO RSHWKEY HAS BEEN SPECIFIED, RSHWKEY WILL DEFAULT * 01050000
.*            TO ZERO.                                                * 01100000
.*                                                                    * 01150000
.*            NOEDIT=YES WILL CREATE A MODE IN WHICH ALL INPUT        * 01200000
.*            RECEIVED FROM THE TERMINAL WILL PASS THROUGH VTIOC      * 01250000
.*            UNINSPECTED AND UNMODIFIED.  NOEDIT=NO WILL BE THE      * 01300000
.*            DEFAULT.                                                * 01350000
.*                                                                    * 01400000
.*            PARTION=YES WILL CREATE A MODE IN WHICH THE COMMAND     * 01406200
.*            PROCESSOR CAN MAKE USE OF THE 16-BIT ADDRESSING SCHEME. * 01412400
.*            WHEN PARTION=YES IS SPECIFIED, NO EDITING OF THE ADDRESS* 01418600
.*            BYTES WILL OCCUR AND THE WCC WILL BE SENT AS IS (I.E.   * 01424800
.*            IT WILL NOT BE MADE PRINTABLE).  PARTION=NO WILL BE THE * 01431000
.*            DEFAULT.                                                * 01437200
.*                                                                    * 01443400
.*       THE MACRO PROTOTYPE IS ABOVE                                 * 01450000
.*                                                                    * 01500000
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  * 01550000
.*&NAME  LA    1,128+64+32+16           SET FLAGS FOR MODE, INITIAL   * 01600000
.**                                     CALL, NOEDIT, AND PARTITION     01650000
.*       SLL   1,24                     SHIFT TO HIGH ORDER BYTE      * 01700000
.*       LA    0,10                     LOAD RSHWKEY                  * 01750000
.*       OR    1,0                      RSHWKEY IN RIGHTMOST BYTE     * 01800000
.*       LA    0,18                     LOAD ENTRY CODE               * 01850000
.*       SLL   0,24                     SHIFT TO HIGH ORDER BYTE      * 01900000
.*       SVC   94                       TERMINAL CONTROL MACRO SVC    * 01950000
.*                                                                    * 02000000
.********************************************************************** 02050000
         LCLA  &SYSFS                                                   02100000
         LCLA  &SYSINIT                                                 02150000
         LCLA  &SYSNEDT                                                 02170000
         LCLA  &SYSPART                                                 02185000
&SYSFS   SETA  128                                                      02200000
&SYSINIT SETA  0                                                        02250000
&SYSNEDT SETA  0                                                        02300000
&SYSPART SETA  0                                                        02325000
         AIF   ('&INITIAL' NE 'YES' AND '&INITIAL' NE 'NO').ERROR1      02350000
         AIF   ('&PARTION' NE 'YES' AND '&PARTION' NE 'NO').ERROR6      02375000
         AIF   ('&NOEDIT' NE 'YES' AND '&NOEDIT' NE 'NO').ERROR5        02400000
         AIF   ('&NOEDIT' EQ 'YES' AND '&A' EQ 'OFF').ERROR4            02450000
         AIF   (&RSHWKEY GT 0 AND &RSHWKEY LT 25).TESTFS                02500000
         AIF   (&RSHWKEY NE 64).ERROR2                                  02550000
         AGO   .NEXT                                                    02600000
.TESTFS  ANOP                                                           02650000
         AIF   ('&A' EQ 'OFF').ERROR3                                   02700000
.NEXT    ANOP                                                           02750000
         AIF   ('&INITIAL' EQ 'NO').TESTNE                              02800000
&SYSINIT SETA  64                                                       02850000
.TESTNE  ANOP                                                           02900000
         AIF   ('&NOEDIT' EQ 'NO').TESTPA                               02950000
&SYSNEDT SETA  32                                                       03000000
.TESTPA  ANOP                                                           03012500
         AIF   ('&PARTION' EQ 'NO').TESTA                               03025000
&SYSPART SETA  16                                                       03037500
.TESTA   ANOP                                                           03050000
         AIF   ('&A' EQ 'OFF').FSOFF                                    03150000
         AGO   .FSON                                                    03175000
.ERROR   IHBERMAC 36,,&A                                                03200000
         MEXIT                                                          03250000
.ERROR1  IHBERMAC 54,,INITIAL                                           03300000
         MEXIT                                                          03350000
.ERROR2  IHBERMAC 54,,RSHWKEY                                           03400000
         MEXIT                                                          03450000
.ERROR3  IHBERMAC 1020,RSHWKEY,&A                                       03500000
         MEXIT                                                          03550000
.ERROR4  IHBERMAC 1020,NOEDIT,&A                                        03600000
         MEXIT                                                          03650000
.ERROR5  IHBERMAC 54,,NOEDIT                                            03700000
         MEXIT                                                          03750000
.ERROR6  IHBERMAC 54,,PARTION                                           03766600
         MEXIT                                                          03783200
.FSON    ANOP                                                           03800000
&NAME    LA    1,&SYSFS+&SYSINIT+&SYSNEDT+&SYSPART SET FLAGS FOR MODE,  03850000
*                                       CALL, NOEDIT AND PARTITION      03900000
         SLL   1,24                     SHIFT TO HIGH ORDER BYTE        03950000
         AGO   .PFCD                                                    04000000
.FSOFF   ANOP                                                           04050000
&NAME    SR    1,1                      CLEAR PARAMETER REGISTER        04100000
         AIF   ('&PARTION' NE 'YES').PFCD                               04116600
&NAME    LA    1,&SYSPART               FS MODE OFF, PARTION=YES        04133200
.PFCD    ANOP                                                           04150000
         AIF   ('&A' EQ 'OFF').ENTRYCD                                  04200000
         AIF   (&RSHWKEY EQ 64).ENTRYCD                                 04250000
         LA    0,&RSHWKEY               LOAD RSHWKEY                    04300000
         OR    1,0                      RSHWKEY IN RIGHTMOST BYTE       04350000
.ENTRYCD ANOP                                                           04400000
         LA    0,18                     LOAD ENTRY CODE                 04450000
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE        04500000
         SVC   94                       TERMINAL CONTROL MACRO SVC      04550000
         MEND                                                           04600000
