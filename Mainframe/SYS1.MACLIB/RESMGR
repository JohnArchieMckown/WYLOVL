*%/* IF PLS, EXCLUDE THE BAL FORM OF THE MACRO                          00025000
         MACRO                                                          06900500
&LABEL   RESMGR &REQUEST,&TYPE=,&ASID=,&TCB=,&ROUTINE=,&TOKEN=,        *06901000
               &ECB=,&TTOKEN=,&PARAM=,&MF=I,&RELATED=             @L1C  06901500
.*                                                                      06901600
.*     *************** START OF SPECIFICATIONS **********************   07151300
.*   MACRO-NAME = RESMGR                                                07151400
.*                                                                      07151500
.*   DESCRIPTIVE-NAME = DYNAMIC RESOURCE MANAGER                        07151600
.*                                                                      07151700
.*   COPYRIGHT = 5745-001                                               07151800
.*               LICENSED MATERIALS - PROPERTY OF IBM                   07151900
.*               THIS MODULE IS "RESTRICTED MATERIALS OF IBM"           07152000
.*               (C) COPYRIGHT IBM CORP. 1988, 1996                     07152100
.*               SEE COPYRIGHT INSTRUCTIONS                             07152200
.*                                                                      07152400
.*   STATUS = HBB6603                                                   07152500
.*                                                                      07152600
.*   EXTERNAL CLASSIFICATION = GUPI                                     07152700
.*                                                                      07153000
.*   LIBRARY = MACLIB                                                   07153100
.*                                                                      07153200
.*   FUNCTION = The RESMGR macro provides the interface to the          07153300
.*              dynamic resource manager (RESMGR) service.              07153400
.*              The macro checks the parameters for the macro form      07153500
.*              specified and generates the parameter list for          07153600
.*              the service.  The macro can be invoked in either        07153700
.*              BAL or PL/S.                                            07153800
.*                                                                      07153900
.*   PROCESSOR = ASSEMBLER H                                            07154000
.*                                                                      07154100
.*   INPUT = Consists of one positional parameter and nine              07154200
.*           keywords using the syntax described below. The             07154300
.*           combinations of parameter and keywords for the             07154400
.*           various macro forms are also described below.              07154500
.*                                                                      07154600
.*      SYNTAX =   label RESMGR request                                 07154700
.*                              ,TYPE = ADDRSPC|TASK                    07154800
.*                              ,ASID = CURRENT|ALL|op1                 07154900
.*                              ,TCB  = CURRENT|ALL|op2                 07155000
.*                              ,TTOKEN = op2                    @L1A   07155100
.*                              ,ROUTINE = (LINK,op3|BRANCH,op2|        07155200
.*                                          PC,op4)                     07155300
.*                              ,TOKEN = op2                            07155400
.*                              ,ECB = op2                              07155500
.*                              ,PARAM = op2                            07155600
.*                              ,MF = I|L|(E,op5)                       07155700
.*                              ,RELATED = op6                          07155800
.*                                                                      07155900
.*   ---------------------------------------------------------------    07156000
.*   | macro    |                |                |                |    07156100
.*   | form --> |  List          |  Standard      |  Execute       |    07156200
.*   | ---------| -------------- | -------------- | -------------- |    07156300
.*   | op1      | Constant       | Constant or    | Constant or    |    07156400
.*   |          |                | Register       | Register       |    07156500
.*   |          |                |  (2) - (12)    |  (2) - (12)    |    07156600
.*   | ----------------------------------------------------------- |    07156700
.*   | op2      | A-type address | A-type address | Rx-type address|    07156800
.*   |          |                | or Register    | or Register    |    07156900
.*   |          |                |  (2) - (12)    |  (2) - (12)    |    07157000
.*   | ----------------------------------------------------------- |    07157100
.*   | op3      | C-type constant| C-type constant| C-type constant|    07157200
.*   |          | or A-type      | or A-type      | or Rx-type     |    07157300
.*   |          | address        | address or     | address or     |    07157400
.*   |          |                | Register       | Register       |    07157500
.*   |          |                |  (2) - (12)    |  (2) - (12)    |    07157600
.*   | ----------------------------------------------------------- |    07157700
.*   | op4      | Constant       | Constant or    | Constant or    |    07157800
.*   |          |                | Register (2) - | Expression or  |    07158000
.*   |          |                |  (12)          | Register (2) - |    07158100
.*   |          |                |                |  (12)          |    07158200
.*   | ----------------------------------------------------------- |    07158300
.*   | op5      | Rx-type address or Register (2) - (12)           |    07158400
.*   | ----------------------------------------------------------- |    07158500
.*   | op6      | May be any valid keyword value for all forms     |    07158600
.*   ---------------------------------------------------------------    07158700
.*                                                                      07158800
.*       ---------------------------------------                        07158900
.*       |               |          |          |                        07159000
.*       |  Request -->  |     A    |    D     |                        07159100
.*       |               |     D    |    E     |                        07159200
.*       |  Keyword      |     D    |    L     |                        07159300
.*       |     |         |          |    E     |                        07159400
.*       |     |         |          |    T     |                        07159500
.*       |     |         |          |    E     |                        07159600
.*       |     V         |          |          |                        07159700
.*       |---------------|----------|----------|                        07159800
.*       | request ......|    A     |    A     |                        07159900
.*       | TYPE .........|    D     |    D     |                        07160000
.*       | ASID .........|    D     |    D     |                        07160100
.*       | TCB ..........|    D     |    D     |                        07160200
.*       | TTOKEN........|    O     |    O     |                 @L1A   07160300
.*       | ROUTINE ......|    R     |          |                        07160400
.*       | TOKEN ........|    R     |    R     |                        07160500
.*       | ECB ..........|          |    O     |                        07160600
.*       | PARAM ........|    O     |          |                        07160700
.*       | MF ...........|    S     |    S     |                        07160800
.*       | RELATED ......|    O     |    O     |                        07160900
.*       ---------------------------------------                        07161000
.*                                                                      07161100
.*                A = Required for list and standard macro forms        07161200
.*                D = Default value supplied if not specified for       07161300
.*                    list or standard form                             07161400
.*                O = Optional keyword                                  07161500
.*                R = Required for standard form                        07161600
.*                S = Default value supplied if not specified           07161700
.*            blank = Error if specified                                07161800
.*                                                                      07161900
.*            Default TYPE is ADDRSPC                                   07162000
.*            Default ASID is CURRENT                                   07162100
.*            Default TCB is CURRENT                                    07162200
.*            Default MF is standard form                               07162300
.*            Note:  When using the TTOKEN keyword, the parameter       07162400
.*                   address provided should NOT be the address of      07162500
.*                   STCBTTKN.  You may, instead, copy STCBTTKN and     07162600
.*                   provide the address of the duplicate.         @L1A 07162700
.*                                                                      07162800
.*                                                                      07162900
.*    OUTPUT =  The macro builds a parameter list for the standard      07163000
.*              and list forms as described below.                      07163100
.*                                                                      07163200
.*      MASK/       HEX                                                 07163300
.*       LENGTH     OFFSET    COMMENTS                                  07163400
.*                                                                      07163500
.*          1         0       Parameter list version number             07163600
.*          1         1       Flags                                     07163700
.*      x'80'         0       0 - TYPE=ADDRSPC was specified            07163800
.*                            1 - TYPE=TASK was specified               07163900
.*      X'40'         0       1 - TCB=ALL was specified                 07164000
.*      x'20'         0       1 - ASID=ALL was specified                07164100
.*                                                                      07164200
.*          1         2       Linkage type                              07164300
.*                            01 - ROUTINE=(LINK,... was specified      07164400
.*                            02 - ROUTINE=(BRANCH,.) was specified     07164500
.*                            03 - ROUTINE=(PC,...) was specified       07164600
.*                                                                      07164700
.*          1         3       Request type                              07164800
.*                            01 - ADD request                          07164900
.*                            02 - DELETE request                       07165000
.*                                                                      07165100
.*          2         4       Reserved                                  07165200
.*                                                                      07165300
.*          2         6       Half-word ASID                            07165400
.*                                                                      07165500
.*          4         8       TCB address                               07165600
.*                                                                      07165700
.*          4         C       ROUTINE routine-name or routine-          07165800
.*                                    address or pc-number              07165900
.*                                                                      07166000
.*          4        10       TOKEN address                             07166100
.*                                                                      07166200
.*          4        14       ECB address                               07166300
.*                                                                      07166400
.*          4        18       PARAM address                             07166500
.*                                                                      07166600
.*          4        1C       TTOKEN address                     @L1A   07166700
.*                                                                      07166800
.*   External-Macros = IHBINNRR                                         07166900
.*                                                                      07167000
.*   CHANGE-ACTIVITY =                                                  07167100
.*       $L0= AR       HBB3310 850627 PDT8: AR SUPPORT                  07167200
.*       $L1= COUPLRTM HBB4410 870831 PDAY: TTOKEN keyword support      07177300
.*       $01= OY37924  HBB4410 901101 PDXB: Fix 24-bit mode BAL         07182300
.*       $P1= PQC1066  HBB6603 960408 PDKD: TTOKEN vs PARAM - this      07184000
.*                                          was FIN APAR OW19726        07185700
.*                                                                      07187400
.*   **************** END OF SPECIFICATIONS **********************      07197500
.*                                                                      07207600
.*********************************************************************  07218100
.*                                                                   *  07218200
.*                 DECLARE LOCAL VARIABLES                           *  07218300
.*                                                                   *  07218400
.*********************************************************************  07218500
.*                                                                      07218600
.*  LOCAL VARIABLE CODES FOR ROUTINE TYPE AND VERSION                   07218700
         LCLA   &RTNCODE,&VERNUM                                        07218800
.*  FLAGS TO TURN ON BITS IN BYTE 1 OF PARAMETER LIST                   07218900
         LCLB  &B(8)                                                    07219000
.*  FLAGS TO TURN OFF BITS IN BYTE 1 OF PARAMETER LIST                  07219100
         LCLB  &C(3)                                                    07219200
.*  MISCELLANEOUS LOCAL VARIABLES                                       07219300
         LCLB  &ERRFLAG                    LOCAL FLAG FOR ERRORS        07219400
         LCLB  &NOKEYS                     LOCAL FLAG FOR KEYWORDS      07219500
         LCLB  &ROUT1                      HOLDS ROUTINE NUMBER         07219600
         LCLB  &ROUT2                      HOLDS ROUTINE NUMBER         07219700
         LCLB  &ROUT3                      HOLDS ROUTINE NUMBER         07219800
         LCLC  &XLAB                       LOCAL LABEL                  07219900
         LCLC  &LABELX                     LOCAL LABEL                  07220000
         LCLC  &MFCODE                     MACRO FORM                   07220100
         LCLC  &NOCALL                     LOCAL LABEL                  07220200
         LCLC  &RETURN                     LOCAL LABEL                  07220300
         LCLC  &TEMP                       HOLDS FLAGS TEMPORARILY      07220400
.*                                                                      07220500
.*********************************************************************  07220600
.*                                                                   *  07220700
.*               INITIALIZE LOCAL VARIABLES                          *  07220800
.*                                                                   *  07220900
.*********************************************************************  07221000
.*                                                                      07221100
&VERNUM  SETA  1                           VERSION NUMBER               07221200
&XLAB    SETC  '&LABEL'                    PARAMETER LIST LABEL         07221300
&LABELX  SETC  'RES&SYSNDX'                                             07221600
&MFCODE  SETC  'I'                         ASSUME STANDARD FORM         07221900
&NOCALL  SETC  'END&SYSNDX'                                             07222200
&RETURN  SETC  'RET&SYSNDX'                                             07222500
.*                                                                      07222800
.*******************************************************************    07225500
.*        SET MFCODE - DEFAULT MF TO STANDARD FORM                 *    07225800
.*******************************************************************    07226100
.*                                                                      07226400
         AIF  ('&MF' EQ '' OR '&MF(1)' EQ 'I').CKEYS                    07226700
&MFCODE  SETC '&MF(1)'                  GET MACRO FORM                  07227000
.*                                                                      07227300
.*********************************************************************  07227600
.* TURN NOKEYS FLAG ON ONLY IF E FORM SPECIFIED AND NO OTHER         *  07227900
.*  KEYWORDS SPECIFIED.                                              *  07228200
.*********************************************************************  07228500
.*                                                                      07228800
.CKEYS   ANOP                                                           07229100
&NOKEYS  SETB  (('&MFCODE' EQ 'E') AND ('&REQUEST' EQ '' AND           *07229400
               '&TYPE' EQ '' AND '&ASID' EQ '' AND                     *07229700
               '&TCB' EQ '' AND '&ROUTINE' EQ '' AND                   *07230000
               '&TOKEN' EQ '' AND '&ECB' EQ '' AND                     *07230300
               '&PARAM' EQ '' AND '&TTOKEN' EQ ''))               @L1C  07230600
.*                                                                      07230900
.*******************************************************************    07231200
.*        CHECK FOR ADDITIONAL POSITIONAL PARAMETERS               *    07231500
.*******************************************************************    07231800
.*                                                                      07232100
         AIF   ((N'&SYSLIST EQ 1) OR                                   *07232400
               (N'&SYSLIST EQ 0 AND '&MFCODE' EQ 'E')).CKMAC            07232700
         MNOTE 12,'*** IHB027  INVALID NUMBER OF POSITIONAL PARAMETERS.*07246300
               '                                                        07259900
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           07273600
.*                                                                      07310500
.*********************************************************************  08677300
.*                                                                   *  08677500
.*                    CHECK MACRO FORM                               *  08677700
.*                                                                   *  08677900
.*********************************************************************  08678100
.*                                                                      08678300
.CKMAC   AIF   ('&MFCODE' EQ 'E' OR '&MFCODE' EQ 'L' OR '&MFCODE' EQ   *08682300
                'I').OKMF                                               08686300
         MNOTE 12,'*** IHB002  THE FIRST ARGUMENT OF THE MF PARAMETER M*08688900
               UST BE ''E'', ''L'', OR ''I''.'                          08691500
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           08694300
         AGO   .CKREQ                                                   08696000
.OKMF    AIF   ((('&MFCODE' EQ 'L' OR '&MFCODE' EQ 'I') AND            *08697700
               N'&MF EQ 1) OR ('&MFCODE' EQ 'E' AND N'&MF EQ 2) OR     *08699400
               ('&MFCODE' EQ 'I' AND N'&MF EQ 0)).CKREQ                 08701100
.*                                         CONTINUE VALIDITY CHECKING   08702800
.ERR1    MNOTE 12,'*** IHB002  INVALID NUMBER OF ARGUMENTS SPECIFIED FO*08705500
               R THE MF PARAMETER.'                                     08708200
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           08711100
.*                                                                      08717400
.*******************************************************************    09561500
.*        CHECK FOR VALID RESOURCE MANAGER REQUEST                 *    09562800
.*******************************************************************    09564100
.*                                                                      09565400
.CKREQ   AIF   (&NOKEYS OR ('&REQUEST' EQ 'ADD' OR                     *09567000
               '&REQUEST' EQ 'DELETE')).CKINV                           09573800
         MNOTE 12,'*** IHB308  POSITIONAL PARAMETER MUST BE ''ADD'' OR *09577300
               ''DELETE''.'                                             09580800
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           09591300
.*                                                                      09595000
.*******************************************************************    10313700
.*        CHECK FOR INVALID KEYWORD VALUES                         *    10314500
.*******************************************************************    10315300
.*                                                                      10316100
.CKINV   AIF   ('&TYPE' EQ 'ADDRSPC' OR '&TYPE' EQ 'TASK' OR           *10317100
               '&TYPE' EQ '').CKRTN                                     10321300
     MNOTE 12,'*** IHB002  THE ARGUMENT OF THE TYPE PARAMETER MUST BE '*10324200
               'ADDRSPC'' OR ''TASK''.'                                 10327100
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           10330000
.CKRTN   AIF   ('&ROUTINE(1)' EQ 'LINK' OR '&ROUTINE(1)' EQ            *10333800
               'BRANCH' OR '&ROUTINE(1)' EQ 'PC' OR                    *10337600
               '&ROUTINE' EQ '').CKOPS                                  10341400
         MNOTE 12,'*** IHB002  THE FIRST ARGUMENT OF THE ROUTINE PARAME*10343900
               TER MUST BE ''LINK'', ''BRANCH'', OR ''PC''.'            10346400
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           10349000
.CKOPS   AIF   ('&ROUTINE' EQ '' OR N'&ROUTINE EQ 2).CKREGS             10349400
         MNOTE 12,'*** IHB002  THE ROUTINE PARAMETER MUST HAVE EXACTLY *10349600
               TWO ARGUMENTS.'                                          10349800
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           10350200
.*                                                                      10350900
.*********************************************************************  14041900
.*                                                                   *  14042300
.*        CHECK FOR REGISTERS WITH LIST FORM OF MACRO                *  14042700
.*                                                                   *  14043100
.*********************************************************************  14043500
.*                                                                      14044300
.CKREGS  AIF   ('&MFCODE' NE 'L').CKMUTX                                14045200
         AIF   ('&ASID' EQ '').TCBREG                                   14046100
         AIF   ('&ASID'(1,1) NE '(').TCBREG                             14047100
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14048000
               F=L.'                                                    14048900
&ERRFLAG SETB  1                                                        14049900
.TCBREG  AIF   ('&TCB' EQ '').TTKNREG                             @L1C  14051300
         AIF   ('&TCB'(1,1) NE '(').TTKNREG                       @L1C  14052700
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14053600
               F=L.'                                                    14053700
&ERRFLAG SETB  1                                                        14053800
.TTKNREG AIF   ('&TTOKEN' EQ '').RTNREG                           @L1A  14053900
         AIF   ('&TTOKEN'(1,1) NE '(').RTNREG                     @L1A  14054000
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14054100
               F=L.'                                              @L1A  14054500
&ERRFLAG SETB  1                                                  @L1A  14055500
.RTNREG  AIF   ('&ROUTINE' EQ '' OR N'&ROUTINE NE 2).TKNREG             14056900
         AIF   ('&ROUTINE(2)'(1,1) NE '(').TKNREG                       14058300
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14059200
               F=L.'                                                    14060100
&ERRFLAG SETB  1                                                        14061100
.TKNREG  AIF   ('&TOKEN' EQ '').ECBREG                                  14062500
         AIF   ('&TOKEN'(1,1) NE '(').ECBREG                            14063900
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14064800
               F=L.'                                                    14065700
&ERRFLAG SETB  1                                                        14066700
.ECBREG  AIF   ('&ECB' EQ '').PARMREG                                   14068100
         AIF   ('&ECB'(1,1) NE '(').PARMREG                             14069500
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14070400
               F=L.'                                                    14071300
&ERRFLAG SETB  1                                                        14072300
.PARMREG AIF   ('&PARAM' EQ '').CKMUTX                                  14073700
         AIF   ('&PARAM'(1,1) NE '(').CKMUTX                            14075100
         MNOTE 12,'*** IHB006  REGISTER NOTATION MAY NOT BE USED WHEN M*14076000
               F=L.'                                                    14076900
&ERRFLAG SETB  1                                                        14077900
.*                                                                      14079300
.*********************************************************************  15213700
.*                                                                   *  15213900
.*        CHECK FOR MUTUALLY EXCLUSIVE PARAMETERS                    *  15214100
.*                                                                   *  15214300
.*********************************************************************  15214500
.*                                                                      15214700
.CKMUTX  AIF   (('&TYPE' EQ 'ADDRSPC' OR '&TYPE' EQ '') AND '&TCB'     *15215200
               NE '' AND '&MFCODE' NE 'E').ERR2                         15217100
         AIF   ('&TYPE' EQ 'ADDRSPC' AND '&TCB' NE '' AND '&MFCODE'    *15219000
               EQ 'E').ERR2                                             15220900
         AGO   .MUTXTKN                                           @L1A  15222800
.ERR2    MNOTE 12,'*** IHB280  THE TCB PARAMETER AND TYPE=ADDRSPC ARE M*15224000
               UTUALLY EXCLUSIVE.'                                      15225200
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15226600
.MUTXTKN AIF   (('&TYPE' EQ 'ADDRSPC' OR                               *15226700
               ('&TYPE' EQ '' AND '&MFCODE' NE 'E')) AND               *15226800
               ('&TTOKEN' NE '' OR '&TCB' NE '')).ERRTTKN         @L1A  15226900
         AIF   ('&TCB' NE '' AND '&TTOKEN' NE '').ERRTTKN         @L1A  15227200
         AGO   .MUTXECB                                                 15227400
.ERRTTKN MNOTE 12,'*** IHB280  THE TTOKEN PARAMETER MAY NOT BE SPECIFIE*15227600
               D WITH THE TCB PARAMETER OR WITH TYPE=ADDRSPC.'    @L1A  15227800
&ERRFLAG SETB  1                           TURN ERROR FLAG ON     @L1A  15228000
.MUTXECB AIF   ('&REQUEST' EQ 'ADD' AND '&ECB' NE '').ERR3              15228500
         AGO   .MUTXRTN                                                 15230400
.ERR3    MNOTE 12,'*** IHB280  THE ECB PARAMETER AND ''ADD'' ARE MUTUAL*15231600
               LY EXCLUSIVE.'                                           15232800
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15234200
.MUTXRTN AIF   ('&REQUEST' EQ 'ADD').CKSTD                              15236500
         AIF   ('&ROUTINE' EQ '').MUTXPRM                               15238800
         MNOTE 12,'*** IHB280  THE ROUTINE PARAMETER AND ''DELETE'' ARE*15240400
                MUTUALLY EXCLUSIVE.'                                    15242000
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15243700
.MUTXPRM AIF   ('&PARAM' EQ '').MUTXTYP                                 15246200
         MNOTE 12,'*** IHB280  THE PARAM PARAMETER AND ''DELETE'' ARE M*15247900
               UTUALLY EXCLUSIVE.'                                      15249600
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15251300
.MUTXTYP AIF   ('&ECB' EQ '').CKSTD                                     15251400
         AIF   ('&MFCODE' NE 'E').MUTXASD                               15251500
         AIF   ('&ASID' EQ '' OR '&TYPE' EQ '' OR ('&TCB' EQ '' AND    *15251600
               '&TYPE' EQ 'TASK')).CKSTD                                15251700
.MUTXASD AIF   ('&ASID' NE 'ALL').ERR4                                  15251800
         AIF   ('&TCB' NE 'ALL' AND '&TYPE' EQ 'TASK').ERR5             15251900
         AGO   .CKSTD                                                   15252000
.ERR4    MNOTE 12,'*** IHB306 THE ECB PARAMETER IS ONLY VALID WITH ASID*15252100
               =ALL.'                                                   15252200
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15252300
         AGO   .CKSTD                                                   15252400
.ERR5    MNOTE 12,'*** IHB306 THE ECB PARAMETER IS ONLY VALID WITH TCB=*15252500
               ALL.'                                                    15252600
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15252700
.*                                                                      15253200
.*********************************************************************  15968300
.*                                                                   *  15968700
.*        CHECK FOR REQUIRED KEYWORDS FOR STANDARD FORM              *  15969100
.*                                                                   *  15969500
.*********************************************************************  15969900
.*                                                                      15970300
.CKSTD   AIF   ('&MFCODE' NE 'I').CKFLG                                 15974400
         AIF   ('&ROUTINE' EQ '' AND '&REQUEST' EQ 'ADD').ERR6          15978500
         AGO   .CKTOKEN                                                 15982700
.ERR6    MNOTE 12,'*** IHB280  THE ROUTINE PARAMETER MUST BE SPECIFIED.*15985200
                '                                                       15987700
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           15990300
.CKTOKEN AIF   ('&TOKEN' NE '').CKFLG                                   15995300
         MNOTE 12,'*** IHB280  THE TOKEN PARAMETER MUST BE SPECIFIED.'  16000300
&ERRFLAG SETB  1                           TURN ERROR FLAG ON           16005500
.*                                                                      16007400
.*******************************************************************    16393700
.*        CHECK ERRORFLAG.  IF ERRORFLAG IS ON, EXIT MACRO.        *    16395700
.*******************************************************************    16397700
.*                                                                      16399700
.CKFLG   AIF   (NOT &ERRFLAG).COMMON       IF ANY ERRORS FOUND EXIT     16402100
         MEXIT                                                          16412500
.*                                                                      16462500
.*********************************************************************  16512500
.*       COMMON AREA FOR ALL FORMS - SET RTNCODE                     *  16562500
.*********************************************************************  16612500
.*                                                                      16662500
.COMMON  ANOP                                                           16687500
&ROUT1   SETB  ('&ROUTINE(1)' EQ 'LINK')                                16712500
&ROUT2   SETB  ('&ROUTINE(1)' EQ 'BRANCH')                              16762500
&ROUT3   SETB  ('&ROUTINE(1)' EQ 'PC')                                  16812500
&RTNCODE SETA  &ROUT1+&ROUT2*2+&ROUT3*3                                 16862500
.*                                                                      16912500
.*********************************************************************  16962500
.*                                                                   *  17590400
.*             COMMON AREA FOR LIST AND STANDARD FORMS               *  17590500
.*                                                                   *  17590600
.*********************************************************************  17590700
.*                                                                      17590800
         AIF   ('&MFCODE' EQ 'E').EFORM                                 17590900
         AIF   ('&MFCODE' EQ 'L').LFORM                                 17591000
         CNOP  0,4                         ALIGN IN-LINE LIST           17591100
&XLAB    IHBINNRR *+8                                                   17591200
         B     &LABELX                     BRANCH AROUND LIST           17591300
&XLAB    SETC  ''                                                       17591400
.LFORM   ANOP                                                           17591500
&XLAB    DS    0F                          LIST NAME                    17591600
         DC    XL1'&VERNUM'                VERSION NUMBER               17591700
&B(1)    SETB  ('&TYPE' EQ 'TASK')                                      17591800
         AIF   (NOT &B(1)).SETASID                                      17591900
&B(2)    SETB  ('&TCB' EQ 'ALL')                                        17592100
.SETASID ANOP                                                           17592600
&B(3)    SETB  ('&ASID' EQ 'ALL')                                       17593000
         DC    BL1'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)' FLAGS BYTE 17593400
         DC    XL1'&RTNCODE'               LINKAGE TYPE                 17593800
         AIF   ('&REQUEST' EQ 'ADD').REQBYTE                            17594200
         DC    XL1'02'                     REQUEST=DELETE               17594300
         AGO   .RESV2                                                   17594400
.REQBYTE DC    XL1'01'                     REQUEST=ADD                  17594500
.RESV2   DC    XL2'00'                     RESERVED                     17594600
         AIF   ('&ASID' EQ '').DCASID                                   17594700
         AIF   ('&ASID' EQ 'ALL' OR '&ASID' EQ 'CURRENT' OR            *17594800
               '&ASID'(1,1) EQ '(').DCASID                              17594900
         DC    AL2(&ASID)                  ASID                         17595000
         AGO   .CKTCB                                                   17595100
.DCASID  DC    H'0'                        ASID                         17595200
.CKTCB   AIF   ('&TCB' EQ '').DCTCB                                     17595300
         AIF   ('&TCB' EQ 'ALL' OR '&TCB' EQ 'CURRENT'                 *17595400
               OR '&TCB'(1,1) EQ '(').DCTCB                             17595500
         DC    A(&TCB)                     TCB ADDRESS                  17595600
         AGO   .RTNADDR                                                 17595700
.DCTCB   DC    A(0)                        TCB ADDRESS                  17595800
.RTNADDR AIF   ('&ROUTINE' EQ '').NORTN                                 17595900
         AIF   ('&ROUTINE(2)'(1,1) EQ '(').NORTN                        17596000
         AIF   ('&ROUTINE(1)' EQ 'LINK' AND '&ROUTINE(2)'(1,1)         *17596100
               EQ '''').CTYPE                                           17596200
         DC    A(&ROUTINE(2))              ROUTINE ADDRESS              17596300
         AGO   .TKNADDR                                                 17596400
.NORTN   DC    A(0)                        ROUTINE ADDRESS              17596700
         AGO   .TKNADDR                                                 17596800
.CTYPE   DC    A(*+20)                     ROUTINE NAME                 17596900
.TKNADDR AIF   ('&TOKEN' EQ '').DCTKN                                   17597000
         AIF   ('&TOKEN'(1,1) EQ '(').DCTKN                             17597100
         DC    A(&TOKEN)                   TOKEN ADDRESS                17597200
         AGO   .ECBADDR                                                 17597400
.DCTKN   DC    A(0)                        TOKEN ADDRESS                17597500
.ECBADDR AIF   ('&ECB' EQ '').DCECB                                     17597600
         AIF   ('&ECB'(1,1) EQ '(').DCECB                               17597700
         DC    A(&ECB)                     ECB ADDRESS                  17597800
         AGO   .PARMADR                                                 17597900
.DCECB   DC    A(0)                        ECB ADDRESS                  17598000
.PARMADR AIF   ('&PARAM' EQ '').DCPARAM                                 17598100
         AIF   ('&PARAM'(1,1) EQ '(').DCPARAM                           17598200
         DC    A(&PARAM)                   PARAM ADDRESS                17598300
         AGO   .TTKADDR                                          @L1A   17598400
.DCPARAM DC    A(0)                        PARAM ADDRESS                17598500
.TTKADDR AIF   ('&TTOKEN' EQ '').DCTTKN                          @L1A   17598700
         AIF   ('&TTOKEN'(1,1) EQ '(').DCTTKN                    @L1A   17598900
         DC    A(&TTOKEN)                  TTOKEN ADDRESS        @L1A   17599100
         AGO   .ENDDC                                            @L1A   17599300
.DCTTKN  DC    A(0)                        TTOKEN ADDRESS        @L1A   17599500
.ENDDC   AIF   ('&ROUTINE' EQ '').NEXT                           @L1C   17599700
         AIF   ('&ROUTINE(2)'(1,1) NE '''').NEXT                        17600300
         DC    CL8&ROUTINE(2)              ROUTINE NAME                 17600900
.*                                         END OF PARAMETER LIST        17601500
.*                                                                      17602100
.*********************************************************************  17603100
.*                                                                   *  17603200
.*             CHECK FOR REGISTERS WITH STANDARD FORM                *  17603300
.*                                                                   *  17603400
.*********************************************************************  17603500
.*                                                                      17603600
.NEXT    AIF   ('&MFCODE' NE 'I').CONT                                  17603700
&LABELX  DS    0F                                                       17603800
         AIF   ('&ASID' EQ '').ICKTCB                                   17603900
         AIF   ('&ASID'(1,1) NE '(').ICKTCB                             17604000
         STH   &ASID(1),6(0,1)             SAVE ASID                    17604100
.ICKTCB  AIF   ('&TCB' EQ '').ICKRTN                                    17604300
         AIF   ('&TCB'(1,1) NE '(').ICKRTN                              17604400
         ST    &TCB(1),8(0,1)              SAVE TCB ADDRESS             17604500
.ICKRTN  AIF   ('&ROUTINE' EQ '' OR N'&ROUTINE NE 2).ICKTKN             17604700
         AIF   ('&ROUTINE(2)'(1,1) NE '(').ICKTKN                       17604800
         ST    &ROUTINE(2),12(0,1)         SAVE ROUTINE ADDRESS         17604900
.ICKTKN  AIF   ('&TOKEN' EQ '').ICKECB                                  17605100
         AIF   ('&TOKEN'(1,1) NE '(').ICKECB                            17605200
         ST    &TOKEN(1),16(0,1)           SAVE TOKEN ADDRESS           17605300
.ICKECB  AIF   ('&ECB' EQ '').ICKPARM                                   17605500
         AIF   ('&ECB'(1,1) NE '(').ICKPARM                             17605600
         ST    &ECB(1),20(0,1)             SAVE ECB ADDRESS             17605700
.ICKPARM AIF   ('&PARAM' EQ '').ICKTTKN                          @L1C   17605900
         AIF   ('&PARAM'(1,1) NE '(').ICKTTKN                    @L1C   17606000
         ST    &PARAM(1),24(0,1)           SAVE PARAM ADDRESS           17606100
.ICKTTKN AIF   ('&TTOKEN' EQ '').CONT                            @L1A   17656100
         AIF   ('&TTOKEN'(1,1) NE '(').CONT                      @L1A   17706100
         ST    &TTOKEN(1),28(0,1)           SAVE TTOKEN ADDRESS  @L1A   17756100
         AGO   .CONT                                                    17806100
.*                                                                      17856100
.*********************************************************************  29585200
.*                                                                   *  29585300
.*          UPDATE PARAMETER LIST AS NEEDED FOR EXECUTE FORM         *  29585400
.*                                                                   *  29585500
.*********************************************************************  29585600
.*                                                                      29585700
.EFORM   ANOP                                                           29585800
&LABEL   IHBINNRR &MF(2)                   SET UP REG FOR PARAM LIST    29585900
         AIF   (&NOKEYS).CONT                                           29586100
         MVI   0(1),X'&VERNUM'             VERSION NUMBER               29586300
&B(1)    SETB  ('&TYPE' EQ 'TASK')                                      29588200
&C(1)    SETB  ('&TYPE' EQ '' OR '&TYPE' EQ 'TASK')                     29590100
&B(2)    SETB  ('&TCB' EQ 'ALL')                                        29592000
&C(2)    SETB  ('&TCB' EQ '' OR '&TCB' EQ 'ALL')                        29593900
&B(3)    SETB  ('&ASID' EQ 'ALL')                                       29595800
&C(3)    SETB  ('&ASID' EQ '' OR '&TCB' EQ 'ALL')                       29597700
.*  DETERMINE IF ANY BITS NEED TO BE RESET                              29599700
&TEMP    SETC  '&C(1)&C(2)&C(3)'.'11111'                                29600000
         AIF   ('&TEMP' EQ '11111111').XRESET                           29600500
         NI    1(1),B'&TEMP'               FLAGS BYTE                   29601000
.*  DETERMINE IF ANY BITS NEED TO BE SET                                29601200
.XRESET  ANOP                                                           29601500
&TEMP    SETC  '&B(1)&B(2)&B(3)'.'00000'                                29602000
         AIF   ('&TEMP' EQ '00000000').XRTN                             29602500
         OI    1(1),B'&TEMP'               FLAGS BYTE                   29603000
.XRTN    AIF   ('&ROUTINE' EQ '').XCKREQ   UPDATE ROUTINE TYPE          29605300
         MVI   2(1),X'&RTNCODE'            UPDATE ROUTINE TYPE          29607600
.XCKREQ  AIF   ('&REQUEST' EQ 'ADD').XADDREQ                            29610000
         MVI   3(1),X'02'                  REQUEST=DELETE               29610500
         AGO   .XCKASID                                                 29611000
.XADDREQ MVI   3(1),X'01'                  REQUEST=ADD                  29611500
.XCKASID AIF   ('&ASID' NE 'ALL' AND '&ASID' NE 'CURRENT' AND          *29612000
               '&ASID' NE '').XSETASD                                   29612500
         AIF   ('&ASID' EQ 'CURRENT').XASDCUR                           29613000
         AGO   .XCKTCB                                                  29613500
.XSETASD AIF   ('&ASID'(1,1) EQ '(').XASDREG                            29614000
         LA    15,&ASID                    GET ASID                     29614500
         AGO   .XSVASID                                                 29615000
.XASDREG STH   &ASID(1),6(1)               SAVE ASID                    29615500
         AGO   .XCKTCB                                                  29616000
.XASDCUR SLR   15,15                       ASID=CURRENT                 29616500
.XSVASID STH   15,6(,1)                    SAVE ASID                    29617000
.XCKTCB  AIF   ('&TCB' NE 'ALL' AND '&TCB' NE 'CURRENT'                *29618000
               AND '&TCB' NE '').XSETCB                                 29618500
         AIF   ('&TCB' EQ 'CURRENT').XTCBCUR                            29619000
         AGO   .XCKRTN                                                  29619500
.XSETCB  AIF   ('&TCB'(1,1) EQ '(').XTCBREG                             29620000
         LA    15,&TCB                     GET TCB ADDRESS              29620500
         AGO   .XSAVTCB                                                 29620700
.XTCBREG ST    &TCB(1),8(1)                SAVE TCB ADDRESS             29620900
         AGO   .XCKRTN                                                  29621100
.XTCBCUR SLR   15,15                       TCB=CURRENT                  29621300
.XSAVTCB ST    15,8(,1)                    SAVE TCB ADDRESS             29621500
.XCKRTN  AIF   ('&ROUTINE' EQ '').XNORTN   ROUTINE WAS NOT SPECIFIED    29621700
         AIF   ('&ROUTINE(2)'(1,1) EQ '(').XRTNREG                      29621900
         AIF   ('&ROUTINE(1)' EQ  'LINK' AND '&ROUTINE(2)'(1,1) EQ     *29622100
               '''').XRTNAME                                            29622300
         LA    15,&ROUTINE(2)              GET ROUTINE ADDRESS          29622800
         ST    15,12(,1)                   SAVE ROUTINE ADDRESS         29623300
         AGO   .XNORTN                                                  29623900
.XRTNREG ST    &ROUTINE(2),12(,1)          SAVE ROUTINE ADDRESS         29624000
         AGO   .XNORTN                                                  29624100
.XRTNAME BAS   15,*+12                                             @01C 29624200
         DC    CL8&ROUTINE(2)              GET ROUTINE NAME             29624300
         ST    15,12(,1)                   SAVE ROUTINE ADDRESS         29624400
.XNORTN  AIF   ('&TOKEN' EQ '').XNOTKN                                  29624800
         AIF   ('&TOKEN'(1,1) EQ '(').XREGTKN                           29624900
         LA    15,&TOKEN                   GET TOKEN ADDRESS            29625100
         ST    15,16(,1)                   SAVE TOKEN ADDRESS           29625200
         AGO   .XNOTKN                                                  29625300
.XREGTKN ST    &TOKEN(1),16(,1)            SAVE TOKEN ADDRESS           29625400
.XNOTKN  AIF   ('&ECB' EQ '').XNOECB                                    29625500
         AIF   ('&ECB'(1,1) EQ '(').XECBREG                             29625600
         LA    15,&ECB                     GET ECB ADDRESS              29625800
         ST    15,20(,1)                   SAVE ECB ADDRESS             29625900
         AGO   .XNOECB                                                  29626000
.XECBREG ST    &ECB(1),20(,1)              SAVE ECB ADDRESS             29626100
.XNOECB  AIF   ('&PARAM' EQ '').XNOTTK                           @L1C   29626200
         AIF   ('&PARAM'(1,1) EQ '(').XPRMREG                           29626300
         LA    15,&PARAM                   GET PARAM ADDRESS            29626500
         ST    15,24(,1)                   SAVE PARAM ADDRESS           29626600
         AGO   .XNOTTK                                           @P1C   29627700
.XPRMREG ST    &PARAM(1),24(,1)            SAVE PARAM ADDRESS           29628800
.XNOTTK  AIF   ('&TTOKEN' EQ '').CONT                            @L1A   29630900
         AIF   ('&TTOKEN'(1,1) EQ '(').XREGTTK                   @L1A   29633000
         LA    15,&TTOKEN                   GET TTOKEN ADDRESS   @L1A   29635100
         ST    15,28(,1)                   SAVE TTOKEN ADDRESS   @L1A   29637200
         AGO   .CONT                                             @L1A   29639300
.XREGTTK ST    &TTOKEN(1),28(,1)           SAVE TTOKEN ADDRESS   @L1A   29641400
.*                                                                      29643500
.*********************************************************************  29651800
.*               COMMON AREA FOR E & I FORMS                         *  29660100
.*********************************************************************  29668400
.*                                                                      29676900
.CONT    AIF   ('&MFCODE' EQ 'L').LDONE    EXIT IF MF = L               30527200
         L     14,16(0,0)                  LOAD ADDRESS OF CVT          30527800
         L     14,772(14,0)                GET SFT ADDRESS              30528400
         L     14,164(14,0)                GET LX/EX FOR RESMGR         30529000
         PC    0(14)                       INVOKE SERVICE               30529600
.LDONE   MEXIT                                                          30530500
         MEND                                                           30535600
* END OF BAL RESMGR MACRO                                            */ 30535700
* RESMGR:  /******************************************************** */ 30535800
*          /*  INVOKE ?RESMGRP - PL/S VERSION OF THE RESMGR MACRO  * */ 30535900
*          /******************************************************** */ 30536000
* MACRO                                /* ?RESMGR INVOKES ?RESMGRP   */ 30536100
*   KEYS(TYPE,ASID,TCB,ROUTINE,TOKEN,ECB,PARAM,                         30536200
*        TTOKEN,MF,RETCODE,RELATED);                /*           @L1C*/ 30536300
* ANS( '?' || MACLABEL || 'RESMGRP ' || MACLIST || MACKEYS || ';' );/*  30536400
*                                      INVOKE ?RESMGRP               */ 30536500
*%END RESMGR;                                                           30536600
