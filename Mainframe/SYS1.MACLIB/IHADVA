* % GOTO DVAL1;             /*                                      */  00500000
         MACRO                                                          01000000
         IHADVA &DSECT=YES,                                        @H5C*01050000
               &INFO=                                              @H5A 01100000
.* /* START OF SPECIFICATIONS ****                                      01510000
.*                                                                      01520000
.*   $MAC(IHADVA):  DEVTYPE return area                                 01526200
.*                                                                      01532400
.*PROPRIETARY V3 STATEMENT                                              01536800
.*LICENSED MATERIALS - PROPERTY OF IBM                                  01541200
.*"RESTRICTED MATERIALS OF IBM"                                         01545600
.*5694-A01                                                              01550000
.*(C) COPYRIGHT 1981,2000  IBM CORP.                                    01554400
.*END PROPRIETARY V3 STATEMENT                                          01558800
.*                                                                      01563400
.*01  STATUS = DFSMS/MVS 3.10                                      @H5C 01571700
.*                                                                      01580000
.*01  DESCRIPTIVE-NAME = DEVTYPE return areas                           01590000
.*                                                                      01600000
.*01  FUNCTION = Map areas returned by the DEVTYPE SVC           @H5C*/ 01601100
* %DVAL1: ;                                                             01602200
*/********************************************************************/ 01603300
*/*                 DEVTYPE return area                              */ 01604400
*/*     THIS MACRO MAPS THE AREA RETURNED TO THE CALLER BY THE       */ 01605500
*/*     DEVTYPE SVC.                                                 */ 01606600
*/*     IN AN ASSEMBLER CALL DSECT=NO MAY BE CODED.  YOU ALSO CAN@H5C*/ 01607700
*/*     CODE INFO=NONE OR ANY COMBINATION OF INFO=(DEVTYPE,DASD, @H5A*/ 01608800
*/*     SUFFIX,AMCAP).                                           @H5A*/ 01609900
*/*                                                                  */ 01611000
*/*     IN A PL/X INCLUDE, DVABASE MAY BE DECLARED AS DEF(LABEL)     */ 01612100
*/*     TO CAUSE IT TO BE USED IN A STRUCTURE.  IF DVABASE IS        */ 01613200
*/*     OMITTED THE STRUCTURE IS DECLARED AS BASED.                  */ 01614300
*/*                                                                  */ 01615400
*/********************************************************************/ 01616500
* % GOTO DVAL2;                                                /*  @H5C 01618200
.*                                                                      01620000
.*01  MODULE-TYPE = MACRO                                               01630000
.*                                                                      01640000
.*02    MACRO-SIZE = NA FOR MAPPING MACRO                               01650000
.*                                                                      01660000
.*02    PARAMETER-RECEIVED = DSECT=                                     01675000
.*03     TYPE = CHARACTER STRING                                        01690000
.*04       VALUE = YES OR NO.  Default is YES.                     @H5C 01690700
.*04         YES.  You get a single area with a DSECT.  Its name   @H5A 01691400
.*             depends on whether you code INFO= & its value.      @H5A 01692100
.*04         NO.  If you omit INFO= or code INFO=NONE, then the    @H5A 01692800
.*04           area begins with the symbol DVAREA and it is not a  @H5A 01693500
.*             DSECT.                                              @H5A 01694200
.*                If you code INFO= any combination of its values  @H5A 01694900
.*             other than NONE, then DVAREA is not defined and     @H5A 01695600
.*             there is no DSECT.                                       01696300
.*02    PARAMETER-RECEIVED = INFO=                                 @H5A 01697000
.*03     TYPE = Character string.  If INFO= is omitted, mapping    @H5A 01697700
.*             is for the area returned by DEVTYPE when INFO       @H5A 01698400
.*             and INFOLIST are omitted and when any one of        @H5A 01699100
.*             INFO=DEVTYPE, DASD and SUFFIX is coded.             @H5A 01699800
.*             They all have the same origin in one DSECT and      @H5A 01700500
.*             therefore do not represent a single invocation.     @H5A 01701200
.*             Also, in that case this macro cannot be invoked     @H5A 01701900
.*             more than once because the invocation always begins @H5A 01702600
.*             with the symbol DVAREA.                             @H5A 01703300
.*               As on the DEVTYPE macro, IHADVA supports multiple @H5A 01704000
.*             values on INFO=.  Also as on DEVTYPE they are       @H5A 01704700
.*             expanded in contiguous storage.  Unlike DEVTYPE,    @H5A 01705400
.*             do not code the same value twice.  When INFO is     @H5A 01706100
.*             coded on this macro, it does no boundary alignment  @H5A 01706800
.*             because DEVTYPE returns them in contiguous storage. @H5A 01707500
.*                If you code DSECT=NO,INFO=xxx, then the symbol   @H5A 01708200
.*             DVAREA is not expanded, allowing you to call IHADVA @H5A 01708900
.*             more than once.                                     @H5A 01709600
.*04       VALUE = NONE.  Expand only the area returned when       @H5A 01710300
.*             INFO and INFOLIST are omitted.  NONE cannot be      @H5A 01711000
.*             in combination with any other value.                @H5A 01711700
.*04       VALUE = DEVTYPE.  Map area returned for INFO=DEVTYPE.   @H5A 01712400
.*04       VALUE = DASD.  Map area returned for INFO=DASD.         @H5A 01713100
.*04       VALUE = SUFFIX.  Map area returned for INFO=SUFFIX.     @H5A 01713800
.*             The above three generate a subset of the fields     @H5A 01714500
.*             that are generated when INFO= is omitted on IHADVA. @H5A 01715200
.*04       VALUE = AMCAP.  Map area returned for INFO=AMCAP.       @H5A 01715900
.*                                                                      01720000
.*01  TABLES = NONE                                                     01730000
.*                                                                      01740000
.*01  INNER-MACROS = NONE                                               01750000
.*                                                                      01760000
.*01  SYSTEM-MACROS = NONE                                              01770000
.*                                                                      01780000
.*01  CHANGE-ACTIVITY = SEE BELOW                                       01790000
.*    VS2 RELEASE 2                                                     01800000
.*       MACRO CREATED                                                  01810000
.*   $H1=CORONADO,FDS1134,T790029,STLGMD:                               01821400
.*   $H2=CALYPSO,JDM1136,T800472,STLFS:    DEVICE FEATURE SUPPORT       01822800
.*   $01=OZ60907,JDM1139,TL,DVAMOD1 DEFINED TWICE                       01824200
.*   $01=OZ63580,HDP1102,TL,DVAMOD1 SHOULD BE DVAMODL              @01A 01825600
.*   $H3=3880M11,JDP1110,T820725,STLGMD:     PAGING DEVICE SUPPORT      01827000
.*   $02=OZ74152,JDP1110,WJ:  COMMENT DELIMETER LEFT OUT           @02A 01828400
.*   $03=OY03884,HDP2230,GEC088: ADD NO ALTERNATE TRACKS BIT   @YA03884 01829200
.*   $H4=INFOLIST,JDZ1110,SJPLJAH: ADD INFOLIST SUPPORT            @H4A 01829600
.*   $04=OW10560,HDZ11B0,SJPLSYW: ADD DS 0F AT START & ORG AT END  @04A 01829700
.*   $H5=>32KBLK,HDZ11F0,990805,SJPLWER: INFO=AMCAP and clean up   @H5A 01829800
.*                                                                      01830000
.**** END OF SPECIFICATIONS ***/  /*                                    01860000
         LCLB  &DSECTDONE Whether DSECT has been generated for INFO@H5A 01890000
         LCLC  &SDEVT,&SDASD,&SSUFFIX,&SAMCAP  INFO section names  @H5A 01940000
&SDEVT   SETC  'DVAUCBTY' DSECT or first symbol for INFO=DEVTYPE   @H5A 01990000
&SDASD   SETC  'DVAIDASD' DSECT or first symbol for INFO=DASD      @H5A 02040000
&SSUFFIX SETC  'DVASUFFX' DSECT or first symbol for INFO=SUFFIX    @H5A 02090000
&SAMCAP  SETC  'DVAAMCAP' DSECT or first symbol for INFO=AMCAP     @H5A 02140000
         AIF   ('&DSECT' NE 'YES' AND '&DSECT' NE 'NO' AND             *02190000
               '&DSECT' NE '').BADDSECT  CHECK VALIDITY OF DSECT=  @H5A 02240000
.*    If INFO= was coded and is not NONE, the caller wants only    @H5A 02290000
.*    an area that is returned for INFO on the DEVTYPE macro.      @H5A 02340000
         AIF   ('&INFO' NE '' AND '&INFO' NE 'NONE').INFO          @H5A 02390000
         AIF   ('&DSECT' EQ 'NO').DVANOD                                06510000
DVAREA   DSECT      OUTPUT AREA FROM DEVTYPE MACRO WITHOUT INFO=.       06560000
         AGO   .DVACONT                                                 07010000
.DVANOD  ANOP                                                           07020000
         DS    0F        ALIGNMENT, NO INFO or INFO=NONE           @04A 07025000
DVAREA   EQU   *                                                        07030000
.DVACONT ANOP                                                           07040000
*                                                                       07500000
*********************************************************************** 08000000
*  WHEN INFOLIST IS OMITTED, THE FIRST TWO WORDS OF THE OUTPUT     @H5A 08050000
*  AREA CONTAINS THE UCBTYP FIELD AND MAXIMUM BLOCK SIZE SUPPORTED @H5C 08100000
*  BY BSAM OR QSAM FOR THE DEVICE.  THEY ARE FOLLOWED BY MORE      @H5C 08150000
*  INFORMATION IF THE DEVICE IS DIRECT ACCESS AND THE CALLER       @H5C 08200000
*  CODED THE DEVTAB PARAMETER.                                     @H5C 08250000
*********************************************************************** 10500000
DVAPREFX DS    0CL8      FIRST WORDS OF DEVTYPE OUTPUT                  10550000
         AGO   .DEVTCON2                                           @H5A 10600000
.DEVTYPE ANOP                                                      @H5A 10650000
*********************************************************************** 10700000
*    SECTION FOR INFO=DEVTYPE.                                     @H5C 10750000
*********************************************************************** 10800000
.* User coded INFO=DEVTYPE.  Check whether DSECT needed.           @H5A 10850000
         AIF   ('&DSECT' EQ 'NO').DEVTCON2                         @H5A 10900000
         AIF   (&DSECTDONE).DEVTCON2                               @H5A 10950000
&SDEVT   DSECT                                                     @H5A 11000000
&SDEVT   SETC  ''        Prevent symbol redefinition               @H5A 11050000
&DSECTDONE SETB 1        Remember we made a DSECT                  @H5A 11100000
.DEVTCON2 ANOP                                                     @H5A 11150000
* THE NEXT FOUR BYTES CONSTITUTE THE WHOLE AREA IF INFO=DEVTYPE IS @H5A 11200000
* CODED OR THE FIRST PART IF INFOLIST AND INFO WERE NOT CODED.     @H5A 11250000
&SDEVT   DS    0XL4      REAL OR SIMULATED UCBTYP FIELD                 11300000
DVAOPTS  DS    XL2       UCB OPTIONS                                    11350000
DVACLASS DS    XL1       DEVICE CLASS                                   11400000
DVAUNIT  DS    XL1       UNIT TYPE                                      11450000
         AIF   ('&INFO' NE '' AND                                      *11500000
               '&INFO' NE 'NONE').MOREINFO Go if any INFO value    @H5A 11550000
DVAMAXRC DS    F         MAXIMUM BLOCK SIZE WHEN NOT USING SAM LBI      11600000
DVADEVLG EQU   *-DVAREA  LENGTH OF THE AREA WHEN DEVTAB NOT CODED       11650000
*                                                                       15000000
*********************************************************************** 15500000
*                  DEVTAB SECTION FOR DASD DEVICES                    * 16000000
*********************************************************************** 16500000
DVATAB   DS    0CL12                                                    17000000
DVACYL   DS    H         PHYSICAL NUMBER OF CYLINDERS PER VOLUME        17050000
DVATRK   DS    H         NUMBER OF TRACKS PER CYLINDER                  17100000
DVATRKLN DS    H         NUMBER OF BYTES PER TRACK, INCLUDING OVERHEAD  17150000
* FORMULAS DESCRIBED HERE ARE NOT ACCURATE FOR CURRENT DISKS.      @H5A 17200000
DVAOVHD  DS    0H        BLOCK OVERHEAD IF DVA2BOV=1                    17250000
*              ******************************************************** 17300000
*              * IF DVA2BOV=0 USE INSTEAD THE FOLLOWING TWO VALUES.   * 17350000
*              ******************************************************** 17400000
DVAOVNLB DS    FL1       OVERHEAD IF NOT LAST BLOCK                     17450000
DVAOVLB  DS    FL1       OVERHEAD IF LAST BLOCK                         17500000
DVAOVNK  DS    FL1       OVERHEAD DECREMENT IF BLOCK NOT KEYED          17550000
DVAFLAGS DS    XL1                                                      17600000
DVABDCYL EQU   X'80'     IF 1, DVACYL IS INVALID                YL02130 17650000
DVADEFLR EQU   X'40'     DEFINE EXTENT, LOCATE RECORD & RELATED        X17700000
                           TRANSFER COMMANDS ARE IMPLEMENTED       &H2A 17750000
DVADEFEX EQU   X'20'     DEFINE EXTENT IMPLEMENTED                 @H2A 17800000
DVA2BOV  EQU   X'08'     IF 1, USE DVAOVHD                              17850000
*                        IF 0, USE DVAOVNLB,DVAOVLB                     17900000
DVAMODL  EQU   X'10'     IF 1 USE DVAMOD1, DVAOVH1, DVAOVH2             17950000
DVAPAGES EQU   X'04'     IF 1, DEVICE SUPPORTS PAGING CCWS         @H3A 18000000
*                        IF 0, DEVICE DOES NOT SUPPORT PAGING CCWS @H3A 18050000
DVANOALT EQU   X'02'     NO ALTERNATE TRACKS ARE AVAILABLE     @YA03884 18100000
DVAFTOL  EQU   X'01'     IF 1, APPLY TOLERANCE FACTOR                   18150000
*                                                                       26500000
DVATOL   DS    H         TOLERANCE FACTOR                               26550000
*                                 ************************************* 27500000
*                                 * APPLY TOLERANCE FACTOR AS FOLLOWS:* 28000000
*                                 * 1. ADD BLOCKSIZE AND KEYLENGTH    * 28500000
*                                 * 2. MULTIPLY BY DVATOL             * 29000000
*                                 * 3. SHIFT RIGHT DVASHFT BITS       * 29500000
*                                 * 4. ADD APPROPRIATE OVERHEADS      * 30000000
*                                 ************************************* 30500000
DVASHFT  EQU   9                        SHIFT AMT TO DIVIDE BY 512      31000000
*                                                                       31500000
DVAENTLG EQU   *-DVATAB  BASIC SIZE OF DEVICE TABLE ENTRY, EXCLUDING    31550000
*                          ADDITIONAL CHARACTERISTICS FOR RPS           31600000
*                                                                       33500000
**********************************************************************  34000000
*              THE FOLLOWING SECTION OF THE TABLE IS PRESENT         *  34500000
*              ONLY FOR RPS DEVICES--TEST UCB2OPT3 IN DVAOPTS.       *  34550000
**********************************************************************  35500000
* TO CALCULATE THE SECTOR FOR NON-MODULO DEVICES PERFORM THE       @H1C 35550000
* FOLLOWING CALCULATION:                                           @H1C 35600000
* SECTOR = ((DVAOVR0 + COST(N-1)) * DVASECTD) / DVATRKLN           @H1C 35650000
**********************************************************************  35700000
DVARPS   DS    0CL4                     RPS SECTION                     36000000
DVAOVR0  DS    H                        OVERHEAD BYTES FOR RECORD 0     36500000
DVASECT  DS    XL1                      NR. SECTORS IN FULL TRACK       37000000
DVASECTD DS    XL1                      NR. DATA SECTORS                37500000
DVARPSLG EQU   *-DVARPS                 SIZE OF RPS SECTION             38000000
**************************************************************     @H1A 38040000
*              THE FOLLOWING AREA USED FOR MODULO DEVICES ONLY.    @H1A 38065500
**************************************************************     @H1A 38091000
* TO CALCULATE THE RECORD OVERHEAD FOR MODULO DEVICES PERFORM      @H1A 38108000
* THE FOLLOWING CALCULATION.                                       @H1A 38125000
* COST = (DL + DVAOVHD2) / DVAMOD1;                                @H1A 38142000
* IF KL ^= 0 THEN                                                  @H1A 38159000
*   COST = COST + ((KL + DVAOVHD2) / DVAMOD1 )                     @H1A 38176000
* COST = (COST + DVAMOD1) + DVAOVHD1                               @H1A 38193000
**************************************************************     @H1A 38210000
* TO CALCULATE THE SECTOR                                          @H1A 38227000
* SECTOR = (SUM RECORD COST + DVAOVR0 ) / DVABPSEC                 @H1A 38244000
**********************************************************************  38261000
* TO CALCULATE THE SECTOR FOR MODULO DEVICES PERFORM THE           @H1A 38278000
* FOLLOWING CALCULATION:                                           @H1A 38295000
* SECTOR = (DVAOVR0 + COST(N-1))/ DVABPSEC                         @H1A 38312000
**********************************************************************  38329000
         ORG   DVAOVNK                                             @H1A 38346000
DVAMOD1  DS    XL1       MODULO FACTOR                             @H1A 38363000
         ORG   DVAOVHD                                             @H1A 38380000
DVAOVH1  DS    H         MODULO OVERHEAD 1                         @H1A 38397000
         ORG   DVATOL                                              @H1A 38414000
DVAOVH2  DS    H         MODULO OVERHEAD 2                         @H1A 38431000
         ORG   DVASECTD                                            @H1A 38448000
DVABPSEC DS    XL1       NUMBER OF BYTES PER SECTOR                @H1A 38456600
DVAEND   EQU   *         ORG TO HERE TO GET THE MAXIMUM SIZE WHEN  @H5A*38465200
                           DEVTYPE INFOLIST IS NOT CODED           @H5A 38473800
*   END OF AREA RETURNED WHEN INFOLIST IS NOT CODED                @H5A 38482400
         AIF   ('&INFO' EQ 'NONE').MEND Exit if only this area     @H5A 38491000
*                                                                       38500000
*   ALL OF THE ABOVE EXCEPT WHEN DVAUCBTY IS RETURNED FOR          @H5A 38502300
*   INFO=DEVTYPE ARE MUTUALLY EXCLUSIVE WITH ALL FOLLOWING.        @H5A 38504600
*   TO PREVENT ACCIDENTAL ADDRESSABILITY TO THE FOLLOWING, YOU     @H5A 38506900
*   CAN CODE 'USING (DVAREA,DVAEND),RX'.                           @H5A 38509200
         ORG   DVAREA    REMAP THE WHOLE AREA                      @H5A 38511500
         AGO   .DASD2                                              @H5A 38513800
.* Handle INFO=DASD.                                               @H5A 38516100
.DASD    AIF   ('&DSECT' EQ 'NO').DASD2                            @H5A 38518400
         AIF   (&DSECTDONE).DASD2                                  @H5A 38520700
&SDASD   DSECT                                                     @H5A 38523000
&SDASD   SETC  ''        Prevent symbol redefinition               @H5A 38525300
&DSECTDONE SETB 1        Prevent another DSECT                     @H5A 38527600
.DASD2   ANOP                                                      @H5A 38529900
*********************************************************************** 38533300
*    SECTION FOR INFO=DASD.                                        @H5C 38566600
*********************************************************************** 38599900
&SDASD   DS    0CL16     INFOLIST POINTS TO LIST WITH INFO=DASD    @H5C 38610400
DVAICYL  DS    FL4       NUMBER OF CYLINDERS, EXCLUDING ALTERNATES @H5A 38620900
DVAITRK  DS    FL4       TRACKS PER CYLINDER                       @H5A 38631400
DVAIFLAG DS    0XL2      FLAGS WITH THREE DEPRECATED SYMBOLS       @H5C 38641900
DVAECKD  EQU   X'8000'   IF 1 ECKD SUPPORTED                       @H4A 38652400
DVALRE   EQU   X'4000'   IF 1 LOCATE RECORD EXTENDED IS SUPPORTED  @H4A 38662900
DVACACHE EQU   X'2000'   IF 1 THE DEVICE IS CACHED                 @H4A 38673400
.* DO NOT ADD MORE BITS LIKE THE ABOVE THAT ARE HARD TO USE IN ASM @H5A 38683900
DVAIFLAG1 DS   X         FLAGS WITH PREFERRED SYMBOLS              @H5A 38694400
DVAECKD1 EQU   X'80'     ECKD SUPPORTED                            @H4A 38704900
DVALRE1  EQU   X'40'     LOCATE RECORD EXTENDED IS SUPPORTED       @H4A 38715400
DVACACHE1 EQU  X'20'     THE DEVICE IS CACHED                      @H4A 38725900
DVAIFLAG2 DS   X         RESERVED                                  @H5A 38736400
         DS    XL2       RESERVED                                  @H4A 38746900
DVAITSET DS    XL1       TRACK SET SIZE                            @H4A 38757400
         DS    XL3       RESERVED                                  @H4A 38767900
         AIF   ('&INFO' NE '').MOREINFO  Branch if any INFO value  @H5A 38778400
.*                                                                 @H5A 38788900
         ORG   DVAREA    REMAP THE WHOLE AREA                      @H5A 38799400
         AGO   .SUFF2    Continue handling INFO not coded          @H5A 38809900
.* Handle INFO=SUFFIX.                                             @H5A 38820400
.SUFFIX  AIF   ('&DSECT' EQ 'NO').SUFF2                            @H5A 38830900
         AIF   (&DSECTDONE).SUFF2                                  @H5A 38841400
&SSUFFIX DSECT                                                     @H5A 38851900
&SSUFFIX SETC  ''        Prevent symbol redefinition               @H5A 38862400
&DSECTDONE SETB 1        Prevent another DSECT                     @H5A 38872900
.SUFF2   ANOP                                                      @H5A 38883400
*********************************************************************** 38893900
*    SECTION FOR INFO=SUFFIX.                                      @H5A 38904400
*********************************************************************** 38914900
&SSUFFIX DS    HL2       LENGTH OF BLOCK SUFFIX                    @H5M 38925400
         AIF   ('&INFO' NE '').MOREINFO  Go handle another INFO    @H5A 38935900
*   THE FOLLOWING ORG IS ALMOST GUARANTEED NOT TO RESULT IN WHAT   @H5A 38946400
*   THE CALLER WANTS BECAUSE THE VARIOUS FORMATS FOR THE OUTPUT    @H5A 38956900
*   AREA ARE MUTUALLY EXCLUSIVE.  THE USER PROBABLY WANTS TO ORG   @H5A 38967400
*   TO DVAEND ABOVE OR TO AN EARLIER FIELD.                        @H5A 38977900
         ORG   ,                                                   @04A 38991500
*              END OF DVA                                               39000000
         MEXIT                                                     @H5A 39020000
.*                                                                 @H5A 39040000
.* Handle the INFO keyword and expand only the areas that the user @H5A 39060000
.* wants.  For campatibility with earlier levels of this macro,    @H5A 39080000
.* which were in SYS1.MACLIB, if INFO is omitted, three of these   @H5A 39100000
.* areas are expanded above even though they cannot be returned    @H5A 39120000
.* in the same DEVTYPE invocation as one in which the user omitted @H5A 39140000
.* INFO on DEVTYPE.                                                @H5A 39160000
.INFO    ANOP                                                      @H5A 39180000
&I       SETA  0         Index before first value                  @H5A 39200000
.MOREINFO ANOP           Continue with next INFO value, if any     @H5A 39220000
&I       SETA  &I+1      Set index to next INFO value              @H5A 39240000
         AIF   (&I GT N'&INFO).MEND   Branch if no more or none    @H5A 39260000
         AIF   ('&INFO(&I)' EQ 'DEVTYPE').DEVTYPE                  @H5A 39280000
         AIF   ('&INFO(&I)' EQ 'DASD').DASD                        @H5A 39300000
         AIF   ('&INFO(&I)' EQ 'SUFFIX').SUFFIX                    @H5A 39320000
         AIF   ('&INFO(&I)' NE 'AMCAP').INFO1                      @H5A 39340000
.* Handle INFO=AMCAP.                                              @H5A 39360000
         AIF   ('&DSECT' EQ 'NO').AMCAP2                           @H5A 39380000
         AIF   (&DSECTDONE).AMCAP2                                 @H5A 39400000
&SAMCAP  DSECT                                                     @H5A 39420000
&SAMCAP  SETC  ''        Prevent symbol redefinition               @H5A 39440000
&DSECTDONE SETB 1        Prevent another DSECT                     @H5A 39460000
.AMCAP2  ANOP                                                      @H5A 39480000
*********************************************************************** 39500000
*    SECTION FOR INFO=AMCAP.                                       @H5A 39520000
*********************************************************************** 39540000
&SAMCAP  DS    0XL32     ACCESS METHOD CAPABILITIES                @H5A 39560000
DVAAMFLG DS    X         FLAGS                                     @H5A 39580000
DVAAMLBI EQU   X'80'     BSAM, QSAM AND (IF DASD) BPAM SUPPORT THE @H5A*39600000
                         LARGE BLOCK INTERFACE AND THE LIMIT IS IN @H5A*39620000
                         THE NEXT DOUBLEWORD.                      @H5A 39640000
         DS    XL7       RESERVED                                  @H5A 39660000
DVAMAXBLK DS   DL8       MAXIMUM BLOCK SIZE SUPPORTED WITH SAM LBI @H5A 39680000
DVAOPTBLK DS   DL8       RECOMMENDED MAXIMUM BLOCK SIZE WITH SAM   @H5A*39700000
                         LBI.  LARGER BLOCKS MIGHT BE LESS         @H5A*39720000
                         EFFICIENT OR LESS RELIABLE.  LESS         @H5A*39740000
                         THAN OR EQUAL TO PREVIOUS FIELD.          @H5A 39760000
DVAMAXLR DS    DL8       MAXIMUM UNSPANNED LOGICAL RECORD LENGTH   @H5A*39780000
                         SUPPORTED BY BSAM, QSAM AND BPAM          @H5A 39800000
         AGO   .MOREINFO Loop to test for another INFO value       @H5A 39820000
.* Add future INFO values here.                                    @H5A 39840000
.INFO1   MNOTE 8,'INVALID VALUE FOR INFO.  YOU CAN CODE ONLY DEVTYPE, D*39860000
               ASD, SUFFIX, AMCAP OR NONE'                         @H5A 39880000
         AGO   .MEND                                               @H5A 39900000
.BADDSECT MNOTE 8,'INVALID VALUE FOR DSECT.  YOU CAN CODE ONLY YES OR N*39920000
               O'                                                  @H5A 39940000
.MEND    MEND                                                           39960000
         AGO   .PLXEND   Skip asm garbage & allow 'COPY IHADVA'. @H5A*/ 39980000
*% DVAL2: ;                                                             40000000
*        % IF DVABASE = ''                                              40010000
*        %    THEN                                                      40020000
*        %      DVABASE = 'BASED';                                      40030000
*DCL   1 DVAREA DVABASE,                                                40500000
         2 DVAPREFX,        /*Area if no INFOLIST=, DEVTAB or RPS @H5C*/40550000
         /*Following four bytes are also returned for INFO=DEVTYPE@H5A*/40600000
           3 DVAUCBTY CHAR(4),    /* UCB TYPE FIELD               @H5C*/40650000
*            4 DVAOPTS BIT(16),   /* UCB OPTIONS                     */ 42000000
*            4 DVACLASS BIT(8),   /* DEVICE CLASS                    */ 42500000
*            4 DVAUNIT BIT(8),    /* UNIT TYPE                       */ 43000000
*          3 DVAMAXRC FIXED(31),  /* MAXIMUM RECORD SIZE             */ 43500000
*                                                                       44000000
*        2 DVATAB,                /* SECTION INCLUDED BY DEVTAB      */ 44500000
*          3 DVACYL FIXED(16),    /* PHYS NUMBER CYL PER VOLUME  @H5C*/ 44550000
*          3 DVATRK FIXED(15),    /* NR OF TRACKS PER CYL            */ 45500000
*          3 DVATRKLN FIXED(15),  /* TRACK LENGTH ( BYTES)           */ 46000000
*          3 DVAOVHD FIXED(15),   /* BLOCK OVERHEAD IF DVA2BOV IS ON */ 46500000
*                                 /***********************************/ 47000000
*                                 /* IF DVA2BOV IS OFF USE INSTEAD   */ 47500000
*                                 /* THE FOLLOWING TWO VALUES        */ 48000000
*            4 DVAOVNLB PTR(8),   /* OVERHEAD NOT LAST BLOCK         */ 48500000
*            4 DVAOVLB PTR(8),    /* OVERHEAD LAST BLOCK             */ 49000000
*                                 /***********************************/ 49500000
*          3 DVAOVNK PTR(8),      /* OVERHEAD DECR IF NOT KEYED      */ 50000000
*          3 DVAFLAGS BIT(8),     /* FLAG BYTE                       */ 50500000
*            4 DVABDCYL BIT(1),   /* IF 1, DVACYL IS INVALID YL02130 */ 50550000
*            4 DVADEFLR BIT(1),   /* DEFINE EXTENT/LOCATE RECORD AND    50600036
                                     RELATED TRANSFER COMMANDS ARE      50650036
                                     IMPLEMENTED                 @H2A*/ 50700036
*            4 DVADEFEX BIT(1),   /* DEFINE EXTENT IMPLEMENTED   @H2A*/ 50750036
*            4 DVAMODL BIT(1),    /* IF ON, USE MODULO TRACK     @01A*/ 51050000
*/*                                  ALGORTIHM                   @02C*/ 51100000
*            4 DVA2BOV BIT(1),    /* IF ON, USE DVAOVHD              */ 51500000
*                                 /* ELSE USE DVAOVNLB & DVAOVLB     */ 52000000
*            4 DVAPAGES BIT(1),   /* IF ON DEVICE SUPPORTS PAGING       52050000
*                                             CCWS               @H3A*/ 52100000
*            4 DVANOALT BIT(1),   /* NO ALT TRKS AVAILABLE   @YA03884*/ 52150000
*            4 DVAFTOL BIT(1),    /* IF ON, APPLY TOLERANCE FACTOR   */ 53000000
*                                                                       53500000
*          3 DVATOL FIXED(15),    /* TOLERANCE FACTOR                */ 54000000
*                                 /***********************************/ 54500000
*                                 /* (BLKSI+KEYLE)*DVATOL/DVADVSR    */ 55000000
*                                 /* GIVES THE ADJUSTED BLOCK SIZE   */ 55500000
*                                 /* TO WHICH APPROPRIATE OVERHEADS  */ 56000000
*                                 /* ARE THEN ADDED.                 */ 56500000
*                                 /***********************************/ 57000000
*            % DVADVSR = '512';   /* FRACTION DENOMINATOR FOR DVATOL */ 57500000
*                                                                       58000000
*        2 DVARPS,                /* RPS SECTION                     */ 58500000
*          3 DVAOVR0 FIXED(15),   /* OVERHEAD BYTES FOR RECORD 0     */ 59000000
*          3 DVASECT PTR(8),      /* NUMBER OF SECTORS IN FULL TRACK */ 59500000
*          3 DVASECTD PTR(8),     /* NUMBER OF DATA SECTORS          */ 60000000
*/***********************************************************    @H1A*/ 60050000
*/*            THE FOLLOWING FIELDS AREA USED FOR MODULO         @H1A*/ 60100000
*/*            DEVICES ONLY.(DVAMODL =  1)                       @H1A*/ 60150000
*/***********************************************************    @H1A*/ 60200000
   DVAMD1  FIXED(8)  DEFINED(DVAOVNK), /* MODULO FACTOR FOR MODULO      60250000
                                                                 @H1A*/ 60300000
   DVAOVH1 FIXED(16) DEFINED(DVAOVHD),/* FIRST MODULO OVERHEAD          60350000
                                                                 @H1A*/ 60400000
   DVAOVH2 FIXED(16) DEFINED(DVATOL); /* FIRST MODULO OVERHEAD          60450000
                                                                 @H1A*/ 60500000
 /*****************************************************************@H5A 60520000
 ! THE FOLLOWING SECTION IS RETURNED BY DEVTYPE FOR INFO=DASD.     @H5A 60540000
 *****************************************************************@H5A*/60590000
 DCL   1 DVAIDASD BASED(ADDR(DVAREA)),  /*                        @H5C*/60640000
*          3 DVAICYL  FIXED(32),  /* NUMBER OF CYLINDERS          @H4A*/60690000
*          3 DVAITRK  FIXED(32),  /* TRACKS PER CYLINDER          @H4A*/60740000
*          3 DVAIFLAG FIXED(16),  /* FLAGS                        @H4A*/60790000
*            4 DVAECKD  BIT(1),   /* IF 1, ECKD SUPPORTED         @H4A*/60840000
*            4 DVALRE   BIT(1),   /* IF 1, LOCATE RECORD EXTENDED       60900000
*                                    SUPPORTED                    @H4A*/60950000
*            4 DVACACHE BIT(1),   /* IF 1, DEVICE IS CACHED       @H4A*/61000000
*            4 *        BIT(13),  /* RESERVED                     @H4A*/61050000
*          3 *        FIXED(16),  /* RESERVED                     @H4A*/61100000
           3 DVAITSET FIXED(8),   /* TRACK SET SIZE               @H5C*/61150000
*          3 *        FIXED(24);  /* RESERVED                     @H4A*/61200000
                                                               /* @H5A*/61250000
 /******************************************************************@H5A61300000
 ! THE FOLLOWING SECTION IS RETURNED BY DEVTYPE FOR INFO=SUFFIX.    @H5A61350000
 !****************************************************************@H5A*/61400000
 DCL   DVASUFFX FIXED(15) BASED(ADDR(DVAREA));  /*SUFFIX LENGTH   @H5C*/61450000
                                                               /* @H5A*/61500000
 /*****************************************************************@H5A 61550000
 ! THE FOLLOWING SECTION IS RETURNED BY DEVTYPE FOR INFO=AMCAP.    @H5A 61600000
 !****************************************************************@H5A*/61650000
 DCL 1 DVAAMCAP CHAR(32)BASED(ADDR(DVAREA)),/*ACCMETH CAPABILITIES@H5M*/61700000
       2 DVAAMFLG BIT(8),    /*FLAGS                              @H5A*/61750000
         3 DVAAMLBI BIT(1),  /*BSAM, QSAM AND (IF DASD) BPAM      @H5A  61800000
                              !SUPPORT THE LARGE BLOCK INTERFACE & @H5A 61850000
                             !THE LIMIT IS IN THE NEXT DOUBLEWORD.@H5A*/61900000
       2 *         CHAR(7),   /*RESERVED                          @H5A*/61950000
       2 DVAMAXBLK BIT(64)    /*MAXIMUM BLOCK SIZE SUPPORTED WITH @H5A*/62000000
                   BDY(BYTE), /*SAM LBI                           @H5A*/62050000
       2 DVAOPTBLK BIT(64)    /*RECOMMENDED MAXIMUM BLOCK SIZE    @H5A*/62100000
                   BDY(BYTE), /*LONGER BLOCKS MIGHT BE LESS       @H5A  62150000
                               !EFFICIENT OR LESS RELIABLE.  LESS @H5A  62200000
                               !THAN OR EQUAL TO PREVIOUS FIELD.  @H5A*/62250000
       2 DVAMAXLR  BIT(64)    /*MAXIMUM UNSPANNED LOGICAL RECORD  @H5A*/62300000
                   BDY(BYTE); /*LENGTH SUPPORTED BY BSAM, QSAM AND@H5A*/62350000
                              /*BPAM                              @H5A  62400000
.PLXEND ANOP    (Must be in PL/X comment)                        @H5A*/ 62450000
