*/*                                                                     00025000
         MACRO                                                          00050000
&LABEL   ISITMGD  &DEB=,&DCB=,&CONCAT=,&DATATYPE=,&MF=S           @L1C  00100000
.********************************************************************** 00150000
.*                                                                    * 00200000
.* $MAC(ISITMGD) COMP(28473): IS_IT_MANAGED INVOCATION MACRO          * 00250000
.*                                                                    * 00300000
.*PROPRIETARY V3 STATEMENT                                              00314200
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00328400
.*"RESTRICTED MATERIALS OF IBM"                                         00342600
.*5695-DF1                                                              00356800
.*(C) COPYRIGHT 1989,1993  IBM CORP.                                    00371000
.*END PROPRIETARY V3 STATEMENT                                          00385200
.*                                                                    * 00400000
.* STATUS - SEE CHANGE ACTIVITY                                    @L1C 00450000
.*                                                                    * 00500000
.* PL/AS INVOCATION: SEE THE INTERNAL PL/AS MACRO, ISITMGDP        @L1C 00550000
.*                                                                    * 00600000
.* ASSEMBLER INVOCATION:                                              * 00650000
.*                                                                    * 00900000
.* <LABEL>  ISITMGD <{DEB=DEBADDR,                                 @L1C 00904200
.*                   | DCB=DCBADDR,}>                              @L1C 00908400
.*                  <CONCAT={NUMBER|ALL},>                         @L1A 00912600
.*                  <DATATYPE={YES|NO},>                           @L1A 00916800
.*                  <{MF=S |                                          * 00921300
.*                    MF=L |                                          * 00928400
.*                    MF=(E,PARMLIST)}>                               * 00935500
.*                                                                    * 00942600
.*  THE FOLLOWING ARE MUTUALLY EXCLUSIVE KEYWORDS:                    * 00950000
.*      (IN THE MF=S FORM, ONE OF THE KEYWORDS IS REQUIRED, BUT IN    * 01000000
.*       THE MF=L OR MF=E FORM THEY ARE OPTIONAL)                     * 01050000
.*                                                                    * 01100000
.*      DEB                                                           * 01150000
.*         PURPOSE: SPECIFIES THE ADDRESS OF THE DEB TO BE PASSED TO  * 01200000
.*                  THE IS_IT_MANAGED FUNCTION.  IT CAN BE AN ADDRESS * 01250000
.*                  IN MEMORY, OR AN ADDRESS IN A REGISTER (2)-(12).  * 01300000
.*                                                                    * 01350000
.*      DCB                                                           * 01400000
.*         PURPOSE: SPECIFIES THE ADDRESS OF THE DCB TO BE PASSED TO  * 01450000
.*                  THE IS_IT_MANAGED FUNCTION.  IT CAN BE AN ADDRESS * 01500000
.*                  IN MEMORY, OR AN ADDRESS IN A REGISTER (2)-(12).  * 01550000
.*                                                                    * 01600000
.*  OPTIONAL KEYWORDS:                                                * 01650000
.*                                                                    * 01700000
.*      CONCAT={NUMBER|ALL}  -  DEFAULT: 0                         @L1C 01710400
.*         PURPOSE: DEFINES THE DATA SET OR DATA SETS TO BE QUERIED@L1C 01720800
.*                                                                 @L1A 01731200
.*             NUMBER: DEFINES THE CONCATENATION NUMBER OF THE ONE @L1A 01741600
.*                  DATA SET TO BE QUERIED. NUMBER CAN BE EITHER   @L1A 01752000
.*                  AN ABSOLUTE EXPRESSION OR A VALUE IN A         @L1A 01762400
.*                  REGISTER (2)-(12). IF AN EXPRESSION, IT CANNOT @L1A 01772800
.*                  BE THE SINGLE SYMBOL 'ALL' BECAUSE THIS WOULD  @L1A 01783200
.*                  BE TREATED AS THE ALL KEYWORD.                 @L1A 01793600
.*             ALL: INDICATES THE INFORMATION RETURNED IS FOR ALL  @L1A 01804000
.*                  OF THE DATA SETS IN THE CONCATENATION. IF TWO  @L1A 01814400
.*                  DATA SETS HAVE DIFFERENT CHARACTERISTICS       @L1A 01824800
.*                  (EG. ONE SMS AND ONE NON-SMS) THE OUTPUT WILL  @L1A 01835200
.*                  REFLECT BOTH CHARACTERISTICS.                  @L1A 01845600
.*                  IF THE CONCAT=ALL IS REQUESTED ON A LEVEL OF   @L1A 01856000
.*                  DFP WHICH DOES NOT SUPPORT THIS REQUEST,       @L1A 01866400
.*                  INFORMATION WILL BE RETURNED FOR THE FIRST     @L1A 01876800
.*                  DATA SET (CONCAT=0). A PROGRAM CAN DETERMINE IF@L1A 01887200
.*                  THE SYSTEM SUPPORTS A SPECIFICATION OF ALL BY  @L1A 01897600
.*                  INSPECTING THE OUTPUT BITS. IF CONCAT=ALL WAS  @L1A 01908000
.*                  RECOGNIZED, AT LEAST TWO OF THE BITS INDICATING@L1A 01918400
.*                  DATA SET KIND WILL BE ON. IF CONCAT=ALL WAS    @L1A 01928800
.*                  NOT RECOGNIZED, AT MOST THE PDSE BIT WILL BE ON@L1A 01939200
.*                                                                    * 01950000
.*      DATATYPE={YES|NO} - DEFAULT: NO                            @L1A 01954500
.*         PURPOSE: REQUESTS THAT THE DATA TYPE IS TO BE RETURNED. @L1A 01959000
.*         THE SUPPORTED DATA TYPES ARE PDSE RECORD FORMAT LIBRARY @L1A 01963500
.*         AND PDSE LOAD MODULE LIBRARY. IF DATATYPE=YES IS        @L1A 01968000
.*         SPECIFIED ON A LEVEL OF DFP WHICH DOES *NOT* SUPPORT THE@L1A 01972500
.*         DATATYPE KEYWORD, NONE OF THE OUTPUT DATATYPE BITS WILL @L1A 01977000
.*         BE SET ON.  IF DATATYPE=YES IS SPECIFIED ON A LEVEL OF  @L1A 01981500
.*         DFP WHICH DOES SUPPORT DATATYPE, AT LEAST ONE OF THE    @L1A 01986000
.*         DATATYPE OUTPUT BITS WILL BE SET ON.                    @L1A 01990500
.*                                                                 @L1A 01995000
.*      MF=S | L | (E,PARMLIST)                                       * 02000000
.*         PURPOSE: SPECIFIES THE MACRO FORM.                         * 02050000
.*         DEFAULT: S                                                 * 02100000
.*                                                                    * 02150000
.*         MF=S                                                       * 02200000
.*           SPECIFIES THE STANDARD FORM OF THE MACRO.  THE "S"       * 02250000
.*           FORM GENERATES CODE TO PUT THE PARAMETERS INTO AN        * 02300000
.*           IN-LINE PARAMETER LIST AND INVOKES IS_IT_MANAGED.        * 02350000
.*                                                                    * 02400000
.*         MF=L                                                       * 02450000
.*           SPECIFIES THE LIST FORM OF THE MACRO.  THE "L" FORM      * 02500000
.*           BUILDS AN IN-LINE IS_IT_MANAGED PARAMETER LIST.          * 02550000
.*           SINCE THE PARAMETER LIST IS BUILT AT ASSEMBLY TIME,      * 02600000
.*           NO PARAMETERS MAY BE SPECIFIED IN REGISTER FORMAT.       * 02650000
.*                                                                    * 02700000
.*         MF=(E,PARMLIST)                                            * 02750000
.*           SPECIFIES THE EXECUTE FORM OF THE MACRO.  THE "E" FORM   * 02800000
.*           GENERATES CODE TO PUT THE PARAMETERS INTO THE            * 02850000
.*           IS_IT_MANAGED PARAMETER LIST SPECIFIED BY PARMLIST AND   * 02883300
.*           INVOKES IS_IT_MANAGED.                                   * 02916600
.*             IF NEITHER DATATYPE OR CONCAT=ALL                   @L1C 02918400
.*           IS SPECIFIED, THE MACRO WILL INITIALIZE THE PARAMETER @L1A 02920200
.*           LIST HEADER IN THE AREA DEFINED BY PARMLIST. (THIS    @L1A 02922000
.*           WAS INITIALLY INTENDED TO ACCOMODATE USERS WHO        @L1A 02923800
.*           OBTAINED DYNAMIC STORAGE FOR THEIR PARAMETER LIST,    @L1A 02925600
.*           RATHER THAN REQUIRING THE CALLER TO COPY A STATIC     @L1A 02927400
.*           VERSION OF THE PARM LIST (OBTAINED BY ISSUING ISITMGD @L1A 02929200
.*           WITH MF(L) PARM) INTO THE DYNAMIC STORAGE. HOWEVER    @L1A 02931000
.*           THIS LIMITS THE ABILITY TO UPDATE VERSIONS OF THE     @L1A 02932800
.*           PARAMTER LIST.)                                       @L1A 02934600
.*             IF EITHER DATATYPE OR CONCAT=ALL IS SPECIFIED, THIS @L1A 02936400
.*           MACRO WILL *NOT* COPY THE PARAMETER LIST HEADER INTO  @L1A 02938200
.*           THE STORAGE DEFINED BY PARMLIST. IF DYNAMIC STORAGE   @L1A 02940000
.*           IS USED BY THE CALLER FOR THE ISITMGD PARAMETER LIST, @L1A 02941800
.*           THE CALLER MUST INITIALIZE THE PARMLIST STORAGE BY    @L1A 02943600
.*           COPYING THE STATIC STORAGE DEFINED BY THE INVOCATION  @L1A 02945400
.*           OF ISITMGD MF(L).                                     @L1A 02947200
.*                                                                    * 02950000
.*           ,PARMLIST                                                * 03000000
.*             IS A REQUIRED INPUT.  IT IS THE NAME (RX-TYPE), OR     * 03050000
.*             ADDRESS IN A REGISTER OF A STORAGE AREA FOR THE        * 03100000
.*             PARAMETER LIST.  THE SPECIAL REGISTER FORM "(1)"       * 03150000
.*             MAY BE USED AND DESIGNATES THAT THE PARAMETER LIST     * 03200000
.*             ADDRESS IS ALREADY IN REGISTER 1.                      * 03250000
.*                                                                    * 03300000
.*  GENERAL PURPOSE REGISTER USAGE:                                   * 03321400
.*    REGISTER  0             REASON CODE.  INITIALIZED TO 0 BY MACRO * 03342800
.*    REGISTER  1             POINTER TO PARAMETER LIST               * 03364200
.*    REGISTER 13             POINTER TO 18 FULLWORD SAVE AREA        * 03385600
.*    REGISTER 14             RETURN ADDRESS                          * 03407000
.*    REGISTER 15             RETURN CODE.  ALSO HOLDS BRANCH ADDR.   * 03428400
.*                                                                    * 03450000
.*  EXTERNAL REFERENCES: NONE                                         * 03460600
.*                                                                    * 03471300
.*  MAPPING MACRO FOR IS_IT_MANAGED PARAMETER LIST: IGWCISM           * 03473600
.*                                                                    * 03475900
.*  CHANGE ACTIVITY:                                                  * 03478400
.*  $L0=PDSE,HDP3320,880206,STLFPC: PDSE SUPPORT                  @L0A* 03485500
.*  $P0=KET1837,HDP3320,890127,STLREB: 24 BIT SUPPORT             @P0A* 03487800
.*  $P1=KET2582,HDP3320,890413,STLREB: FIX KET1837                @P1A* 03490100
.*                                                                    * 03492600
.*  DFP 4.1.0 CHANGES:                                            @L1C  03495000
.*  $L1=PM,HDP4410,910320,SJPLREB: DATATYPE AND CONCAT=ALL PARMS  @L1A* 03497400
.********************************************************************** 03500000
.*               VARIABLE DECLARATIONS                                * 03550000
.********************************************************************** 03600000
         LCLC     &BLKPTR                HOLDS CONTENTS OF DEB OR DCB   03650000
         LCLC     &BLKREG                BLKPTR MINUS ENCLOSING PARENS  03700000
         LCLB     &ISDCB                 WAS A DCB ADDRESS SUPPLIED?    03750000
         LCLA     &SEVCODE               THIS MACRO'S ERROR CODE        03800000
&SEVCODE SETA  0                         INITIALIZE TO SEVERITY 0       03850000
         LCLC     &C                     SCRATCH CHARACTER STRING  @L1A 03852200
.*-----------------------------------------------------------------@L1A 03854400
.*      OR MASKS FOR SETTING ISMIFLG1 BITS - CONTAINS 1'S FOR ALL  @L1A 03856600
.*      BITS TO BE SET ON.                                         @L1A 03858800
.*-----------------------------------------------------------------@L1A 03861000
         LCLC     &ODEBDCB       OR MASK-BITS 0 AND 1 TO INDICATE  @L1A 03863200
.*                                       IF DCB OR DEB WAS INPUT   @L1A 03865400
         LCLC     &OALL          OR MASK-BIT 2 INDICATES IF ALL    @L1A 03867600
.*                                       PARM SPECIFIED            @L1A 03869800
         LCLC     &ODTYPE        OR MASK-BIT 3 INDICATES DATATYPE  @L1A 03872000
.*                                       PARM SPECIFIED            @L1A 03874200
.*-----------------------------------------------------------------@L1A 03876400
.*     AND MASKS FOR SETTING ISMIFLG1 BITS - CONTAINS 0'S FOR BITS @L1A 03878600
.*     TO BE SET OFF.                                              @L1A 03880800
.*-----------------------------------------------------------------@L1A 03883000
         LCLC     &ADEBDCB      AND MASK-BITS 0 AND 1 TO INDICATE  @L1A 03885200
.*                                       IF DCB OR DEB WAS INPUT   @L1A 03887400
         LCLC     &AALL         AND MASK-BIT 2 INDICATES IF ALL    @L1A 03889600
.*                                       PARM SPECIFIED            @L1A 03891800
         LCLC     &ADTYPE       AND MASK-BIT 3 INDICATES DATATYPE  @L1A 03894000
.*                                       PARM SPECIFIED            @L1A 03896200
.********************************************************************** 03900000
.*               CONSTANT DECLARATIONS                                * 03950000
.********************************************************************** 04000000
.*    PARAMETER LIST OFFSETS:                                           04033300
.*-----------------------------------------------------------------@L1A 04066600
         LCLC  &ISMID                    THE ISMID FIELD                04100000
&ISMID   SETC  '0'                                                      04150000
         LCLC  &ISMLEN                   THE ISMLEN FIELD               04200000
&ISMLEN  SETC  '8'                                                      04250000
         LCLC  &ISMLVL                   THE ISMLVL FIELD               04300000
&ISMLVL  SETC  '12'                                                     04350000
         LCLC  &ISMADDR                  THE ISMADDR FIELD              04400000
&ISMADDR SETC  '16'                                                     04450000
         LCLC  &ISMCON                   THE ISMCON FIELD               04500000
&ISMCON  SETC  '20'                                                     04550000
         LCLC  &ISMIFL1                  THE ISMIFLG1 FIELD             04600000
&ISMIFL1 SETC  '21'                                                     04650000
.*-----------------------------------------------------------------@L1A 04651700
.*    OFFSETS AND CONSTANTS USED DURING THE LINKAGE:                    04653400
.*-----------------------------------------------------------------@L1A 04655100
         LCLC  &PSACVT                   CVT POINTER IN THE PSA         04657000
&PSACVT  SETC  '16'                                                     04660500
         LCLC  &CVTSCR1                  POINTER TO SYSTEM CONVERT @L1C 04662800
.*                                         ROUTINE BRANCH TABLE    @L1A 04665100
&CVTSCR1 SETC  '232'                                                    04667500
         LCLC  &BITOFF                   OFFSET OF BITMASK              04671000
&BITOFF  SETC  '4'                                                      04674500
         LCLC  &BITMASK                  BITMASK WHICH INDICATES        04678000
&BITMASK SETC  'B''00000010'''             EXISTENCE OF IS_IT_MANAGED   04681500
         LCLC  &ISITOFF                  OFFSET IN BRANCH TABLE OF      04685000
&ISITOFF SETC  '24'                        IS_IT_MANAGED'S ADDRESS      04688500
         LCLC  &NOTSUPP                  FUNCTION_NOT_SUPPORTED         04692000
&NOTSUPP SETC  '8'                         ERROR CODE                   04695500
.********************************************************************** 04700000
.*                 START OF THE MAIN MACRO BODY                       * 04750000
.********************************************************************** 04800000
.*           SET ISDCB TO TRUE IF A DCB WAS SUPPLIED.                 * 04850000
.********************************************************************** 04900000
&ISDCB   SETB  ('&DCB' NE '')                                           04950000
.********************************************************************** 05000000
.*         SET BLKPTR TO CONTAIN EITHER THE DEB OR DCB INPUT.         * 05050000
.*       SET BLKREG TO CONTAIN THE REGISTER NUMBER (USED LATER,       * 05100000
.*                ONLY IF A REGISTER WAS SUPPLIED).                   * 05150000
.********************************************************************** 05200000
&BLKPTR  SETC  '&DCB'                                                   05250000
&BLKREG  SETC  '&DCB(1)'                                                05300000
         AIF   ('&BLKPTR' NE '').ERR1                                   05350000
&BLKPTR  SETC  '&DEB'                                                   05400000
&BLKREG  SETC  '&DEB(1)'                                                05450000
.********************************************************************** 05500000
.*                                                                    * 05550000
.*               PERFORM ALL ERROR CHECKING UP FRONT                  * 05600000
.*                                                                    * 05650000
.********************************************************************** 05700000
.*         THE DEB AND DCB PARAMETERS ARE MUTUALLY EXCLUSIVE          * 05750000
.*                    ENFORCE THIS RESTRICTION.                       * 05800000
.********************************************************************** 05850000
.ERR1    AIF   (('&DCB' EQ '') OR ('&DEB' EQ '')).ERR2                  05900000
         MNOTE 12,'*** BOTH A DCB AND DEB CAN NOT BE SUPPLIED'          05950000
&SEVCODE SETA  12                                                       06000000
.********************************************************************** 06050000
.*            CHECK THE FORMAT OF THE MF= PARAMETER.                  * 06100000
.********************************************************************** 06150000
.ERR2    AIF   ('&MF' EQ 'S').ERR3                                      06200000
         AIF   ('&MF' EQ 'L').ERR3                                      06250000
         AIF   (N'&MF NE 2).ERR2A                                       06300000
         AIF   ('&MF(1)' NE 'E').ERR2A                                  06350000
         AIF   ('&MF(2)' NE '').ERR3                                    06400000
.ERR2A   MNOTE 12,'*** THE MF PARAMETER HAS AN INVALID FORMAT'          06450000
&SEVCODE SETA  12                                                       06500000
.********************************************************************** 06550000
.*    THE DEB/DCB PARAMETER MAY NOT BE IN A REGISTER FOR THE L-FORM.  * 06600000
.*   CHECK BOTH THE DEB AND DCB PARAMETERS IN CASE BOTH WERE SUPPLIED * 06650000
.********************************************************************** 06700000
.ERR3    AIF   ('&MF' NE 'L').ERR4                                      06750000
         AIF   ('&DCB' EQ '').ERR3A                                     06800000
         AIF   ('&DCB'(1,1) EQ '(').ERR3B                               06850000
.ERR3A   AIF   ('&DEB' EQ '').ERR4                                      06900000
         AIF   ('&DEB'(1,1) NE '(').ERR4                                06950000
.ERR3B   MNOTE 12,'*** THE DCB/DEB PARAMETER MAY NOT BE IN A REGISTER'  07000000
&SEVCODE SETA  12                                                       07050000
.********************************************************************** 07100000
.*    THE CONCAT PARAMETER MAY NOT BE IN A REGISTER FOR THE L-FORM.   * 07150000
.*                 ENFORCE THIS RESTRICTION                           * 07200000
.********************************************************************** 07250000
.ERR4    AIF   ('&MF' NE 'L').ERR5                                      07300000
         AIF   ('&CONCAT' EQ '').ERR5                                   07350000
         AIF   ('&CONCAT'(1,1) NE '(').ERR5                             07400000
         MNOTE 12,'*** THE CONCAT PARAMETER MAY NOT BE IN A REGISTER'   07450000
&SEVCODE SETA  12                                                       07500000
.********************************************************************** 07550000
.*    THE S-FORM REQUIRES THAT EITHER A DEB OR DCB BE SUPPLIED.       * 07600000
.*                 ENFORCE THIS REQUIREMENT                           * 07650000
.********************************************************************** 07700000
.ERR5    AIF   ('&MF' NE 'S').ERR6                                      07750000
         AIF   (('&DEB' NE '') OR ('&DCB' NE '')).ERR6                  07800000
         MNOTE 12,'*** A DCB OR DEB MUST BE SUPPLIED'                   07850000
&SEVCODE SETA  12                                                       07900000
.********************************************************************** 07950000
.*     TEST TO ENSURE THAT DATATYPE PARAMETERS ARE VALID           @L1A 07956200
.********************************************************************** 07962400
.ERR6    AIF   (('&DATATYPE' EQ '') OR ('&DATATYPE' EQ 'NO')).ERR7 @L1A 07968600
         AIF   ('&DATATYPE' EQ 'YES').ERR7                         @L1A 07974800
       MNOTE 12,'*** THE DATATYPE PARAMETER HAS AN INVALID FORMAT' @L1A 07981000
&SEVCODE SETA  12                                                  @L1A 07987200
.********************************************************************** 07993400
.*     ANY ERROR CODE OVER 4 IS SEVERE ENOUGH TO STOP EXPANSION.      * 08000000
.*               LEAVE THE MACRO IF THIS IS THE CASE.                 * 08050000
.********************************************************************** 08100000
.ERR7    AIF   (&SEVCODE EQ 0).ERREND                              @L1C 08150000
         MEXIT                                                          08200000
.ERREND  ANOP                                                      @L1A 08200600
.********************************************************************** 08201200
.*         SET UP ISMIFLG1                                         @L1A 08201800
.********************************************************************** 08202400
.*         SETUP THE DCB OR DEB SUPPLIED BITS                      @L1A 08203000
.*-----------------------------------------------------------------@L1A 08203600
         AIF   ('&DEB' NE '').IFLG1   DEB SPECIFIED                @L1A 08204200
         AIF   ('&DCB' NE '').IFLG2   DCB SPECIFIED                @L1A 08204800
.*-----------------------------------------------------------------@L1A 08205400
.*       NEITHER DCB NOR DEB SPECIFIED                             @L1A 08206000
.*-----------------------------------------------------------------@L1A 08206600
&ODEBDCB SETC  '00'                 DON'T SET BITS ON              @L1A 08207200
&ADEBDCB SETC  '11'                 DON'T SET BITS OFF             @L1A 08207800
         AGO   .IFLG3                                              @L1A 08208400
.*-----------------------------------------------------------------@L1A 08209000
.*         DEB INPUT                                               @L1A 08209600
.*-----------------------------------------------------------------@L1A 08210200
.IFLG1   ANOP                                                      @L1A 08210800
&ODEBDCB SETC  '10'                 SET ON  ISMDEB                 @L1A 08211400
&ADEBDCB SETC  '10'                 SET OFF ISMDCB                 @L1A 08212000
         AGO   .IFLG3                                              @L1A 08212600
.*-----------------------------------------------------------------@L1A 08213200
.*         DCB INPUT                                               @L1A 08213800
.*-----------------------------------------------------------------@L1A 08214400
.IFLG2   ANOP                                                      @L1A 08215000
&ODEBDCB SETC  '01'                 SET ON ISMDCB                  @L1A 08215600
&ADEBDCB SETC  '01'                 SET OFF ISMDEB                 @L1A 08216200
.*-----------------------------------------------------------------@L1A 08216800
.*         SETUP THE ALL KEYWORD SPECIFIED BIT                     @L1A 08217400
.*-----------------------------------------------------------------@L1A 08218000
.IFLG3   AIF   ('&CONCAT' EQ 'ALL').IFLG4                          @L1A 08218600
         AIF   ('&CONCAT' EQ '').IFLG5                             @L1A 08219200
.*-----------------------------------------------------------------@L1A 08219800
.*       CONCAT  EQ  EXPRESSION                                    @L1A 08220400
.*-----------------------------------------------------------------@L1A 08221000
&OALL    SETC  '0'                  DON'T SET ON ISMALL            @L1A 08221600
&AALL    SETC  '0'                  SET OFF ISMALL                 @L1A 08222200
         AGO   .IFLG6                                              @L1A 08222800
.*-----------------------------------------------------------------@L1A 08223400
.*       CONCAT  EQ  'ALL'                                         @L1A 08224000
.*-----------------------------------------------------------------@L1A 08224600
.IFLG4   ANOP                                                      @L1A 08225200
&OALL    SETC  '1'                  SET ON ISMALL                  @L1A 08225800
&AALL    SETC  '1'                  DON'T SET OFF ISMALL           @L1A 08226400
         AGO   .IFLG6                                              @L1A 08227000
.*-----------------------------------------------------------------@L1A 08227600
.*       CONCAT NOT SPECIFIED                                      @L1A 08228200
.*-----------------------------------------------------------------@L1A 08228800
.IFLG5   ANOP                                                      @L1A 08229400
&OALL    SETC  '0'                  DON'T SET ON ISMALL            @L1A 08230000
&AALL    SETC  '1'                  DON'T SET OFF ISMALL           @L1A 08230600
.*-----------------------------------------------------------------@L1A 08231200
.*         SETUP THE DATATYPE KEYWORD SPECIFIED BIT                @L1A 08231800
.*-----------------------------------------------------------------@L1A 08232400
.IFLG6   AIF   ('&DATATYPE' EQ 'YES').IFLG7                        @L1A 08233000
         AIF   ('&DATATYPE' EQ '').IFLG7A                          @L1A 08233600
.*-----------------------------------------------------------------@L1A 08234200
.*       DATATYPE EQ 'NO'                                          @L1A 08234800
.*-----------------------------------------------------------------@L1A 08235400
&ODTYPE  SETC  '0'                  DON'T SET ON ISMDTYPE          @L1A 08236000
&ADTYPE  SETC  '0'                  SET OFF ISMDTYPE               @L1A 08236600
         AGO   .IFLGEND                                            @L1A 08237200
.*-----------------------------------------------------------------@L1A 08237800
.*       DATATYPE EQ 'YES'                                         @L1A 08238400
.*-----------------------------------------------------------------@L1A 08239000
.IFLG7   ANOP                                                      @L1A 08239600
&ODTYPE  SETC  '1'                  SET ON ISMDTYPE                @L1A 08240200
&ADTYPE  SETC  '1'                  DON'T SET OFF ISMDTYPE         @L1A 08240800
         AGO   .IFLGEND                                            @L1A 08241400
.*-----------------------------------------------------------------@L1A 08242000
.*       DATATYPE NOT SPECIFIED                                    @L1A 08242600
.*-----------------------------------------------------------------@L1A 08243200
.IFLG7A  ANOP                                                      @L1A 08243800
&ODTYPE  SETC  '0'                  DON'T SET ON ISMDTYPE          @L1A 08244400
&ADTYPE  SETC  '1'                  DON'T SET OFF ISMDTYPE         @L1A 08245000
.*-----------------------------------------------------------------@L1A 08245600
.*         SETUP OF ISMIFLG1 BITS COMPLETE                         @L1A 08246200
.*-----------------------------------------------------------------@L1A 08246800
.IFLGEND ANOP                                                      @L1A 08247400
.********************************************************************** 08250000
.*                                                                    * 08300000
.*               BRANCH TO THE APPROPRIATE MF= HANDLER                * 08350000
.*                                                                    * 08400000
.********************************************************************** 08450000
         AIF   ('&MF' EQ 'S').MFS                                  @L1C 08500000
         AIF   ('&MF' EQ 'L').MFL                                       08550000
.********************************************************************** 08600000
.*                                                                    * 08650000
.*                HANDLE EXECUTE FORM (MF=E)                          * 08700000
.*                                                                    * 08750000
.********************************************************************** 08800000
.MFE     ANOP                                                           08850000
.********************************************************************** 08900000
.*             SETUP ALIGNMENT AND CHECK THE REGISTERS                * 08950000
.********************************************************************** 09000000
         CNOP  0,4                                                      09050000
         AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').E10        @L1C  09100000
         AIF   ('&CONCAT'(1,1) NE '(').E10                              09150000
         DS    0XL(&CONCAT-1)            CHECK CONCAT REG LOWER BOUND   09200000
         DS    0XL(13-&CONCAT)           CHECK CONCAT REG UPPER BOUND   09250000
.E10     AIF   ('&BLKPTR' EQ '').E20                                    09300000
         AIF   ('&BLKPTR'(1,1) NE '(').E20                              09350000
         DS    0XL(&BLKPTR-1)            CHECK DCB/DEB REG LOWER BOUND  09400000
         DS    0XL(13-&BLKPTR)           CHECK DCB/DEB REG UPPER BOUND  09450000
.********************************************************************** 09500000
.*    MOVE THE PARAMETER LIST ADDRESS INTO REGISTER 1 AND PLACE THE   * 09550000
.*                       PASSED LABEL DOWN.                           * 09600000
.********************************************************************** 09650000
.E20     AIF   ('&MF(2)' EQ '(1)').E40                                  09700000
         AIF   ('&MF(2)'(1,1) EQ '(').E30                               09750000
&LABEL   LA    1,&MF(2)                  GET PARAMETER LIST ADDRESS     09800000
         AGO   .E50                                                     09850000
.E30     ANOP                                                           09900000
&LABEL   LR    1,&MF(2,1)                TRANSFER PARM LIST ADDRESS     09950000
         LA    1,0(0,1)                  CLEAR HIGH BIT/BYTE            09975000
         AGO   .E50                                                     10000000
.E40     ANOP                                                           10050000
&LABEL   LA    1,0(0,1)                  CLEAR HIGH BIT/BYTE            10100000
.********************************************************************** 10150000
.*     SET THE PARAMETER LIST HEADER IF NEW PARMS NOT SPECIFIED    @L1C 10200000
.********************************************************************** 10250000
.E50     AIF   ('&ODTYPE' EQ '1' OR '&OALL' EQ '1').E55            @L1A 10283300
         BAS   15,*+20                   BRANCH AROUND HEADER      @L1C 10316600
         DC    CL8'ISITMGD '             PARAMETER LIST ID              10350000
         DC    FL4'28'                   PARAMETER LIST LENGTH          10400000
         DC    X'01'                     PARAMETER LIST LEVEL           10450000
         DC    3X'00'                    RESERVED BYTES                 10500000
         MVC   0(16,1),0(15)             MOVE HEADER TO PARAMETER LIST  10550000
.E55     ANOP                            DO NOT COPY HEADER. THIS  @L1A 10558300
.*                                        SHOULD NEVER HAVE BEEN   @L1A 10566600
.*                                        DONE IN INITIAL VERSION  @L1A 10574900
.*                                        OF THIS MACRO. THIS IS   @L1A 10583200
.*                                        CALLERS RESPONSIBILITY.  @L1A 10591500
.********************************************************************** 10600000
.*        SET THE ISMADDR PARAMETER AND CHECK REGISTER BOUNDS         * 10650000
.********************************************************************** 10700000
         AIF   ('&BLKPTR' EQ '').E70                               @L1C 10750000
         AIF   ('&BLKPTR'(1,1) EQ '(').E60                              10800000
         LA    0,&BLKPTR                 GET DCB/DEB ADDRESS            10850000
         ST    0,&ISMADDR.(0,1)          STORE DCB/DEB ADDESS           10900000
         AGO   .E70                                                     10950000
.E60     LA    15,0(0,&BLKREG)           CLEAR HIGH BIT/BYTE            10983300
         ST    15,&ISMADDR.(0,1)         STORE DCB/DEB ADDRESS          11016600
.********************************************************************** 11050000
.*       SET ISMCON                                                @L1C 11100000
.********************************************************************** 11150000
.E70     AIF   ('&CONCAT' EQ '').E100                              @L1C 11183300
         AIF   ('&CONCAT'(1,1) EQ '(').E80                         @L1C 11216600
         AIF   ('&CONCAT' EQ 'ALL').E90                            @L1A 11249900
         MVI   &ISMCON.(1),&CONCAT       STORE CONCATENATION NUMBER     11283200
         AGO   .E100                                               @L1C 11316500
.E80     STC   &CONCAT(1),&ISMCON.(0,1)  STORE CONCATENATION NUMBER     11349800
         AGO   .E100                                               @L1A 11383100
.E90     MVI   &ISMCON.(1),0             CLEAR CONCATENATION NUMBER     11416400
.********************************************************************** 11450000
.*       SET ISMIFLG1                                              @L1A 11500000
.********************************************************************** 11550000
.E100    AIF   ('&ODEBDCB.&OALL.&ODTYPE' EQ '0000').E110           @L1A 11600000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1A 11650000
         OI    &ISMIFL1.(1),B'&C.'       SET ON INPUT BITS              11700000
.E110    AIF   ('&ADEBDCB.&AALL.&ADTYPE' EQ '1111').EEND           @L1A 11750000
&C       SETC  '&ADEBDCB.&AALL.&ADTYPE.1111'                       @L1A 11800000
         NI    &ISMIFL1.(1),B'&C.'       SET OFF INPUT BITS             11850000
.********************************************************************** 11900000
.*               CALL THE IS_IT_MANAGED ROUTINE.                      * 11950000
.*       USE THE CODE SHARED BY THE MF=E AND MF=S FORMATS             * 12000000
.********************************************************************** 12050000
.EEND    AGO   .SE10                                               @L1C 12100000
.********************************************************************** 12150000
.*                                                                    * 12200000
.*               HANDLE STANDARD FORM (MF=S)                          * 12250000
.*                                                                    * 12300000
.********************************************************************** 12350000
.MFS     ANOP                                                           12400000
.********************************************************************** 12450000
.*             SETUP ALIGNMENT AND CHECK THE REGISTERS                * 12500000
.********************************************************************** 12550000
         CNOP  0,4                                                      12600000
         AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').S10         @L1C 12650000
         AIF   ('&CONCAT'(1,1) NE '(').S10                              12700000
         DS    0XL(&CONCAT-1)            CHECK CONCAT REG LOWER BOUND   12750000
         DS    0XL(13-&CONCAT)           CHECK CONCAT REG UPPER BOUND   12800000
.S10     AIF   ('&BLKPTR' EQ '').S20                                    12850000
         AIF   ('&BLKPTR'(1,1) NE '(').S20                              12900000
         DS    0XL(&BLKPTR-1)            CHECK DCB/DEB REG LOWER BOUND  12950000
         DS    0XL(13-&BLKPTR)           CHECK DCB/DEB REG UPPER BOUND  13000000
.********************************************************************** 13050000
.*             CREATE THE PARAMETER LIST HEADER.                      * 13100000
.********************************************************************** 13150000
.S20     ANOP                                                           13200000
&LABEL   BAS   1,*+32                    BRANCH AROUND PARAMETER LIST   13250000
         DC    CL8'ISITMGD '             PARAMETER LIST ID              13300000
         DC    F'28'                     PARAMETER LIST LENGTH          13350000
         DC    X'01'                     PARAMETER LIST LEVEL           13400000
         DC    3X'00'                    RESERVED BYTES                 13450000
.********************************************************************** 13500000
.*               SET THE ISMADDR PARAMETER                            * 13550000
.********************************************************************** 13600000
         AIF   ('&BLKPTR'(1,1) EQ '(').S30                              13650000
         DC    A(&BLKPTR)                INPUT PTR TO DCB OR DEB        13700000
         AGO   .S40                                                     13750000
.S30     DC    A(0)                      INPUT PTR TO DCB OR DEB        13800000
.********************************************************************** 13850000
.*              SET THE CONCATENATION NUMBER                          * 13900000
.********************************************************************** 13950000
.S40     AIF   ('&CONCAT' EQ '').S50                                    14000000
         AIF   ('&CONCAT'(1,1) EQ '(').S50                              14050000
         AIF   ('&CONCAT' EQ 'ALL').S50                            @L1A 14075000
         DC    AL1(&CONCAT)              CONCATENATION NUMBER           14100000
         AGO   .S60                                                     14150000
.S50     DC    AL1(0)                    CONCATENATION NUMBER           14200000
.********************************************************************** 14250000
.*            SET THE ISIFLG1 FIELD                                @L1C 14300000
.********************************************************************** 14350000
.S60     ANOP                                                      @L1A 14400000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1A 14450000
         DC    B'&C.'                    INPUT FLAGS                    14500000
.********************************************************************** 14600000
.*              CREATE THE PARAMETER LIST TAIL.                       * 14650000
.********************************************************************** 14700000
         DC    6X'00'                    OUTPUT/RESERVED BYTES          14746600
.*                                                                 @L1D 14793200
.********************************************************************** 14840000
.*        DYNAMICALLY ALTER THE PARAMETER LIST AS IS NECESSARY.       * 14850000
.********************************************************************** 14900000
.*                MODIFY THE ISMADDR PARAMETER                        * 14950000
.********************************************************************** 15000000
         AIF   ('&BLKPTR'(1,1) NE '(').S90                              15050000
         LA    15,0(0,&BLKREG)           CLEAR HIGH BIT/BYTE            15083300
         ST    15,&ISMADDR.(0,1)         SET DCB/DEB ADDRESS            15116600
.********************************************************************** 15150000
.*                 MODIFY THE ISMCON PARAMETER                        * 15200000
.********************************************************************** 15250000
.S90     AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').SEND        @L1A 15300000
         AIF   ('&CONCAT'(1,1) NE '(').SEND                        @L1A 15350000
         STC   &CONCAT(1),&ISMCON.(0,1)  SET CONCATENATION NUMBER       15400000
.SEND    ANOP                                                      @L1A 15425000
.********************************************************************** 15450000
.*            COMMON CODE SECTION TO CALL IS_IT_MANAGED               * 15500000
.*     BOTH THE MF=E AND MF=S FORMATS USE THIS INVOCATION SECTION     * 15550000
.********************************************************************** 15600000
.SE10    SR    0,0                       ZERO REASON CODE REGISTER      15650000
         L     15,&PSACVT.(0,0)          GET CVT POINTER                15700000
         L     15,&CVTSCR1.(0,15)        GET POINTER TO BRANCH TABLE    15750000
         TM    &BITOFF.(15),&BITMASK     DOES ROUTINE EXIST?            15800000
         BNZ   *+12                      YES - GO CALL ROUTINE          15850000
         LA    15,&NOTSUPP.(0,0)         SET RETURN CODE 8              15900000
         B     *+10                      BRANCH AROUND ROUTINE CALL     15950000
         L     15,&ISITOFF.(0,15)        GET ROUTINE ADDRESS            16000000
         BASSM 14,15                     CALL ROUTINE                   16050000
         MEXIT                                                          16250000
.********************************************************************** 16300000
.*                                                                    * 16350000
.*                 HANDLE LIST FORM (MF=L)                            * 16400000
.*                                                                    * 16450000
.********************************************************************** 16500000
.MFL     ANOP                                                           16550000
.********************************************************************** 16600000
.*             CREATE THE PARAMETER LIST HEADER.                      * 16650000
.********************************************************************** 16700000
&LABEL   DS    0F                        BUILD LIST ON WORD BOUNDARY    16750000
         DC    CL8'ISITMGD '             PARAMETER LIST ID              16800000
         DC    F'28'                     PARAMETER LIST LENGTH          16850000
         DC    X'01'                     PARAMETER LIST LEVEL           16900000
         DC    3X'00'                    RESERVED BYTES                 16950000
.********************************************************************** 17000000
.*               SET THE ISMADDR PARAMETER                            * 17050000
.********************************************************************** 17100000
         AIF   ('&BLKPTR' EQ '').L10                                    17150000
         DC    A(&BLKPTR)                INPUT PTR TO DCB OR DEB        17200000
         AGO   .L20                                                     17250000
.L10     DC    A(0)                      INPUT PTR TO DCB OR DEB        17300000
.********************************************************************** 17350000
.*              SET THE CONCATENATION NUMBER                          * 17400000
.********************************************************************** 17450000
.L20     AIF   ('&CONCAT' EQ '' OR '&CONCAT' EQ 'ALL').L30         @L1C 17500000
         DC    AL1(&CONCAT)              CONCATENATION NUMBER           17550000
         AGO   .L40                                                     17600000
.L30     DC    AL1(0)                    CONCATENATION NUMBER           17650000
.********************************************************************** 17700000
.*            SET THE ISIFLG1 BYTE                                 @L1C 17750000
.********************************************************************** 17800000
.L40     ANOP                                                      @L1C 17850000
&C       SETC  '&ODEBDCB.&OALL.&ODTYPE.0000'                       @L1C 17900000
         DC    B'&C.'                    ISIFLG1 BYTE                   17950000
.********************************************************************** 18200000
.*              CREATE THE PARAMETER LIST TAIL.                       * 18250000
.********************************************************************** 18300000
.L70     DC    6X'00'                    OUTPUT AND RESERVED BYTES      18350000
         MEND                                                           18400000
**/                                                                     18440000
*/********************************************************************/ 18480000
*/* INVOKE THE INTERNAL PL/AS VERSION OF THE MACRO                   */ 18520000
*/********************************************************************/ 18560000
*%ISITMGD:  MACRO KEYS(DEB, DCB, CONCAT, DATATYPE, MF);        /*@L1C*/ 18600000
*   ANS ('?' || MACLABEL || ' ISITMGDP ' || MACLIST ||                  18650000
*        ' ' || MACKEYS  || ';') COL(MACLMAR) RESCAN;                   18700000
*%END ISITMGD;                                                          18750000
