*%/*                                                                    00050000
         MACRO                                                          00100000
&NAME    LSEXPAND &NORMAL=,        Size of normal link stack           *00150000
               &RECOVERY=          Size of RECOVERY link stack          00200000
         AGO   .SKIP                                                 */ 00250000
*/****  START OF SPECIFICATIONS  *************************************/ 00300000
*/*                                                                  */ 00350000
*/*01*  MACRO NAME = LSEXPAND                                        */ 00400000
*/*                                                                  */ 00450000
*/*01*  DESCRIPTIVE NAME = Linkage Stack Expand Macro                */ 00500000
*/*                                                                  */ 00550000
*/*01*  COPYRIGHT=                                                   */ 00600000
*/*         5685-001                                                 */ 00650000
*/*         THIS MACRO IS "RESTRICTED MATERIALS OF IBM"              */ 00700000
*/*         (C) COPYRIGHT IBM CORP. 1988                             */ 00750000
*/*         LICENSED MATERIALS - PROPERTY OF IBM                     */ 00800000
*/*                                                                  */ 00950000
*/*01*  STATUS =  HBB3310                                            */ 01000000
*/*                                                                  */ 01050000
*/*01*  FUNCTION =                                                   */ 01100000
*/*       Provides an interface to the Linkage Stack Expand          */ 01150000
*/*       routine (IEAVLSEX). IEAVLSEX will then expand the normal   */ 01200000
*/*       or recovery LS to the size indicated by LSEXPAND.          */ 01250000
*/*                                                                  */ 01300000
*/*02*    OPERATION =                                                */ 01350000
*/*         The LSEXPAND macro parses the keywords and invokes       */ 01400000
*/*         IEAVLSEX.                                                */ 01450000
*/*         Reg 0 will be set to the size of the normal stack.       */ 01500000
*/*         Reg 1 will be set to the size of the recovery stack.     */ 01550000
*/*         LSEXPAND then issues a PC to the IEAVLSEX service        */ 01600000
*/*         routine using register 14.                               */ 01650000
*/*                                                                  */ 01700000
*/*         In the event of an error, syntax checking will attempt   */ 01750000
*/*         to continue for the remainder of the input. No data      */ 01800000
*/*         will be generated.                                       */ 01850000
*/*                                                                  */ 01900000
*/*01*  INPUT =  (BAL)                                               */ 01950000
*/*                                                                  */ 02000000
*/*       ,NORMAL=n         Expand the task's normal link stack      */ 02050000
*/*                          to n entries (n must be > zero).        */ 02100000
*/*                          Decimal number, constant or register.   */ 02150000
*/*                                                                  */ 02200000
*/*       ,RECOVERY=n       Expand the task's recovery link stack    */ 02250000
*/*                          to n entries (n must be > zero).        */ 02300000
*/*                          Decimal number, constant or register.   */ 02350000
*/*                                                                  */ 02400000
*/*                                                                  */ 02450000
*/*01*  INPUT =  (PLAS)                                              */ 02500000
*/*                         See LSEXPNDP for PLAS input description. */ 02550000
*/*                                                                  */ 02600000
*/*                                                                  */ 02650000
*/*02*    NOTES =                                                    */ 02700000
*/*         -  If explicit register notation is specified, only      */ 02750000
*/*            registers (2) through (12) may be used.               */ 02800000
*/*                                                                  */ 02850000
*/*01*  NOTES =                                                      */ 02900000
*/*                                                                  */ 02950000
*/*02*    RESTRICTIONS = None                                        */ 03000000
*/*                                                                  */ 03050000
*/*02*    DEPENDENCIES = None                                        */ 03100000
*/*                                                                  */ 03150000
*/*03*      CHARACTER-CODE DEPENDENCIES= EBCDIC                      */ 03200000
*/*                                                                  */ 03250000
*/*01*  MODULE TYPE = BIL MACRO                                      */ 03300000
*/*                                                                  */ 03350000
*/*02*    PROCESSOR = ASSEMBLER-H  or  PLAS                          */ 03400000
*/*                                                                  */ 03450000
*/*02*    INVOCATION = See MACRO definition                          */ 03500000
*/*                                                                  */ 03550000
*/*01*  COMPONENT  =  SC1C5 (SUPERVISOR CONTROL)                     */ 03600000
*/*                                                                  */ 03650000
*/*01*  DISTRIBUTION LIBRARY = AMACLIB                               */ 03700000
*/*                                                                  */ 03750000
*/**** END OF SPECIFICATIONS *****************************************/ 03800000
*/*                                                                     03850000
.*                                                                      03900000
.*   CHANGE ACTIVITY =                                                  03950000
.*      $L0=AR      HBB3310 860418 PD16LD: Access Registers             04000000
.*      OY22468     Correct macro logic to accept normal, and           04016600
.*                  recovery values greater than 4095 decimal  @YA22468 04033200
.*                                                                      04050000
.********************************************************************** 04100000
.*                                                                      04150000
.SKIP    ANOP ,                                                         04200000
*        MACDATE  06/29/89                                              04250000
.*                                                                      04300000
.********************************************************************** 04350000
.*                                                                    * 04400000
.*       Declare GLOBAL and LOCAL variables                           * 04450000
.*                                                                    * 04500000
.********************************************************************** 04550000
.*                                                                      04600000
         LCLA  &AWK           Arithmetic work variable                  04650000
         LCLC  &CWK           Character work variable                   04700000
         LCLA  &NREG          GPR for NORMAL value                      04800000
         LCLB  &NREGI         Normal register usage indicator  @YA22468 04825000
         LCLC  &NVAL          NORMAL value                              04850000
         LCLB  &IHBERR        Error flag                                04900000
         LCLA  &L1            Lower bound                               04950000
         LCLA  &OSFT          Offset into System Function Table         05000000
         LCLA  &RREG          GPR for RECOVERY value                    05100000
         LCLB  &RREGI         Recovery reg usage indicator     @YA22468 05125000
         LCLC  &RVAL          RECOVERY value                            05150000
         LCLA  &R1            Register usage lower bound                05200000
         LCLA  &R2            Register usage upper bound                05250000
         LCLC  &BRLABEL       Branch label                     @YA22468 05275000
.*                                                                      05300000
.********************************************************************** 05350000
.*                                                                    * 05400000
.*       Constants                                                    * 05450000
.*                                                                    * 05500000
.********************************************************************** 05550000
.*                                                                      05600000
&OSFT    SETA   152           Offset into SFT for NORMAL/RECOVERY       05650000
&NREG    SETA   0             GPR for NORMAL  value                     05700000
&RREG    SETA   1             GPR for RECOVERY value                    05750000
.*                                                                      05800000
&R1      SETA   2             General register lower bound              05850000
&R2      SETA  12             General register upper bound              05900000
.*                                                                      05950000
.********************************************************************** 06000000
.*                                                                    * 06050000
.*       Initialization                                               * 06100000
.*                                                                    * 06150000
.********************************************************************** 06200000
         ACTR  10000          Set for infinite loop control             06250000
.*                            Reset Global Variables                    06300000
&IHBERR  SETB  0              Reset error flag                          06350000
&NREGI   SETB  0              Reset normal reg indicator       @YA22468 06366600
&RREGI   SETB  0              Reset recovery reg indicator     @YA22468 06383200
&BRLABEL SETC  'IHB&SYSNDX.A' Set unique branch label          @YA22468 06391600
.*                                                                      06400000
.********************************************************************** 06450000
.*                                                                    * 06500000
.*       Validate parameters                                          * 06550000
.*                                                                    * 06600000
.********************************************************************** 06650000
.*                                                                      06700000
         AIF   (N'&SYSLIST GE 1).ERR01    Too many positionals          06750000
.EXPOS   ANOP ,                                                         06800000
.*                                        NORMAL or RECOVERY required   06850000
         AIF   ('&NORMAL' EQ '' AND '&RECOVERY' EQ '').ERR09            06900000
.*                                                                      06950000
.********************************************************************** 07000000
.*                                                                    * 07050000
.*       NORMAL=n                                                     * 07100000
.*                                                                    * 07150000
.********************************************************************** 07200000
.*                                                                      07250000
&L1      SETA  1                             Minimum value              07300000
         AIF   ('&NORMAL' EQ '').SETNDEF     Take default      @YA22468 07350000
         AIF   (N'&NORMAL GT 1).ERR11A       Too many sublists          07500000
&NVAL    SETC  '&NORMAL(1)'                  Set value                  07550000
         AIF   ('&NORMAL'(1,1) EQ '(').NORREG  Register form            07600000
         AIF   (T'&NORMAL NE 'N').EXNORM Must be constant name @YA22468 07650000
         AIF   (&NORMAL LT &L1).ERR11B   Minimum value                  07700000
         AGO   .EXNORM                   Done                           07750000
.NORREG  ANOP  ,                                                        07900000
         AIF   (T'&NORMAL NE 'N').NORMREG    Must be a register name    07950000
&AWK     SETA  K'&NORMAL-2                   Length of register         08000000
&CWK     SETC  '&NORMAL'(2,&AWK)             Isolate register           08050000
         AIF   ('&CWK' LT '&R1' OR '&CWK' GT '&R2').ERR11C Reg invalid  08100000
.NORMREG ANOP  ,                             Register                   08150000
&NREGI   SETB  1            Indicate register usage            @YA22468 08170000
         AGO   .EXNORM      Done, go process recovery operand  @YA22468 08190000
.SETNDEF ANOP  ,                                               @YA22468 08210000
&NVAL    SETC  '0'          normal default taken               @YA22468 08230000
.EXNORM  ANOP  ,                                                        08250000
.*                                                                    * 08300000
.********************************************************************** 08350000
.*                                                                    * 08400000
.*       RECOVERY=n                                                   * 08450000
.*                                                                    * 08500000
.********************************************************************** 08550000
.*                                                                      08600000
&L1      SETA  1                             Minimum value              08650000
         AIF   ('&RECOVERY' EQ '').SETRDEF   Take default      @YA22468 08700000
         AIF   (N'&RECOVERY GT 1).ERR12A     Too many sublists          08850000
&RVAL    SETC  '&RECOVERY(1)'                Set value         @YA22468 08875000
         AIF   ('&RECOVERY'(1,1) EQ '(').RECREG Register form           08900000
         AIF   (T'&RECOVERY NE 'N').EXRECV   Must be a constant         08933300
.*                                           name              @YA22468 08966600
         AIF   (&RECOVERY LT &L1).ERR12B     Minimum value              09000000
         AGO   .EXRECV                       Done                       09200000
.RECREG  ANOP  ,                                                        09250000
         AIF   (T'&RECOVERY NE 'N').RECOREG  Must be a register name    09300000
&AWK     SETA  K'&RECOVERY-2                 Length of register         09350000
&CWK     SETC  '&RECOVERY'(2,&AWK)           Isolate register           09400000
         AIF   ('&CWK' LT '&R1' OR '&CWK' GT '&R2').ERR12C Reg invalid  09450000
.RECOREG ANOP  ,                             Register                   09500000
&RREGI   SETB  1            Indicate register usage            @YA22468 09530000
         AGO   .EXRECV      Done, go generate entry linkage    @YA22468 09560000
.SETRDEF ANOP  ,                                               @YA22468 09590000
&RVAL    SETC  '0'          recovery default taken             @YA22468 09620000
.EXRECV  ANOP  ,                                                        09650000
.*                                                                    * 09700000
.********************************************************************** 09750000
.*                                                                    * 09800000
.*       Generate Entry Linkage                                       * 09850000
.*                                                                    * 09900000
.********************************************************************** 09950000
.*                                                                      10000000
         AIF   (&IHBERR EQ 1).EXIT     Exit if errors encountered       10050000
.*                                                                      10100000
&AWK     SETA  0                       Comment not printed              10150000
         AIF   ('&NORMAL' EQ '' OR T'&NORMAL EQ 'N').TESTREC            10200000
&AWK     SETA  1                       Comment printed                  10250000
*        THE FOLLOWING ORG INSTRUCTIONS WILL CAUSE AN ASSEMBLY ERROR    10300000
*        IF A RELOCATABLE EXPRESSION IS USED.                           10350000
         ORG   *+&NVAL                 TEST FOR ABSOLUTE VALUE          10400000
         ORG   *-&NVAL                 OF NORMAL PARAMETER              10450000
.*                                                                      10500000
.TESTREC ANOP  ,                                                        10550000
         AIF   ('&RECOVERY' EQ '' OR T'&RECOVERY EQ 'N').NOTREL         10600000
         AIF   (&AWK EQ 1).RECTEST     Comment was printed              10650000
*        THE FOLLOWING ORG INSTRUCTIONS WILL CAUSE AN ASSEMBLY ERROR    10700000
*        IF A RELOCATABLE EXPRESSION IS USED.                           10750000
.RECTEST ANOP  ,                                                        10800000
         ORG   *+&RVAL                 TEST FOR ABSOLUTE VALUE          10850000
         ORG   *-&RVAL                 OF RECOVERY PARAMETER            10900000
.*                                                                      10950000
.NOTREL  ANOP  ,                                                        11000000
.*                                                                      11150000
         AIF   ('&NREGI' EQ '1' OR '&RREGI' EQ '1').REGUSE  If any regs 11150900
.*                                  are used, bypass the LM    @YA22468 11151800
.*                                                                      11152700
.*  NOTE: &NVAL,&RVAL may contain either specified or default           11153600
.*        values                                               @YA22468 11154500
.*                                                                      11155400
         B     &BRLABEL                BRANCH AROUND DC        @YA22468 11156300
.*                                                                      11157200
         DC    A(&NVAL)                NORMAL LSS SIZE         @YA22468 11158100
.*                                                                      11159000
         DC    A(&RVAL)                RECOVERY LSS SIZE       @YA22468 11159900
.*                                                                      11160800
&BRLABEL LM    &NREG,&RREG,*-8         LOAD NORMAL AND                  11161700
*                                      RECOVERY LSS SIZES      @YA22468 11162600
.*                                                                      11163500
         AGO   .FINISH                 Continue entry linkage  @YA22468 11164400
.*                                                                      11165300
.REGUSE  ANOP  ,          Reg being used, process individually @YA22468 11166200
.*                                                                      11167100
         AIF   ('&NREGI' EQ '1').CKREC If reg specified, cont. @YA22468 11168000
.*                                                                      11168900
         B     &BRLABEL                BRANCH AROUND DC        @YA22468 11169800
.*                                                                      11170700
         DC    A(&NVAL)                NORMAL LSS SIZE         @YA22468 11171600
.*                                                                      11172500
&BRLABEL L     &NREG,*-4               LOAD NORMAL LSS SIZE    @YA22468 11173400
.*                                                                      11174300
.CKREC   ANOP  ,                                               @YA22468 11175200
.*                                                                      11176100
         AIF   ('&RREGI' EQ '1').DOREGS If reg specified, cont @YA22468 11177000
.*                                                                      11177900
         B     &BRLABEL                BRANCH AROUND DC        @YA22468 11178800
.*                                                                      11179700
         DC    A(&RVAL)                RECOVERY LSS SIZE       @YA22468 11180600
.*                                                                      11181500
&BRLABEL L     &RREG,*-4               LOAD RECOVERY LSS SIZE  @YA22468 11182400
.*                                                                      11183300
.*                                                                      11184200
.DOREGS  ANOP  ,           Process register specifications     @YA22468 11185100
.*                                                                      11186000
         AIF   ('&NREGI' NE '1').DOREG1 If normal reg not               11186900
.*                                      specified, continue    @YA22468 11187800
.*                                                                      11188700
         LR    &NREG,&NVAL             LOAD NORMAL LSS SIZE    @YA22468 11189600
.*                                                                      11190500
.DOREG1  ANOP  ,                                               @YA22468 11191400
.*                                                                      11192300
         AIF   ('&RREGI' NE '1').FINISH If recovery reg not             11193200
.*                                      specified, continue    @YA22468 11194100
.*                                                                      11195000
         LR    &RREG,&RVAL             LOAD RECOVERY LSS SIZE  @YA22468 11195900
.*                                                                      11196800
.*                                                                      11197700
.FINISH  ANOP  ,                                               @YA22468 11198600
         L     14,16(0,0)              CVT ADDRESS                      11200000
         L     14,772(14,0)            SYSTEM FUNCTION TABLE ADDR       11250000
         L     14,&OSFT.(14,0)         GET LX/EX FOR IEAVLSEX           11300000
         PC    0(14)                   PC TO IEAVLSEX SERVICE ROUTINE   11350000
         AGO   .EXIT                   Done                             11400000
.*                                                                      11450000
.********************************************************************** 11500000
.*                                                                    * 11550000
.*       MNOTES                                                       * 11600000
.*                                                                    * 11650000
.********************************************************************** 11700000
.*                                                                      11750000
.ERR01   ANOP  ,                                                        11800000
         MNOTE 8,' TOO MANY POSITIONAL PARAMETERS'                      11850000
&IHBERR  SETB  1                    Indicate error                      11900000
         AGO   .EXPOS               Continue keyword validation         11950000
.*                                                                      12000000
.ERR09   ANOP  ,                                                        12050000
         MNOTE 8,' NORMAL OR RECOVERY MUST BE SPECIFIED'                12100000
         AGO   .EXIT                Done                                12150000
.********************************************************************** 12200000
.ERR11A  ANOP  ,                                                        12250000
         MNOTE 8,' NORMAL PARAMETER CONTAINS TOO MANY SUBLISTS'         12300000
&IHBERR  SETB  1                    Indicate error                      12350000
         AGO   .EXNORM              Continue keyword validation         12400000
.*                                                                      12450000
.ERR11B  ANOP  ,                                                        12500000
         MNOTE 8,' NORMAL PARAMETER MUST BE A DECIMAL VALUE GREATER THAX12550000
               N ZERO'                                                  12600000
&IHBERR  SETB  1                    Indicate error                      12650000
         AGO   .EXNORM              Continue keyword validation         12700000
.*                                                                      12750000
.ERR11C  ANOP  ,                                                        12800000
         MNOTE 8,' NORMAL MUST USE REGISTER &R1 THRU &R2'               12850000
&IHBERR  SETB  1                    Indicate error                      12900000
         AGO   .EXNORM              Continue keyword validation         12950000
.********************************************************************** 13000000
.ERR12A  ANOP  ,                                                        13050000
         MNOTE 8,' RECOVERY PARAMETER CONTAINS TOO MANY SUBLISTS'       13100000
&IHBERR  SETB  1                    Indicate error                      13150000
         AGO   .EXRECV              Continue keyword validation         13200000
.*                                                                      13250000
.ERR12B  ANOP  ,                                                        13300000
         MNOTE 8,' RECOVERY PARAMETER MUST BE A DECIMAL VALUE GREATER TX13350000
               HAN ZERO'                                                13400000
&IHBERR  SETB  1                    Indicate error                      13450000
         AGO   .EXRECV              Continue keyword validation         13500000
.*                                                                      13550000
.ERR12C  ANOP  ,                                                        13600000
         MNOTE 8,' RECOVERY MUST USE REGISTER &R1 THRU &R2'             13650000
&IHBERR  SETB  1                    Indicate error                      13700000
         AGO   .EXRECV              Continue keyword validation         13750000
.*                                                                      13800000
.********************************************************************** 13850000
.*                                                                    * 13900000
.*       EXIT                                                         * 13950000
.*                                                                    * 14000000
.********************************************************************** 14050000
.EXIT    ANOP  ,                                                        14100000
         MNOTE ' '                    Null MNOTES allow spacing to      14150000
.*                                     occur if NOGEN specified         14200000
         MEND ,                                                         14250000
**/                                                                     14300000
* LSEXPAND:                                                             14350000
*    MACRO                                                              14400000
*     KEYS     (NORMAL,       /* Size of normal link stack           */ 14450000
*               RECOVERY);    /* Size of RECOVERY link stack         */ 14500000
*     ANS('?'|| MACLABEL||' LSEXPNDP '||MACLIST||MACKEYS||';');         14550000
*%END    LSEXPAND;                                                      14600000
