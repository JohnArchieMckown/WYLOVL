*%/*                                                                    00050000
         MACRO                                                          00100000
&NAME    WAIT &CNT,                Event number                        *00106700
               &ECB=,              ECB address                         *00113400
               &ECBLIST=,          ECB list address                    *00120100
               &LONG=NO,           NO | YES                            *00126800
               &LINKAGE=SVC,       SVC | SYSTEM                        *00133500
               &EUT=,              NOSAVE|SAVE                     @01A*00136800
               &RELATED=           Any value                       @L1C 00140200
         AGO   .SKIP                                                 */ 00146900
*/****  START OF SPECIFICATIONS  *************************************/ 00153600
*/*                                                                  */ 00160300
*/*01*  MACRO NAME = WAIT                                            */ 00167000
*/*                                                                  */ 00173700
*/*01*  DESCRIPTIVE NAME = WAIT Macro                                */ 00180400
*/*                                                                  */ 00187100
*/*01*  COPYRIGHT=                                                   */ 00193800
*/*         5647-A01                                                 */ 00200500
*/*         THIS MACRO IS "RESTRICTED MATERIALS OF IBM"              */ 00207200
*/*         (C) COPYRIGHT IBM CORP. 1988, 2000                       */ 00213900
*/*         LICENSED MATERIALS - PROPERTY OF IBM                     */ 00220600
*/*                                                                  */ 00240700
*/*01*  STATUS =  HBB7703                                            */ 00244800
*/*                                                                  */ 00249000
*/*01*  EXTERNAL CLASSIFICATION: PI                                  */ 00250600
*/*01*  END OF EXTERNAL CLASSIFICATION:                              */ 00252200
*/*                                                                  */ 00254100
*/*01*  FUNCTION =                                                   */ 00260800
*/*       Provides an interface to the WAIT service routine          */ 00267500
*/*       (IEAVEWAT).                                                */ 00274200
*/*                                                                  */ 00280900
*/*02*    OPERATION =                                                */ 00287600
*/*         The WAIT macro parses the keywords and invokes           */ 00294300
*/*           IEAVEWAT via a stacking PC or an SVC.                  */ 00301000
*/*         Register 0 will contain the WAIT count. If the high      */ 00307700
*/*           order bit is on, a LONG WAIT was requested.            */ 00314400
*/*           If the second bit is on, saving EUTs was requested.    */ 00317700
*/*         Register 1 will contain the ECB or ECBLIST address.      */ 00321100
*/*           The ECBLIST address is complemented by the             */ 00327800
*/*           generated text to distinguish it from an               */ 00334500
*/*           ECB address.                                           */ 00341200
*/*         The WAIT service routine (IEAVEWAT) is invoked using     */ 00347900
*/*           SVC(1) or register 14 for the PC.                      */ 00354600
*/*                                                                  */ 00361300
*/*         In the event of an error, syntax checking will attempt   */ 00368000
*/*         to continue for the remainder of the input. No data      */ 00374700
*/*         will be generated.                                       */ 00381400
*/*                                                                  */ 00388100
*/*                                                                  */ 00394800
*/*01*  INPUT =  (BAL)                                               */ 00401500
*/*                                                                  */ 00408200
*/*       ,CNT              Event number. Default: 1                 */ 00414900
*/*                          Decimal number, constant or register.   */ 00421600
*/*                                                                  */ 00428300
*/*       ,ECB=             ECB address.                             */ 00435000
*/*                                                                  */ 00441700
*/*       ,ECBLIST=         ECB list address. The ECB must be        */ 00448400
*/*                         addressable in the home address space.   */ 00455100
*/*                         If the ECBLIST option is specified, both */ 00461800
*/*                         the ECBLIST and all ECBs in the list     */ 00468500
*/*                         must be in the home address space.       */ 00475200
*/*                          Rx-type address or register.            */ 00481900
*/*                                                                  */ 00488600
*/*          ,LINKAGE=      SVC | SYSTEM  Default: SVC               */ 00495300
*/*                         SVC - specifies SVC linkage to the       */ 00502000
*/*                         service routine. Valid only in           */ 00508700
*/*                         primary (P=H=S) mode.                    */ 00515400
*/*                         SYSTEM - specifies PC linkage to the     */ 00522100
*/*                         service routine. Valid in both primary   */ 00528800
*/*                         (P=H) mode and primary (P^H) mode.       */ 00535500
*/*                                                                  */ 00542200
*/*            ,EUT=        NOSAVE | SAVE   Default: NOSAVE          */ 00543800
*/*                         (Only valid with LINKAGE=SYSTEM)         */ 00545400
*/*                                                                  */ 00547000
*/*          ,LONG=         NO | YES  Default: NO                    */ 00548900
*/*                                                                  */ 00555600
*/*          ,RELATED=      Any value                                */ 00562300
*/*                                                                  */ 00569000
*/*                                                                  */ 00575700
*/*01*  INPUT =  (PLAS)                                              */ 00582400
*/*                         See WAITP for PLAS input description.    */ 00589100
*/*                                                                  */ 00595800
*/*                                                                  */ 00602500
*/*02*    NOTES =                                                    */ 00609200
*/*         -  ECB and ECBLIST are mutually exclusive.               */ 00615900
*/*         -  EUT is only valid with LINKAGE=SYSTEM.                */ 00619200
*/*                                                                  */ 00622600
*/*01*  NOTES =                                                      */ 00629300
*/*                                                                  */ 00636000
*/*02*    RESTRICTIONS = None                                        */ 00642700
*/*                                                                  */ 00649400
*/*02*    DEPENDENCIES = None                                        */ 00656100
*/*                                                                  */ 00662800
*/*03*      CHARACTER-CODE DEPENDENCIES= EBCDIC                      */ 00669500
*/*                                                                  */ 00676200
*/*01*  MODULE TYPE = BIL MACRO                                      */ 00682900
*/*                                                                  */ 00689600
*/*02*    PROCESSOR = ASSEMBLER-H  or  PLAS                          */ 00696300
*/*                                                                  */ 00703000
*/*02*    INVOCATION = See MACRO definition                          */ 00709700
*/*                                                                  */ 00716400
*/*01*  COMPONENT  =  SC1CL (TASK MANAGEMENT)                        */ 00723100
*/*                                                                  */ 00729800
*/*01*  DISTRIBUTION LIBRARY = AMACLIB                               */ 00736500
*/*                                                                  */ 00743200
*/**** END OF SPECIFICATIONS *****************************************/ 00749900
*/*                                                                     00756600
.*                                                                      00763300
.*   CHANGE ACTIVITY =                                                  00770000
.*      $L1=AR      HBB3310 860825 PD16LD: Access Registers             00776700
.*      $01=OY18807 HBB3310 890201 PD16Q9: EUT keyword                  00780000
.*      $L2=64BITSUP HBB7703 981204 PD00XB: ESAME                       00781700
.*                                                                      00783400
.********************************************************************** 00790100
.*                                                                      00796800
.SKIP    ANOP ,                                                    @L1A 00803500
*        MACDATE  10/20/88                                         @01C 00810200
.*                                                                      00816900
.********************************************************************** 00823600
.*                                                                    * 00830300
.*       Declare GLOBAL and LOCAL variables                           * 00837000
.*                                                                    * 00843700
.********************************************************************** 00850400
.*                                                                      00857100
         LCLB  &IHBERR        Error flag                           @L1A 00863800
         LCLA  &OSFT          Offset into System Function Table    @L1A 00870500
         LCLC  &EM(2)         Error messages                       @L1A 00877200
         GBLC  &SYSAM64                                            @L2A 00879400
         SYSSTATE TEST                                             @L2A 00881600
.*                                                                      00883900
.********************************************************************** 00890600
.*                                                                    * 00897300
.*       Constants                                                    * 00904000
.*                                                                    * 00910700
.********************************************************************** 00917400
.*                                                                      00924100
&OSFT    SETA   168           Offset into SFT for WAIT             @L1A 00930800
.*                                                                      00937500
.********************************************************************** 00944200
.*                                                                    * 00950900
.*       Initialization                                               * 00957600
.*                                                                    * 00964300
.********************************************************************** 00971000
         ACTR  10000          Set for infinite loop control        @L1A 00977700
.*                            Reset Global Variables               @L1A 00984400
&IHBERR  SETB  0              Reset error flag                     @L1A 00991100
.*                                                                      00997800
.********************************************************************** 01004500
.*                                                                    * 01011200
.*       Error messages                                               * 01017900
.*                                                                    * 01024600
.********************************************************************** 01031300
.*                                                                      01038000
&EM(1)   SETC  'PARAMETER CONTAINS TOO MANY SUBLISTS. ADDITIONAL ARGUMEx01044700
               NTS ARE IGNORED.'                                   @L1A 01051400
.*                                                                      01058100
.********************************************************************** 01064800
.*       Validate SYSLIST                                             * 01071500
.********************************************************************** 01078200
.*                                                                      01084900
         AIF   (N'&SYSLIST GE 2).ERR01     Too many positionals    @L1A 01091600
.XERR01  ANOP ,                                                    @L1A 01098300
         AIF   (N'&SYSLIST(1) GE 2).WARN02  Too many sublists      @L1A 01105000
.XERR02  ANOP ,                                                    @L1A 01111700
.*                                                                      01118400
.********************************************************************** 01125100
.*       Validate ECB and ECBLIST                                     * 01131800
.********************************************************************** 01138500
.*                                                                      01145200
         AIF   ('&ECB&ECBLIST' EQ '').ERR03 One is required        @L1C 01151900
.XERR03  ANOP ,                                                    @L1A 01158600
         AIF   ('&ECB' NE '' AND '&ECBLIST' NE '').ERR04           @L1C 01165300
.XERR04  ANOP ,                                                    @L1A 01172000
         AIF   (N'&ECB GT 1).WARN05       Too many sublists        @L1A 01178700
.XERR05  ANOP ,                                                    @L1A 01185400
         AIF   (N'&ECBLIST GT 1).WARN06   Too many sublists        @L1A 01192100
.XERR06  ANOP ,                                                    @L1A 01198800
.*                                                                      01205500
.********************************************************************** 01212200
.*       Validate LONG, LINKAGE and EUT                               * 01218900
.********************************************************************** 01225600
.*                                                                      01232300
         AIF   ('&LONG' NE 'YES' AND '&LONG' NE 'NO').ERR07        @L1C 01239000
.XERR07  ANOP ,                                                    @L1A 01245700
         AIF   ('&LINKAGE' NE 'SVC' AND '&LINKAGE' NE 'SYSTEM').ERR08   01252400
.XERR08  ANOP ,                                                    @L1A 01259100
         AIF  ('&EUT' EQ '').XERR09                                @01A 01260000
.CHKEUT  AIF  ('&EUT' NE 'SAVE' AND '&EUT' NE 'NOSAVE').ERR09      @01A 01260900
         AIF  ('&EUT' EQ 'NOSAVE' AND '&LINKAGE' NE 'SYSTEM').ERR10    x01261800
                                                                   @01A 01262700
         AIF  ('&EUT' EQ 'SAVE' AND '&LINKAGE' NE 'SYSTEM').ERR10  @01A 01263600
.XERR09  ANOP ,                                                    @01A 01264500
         AIF  (&IHBERR EQ 1).EXIT     Exit if errors encountered   @L1A 01265800
.*                                                                    * 01272500
.********************************************************************** 01279200
.*                                                                    * 01285900
.*       PROCESS CNT                                                  * 01292600
.*                                                                    * 01299300
.********************************************************************** 01306000
.*                                                                      01312700
         AIF   ('&CNT' EQ '').CONTF         COUNT not specified    @L1M 01319400
         AIF   ('&CNT'(1,1) EQ '(').CONTG                               01326100
&NAME    LA    0,&CNT.(0,0)                LOAD PARAMETER REG 0    @L1C 01332800
         AGO   .CONTA                                                   01339500
.CONTF   ANOP  ,                                                   @L1A 01346200
&NAME    LA    0,1(0,0)                    COUNT OMITTED,1 USED    @L1C 01352900
         AGO   .CONTA                                                   01359600
.CONTG   ANOP  ,                           COUNT in a register     @L1C 01366300
&NAME    LR    0,&CNT(1)                   LOAD PARAMETER REG 0    @L1C 01373000
.*                                                                    * 01379700
.********************************************************************** 01386400
.*                                                                    * 01393100
.*       PROCESS ECB and ECBLIST                                      * 01399800
.*                                                                    * 01406500
.********************************************************************** 01413200
.*                                                                      01419900
.CONTA   ANOP  ,                                                        01426600
         AIF   ('&ECB' NE '').CHECK          ECB specified         @L1M 01433300
         AIF   ('&ECBLIST'(1,1) EQ '(').REG  ECBLIST in register   @L1M 01440000
         AGO   .CONTD                        ECBLIST specified     @L1M 01446700
.CHECK   AIF   ('&ECB'(1,1) NE '(').CONTD    ECB not in register   @L1M 01453400
.REG     ANOP  ,                                                   @L1M 01460100
         LA    1,0(0,&ECB(1)&ECBLIST(1))   CLEAR HIGH BIT/BYTE     @L1C 01466800
         AGO   .CONTC                        Continue              @L1C 01473500
.CONTD   ANOP                                                           01480200
         IHBINNRA &ECB&ECBLIST             LOAD PARM REG 1         @L1C 01486900
.*                                                                      01493600
.CONTC   AIF   ('&ECBLIST' EQ '').CONTB      Was ECBLIST used?     @L1C 01500300
         LCR   1,1                         INDICATE ECBLIST USED        01507000
.CONTB   AIF   ('&LONG' EQ 'NO').CONTE       Test for long WAIT    @01C 01511400
         AIF   ('&EUT' EQ 'SAVE').SETC     EUT=SAVE, set both bits @01A 01515800
         BCR   8,0                         GIVES AN INLINE '80'         01520400
         ICM   0,8,*-1                     INSERT INTO HI BYTE          01527100
         AGO   .CONT                                               @01A 01527800
.SETC    ANOP  ,                           Long wait and save EUTs @01A 01528500
         BCR   12,0                        GIVES AN INLINE 'C0'         01529200
         ICM   0,8,*-1                     INSERT INTO HI BYTE          01529900
         AGO   .CONT                                               @01A 01530600
.CONTE   AIF   ('&EUT' NE 'SAVE').CONT     Test for saving EUTs    @01A 01531300
         BCR   4,0                         GIVES AN INLINE '40'         01532000
         ICM   0,8,*-1                     INSERT INTO HI BYTE          01532700
.*                                                                    * 01533800
.********************************************************************** 01540500
.*                                                                    * 01547200
.*       Generate Entry Linkage                                       * 01553900
.*                                                                    * 01560600
.********************************************************************** 01567300
.CONT    ANOP  ,                                                   @L1C 01574000
         AIF ('&LINKAGE' EQ 'SVC').GENSVC   Generate SVC linkage   @L1A 01580700
         AIF   ('&SYSAM64' NE 'YES').NOT64A1                       @L2A 01582000
         LLGT  14,16(0,0)                  CVT ADDRESS             @L1A 01583300
         AGO   .NOT64A2                                            @L2A 01584600
.NOT64A1 ANOP                                                      @L2A 01585900
         L     14,16(0,0)                  CVT ADDRESS             @L1A 01587400
.NOT64A2 ANOP                                                      @L2A 01590700
         L     14,772(,14)                 SYSTEM FUNCTION TABLE   @L1A 01594100
         L     14,&OSFT.(,14)              GET LX/EX FROM SFT      @L1A 01600800
         PC    0(14)                       PC TO WAIT SERVICE RTNE @L1A 01607500
         AGO   .EXIT                                               @L1C 01614200
.GENSVC  ANOP  ,                    SVC linkage                    @L1A 01620900
         SVC   1                           LINK TO WAIT ROUTINE    @L1C 01627600
         AGO   .EXIT                                               @L1C 01634300
.*                                                                      01641000
.********************************************************************** 01647700
.*                                                                    * 01654400
.*       MNOTES                                                       * 01661100
.*                                                                    * 01667800
.********************************************************************** 01674500
.*                                                                      01681200
.ERR01   ANOP  ,                                                        01687900
         MNOTE 8,' TOO MANY POSITIONAL PARAMETERS STARTING AT &SYSLIST(x01694600
               2)'                                                 @L1A 01701300
&IHBERR  SETB  1                    Indicate error                 @L1A 01708000
         AGO   .XERR01              Continue keyword validation    @L1A 01714700
.*                                                                      01721400
.WARN02  ANOP  ,                                                        01728100
         MNOTE 4,' POSITIONAL &EM(1)'                              @L1A 01734800
         AGO   .XERR02              Continue keyword validation    @L1A 01741500
.*                                                                      01748200
.ERR03   ANOP  ,                                                   @L1C 01754900
         MNOTE 8,' ECB OR ECBLIST MUST BE SPECIFIED'               @L1A 01761600
&IHBERR  SETB  1                    Indicate error                 @L1A 01768300
         AGO   .XERR03              Continue keyword validation    @L1C 01775000
.*                                                                      01781700
.ERR04   ANOP  ,                                                   @L1C 01788400
         MNOTE 8,' ECB AND ECBLIST ARE MUTUALLY EXCLUSIVE'         @L1A 01795100
&IHBERR  SETB  1                    Indicate error                 @L1A 01801800
         AGO   .XERR04              Continue keyword validation    @L1C 01808500
.*                                                                      01815200
.WARN05  ANOP  ,                                                        01821900
         MNOTE 4,' ECB &EM(1)'                                     @L1A 01828600
         AGO   .XERR05              Continue keyword validation    @L1A 01835300
.*                                                                      01842000
.WARN06  ANOP  ,                                                        01848700
         MNOTE 4,' ECBLIST &EM(1)'                                 @L1A 01855400
         AGO   .XERR06              Continue keyword validation    @L1A 01862100
.*                                                                      01868800
.ERR07   ANOP  ,                                                   @L1C 01875500
         MNOTE 8,' LONG PARAMETER MUST BE (YES) OR (NO)'           @L1A 01882200
&IHBERR  SETB  1                    Indicate error                 @L1A 01888900
         AGO   .XERR07              Continue keyword validation    @L1C 01895600
.*                                                                      01902300
.ERR08   ANOP  ,                                                   @L1A 01909000
         MNOTE 8,' LINKAGE PARAMETER MUST BE (SVC) OR (SYSTEM)'    @L1A 01915700
&IHBERR  SETB  1                    Indicate error                 @L1A 01922400
         AGO   .XERR08              Continue keyword validation    @L1A 01929100
.*                                                                      01935800
.ERR09   ANOP  ,                                                   @01A 01936300
         MNOTE 8,' EUT PARAMETER MUST BE (SAVE) OR (NOSAVE)'       @01A 01936800
&IHBERR  SETB  1                    Indicate error                 @01A 01937300
         AGO   .XERR09              Continue keyword validation    @01A 01937800
.*                                                                      01938300
.ERR10   ANOP  ,                                                   @01A 01938800
         MNOTE 8,' EUT PARAMETER IS ONLY VALID WITH LINKAGE=SYSTEM'    x01939300
                                                                   @01A 01939800
&IHBERR  SETB  1                    Indicate error                 @01A 01940300
         AGO   .XERR09              Continue keyword validation    @01A 01940800
.*                                                                      01941300
.********************************************************************** 01942500
.*                                                                    * 01949200
.*       EXIT                                                         * 01955900
.*                                                                    * 01962600
.********************************************************************** 01969300
.EXIT    ANOP  ,                                                        01976000
         MEND                                                           02000000
**/                                                                     02050000
*  WAIT : /***********************************************************/ 02100000
*/*                                                                  */ 02150000
*/*               PLAS VERSION OF THE WAIT MACRO                     */ 02200000
*/*                                                                  */ 02250000
*/********************************************************************/ 02300000
*                                                                       02350000
* MACRO                                                                 02400000
*   KEYS( ECB, ECBLIST, LONG, LINKAGE, EUT, RELATED );         /*@L1C*/ 02450000
*        ANS('?'||MACLABEL||'WAITP '||MACLIST||MACKEYS||';');           02500000
*%END  WAIT;                                                            02550000
