*%/*                                                                    00050000
         MACRO                                                          00100000
&NAME    CONVTOD &CONVVAL=,        Address of input time and date vals *00150000
               &TODVAL=,           Address of converted 64 bit value   *00183300
               &ETODVAL=,          Address of converted 128 bit value  *00216600
               &TIMETYPE=,         Type of time format                 *00250000
               &DATETYPE=,         Type of date format                 *00300000
               &OFFSET=,           Address of an offset value          *00350000
               &MF=                Macro form                      @L1C 00400000
         AGO   .SKIP                                                 */ 00450000
*/****  START OF SPECIFICATIONS  *************************************/ 00500000
*/*                                                                  */ 00550000
*/*01*  MACRO NAME = CONVTOD                                         */ 00600000
*/*                                                                  */ 00650000
*/*01*  DESCRIPTIVE NAME = CONVTOD macro                             */ 00700000
*/*                                                                  */ 00750000
*/*01*  PROPRIETARY STATEMENT =                                  @L1C*/ 00761100
*/*                                                                  */ 00772200
*/*   LICENSED MATERIALS - PROPERTY OF IBM                           */ 00786000
*/*   THIS MODULE IS "RESTRICTED MATERIALS OF IBM"                   */ 00799800
*/*   5647-A01 (C) COPYRIGHT IBM CORP. 1991, 1999                    */ 00813600
*/*                                                                  */ 00827700
*/*01*  STATUS =  HBB6608                                        @L1C*/ 00838800
*/*                                                                  */ 00841000
*/*01* EXTERNAL CLASSIFICATION:                                      */ 00843200
*/*02*         GUPI:BASE                                             */ 00845400
*/*01* END OF EXTERNAL CLASSIFICATION:                               */ 00847600
*/*                                                                  */ 00850000
*/*01*  FUNCTION =                                                   */ 00900000
*/*       Converts input time and date values to a TOD format value. */ 00950000
*/*                                                                  */ 01000000
*/*02*    OPERATION =                                                */ 01050000
*/*         The CONVTOD macro parses the keywords and invokes the    */ 01100000
*/*         the CONVTOD Service Routine (IEATCNVT) via a PC.         */ 01150000
*/*                                                                  */ 01200000
*/*01*  SYNTAX =                                                     */ 01250000
*/*                                                                  */ 01300000
*/*      CONVTOD CONVVAL=RX-address | (2)-(12)                       */ 01350000
*/*                             Specifies the address of a 4-word    */ 01400000
*/*                             (16-byte) area that contains the     */ 01450000
*/*                             input time and date values.          */ 01500000
*/*                             Required keyword.                    */ 01550000
*/*                                                                  */ 01600000
*/*                             The time of day is in the first 2    */ 01650000
*/*                             words and the date is in the third   */ 01700000
*/*                             word of the specified area.  The     */ 01733300
*/*                             fourth word is reserved.             */ 01766600
*/*                                                                  */ 01800000
*/*              type                                                */ 01816600
*/*                                                                  */ 01833200
*/*                Either TODVAL or ETODVAL is required.             */ 01849800
*/*                                                                  */ 01866400
*/*                     ,TODVAL=Rx-address | (2)-(12)                */ 01883000
*/*                             Specifies the address of a           */ 01900000
*/*                             doubleword (8-byte) area to which    */ 01950000
*/*                             the converted 64-bit TOD value is    */ 01963600
*/*                             returned.                            */ 01977200
*/*                                                                  */ 01990800
*/*                                                                  */ 02004400
*/*                                                                  */ 02018000
*/*                     ,ETODVAL=Rx-address | (2)-(12)               */ 02031600
*/*                             Specifies the address of a           */ 02045200
*/*                             quadword (16-byte) area to which     */ 02058800
*/*                             the converted 128-bit TOD value is   */ 02072400
*/*                             returned.                            */ 02086000
*/*                                                                  */ 02100000
*/*                                                                  */ 02150000
*/*                                                                  */ 02200000
*/*                 ,TIMETYPE=  Specifies the format of the input    */ 02250000
*/*                             time value:                          */ 02300000
*/*                                                                  */ 02350000
*/*                        DEC  (Default) Decimal digits without a   */ 02387500
*/*                             sign, of the form HHMMSSthmiju0000,  */ 02425000
*/*                             where:                               */ 02462500
*/*                             HH is hours (based on a 24-hour      */ 02500000
*/*                                           clock)                 */ 02550000
*/*                             MM is minutes                        */ 02600000
*/*                             SS is seconds                        */ 02650000
*/*                             t  is tenths of seconds              */ 02700000
*/*                             h  is hundredths of seconds          */ 02750000
*/*                             m  is milliseconds                   */ 02800000
*/*                             i  is ten-thousandths of seconds     */ 02850000
*/*                             j  is hundred-thousandths of seconds */ 02900000
*/*                             u  is microseconds                   */ 02950000
*/*                           Note that HHMMSSth are in the first    */ 03000000
*/*                           word and the remainder is left         */ 03050000
*/*                           justified in the second.               */ 03100000
*/*                                                                  */ 03150000
*/*                        BIN  Unsigned binary 32 bit number with   */ 03200000
*/*                             the low-order bit equivalent to 0.01 */ 03250000
*/*                             seconds.  The second word is ignored.*/ 03300000
*/*                                                                  */ 03350000
*/*                        MIC  Unsigned 64 bit number where bit 51  */ 03400000
*/*                             is equal to 1 microsecond.           */ 03450000
*/*                                                                  */ 03500000
*/*                                                                  */ 03550000
*/*                 ,DATETYPE=  Specifies the format of the input    */ 03600000
*/*                             date value:                          */ 03650000
*/*                                                                  */ 03700000
*/*                      YYDDD  Packed decimal digits of the form    */ 03750000
*/*                             0CYYDDDF,                            */ 03800000
*/*                             where:                               */ 03850000
*/*                             0C is the century                    */ 03900000
*/*                               00 - the century is 19YY           */ 03950000
*/*                               01 - the century is 20YY           */ 04000000
*/*                             YY is the two digits of the year     */ 04050000
*/*                             DDD is the day of the year           */ 04100000
*/*                             F is a sign to allow the date to be  */ 04150000
*/*                             unpacked.                            */ 04200000
*/*                                                                  */ 04250000
*/*                    YYYYDDD  (Default) Decimal digits of          */ 04300000
*/*                             the form YYYYDDD, where:             */ 04350000
*/*                             YYYY is the four digits of the year  */ 04400000
*/*                             DDD is the day of the year           */ 04450000
*/*                                                                  */ 04500000
*/*                   MMDDYYYY  Decimal digits without a sign, of    */ 04550000
*/*                             the form MMDDYYYY where:             */ 04600000
*/*                             MM  is the month of the year         */ 04650000
*/*                             DD  is the day of the month          */ 04700000
*/*                             YYYY is the four digits of the year  */ 04750000
*/*                                                                  */ 04800000
*/*                   DDMMYYYY  Decimal digits without a sign, of    */ 04850000
*/*                             the form DDMMYYYY, where:            */ 04900000
*/*                             DD  is the day of the month          */ 04950000
*/*                             MM  is the month of the year         */ 05000000
*/*                             YYYY is the four digits of the year  */ 05050000
*/*                                                                  */ 05100000
*/*                   YYYYMMDD  Decimal digits without a sign, of    */ 05150000
*/*                             the form YYYYMMDD, where:            */ 05200000
*/*                             YYYY is the four digits of the year  */ 05250000
*/*                             MM  is the month of the year         */ 05300000
*/*                             DD  is the day of the month          */ 05350000
*/*                                                                  */ 05400000
*/*                      Note that the second word of the input date */ 05450000
*/*                      value is ignored.                           */ 05500000
*/*                                                                  */ 05550000
*/*                 ,OFFSET=Rx-address | (2)-(12)                    */ 05600000
*/*                             Specifies the address of a word      */ 05650000
*/*                             containing a packed decimal time     */ 05675000
*/*                             offset value of the form 000HHMMX,   */ 05700000
*/*                             where X is the sign (F for plus and  */ 05725000
*/*                             D for minus).  The default is        */ 05750000
*/*                             0000000F (ie. +0).  This offset is   */ 05775000
*/*                             to be added to the input time and    */ 05800000
*/*                             date value.                          */ 05825000
*/*                                                                  */ 05850000
*/*                 ,MF=         Macro form                          */ 05900000
*/*                                                                  */ 05950000
*/*                    L        List form                            */ 06000000
*/*                             NOTE: No other keywords are valid on */ 06050000
*/*                             the list form.                       */ 06100000
*/*                                                                  */ 06150000
*/*                    (E,rx-address) | (E,(1)-(12))                 */ 06200000
*/*                             Execute form                         */ 06250000
*/*                                                                  */ 06300000
*/*                                                                  */ 06350000
*/*01*  INVOKING REQUIREMENTS =                                      */ 06400000
*/*       Authorization          = None required                     */ 06450000
*/*       Dispatchable unit mode = Any                               */ 06500000
*/*       Cross memory mode      = Any                               */ 06550000
*/*       Amode                  = Any - all addresses specified     */ 06600000
*/*                                must be clean 31-bit addresses    */ 06650000
*/*       ASC mode               = Primary or AR                     */ 06700000
*/*       Serialization          = None required - the caller may    */ 06750000
*/*                                enabled or disabled               */ 06800000
*/*                                                                  */ 06850000
*/*01*  NOTES =                                                      */ 06900000
*/*                                                                  */ 06950000
*/*02*    RESTRICTIONS = None                                        */ 07000000
*/*                                                                  */ 07050000
*/*02*    DEPENDENCIES = None                                        */ 07100000
*/*                                                                  */ 07150000
*/*03*      CHARACTER-CODE DEPENDENCIES= EBCDIC                      */ 07200000
*/*                                                                  */ 07250000
*/*01*  MODULE TYPE = BAL Macro (PL/AS version: CONVTODP)            */ 07300000
*/*                                                                  */ 07350000
*/*01*  CHANGE ACTIVITY =                                            */ 07362500
*/* $D0=DKI0001  JBB4422  910308  PD16H5: CONVTOD macro              */ 07375000
*/* $P1=PKI0225  JBB4422  910503  PD16H5: Fix CONVVAL in a register  */ 07381200
*/* $L1=STCKE    HBB6608  980530  B7HGH5: 128 bit TOD support        */ 07384300
*/* $01=OW44449  HBB6605  000601  U2IAXZ: For execute form, if (1)   */ 07389490
*/*                                       was specified, ensure AR 1 */ 07391480
*/*                                       is zeroed if in primary    */ 07393470
*/*                                       ASC mode.                  */ 07395460
*/*                                                                  */ 07397450
*/*01*  COMPONENT  =  SC1CV (Timer Supervision)                      */ 07400000
*/*                                                                  */ 07450000
*/*01*  DISTRIBUTION LIBRARY = AMACLIB                               */ 07500000
*/*                                                                  */ 07550000
*/**** END OF SPECIFICATIONS *****************************************/ 07600000
*/*                                                                     07950000
.SKIP    ANOP ,                                                         08000000
*        MACDATE  05/30/98                                         @L1C 08050000
.*                                                                      08100000
.********************************************************************** 08150000
.*                                                                    * 08200000
.*       Declare GLOBAL and LOCAL variables                           * 08250000
.*                                                                    * 08300000
.********************************************************************** 08350000
.*                                                                      08400000
         LCLA  &DATET         DATETYPE bitstring                        08450000
         LCLA  &TIMET         TIMETYPE bitstring                        08500000
         LCLB  &D(5)          DATETYPE specified                        08550000
         LCLB  &T(3)          TIMETYPE specified                        08600000
         LCLB  &IHBERR        Error flag                                08650000
         LCLC  &REGA          Register name                             08700000
         LCLC  &REGB          Register name                             08750000
         LCLC  &REGC          Register name                             08800000
         LCLC  &REGD          Register name                             08850000
         LCLC  &REGE          Register name                             08900000
         LCLC  &INAME         Label for standard form parameter list    08950000
         LCLC  &ENAME         Branch label for the standard form        09000000
.*                                                                    * 09050000
.********************************************************************** 09100000
.*      Check for positional parameters                               * 09150000
.********************************************************************** 09200000
         AIF   (N'&SYSLIST GE 1).ERR01   Too many positionals           09250000
.*                                                                    * 09300000
.********************************************************************** 09350000
.*      Validate CONVVAL parameter                                    * 09400000
.********************************************************************** 09450000
.CHKSTK  ANOP  ,                                                        09500000
         AIF   ('&CONVVAL' EQ '').CHKMF1 Check for MF=L if CONVVAL not  09550000
.*                                       specified                      09600000
         AIF   (N'&CONVVAL GT 1).ERR02   Too many sublists              09650000
         AGO   .CHKTODV                                                 09700000
.CHKMF1  ANOP  ,                                                        09750000
         AIF   ('&MF' NE 'L').ERR03      CONVVAL required unless MF=L   09800000
.*                                                                    * 09850000
.********************************************************************** 09900000
.*      Validate TODVAL/ETODVAL parameter                             * 09950000
.********************************************************************** 10000000
.CHKTODV ANOP  ,                                                        10050000
         AIF   ('&TODVAL' NE '' AND '&ETODVAL' NE '').ERR16        @L1A 10080000
         AIF   ('&TODVAL' EQ '').CHKET1  Check for STCKEVAL if STCKVAL  10110000
.*                                       not specified                  10140000
         AIF   (N'&TODVAL GT 1).ERR04    Too many sublists              10170000
         AGO   .CHKTIME                                                 10200000
.CHKET1  ANOP  ,                                                        10210000
         AIF   ('&ETODVAL' EQ '').CHKMF2 Check for MF=L            @L1A 10220000
         AIF   (N'&ETODVAL GT 1).ERR17   Too many sublists         @L1A 10230000
         AGO   .CHKTIME                                            @L1A 10240000
.CHKMF2  ANOP  ,                                                        10250000
         AIF   ('&MF' NE 'L').ERR05      TODVAL or ETODVAL         @L1C 10283300
.*                                       required unless MF=L         * 10316600
.*                                                                    * 10350000
.********************************************************************** 10400000
.*      Validate TIMETYPE parameter, if present                       * 10450000
.********************************************************************** 10500000
.CHKTIME ANOP  ,                                                        10550000
         AIF   (N'&TIMETYPE GT 1).ERR13  Too many sublists              10600000
&T(1)    SETB  ('&TIMETYPE' EQ 'BIN')                                   10650000
&T(2)    SETB  ('&TIMETYPE' EQ 'DEC' OR '&TIMETYPE' EQ '')              10700000
&T(3)    SETB  ('&TIMETYPE' EQ 'MIC')                                   10750000
&TIMET   SETA  &T(1)+(&T(2)*2)+(&T(3)*3) Generate TIMETYPE bitstring    10800000
         AIF   (&TIMET EQ 0).ERR06       Invalid TIMETYPE specified     10850000
.*                                                                    * 10900000
.********************************************************************** 10950000
.*      Validate DATETYPE parameter, if present                       * 11000000
.********************************************************************** 11050000
.CHKDATE ANOP  ,                                                        11100000
         AIF   (N'&DATETYPE GT 1).ERR14  Too many sublists              11150000
&D(1)    SETB  ('&DATETYPE' EQ 'MMDDYYYY')                              11200000
&D(2)    SETB  ('&DATETYPE' EQ 'DDMMYYYY')                              11250000
&D(3)    SETB  ('&DATETYPE' EQ 'YYYYMMDD')                              11300000
&D(4)    SETB  ('&DATETYPE' EQ 'YYDDD')                                 11350000
&D(5)    SETB  ('&DATETYPE' EQ 'YYYYDDD' OR '&DATETYPE' EQ '')          11400000
         AIF   (&D(1) EQ 0 AND &D(2) EQ 0 AND &D(3) EQ 0 AND &D(4) EQ 0X11450000
                AND &D(5) EQ 0).ERR07    Invalid DATETYPE specified     11500000
&DATET   SETA  &D(1)+&D(2)*2+&D(3)*3+&D(4)*4      DATETYPE bitstring    11550000
.*                                                                    * 11600000
.********************************************************************** 11650000
.*      Validate OFFSET parameter                                     * 11700000
.********************************************************************** 11750000
.CHKOFF  ANOP  ,                                                        11800000
         AIF   ('&OFFSET' EQ '').CHKMF  Continue checking               11850000
         AIF   (N'&OFFSET GT 1).ERR015  Too many sublists               11900000
.*                                                                    * 11950000
.********************************************************************** 12000000
.*       Validate MF keywords and options                             * 12050000
.********************************************************************** 12100000
.CHKMF   ANOP                                                           12150000
         AIF   ('&MF' EQ '').STD        Default is Standard form        12200000
         AIF   ('&MF' EQ 'L').LIST      List form                       12250000
         AIF   (N'&MF GT 2).ERR08       Too many sublists               12300000
         AIF   ('&MF(1)' NE 'E').ERR09  Must be E, if not L             12350000
         AIF   ('&MF(2)' EQ '').ERR10   Parameter list address required 12400000
         AIF   (N'&MF(2) GT 2).ERR11    Too many addresses              12450000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors            12500000
.*                                                                    * 12550000
.********************************************************************** 12600000
.*  Generate PC linkage to service routine for Execute form of macro  * 12650000
.********************************************************************** 12700000
         AIF   ('&MF(2)'(1,1) EQ '(').ISAREGA                           12750000
&NAME    LAE   1,&MF(2)                PARAMETER LIST ADDRESS           12800000
         AGO   .CONT                                                    12850000
.ISAREGA ANOP                                                      @01C 12919990
&REGA    SETC  '&MF(2)'(2,K'&MF(2)-2)  Extract register name            12950000
&NAME    LAE   1,0(0,&REGA)            PARAMETER LIST ADDRESS           13000000
.CONT    ANOP                                                           13050000
         XC    0(32,1),0(1)            CLEAR PARAMETER LIST             13100000
         AIF   ('&ETODVAL' EQ '').TODOPT                           @L1A 13112500
&DATET   SETA  &DATET+128              Indicate ETOD request       @L1A 13125000
.TODOPT  ANOP                                                      @L1A 13137500
         MVI   2(1),&DATET             SET DATETYPE FLAGS IN PARM LIST  13150000
         MVI   3(1),&TIMET             SET TIMETYPE FLAGS IN PARM LIST  13200000
         AIF   ('&CONVVAL'(1,1) EQ '(').ISAREGB                    @P1C 13250000
         LAE   14,&CONVVAL             ADDRESS OF CONVVAL               13300000
         AGO   .MOVEVAL                                                 13350000
.ISAREGB ANOP                                                           13400000
&REGB    SETC  '&CONVVAL'(2,K'&CONVVAL-2) Extract register name         13450000
         LAE   14,0(0,&REGB)           ADDRESS OF CONVVAL               13500000
.MOVEVAL ANOP                                                           13550000
         MVC   8(16,1),0(14)           SET CONVVAL IN PARM LIST    @P1C 13600000
.COMMON  ANOP                                                           13650000
         AIF   ('&OFFSET' EQ '').NOOFF                                  13700000
         AIF   ('&OFFSET'(1,1) EQ '(').ISAREGC                          13750000
         LAE   14,&OFFSET              ADDRESS OF OFFSET                13800000
         AGO   .MOVEOFF                                                 13850000
.ISAREGC ANOP                                                           13900000
&REGC    SETC  '&OFFSET'(2,K'&OFFSET-2) Extract register name           13950000
         LAE   14,0(0,&REGC)           ADDRESS OF OFFSET                14000000
.MOVEOFF ANOP                                                           14050000
         MVC   4(4,1),0(14)            SET OFFSET IN PARM LIST          14100000
.NOOFF   ANOP                                                           14150000
         L     14,16(0,0)              CVT ADDRESS                      14200000
         L     14,772(14,0)            SFT ADDRESS                      14250000
         L     14,352(14,0)            LX/EX FOR SERVICE ROUTINE        14300000
         PC    0(14)                                                    14350000
         AIF   ('&ETODVAL' NE '').ETOD                             @L1A 14375000
         AIF   ('&TODVAL'(1,1) EQ '(').ISAREGD In a register            14400000
         LAE   14,&TODVAL              ADDRESS OF TODVAL                14450000
         AGO   .MOVE                                                    14500000
.ISAREGD ANOP                                                           14550000
&REGD    SETC  '&TODVAL'(2,K'&TODVAL-2) EXTRACT REGISTER NAME           14600000
         LAE   14,0(0,&REGD)           ADDRESS OF TODVAL                14650000
.MOVE    ANOP                                                           14700000
         MVC   0(8,14),24(1)           MOVE OUTPUT FROM PARM LIST       14750000
*                                      TO TODVAL AREA                   14800000
         AGO   .EXIT                                               @L1A 14803800
.ETOD    ANOP                                                      @L1A 14807600
         AIF   ('&ETODVAL'(1,1) EQ '(').ISAREGF In a register      @L1A 14811400
         LAE   14,&ETODVAL             ADDRESS OF ETODVAL          @L1A 14815200
         AGO   .MOVE1                                              @L1A 14819000
.ISAREGF ANOP                                                      @L1A 14822800
&REGD    SETC  '&ETODVAL'(2,K'&ETODVAL-2) EXTRACT REGISTER NAME    @L1A 14826600
         LAE   14,0(0,&REGD)           ADDRESS OF ETODVAL          @L1A 14830400
.MOVE1   ANOP                                                      @L1A 14834200
         MVC   0(16,14),16(1)          MOVE OUTPUT FROM PARM LIST  @L1A 14838000
*                                      TO TODVAL AREA              @L1A 14841800
.EXIT    ANOP                                                      @L1A 14845600
         MEXIT                                                          14850000
.********************************************************************** 14900000
.*       Generate parameter list for Standard form of macro           * 14950000
.********************************************************************** 15000000
.STD     ANOP                                                           15050000
         AIF   (&IHBERR EQ 1).DONE     Exit if errors                   15100000
&INAME   SETC   'IHB'.'&SYSNDX'.'K'                                     15150000
&ENAME   SETC   'IHB'.'&SYSNDX'.'A'                                     15200000
&NAME    DS    0H                                                       15250000
         LAE   1,0(0,0)                ZERO ACCESS REGISTER 1           15300000
         LA    1,&INAME                POINT TO PARAMETER LIST          15350000
         XC    0(32,1),0(1)            CLEAR PARAMETER LIST             15400000
         AIF   ('&ETODVAL' EQ '').TODOPT1                          @L1A 15412500
&DATET   SETA  &DATET+128              Indicate ETOD request       @L1A 15425000
.TODOPT1 ANOP                                                      @L1A 15437500
         MVI   2(1),&DATET             SET DATETYPE FLAGS IN PARM LIST  15450000
         MVI   3(1),&TIMET             SET TIMETYPE FLAGS IN PARM LIST  15500000
         AIF   ('&CONVVAL'(1,1) EQ '(').ISAREGE  CONVVAL in a register  15550000
         LAE   14,&CONVVAL             ADDRESS OF CONVVAL               15600000
         AGO   .MOVEIT                                                  15650000
.ISAREGE ANOP                                                           15700000
&REGE    SETC  '&CONVVAL'(2,K'&CONVVAL-2) Extract register name         15750000
         LAE   14,0(0,&REGE)           ADDRESS OF CONVVAL               15800000
.MOVEIT  ANOP                                                           15850000
         MVC   8(16,1),0(14)           MOVE CONVVAL INTO PARM LIST      15900000
         B     &ENAME                  BRANCH AROUND LIST               15950000
&INAME   DS    0F                                                       16000000
         DC    FL2'0'                                                   16050000
         DC    FL1'0'                                                   16100000
         DC    FL1'0'                                                   16150000
         DC    1F'0'                                                    16200000
         DC    2F'0'                                                    16250000
         DC    2F'0'                                                    16300000
         DC    2F'0'                                                    16350000
&ENAME   DS    0H                                                       16400000
         AGO    .COMMON                                                 16450000
.********************************************************************** 16500000
.*       Generate parameter list for List form of macro               * 16550000
.********************************************************************** 16600000
.LIST    ANOP                                                           16650000
         AIF   ('&CONVVAL' NE '' OR '&TODVAL' NE '' OR '&TIMETYPE' NE  X16700000
               '' OR '&DATETYPE' NE '' OR '&OFFSET' NE '').ERR12        16750000
         AIF   (&IHBERR EQ 1).DONE     Exit if errors                   16800000
&NAME    DS    0F                                                       16850000
         DS    XL32                                                     16900000
.DONE    MEXIT                                                          16950000
.*                                                                    * 17000000
.********************************************************************** 17050000
.*                                                                    * 17100000
.*       Error Messages                                               * 17150000
.*                                                                    * 17200000
.********************************************************************** 17250000
.ERR01   MNOTE 12,' EXCESSIVE POSITIONAL PARAMETERS - &SYSLIST(1)'      17300000
&IHBERR  SETB  1                        Indicate error                  17350000
         AGO   .CHKSTK                  Continue                        17400000
.ERR02   MNOTE 12,' ''CONVVAL'' PARAMETER CONTAINS TOO MANY SUBLISTS.'  17450000
&IHBERR  SETB  1                          Indicate error                17500000
         AGO   .CHKTODV                   Continue                      17550000
.ERR03   MNOTE 12,' ''CONVVAL'' PARAMETER IS REQUIRED.'                 17600000
&IHBERR  SETB  1                          Indicate error                17650000
         AGO   .CHKTODV                   Continue                      17700000
.ERR04   MNOTE 12,' ''TODVAL'' PARAMETER CONTAINS TOO MANY SUBLISTS.'   17750000
&IHBERR  SETB  1                          Indicate error                17800000
         AGO   .CHKTIME                   Continue                      17850000
.ERR05   MNOTE 12,' ''TODVAL'' OR ''ETODVAL'' PARAMETER IS REQUIRED.'   17883300
*                                                                  @L1C 17916600
&IHBERR  SETB  1                          Indicate error                17950000
         AGO   .CHKTIME                   Continue                      18000000
.ERR06   MNOTE 12,' ''TIMETYPE'' PARAMETER MUST BE ''DEC'', ''BIN'', ORX18050000
                ''MIC''.'                                               18100000
&IHBERR  SETB  1                          Indicate error                18150000
         AGO   .CHKDATE                   Continue                      18200000
.ERR07   MNOTE 12,' ''DATETYPE'' PARAMETER MUST BE ''YYYYDDD'', ''MMDDYX18250000
               YYY'', ''DDMMYYYY'', ''YYYYMMDD'', ''YYDDD''.'           18300000
&IHBERR  SETB  1                          Indicate error                18350000
         AGO   .CHKOFF                    Continue                      18400000
.ERR08   MNOTE 12,' ''MF'' PARAMETER CONTAINS TOO MANY SUBLISTS.'       18450000
&IHBERR  SETB  1                          Indicate error                18500000
         AGO   .DONE                      Exit                          18550000
.ERR09   MNOTE 12,' ''MF'' PARAMETER MUST BE ''L'' OR ''E''.'           18600000
&IHBERR  SETB  1                          Indicate error                18650000
         AGO   .DONE                      Exit                          18700000
.ERR10   MNOTE 12,' NO PARAMETER LIST ADDRESS WAS SPECIFIED FOR ''MF'' X18750000
               PARAMETER.'                                              18800000
&IHBERR  SETB  1                          Indicate error                18850000
         AGO   .DONE                      Exit                          18900000
.ERR11   MNOTE 12,' TOO MANY PARAMETER LIST ADDRESSES SPECIFIED FOR ''MX18950000
               F'' PARAMETER.'                                          19000000
&IHBERR  SETB  1                          Indicate error                19050000
         AGO   .DONE                      Exit                          19100000
.ERR12   MNOTE 12,' ''CONVVAL'', ''TODVAL'', ''TIMETYPE'', ''DATETYPE''x19150000
               , AND ''OFFSET'' NOT VALID WITH MF=L.'                   19200000
&IHBERR  SETB  1                          Indicate error                19250000
         AGO   .DONE                      Exit                          19300000
.ERR13   MNOTE 12,' ''TIMETYPE'' PARAMETER CONTAINS TOO MANY SUBLISTS.' 19350000
&IHBERR  SETB  1                          Indicate error                19400000
         AGO   .CHKDATE                   Continue                      19450000
.ERR14   MNOTE 12,' ''DATETYPE'' PARAMETER CONTAINS TOO MANY SUBLISTS.' 19500000
&IHBERR  SETB  1                          Indicate error                19550000
         AGO   .CHKOFF                    Continue                      19600000
.ERR015  MNOTE 12,' ''OFFSET'' PARAMETER CONTAINS TOO MANY SUBLISTS.'   19650000
&IHBERR  SETB  1                          Indicate error                19700000
         AGO   .CHKMF                     Continue                      19750000
.ERR16   MNOTE 12,' SPECIFY EITHER ''TODVAL'' OR ''ETODVAL'', NOT BOTH.x19755500
               '                                                   @L1A 19761000
&IHBERR  SETB  1                          Indicate error           @L1A 19766500
         AGO   .CHKTIME                   Continue                 @L1A 19772000
.ERR17   MNOTE 12,' ''ETODVAL'' PARAMETER CONTAINS TOO MANY SUBLISTS.'  19777500
*                                                                  @L1A 19783000
&IHBERR  SETB  1                          Indicate error           @L1A 19788500
         AGO   .CHKTIME                   Continue                 @L1A 19794000
         MEND                                                           19800000
**/                                                                     19850000
* CONVTOD:    MACRO                                                     19900000
*        KEYS(CONVVAL,TODVAL,ETODVAL,TIMETYPE,DATETYPE,OFFSET,MF); /*   19933300
*                                                                @L1C*/ 19966600
*        ANS('?'||MACLABEL||'CONVTODP '||MACLIST||MACKEYS||';');        20000000
*                                                                       20025000
*%END;                                                                  20050000
