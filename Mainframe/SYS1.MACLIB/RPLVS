         MACRO                                                          00050000
&NAME    RPLVS &ACBDATA=,&AREA=0,&ARG=0,&NXTRPL=0,&KEYLEN=0,   @ZA34940-00100000
               &ECB=,&BUFL=,&LEN=,&MSGLEN=0,&ERRMSG=,          @ZA34940-00130000
               &CCHAR=0,&OPTCD=,&NIB=,&BRANCH=,                  X03004-00200000
               &ACB=,&AREALEN=,&MSGAREA=,&RECLEN=,               X03004-00250000
               &AM=,&AAREA=0,&AAREALN=0,&EXIT=,&ARECLEN=0,       X03004-00300000
               &SEQNO=0,&USENSEO=0,&IBSQVAL=0,&OBSQVAL=0,       X3004BH-00350000
               &POST=,&RESPOND=,&CHAIN=,&CONTROL=,&CHNGDIR=,    X3004BH-00400000
               &BRACKET=,&SSENSMO=0,&TRANSID=0,                X03004JS-00450000
               &SSENSEO=,&RTYPE=,&STYPE=,&IBSQAC=,&OBSQAC=,     X04SVHS-00500000
               &SIGDATA=0,&CODESEL=,                           @Z40BHUC-00550000
               &CRYPT=,&LUWID=,&TIMEOUT=,                          @LAC-00585000
               &PARMS=,&PARM2=,&PARM3=                         @ZA34940 00620000
.*                                                                      00640000
.*  $MAC(RPLVS):                                                        00642200
.*                                                                      00644400
.*PROPRIETARY V3 STATEMENT                                              00645600
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00646800
.*"RESTRICTED MATERIALS OF IBM"                                         00648000
.*5695-DF1                                                              00649200
.*(C) COPYRIGHT 5  IBM CORP.                                            00650400
.*END PROPRIETARY V3 STATEMENT                                          00651600
.*                                                                      00653200
.*  STATUS - DFSMS/MVS RELEASE 1.3.0                                    00655400
.*                                                                      00657600
.* FUNCTION -  THE FUNCTION OF THIS MACRO IS TO BUILD, AT ASSEMBLY      00686200
.*             TIME, AN RPL (REQUEST PARAMETER LIST) FOR A VSAM USER.   00714800
.*             RPLVS IS INVOKED BY MACRO RPL WHEN A USER ISSUES AN RPL  00743400
.*             MACRO WITH NO 'AM' SPECIFIED OR 'AM=VSAM' SPECIFIED.     00772000
.*                                                                      00800600
.* CHANGE ACTIVITY                                                      00829200
.*                                                                      00857800
.* $L9=DFP,HDP2220,,STLJRB: MVS/XA DFP RELEASE 2.2 VSAM 31-BIT     @L9A 00886600
.* CHANGE CONTINUATION CHARACTER TO '-'                        @YA21786 00889900
.* $LA=DFSMS,HDZ11C0,043093,SJPLRG: VSAM RLS                       @LAA 00893200
.* $EA=DFSMS,HDZ11C0,091494,SJPLDKT: EXTENDED ADDRESSABILITY       @EAA 00894800
.* $EA=OW15531,HDZ11C0,091995,SJPLTLD: REPLACE ERROR BW15531   @WA15531 00895600
.******************************************************************@LAC 00896500
.*                                                             @ZA34940 00900000
.*             THE FOLLOWING GLOBALS ARE SET IN IDAVSOPT WITH  @ZA34940 00920000
.*             THE VALUES OF THE VARIOUS OPTCD BYTES           @ZA34940 00940000
         GBLC  &IDAOP1,&IDAOP2,&IDAOP5,&IDAOP6,&IDAOP7,&IDAOP8  X04SVHS 01000000
         GBLC  &IDAOP9,&IDAOPA,&IDAOPB,&IDAOPC  OPTCD CODES     X04SVHS 01050000
         GBLC  &IDAOP3                  OPTCD CODE BYTE 3          @LAA 01075000
         GBLC  &IDAOP4                  OPTCD CODE BYTE 4          @LEA 01087500
         GBLC  &IDABRCH                 BRANCH CODE             X04SVHS 01100000
         GBLC  &IDASNSO                 SSENSEO CODES           X04SVHS 01150000
         GBLC  &IDACRYP                 ENCRIPTION CODE        @G40AKCM 01200000
         GBLC  &IDARH3                 CHNGDIR/BRACKET/CODESEL @Z40BHUC 01250000
         GBLC  &IDASRTY                 STYPE, RTYPE CODES      X04SVHS 01300000
         GBLC  &IDACHN                  CHAIN CODES             X04SVHS 01350000
         GBLC  &IDAPORE                 POST RESPOND CODES      X04SVHS 01400000
         GBLC  &IDACON1,&IDACON2,&IDACON3 CONTROL CODES         X04SVHS 01450000
         GBLC  &IDAOBQC                 OBSQAC CODES            X04SVHS 01500000
         GBLC  &IDAIBQC                 IBSQAC CODES            X04SVHS 01550000
         GBLA  &IDAENO                  NUMBER OF ELEMENTS     @ZA06420 01600000
.* LOCALS                                                               01650000
         LCLA  &LENG                    SUBPARAMETER RPL LENGTH  X03004 01700000
         LCLA  &SUBTYPE                 SUBTYPE CODE            X04SVHS 01750000
         LCLB  &ECBX                                            X04SVHS 01800000
         LCLB  &EXITB,&NOEXIT,&NIBD                             X04SVHS 01850000
.*             INITIALIZE GLOBAL VARIABLES                              01900000
&IDAOP1  SETC  '0000000'                                        X04SVHS 01950000
&IDAOP2  SETC  '00000000'                                       X04SVHS 02000000
&IDAOP3  SETC  '00000000'                                          @LAA 02025000
&IDAOP4  SETC  '00000000'                                          @EAA 02037500
&IDAOP5  SETC  '00000000'                                       X04SVHS 02050000
&IDAOP6  SETC  '00000000'                                       X04SVHS 02100000
&IDAOP7  SETC  '00000000'                                       X04SVHS 02150000
&IDAOP8  SETC  '00000000'                                       X04SVHS 02200000
&IDAOP9  SETC  '00000000'                                       X04SVHS 02250000
&IDAOPA  SETC  '00000000'                                       X04SVHS 02300000
&IDAOPB  SETC  '00000000'                                       X04SVHS 02350000
&IDAOPC  SETC  '00000000'                                       X04SVHS 02400000
&IDABRCH SETC  '0'                                              X04SVHS 02450000
&IDASNSO SETC  '00000000'                                       X04SVHS 02500000
&IDARH3  SETC  '00000000'                                       X04SVHS 02550000
&IDASRTY SETC  '00000000'                                       X04SVHS 02600000
&IDACRYP SETC  '0'                                             @G40AKCM 02650000
&IDACHN  SETC  '00000000'                                       X04SVHS 02700000
&IDAPORE SETC  '00000000'                                       X04SVHS 02750000
&IDACON1 SETC  '00000000'                                       X04SVHS 02800000
&IDACON2 SETC  '00000000'                                       X04SVHS 02850000
&IDACON3 SETC  '00000000'                                       X04SVHS 02900000
&IDAOBQC SETC  '00000000'                                       X04SVHS 02950000
&IDAIBQC SETC  '00000000'                                       X04SVHS 03000000
&IDAENO  SETA  0                                               @ZA06420 03050000
.*                                                                      03100000
&LENG    SETA  76                       SET NON VTAM LENGTH      X03004 03150000
         AIF   ('&AM' NE 'VTAM').SETLEN  WAS VTAM SPECIFIED      X03004 03200000
&LENG    SETA  112                      YES; SET VTAM LENGTH   @Z40BHUC 03250000
.SETLEN  ANOP                           NO; USE NON VTAM LENGTH  X03004 03300000
&NAME    DS    0F                                                       03350000
         DC    AL1(0)                   RPL IDENTIFICATION              03400000
&SUBTYPE SETA  X'10'                    SET VSAM SUBTYPE        X04SVHS 03450000
         AIF   ('&AM' EQ 'VSAM').SSTYPE CHK FOR AM = VSAM               03500000
         AIF   ('&AM' EQ '').SSTYPE     DEFAULT AM = VSAM               03550000
&SUBTYPE SETA  X'20'                    SET VTAM SUBTYPE        X04SVHS 03600000
         AIF   ('&AM' EQ 'VTAM').SSTYPE CHK FOR AM = VTAM       X04SVHS 03650000
.*                                      IF NOT ERROR            X04SVHS 03700000
&SUBTYPE SETA  0                        SET SUBTYPE TO ZERO     X04SVHS 03750000
         IDAERMAC 3,AM,&AM              ISSUE ERROR MSG            @L9C 03800000
.SSTYPE  ANOP                                                   X04SVHS 03850000
         DC    AL1(&SUBTYPE)            RPL SUBTYPE             X04SVHS 03900000
         DC    AL1(0)                   RPL REQUEST TYPE                03950000
         DC    AL1(&LENG)               RPL LENGTH               X03004 04000000
         IDAVSRPL OPTCD=&OPTCD,BRANCH=&BRANCH,POST=&POST,      @ZA34940-04050000
               RESPOND=&RESPOND,CHAIN=&CHAIN,CONTROL=&CONTROL,         -04100000
               CHNGDIR=&CHNGDIR,BRACKET=&BRACKET,SSENSEO=&SSENSEO,     -04150000
               CODESEL=&CODESEL,                               @Z40BHUC-04200000
               RTYPE=&RTYPE,STYPE=&STYPE,IBSQAC=&IBSQAC,               -04250000
               OBSQAC=&OBSQAC,CALLER=RPL,AM=&AM,                X04SVHS-04300000
               CRYPT=&CRYPT                                    @G40AKCM 04350000
.*                                                                      04400000
.*             IDAVSRPL IS CALLED TO PROCESS BIT KEYWORDS      @ZA34940 04450000
.*             OPTCD,                                          @ZA34940 04480000
.*             BRANCH, POST, RESPOND, CHAIN, CONTROL, CHNGDIR, BRACKET, 04550000
.*             CODESEL, SSENSEO, RTYPE, STYPE, IBSQAC, AND OBSQAC.      04600000
.*             IT WILL VALIDITY CHECK THE OPTIONS SPECIFIED, MUTUALLY   04650000
.*             EXCLUSIVE CHECK WHERE APPLICABLE, AND SET GLOBALS        04700000
.*             WITH THE VALUES OF THE KEYWORDS.                         04750000
.*                                                                      04800000
         DC    A(0) .                   POINTER TO PLACEHOLDER          04850000
         AIF   ('&ECB' EQ '' AND '&EXIT' EQ '').DEFAULT         X04SVHS 04900000
         AIF   ('&ECB' EQ '').EXIT      TRY EXIT IF NOT ECB      X03004 04950000
         DC    A(&ECB) .                POINTER TO ECB                  05000000
         AIF   ('&EXIT' EQ '').NOEXTSP                          X04SVHS 05050000
         IDAERMAC 19,ECB,EXIT           ISSUE ERROR MSG            @L9C 05100000
         AGO   .NOEXTSP                                          X03004 05140000
.EXIT    AIF   ('&EXIT' EQ '').DEFAULT  DEFAULT IF NOT EXIT     X04SVHS 05180000
         AIF   ('&AM' EQ 'VTAM').SETEXIT  WAS VTAM SPECIFIED     X03004 05220000
         IDAERMAC 18,EXIT               ISSUE ERROR MSG            @L9C 05260000
         AGO   .DEFAULT                 DO NOT SET ASYNCH EXIT   X03004 05350000
.SETEXIT ANOP                           SET ASYNCH EXIT          X03004 05450000
         DC    A(&EXIT) .               POINTER TO ASYNCH EXIT   X03004 05500000
&EXITB   SETB  1                                                 X03004 05550000
         AGO   .ENDEXIT                                          X03004 05600000
.DEFAULT ANOP                                                    X03004 05650000
         DC    A(0) .                   ECB                             05700000
.NOEXTSP ANOP                                                    X03004 05750000
&NOEXIT  SETB  1                                                 X03004 05800000
.ENDEXIT ANOP                                                    X03004 05850000
         DC    BL1'0' .                 STATUS BYTE                     05900000
         DC    XL3'0'                   FEEDBACK CODES                  05950000
.*                                                              X3004BH 06000000
.* IF AM=VTAM, A VTAM SECTION OVERLAYS THE TWO WORDS CONTAINING X3004BH 06050000
.* KEYLEN,STRING ID AND CONTROL CHARACTER POINTER.  IF NOT      X3004BH 06100000
.* VTAM, ALL VTAM KEYWORDS FOR THIS AREA ARE INVALID.           X3004BH 06150000
.*                                                              X3004BH 06200000
         AIF   ('&AM' EQ 'VTAM').OVLY16 IF VTAM GO TO OVERLAY   X3004BH 06250000
         DC    AL2(&KEYLEN) .           KEY LENGTH                      06300000
         AIF   ('&TRANSID' NE '0' AND '&TIMEOUT' NE '').MUTEX1     @LAA 06308300
         AIF   ('&TIMEOUT' EQ '').DOTID   GENERATE TRANSID         @LAA 06316600
.*****   CHECK FOR VALID TIMEOUT...0 <= TIMEOUT <= 65536           @LAA 06324900
         AIF   (T'&TIMEOUT NE 'N').TIME2    NOT NUMERIC            @LAA 06333200
         AIF   ('&TIMEOUT'(1,1) EQ '(').TIME2  SKIP RANGE CHK      @LAA 06341500
.*                                       FOR NUMERIC IN PARENS     @LAC 06349800
         AIF   (&TIMEOUT GE 0).TIME1    TIMEOUT >= 0...OK          @LAA 06358100
         IDAERMAC 3,TIMEOUT,&TIMEOUT    ISSUE ERROR MESSAGE        @LAA 06366400
.TIME1   AIF   (&TIMEOUT LE 65536).TIME2 TIMEOUT <= 65536...OK     @LAA 06374700
         IDAERMAC 3,TIMEOUT,&TIMEOUT    ISSUE ERROR MESSAGE        @LAA 06383000
.TIME2   ANOP                                                      @LAA 06391300
         DC    AL2(&TIMEOUT)            DEADLOCK TIMEOUT VALUE     @LAA 06399600
         AGO   .LUWID                   GO CHECK FOR LUWID/CCHAR   @LAA 06407900
.MUTEX1  IDAERMAC 19,TRANSID,TIMEOUT                               @LAA 06416200
.DOTID   DC    AL2(&TRANSID) .          TRANSID                    @LAC 06424500
.LUWID   ANOP                                                      @LAA 06432800
         AIF   ('&CCHAR' NE '0' AND '&LUWID' NE '').MUTEX2         @LAA 06441100
         AIF   ('&LUWID' EQ '').DOCC    GENERATE CCHAR             @LAA 06449400
         DC    A(&LUWID)                LOGICAL UNIT OF WORK ID    @LAA 06457700
         AGO   .ACB                     GO GENERATE ACB PTR        @LAA 06466000
.MUTEX2  IDAERMAC 19,LUWID,CCHAR                                   @LAA 06474300
.DOCC    DC    A(&CCHAR)                POINTER TO CONTROL CHARACTER    06482600
.*                                                                 @LAC 06490900
.*             THE ACB ADDRESS CAN BE SPECIFIED BY ACB= OR ACBDATA=     06500000
.ACB     ANOP                                                   X3004BH 06550000
         AIF   ('&ACBDATA' EQ '').TRYACB CHECK FOR ACB= IF NOT ACBDATA  06600000
         DC    A(&ACBDATA) .            POINTER TO DATA ACB             06650000
         AGO   .ACBEND                                                  06700000
.TRYACB  ANOP                                                           06750000
         AIF   ('&ACB' EQ '').NOACB      WAS ACB= SPECIFIED             06800000
         DC    A(&ACB) .                POINTER TO ACB                  06850000
         AGO   .ACBEND                                                  06900000
.NOACB   ANOP                                                           06950000
         DC    A(0) .                   RESERVED FOR ACB POINTER        07000000
.ACBEND  ANOP                                                           07050000
         DC    A(0) .                   POINTER TO TCB                  07100000
         DC    A(&AREA) .               POINTER TO RECORD AREA          07150000
.NIBARG  AIF   ('&NIB' EQ '').NONIB     BR IF NIB NOT SPECIFIED  X03004 07200000
         AIF   ('&ARG' EQ '0').NIB      BR IF ARG NOT SPECIFIED  X03004 07250000
         IDAERMAC 19,NIB,ARG            ISSUE ERROR MSG            @L9C 07300000
         DC    A(0)                     POINTER TO ARGUMENT      X03004 07350000
         AGO   .OPTCD                                           X3004BH 07400000
.NIB     ANOP                           NIB CHECK                X03004 07450000
         AIF   ('&AM' EQ 'VTAM').SETNIB WAS VTAM SPECIFIED       X03004 07500000
         IDAERMAC 18,NIB                ISSUE ERROR MSG            @L9C 07550000
         AGO   .NONIB                   SKIP NIB SETUP           X03004 07600000
.SETNIB  ANOP                           SET NIB                  X03004 07650000
         DC    A(&NIB)                  POINTER TO NIB           X03004 07700000
&NIBD    SETB  1                        INDICATE NIB SPECIFIED   X03004 07750000
         AGO   .OPTCD                                           X3004BH 07800000
.NONIB   ANOP                                                    X03004 07850000
         DC    A(&ARG) .                POINTER TO ARGUMENT             07900000
.OPTCD   ANOP                                                   X3004BH 07950000
&ECBX    SETB  ('&ECB' NE '' AND '&ECB' NE '0')                @XM01126 08000000
         DC    B'&IDAOP1&ECBX' .        OPTCD BYTE 1                    08050000
         DC    B'&IDAOP2' .            OPTCD BYTE 2                     08100000
         DC    B'&IDAOP3'               OPTCD BYTE 3               @LAC 08150000
         DC    B'&IDAOP4'               OPTCD BYTE 4               @EAC 08200000
         DC    A(&NXTRPL) .             POINTER TO NEXT RPL             08250000
.*             THE LRECL SIZE CAN BE SPECIFIED BY LEN= OR RECLEN=       08300000
         AIF   ('&LEN' EQ '').TRYLREC CHECK FOR RECLEN= IF NOT LEN      08350000
         DC    A(&LEN) .                LOGICAL RECORD LENGTH           08400000
         AGO   .LENEND                                                  08450000
.TRYLREC ANOP                                                           08500000
         AIF   ('&RECLEN' EQ '').NOLEN  WAS RECLEN= SPECIFIED           08550000
         DC    A(&RECLEN) .             LOGICAL RECORD LENGTH           08600000
         AGO   .LENEND                                                  08650000
.NOLEN   ANOP                                                           08700000
         DC    A(0) .                   RESERVED FOR LOGICAL REC LEN    08750000
.LENEND  ANOP                                                           08800000
.*             THE BUFFER SIZE CAN BE SPECIFIED BY BUFL= OR AREALEN=    08850000
         AIF   ('&BUFL' EQ '').TRYALEN CHECK FOR AREALEN= IF NOT BUFL   08900000
         DC    A(&BUFL) .               BUFFER LENGTH                   08950000
         AGO   .ALENEND                                                 09000000
.TRYALEN ANOP                                                           09050000
         AIF   ('&AREALEN' EQ '').NOALEN  WAS AREALEN= SPECIFIED        09100000
         DC    A(&AREALEN) .            BUFFER LENGTH                   09150000
         AGO   .ALENEND                                                 09200000
.NOALEN  ANOP                                                           09250000
         DC    A(0) .                   RESERVED FOR BUFFER LENGTH      09300000
.ALENEND ANOP                                                           09350000
*                                       OPTCD BYTES 5-8          X03004 09400000
         DC    B'&IDAOP5' .            BYTE 5                  X04SVHS  09450000
         DC    B'&IDAOP6' .            BYTE 6                  X04SVHS  09500000
         DC    B'&IDAOP7' .            BYTE 7                  X04SVHS  09550000
         DC    B'&IDAOP8' .            BYTE 8                  X04SVHS  09600000
.*                                                              X3004BH 09650000
.* IF AM=VTAM, A VTAM SECTION OVERLAYS THE TWO RBA WORDS.  IF   X3004BH 09700000
.* NOT VTAM, ALL VTAM KEYWORDS FOR THIS AREA ARE INVALID.       X3004BH 09750000
.*                                                              X3004BH 09800000
         AIF   ('&AM' EQ 'VTAM').OVLY60 IF VTAM GO TO OVERLAY   X3004BH 09850000
         AIF   ('&OBSQVAL' EQ '0').TSTIVL BRANCH IF NOT OBSQVAL X3004BH 09900000
         IDAERMAC 18,OBSQVAL            ISSUE ERROR MSG            @L9C 09950000
.TSTIVL  AIF   ('&IBSQVAL' EQ '0').TSTSEQ  BRANCH IF NOT IBSQVALX3004BH 09990000
         IDAERMAC 18,IBSQVAL            ISSUE ERROR MSG            @L9C 10030000
.TSTSEQ  AIF   ('&SEQNO' EQ '0').DEF60    BRANCH IF NOT SEQNO   X3004BH 10070000
         IDAERMAC 18,SEQNO              ISSUE ERROR MS             @L9C 10110000
.DEF60   ANOP                                                   X3004BH 10200000
         DC    XL8'0' .                 RBA                             10250000
.TSTBRCH ANOP                                                   X3004BH 10300000
         DC    B'0&NOEXIT&EXITB.0&IDACRYP&NIBD&IDABRCH.0'      @G40AKSL 10350000
         DC    AL1(0)                   ACTIVE INDICATOR                10400000
         DC    AL2(&MSGLEN) .           MAXIMUM ERROR MSG LENGTH        10450000
.*             THE MSGAREA CAN BE SPECIFIED BY ERRMSG OR MSGAREA        10500000
         AIF   ('&ERRMSG' EQ '').TRYMA CHECK FOR MSGAREA= IF NOT ERRMSG 10550000
         DC    A(&ERRMSG) .             POINTER TO ERROR MSG AREA       10600000
         AGO   .MAEND                                                   10650000
.TRYMA   ANOP                                                           10700000
         AIF   ('&MSGAREA' EQ '').NOMA  WAS MSGAREA= SPECIFIED          10750000
         DC    A(&MSGAREA) .            POINTER TO MESSAGE AREA         10800000
         AGO   .MAEND                                                   10850000
.NOMA    ANOP                                                           10900000
         DC    A(0) .                   RESERVED FOR MESSAGE AREA PTR   10950000
.MAEND   ANOP                                                    X03004 11000000
         AIF   ('&AM' EQ 'VTAM').VTAMEXT  WAS VTAM SPECIFIED     X03004 11050000
         AIF   ('&AAREA' EQ '0').NOAAREA  WAS AAREA SPECIFIED    X03004 11100000
         IDAERMAC 18,AAREA              ISSUE ERROR MSG            @L9C 11150000
.NOAAREA ANOP                           CHECK FOR AAREALN        X03004 11190000
         AIF   ('&AAREALN' EQ '0').NOARELN  AAREALN SPECIFIED    X03004 11230000
         IDAERMAC 18,AAREALN            ISSUE ERROR MSG            @L9C 11270000
.NOARELN ANOP                           CHECK FOR ARECLEN        X03004 11310000
         AIF   ('&ARECLEN' EQ '0').NOARCLN WAS ARECLEN SPECIFIED X03004 11350000
         IDAERMAC 18,ARECLEN            ISSUE ERROR MSG            @L9C 11390000
.NOARCLN ANOP                           CHECK FOR USENSEO       X3004BH 11430000
         AIF   ('&USENSEO' EQ '0').NOUSNSO WAS USENSEO SPECIFIEDX3004BH 11470000
         IDAERMAC 18,USENSEO            ISSUE ERROR MSG            @L9C 11510000
.NOUSNSO ANOP                                                   X3004BH 11550000
         AIF   ('&SSENSMO' EQ '0').NOSSNMO WAS SSENSMO SPEC    X03004JS 11590000
         IDAERMAC 18,SSENSMO            ISSUE ERROR MSG            @L9C 11630000
.NOSSNMO ANOP                           CHECK FOR SIGDATA      @Z40BHUC 11670000
         AIF   ('&SIGDATA' EQ '0').NOSIGDA WAS SIGDATA SPEC    @Z40BHUC 11710000
         IDAERMAC 18,SIGDATA            ISSUE ERROR MSG            @L9C 11750000
.NOSIGDA ANOP                           END OF CHECK           @Z40BHUC 11950000
         MEXIT                          NOT VTAM - END MACRO    X3004BH 12000000
.VTAMEXT ANOP                           SET VTAM PARAMETERS      X03004 12050000
         DC    A(&AAREA) .              ADDITIONAL DATA AREA PTR X03004 12100000
         DC    A(&AAREALN) .            ADDITIONAL AREA LENGTH   X03004 12150000
         DC    A(&ARECLEN) .            ADDITIONAL RECORD LENGTH X03004 12200000
         DC    XL4'0'                   FEEDBACK AREA            X03004 12250000
         DC    XL4'0'                   USER FIELD               X03004 12300000
         DC    B'&IDAOP9' .            OPTCD BYTE 9            X04SVHS  12350000
         DC    B'&IDAOPA' .            OPTCD BYTE 10           X04SVHS  12400000
         DC    B'&IDAOPB' .            OPTCD BYTE 11           X04SVHS  12450000
         DC    B'&IDAOPC' .            OPTCD BYTE 12           X04SVHS  12500000
         DC    B'&IDASNSO'              SYSTEM SENSE OUTPUT     X04SVHS 12550000
*                                       DATA                   X03004JS 12600000
         DC    AL1(&SSENSMO)            SYSTEM SENSE MODIFIER           12650000
*                                       OUTPUT                 X03004JS 12700000
         DC    AL2(&USENSEO)            USER SENSE OUTPUT       X3004BH 12750000
         DC    A(0)                     SAVE AREA OF FAST PATH @Z40BHUC 12800000
         DC    A(&SIGDATA)              SIGNAL DATA FIELD      @Z40BHUC 12850000
*                                                                       12900000
         MEXIT                          END OF VTAM EXTENSION   X3004BH 12950000
.*                                                              X3004BH 13000000
.* PROCESSING FOR VTAM FIELDS WHICH START AT OFFSET 16          X3004BH 13050000
.*                                                              X3004BH 13100000
.OVLY16  ANOP                                                   X3004BH 13150000
         DC    B'&IDARH3'               THIRD REQUEST HEADER            13200000
*                                                BYTE           X04SVHS 13250000
.*                                                              X3004BH 13300000
.* STYPE/RTYPE                                                  X3004BH 13350000
         DC    B'&IDASRTY'              SEND/RECEIVE TYPE       X04SVHS 13400000
         DC    B'&IDACHN'               REQUEST UNIT CHAIN POSITION     13450000
.*                                                              X04SVHS 13500000
         DC    X'00'                             VTAM FLAGS     X3004BH 13550000
         DC    B'&IDAPORE'              POST/RESPOND FLAGS      X04SVHS 13600000
         DC    B'&IDACON1'              DATA CONTROL            X04SVHS 13650000
         DC    B'&IDACON2'              DATA CONTROL            X04SVHS 13700000
         DC    B'&IDACON3'              SESSION CONTROL         X04SVHS 13750000
         AGO   .ACB                                             X3004BH 13800000
.*                                                              X3004BH 13850000
.* PROCESSING FOR VTAM FIELDS WHICH START AT OFFSET 60          X3004BH 13900000
.*                                                              X3004BH 13950000
.OVLY60  ANOP                                                   X3004BH 14000000
         DC    AL2(&OBSQVAL)            STSN OUTBOUND SEQ NO    X3004BH 14050000
         DC    AL2(&IBSQVAL)            STSN INBOUND SEQ NO     X3004BH 14100000
         DC    B'&IDAOBQC'              STSN OUTBOUND ACTION CODES      14150000
.*                                                              X04SVHS 14200000
         DC    B'&IDAIBQC'              STSN INBOUND ACTION CODES       14250000
.*                                                              X04SVHS 14300000
         DC    AL2(&SEQNO)              SEQUENCE NUMBER         X3004BH 14350000
         AGO   .TSTBRCH                                         X3004BH 14400000
         MEND                                                           14450000
