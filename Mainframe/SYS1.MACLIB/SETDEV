         MACRO                                                          02000003
&ANYNAME SETDEV &DCBA,       ADDRESS OF DCB                            X04000003
               &DEVT=,       DEVICE TYPE - SPECIFIED BY GENERIC NAME   X06000003
               &FRID=,       FORMAT RECORD IDENTIFER FOR 3886          X08000003
               &IREC=,       ADDRESS OF IREC MACRO FOR 3890            X10000003
               &SUBRTN=,           SCI SUBROUTINE OPERAND              X12000003
               &PROGRAM=,          SCI PROGRAM OPERAND                 X14000003
               &MF=          MACRO FORM                                 16000003
         GBLB  &QUITSW                                                  18000003
.*  FUNCTION -  LOAD DEVICE DEPENDENT CONTROL INFORMATION               22000003
.*  DEVICES SUPPORTED - 3886 , 3890                                     24000003
.*                                                                      26000003
&QUITSW  SETB  0                       INITIALIZE QUIT SWITCH TO 0      28000003
.*             TEST FOR VALID DCB,DEVT AND MF SPEC /ALIGN PARMLST ON WD 30000003
         TSTCOM &DCBA,&DEVT,&MF                                         32000003
.*             TEST FOR 3886  AND THEN PROCESS 3886 PARAMETERS          38000003
         AIF   ('&DEVT' NE '3886').NXTDEV1  TEST IF DEVICE = 3886       40000003
         AIF   (&QUITSW).SKIPSVC   ERRORS FOUND                         42000003
&ANYNAME CALL3886 &DCBA,&FRID,&MF                                       44000003
         AIF   (NOT &QUITSW).GENEND    TEST IF QUIT SWITCH ON           46000003
         MEXIT                                                          48000003
.*             TEST FOR 3890 AND THEN PROCESS 3890 PARAMETERS           50000003
.NXTDEV1 AIF   ('&DEVT' NE '3890').NXTDEV2 TEST FOR DEVICE = 3890       52000003
.*             YES GO SET UP PARAMETER LIST FOR 3890                    54000003
&ANYNAME CALL3890 &DCBA,&IREC,&SUBRTN,&PROGRAM,&MF                      56000003
         AIF   (NOT &QUITSW).GENEND    TEST IF QUIT SWITCH ON           60000003
         MEXIT                                                          62000003
.*       GENERATE PROPER END LINKAGE- SVC   81 FOR STANDARD AND EXECUTE 64000003
.NXTDEV2 AIF   (&QUITSW).SKIPSVC   TEST FOR ERROR FOUND                 66000003
.GENEND  AIF   ('&MF' EQ 'L').SKIPSVC                                   68000003
         SVC   81             ISSUE SVC FOR SETDEV                      70000003
.SKIPSVC ANOP                                                           72000003
         MEND                                                           74000003
