         MACRO                                                                  
         IKJUNFLD &VERIFCK=,&SUBFLD=,&EXT                    @E23X2C6           
.**01* PROPRIETARY STATEMENT=                                         *         
.* **PROPRIETARY_STATEMENT*********************************************         
.*                                                                    *         
.*                                                                    *         
.*  LICENSED MATERIALS - PROPERTY OF IBM                              *         
.*  THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                       *         
.*  5685-025 (C) COPYRIGHT IBM CORP. 1987, 1991                       *         
.*  SEE COPYRIGHT INSTRUCTIONS                                        *         
.*                                                                    *         
.*  STATUS= JTE23X2                                                   *         
.*                                                                    *         
.* **END_OF_PROPRIETARY_STATEMENT**************************************         
.********************************************************************           
.*  TITLE - IKJUNFLD MACRO                                                      
.*                                                                              
.*  FUNCTION - THE IKJUNFLD MACRO IS USED BY COMMAND                            
.*             PROCESSORS TO DEFINE TO PARSE ANY PARAMETERS                     
.*             THAT ARE TO BE VERIFIED BY A VERIFY EXIT                         
.*                                                                              
.*                                                                              
.*                                                                              
.*  CHANGE ACTIVITY -                                                           
.*             CREATED FOR TSO/E RELEASE 4                                      
.*                                                                              
.*                                                                              
.*                                                                              
.*  INPUT - THE PROTOTYPE STATEMENT SHOWN ABOVE AND BELOW:                      
.*                                                                              
.*             IKJUNFLD &SUBFLD=,&VERIFCK=,&EXT              @E23X2C6           
.*                                                                              
.*          ITS PARAMETERS REPRESENT THE FOLLOWING:                             
.*                                                                              
.*             &SUBFLD -  Represents the label on the                           
.*                        CORRESPONDING IKJSUBF MACRO.                          
.*             &VERIFCK - SPECIFIES THE ADDRESS OF A USER                       
.*                        SUPPLIED VERIFY EXIT ROUTINE                          
.*             &EXT -     New keyword parameter to specify                      
.*                        option for extended support.       @E23X2C6           
.*                                                                              
.*  OUTPUT - THIS MACRO PRODUCES A PARAMETER CONTROL ENTRY                      
.*          (PCE) TO BE PASSED TO THE PARSE SERVICE ROUTINE.                    
.*                                                                              
.*  MESSAGES:                                                                   
.*        - IKJUNFLD CAN HAVE ONLY 1 POSITIONAL PARAMETER    @E23X2C6           
.*                                                                              
.*        - REQUIRED OPERAND VERIFCK WAS OMITTED                                
.*                                                                              
.*        - '&EXT' IS AN INVALID PARAMETER                   @E23X2C6           
.*                                                                              
.*  Change Activity:                                                            
.*                                                                              
.*A-000000-999999                                            @E2467K7           
.* E23X2C6 - JTE23X2 TSO/E 2.3.1 EXTENDED IKJUNFLD SUPPORT   @E23X2C6*          
.**********************************************************************         
.*A-000000-999999                                            @E2467K7*          
         GBLC  &IKJCSNM,&IKJKWST                                                
         GBLA  &UNKSUB                                       @E23X2C6           
         LCLA  &LASTNDX                                      @E23X2C6           
&LASTNDX SETA  &SYSNDX-1                                     @E23X2C6           
         LCLC  &KWSTSAV                                                         
         LCLB  &FSUBF,&KWSTSW,&FEXT                          @E23X2C6           
         AIF   (N'&SYSLIST LE 2).A0  ONLY 1 POSIT. PARM      @E23X2C6           
         MNOTE 8,'IKJUNFLD CAN HAVE ONLY 1 POSITIONAL PARAMETER'                
*                                                            @E23X2C6           
         AGO   .END                           TERMINATE      @E23X2C6           
.A0      ANOP                                                                   
&FEXT    SETB  ('&EXT' EQ 'EXT')                             @E23X2C6           
         AIF   ('&EXT' EQ 'EXT').TESTEXT                     @E23X2C6           
         AIF   ('&EXT' EQ '').VALID                          @E23X2C6           
         MNOTE 8,'''&EXT'' IS AN INVALID PARAMETER'                             
*                                                            @E23X2C6           
         AGO   .END                           TERMINATE      @E23X2C6           
.TESTEXT ANOP                                                @E23X2C6           
         AIF   (&UNKSUB EQ &LASTNDX).VALID                   @E23X2C6           
         MNOTE 8,'IKJUNFLD with EXT must be preceded by the IKJSUBF mac*        
               ro'                                           @E23X2C6           
         AGO   .END                           TERMINATE      @E23X2C6           
.VALID   ANOP                                                                   
         AIF   ('&VERIFCK' NE '').A1          WAS VERIFCK ENTERED               
         MNOTE 8,'REQUIRED OPERAND VERIFCK WAS OMITTED'                         
         AGO .END                             TERMINATE                         
.A1      ANOP                                                                   
&FSUBF   SETB  ('&SUBFLD' NE '')              SUBFIELD FLAG                     
&IKJCSNM CSECT ,                              RESUME PCL CSECT                  
         AIF   ('&IKJKWST' EQ '').NOSET IF NULL, NO SET NECESSARY               
&IKJKWST EQU   *-&IKJCSNM              OFFSET TO FIRST KEYWORD                  
&IKJKWST SETC  ''                                                               
.NOSET   ANOP                                                                   
UFLD&SYSNDX EQU *                                                               
         DC    BL2'0101&FEXT.00000000000'     FLAGS          @E23X2C6           
         DC    AL2(UEND&SYSNDX-UFLD&SYSNDX)   LENGTH OF UNFLD PCE               
         DC    AL4(&VERIFCK)                  VERIFY EXIT ADDRESS               
UKWD&SYSNDX IKJKEYWD                                                            
.*********************************************************************          
.* ISSUE THE APPROPRIATE CALL TO THE IKJNAME MACRO BASED ON WHETHER             
.* THE SUBFLD AND EXT OPERANDS WERE ENTERED ON THE IKJUNFLD MACRO.              
.* FIRST CHECK FOR EXT OPERAND, THEN BRANCH TO APPROPRIATE SECTION              
.* AND CHECK FOR THE SUBFLD OPERAND.                                            
.*                                                                              
.* A2 = IKJNAME 31  blanks + subfield                                           
.* A3 = IKJNAME 31  blanks                                                      
.* A4 = IKJNAME 250 blanks ext + subfield                                       
.* A5 = IKJNAME 250 blanks ext                                                  
.*                                                           @E23X2C6           
.*********************************************************************          
         AIF   ('&FEXT' EQ '1').A4                           @E23X2C6           
         AIF   ('&FSUBF' EQ '0').A3                          @E23X2C6           
.A2      ANOP                                                @E23X2C6           
*        IKJNAME  '       31 Blanks long          ',SUBFLD=&SUBFLD              
*                                                            @E23X2C6           
         IKJNAME  '                               ',SUBFLD=&SUBFLD              
         AGO   .A6                                           @E23X2C6           
.A3      ANOP                                                                   
*        IKJNAME  '       31 Blanks long          '          @E23X2C6           
         IKJNAME  '                               '                             
         AGO   .A6                                           @E23X2C6           
.A4      ANOP                                                @E23X2C6           
         AIF   ('&FSUBF' EQ '0').A5                          @E23X2C6           
*        IKJNAME  '       250 blanks long                              *        
                                                                       *        
                                                                       *        
                                                                       *        
                                             ',SUBFLD=&SUBFLD @E23X2C6          
         IKJNAME  '                                                    *        
                                                                       *        
                                                                       *        
                                                                       *        
                                             ',SUBFLD=&SUBFLD @E23X2C6          
         AGO   .A6                                           @E23X2C6           
.A5      ANOP                                                @E23X2C6           
*        IKJNAME  '       250 blanks long                              *        
                                                                       *        
                                                                       *        
                                                                       *        
                                             '               @E23X2C6           
         IKJNAME  '                                                    *        
                                                                       *        
                                                                       *        
                                                                       *        
                                             '               @E23X2C6           
.A6      ANOP                                                @E23X2C6           
UEND&SYSNDX EQU *                                                               
&SYSECT  CSECT ,                              RESUME USERS CSECT                
.END     MEXIT                                                                  
         MEND                                                                   
