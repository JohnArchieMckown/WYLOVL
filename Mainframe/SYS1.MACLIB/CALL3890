         MACRO                                                          00500003
&NAME    CALL3890 &DCBX,&IRECX,&SRTNX,&PGMX,&MFX                        01000003
         GBLB  &QUITSW                                                  02000003
         LCLC  &LSTADDR,&DCBV,&IRECV,&LBL,&SRTNV               @ZA04479 02500002
         LCLA  &LEN                                                     03000003
         LCLB  &N1,&N2                                         @ZA04479 03500002
&DCBV    SETC  '0'                     INITIALIZE DCB VALUE             04000003
&IRECV   SETC  '0'                     INITIALIZE IREC VALUE            04500003
&N2      SETB  1                  INIT REG OPERAND SW ON       @ZA04479 04550002
.*                                                                      05000003
.****************  VERIFY MACRO OPERANDS  ****************************  05500003
.*                                                                      06000003
.VERIFY  AIF   ('&MFX' NE '').VER020   TEST FOR LIST/EXECUTE FORMAT     06500003
.* STANDARD FORMAT                                                      07000003
         AIF   ('&IRECX' EQ '').ERR010 ERROR IF IREC ABSENT             07500003
         AIF   ('&IRECX' EQ '(0)' OR '&IRECX' EQ '(1)').ERR020          08000003
.VER010  AIF   ('&SRTNX' EQ '').VER050  TEST FOR SUBRTN PARM            08500003
         AIF   ('&PGMX' EQ '').ERR030  INSURE PROGRAM PARM PRESENT      09000003
         AGO   .VER040                 GO VERIFY SUBRTN PARM            09500003
.VER020  AIF   ('&MFX' EQ 'L').VER030  TEST FOR LIST FORMAT             10000003
.* EXECUTE FORMAT                                                       10500003
         AIF   ('&IRECX' EQ '(0)' OR '&IRECX' EQ '(1)').ERR020          11000003
         AGO   .VER040                 GO VERIFY SUBRTN OPERAND         11500003
.* LIST FORMAT                                                          12000003
.VER030  AIF   ('&IRECX' EQ '').VER040 IREC SPECIFIED                   12500003
         AIF   ('&IRECX'(1,1) EQ '(').ERR020 INSURE VALID IREC PARM     13000003
.* STANDARD, LIST, AND EXECUTE FORMAT                          @ZA04479 13050002
.VER040  AIF   ('&SRTNX' EQ '').VER050 SUBRTN SPECIFIED                 13500003
         AIF   ('&SRTNX'(1,1) NE '(').VER045                            14000003
         AIF   ('&SRTNX(2)' EQ '').ERR040 ADDRESS SPECIFIED             14500003
&SRTNV   SETC  '&SRTNX(2)'                                     @ZA04479 14550002
&N2      SETB  ('&SRTNV'(1,1) NE '(') SW = TRUE IF NOT IN REG  @ZA04479 14600002
         AIF   (&N2).VER041       BRANCH IF NOT REGISTER       @ZA04479 14650002
         AIF   ('&MFX' NE '' AND '&MFX' EQ 'L').ERR040         @ZA04479 14700002
.VER041  AIF   (T'&SRTNX(1) NE 'N').ERR040 SELF-DEFINING TERM  @ZA04479 14750002
&LEN     SETA  &SRTNX(1)               SET LENGTH OF SUBRTN             15500003
         AIF   (&LEN EQ 0).ERR040      LENGTH EQUAL 0                   16000003
         AGO   .VER050                 GO TEST QUIT SWITCH              16500003
.VER045  AIF   (T'&SRTNX EQ 'N').ERR040 SELF-DEFINING TERM              17000003
.VER050  AIF   (&QUITSW).EOJ           ERRORS FOUND                     17500003
         AIF   ('&MFX' EQ 'L').PARMLST TEST FOR LIST FORMAT             18000003
&LBL     SETC  'IHB1&SYSNDX'           SET LABEL                        18500003
         AIF   ('&NAME' EQ '').VER060  MACRO LABEL PRESENT              19000003
&NAME    EQU   *                                                        19500003
.VER060  AIF   ('&MFX' NE '').EXECUTE  TEST FOR EXECUTE FORMAT          20000003
.*                                                                      20500003
.****************  STANDARD FORMAT INSTRUCTION GENERATION  ***********  21000003
.*                                                                      21500003
.STNDARD AIF   ('&DCBX'(1,1) EQ '(').STD030 REGISTER NOTATION - DCB     22000003
&DCBV    SETC  '&DCBX'                 SET DCB VALUE FOR PARMLIST       23000003
.STD010  AIF   ('&IRECX'(1,1) EQ '(').STD040 REGISTER NOTATION - IREC   23500003
&IRECV   SETC  '&IRECX'                SET IREC VALUE FOR PARMLIST      24000003
.STD020  AIF   (&N2).STD021       BR IF SRTN ADDR NOT IN REG   @ZA04479 24050002
         ST    &SRTNX(2),IHB1&SYSNDX+16 STORE SRT ADDR IN LIST @ZA04479 24100002
.STD021  BAL   1,IHB1&SYSNDX+28   BRANCH TO ISSUE SETDEV SVC   @ZA04479 24150002
         AGO   .PARMLST                GO EXPAND PARMLIST               28000003
.STD030  ST    &DCBX(1),IHB1&SYSNDX    STORE DCB ADDRESS                28500003
         AGO   .STD010                 GO TEST IREC PARM                29500003
.STD040  ST    &IRECX(1),IHB1&SYSNDX+4 STORE IREC ADDRESS               30000003
         AGO   .STD020                 GO EXPAND INSTRUCTIONS           31000003
.*                                                                      31500003
.****************  EXECUTE FORMAT INSTRUCTION GENERATION  ************  32000003
.*                                                                      32500003
.EXECUTE ANOP                                                           33000003
&LSTADDR SETC  '&MFX(2)'               SET ADDRESS OF LIST              33500003
         AIF   ('&LSTADDR' EQ '(1)').EXC020 LIST IN REGISTER 1          34000003
         AIF   ('&LSTADDR'(1,1) EQ '(').EXC010 ADDRESS LIST IN REGISTER 34500003
         LA    1,&MFX(2)               SETDEV PARAMETER LIST ADDRESS    35000003
         AGO   .EXC020                 GO TEST DCB PARM                 35500003
.EXC010  LR    1,&MFX(2)               SETDEV PARAMETER LIST ADDRESS    36000003
.EXC020  AIF   ('&DCBX' NE '').EXC060  DCB SPECIFIED                    36500003
.EXC030  AIF   ('&IRECX' NE '').EXC080 IREC SPECIFIED                   37000003
.EXC040  AIF   ('&PGMX' NE '').EXC100  PROGRAM SPECIFIED                37500003
.EXC050  AIF   ('&SRTNX' NE '').EXC140 SUBRTN SPECIFIED                 38000003
.EXC055  AGO   .EOJ                        FINISHED                     38500003
.* STORE DCB ADDRESS IN SETDEV PARMLIST                                 42500003
.EXC060  AIF   ('&DCBX'(1,1) EQ '(').EXC070 REGISTER NOTATION           43000003
         LA    0,&DCBX                 DCB ADDRESS                      43500003
         ST    0,0(0,1)                STORE DCB ADDRESS                44000003
         AGO   .EXC030                 GO TEST IREC PARM                44500003
.EXC070  ST    &DCBX(1),0(0,1)         STORE DCB ADDRESS                45000003
         AGO   .EXC030                 GO TEST IREC PARM                45500003
.* STORE IREC ADDRESS IN SETDEV PARMLIST                                46000003
.EXC080  AIF   ('&IRECX'(1,1) EQ '(').EXC090 REGISTER NOTATION          46500003
         LA    0,&IRECX                IREC ADDRESS                     47000003
         ST    0,4(0,1)                STORE IREC ADDRESS               47500003
         AGO   .EXC040                 GO TEST PROGRAM PARM             48000003
.EXC090  ST    &IRECX(1),4(0,1)        STORE IREC ADDRESS               48500003
         AGO   .EXC040                 GO TEST PROGRAM PARM             49000003
.* STORE PROGRAM NAME IN SETDEV PARMLIST                                49500003
.EXC100  AIF   ('&SRTNX' NE '').EXC110 SUBRTN SPECIFIED ALSO            50000003
         MVC   20(8,1),IHB1&SYSNDX     STORE PROGRAM NAME               50500003
         BC    15,IHB1&SYSNDX+8        BRANCH AROUND CONSTANT           51000003
&LBL     DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME           51500003
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS    52000003
.* STORE PROGRAM AND SUBRTN IN SETDEV PARMLIST                          52500003
.EXC110  MVC   12(16,1),IHB1&SYSNDX    STORE PROGRAM AND SUBRTN PARMS   53000003
         BC    15,IHB1&SYSNDX+16       BRANCH AROUND CONSTANTS          53500003
         AIF   ('&SRTNX(2)' NE '').EXC120 SUBRTN IN CORE                54000003
&LBL     DC    CL8'&SRTNX'             SCI SUBROUTINE MEMBERNAME        54500003
         DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME           55000003
         NI    11(1),X'7F'              RESET SUBRTN FLAG               55500003
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS    56000003
.EXC120  CNOP  0,4                     FULLWORD BOUNDARY ALLIGNMENT     56500003
&LBL     DC    A(&LEN)                 SCI SUBROUTINE LENGTH            57000003
         AIF   (&N2).EXC121       BR IF SRTN ADDR NOT IN LIST  @ZA04479 57050002
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479 57100002
         AGO   .EXC122                                         @ZA04479 57150002
.EXC121  DC    A(&SRTNX(2))            SCI SUBROUTINE ADDRESS  @ZA04479 57200002
.EXC122  DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME  @ZA04479 57250002
         AIF   (&N2).EXC123       BR IF SRTN ADDR NOT IN REG   @ZA04479 57300002
         ST    &SRTNX(2),16(,1)   STORE SRTN ADDR IN PARM LIST @ZA04479 57350002
.EXC123  OI    11(1),X'80'        INDICATE SUBROUTINE RESIDENT @ZA04479 57400002
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS    59000003
.* STORE SCI SUBROUTINE PARAMETERS IN SETDEV PARMLIST                   59500003
.EXC140  MVC   12(8,1),IHB1&SYSNDX     STORE SUBRTN PARM                60000003
         BC    15,IHB1&SYSNDX+8        BRANCH AROUND CONSTANTS          60500003
         AIF   ('&SRTNX(2)' NE '').EXC150 SUBRTN IN CORE                61000003
&LBL     DC    CL8'&SRTNX'             SCI SUBROUTNE MEMBERNAME         61500003
         NI    11(1),X'7F'              RESET SUBRTN FLAG               62000003
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS    62500003
.EXC150  CNOP  0,4                     FULLWORD BOUNDARY ALLIGNMENT     63000003
&LBL     DC    A(&LEN)                 SCI SUBROUTINE LENGTH            63500003
         AIF   (&N2).EXC151       BR IF SRTN ADDR NOT IN REG   @ZA04479 63550002
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479 63600002
         ST    &SRTNX(2),16(,1)   STORE SRTN ADDR IN PARM LIST @ZA04479 63650002
         AGO   .EXC152                                         @ZA04479 63700002
.EXC151  DC    A(&SRTNX(2))       SCI SUBROUTINE ADDRESS       @ZA04479 63750002
.EXC152  OI    11(1),X'80'        INDICATE SUBROUTINE RESIDENT @ZA04479 63800002
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS    65000003
.*                                                                      65500003
.****************  SETDEV PARMLIST MACRO GENERATION  *****************  66000003
.*                                                                      66500003
.PARMLST AIF   ('&MFX'NE 'L').PARM040  TEST FOR LIST FORMAT             67000003
         AIF   ('&DCBX' NE '').PARM020 DCB PARM SPECIFIED               67500003
.PARM010 AIF   ('&IRECX' NE '').PARM030 IREC SPECIFIED                  68000003
.PARM015 ANOP                                                           68500003
&NAME    DC    A(&DCBV)                DCB ADDRESS                      69000003
         AGO   .PARM050                CONTINUE EXPANSION               69500003
.PARM020 ANOP                                                           70000003
&DCBV    SETC  '&DCBX'                 SET VALUE FOR DCB                70500003
         AGO   .PARM010                GO TEST IREC PARM                71000003
.PARM030 ANOP                                                           71500003
&IRECV   SETC  '&IRECX'                SET IREC VALUE                   72000003
         AGO   .PARM015                GO EXPAND DCB ADDRESS            72500003
.PARM040 ANOP                                                           73000003
&LBL     DC    A(&DCBV)                DCB ADDRESS                      73500003
.PARM050 DC    A(&IRECV)               IREC ADDRESS                     74000003
         DC    XL3'FF0200'             SETDEV PARAMETERS                74500003
&N1      SETB  ('&SRTNX(2)' NE '')                                      75000003
         DC    BL1'&N1.0000000'        SETDEV FLAGS                     75500003
         AIF   ('&SRTNX' NE '').PARM060 SUBRTN SPECIFIED                76000003
         DC    XL8'00'                 SCI SUBROUTINE PARAMETER         76500003
         AGO   .PARM080                                                 77000003
.PARM060 AIF   (&N1).PARM070           SUBRTN IN MAIN STORAGE           77500003
         DC    CL8'&SRTNX'             SCI SUBROUTINE MEMBERNAME        78000003
         AGO   .PARM080                                                 78500003
.PARM070 DC    A(&LEN)                 SCI SUBROUTINE LENGTH            79000003
         AIF   (&N2).PARM071      BR IF SRTN ADDR NOT IN REG   @ZA04479 79050002
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479 79100002
         AGO   .PARM080                                        @ZA04479 79150002
.PARM071 DC    A(&SRTNX(2))       SCI SUBROUTINE ADDRESS       @ZA04479 79200002
.PARM080 AIF   ('&PGMX' NE '').PARM090 PROGRAM SPECIFIED                80000003
         DC    XL8'00'                 SCI PROGRAM MEMBERNAME           80500003
         MEXIT                                                          81000003
.PARM090 DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME           81500003
         MEXIT                                                          82500003
.*                                                                      83000003
.****************  MNOTES AND ERROR CONDITIONS  **********************  83500003
.*                                                                      84000003
.ERR010  MNOTE 12,'***  IHB001  IREC OPERAND REQ''D-NOT SPECIFIED'      84500003
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH     85000003
         AGO   .VER010                 GO VERIFY NEXT OPERAND           85500003
.ERR020  ANOP                                                           86000003
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH     86500003
         AIF   ('&MFX' NE 'L').ERR025  LIST FORMAT                      87000003
         MNOTE 12,'***  IHB006  INVALID REGISTER NOTATION WITH MF=L FORX87500003
               M.'                                                      88000003
         AGO   .VER040                 GO VERIFY NEXT PARAMETER         88500003
.ERR025  MNOTE 12,'***  IHB002  IREC OPERAND INVALID-&IRECX'            89000003
         AIF   ('&MFX' EQ '').VER010   GO TEST NEXT MACRO OPERAND       89500003
         AGO   .VER040                 GO VERIFY NEXT MACRO OPERAND     90000003
.ERR030  MNOTE 12,'***  IHB414  PROGRAM OPERAND REQ''D-NOT SPECIFIED'   90500003
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH     91000003
         AGO   .VER040                 GO TEST NEXT MACRO OPERAND       91500003
.ERR040  MNOTE 12,'***  IHB415  INVALID SUBRTN OPERAND SPECIFIED'       92000003
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH     92500003
         MEXIT                                                          93000003
.EOJ     MEND                                                           93500003
