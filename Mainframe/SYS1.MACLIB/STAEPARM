         MACRO                                                          00600000
&NAME    STAEPARM  &FLAG=,                                             X01200000
               &PSEUDO=,                                               X01800000
               &LIST=,                                                 X02400000
               &ADDR=,                                                 X03000000
               &TYPE=A,                                                X03050000
               &MF=                                                     03600000
.*                                                                      04200000
.*       THIS MACRO BUILDS THE CONTROL BLOCK THAT IS PASSED TO THE      04800000
.*       IKJEHSIR EXIT ROUTINE VIA THE PARAM= PARAMETER OF THE STAE     05400000
.*       MACRO.                                                         06000000
.*                                                                      06600000
.*       &FLAG  IS THE FLAG TO SET IN THE BLOCK, CAN BE ONE OF TWO      07200000
.*             SETTINGS:  CP OR TMP.                                    07800000
.*             IF IT IS CP, THE DEFAULT, IT SAYS THE CP WANTS TO HANDLE 08400000
.*             SOME ABENDS.  IF IT IS TMP, IT SAYS THAT THE CP WANTS    09000000
.*             THE TMP TO HANDLE ALL ABENDS.                            09600000
.*       &PSEUDO  THE ADDRESS OF THE PSEUDO RETRY ROUTINE BUILT WITH    10200000
.*             THE PSEUDO MACRO.  FOR A FULL EXPLAINATION OF THE        11400000
.*             PSEUDO RETRY ROUTINE, SEE THE PSEUDO MACRO.              12000000
.*       &LIST  THIS IS THE ADDRESS OF THE RETRY CODE TABLE BUILD       12600000
.*             WITH THE STABUILD MACRO. FOR A FULLER EXPLAINATION,      13200000
.*             OF THE RETRY CODE TABLE SEE THE STABUILD MACRO.          13800000
.*       &ADDR  THIS IS THE ADDRESS OF THE MF=L FORM OF THIS MACRO.     14400000
.*             IT IS USED BY THE MF=E MACRO TO FILL IT IN, OR IT IS     15000000
.*             USED BY THE MF=L FORM AS A NAME IF NO NAME IS PROVIDED   15600000
.*       &TYPE TYPE OF MACRO EXPANSION FOR MF=E                         15650000
.*             A  SAYS THAT NEW BLOCK BEING BUILD                       15700000
.*             C  SAYS THAT OLD BLOCK BEING CHANGED                     15750000
.*                                                                      15795000
.*                                                                      15840000
.*PROPRIETARY V3 STATEMENT                                              15885000
.*LICENSED MATERIALS - PROPERTY OF IBM                                  15930000
.*"RESTRICTED MATERIALS OF IBM"                                         15975000
.*5695-DF1                                                              16020000
.*(C) COPYRIGHT 1992  IBM CORP.                                         16065000
.*END PROPRIETARY V3 STATEMENT                                          16115000
.*                                                                      16200000
         LCLA  &FNUM                                                    16800000
         LCLB  &F(8)                                                    17400000
.*                                                                      18000000
.*       IF NO FLAG IS GIVEN, DEFAULT TO CP                             18600000
.*                                                                      19200000
         AIF   (T'&FLAG EQ 'O').SET1                                    19800000
.*                                                                      20400000
.*       LOOP THRU THE FLAG POSSIBILITIES AND SET SOME BITS             21000000
.*                                                                      21600000
&FNUM    SETA  N'&FLAG   NUMBER OF FLAG SETTINGS                        22200000
.LOOP1   AIF   ('&FLAG(&FNUM)' NE 'CP').LOOP2                           22800000
&F(8)    SETB  1                                                        23400000
.LOOP2   AIF   ('&FLAG(&FNUM)' NE 'TMP').LOOP3                          24000000
&F(7)    SETB  1                                                        24600000
.LOOP3   ANOP                                                           25200000
&FNUM    SETA  &FNUM-1                                                  25800000
         AIF   (&FNUM NE 0).LOOP1                                       26400000
         AIF   ('&TYPE' EQ 'C').SETTC      ARE WE CHANGING AN OLD BLOCK 26410000
         AIF   (&F(8) EQ 0 AND &F(7) EQ 0).WHOKNOW  BAD FLAG            26420000
.SETTC   AIF   (&F(8) EQ 1 AND &F(7) EQ 1).BADFLAG  CANT BE BOTH        26450000
&FNUM    SETA  &F(7)*2+&F(8)                                            27000000
         AGO   .SET                                                     27600000
.SET1    ANOP                                                           28200000
&FNUM    SETA  1        DEFAULT TO CP                                   28800000
.SET     ANOP                                                           29400000
.*                                                                      30000000
.*             IF ITS LIST FORM, GO CHECK ITS REQUIREMENTS              30600000
.*                                                                      31200000
         AIF   ('&MF' EQ 'L').MFL                                       31800000
.*                                                                      31850000
.*   EXECUTE FORM OF MACRO                                              31900000
.*                                                                      31950000
         AIF   ('&TYPE' EQ 'C').TYPEC   CHANGING PREVIOUS               32000000
         AIF   (T'&PSEUDO EQ 'O').NOPS   NEED PSEUDO PARM               33000000
         AIF   (&F(7) EQ 1).ADDR  DONT NEED LIST IF TMP SPECIFIED       33050000
         AIF   (T'&LIST EQ 'O').NOLST    NEED A LIST                    33600000
.ADDR    AIF   (T'&ADDR EQ 'O').NOADDR  NEED AN ADDRESS                 34200000
         SPACE 1                                                        34800000
.*                                                                      35400000
.*             CHECK FOR SOME THINGS BEING IN REGISTERS                 36000000
.*                                                                      36600000
         AIF   ('&ADDR'(1,1) EQ '(').REGAD   ADDRESS IN REG             37200000
         AIF   (T'&LIST EQ 'O').NLIST  MAY NOT HAVE A LIST              38400000
         AIF   ('&LIST'(1,1) EQ '(').RLIST  LIST IN REG                 38450000
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS            39000000
         AGO   .NLIST1             BYPASS                               39600000
.NLIST   ANOP                                                           40200000
&NAME    LA    15,0                LIST OMITTED                         40800000
.NLIST1  ST    15,&ADDR            STORE LIST POINTER                   41400000
         AGO   .RLIST1                                                  42000000
.RLIST   ANOP                                                           42600000
&NAME    ST    &LIST(1),&ADDR      LIST OF CODES                        43200000
.RLIST1  MVI   &ADDR,&FNUM         STORE FLAG                           43800000
         AIF   ('&PSEUDO'(1,1) EQ '(').RPS   PSEUDO IN REG              45050000
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE                 45600000
         ST    15,&ADDR+4          STORE PSEUDO POINTER                 47400000
         AGO   .END                                                     48000000
.RPS     ST    &PSEUDO(1),&ADDR+4  STORE PSEUDO RETRY ROUTINE           48600000
.END     SPACE 1                                                        49200000
         MEXIT                                                          49800000
.REGAD   AIF   (T'&LIST EQ 'O').NLIST2  NEED NOT HAVE IT                51000000
         AIF   ('&LIST'(1,1) EQ '(').REGAD1   LIST IN A REG             51050000
&NAME    LA    15,&LIST            LIST OF CODES OR POINTERS            51600000
         AGO   .NLIST3                                                  52200000
.NLIST2  ANOP                                                           52800000
&NAME    LA    15,0                LIST OMITTED                         53400000
.NLIST3  ST    15,0(&ADDR(1))      STORE LIST POINTER                   54000000
         AGO   .REGAD2                                                  54600000
.REGAD1  ANOP                                                           55200000
&NAME    ST    &LIST(1),0(&ADDR(1))  LIST OF CODES                      55800000
.REGAD2  MVI   0(&ADDR(1)),&FNUM   STORE FLAG BYTE                      56400000
         AIF   ('&PSEUDO'(1,1) EQ '(').REGAD3                           57650000
         LA    15,&PSEUDO          PSEUDO RETRY ROUTINE                 58200000
         ST    15,4(&ADDR(1))      STORE PSEUDO POINTER                 60000000
         AGO   .END                                                     60600000
.REGAD3  ST    &PSEUDO(1),4(&ADDR(1))  STORE PSEUDO RETRY ROUTINE       61200000
         AGO   .END                                                     61800000
.*                                                                      61850000
.*      CHANGING PREVIOUS STAEPARM  TYPE=C                              61900000
.*                                                                      61950000
.TYPEC   AIF   (T'&ADDR EQ 'O').NOADDR      NEED AN ADDRESS             62000000
         AIF   ('&ADDR'(1,1) EQ '(').REGTC  ADDRESS IN REG              62050000
         AIF   (T'&LIST EQ 'O' AND T'&FLAG EQ 'O').PSEUTC  ONLY PSEUDO  62100000
         AIF   (T'&LIST NE 'O' AND T'&FLAG NE 'O').BOTHTC   BOTH GIVEN  62150000
         AIF   (T'&LIST EQ 'O').FLAGTC      FLAG ONLY                   62200000
&NAME    IC    15,&ADDR            SAVE FLAG BYTE                       62250000
         AIF   ('&LIST'(1,1) EQ '(').LSTTCR  LIST IN REG                62300000
         LA    14,&LIST            NEW LIST ADDRESS                     62350000
         ST    14,&ADDR            STORE IT                             62360000
.LSTTCR1 STC   15,&ADDR            RESTORE FLAG                         62370000
         AGO   .PSEUTC1            GO ON                                62380000
.LSTTCR  ST    &LIST(1),&ADDR      STORE NEW LIST ADDRESS               62390000
         AGO   .LSTTCR1                                                 62392000
.FLAGTC  ANOP                                                           62394000
&NAME    MVI   &ADDR,&FNUM         STORE NEW FLAG                       62396000
         AGO   .PSEUTC1                                                 62398000
.BOTHTC  AIF   ('&LIST'(1,1) EQ '(').BOTHTCR  LIST IN REG               62398400
&NAME    LA    14,&LIST            NEW LIST ADDRESS                     62398800
         ST    14,&ADDR            STORE IT                             62399200
.BOTHTC1 MVI   &ADDR,&FNUM         NEW FLAG BYTE                        62399600
         AGO   .PSEUTC1                                                 62399700
.BOTHTCR ANOP                                                           62412200
&NAME    ST    &LIST(1),&ADDR      STORE NEW LIST ADDRESS               62422200
         AGO   .BOTHTC1                                                 62424200
.PSEUTC  AIF   (T'&PSEUDO EQ 'O').WHY  EVERYTHING MISSING               62424600
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEUTCR  PSEUDO IN REG           62424900
&NAME    LA    15,&PSEUDO          NEW PSEUDO POINTER                   62437400
         ST    15,&ADDR+4          STORE IT                             62449900
         AGO   .END                                                     62459900
.PSEUTCR ANOP                                                           62469900
&NAME    ST    &PSEUDO(1),&ADDR+4  STORE NEW PSEUDO POINTER             62479900
         AGO   .END                                                     62489900
.PSEUTC1 AIF   (T'&PSEUDO EQ 'O').END  PSEUDO OMITTED                   62491900
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEUTC2  PSEUDO IN REG           62493900
         LA    15,&PSEUDO          NEW PSEUDO POINTER                   62495900
         ST    15,&ADDR+4          STORE IT                             62497900
         AGO   .END                                                     62498300
.PSEUTC2 ST    &PSEUDO(1),&ADDR+4  STORE NEW PSEUDO POINTER             62498700
         AGO   .END                                                     62499100
.REGTC   AIF   (T'&LIST EQ 'O' AND T'&FLAG EQ 'O').PSEURC  ONLY PSEUDO  62499500
         AIF   (T'&LIST NE 'O' AND T'&FLAG NE 'O').BOTHRC  BOTH GIVEN   62499600
         AIF   (T'&LIST EQ 'O').FLAGRC  FLAG ONLY                       62499700
&NAME    IC    15,0(&ADDR(1))      SAVE FLAG BYTE                       62499800
         AIF   ('&LIST'(1,1) EQ '(').LSTRCR  LIST IN REG                62549800
         LA    14,&LIST            NEW LIST ADDRESS                     62559800
         ST    14,0(&ADDR(1))      STORE IT                             62569800
.LSTRCR1 STC   15,0(&ADDR(1))      RESTORE FLAG                         62579800
         AGO   .PSEURC1            GO ON                                62589800
.LSTRCR  ST    &LIST(1),0(&ADDR(1))  STORE NEW LIST ADDRESS             62591800
         AGO   .LSTRCR1                                                 62593800
.FLAGRC  ANOP                                                           62595800
&NAME    MVI   0(&ADDR(1)),&FNUM   STORE NEW FLAG                       62597800
         AGO   .PSEURC1  ONWARD                                         62598200
.BOTHRC  AIF   ('&LIST'(1,1) EQ '(').BOTHRCR  LIST IN REG               62598600
&NAME    LA    14,&LIST            NEW LIST ADDRESS                     62599000
         ST    14,0(&ADDR(1))      STORE IT                             62599400
.BOTHRC1 MVI   0(&ADDR(1)),&FNUM   NEW FLAG BYTE                        62599500
         AGO   .PSEURC1                                                 62599600
.BOTHRCR ANOP                                                           62616300
&NAME    ST    &LIST(1),0(&ADDR(1)) STORE NEW LIST POINTER              62626300
         AGO   .BOTHRC1                                                 62628300
.PSEURC  AIF   (T'&PSEUDO EQ 'O').WHY  EVERYTHING MISSING               62630300
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEURCR  PSEUDO IN REG           62633000
&NAME    LA    15,&PSEUDO          NEW PSEUDO POINTER                   62649700
         ST    15,4(&ADDR(1))      STORE IT                             62659700
         AGO   .END                                                     62669700
.PSEURCR ANOP                                                           62679700
&NAME    ST    &PSEUDO(1),4(&ADDR(1))  STORE NEW PSEUDO POINTER         62689700
         AGO   .END                                                     62691700
.PSEURC1 AIF   (T'&PSEUDO EQ 'O').END  PSEUDO OMITTED                   62693700
         AIF   ('&PSEUDO'(1,1) EQ '(').PSEURC2  PSEUDO IN REG           62695700
         LA    15,&PSEUDO          NEW PSEUDO POINTER                   62697700
         ST    15,4(&ADDR(1))      STORE IT                             62698100
         AGO   .END                                                     62698500
.PSEURC2 ST    &PSEUDO(1),4(&ADDR(1))  STORE NEW PSEUDO POINTER         62698900
         AGO   .END                                                     62699300
.*                                                                      62699700
.*                            LIST FORM                                 63000000
.*                                                                      63600000
.*       NEED EITHER A NAME OR AN ADDR FOR MAKING A NAME FOR THIS       64200000
.*                                                                      64800000
.MFL     AIF   (T'&NAME EQ 'O' AND T'&ADDR EQ 'O').BOTH                 65400000
.*                                                                      66000000
.*             THE NAME WILL OVER-RIDE THE ADDR PARAMETER               66600000
.*                                                                      67200000
         AIF   (T'&ADDR EQ 'O').NAME                                    67800000
         AIF   (T'&NAME NE 'O').NAME                                    68400000
.*                                                                      69000000
.*             IF ADDR IS REG-FORM, NO GOOD                             69600000
.*                                                                      70200000
         AIF   ('&ADDR'(1,1) EQ '(').BADADR                             70800000
&ADDR    DS    0F                  FULLWORD ALIGNMENT                   71400000
         AGO   .DCNUM                                                   72000000
.NAME    ANOP                                                           72600000
&NAME    DS    0F                  FULLWORD ALIGNMENT                   73200000
.DCNUM   AIF   (T'&FLAG EQ 'O').DCNUM1     NEED NOT AHVE A FLAG         73250000
         DC    AL1(&FNUM)          FLAG BYTE                            73800000
         AGO   .DCNUM2      BYPASS                                      73850000
.DCNUM1  DC    AL1(0)              FLAG BYTE                            73900000
.DCNUM2  AIF   (T'&LIST EQ 'O').MFL1                                    74400000
         DC    AL3(&LIST)          POINTER TO CODES                     75000000
         AGO   .MFL2                                                    75600000
.MFL1    DC    AL3(0)              POINTER TO CODES                     76200000
.MFL2    AIF   (T'&PSEUDO EQ 'O').MFL3                                  76800000
         DC    A(&PSEUDO)          PSEUDO RETRY ROUTINE                 77400000
         AGO   .END                                                     78000000
.MFL3    DC    A(0)                PSEUDO RETRY ROUTINE                 78600000
         AGO   .END                                                     79200000
.NOPS    MNOTE 8,'PSEUDO PARAMETER OMITTED'                             79800000
         SPACE 1                                                        80400000
         MEXIT                                                          81000000
.BADFLAG MNOTE  8,'CANT HAVE BOTH FLAG=CP AND TMP'                      81050000
         SPACE 1                                                        81100000
         MEXIT                                                          81150000
.NOLST   MNOTE 8,'LIST PARAMETER OMITTED'                               81600000
         SPACE 1                                                        82200000
         MEXIT                                                          82800000
.NOADDR  MNOTE 8,'ADDR PARAMETER OMITTED'                               83400000
         SPACE 1                                                        84000000
         MEXIT                                                          84600000
.BOTH    MNOTE 8,'BOTH ADDR AND NAME PARAMETERS OMITTED'                85200000
         SPACE 1                                                        85800000
         MEXIT                                                          86400000
.BADADR  MNOTE 8,'CANNOT NAME STAEPARM WITH A REGISTER'                 87000000
         SPACE 1                                                        87600000
         MEXIT                                                          88200000
.WHOKNOW MNOTE 8,'UNKNOWN FLAG TYPE &FLAG'                              88250000
         SPACE 1                                                        88300000
         MEXIT                                                          88350000
.WHY     MNOTE 8,'NO OPERANDS ON TYPE=C'                                88400000
         SPACE 1                                                        88450000
         MEXIT                                                          88500000
         MEND                                                           88800000
