         MACRO                                                          02000001
&NAME    ENTR  &SCI1=,&SCIERR=,&PGMLEN=                                 04000001
         GBLA  &DISP               GLOBAL FOR INSTRUCTION DISPLACEMENT  06000001
         GBLC  &LBL1               GLOBAL FOR START SCI PGM             08000001
         LCLA  &PARM                                                    10000001
         LCLB  &QUITSW                                                  12000001
&LBL1    SETC  'IHB0&SYSNDX'       START OF SCI PGM OR SUBROUTINE       14000001
         AIF   ('&NAME' EQ '').ENT000 LABEL SPECIFIED                   16000001
&LBL1    SETC  '&NAME'             YES,SET LABEL NAME                   18000001
.ENT000  AIF   ('&PGMLEN' EQ '').ENT020 PGMLEN PARM PRESENT?            20000001
         AIF   (T'&PGMLEN NE 'N').ERR030 SELF-DEFINING TERM             22000001
&DISP    SETA  &PGMLEN+128         DISPLACEMENT FOR SCI INSTRUCTIONS    24000001
         AIF   (&DISP LE 128).ERR030 VERIFY PGMLEN PARAMETER            26000001
.ENT010  AIF   ('&SCI1' NE '').ERR010   PGMLEN ONLY?                    28000001
         AIF   ('&SCIERR' NE '').ERR010 PGMLEN ONLY?                    30000001
         AIF   (&QUITSW).FINI      IF ERROR FOUND,GO TO MACRO END       32000001
&LBL1    EQU   *                   START OF SCI SUBROUTINE              34000001
         MEXIT                                                          36000001
.ENT020  AIF   (T'&SCIERR EQ 'O').ERR020 TEST FOR SCIERR OMITTED        38000001
&DISP    SETA  128                 SET SCI INSTRUCTION DISPLACEMENT     40000001
&LBL1    DC    CL8'ENTRMACR'       SCI PROGRAM NAME                     42000001
         DC    AL2(0)              RESERVED                             44000001
         DC    XL6'00'             SCI ERROR DATA FIELD                 46000001
         AIF   (T'&SCI1 EQ 'O').ENT030 TEST SCI1 OMITTED                48000001
         AIF   (T'&SCI1 EQ 'N').ENT040 EXPLICIT ADDRESS FOR SCI1        50000001
         DC    AL2(&SCI1-&LBL1+128) FIRST EXECUTABLE SCI ADDRESS        52000001
         AGO   .ENT050             GO CHECK SCIERR                      54000001
.ENT030  DC    AL2(160)            FIRST EXECUTABLE SCI ADDRESS         56000001
         AGO   .ENT050             GO CHECK SCIERR PARAMETER            58000001
.ENT040  ANOP                                                           60000001
&PARM    SETA  &SCI1               DECIMAL ADDRESS VALUE FOR SCI1       62000001
         DC    AL2(&PARM)          FIRST EXECUTABLE SCI ADDRESS         64000001
.ENT050  AIF   (T'&SCIERR EQ 'N').ENT060 EXPLICIT ADDRESS FOR SCIERR    66000001
         DC    AL2(&SCIERR-&LBL1+128) SCI ERROR RTN ADDRESS             68000001
         AGO   .ENT070             CONTINUE MACRO EXPANSION             70000001
.ENT060  ANOP                                                           72000001
&PARM    SETA  &SCIERR             SCIERR ADDRESS                       74000001
         DC    AL2(&PARM)          SCI ERROR RTN ADDRESS                76000001
.ENT070  DC    XL12'00'            RESERVED                             78000001
         MEXIT                                                          80000001
.ERR010  MNOTE 12,'***  IHB405  MACRO CONTAINS INCONSISTENT OPERANDS'   82000001
         MEXIT                                                          84000001
.ERR020  MNOTE 12,'***  IHB412  SCIERR OPERAND REQ''D- NOT SPECIFIED'   86000001
         MEXIT                                                          88000001
.ERR030  MNOTE 12,'***  IHB413  INVALID PGMLEN OPERAND SPECIFIED'       90000001
&QUITSW  SETB  1                   SET MACRO TERMINATION SWITCH         92000001
         AGO   .ENT010             CONTINUE CHECKING MACRO OPERANDS     94000001
.FINI    MEND                                                           96000001
