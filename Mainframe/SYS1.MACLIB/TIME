*%/*                                                                    01070002
         MACRO                                                          01150002
&NAME    TIME  &UNIT,&ADDR,        Time and date format                *01170800
               &ZONE=,             Time and date type                  *01191600
               &ERRET=,            Error routine address               *01212400
               &LINKAGE=,          SVC or PC                       @D2A*01233200
               &DATETYPE=,         Date format for LINKAGE=SYSTEM  @D3A*01254000
               &MF=                Macro form                      @D2A 01274800
         AGO   .SKIP                                                 */ 01295600
*/****  START OF SPECIFICATIONS  *************************************/ 01316400
*/*                                                                  */ 01337200
*/*01*  MACRO NAME = TIME                                            */ 01358000
*/*                                                                  */ 01378800
*/*01*  DESCRIPTIVE NAME = TIME Macro                                */ 01399600
*/*                                                                  */ 01420400
*/*01*  PROPRIETARY STATEMENT =                                      */ 01441200
*/*                                                                  */ 01545200
*/*    LICENSED MATERIALS - PROPERTY OF IBM                          */ 01552100
*/*    THIS MODULE IS "RESTRICTED MATERIALS OF IBM"                  */ 01559000
*/*    5647-A01 (C) COPYRIGHT IBM CORP. 1990, 2000                   */ 01565900
*/*                                                                  */ 01572800
*/*01*  STATUS =  HBB7703                                            */ 01576900
*/*                                                                  */ 01581100
*/*01* EXTERNAL CLASSIFICATION:                                      */ 01582500
*/*02*         GUPI:BASE                                             */ 01583900
*/*01* END OF EXTERNAL CLASSIFICATION:                               */ 01585300
*/*                                                                  */ 01586800
*/*01*  FUNCTION =                                                   */ 01607600
*/*       Returns the time of day and date. The format may be        */ 01628400
*/*       local or GMT time.                                         */ 01649200
*/*                                                                  */ 01670000
*/*02*    OPERATION =                                                */ 01690800
*/*         The TIME macro parses the keywords and invokes the       */ 01711600
*/*         TIME service routine (IEAVRT01) via SVC(11) or           */ 01732400
*/*         IEATTIME via PC.                                         */ 01753200
*/*                                                                  */ 01774000
*/*       SVC Invocations                                            */ 01794800
*/*         The time of day is returned in R0 for DEC, BIN, and      */ 01815600
*/*         TU. For MIC and STCK, the time of day is returned        */ 01836400
*/*         in the specified address.                                */ 01857200
*/*         The date is returned in R1 as packed decimal in the      */ 01878000
*/*         form 0CYYDDDF, where:                                    */ 01898800
*/*              0C  - is the century (00= 1900-1999, 01= 2000-2099) */ 01919600
*/*              YY  - is the last two digits of the year            */ 01940400
*/*              DDD - is the day of the year                        */ 01961200
*/*              F   - allows for unpacking                          */ 01982000
*/*                                                                  */ 02002800
*/*       PC Invocations                                             */ 02023600
*/*         The time of day is returned to the first two words of    */ 02044400
*/*         a user specified fourword area.  The date is returned    */ 02065200
*/*         to the third word in decimal format (NOT packed decimal) */ 02086000
*/*         in the forms given below.                                */ 02106800
*/*                                                                  */ 02127600
*/*01*  INVOCATION = (BAL)                                           */ 02148400
*/*                                                                  */ 02169200
*/*        unit       Specifies the time of day format to be         */ 02190000
*/*                        returned.                                 */ 02210800
*/*                DEC   - Decimal (HHMMSSth). (Default)             */ 02231600
*/*                                                                  */ 02252400
*/*                BIN   - Unsigned 32-bit binary number in          */ 02273200
*/*                        hundredths of seconds.                    */ 02294000
*/*                                                                  */ 02314800
*/*                MIC   - Microseconds, where bit 51= 1 microsec.   */ 02335600
*/*                                                                  */ 02356400
*/*                STCK  - Unsigned 64-bit fixed-point number, where */ 02377200
*/*                        bit 51= 1 microsecond (TOD contents).     */ 02398000
*/*                                                                  */ 02403200
*/*                STCKE - Unsigned 128-bit fixed-point number, where*/ 02408400
*/*                        bit 59= 1 microsecond (TOD contents).     */ 02413600
*/*                                                                  */ 02418800
*/*                TU    - Unsigned 32-bit binary number in Timer    */ 02439600
*/*                        Units, where 1 TU= 26.04166 microseconds. */ 02460400
*/*                    Note: TU is NOT valid for LINKAGE=SYSTEM.     */ 02481200
*/*                                                                  */ 02502000
*/*       ,addr       Address of an 8-byte area for MIC and STCK.    */ 02522800
*/*                   For LINKAGE=SYSTEM, the address of a 16-byte   */ 02543600
*/*                   area for ALL formats.                          */ 02564400
*/*                                                                  */ 02585200
*/*          ,ZONE=      Return local, or GMT time and date in       */ 02606000
*/*                      units of DEC, BIN, MIC, or TU.              */ 02626800
*/*                      ZONE must be null when unit is STCK.        */ 02647600
*/*                                                                  */ 02668400
*/*                LT      Local time and date. (Default)            */ 02689200
*/*                                                                  */ 02710000
*/*                GMT     Greenwich mean time and date.             */ 02730800
*/*                                                                  */ 02751600
*/*          ,ERRET=     Error routine address.                      */ 02772400
*/*                      This is an obsolete keyword and is ignored. */ 02793200
*/*                                                                  */ 02814000
*/*          ,LINKAGE=   Method of linking to the service routine.   */ 02834800
*/*                SVC     SVC                                       */ 02855600
*/*                                                                  */ 02876400
*/*                SYSTEM  PC                                        */ 02897200
*/*                                                                  */ 02918000
*/*          ,DATETYPE=   Specifies the date format that the TOD     */ 02938800
*/*                       value is converted to:                     */ 02959600
*/*              YYYYDDD= (Default) Decimal digits of the            */ 02980400
*/*                       form 0YYYYDDD, where:                      */ 03001200
*/*                       YYYY is the four digits of the year        */ 03022000
*/*                       DDD is the day of the year                 */ 03042800
*/*                                                                  */ 03063600
*/*             MMDDYYYY= Decimal digits of the form MMDDYYYY,       */ 03084400
*/*                       where:                                     */ 03105200
*/*                       MM  is the month of the year               */ 03126000
*/*                       DD  is the day of the month                */ 03146800
*/*                       YYYY is the four digits of the year        */ 03167600
*/*                                                                  */ 03188400
*/*             DDMMYYYY= Decimal digits of the form DDMMYYYY,       */ 03209200
*/*                       where:                                     */ 03230000
*/*                       DD  is the day of the month                */ 03250800
*/*                       MM  is the month of the year               */ 03271600
*/*                       YYYY is the four digits of the year        */ 03292400
*/*                                                                  */ 03313200
*/*             YYYYMMDD= Decimal digits of the form YYYYMMDD,       */ 03334000
*/*                       where:                                     */ 03354800
*/*                       YYYY is the four digits of the year        */ 03375600
*/*                       MM  is the month of the year               */ 03396400
*/*                       DD  is the day of the month                */ 03417200
*/*                                                                  */ 03438000
*/*                                                                  */ 03458800
*/*          ,MF=         Macro form.  Default is Standard form.     */ 03479600
*/*                L        Generate a parameter list.               */ 03500400
*/*                                                                  */ 03521200
*/*               (E,addr)  Execute, where addr is the address of    */ 03542000
*/*                         the parameter list to be used.           */ 03562800
*/*                                                                  */ 03583600
*/*                                                                  */ 03604400
*/*01*  INVOCATION = (PLAS)                                          */ 03625200
*/*                        See TIMEP for PLAS input description.     */ 03646000
*/*                                                                  */ 03666800
*/*01*  NOTES =                                                      */ 03687600
*/*                                                                  */ 03708400
*/*02*    RESTRICTIONS = None                                        */ 03729200
*/*                                                                  */ 03750000
*/*02*    DEPENDENCIES = None                                        */ 03770800
*/*                                                                  */ 03791600
*/*03*      CHARACTER-CODE DEPENDENCIES= EBCDIC                      */ 03812400
*/*                                                                  */ 03833200
*/*01*  MODULE TYPE = BIL MACRO                                      */ 03854000
*/*                                                                  */ 03874800
*/*02*    PROCESSOR = ASSEMBLER-H  or  PLAS                          */ 03895600
*/*                                                                  */ 03916400
*/*02*    INVOCATION = See MACRO definition                          */ 03937200
*/*                                                                  */ 03958000
*/*01*  COMPONENT  =  SC1CV (Timer Supervision)                      */ 03978800
*/*                                                                  */ 03999600
*/*01*  DISTRIBUTION LIBRARY = AMACLIB                               */ 04020400
*/*                                                                  */ 04041200
*/**** END OF SPECIFICATIONS *****************************************/ 04062000
*/*                                                                     04082800
*                                                                       04103600
*    CHANGE ACTIVITY =                                                  04124400
*    $L0=        AOS/R2   721003  PZD81 :                               04145200
*    $L1=CLOCK   HBB4410  880411  PD16LD:  ETR support                  04166000
*    $D1=DCR1006 HBB4410  880819  PD16CU:  ETR support                  04186800
*    $D2=DHB1022 HBB4410  890206  PD16H5:  PC entry support             04207600
*    $P1=PHB1085 HBB4410  890206  PD16H5:  Fix flag                     04228400
*    $D3=DHB1031 HBB4410  890313  PD162M:  DATETYPE support             04249200
*    $P2=PEO3587 HBB4410  900226  PD16H5:  Remove ERRET support.        04270000
*    $P3=PEO3805 HBB4410  900226  PD16H5:  Fix mnote level.             04290800
*    $P4=PEO4152 HBB4410  900504  PD16H5:  4 digit year.                04311600
*    $L2=STCKE   HBB6608  980530  B7HGH5:  128 bit TOD support          04322000
*    $P5=PWK0545 HBB6608  981210  B7HGH5:  Reject datetype with STCKE   04327200
*    $P6=PXD0368 HBB7703  990908  U2IAXZ:  For linkage=system in        04328500
*                                          primary ASC mode, zero AR1.  04329800
*                                          FIN APAR OW40591.            04331100
*                                                                       04332400
*********************************************************************** 04353200
.*                                                                      04374000
.SKIP    ANOP ,                                                    @L1A 04394800
*        MACDATE  09/08/99                                              04415600
.*                                                                      04436400
.********************************************************************** 04457200
.*                                                                    * 04478000
.*       Declare GLOBAL and LOCAL variables                           * 04498800
.*                                                                    * 04519600
.********************************************************************** 04540400
.*                                                                      04561200
         LCLA  &DATET         DATETYPE bitstring                   @D3A 04582000
         LCLA  &CTR           Return code flag                          04602800
         LCLB  &B(5)          UNIT flag                            @L2C 04623600
         LCLB  &D(4)          DATETYPE flag                        @D3A 04644400
         LCLB  &IHBERR        Error flag                           @L1A 04665200
         LCLB  &SYSTEM        Linkage type flag                    @D2A 04686000
         LCLC  &AR                                                 @D2A 04706800
         LCLC  &PL                                                 @D2A 04727600
         LCLC  &INAME                                              @D2A 04748400
         LCLC  &ENAME                                              @D2A 04769200
.*                                                                    * 04790000
.********************************************************************** 04810800
.*      Check number of positionals                                   * 04831600
.********************************************************************** 04852400
         AIF   (N'&SYSLIST GE 3).E1      Too many positionals      @L1A 04873200
.EXPOS   ANOP  ,                                                   @L1A 04894000
.*                                                                    * 04914800
.********************************************************************** 04935600
.*      ADDR operand                                                  * 04956400
.********************************************************************** 04977200
         AIF   (('&UNIT' EQ 'DEC' OR '&UNIT' EQ '' OR '&UNIT' EQ 'TU' O*05000002
               R '&UNIT' EQ 'BIN') AND ('&ADDR' NE '' AND '&LINKAGE'   *05050000
               NE 'SYSTEM')).E2                                    @D2C 05100000
         AIF   (('&UNIT' EQ 'MIC' OR '&UNIT' EQ 'STCK' OR '&UNIT' EQ   *05150000
                'STCKE') AND '&ADDR' EQ '' ).E3                    @L2C 05200000
.EXADDR  ANOP  ,                                                   @L1A 05250000
.*                                                                      05300000
.********************************************************************** 05350000
.*       UNIT operand                                                 * 05400000
.********************************************************************** 05450000
&B(1)    SETB  ('&UNIT' EQ 'DEC' OR '&UNIT' EQ '')                      07000002
&B(2)    SETB  ('&UNIT' EQ 'BIN')                                       08000002
&B(3)    SETB  ('&UNIT' EQ 'MIC')                                       09000000
&B(4)    SETB  ('&UNIT' EQ 'STCK')                                      10000002
&B(5)    SETB  ('&UNIT' EQ 'STCKE')                                @L2A 10016600
         AIF   ('&UNIT' EQ 'STCKE' AND '&LINKAGE' NE 'SYSTEM').E22 @L2A 10033200
         AIF   ('&UNIT' EQ 'STCKE' AND '&DATETYPE' NE '').E23      @P5A 10041600
         AIF   ('&UNIT' EQ 'TU').EXUNIT                            @L1C 10050000
&CTR     SETA  &B(2)+2*&B(1)+3*&B(3)+4*&B(4)+5*&B(5) Set UNIT bit  @L2C 10100000
         AIF   (&CTR EQ 0 AND '&LINKAGE' NE 'SYSTEM').E5           @D2C 10150000
         AIF   (&CTR EQ 0 AND '&LINKAGE' EQ 'SYSTEM').E17          @D2A 10200000
.EXUNIT  ANOP  ,                                                   @L1A 10250000
.*                                                                      10300000
.********************************************************************** 10350000
.*       ZONE keyword                                                 * 10400000
.********************************************************************** 10450000
         AIF   ('&ZONE' EQ '').EXZONE                              @L1C 10500000
         AIF   ('&UNIT' EQ 'STCK').E7           ZONE meaningless        10550000
         AIF   ('&UNIT' EQ 'STCKE').E7          ZONE meaningless   @L2A 10575000
         AIF   ('&ZONE' EQ 'LT').EXZONE                            @L1C 10600000
         AIF   ('&ZONE' NE 'GMT').E8            ZONE invalid            10650000
&CTR     SETA  &CTR+128                           GMT FLAG         @L1C 10700000
.EXZONE  ANOP  ,                                                   @L1A 10750000
.*                                                                      10800000
.********************************************************************** 10850000
.*       LINKAGE keyword                                              * 10900000
.********************************************************************** 10950000
         AIF   ('&LINKAGE' EQ 'SVC' OR '&LINKAGE' EQ '').EXLINK    @D2A 11000000
         AIF   ('&LINKAGE' NE 'SYSTEM').E9                         @D2A 11050000
         AGO   .PCLINK                                             @D2A 11100000
&SYSTEM  SETB  1                                                   @D2A 11150000
.EXLINK  ANOP                                                      @D2A 11200000
.********************************************************************** 11250000
.*       MF keyword                                                   * 11300000
.********************************************************************** 11350000
         AIF   ('&MF' NE '').E14                                   @D2A 11400000
.E14CONT ANOP                                                      @D2A 11450000
.*                                                                      11500000
.********************************************************************** 11550000
.*       ERRET keyword                                                * 11600000
.*           This keyword is obsolete.                             @P2A 11650000
.********************************************************************** 11700000
         AIF   ('&ERRET' NE '').E10     ERRET no longer supported @P2C  11750000
.E10CONT  ANOP                                                     @P2C 11800000
.*                                                                      11850000
.********************************************************************** 11900000
.*       DATETYPE keyword                                             * 11950000
.********************************************************************** 12000000
         AIF   ('&DATETYPE' NE '').E19                             @D3A 12050000
.E19CONT ANOP                                                      @D2A 12100000
.*                                                                      12150000
.********************************************************************** 12200000
.*       Generate code to set up SVC                                  * 12250000
.********************************************************************** 12300000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @L1A 12350000
         AIF   (&CTR NE 0).LC                                           12400000
&NAME    SR    1,1                     ZERO 1 TO SPECIFY UNIT           12450000
         AGO   .SVC                                                     12500000
.LC      ANOP                                                           21000002
&NAME    LA    1,&CTR.(0,0)            LOAD 1 TO SPECIFY UNIT           22000000
         AIF   ('&UNIT' NE 'MIC' AND '&UNIT' NE 'STCK').SVC             23000002
         AIF   ('&ADDR'(1,1) EQ '(').REG                                25000002
         LA    0,&ADDR                                                  26000000
         AGO   .SVC                                                     27000000
.REG     ANOP                                                           28000002
         AIF   ('&ADDR' EQ '(0)').SVC                                   29000002
         LR    0,&ADDR(1)                                               30000002
.********************************************************************** 30050000
.*       Issue SVC                                                    * 30100000
.********************************************************************** 30150000
.SVC     SVC   11                      ISSUE TIME SVC                   30200000
.DONE    MEXIT                                                          42000002
.*                                                                      42050000
.********************************************************************** 42100000
.*       Validate PC linkage keywords and options                     * 42150000
.********************************************************************** 42200000
.PCLINK  ANOP                                                      @D2A 42250000
         AIF   ('&ERRET' NE '').E6      ERRET not supported on PC  @D2A 42300000
.E6CONT  ANOP                                                      @D2A 42350000
         AIF   ('&UNIT' EQ 'TU').E4     TU not supported on PC     @D2A 42400000
.E4CONT  ANOP                                                      @D2A 42450000
         AIF   ('&ADDR' EQ '' AND '&MF' NE 'L').E18  Except for    @D2A 42500000
.*                                      list form, address required     42550000
.E18CONT ANOP                                                      @D2A 42600000
         AIF   (N'&DATETYPE GT 1).E20  Too many sublists           @D3A 42650000
&D(1)    SETB  ('&DATETYPE' EQ 'MMDDYYYY')                         @P4C 42700000
&D(2)    SETB  ('&DATETYPE' EQ 'DDMMYYYY')                         @P4C 42750000
&D(3)    SETB  ('&DATETYPE' EQ 'YYYYMMDD')                         @P4C 42800000
&D(4)    SETB  ('&DATETYPE' EQ 'YYYYDDD' OR '&DATETYPE' EQ '')     @P4C 42850000
         AIF   (&D(1) EQ 0 AND &D(2) EQ 0 AND &D(3) EQ 0 AND &D(4) EQ 0X42900000
               ).E21                    Invalid DATETYPE specified @D3A 42950000
&DATET   SETA  &D(1)+&D(2)*2+&D(3)*3    Create DATATYPE bitstring  @D3A 43000000
.********************************************************************** 43050000
.*       Validate MF keywords and options                             * 43100000
.********************************************************************** 43150000
.E21CONT ANOP                                                      @D2A 43200000
         AIF   ('&MF' EQ '').STD        Default is Standard form   @D2A 43250000
         AIF   ('&MF' EQ 'L').LIST      List form                  @D2A 43300000
         AIF   (N'&MF GT 2).E11         Too many sublists          @D2A 43350000
         AIF   ('&MF(1)' NE 'E').E12    Ensure E was specified     @D2A 43400000
         AIF   ('&MF(2)' EQ '').E13     Must be an address         @D2A 43450000
         AIF   (N'&MF(2) GT 2).E16      Too many addresses         @D2A 43500000
.ERRCHK  ANOP                                                      @D2A 43550000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 43600000
.********************************************************************** 43650000
.*       Generate PC linkage to service routine                       * 43700000
.********************************************************************** 43750000
         AIF   ('&MF(2)'(1,1) EQ '(').ISAREG                       @D2A 43800000
&NAME    LAE   1,&MF(2)                PARAMETER LIST ADDRESS      @D2A 43850000
         AGO   .CONT                                               @D2A 43900000
.ISAREG  ANOP                                                      @P6C 43950000
&PL      SETC  '&MF(2)'(2,K'&MF(2)-2)  EXTRACT REGISTER NAME       @D2A 44000000
&NAME    LAE   1,0(0,&PL)              PARAMETER LIST ADDRESS      @D2A 44050000
.CONT    ANOP                                                      @D2A 44100000
         XC    0(28,1),0(1)            CLEAR PARAMETER LIST        @D2A 44150000
.COMMON  MVI   3(1),&CTR               TIME FLAGS INTO P LIST      @D2A 44200000
         MVI   2(1),&DATET             DATE FLAGS INTO P LIST      @D3A 44250000
         L     14,16(0,0)              CVT ADDRESS                 @D2A 44300000
         L     14,772(14,0)            SFT ADDRESS                 @D2A 44350000
         L     14,304(14,0)            LX/EX FOR SERVICE ROUTINE   @D2A 44400000
         PC    0(14)                                               @D2A 44450000
         AIF   ('&ADDR'(1,1) EQ '(').REGAD   IN A REGISTER         @D2A 44500000
         LAE   14,&ADDR                ADDRESS OF USER'S AREA      @D2A 44550000
         AGO   .MOVE                                               @D2A 44600000
.REGAD   ANOP                                                      @D2A 44650000
&AR      SETC  '&ADDR'(2,K'&ADDR-2)    EXTRACT REGISTER NAME       @D2A 44700000
         LAE   14,0(0,&AR)             ADDRESS OF USER'S AREA      @D2A 44750000
.MOVE    ANOP                                                      @D2A 44800000
         MVC   0(16,14),12(1)          MOVE OUTPUT FROM PARM LIST  @D2A 44850000
*                                      TO THE USER'S AREA               44900000
         MEXIT                                                     @D2A 44950000
.********************************************************************** 45000000
.*       Generate parameter list for Standard form of macro           * 45050000
.********************************************************************** 45100000
.STD     ANOP                                                      @D2A 45150000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 45200000
&INAME   SETC   'IHB'.'&SYSNDX'.'D'                                @D2A 45250000
&ENAME   SETC   'IHB'.'&SYSNDX'.'W'                                @D2A 45300000
&NAME    DS    0H                                                  @D2A 45350000
         LAE   1,0(0,0)                 ZERO ACCESS REGISTER 1     @D2A 45400000
         LA    1,&INAME                 POINT TO PARAMETER LIST    @D2A 45450000
         B     &ENAME                   BRANCH AROUND LIST         @D2A 45500000
&INAME   DS    0F                                                  @D3A 45550000
         DC    FL2'0'                                              @D3C 45600000
         DC    FL1'0'                                              @D3A 45650000
         DC    FL1'0'                                              @D3A 45700000
         DC    2F'0'                                               @D2A 45750000
         DC    2F'0'                                               @D2A 45800000
         DC    2F'0'                                               @D2A 45850000
&ENAME   DS    0H                                                  @D2A 45900000
         AGO    .COMMON                                            @D2A 45950000
.********************************************************************** 46000000
.*       Generate List form parameter list                            * 46050000
.********************************************************************** 46100000
.LIST    ANOP                                                      @D2A 46150000
         AIF   ('&UNIT' NE '' OR '&ZONE' NE '' OR '&ADDR' NE '' OR '&DAX46200000
               TETYPE' NE '').E15                                  @D3C 46250000
.E15CONT ANOP                                                      @D2A 46300000
         AIF   (&IHBERR EQ 1).DONE            Exit if errors       @D2A 46350000
&NAME    DS    0F                                                  @D3A 46400000
         DS    XL28                                                @D3C 46450000
         MEXIT                                                     @D2A 46500000
.*                                                                    * 46550000
.********************************************************************** 46600000
.*                                                                    * 46650000
.*       Error Messages                                               * 46700000
.*                                                                    * 46750000
.********************************************************************** 46800000
.E1      MNOTE 12,' TOO MANY POSITIONAL PARAMETERS - &SYSLIST(3)'  @L1A 46850000
&IHBERR  SETB  1                          Indicate error           @L1A 46900000
         AGO   .EXPOS                     Continue                 @L1A 46950000
.E2      MNOTE 12,' AN ADDRESS WAS SPECIFIED WITH AN OPTION OTHER THAN X47000000
               ''STCK'' OR ''MIC'' WHEN LINKAGE=SYSTEM NOT SPECIFIED '  47050000
&IHBERR  SETB  1                          Indicate error           @L1A 47100000
         AGO   .EXADDR                    Continue                 @L1C 47150000
.E3      MNOTE 12,' AN ADDRESS IS REQUIRED WITH &SYSLIST(1)'       @L1C 47200000
&IHBERR  SETB  1                          Indicate error           @L1A 47250000
         AGO   .EXADDR                    Continue                 @L1C 47300000
.E4      MNOTE 12,' ''TU'' IS NOT VALID WITH LINKAGE=SYSTEM '      @D2A 47350000
&IHBERR  SETB  1                          Indicate error           @D2A 47400000
         AGO   .E4CONT                    Continue                 @D2A 47450000
.E5      MNOTE 12,' &UNIT IS NOT VALID. UNIT MUST BE ''DEC'' ''BIN'' ''X47500000
               TU'' ''MIC'' OR ''STCK'' '                          @L1C 47550000
&IHBERR  SETB  1                          Indicate error           @L1A 47600000
         AGO   .EXUNIT                    Continue                 @L1C 47650000
.E6      MNOTE 12,' ''ERRET'' IS NOT VALID WITH LINKAGE=SYSTEM '   @D2A 47700000
&IHBERR  SETB  1                          Indicate error           @D2A 47750000
         AGO   .E6CONT                    Continue                 @D2A 47800000
.E7      MNOTE  0,' ZONE NOT MEANINGFUL WITH STCK/STCKE--ZONE IGNORED'  47833300
.*                                                                 @L2C 47866600
         AGO   .EXZONE                    Continue                 @L1C 47900000
.E8      MNOTE 12,' ZONE PARAMETER MUST BE ''LT'' OR ''GMT'' '     @D1C 47950000
&IHBERR  SETB  1                          Indicate error           @L1A 48000000
         AGO   .EXZONE                    Continue                 @L1C 48050000
.E9      MNOTE 12,'LINKAGE KEYWORD MUST BE ''SVC'' OR ''SYSTEM'' ' @D2A 48100000
&IHBERR  SETB  1                          Indicate error           @D2A 48150000
         AGO   .EXLINK                    Continue                 @D2A 48200000
.E10     MNOTE 0,' THE ERRET KEYWORD IS NO LONGER SUPPORTED. IT IS IGNOX48250000
               RED.'                                               @P3C 48300000
         AGO   .E10CONT                   Continue                 @P2C 48350000
.E11     MNOTE 12,' TOO MANY SUBLISTS SPECIFIED FOR ''MF'' KEYWORD '    48400000
&IHBERR  SETB  1                          Indicate error           @D2A 48450000
         AGO   .ERRCHK                    Continue                 @D2A 48500000
.E12     MNOTE 12,' MF KEYWORD MUST BE ''L'' OR ''E'' '            @D2A 48550000
&IHBERR  SETB  1                          Indicate error           @D2A 48600000
         AGO   .ERRCHK                    Continue                 @D2A 48650000
.E13     MNOTE 12,' NO PARAMETER LIST ADDRESS SPECIFIED FOR ''MF'' KEYW*48700000
               ORD '                                               @D2A 48750000
&IHBERR  SETB  1                          Indicate error           @D2A 48800000
         AGO   .ERRCHK                    Continue                 @D2A 48850000
.E14     MNOTE 12,' MF KEYWORD IS ONLY VALID WITH LINKAGE=SYSTEM ' @D2A 48900000
&IHBERR  SETB  1                          Indicate error           @D2A 48950000
         AGO   .E14CONT                   Continue                 @D2A 49000000
.E15     MNOTE 12,' UNIT, ADDR, ZONE, AND DATETYPE NOT VALID WITH MF=L X49050000
               '                                                   @D3C 49100000
&IHBERR  SETB  1                          Indicate error           @D2A 49150000
         AGO   .E15CONT                   Continue                 @D2A 49200000
.E16     MNOTE 12,' FOR MF KEYWORD - TOO MANY PARAMETER ADDRESSES SPECI*49250000
                FIED '                                             @D2A 49300000
&IHBERR  SETB  1                          Indicate error           @D2A 49350000
         AGO   .ERRCHK                    Continue                 @D2A 49400000
.E17     MNOTE 12,' &UNIT IS NOT VALID. UNIT MUST BE ''DEC'' ''BIN'' ''X49450000
               MIC'' ''STCK'' OR ''STCKE'' '                       @L2C 49500000
&IHBERR  SETB  1                          Indicate error           @D2A 49550000
         AGO   .EXUNIT                    Continue                 @D2A 49600000
.E18     MNOTE 12,' ADDRESS IS REQUIRED FOR LINKAGE=SYSTEM '       @D2A 49650000
&IHBERR  SETB  1                          Indicate error           @D2A 49700000
         AGO   .E18CONT                   Continue                 @D2A 49750000
.E19     MNOTE 12,' ''DATETYPE'' IS ONLY VALID WITH LINKAGE=SYSTEM '   X49800000
                                                                   @D3C 49850000
&IHBERR  SETB  1                          Indicate error           @D3A 49900000
         AGO   .E19CONT                   Continue                 @D3A 49950000
.E20     MNOTE 12,' TOO MANY SUBLISTS SPECIFIED FOR ''DATETYPE'' KEYWORX50000000
               D '                                                 @D3A 50050000
&IHBERR  SETB  1                          Indicate error           @D3A 50100000
         AGO   .E21CONT                   Continue                 @D3A 50150000
.E21     MNOTE 12,' DATETYPE KEYWORD MUST BE ''YYYYDDD'' ''MMDDYYYY'' 'X50200000
               'DDMMYYYY'' OR ''YYYYMMDD'' '                       @P4C 50250000
&IHBERR  SETB  1                          Indicate error           @D3A 50300000
         AGO   .E21CONT                   Continue                 @D3A 50350000
.E22     MNOTE 12,' ''STCKE'' IS ONLY VALID WITH LINKAGE=SYSTEM'   @L2A 50400000
&IHBERR  SETB  1                          Indicate error           @L2A 50450000
         AGO   .EXUNIT                    Continue                 @L2A 50500000
.E23     MNOTE 12,' DATETYPE NOT ALLOWED WITH STCKE'               @P5A 50550000
&IHBERR  SETB  1                          Indicate error           @P5A 50600000
.*                                                                 @P5A 50650000
         AGO   .EXUNIT                    Continue                 @P5A 50700000
         MEND                                                           53000002
**/                                                                     53050002
*TIME:   MACRO                                                          53100002
*        KEYS(ZONE,ERRET,LINKAGE,MF,DATETYPE);                /* @P1C*/ 53150000
*        ANS('?'||MACLABEL||'TIMEP '||MACLIST||MACKEYS||';');           53200002
*%END;                                                                  53250002
