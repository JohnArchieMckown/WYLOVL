         MACRO                                                          00050000
         IST6MCRJ &CONVID=,&SENSE=,&BRANCH=,&ECB=,&EXIT=,&OPTCD=,      X00100000
               &QUALIFY=,&SESSID=,&SESSIDL=,&DEACTYP=,&CGID=,          X00150000
               &INVALP1=,&INVALP2=,&VTRINA=,&VTRINL=,&VTROUTA=,        X00183300
               &VTROUTL=                                           @D1C 00216600
.*                                                                      00250000
.* /***** START OF SPECIFICATIONS ************************************/ 00300000
.* /*                                                                */ 00350000
.* /*  MACRO NAME: IST6MCRJ                                          */ 00400000
.* /*                                                                */ 00450000
.* /*  DESCRIPTIVE NAME: CONTROL = REJECT PROCESSOR                  */ 00500000
.* /*                                                                */ 00550000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00600000
.* /*                                                                */ 00650000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00700000
.* /*                                                                */ 00750000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1988, 1994.       */ 00800000
.* /*             ALL RIGHTS RESERVED.                               */ 00850000
.* /*                                                                */ 00900000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00950000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01000000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01050000
.* /*                                                                */ 01100000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01150000
.* /*                                                                */ 01200000
.* /*  FUNCTION:                                                     */ 01250000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01300000
.* /*      CONTROL=REJECT PARAMETER ON THE APPCCMD MACRO.            */ 01350000
.* /*                                                                */ 01400000
.* /*  NOTES:                                                        */ 01450000
.* /*                                                                */ 01500000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01550000
.* /*                                                                */ 01600000
.* /*  CREATED BY:                                                   */ 01650000
.* /*                                                                */ 01700000
.* /*  INTERNAL MACROS:                                              */ 01750000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCUI                       */ 01800000
.* /*                                                                */ 01850000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01900000
.* /*                                                                */ 01950000
.* /*  CHANGE ACTIVITY:                                              */ 02000000
.* /*    05/27/86 - ZERO OUT THE QUALIFY FIELD               @P030472*/ 02050000
.* /*                                                                */ 02100000
.* /* $MAC(IST6MCRJ),COMP(API),PROD(VTAM): CONTROL = REJECT PROCESSOR*/ 02150000
.* /*                                                                */ 02200000
.* /* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS          */ 02250000
.* /* ---- -------- ------- ------ ------  ------------------------- */ 02300000
.* /* $F1= FS1@@CLD HVT3303 880425 255471: SYNCPOINT SUPPORT         */ 02350000
.* /* $Q1= P062052  HVT3303 881025 255471: SKIP THE MVC OF SESSID    */ 02400000
.* /*                                      IF SESSIDL EQUALS 0       */ 02450000
.* /* $Q2= P063278  HVT3303 881130 255471: IMPLEMENT SESSION         */ 02500000
.* /*                                      DEACTIVATION DELAYED      */ 02550000
.* /*                                      FUNCTION                  */ 02600000
.* /* $Q3= P063295  HVT3303 881217 991843: ALLOW REGISTER NOTATION   */ 02650000
.* /*                                      FOR SESSID AND SESSIDL    */ 02700000
.* /* $Q4= P064302  HVT3303 890113 255471: ALLOW REGISTER NOTATION   */ 02750000
.* /*                                      FOR DEACTYP AND ISSUE     */ 02800000
.* /*                                      ERROR MESSAGE IF INVALID  */ 02850000
.* /*                                      PARAMETER ISSUED FOR      */ 02900000
.* /*                                      QUALIFY=CONV              */ 02950000
.* /* $BB= ACC@@AMS HVT3401 891025 615520: PL/AS CONVERSION          */ 03000000
.* /* $T1= P097994  HATAPPN 911002 692313: DO NOT DEFAULT QUALIFY    */ 03050000
.* /* $T2= P098263  HATAPPN 911011 692313: Bad fix P097994           */ 03100000
.* /* $KF= S4N@@CLD HATAPPN 910326 692313: CP-CP SESSIONS            */ 03150000
.* /* $I1= FF@@@MST APPCFDX 920302 879315: APPC Full-Duplex support  */ 03200000
.* /* $01= OY55939  HATAPPN 921014 495139: INITIALIZATION OF REG15   */ 03250000
.* /* $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services     */ 03300000
.* /* $L2= KA@DR006 APPCRR  940713 576146: Vector Format             */ 03350000
.* /* $D1= ID@@@MST FPAPI   950316 143576: Fast Path API for APPC    */ 03375000
.* /*                                                                */ 03400000
.* /***** END OF SPECIFICATIONS **************************************/ 03450000
.*                                                                      03500000
.* A-000000-999999                                             @R495810 03550000
.*                                                                      03600000
         GBLB  &ISTGEN                                                  03650000
         GBLB  &ISTRJ                                                   03700000
         AIF   (&ISTRJ EQ 1).RJINVKW                                    03750000
&ISTRJ   SETB  1                                                        03800000
.*                                     CHECK INVALID APPCCMD       @F1A 03850000
.RJINVKW ANOP                                                           03900000
         IST6MCUC REJECT,CONTROL,INVALKW=&INVALP1                  @I1C 03950000
         IST6MCUC REJECT,CONTROL,INVALKW=&INVALP2                  @I1A 04000000
.*                                     SET CONVID IN APPC AREA     @F1A 04050000
.RJID    ANOP                                                           04100000
         AIF   ('&CONVID' EQ '').RJCGID                                 04150000
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X04200000
               KW=CONVID                                                04250000
.*                                     SET CGID IN APPC AREA       @KFA 04300000
.RJCGID  ANOP                                                           04350000
         AIF   ('&CGID' EQ '').RJSENSE                                  04400000
         IST6MCUA KWVAL=&CGID,LOC=RPL6CGID-ISTRPL6X,BASEREG=14,        X04450000
               KW=CGID                                                  04500000
.*                                     SET SENSE IN APPC AREA      @F1A 04550000
.RJSENSE ANOP                                                           04600000
         AIF   ('&SENSE' EQ '').RJVECT                             @L1C 04650000
         IST6MCUI KW=SENSE,KWVAL=&SENSE,LEN=4,                         X04700000
               LOC=RPL6SNSO-ISTRPL6X,BASEREG=14                         04750000
.RJVECT  ANOP                          ENSURE BOTH PARMS ARE CODED @L1A 04800000
.*                                     OR BOTH ARE NOT CODED       @L1A 04850000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).RJVOUT    @D1C 04900000
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).RJERVI     @D1C 04950000
.*                                     STORE VECTOR AREA INPUT ADDRESS  05050000
.*                                                                 @L1A 05100000
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X05150000
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C 05200000
.*                                     STORE VECTOR AREA INPUT LENGTH   05250000
.*                                                                 @L1A 05300000
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X05350000
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C 05370000
         AGO   .RJVOUT                                             @D1C 05390000
.RJERVI  ANOP                                                      @L1A 05410000
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C 05430000
.RJVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED      05450000
.*                                     OR BOTH ARE NOT CODED       @D1A 05470000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).RJCOMKW @D1A 05490000
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).RJERVO   @D1A 05510000
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X05530000
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A 05550000
.*                                     PUT VECTOR AREA INPUT ADDRESS    05570000
.*                                     IN APPC AREA                @D1A 05590000
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X05610000
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A 05630000
.*                                     PUT VECTOR AREA INPUT LENGTH     05650000
.*                                     IN APPC AREA                @D1A 05670000
         AGO   .RJCOMKW                                            @D1A 05690000
.RJERVO  ANOP                                                      @D1A 05710000
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A 05730000
.*                                     CHECK CONMODE PARAMETER     @F1A 05750000
.RJCOMKW ANOP                                                           05800000
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,BRANCH=&BRANCH,     X05850000
               CKCMODE=NO,               DO NOT CHECK CONMODE PARAMETERX05900000
               CKSYN=YES,          ASY/SYN IS VALID FOR OPTCD PARAMETERX05950000
               CKBUFF=NO           BUFFLST/NBUFFLST IS INVALID FOR      06000000
.*                                 OPTCD PARAMETER                      06050000
.*                                     CHECK QUALIFY PARAMETER     @F1A 06100000
.RJQUALI ANOP                                                           06150000
         AIF   ('&QUALIFY' EQ '').RJNQUAL                          @T1A 06200000
         AIF   ('&QUALIFY' EQ 'CONV').RJCONV                       @T1A 06250000
         AIF   ('&QUALIFY' EQ 'CONVGRP').RJCNGP                         06300000
.*                                     QUALIFY=CONVGRP             @KFA 06350000
         AIF   ('&QUALIFY' EQ 'SESSION').RJSSION                        06400000
         IST6MCER 5,QUALIFY,&QUALIFY   NOT VALID QUALIFY VALUE          06450000
.*                                     NOT CODE QUALIFY            @T2A 06500000
.RJNQUAL ANOP                                                      @T2A 06550000
         AIF   (NOT &ISTGEN).RJEXIT                                     06600000
         AIF   (NOT '&SESSID' EQ '').RJSESID                       @T2A 06650000
         AIF   (NOT '&SESSIDL' EQ '').RJSSIDL                      @T2A 06700000
         AIF   (NOT '&DEACTYP' EQ '').RJTYPE                       @T2A 06750000
         AGO   .RJREQ                                                   06800000
.*                                     QUALIFY=CONV                @F1A 06850000
.RJCONV  ANOP                                                           06900000
         AIF   (NOT &ISTGEN).RJEXIT                                     06950000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CONV                           07000000
         AIF   (NOT '&SESSID' EQ '').RJCSSID                       @Q4A 07050000
         AIF   (NOT '&SESSIDL' EQ '').RJCSIDL                      @Q4A 07100000
         AIF   (NOT '&DEACTYP' EQ '').RJCDETP                      @Q4A 07150000
         AGO   .RJSETBL                                                 07200000
.RJCNGP  ANOP                                                      @KFA 07250000
         AIF   (NOT &ISTGEN).RJEXIT                                @KFA 07300000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6CNGP                      @KFA 07350000
         AIF   (NOT '&SESSID' EQ '').RJCSSID                       @KFA 07400000
         AIF   (NOT '&SESSIDL' EQ '').RJCSIDL                      @KFA 07450000
         AIF   (NOT '&DEACTYP' EQ '').RJTYPE                       @KFA 07500000
         AGO   .RJSETBL                                            @KFA 07550000
.*                                     INVALID SESSID PARAMETER    @Q4A 07600000
.RJCSSID ANOP                                                           07650000
         IST6MCER 6,SESSID,&QUALIFY,QUALIFY                             07700000
         AGO   .RJSETBL                                                 07750000
.*                                     INVALID SESSIDL PARAMETER   @Q4A 07800000
.RJCSIDL ANOP                                                           07850000
         IST6MCER 6,SESSIDL,&QUALIFY,QUALIFY                            07900000
         AGO   .RJSETBL                                                 07950000
.*                                     INVALID DEACTYP PARAMETER   @Q4A 08000000
.RJCDETP ANOP                                                           08050000
         IST6MCER 6,DEACTYP,&QUALIFY,QUALIFY                            08100000
         AGO   .RJSETBL                                                 08150000
.*                                     QUALIFY=SESSION             @F1A 08200000
.RJSSION ANOP                                                           08250000
         AIF   (NOT &ISTGEN).RJEXIT                                     08300000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SESN                           08350000
.*                                     CHECK SESSID PARAMETER      @F1A 08400000
.RJSESID ANOP                                                           08450000
         AIF   ('&SESSID' EQ '').RJTYPE                                 08500000
         AIF   ('&SESSID'(K'&SESSID,1) EQ '''').RJERROR                 08550000
         AGO   .RJSSIDL                                                 08600000
.*                                     INVALID SESSID DATA TYPE    @F1A 08650000
.RJERROR ANOP                                                           08700000
         IST6MCER 11,SESSID,&SESSID                                     08750000
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES           08800000
.*                                     CHECK SESSIDL PARAMETER     @F1A 08850000
.RJSSIDL ANOP                                                           08900000
         AIF   ('&SESSIDL' EQ '').RJTYPE                                08950000
         AIF   ('&SESSIDL'(K'&SESSIDL,1) EQ '''').RJLENER               09000000
         AIF   (NOT &ISTGEN).RJEXIT                                     09050000
         AIF   ('&SESSIDL'(1,1) EQ '(').SLENGTH  REGISTER NOTATION      09100000
.*                                     SPECIFIED                   @Q3A 09150000
         SLR   15,15                   INITIALIZE REG15            @01A 09200000
         IC    15,&SESSIDL                                         @Q1A 09250000
         AGO   .SETSSL                                             @Q3A 09300000
.*                                     OBTAIN LENGTH SPECIFIED     @Q3A 09350000
.SLENGTH ANOP                                                      @Q3A 09400000
         LR    15,&SESSIDL(1)                                      @Q3A 09450000
.*                                     SET SESSIDL IN APPC AREA    @F1A 09500000
.SETSSL  ANOP                                                      @Q3A 09550000
         AIF   (NOT &ISTGEN).RJEXIT                                     09600000
         STC   15,RPL6SIDL-ISTRPL6X(14)                                 09650000
         LTR   15,15                   SESSIDL=0 ?                 @Q1A 09700000
         BZ    *+10                                                @Q1A 09750000
         AIF   ('&SESSID'(1,1) EQ '(').RJIREGA   REGISTER NOTATION      09800000
.*                                     SPECIFIED                   @Q3A 09850000
.*                                     SET SESSID IN APPC AREA     @F1A 09900000
         BCTR  15,0                                                     09950000
         EX    15,*+8                                                   10000000
         B     *+10                                                     10050000
         MVC   RPL6SSID-ISTRPL6X(1,14),&SESSID                     @Q1A 10100000
         AGO   .RJTYPE                                             @Q2C 10150000
.*                                     SET SESSID IN APPC AREA     @Q3A 10200000
.RJIREGA ANOP                                                      @Q3A 10250000
         AIF   (NOT &ISTGEN).RJEXIT                                     10300000
         BCTR  15,0                                                @Q3M 10350000
         EX    15,*+8                                              @Q3M 10400000
         B     *+10                                                @Q3M 10450000
         MVC   RPL6SSID-ISTRPL6X(1,14),0(&SESSID(1))               @Q3C 10500000
         AGO   .RJTYPE                                             @Q2C 10550000
.*                                     INVALID SESSIDL DATA TYPE   @F1A 10600000
.RJLENER ANOP                                                           10650000
         IST6MCER 11,SESSIDL,&SESSIDL                                   10700000
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES           10750000
.*                                     CHECK DEACTYP PARAMETER     @Q2A 10800000
.RJTYPE  ANOP                                                           10850000
         AIF   ('&DEACTYP' EQ '').RJREQ                            @Q2A 10900000
         AIF   ('&DEACTYP'(K'&DEACTYP,1) EQ '''').RJTYPER               10950000
         AIF   ('&DEACTYP'(1,1) EQ '(').RJTPREG  REGISTER NOTATION      11000000
.*                                     SPECIFIED                   @Q4C 11050000
.*                                     SET DEACTYP IN APPC AREA    @Q4A 11100000
         AIF   (NOT &ISTGEN).RJEXIT                                     11150000
         MVC   RPL6DETP-ISTRPL6X(1,14),&DEACTYP                         11200000
         AGO   .RJREQ                                                   11250000
.*                                     SET DEACTYP IN APPC AREA    @Q4A 11300000
.RJTPREG ANOP                                                           11350000
         AIF   (NOT &ISTGEN).RJEXIT                                     11400000
         MVC   RPL6DETP-ISTRPL6X(1,14),0(&DEACTYP(1))                   11450000
         AGO   .RJREQ                                                   11500000
.*                                     INVALID DEACTYP DATA TYPE   @Q2A 11550000
.RJTYPER ANOP                                                           11600000
         IST6MCER 11,DEACTYP,&DEACTYP                                   11650000
         AGO   .RJREQ                  DEFAULT TO RPL6 VALUES           11700000
.*                                     ZERO OUT SESSID AND SESSIDL @F1A 11750000
.RJSETBL ANOP                                                           11800000
         AIF   (NOT &ISTGEN).RJEXIT                                     11850000
         XC    RPL6SSID-ISTRPL6X(8,14),RPL6SSID-ISTRPL6X(14)            11900000
         MVI   RPL6SIDL-ISTRPL6X(14),0                                  11950000
.*                                     SET REJECT REQUEST CODE IN       12000000
.*                                     APPC AREA                   @F1A 12050000
.RJREQ   ANOP                                                           12100000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6RJCT                            12150000
*                                                                       12200000
.RJEXIT  MEND                                                           12250000
