         MACRO                                                          01000000
&NAME    XLATE &AREA,&LENGTH,&TO=E                                      02000000
.*                                                                      02010000
.*       MACRO DELETIONS/CHANGES FOR VS2 RELEASE 4                      02020000
.*0000                                                         @ZA02217 02021000
.*                                                                      02030000
.*                                                                      02040000
.********************************************************************** 02050000
.*                                                                      02060000
.* MODULE NAME = XLATE (VS2-2)                                          02070000
.*                                                                      02080000
.* DESCRIPTIVE NAME = EBCDIC/ASCII TRANSLATE SVC                        02090000
.*                                                                      02100000
*/* COPYRIGHT =                                                      */ 02102700
*/*                                                                  */ 02105400
*/*PROPRIETARY V3 STATEMENT                                          */ 02108100
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 02110800
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 02113500
*/*5695-DF1                                                          */ 02116200
*/*(C) COPYRIGHT 1992  IBM CORP.                                     */ 02118900
*/*END PROPRIETARY V3 STATEMENT                                      */ 02122900
*/*                                                                  */ 02127000
.* STATUS = CHANGE LEVEL 001                                            02130000
.*                                                                      02140000
.* FUNCTION = THIS MACRO SETS UP THE INTERFACE FOR SVC 103 (IGC0010C).  02150000
.*            IS IS USUALLY USED WITH READING OR WRITING ASCII          02152000
.*            AL OR NL MAGNETIC TAPE.                                   02155000
.*            IT FIRST LOADS REGISTER 0 WITH THE NUMBER OF BYTES        02160000
.*            TO BE TRANSLATED AND REGISTER 1 WITH THE ADDRESS OF       02170000
.*            THE DATA TO BE TRANSLATED.  BIT 0 OF REGISTER 1 IS SET    02180000
.*            TO INDICATE THE DIRECTION OF TRANSLATION:  BIT 0 = 0      02190000
.*            INDICATES TO TRANSLATE FROM ASCII TO EBCDIC, BIT 0 = 1    02200000
.*            INDICATES TO TRANSLATE FROM EBCDIC TO ASCII.  IT THEN     02210000
.*            ISSUES SVC 103.                                           02220000
.*                                                                      02230000
.* NOTES = SEE BELOW                                                    02240000
.*                                                                      02250000
.*      DEPENDENCIES = NONE                                             02260000
.*                                                                      02270000
.*      RESTRICTIONS = REGISTERS USED AS ARGUMENTS MUST BE IN THE       02280000
.*                     FORM (R), WHERE R MAY BE ANY REGISTER 2 - 15.    02290000
.*                     THE HIGH ORDER BYTE IS SUPPOSED TO BE ZERO.      02292000
.*                     WHERE REGISTER 0 OR 1 IS ALLOWED, FOR            02294000
.*                     EFFIENCY IT SHOULD BE WRITTEN AS (0) OR (1)      02296000
.*                     AND NOT (REG0).                                  02298000
.*                                                                      02300000
.*      REGISTER CONVENTIONS = NOT APPLICABLE                           02310000
.*                                                                      02320000
.* PATCH LABEL = NOT APPLICABLE                                         02330000
.*                                                                      02340000
.* MODULE TYPE = MACRO                                                  02350000
.*                                                                      02360000
.*      PROCESSOR = ASSEMBLER LANGUAGE                                  02370000
.*                                                                      02380000
.*      MODULE SIZE = NOT APPLICABLE                                    02390000
.*                                                                      02400000
.*      ATTRIBUTES = NOT APPLICABLE                                     02410000
.*                                                                      02420000
.* ENTRY POINT = NOT APPLICABLE                                         02430000
.*                                                                      02440000
.*      PURPOSE = SEE FUNCTION                                          02450000
.*                                                                      02460000
.*      LINKAGE = LABEL XLATE AREA,LENGTH,TO=                           02470000
.*                                                                      02500000
.*      INPUT =   OPTIONAL LABEL SPECIFIED AS AN ACCEPTABLE ASSEMBLER   02530000
.*                LABEL.                                                02540000
.*                                                                      02550000
.*                THE AREA ARGUMENT IS THE FIRST POSITIONAL PARAMETER.  02560000
.*                IT MAY BE (1) THE NAME OF A DATA AREA,                02570000
.*                (2) REGISTER 1 OR 2-15 CONTAINING THE ADDRESS         02580000
.*                OF THE DATA AREA TO BE TRANSLATED.                    02590000
.*                                                                      02610000
.*                THE LENGTH ARGUMENT IS THE SECOND POSITIONAL          02620000
.*                PARAMETER.  IT MAY BE (1) THE BYTE COUNT, OR          02630000
.*                (2) REGISTER 0 OR 2-15 CONTAINING THE BYTE COUNT.     02640000
.*                                                                      02660000
.*                TO= IS A KEYWORD PARAMETER WHOSE VALUES ARE TO=A      02670000
.*                (TRANSLATE TO ASCII) AND TO=E (TRANSLATE TO EBCDIC).  02680000
.*                                                                      02690000
.*      OUTPUT = INTERFACE TO SVC 103                                   02700000
.*                                                                      02710000
.* EXIT-NORMAL = MEND STATEMENT                                         02720000
.*                                                                      02730000
.* EXIT-ERROR = ERROR MESSAGES:                                         02740000
.*                                                                      02750000
.*      MNOTE 8,EXCESSIVE POSITIONAL PARAMETERS - IGNORED               02755000
.*      MNOTE 8,AREA AND LENGTH OPERANDS ARE EQUAL                      02760000
.*      MNOTE 12,IHB004 REQUIRED OPERAND(S) NOT SPECIFIED               02770000
.*      MNOTE 8,INVALID REGISTER SPECIFICATION                          02780000
.*                                                                      02790000
.* EXTERNAL REFERENCES = NONE                                           02800000
.*                                                                      02810000
.*      ROUTINES = NONE                                                 02820000
.*                                                                      02830000
.*      DATA AREAS = NONE                                               02840000
.*                                                                      02850000
.*      CONTROL BLOCKS = NONE                                           02860000
.*                                                                      02870000
.* TABLES = NONE                                                        02880000
.*                                                                      02890000
.* MACROS = IHBERMAC                                                    02900000
.*                                                                      02910000
.* CHANGE ACTIVITY = SEE DELETIONS/CHANGES PRECEDING THE MACRO PROLOGUE 02920000
.*                                                                      02930000
.********************************************************************** 02940000
.*                                                                      02950000
         AIF   (N'&SYSLIST EQ 2).TWOARGS                       @ZA02217 17000000
         AIF   (N'&SYSLIST LT 2).ERR1                                   18000000
         MNOTE 8,'EXCESSIVE POSITIONAL PARAMETERS - IGNORED'   @ZA02217 18500000
.TWOARGS AIF   ('&AREA' EQ '&LENGTH').ERR                      @ZA02217 19000000
         AIF   ('&AREA' EQ '(0)' OR '&LENGTH' EQ '(1)').ERR2            20000000
         AIF   ('&AREA'(1,1) EQ '(').REG1                      @ZA02217 29000000
&NAME    LA    1,&AREA                  ADDRESS OF AREA TO TRANSLATE    31000000
         AGO   .LEN1                                                    32000000
.REG1    AIF   ('&AREA' EQ '(1)').NAME                         @ZA02217 33000000
&NAME    LR    1,&AREA(1)               ADDRESS OF AREA TO TRANSLATE    34000000
         AGO   .LEN1                                                    35000000
.NAME    AIF   ('&NAME' EQ '').LEN1                            @ZA02217 35500000
&NAME    DS    0H                                                       36000000
.LEN1    AIF   ('&LENGTH'(1,1) EQ '(').REG2                             37000000
         LA    0,&LENGTH.(,0)           LENGTH TO TRANSLATE             40000000
         AGO   .TO                                             @ZA02217 41000000
.REG2    AIF   ('&LENGTH' EQ '(0)').TO                         @ZA02217 42000000
         LR    0,&LENGTH(1)             LENGTH TO TRANSLATE             43000000
.TO      AIF   ('&TO' EQ 'E').SVC       ALREADY SET ASCII TO EBCDIC     44000000
         LA    15,X'80'                 CONSTRUCT BIT MASK              45000000
         SLL   15,24                    IN HIGH ORDER BYTE              46000000
         OR    1,15                     SET EBCDIC TO ASCII INDICATOR   47000000
.SVC     SVC   103                      ISSUE XLATE SVC                 48000000
         MEXIT                                                 @ZA02217 49000000
.ERR     MNOTE 8,'AREA AND LENGTH OPERANDS ARE EQUAL'          @ZA02217 52000000
         MEXIT                                                          53000000
.ERR1    IHBERMAC 24  IHB004 REQUIRED OPERAND(S) NOT SPECIFIED @ZA02217 54000000
         MEXIT                                                          55000000
.ERR2    MNOTE 8,'INVALID REGISTER SPECIFICATION'                       56000000
         MEND                                                  @ZA02217 57000000
