*%/*                                                                    03050002
         MACRO                                                          03100002
&NAME    LINK  &EP=,&EPLOC=,&DE=,&PARAM=,&DCB=,&VL=,&ID=,&SF=I,        X06000000
               &MF=I,&ERRET=,&HIARCHY=,&LSEARCH=              @G381P2A  09000000
.********************************************************************** 09003500
.*                                                                    * 09007000
.* NAME = LINK                                                        * 09010500
.* COPYRIGHT = 5647-A01                                               * 09013400
.*             THIS MODULE IS "RESTRICTED MATERIALS OF IBM"           * 09016400
.*             (C) COPYRIGHT IBM CORP. 1981, 2000                     * 09020500
.*             LICENSED MATERIALS - PROPERTY OF IBM                   * 09024600
.*                                                                    * 09028700
.*01*  EXTERNAL CLASSIFICATION: PI                                    * 09030300
.*01*  END OF EXTERNAL CLASSIFICATION:                                * 09031900
.*                                                                    * 09033500
.* STATUS = HBB7703                                                   * 09035100
.*                                                                    * 09036900
.* CHANGE ACTIVITY =                                                  * 09037400
.*   $L1 =    AR    HBB3310 850901 PDXB: AR SUPPORT                   * 09037900
.*   $L2= 64BITCSV  HBB7703 981204 PD00XB: ESAME                      * 09038200
.*                                                                    * 09038500
.********************************************************************** 09045100
         GBLB  &IHBSWA,&IHBSWB                                @G381P2A  09050000
         LCLB  &XSVC                                          @G381P2A  09100000
         GBLC  &SYSASCE           ASC ENVIRONMENT                  @L1A 09104100
         GBLC  &SYSAM64           AMODE                            @L2A 09105400
         GBLA  &SYSALVL           ARCHLVL                          @L2A 09106700
         SYSSTATE TEST            WHAT IS ASC ENVIRONMENT          @L1A 09108200
         AIF ('&SYSAM64' EQ 'YES').INVKLINKX AMODE 64, LINKX       @L2A 09109500
         AIF (&SYSALVL GT 1).INVKLINKX ESAME, LINKX                @L2A 09110800
         AIF ('&SYSASCE' EQ 'P').INVKLNK PRIMARY, PROCESS NORMALLY @L1A 09112300
.INVKLINKX ANOP                                                    @L2A 09114300
         MNOTE 0,'*** LINKX MACRO SUBSTITUTED FOR LINK MACRO BECAUSE ASX09116400
               CENV=AR/ANY, AMODE64=YES, OR ARCHLVL>1 SPECIFIED ON PREVX09120500
               IOUSLY ISSUED SYSSTATE MACRO'                       @L2C 09124600
&NAME    LINKX EP=&EP,EPLOC=&EPLOC,DE=&DE,PARAM=&PARAM,                X09128700
               DCB=&DCB,VL=&VL,ID=&ID,SF=&SF,MF=&MF,                   X09132800
               ERRET=&ERRET,HIARCHY=&HIARCHY,LSEARCH=&LSEARCH      @L1A 09136900
         MEXIT                                                     @L1A 09141000
.INVKLNK ANOP                                                      @L1A 09145100
&IHBSWA  SETB  ('&VL' EQ '1')                                 @G381P2A  09150000
&IHBSWB  SETB  ('&SF' EQ '(E,(15))')                          @G381P2A  09200000
&XSVC    SETB  ('&LSEARCH' NE '')                             @G381P2A  09250000
         AIF   ('&MF' EQ 'L').E1                                        21000000
         AIF   ('&SF' EQ 'L' AND '&MF&PARAM&ID&VL' NE 'I').E2           24000000
         AIF   ('&NAME' EQ '' OR '&SF' EQ 'L').CONTA                    27000000
&NAME    DS    0H                                                       30000000
.CONTA   AIF   ('&PARAM' EQ '' AND '&MF(1)' NE 'E').CONTB               33000000
         IHBOPLST  ,&PARAM,MF=&MF                                       36000000
.CONTB   ANOP                                                           39000000
.* BRANCH IF ANY NEW OPERANDS REQUIRING EXTENDED SVC SPECIFIED          42000000
         AIF   (NOT &XSVC).OLDGEN NO OPERANDS REQUIRING EXTENDED SVC    42050000
&NAME    IHBINNRX &EP,&EPLOC,&DE,&DCB,&LSEARCH,                @G381P2AX42100000
               ,,,LINK,&PARAM,&MF,                             @G381P2AX42150000
               ERRET=&ERRET,SF=&SF                             @G381P2A 42200000
         AGO   .CONTC                                          @G381P2A 42250000
.OLDGEN  ANOP                                                  @G381P2A 42300000
&NAME    IHBINNRB &EP,&EPLOC,&DE,&DCB,                         @G381P2AX42350000
               ERRET=&ERRET,SF=&SF                             @G381P2A 42400000
.CONTC   ANOP                                                  @G381P2A 42450000
         AIF   ('&SF' EQ 'L').END                              @G381P2A 42500000
         AIF   (NOT &XSVC).OLDSVC NO OPERANDS REQUIRING EXTENDED SVC    42550000
.*                                                             @G381P2A 42600000
         LA    15,7           LOAD LINK EXTENDED SVC ROUTING CODE       42650000
.*                                                             @G381P2A 42700000
         SVC   122                 ISSUE EXTENDED SVC          @G381P2A 42750000
         AGO   .CONTD                                          @G381P2A 42800000
.OLDSVC  ANOP                                                  @G381P2A 42850000
         SVC   6                   ISSUE LINK SVC              @G381P2A 42900000
.CONTD   ANOP                                                  @G381P2A 42950000
         AIF   ('&ID' EQ '').END                                        51000000
         DC    X'4700'             NOP INSTRUCTION WITH                 54000000
         DC    AL2(&ID)            ID IN LAST TWO BYTES                 57000000
         MEXIT                                                          60000000
.E1      IHBERMAC 84                                                    63000000
         MEXIT                                                          66000000
.E2      IHBERMAC 75                                                    69000000
.END     MEND                                                           72000000
**/                                                                     72050002
*LINK:   MACRO                                                          72100002
*        KEYS(MF,EP,DE,DCB,ID,SF,PARAM,ERRET,VL,LSEARCH);               72150000
*                                                         /* @G381P2A*/ 72200000
*        DCL SYSASCE CHAR EXT;     /* GLOBAL FOR SYSSTATE MACRO  @L2A*/ 72206200
*        DCL SYSAM64 CHAR EXT;     /* GLOBAL FOR SYSSTATE MACRO  @L2A*/ 72212400
*        DCL SYSALVL FIXED EXT;    /* GLOBAL FOR SYSSTATE MACRO  @L2A*/ 72218600
*        ANS('?SYSSTATE (TEST);'); /* OBTAIN DEFAULTS            @L2A*/ 72224800
*        IF SYSASCE = 'P'          /* INVOKE NORMAL LINK         @L2A*/ 72231000
*         & SYSAM64 ^= 'YES'       /* INVOKE NORMAL LINK         @L2A*/ 72237300
*         & SYSALVL <= 1 THEN      /* INVOKE NORMAL LINK         @L2A*/ 72243600
*          ANS('?'||MACLABEL||'LINKP '||MACLIST||MACKEYS||';');         72250000
*        ELSE DO;                  /* INVOKE LINKX               @L2A*/ 72262500
*          ANS MSG('LINKX MACRO SUBSTITUTED FOR LINK MACRO'||           72275000
*                  ' BECAUSE ASCENV(AR/ANY), AMODE64(YES), OR' ||       72284300
*                  ' ARCHLVL(>1) SPECIFIED ON PREVIOUSLY ISSUED' ||     72293600
*                  ' SYSSTATE MACRO');        /*                 @L2C*/ 72303000
*          ANS('?'||MACLABEL||'LINKXP '||MACLIST||MACKEYS||';'); /*     72312500
*                                    INVOKE X-VERSION            @L2A*/ 72321800
*        END;                      /*                            @L2A*/ 72331100
*                                                                       72340400
*%END;                                                    /* @G381P2A*/ 72350000
