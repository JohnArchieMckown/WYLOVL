*/*                                                                  */ 00002000
*/*PROPRIETARY V3 STATEMENT                                          */ 00004000
*/*LICENSED MATERIALS - PROPERTY OF IBM                              */ 00006000
*/*"RESTRICTED MATERIALS OF IBM"                                     */ 00008000
*/*5695-DF1                                                          */ 00010000
*/*(C) COPYRIGHT 1992  IBM CORP.                                     */ 00012000
*/*END PROPRIETARY V3 STATEMENT                                      */ 00015000
*/*                                                                  */ 00018000
*/*       NOTE END DELIMITER MISSING UNTIL BEFORE PL/X INVOCATION @L1A  00019000
         MACRO                                                          00020000
&NAME    BLDL  &DCB,&LIST,&OPTION1,&OPTION2,&OPTION3,&DUMMY=      @L2C  00022200
.********************************************************************** 00024400
.*                                                                    * 00026600
.*                                                                    * 00037600
.* STATUS = MVS/ESA DFP RELEASE 3.2.0                                 * 00039800
.*                                                                    * 00042000
.* CHANGE ACTIVITY =                                                  * 00044200
.*                                                                    * 00046400
.*      VS2 RELEASE 03 CHANGES/DELETIONS                              * 00048600
.*A001100-001140                                                ZA00734 00052000
.*  $L0=PDSE,HDP3320,880206,STLFPC: PDSE SUPPORT                  @L0A* 00054000
.*  $L1=       ,       ,      ,SJPLMET: PLX SUPPORT               @L1A* 00054500
.*  $01=OW01366,HDZ11B0,940112,SJPLREB: BYPASSLLA OPTION          @01A* 00055000
.*  $L2=DYNLINK,HDZ11C0,960430,SJPLREB: NODEBCHK OPTION - OW19701 @L2A* 00055500
.*                                                                    * 00056000
.********************************************************************** 00058000
         LCLB  &B0,&B1,&B2,&B3,&B4,&B5,&B6,&B7  BITS IN A BYTE    @01A  00058600
         LCLC  &TEMPC               TO DO ALIGNMENT FOR ESTHETICS @01A  00059200
         AIF   ('&DCB' EQ '').E1                                        00060000
         AIF   ('&LIST' EQ '').E2                                       00080000
&NAME    IHBINNRA &DCB,&LIST              LOAD REG 1 AND REG 0          00100000
.*  REGISTER 1 IS MADE POSITIVE SO THE SVC CODE WILL KNOW THIS  ZA00734 00112000
.*  REQUEST IS FROM BLDL AND NOT THE FIND MACRO.                ZA00734 00114000
         LA    1,0(1)                     CLEAR HIGH ORDER BYTE ZA00734 00114200
.*  TEST FOR VALID OPTIONS TO INITIALIZE A PREFIX                  @L0A 00114400
.OPT     AIF   ('&OPTION1' EQ '').FINIS                            @L0A 00114600
         AIF   ('&OPTION1' NE 'NOCONNECT' AND                          *00114700
                '&OPTION1' NE 'NODEBCHK' AND                           *00114800
               '&OPTION1' NE 'BYPASSLLA').E3     INVALID OPTION 1  @L2C 00114900
.OPT2    AIF   ('&OPTION2' NE '' AND                                   *00115000
               '&OPTION2' NE 'NOCONNECT' AND                           *00115100
               '&OPTION2' NE 'NODEBCHK' AND                            *00115200
               '&OPTION2' NE 'BYPASSLLA').E4     INVALID OPTION 2  @L2C 00115300
.OPT3    AIF   ('&OPTION3' NE '' AND                                   *00115400
               '&OPTION3' NE 'NOCONNECT' AND                           *00115500
               '&OPTION3' NE 'NODEBCHK' AND                            *00115600
               '&OPTION3' NE 'BYPASSLLA').E5     INVALID OPTION 3  @L2A 00115700
.OPTEND  ANOP                                                      @L2A 00115800
.*  REGISTER 15 IS LOADED WITH LIST ADDRESS FOR OPTION PROCESSING. @L0A 00115900
         LR    15,0                       LOAD REG 15 TO CLEAR THE @L0A 00116000
         LA    15,0(,15)                  HIGH ORDER BIT           @L0A 00116100
         LA    0,X'80'                   SET HIGH ORDER BIT ON     @L0A 00116200
         SLL   0,24                                                @L0A 00116300
         OR    15,0                                                @L0A 00116400
         LA    0,8                       SET PREFIX LENGTH         @L0A 00116600
         SLR   15,0                      INITIALIZE THE PREFIX     @L0A 00116800
         XC    0(8,15),0(15)                                       @L0A 00117000
         ST    0,4(,15)                                            @L0A 00117200
         ALR   0,15                                                @L0A 00117400
&B0      SETB  ('&OPTION1' EQ 'NOCONNECT' OR                           *00117500
               '&OPTION2' EQ 'NOCONNECT' OR                            *00117600
               '&OPTION3' EQ 'NOCONNECT') NOCONNECT OPTION         @L2C 00117700
&B1      SETB  ('&OPTION1' EQ 'BYPASSLLA' OR                           *00117800
               '&OPTION2' EQ 'BYPASSLLA' OR                            *00117900
               '&OPTION3' EQ 'BYPASSLLA') BYPASSLLA OPTION         @L2C 00118000
&B2      SETB  ('&OPTION1' EQ 'NODEBCHK' OR                            *00118100
               '&OPTION2' EQ 'NODEBCHK' OR                             *00118200
               '&OPTION3' EQ 'NODEBCHK') NODEBCHK OPTION           @L2A 00118300
&TEMPC   SETC  '&B0&B1&B2.00000'   TO PRESERVE COMMENT ALIGNMENT   @L2C 00118400
         MVI   3(15),B'&TEMPC'           SET OPTIONS               @01C 00118500
         AGO   .SVC                      BYPASS CODE FOR NO OPTION @L0A 00118600
.FINIS   ANOP                                                      @L0A 00118700
.*  CLEAR REGISTER 15 TO INSURE THAT OPTIONS ARE NOT PROCESSED.    @L0A 00118800
         SLR   15,15                     FOR NO OPTION             @L0A 00118900
.SVC     ANOP                                                      @L0A 00119300
         SVC   18                                LINK TO BLDL ROUTINE   00120000
         MEXIT                                                          00140000
.E1      IHBERMAC 6                     DCB ADDR MISSING                00150000
         AIF   ('&LIST' EQ '').E2                                       00160000
         AGO .E2A                                                       00210000
.E2      IHBERMAC 34                    LIST ADDR MISSING               05180000
.E2A     AIF   ('&OPTION1' NE '').OPT                             @01A  05230000
         MEXIT                                                    @01A  05280000
.E3      IHBERMAC 50                    INVALID OPTION 1          @01A  05330000
         AGO   .OPT2                    PROCESS OPTION 2          @01A  05380000
.E4      IHBERMAC 51                    INVALID OPTION 2          @01A  05430000
         AGO   .OPT3                    PROCESS OPTION 3          @01A  05480000
.E5      IHBERMAC 53                    INVALID OPTION 3          @01A  05530000
         AGO   .OPTEND                  PROCESS ANY OTHERS        @01A  05580000
         MEND                                                           40200000
* */%BLDL: MACRO KEYS(1:1(DCB_NAME,DCB_ADDR),1:1(AREA_NAME,    /*@L1A*/ 40250000
*       AREA_ADDR),RETCODE,RSNCODE,CONNECT,BYPASSLLA           /*@01C*/ 40283300
*       ,DEBCHK);                                              /*@L2C*/ 40316600
*  ANS('? '||MACLABEL||' BLDLP '||MACKEYS||';') RESCAN SKIP;   /*@L1A*/ 40350000
*  %END BLDL;                                                  /*@L1A*/ 40400000
