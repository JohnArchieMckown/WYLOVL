        MACRO                                                           00050000
&NAME    ACBVS &DDNAME=,&EXLST=0,&MACRF=,                      @ZA34940-00100000
               &JFCB=0,&BUFND=0,&BUFNI=0,&PASSWD=0,                    -00150000
               &BLKSIZE=0,&LRECL=0,&BUFSP=0,&SHRPOOL=,             @L1C-00150200
               &CCTYPE=,&STRNO=1,&CATALOG=NO,                  @ZA07566-00150400
               &BSTRNO=0,&MAREA=0,&MLEN=0,&CRA=,                X04SVHS-00150600
               &AM=,&APPLID=0,&USERPTR=0,                      @ZA34940-00150800
               &RMODE31=,                                          @LPA-00150900
               &PARMS=,&PARM2=,&PARM3=,                            @LQC-00151000
               &SUBSYSNM=,&RLSREAD=,&CTRLACB=                      @LQA 00151100
.*                                                                      00151200
.* $MAC(ACBVS) COMP(SC1DE):                                             00151400
.*                                                                      00151600
.*PROPRIETARY V3 STATEMENT                                              00151700
.*LICENSED MATERIALS - PROPERTY OF IBM                                  00151800
.*"RESTRICTED MATERIALS OF IBM"                                         00151900
.*5695-DF1                                                              00152000
.*(C) COPYRIGHT 1983,1995  IBM CORP.                                    00152100
.*END PROPRIETARY V3 STATEMENT                                          00152400
.*                                                                      00152800
.* STATUS = DFSMS/MVS VERSION 1 RELEASE 4.0                             00153000
.*                                                                      00153200
.* $L1=DFP,JDP1110,,STLJRB: MVS/XA DFP RELEASE 1.1 VSAM 31-BIT     @L1A 00153400
.* $L9=DFP,HDP2230,,STLJRB: MVS/XA DFP RELEASE 2.3 VSAM 31-BIT     @L9A 00153500
.* $LP=DFP,HDP2230,,STLJTP: MVS/XA DFP RELEASE 2.3 31/24 BIT       @LPA 00153600
.* APAR TO SHIP 31/24 BIT CODE                                 @YA03564 00153700
.* CHANGE CONTINUATION CHARACTER TO '-'                        @YA21154 00153800
.* CHECK KEYWORD VALUES                                        @YA37879 00153900
.* COMPLETE THE FIX FOR OY37879                                @YA39421 00154000
.* CHECK KEYWORD VALUES (PE OY39421). ALLOW PARMS IN PARENS    @YA41534 00154100
.*                                                                      00154200
.* FUNCTION-   THE FUNCTION OF THIS MACRO IS TO BUILD,         @ZA34940 00154300
.*             AT ASSEMBLY TIME, AN ACB (ACCESS-METHOD         @ZA34940 00154400
.*             CONTROL BLOCK) FOR A VSAM USER.                 @ZA34940 00154500
.*             ACBVS IS INVOKED BY MACRO ACB WHEN A USER       @ZA34940 00154600
.*             HAS ISSUED AN ACB MACRO WITH NO 'AM'            @ZA34940 00154700
.*             SPECIFIED, OR 'AM=VSAM' SPECIFIED.              @ZA34940 00154800
.*                                                             @ZA34940 00155000
.* CHANGE ACTIVITY                                             @ZA34940 00155200
.*      CBMM SPLIT SUPPORT                                     @ZA34940 00155400
.* $LQ=DFSMS,HDZ11C0,930430,SJPLRG: VSAM RLS                       @LQA 00155500
.* $LR=OW16321,HDZ11C0,951013,SJPLLLL: MAKE ACB AM=VSAM A DEFAULT  @LRA 00155600
.* $LX=DFSMS,HDZ11D0,09/11/96,SJPLJRB: SYSTEM MANAGED BUFFERS      @LXA 00155700
.* $L2=K150064,HDZ11E0,10/07/97,TMENENDEZ:  TVS CRE SUPPORT        @L2A 00155800
.******************************************************************@LQC 00155900
.* GLOBALS                                                      X04SVHS 00156000
         GBLC  &IDAMAC1,&IDAMAC2,&IDAMAC3,&IDAMAC4 MACRF CODES     @LQC 00156100
         GBLB  &IDARLS                  MACRF=RLS SPECIFIED        @LQA 00156200
         GBLC  &IDAIFL1                 CATALOG/CRA CODES       X04SVHS 00156300
         GBLA  &IDAENO                  NUMBER OF ELEMENTS     @ZA06420 00156400
.* LOCALS                                                       X04SVHS 00156600
         LCLA  &VTMEXT                  LENGTH OF VTAM EXT     X03004HS 00156800
         LCLA  &LEN                     SUBPARAMETER ACB LENGTH  X03004 00157000
         LCLA  &STYPE                   ACCESS METHOD ID        X04SVHS 00157200
         LCLB  &A,&M                    CCTYPE BITS                     00157400
         LCLB  &R31B                    RMODE31 BUFFER REQUESTED  @LPA  00157500
         LCLB  &R31C                    RMODE31 CB REQUESTED      @LPA  00157600
         LCLB  &CACB                    ACB IS A CONTROL VSAM ACB @LQA  00157700
         LCLB  &CNRD,&RDI,&CR,&NRI,&CRE READ INTEGRITY FLAGS      @L2C  00157800
.*                                                                      00157900
.*             INITIALIZE GLOBALS TO ZERO                               00158000
&R31B    SETB  0                        SET RMODE31B DEFAULT      @LPA  00158100
&R31C    SETB  0                        SET RMODE31C DEFAULT      @LPA  00158200
&IDAMAC1 SETC  '00000000'                                       X04SVHS 00158300
&IDAMAC2 SETC  '00000000'                                       X04SVHS 00158400
&IDAMAC3 SETC  '00000000'                                       X04SVHS 00158500
&IDAMAC4 SETC  '00000000'                                          @LQA 00158600
&IDARLS  SETB  0                                                   @LQA 00158700
&IDAIFL1 SETC  '00000000'                                       X04SVHS 00158800
&IDAENO  SETA  0                                               @ZA06420 00158900
.*                                                                      00159000
         IDAVSACB MACRF=&MACRF,CATALOG=&CATALOG,CRA=&CRA,      @ZA34940-00159200
               AM=&AM,CALLER=ACB,RMODE31=&RMODE31                  @LPC 00159400
.*                   SET VALUES FOR BIT KEYWORDS IN GLOBALS    @ZA34940 00159600
.*           IDAVSACB IS CALLED TO PROCESS BIT KEYWORDS MACRF, @ZA34940 00159800
.*             CATALOG, AND CRA.  IT WILL VALIDITY CHECK OPTIONS        00160000
.*             SPECIFIED, MUTUALLY EXCLUSIVE CHECK WHERE APPLICABLE,    00160200
.*             AND SET GLOBALS WITH THE VALUES OF THE KEYWORDS.         00160400
.*                                                                      00160600
&LEN     SETA  76                       SET FOR COMMON LENGTH    X03004 00160800
.*                     DELETE LOGIC THAT SETS SUBTYPE=0 AS DEFAULT @LRD 00161200
.*                                                                      00161600
&STYPE   SETA  X'10'                    SUBTYPE = VSAM AS DEFAULT  @LRC 00161700
         AIF   (('&AM' NE 'VSAM') AND ('&AM' NE '')).CHKVTAM ^VSAM @LRC 00161800
.*****   CHECK FOR VSAM CONTROL ACB                                @LQA 00162000
         AIF   ('&CTRLACB' EQ 'YES' OR '&CTRLACB' EQ 'NO').CHKCTL  @LQA 00162100
         IDAERMAC 3,CTRLACB,&CTRLACB    ISSUE ERROR MESSAGE        @LQA 00162200
.CHKCTL  AIF   ('&CTRLACB' NE 'YES').SETLEN NO CHANGE TO SUBTYPE   @LQC 00162300
&STYPE   SETA  X'12'                    ACB=VSAM CONTROL ACB TYPE  @LQA 00162400
&CACB    SETB  1                        ACB IS A CONTROL VSAM ACB  @LQA 00162500
         AGO   .SETLEN                                             @LQA 00162600
.CHKVTAM ANOP                                                      @LQA 00162700
&VTMEXT  SETA  12                       NO, SET VTAM EXT LEN   X03004HS 00162800
&STYPE   SETA  X'20'                    SET VTAM SUBTYPE        X04SVHS 00162900
         AIF   ('&AM' EQ 'VTAM').SETLEN IF VTAM SET VTAM LEN    X04SVHS 00163000
.*                                      AND SUBTYPE             X04SVHS 00163100
.*                                      ELSE ERROR              X04SVHS 00163200
&STYPE   SETA  0                        ZERO SUBTYPE            X04SVHS 00163300
&VTMEXT  SETA  0                        GENERATE NORMAL LEN ACB X04SVHS 00163400
         IDAERMAC 3,AM,&AM              ISSUE ERROR MSG            @L9C 00163600
.SETLEN  ANOP                                                   X04SVHS 00163800
&NAME    DS    0F                                                       00164000
         DC    X'A0' .                  ACB IDENTIFICATION              00164200
         DC    AL1(&STYPE)              ACB SUBTYPE             X04SVHS 00164400
.*             X'11' - X'1F' ARE RESERVED FOR VSAM              X04SVHS 00164600
.*             X'21' - X'2F' ARE RESERVED FOR VTAM              X04SVHS 00164800
         DC    AL2(&LEN+&VTMEXT) .      ACB LENGTH             X03004HS 00165000
.*****   CHECK FOR SUBSYSTEM NAME                                  @LQA 00165100
         AIF   ('&SUBSYSNM' EQ '').NOSSNM GO IF SUBSYSNM NOT GIVEN @LQA 00165200
         DC    A(&SUBSYSNM)             ADDRESS OF SUBSYSNM        @LQA 00165300
         AGO   .VTAMEX                  PROCESS NEXT FIELD         @LQA 00165400
.NOSSNM  ANOP                                                      @LQA 00165500
         AIF   (&CACB).ZSSNM            GO IF ACB=CONTROL ACB      @LQA 00165600
         DC    A(0) .                   AMB LIST POINTER                00165700
         AGO   .VTAMEX                  PROCESS NEXT FIELD         @LQA 00165800
.ZSSNM   DC    A(0)                     ADDRESS OF SUBSYSNM        @LQA 00165900
.VTAMEX  AIF   ('&AM' NE 'VTAM').SETIRR IF NOT VTAM ACBINRTN=ZERO  @LQC 00166100
.*                                                             X03004HS 00166200
         DC    A(*+&LEN-8)  .           PTR TO VTAM EXTENSION  X03004HS 00166300
         AGO   .MACRF1                                         X03004HS 00166400
.SETIRR  ANOP                                                           00166500
         DC    A(0) .                   INTERFACE ROUTINE POINTER       00166600
.*       MACRF PROCESSING                                               00166700
.MACRF1  ANOP                                                   X04SVHS 00166800
         DC    B'&IDAMAC1'              MACRF(1)                X04SVHS 00167000
         DC    B'&IDAMAC2'              MACRF(2)                X04SVHS 00167200
.*****   CHECK FOR VALID BSTRNO...0 <= BSTRNO <= 255           @YA37879 00167300
         AIF   (T'&BSTRNO NE 'N').BSTRNO2  NOT NUMERIC         @YA39421 00167400
         AIF   ('&BSTRNO'(1,1) EQ '(').BSTRNO2  SKIP RANGE CHK @YA41534 00167500
*                                       FOR NUMERIC IN PARENS  @YA41534 00167600
         AIF   (&BSTRNO GE 0).BSTRNO1   BSTRNO >= 0...OK       @YA37879 00167700
         IDAERMAC 3,BSTRNO,&BSTRNO      ISSUE ERROR MESSAGE    @YA37879 00167800
.BSTRNO1 AIF   (&BSTRNO LE 255).BSTRNO2 BSTRNO <= 255...OK     @YA37879 00167900
         IDAERMAC 3,BSTRNO,&BSTRNO      ISSUE ERROR MESSAGE    @YA37879 00168000
.BSTRNO2 ANOP                                                  @YA37879 00168100
         DC    AL1(&BSTRNO) .           NO OF CONCURRENT        X04SVHS 00168200
*                                       STRINGS FOR AIX PATH    X04SVHS 00168300
.*****   CHECK FOR VALID STRNO...1 <= STRNO <= 255             @YA37879 00168400
         AIF   (T'&STRNO NE 'N').STRNO2  NOT NUMERIC           @YA39421 00168500
         AIF   ('&STRNO'(1,1) EQ '(').STRNO2  SKIP RANGE CHECK @YA41534 00168600
*                                       FOR NUMERIC IN PARENS  @YA41534 00168700
         AIF   (&STRNO GE 1).STRNO1     STRNO >= 1...OK        @YA37879 00168800
         IDAERMAC 3,STRNO,&STRNO        ISSUE ERROR MESSAGE    @YA37879 00168900
.STRNO1 AIF   (&STRNO LE 255).STRNO2    STRNO <= 255...OK      @YA37879 00169000
         IDAERMAC 3,STRNO,&STRNO        ISSUE ERROR MESSAGE    @YA37879 00169100
.STRNO2 ANOP                                                   @YA37879 00169200
         DC    AL1(&STRNO) .            NUMBER OF STRINGS       X04SVHS 00169300
.*****   CHECK FOR VALID BUFND...0 <= BUFND <= X'FFFF'         @YA37879 00169400
         AIF   (T'&BUFND NE 'N').BUFND2  NOT NUMERIC           @YA39421 00169500
         AIF   ('&BUFND'(1,1) EQ '(').BUFND2  SKIP RANGE CHECK @YA41534 00169600
*                                       FOR NUMERIC IN PARENS  @YA41534 00169700
         AIF   (&BUFND GE 0).BUFND1     BUFND >= 0...OK        @YA37879 00169800
         IDAERMAC 3,BUFND,&BUFND        ISSUE ERROR MESSAGE    @YA37879 00169900
.BUFND1 AIF   (&BUFND LE 65535).BUFND2  BUFND <= 255...OK      @YA37879 00170000
         IDAERMAC 3,BUFND,&BUFND        ISSUE ERROR MESSAGE    @YA37879 00170100
.BUFND2 ANOP                                                   @YA37879 00170200
         DC    AL2(&BUFND) .            NUMBER OF DATA BUFFERS          00170300
.*****   CHECK FOR VALID BUFNI...0 <= BUFNI <= X'FFFF'         @YA37879 00170400
         AIF   (T'&BUFNI NE 'N').BUFNI2  NOT NUMERIC           @YA39421 00170500
         AIF   ('&BUFNI'(1,1) EQ '(').BUFNI2  SKIP RANGE CHECK @YA41534 00170600
*                                       FOR NUMERIC IN PARENS  @YA41534 00170700
         AIF   (&BUFNI GE 0).BUFNI1     BUFNI >= 0...OK        @YA37879 00170800
         IDAERMAC 3,BUFNI,&BUFNI        ISSUE ERROR MESSAGE    @YA37879 00170900
.BUFNI1 AIF   (&BUFNI LE 65535).BUFNI2  BUFNI <= 255...OK      @YA37879 00171000
         IDAERMAC 3,BUFNI,&BUFNI        ISSUE ERROR MESSAGE    @YA37879 00171100
.BUFNI2 ANOP                                                   @YA37879 00171200
         DC    AL2(&BUFNI) .            NUMBER OF INDEX BUFFERS         00171300
         DC    B'&IDAMAC3'              MACRF(3)                X04SVHS 00171400
.*****   CHECK FOR VALID SHRPOOL...0 <= SHRPOOL <= 255             @LXC 00171500
         AIF   (&IDARLS AND '&SHRPOOL' NE '0').SHRINV              @LQA 00171600
         AGO   .SHRP0                                              @LQA 00171700
.SHRINV  IDAERMAC 19,MACRFRLS,SHRPOOL                              @LQA 00171800
.SHRP0   AIF   (T'&SHRPOOL NE 'N').SHRP2  NOT NUMERIC              @LQC 00171900
         AIF   ('&SHRPOOL'(1,1) EQ '(').SHRP2 SKIP RANGE CHECK @YA41534 00172000
*                                       FOR NUMERIC IN PARENS  @YA41534 00172100
         AIF   (&SHRPOOL GE 0).SHRP1    SHRPOOL >= 0...OK      @YA37879 00172200
         IDAERMAC 3,SHRPOOL,&SHRPOOL    ISSUE ERROR MESSAGE    @YA37879 00172300
.SHRP1  AIF   (&SHRPOOL LE 255).SHRP2   SHRPOOL <= 255...OK        @LXC 00172400
         IDAERMAC 3,SHRPOOL,&SHRPOOL    ISSUE ERROR MESSAGE    @YA37879 00172500
.SHRP2  ANOP                                                   @YA37879 00172600
         DC    AL1(&SHRPOOL)            SHARED RESOURCE POOL ID    @L1C 00172700
.*****   MACRF BYTE 4                                              @LQA 00172800
         AIF   ('&IDAMAC4' EQ '00000000').NOMAC4 GO IF BYTE IS 0   @LQA 00172900
         DC    AL1(0)                   ACB REASON CODE FIELD      @LQA 00173000
         DC    B'&IDAMAC4'              MACRF(4)                   @LQA 00173100
         AGO   .RECFM                                              @LQA 00173200
.NOMAC4  ANOP                                                      @LQA 00173300
         DC    AL2(0) .                 JES BUFFER POOL/NUMBER  X04SVHS 00173400
*                                       OF JOURNAL BUFFERS              00173500
.RECFM   ANOP                                                      @LQA 00173600
         DC    X'80' .                  RECFM=A                         00173700
.*****   VSAM-RLS READ INTEGRITY FLAGS                             @LQA 00173800
         AIF   ('&CCTYPE' NE '' AND '&RLSREAD' NE '').MUTEX1       @LQA 00173900
         AIF   ('&RLSREAD' EQ '').CCTST GENERATE CCTYPE FIELD      @LQA 00174000
         AGO   .RLSRD                                              @LQA 00174100
.MUTEX1  IDAERMAC 19,RLSREAD,CCTYPE     MUTUALLY EXCLUSIVE FIELDS  @LQA 00174200
.RLSRD   ANOP                                                      @LQA 00174300
&CR      SETB  ('&RLSREAD' EQ 'CR')     CONSISTENT READ            @LQA 00174400
&NRI     SETB  ('&RLSREAD' EQ 'NRI')    NO READ INTEGRITY          @LQA 00174500
&CRE     SETB  ('&RLSREAD' EQ 'CRE')    CONSISTENT READ EXTENDED   @L2A 00174600
         AIF   (&CR OR &NRI OR &CRE OR '&RLSREAD' EQ 'NORD').RDSET @L2C 00174700
         IDAERMAC 3,RLSREAD,&RLSREAD    INVALID RLSREAD OPTION     @LQA 00174800
         DC    B'00000000'              NO READ INTEGRITY OPTIONS  @LQA 00174900
         AGO   .DSORG                                              @LQA 00175000
.RDSET   ANOP                                                      @LQA 00175100
&RDI     SETB  (&CR OR &NRI OR &CRE)    SET RDI BIT                @L2C 00175200
&CNRD    SETB  (&CR EQ 1)               SET CNRD BIT               @LQA 00175300
&CRED    SETB  (&CRE EQ 1)              SET CRED BIT               @L2A 00175400
         DC    B'0&RDI&CNRD.00&CRED.00' READ INTEGRITY OPTIONS     @L2C 00175500
         AGO   .DSORG                                              @LQA 00175600
.*****   VTAM CONTROL CHARACTER                                    @LQA 00175700
.CCTST   ANOP                                                      @LQA 00175800
         AIF   ('&CCTYPE' EQ '').CCSET                                  00175900
&A       SETB  ('&CCTYPE' EQ 'A')                                       00176000
&M       SETB  ('&CCTYPE' EQ 'M')                                       00176100
         AIF   (&A OR &M).CCSET                                         00176200
         IDAERMAC 5,CCTYPE,&CCTYPE                                 @L9C 00176300
.CCSET   DC    B'00000&A&M.0' .         CONTROL CHARACTER TYPE          00176400
.*****   DSORG FIELD                                               @LQA 00176500
.DSORG   ANOP                                                      @LQA 00176600
         DC    X'0008' .                DSORG=ACB                       00176700
         AIF   ('&MAREA' EQ '0').GENM0 IF &MAREA = 0 BYPASS     X04SVHS 00176800
.*                                      CHKING FOR AM=VSAM      X04SVHS 00176900
         AIF   ('&AM' EQ '' OR '&AM' EQ 'VSAM').GENMA           X04SVHS 00177000
.*                                      IF NOT VSAM ACB, ERROR  X04SVHS 00177100
         IDAERMAC 24,MAREA              ISSUE ERR MSG              @L9C 00177200
         AGO   .GENM0                                           X04SVHS 00177300
.GENMA   ANOP                                                   X04SVHS 00177400
         DC    A(&MAREA)  .             MESSAGE AREA            X04SVHS 00177500
         AGO   .GPSWD                                           X04SVHS 00177600
.GENM0   ANOP                                                   X04SVHS 00177700
         DC    A(0)                                             X04SVHS 00177800
.GPSWD   ANOP                                                   X04SVHS 00177900
         DC    A(&PASSWD) .             PASSWORD POINTER                00178000
         DC    A(&EXLST) .              EXIT LIST POINTER               00178100
         AIF   ('&DDNAME' NE '' AND '&AM' NE 'VTAM').DDNMCHK   @ZA31869 00178200
         AIF   ('&DDNAME' EQ '').VTAM   NO DDNAME SPECIFIED?     X03004 00178300
         IDAERMAC 24,DDNAME             NOT A VTAM KEYWORD         @L9C 00178400
         AGO   .ACBB2                                            X03004 00178500
.VTAM    ANOP                                                    X03004 00178600
         AIF   ('&AM' NE 'VTAM').NONAME TRY LABEL IF NO VTAM     X03004 00178700
         DC    X'FF' .                  INDICATE VTAM            X03004 00178800
         DC    XL7'00' .                PAD WITH ZEROS           X03004 00178900
         AGO   .SETR31                                             @LPC 00179000
.NONAME  ANOP                                                    X03004 00179100
         AIF   ('&NAME' EQ '').BLANK    IS THERE A LABEL                00179200
         DC    CL8'&NAME' .             DDNAME                          00179300
         AGO   .SETR31                                             @LPC 00179400
.BLANK   DC    CL8' ' .                 DDNAME                          00179500
         AGO   .SETR31                                             @LPC 00179600
.DDNMCHK ANOP                                                  @ZA31869 00179700
         AIF   ('&CTRLACB' NE 'YES').DDNMC2                        @LQA 00179800
         IDAERMAC 19,CTRLACB,DDNAME                                @LQA 00179900
.DDNMC2  AIF   (K'&DDNAME GT 8).DDNMERR                            @LQC 00180000
         AIF   (('&DDNAME'(1,1) LT 'A' OR '&DDNAME'(1,1) GT 'Z') AND   -00180100
               ('&DDNAME'(1,1) NE '#' AND '&DDNAME'(1,1) NE '@' AND    -00180200
               '&DDNAME'(1,1) NE '$')).DDNMERR CHECK FOR VALID @ZA37504 00180300
         AGO   .ACBB2                          DDNAME          @ZA31869 00180400
.DDNMERR IDAERMAC 3,DDNAME,&DDNAME      INVALID DDNAME             @L9C 00180500
.ACBB2   DC    CL8'&DDNAME' .           DDNAME                          00180600
.SETR31  ANOP                           SET RMODE31 VALUES         @LPA 00180700
         AIF   ('&RMODE31' EQ '').ACBB3                            @LPA 00180800
         AIF   ('&RMODE31' NE 'ALL').SETR31B    RMODE31=ALL USED?  @LPA 00180900
&R31B    SETB  1                        SET BUFFER ABOVE REQUESTED @LPA 00181000
&R31C    SETB  1                        SET CB ABOVE REQUESTED     @LPA 00181100
         AGO   .ACBB3                                              @LPA 00181200
.SETR31B AIF   ('&RMODE31' NE 'BUFF').SETR31C   RMODE31=BUFF USED? @LPA 00181300
&R31B    SETB  1                        SET BUFFER ABOVE REQUESTED @LPA 00181400
         AGO   .ACBB3                                              @LPA 00181500
.SETR31C AIF   ('&RMODE31' NE 'CB').SETR31D     RMODE31=CB USED?   @LPA 00181600
&R31C    SETB  1                        SET CB ABOVE REQUESTED     @LPA 00181700
         AGO   .ACBB3                                              @LPA 00181800
.SETR31D AIF   ('&RMODE31' EQ 'NONE').ACBB3     RMODE31=NONE?      @LPA 00181900
         IDAERMAC 3,RMODE31,&RMODE31    BAD RMODE31 PARAMETER      @LPA 00182000
.ACBB3   DC    B'&R31B.&R31C.000010'    OFLAGS                     @LPC 00182100
         DC    AL1(0) .                 ERFLAGS                         00182200
         DC    B'&IDAIFL1'              INFLGS(1)               X04SVHS 00182300
         DC    B'00000000'              INFLGS(2)               X04SVHS 00182400
         DC    A(&JFCB) .               OPENJ JFCB POINTER              00182500
         DC    A(&BUFSP)                BUFFER SPACE                    00182600
         AIF   ('&BLKSIZE' EQ '0').CHKMLN                       X04SVHS 00182700
         AIF   ('&MLEN' EQ '0').BLKSET                          X04SVHS 00182800
.*             IF BLKSIZE AND MLEN ARE BOTH SPECIFIED           X04SVHS 00182900
.*             IT IS AN ERROR                                   X04SVHS 00183000
         IDAERMAC 19,BLKSIZE,MLEN                                  @L9C 00183100
.BLKSET  ANOP                                                   X04SVHS 00183200
         DC    AL2(&BLKSIZE) .          BLOCK SIZE                      00183300
         AGO   .LRECLST                                         X04SVHS 00183400
.CHKMLN  ANOP                                                   X04SVHS 00183500
         AIF   ('&MLEN' EQ '0').BLKSET                          X04SVHS 00183600
.*                                      IF MLEN IS SPEC,        X04SVHS 00183700
.*                                      CHECK FOR VSAM ACB      X04SVHS 00183800
         AIF   ('&AM' NE '' AND '&AM' NE 'VSAM').MLERR          X04SVHS 00183900
.*                                      IF NOT VSAM ACB, ERROR  X04SVHS 00184000
         DC    AL2(&MLEN)               MESSAGE LENGTH          X04SVHS 00184100
         AGO   .LRECLST                                         X04SVHS 00184200
.MLERR   IDAERMAC 24,MLEN               ISSUE ERR MSG              @L9C 00184300
         AGO   .BLKSET                                          X04SVHS 00184400
.LRECLST ANOP                                                   X04SVHS 00184500
         DC    AL2(&LRECL) .            RECORD SIZE                     00184600
         AIF   ('&USERPTR' EQ '0').UPTR  NO USER PTR PROVIDED           00184700
         DC    A(&USERPTR)              ADDRESS OF USER WORKAREA        00184800
         AGO   .CBMWP                                                   00184900
.UPTR    DC    A(0) .                   USER WORKAREA POINTER           00185000
.CBMWP   DC    A(0)                     CONTROL BLOCK MANIPULATION      00185100
*                                       WORKAREA POINTER                00185200
         AIF   ('&AM' EQ 'VTAM').SETAPP  WAS VTAM SPECIFIED      X03004 00185300
         AIF   ('&APPLID' EQ '0').NONAPP  WAS APPLID SPECIFIED   X03004 00185400
         IDAERMAC 18,APPLID                                        @L9C 00185500
.NONAPP  ANOP                           IGNORE APPLID            X03004 00185600
         DC    A(0) .                   PTR TO APPLICATION NAME  X03004 00185700
         MEXIT                          END THE MACRO            X03004 00185800
.SETAPP  ANOP                           VTAM SPECIFIED           X03004 00185900
         DC    A(&APPLID) .             PTR TO APPLICATION NAME  X03004 00186000
         LA    15,32                    SET RETURN CODE(VTAM)    X03004 00186200
         BR    14                       RETURN TO CALLER(VTAM)   X03004 00186400
         DC    H'0'                     RESERVED                 X03004 00186600
         DC    F'0'                     RESERVED                 X03004 00186800
.OUT     MEND                                                           00187000
