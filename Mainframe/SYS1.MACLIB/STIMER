*********************************************************************** 00001900
*                                                                     * 00003800
* MACRO NAME - STIMER                                                 * 00005700
*                                                                     * 00007600
* DESCRIPTIVE NAME - Set TIMER to a specified interval                * 00009500
*                                                                     * 00011400
* PROPRIETARY STATEMENT =                                             * 00013300
*                                                                     * 00015200
*   LICENSED MATERIALS-PROPERTY OF IBM                                * 00016700
*   THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                       * 00018200
*   5695-047 (C)COPYRIGHT IBM CORP. 1981, 1990                        * 00019700
*   SEE COPYRIGHT INSTRUCTIONS                                    @L1C* 00021200
*                                                                     * 00022800
* STATUS -  HBB4410                                               @L1C* 00023400
*                                                                     * 00024000
* FUNCTION -                                                          * 00024700
*                                                                     * 00026600
*    The STIMER macro allows the issuer to set a timer to a specified * 00029900
*    interval(less than 24 hours) or to an interval that will expire  * 00033200
*    at a specified time of day.                                      * 00036500
*                                                                     * 00039900
*    When the time interval expires, the EXIT routine, if specified,  * 00040400
*    will be given control asynchronously.                            * 00040900
*    An error exit may also be specified which will be given control  * 00041400
*    if the STIMER function cannot be performed.                      * 00041900
*    The Service Routine IEAVRT00 will determine the AMODE    @ZMC3178* 00042400
*    of the macro issuer from the PSW saved by SVC FLIH.      @ZMC3178* 00042900
*                                                                     * 00043700
* INPUT -                                                             * 00046800
*    The type of time intervals that may be specified are:            * 00049900
*                                                                     * 00053200
*    REAL - measured continuously in real-time                        * 00055400
*    TASK - measured when the task is active                          * 00057600
*    WAIT - measured continuously in real-time. The task is placed in * 00059800
*           a wait until the interval expires.                        * 00062000
*                                                                     * 00064600
*    The asynchronous EXIT Routine may be specified only for REAL or  * 00074400
*    TASK time intervals. The EXIT Routine address may either be an   * 00084200
*    RX-type address or register (0),(2)-(12), or (15). The EXIT      * 00094000
*    routine will receive control in the addressing mode in effect    * 00103800
*    at the time the STIMER macro is issued.                          * 00113600
*                                                                     * 00123500
*    The TIMER interval may be specified in one of several ways:      * 00123600
*         (one of the following must be specified)                    * 00123700
*         BINTVL=ADDR - Address of a 32-bit unsigned binary number    * 00124200
*                       containing the time interval value. The       * 00124300
*                       value of the low-order bit is .01 seconds.    * 00124400
*         DINTVL=ADDR - Address of a double word on a double word     * 00124500
*                       boundary containing the time interval value.  * 00124600
*                       The time interval is presented as unpacked    * 00124700
*                       decimal digits of the form: HHMMSSth.         * 00124800
*         GMT=ADDR    - Address of an 8-byte area containing the      * 00124900
*                       Greenwich Mean Time at which the interval     * 00125000
*                       is to complete. Presented in the same form as * 00125100
*                       DINTVL (HHMMSSth).                            * 00125200
*         LT=ADDR     - Local Time.                                   * 00125300
*                       Address of a double word on a double word     * 00125400
*                       boundary containing the time of day at which  * 00125500
*                       the interval is to complete. Presented in the * 00125600
*                       same form as DINTVL(HHMMSSth).                * 00125700
*         MICVL=ADDR  - Address of a double word on a double word     * 00125800
*                       boundary containing the time interval value.  * 00125900
*                       The time interval is presented as an          * 00126000
*                       unsigned 64-bit binary number. Bit 51 is the  * 00126100
*                       low order bit of the interval value and       * 00126200
*                       equivalent to 1 microsecond.                  * 00126300
*         TOD=ADDR    - Time of Day. (Same as LT above. The use of    * 00126400
*                       this keyword is not encouraged, it is         * 00126500
*                       maintained only for compatibility.)           * 00126600
*         TUINTVL=ADDR- Address of a fullword on a fullword boundary  * 00126700
*                       containing the time interval value presented  * 00126800
*                       as an unsigned 32-bit binary number, the low- * 00126900
*                       order bit of which has a value of 1 timer     * 00127000
*                       unit (approximately 26.04166 microseconds).   * 00127100
*         NOTE: GMT, LT, and TOD may not be specified for             * 00127200
*                       TASK time intervals.                          * 00127300
*                                                                     * 00129200
*         The address specified for the time interval value may be    * 00132300
*         either an RX-type address of register (1) or (2)-(12).      * 00135400
*                                                                     * 00138700
*    An error exit may be specified (via the ERRET=ADDR option) which * 00138900
*    will be given control if the STIMER function cannot be performed.* 00139100
*    The address of the error exit may be either an RX-type address   * 00139300
*    or register (2) - (12)                                           * 00139500
*                                                                     * 00139700
*                                                                     * 00139900
* NOTES -                                                             * 00140400
*                                                                     * 00140900
*   RESTRICTIONS - None                                               * 00141400
*                                                                     * 00141900
*   DEPENDENCIES - None                                               * 00142400
*                                                                     * 00142900
*     CHARACTER-CODE DEPENDENCIES - EBCDIC                            * 00143400
*                                                                     * 00143900
* COMPONENT  -  SC1CV (TIMER)                                         * 00144400
*                                                                     * 00144900
* DISTRIBUTION LIBRARY - AMACLIB                                      * 00145400
*                                                                     * 00145900
*                                                                     * 00146400
*  CHANGE-ACTIVITY =                                                  * 00146900
*        PCC3178            - Change interface to service rtn @ZMC3178* 00147400
*    $L1=CLOCK   HBB4410  880411  PD16LD:  Local Time (LT)            * 00147900
*    $D1=DCR1006 HBB4410  880819  PD16CU:  ETR support                * 00148400
*                                                                     * 00148900
*********************************************************************** 00149400
         MACRO                                                          00150000
&NAME    STIMER &TYPE,&EXIT,&DINTVL=,&BINTVL=,&TUINTVL=,&TOD=,&MICVL=,&*00200000
               GMT=,&LT=,&ERRET=                                   @D1C 00250000
.* A 000000-999999                                             @G860PPK 00252500
.********************************************************************** 00255000
.*       DECLARE THE GLOBAL VARIABLE AND INVOKE THE SPLEVEL MACRO     * 00260000
.*       TO DETERMINE WHICH FORMAT OF THE PARAMETER LIST SHOULD       * 00265000
.*       BE BUILT. IF THE MVS/370 FORMAT IS REQUIRED, INVOKE THE      * 00270000
.*       STIMER7 MACRO.                                       @G860PXK* 00275000
.********************************************************************** 00280000
         GBLC  &SYSSPLV                                        @G860PXK 00285000
         SPLEVEL TEST                                          @G860PXK 00290000
         AIF   ('&SYSSPLV' EQ '1').SP1  WHICH FORMAT ?         @G860PXK 00295000
*        MACDATE = 08/19/88                                        @L1C 00300000
         LCLB  &B(9)              Bit map of parameters            @D1C 00350000
         LCLA  &SUM               Flag byte                             00400000
         LCLC  &A                 Time interval value                   00410000
.* -------------------------------------------------------------------- 00420000
.* --------------------------------CHECK FOR PARAMETER ERRORS---------- 00430000
.* -------------------------------------------------------------------- 00440000
         AIF   ('&DINTVL&BINTVL&TUINTVL&TOD&LT&MICVL&GMT' EQ '' OR     *00450000
               '&TYPE' EQ '').E2   TYPE or units missing           @D1C 00471400
         AIF   ('&EXIT' NE '' AND '&TYPE' EQ 'WAIT').NMNOTE             00492800
.*                                 EXIT SPECIFIED WITH WAIT             00514200
.* -------------------------------------------------------------------- 00535600
.* --------------------------------SET LOCAL BIT VALUES --------------- 00557000
.* -------------------------------------------------------------------- 00578400
&B(1)    SETB  ('&TYPE' EQ 'TASK')                                      00600000
&B(2)    SETB  ('&TYPE' EQ 'REAL')                                      00650000
&B(3)    SETB  ('&TYPE' EQ 'WAIT')                                      00700000
&B(4)    SETB  ('&DINTVL' NE '')                                        00750000
&B(5)    SETB  ('&BINTVL' NE '')                                        00800000
&B(6)    SETB  ('&TUINTVL' NE '')                                       00850000
&B(7)    SETB  ('&TOD' NE '' OR '&LT' NE '')                       @L1C 00900000
&B(8)    SETB  ('&MICVL' NE '')                                         00950000
&B(9)    SETB  ('&GMT' NE '')                                           00979200
.* -------------------------------------------------------------------- 01008500
.* --------------------------------CHECK FOR PARAMETER ERRORS---------- 01017000
.* -------------------------------------------------------------------- 01025500
         AIF   (&B(1)+&B(2)+&B(3) NE 1).E1        PARAMETER ERROR -     01034000
.*                                 TYPE SPECIFIED IS INVALID            01042500
         AIF   (&B(1) AND (&B(7) OR &B(9))).E3                     @D1C 01045600
.*                                 PARAMETER ERROR - TASK               01048700
.*                                 SPECIFIED WITH GMT LT OR TOD         01051800
         AIF  (&B(4)+&B(5)+&B(6)+&B(7)+&B(8)+&B(9) NE 1).E4        @D1C 01054900
.*                                 Too many time intervals              01058000
         AIF  ('&TOD' NE '' AND '&LT' NE '').E4                    @L1A 01061100
.*                                 TOD and LT are the same              01064200
.* -------------------------------------------------------------------- 01068000
.* --------------------------------INDICATE NEW FORMAT PARAMETERS------ 01076500
.* -------------------------------------------------------------------- 01085000
&SUM     SETA  128                 NEW FORMAT PARMS                     01091300
.*                                 Time interval value                  01097600
&A       SETC  '&DINTVL&BINTVL&TUINTVL&TOD&LT&MICVL&GMT'           @D1C 01103900
.* -------------------------------------------------------------------- 01110500
.* --------------------------------GENERATE LABEL---------------------- 01119000
.* -------------------------------------------------------------------- 01127500
         AIF   ('&NAME' EQ '').NONAME             NO LABEL ON MACRO     01136000
&NAME    DS    0H                                                       01144500
.* -------------------------------------------------------------------- 01153000
.* --------------------------------PUT TIME VALUE ADDRESS IN REG 1 ---- 01161500
.* -------------------------------------------------------------------- 01170000
.NONAME  AIF   ('&A' EQ '(1)').CHKB               TIME VALUE IN REG 1   01178500
         AIF   ('&A'(1,1) EQ '(').REGA            TIME VALUE IN REG     01187000
         LA    1,&A                               LOAD ADDR TIME VALUE  01195500
         AGO   .CHKB                              CHECK FOR EXIT RTN    01204000
.REGA    ANOP                                     TIME VALUE IN REG     01212500
         LR    1,&A                               LOAD ADDR TIME VALUE  01221000
.* -------------------------------------------------------------------- 01229500
.* --------------------------------PUT EXIT ADDRESS IN REG 15 --------- 01238000
.* -------------------------------------------------------------------- 01246500
.CHKB    AIF   ('&EXIT' EQ '').NOEXT              NO EXIT RTN           01255000
         AIF   ('&EXIT' EQ '(15)').PRMX         EXIT RTN IN 15 @ZMC3178 01263500
         AIF   ('&EXIT'(1,1) EQ '(').REGB         EXIT RTN IN REG       01272000
         LA    15,&EXIT                           LOAD EXIT ADDRESS     01280500
         AGO   .PRMX                              CHECK FOR ERRET       01306000
.REGB    LR    15,&EXIT(1)                        LOAD EXIT ADDRESS     01314500
         AGO   .PRMX                              CHECK FOR ERRET       01331500
.NOEXT   SR    15,15                              INDICATE NO EXIT      01340000
.* -------------------------------------------------------------------- 01348500
.* --------------------------------PUT OPTION BYTE IN REG 0 ----------- 01357000
.* -------------------------------------------------------------------- 01365500
.PRMX    AIF   ('&ERRET' EQ '').NORET             NO ERROR ROUTINE      01374000
&SUM     SETA  &SUM+8                             RETURN CODE FLAG      01382500
.NORET   ANOP                                     NO ERROR ROUTINE      01391000
&SUM     SETA  &SUM+&B(3)+3*&B(2)+16*&B(5)+48*&B(4)+112*&B(7)+32*&B(8)+*01400000
               96*&B(9)                                            @D1C 01450000
         LA    0,&SUM.(0,0)                      LOAD FLAG BYTE         01500000
         SLL   0,24(0)                           SHIFT TO HI-ORDER BYTE 01550000
.* -------------------------------------------------------------------- 01600000
.* --------------------------------ISSUE SVC -------------------------- 01650000
.* -------------------------------------------------------------------- 01700000
.SVC     SVC   47                                ISSUE STIMER SVC       02300000
.* -------------------------------------------------------------------- 02331200
.* --------------------------------PROCESS ERRET OPTION---------------- 02362400
.* -------------------------------------------------------------------- 02393600
         AIF   ('&ERRET' EQ '').DONE             NO ERROR ROUTINE       02424800
         LTR   15,15                             ANY RETURN CODE        02456000
         AIF   ('&ERRET'(1,1) EQ '(').BCR        ERRET IN REGISTER      02487200
         BNZ   &ERRET                            YES. BR TO USER EXIT   02518400
         AGO   .DONE                                                    02550000
.BCR     BCR   7,&ERRET(1)                       YES. BR TO USER EXIT   02600000
.DONE    MEXIT                                                          02650000
.* -------------------------------------------------------------------- 02691600
.* --------------------------------MNOTES FOR ERRORS ------------------ 02733200
.* -------------------------------------------------------------------- 02774800
.NMNOTE  ANOP                                                           02816400
         MNOTE 12,'AN EXIT ADDRESS IS NOT VALID WITH WAIT - &EXIT' @L1C 02858000
         MEXIT                                                          02900000
.E1      ANOP  ,                                                        02950000
         MNOTE 12,'FIRST OPERAND IS NOT VALID - &TYPE'             @L1C 03000000
         MEXIT                                                          03050000
.E2      ANOP  ,                                                        03083300
         MNOTE 12,'REQUIRED OPERAND(S) NOT SPECIFIED'                   03116600
         MEXIT                                                          03150000
.E3      ANOP                                                           03183300
         MNOTE 12,'GMT, LT, OR TOD IS NOT VALID WITH TASK'         @D1C 03216600
         MEXIT                                                          03250000
.E4      ANOP  ,                                                        03256200
         MNOTE 12,'ONLY ONE TIME INTERVAL IS ALLOWED'              @L1A 03262400
         MEXIT                                                          03268600
.* -------------------------------------------------------------------- 03275000
.********************************************************************** 03278500
.*       INVOKE THE MVS/370 FORMAT                            @G860PXK* 03282000
.********************************************************************** 03285500
.SP1     ANOP                                                  @G860PXK 03289000
&NAME    STIMER7 &TYPE,&EXIT,DINTVL=&DINTVL,BINTVL=&BINTVL,            *03291700
               TUINTVL=&TUINTVL,TOD=&TOD,MICVL=&MICVL,GMT=&GMT,        *03294400
               ERRET=&ERRET                                             03297100
         MEND                                                           03300000
