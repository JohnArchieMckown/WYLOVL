         MACRO                                                          00050000
         IST6MCTE &AREA=,&AREALEN=,&QUALIFY=,&CONVID=,&BRANCH=,        X00100000
               &ECB=,&EXIT=,&OPTCD=,&INVALP1=,&INVALP2=,&VTRINA=,      X00150000
               &VTRINL=,&VTROUTA=,&VTROUTL=                        @D1C 00200000
.*                                                                      00250000
.* /***** START OF SPECIFICATIONS ************************************/ 00300000
.* /*                                                                */ 00350000
.* /*  MACRO NAME: IST6MCTE                                          */ 00400000
.* /*                                                                */ 00450000
.* /*  DESCRIPTIVE NAME: CONTROL = TEST STATUS PROCESSOR             */ 00500000
.* /*                                                                */ 00550000
.* /*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM               */ 00600000
.* /*                                                                */ 00650000
.* /*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"*/ 00700000
.* /*                                                                */ 00750000
.* /*             5695-117 (C) COPYRIGHT IBM CORP. 1993, 1994.       */ 00800000
.* /*             ALL RIGHTS RESERVED.                               */ 00850000
.* /*                                                                */ 00900000
.* /*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -          */ 00950000
.* /*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY       */ 01000000
.* /*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.           */ 01050000
.* /*                                                                */ 01100000
.* /*             SEE COPYRIGHT INSTRUCTIONS.                        */ 01150000
.* /*                                                                */ 01200000
.* /*                                                                */ 01250000
.* /*  FUNCTION:                                                     */ 01300000
.* /*      GENERATES IN-LINE CODE FOR PROCESSING THE                 */ 01350000
.* /*      CONTROL=TESTSTAT PARAMETER ON THE APPCCMD MACRO.          */ 01400000
.* /*                                                                */ 01450000
.* /*  NOTES:                                                        */ 01500000
.* /*                                                                */ 01550000
.* /*  PROCESSOR: ASSEMBLER                                          */ 01600000
.* /*                                                                */ 01650000
.* /*  CREATED BY:                                                   */ 01700000
.* /*                                                                */ 01750000
.* /*  INTERNAL MACROS:                                              */ 01800000
.* /*      IST6MCUA,IST6MCUP,IST6MCUC,IST6MCER                       */ 01850000
.* /*                                                                */ 01900000
.* /*  EXTERNAL REFERENCES: NONE                                     */ 01950000
.* /*                                                                */ 02000000
.* /*  CHANGE ACTIVITY:                                              */ 02050000
.* /*                                                                */ 02100000
.* /*  $MAC(IST6MCTE),COMP(API),PROD(VTAM): CONTROL=TESTSTAT         */ 02150000
.* /*                                                                */ 02200000
.* /*  FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS         */ 02250000
.* /*  ---- -------- ------- ------ ------  ---------------------    */ 02300000
.* /*  $I0= FT@@@MST APPCTST 920212 510689: APPC TESTSTAT            */ 02350000
.* /*  $L1= KA@@@PLD APPCRR  930726 446638: DCE Security Services    */ 02400000
.* /*  $Y1= P114821  APPCRR  940519 576146: DCE Security Services    */ 02450000
.* /*  $L2= KA@DR006 APPCRR  940713 576146: Vector Format            */ 02500000
.* /*  $D1= ID@@@MST FPAPI   950310 143576: Fast Path API for APPC   */ 02525000
.* /*                                                                */ 02550000
.* /***** END OF SPECIFICATIONS **************************************/ 02600000
.*                                                                      02650000
         GBLB  &ISTGEN                                                  02700000
         GBLB  &ISTTE                                                   02750000
         AIF   (&ISTTE EQ 1).TEINVKW                                    02800000
*                                                                       02850000
*  IST6MCTE - ACF/VTAM                                                  02900000
*                                                                       02950000
*  COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                      03000000
*                                                                       03050000
*             THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM"       03100000
*                                                                       03150000
*             5695-117 (C) COPYRIGHT IBM CORP. 1993.                    03200000
*             ALL RIGHTS RESERVED.                                      03250000
*                                                                       03300000
*             U.S. GOVERNMENT USERS RESTRICTED RIGHTS -                 03350000
*             USE, DUPLICATION OR DISCLOSURE RESTRICTED BY              03400000
*             GSA ADP SCHEDULE CONTRACT WITH IBM CORP.                  03450000
*                                                                       03500000
*             SEE COPYRIGHT INSTRUCTIONS.                               03550000
*                                                                       03600000
&ISTTE   SETB  1                                                        03650000
.TEINVKW ANOP                                                           03700000
         IST6MCUC TESTSTAT,CONTROL,INVALKW=&INVALP1                     03750000
         IST6MCUC TESTSTAT,CONTROL,INVALKW=&INVALP2                     03800000
.*                                     CHECK INVALID APPCCMD            03850000
.TEAREA  ANOP                       ** AREA= PROCESSING **              03900000
         AIF   ('&AREA' EQ '').TEALEN                                   03950000
         IST6MCUA KWVAL=&AREA,LOC=RPLAREA-IFGRPL,BASEREG=1,            X04000000
               KW=AREA_ADDRESS                                          04050000
.*                                     PUT AREA ADDRESS IN RPL          04100000
.TEALEN  ANOP                       ** AREALEN= PROCESSING **           04150000
         AIF   ('&AREALEN' EQ '').TEQFY                                 04200000
         IST6MCUA KWVAL=&AREALEN,LOC=RPLBUFL-IFGRPL,BASEREG=1,         X04250000
               KW=AREA_LENGTH                                           04300000
.*                                     PUT AREA LENGTH IN RPL           04350000
.TEQFY   ANOP                       ** QUALIFY= PROCESSING **           04400000
         AIF   ('&QUALIFY' EQ '').TEID                                  04450000
.TEQFY1  ANOP                                                           04500000
         AIF   ('&QUALIFY' NE 'ALL').TEQFY2                             04550000
         IST6MCUC ALL,QUALIFY,INVALKW=(CONVID=&CONVID)                  04600000
         AIF   (NOT &ISTGEN).TECOMKW                                    04650000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6QALL                           04700000
*                                      PUT QALL CODE IN APPC AREA       04750000
         AGO   .TECOMKW                                                 04800000
.TEQFY2  ANOP                                                           04850000
         AIF   ('&QUALIFY' NE 'IALL').TEQFY3                            04900000
         IST6MCUC IALL,QUALIFY,INVALKW=(CONVID=&CONVID)                 04950000
         AIF   (NOT &ISTGEN).TECOMKW                                    05000000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6IALL                           05050000
*                                      PUT IALL CODE IN APPC AREA       05100000
         AGO   .TECOMKW                                                 05150000
.TEQFY3  ANOP                                                           05200000
         AIF   ('&QUALIFY' NE 'SPEC').TEQFY4                            05250000
         AIF   (NOT &ISTGEN).TECOMKW                                    05300000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6SPEC                           05350000
*                                      PUT SPEC CODE IN APPC AREA       05400000
         AGO   .TEID                                                    05450000
.TEQFY4  ANOP                                                           05500000
         AIF   ('&QUALIFY' NE 'ISPEC').TEQFYER                          05550000
         AIF   (NOT &ISTGEN).TECOMKW                                    05600000
         MVI   RPL6QUAL-ISTRPL6X(14),RPL6ISPC                           05650000
*                                      PUT ISPEC CODE IN APPC AREA      05700000
         AGO   .TEID                                                    05750000
.TEQFYER ANOP                                                           05800000
         IST6MCER 5,QUALIFY,&QUALIFY                                    05850000
.TEID    ANOP                       ** CONVID= PROCESSING **            05900000
         AIF   ('&CONVID' EQ '').TECOMKW                                05950000
         IST6MCUA KWVAL=&CONVID,LOC=RPL6CNVD-ISTRPL6X,BASEREG=14,      X06000000
               KW=CONVID                                                06050000
.*                                     PUT CONVID IN APPC AREA          06100000
.TECOMKW ANOP                       ** COMMON KEYWORDS **               06150000
         IST6MCUP OPTCD=&OPTCD,ECB=&ECB,EXIT=&EXIT,BRANCH=&BRANCH,     X06200000
               CKCMODE=NO,             CHECK CONMODE AND CONXMOD       X06250000
               CKSYN=YES,              ASY/SYN IS VALID FOR OPTCD      X06300000
               CKBUFF=NO               BUFFLST/NBUFFLST IS INVALID      06350000
.*                                     VALIDATE & SET COMMON KEYWORDS   06400000
.TEVECT  ANOP                          ENSURE BOTH PARMS CODED OR NOT   06450000
.*                                     CODED                  @L1A @Y1M 06500000
         AIF   (('&VTRINA' EQ '') AND ('&VTRINL' EQ '')).TEVOUT    @D1C 06550000
         AIF   (('&VTRINA' EQ '') OR ('&VTRINL' EQ '')).TEERVI     @D1C 06600000
         IST6MCUA KWVAL=&VTRINA,LOC=RPL6VAIA-ISTRPL6X,BASEREG=14,      X06750000
               KW=VTAM_TO_APPL_VECTOR_ADDRESS                      @D1C 06800000
.*                                     STORE VECTOR AREA INPUT ADDRESS  06850000
.*                                                                 @L1A 06900000
         IST6MCUA KWVAL=&VTRINL,LOC=RPL6VAIL-ISTRPL6X,BASEREG=14,      X06950000
               KW=VTAM_TO_APPL_VECTOR_LENGTH,LEN=2                 @D1C 07000000
.*                                     STORE VECTOR AREA INPUT LENGTH   07050000
.*                                                                 @L1A 07100000
         AGO   .TEVOUT                                             @D1C 07118400
.TEERVI  ANOP                                                 @L1A @Y1M 07136800
         IST6MCER 13,VTRINA,VTRINL     ONLY ONE PARM CODED         @D1C 07155200
.TEVOUT  ANOP                          ENSURE BOTH PARMS ARE CODED      07173600
.*                                     OR BOTH ARE NOT CODED       @D1A 07192000
         AIF   (('&VTROUTA' EQ '') AND ('&VTROUTL' EQ '')).TEREQ   @D1A 07210400
         AIF   (('&VTROUTA' EQ '') OR ('&VTROUTL' EQ '')).TEERVO   @D1A 07228800
         IST6MCUA KWVAL=&VTROUTA,KW=APPL_TO_VTAM_VECTOR_ADDRESS,       X07247200
               LOC=RPL6VAOA-ISTRPL6X,BASEREG=14                    @D1A 07265600
.*                                     PUT VECTOR AREA INPUT ADDRESS    07284000
.*                                     IN APPC AREA                @D1A 07302400
         IST6MCUA KWVAL=&VTROUTL,KW=APPL_TO_VTAM_VECTOR_LENGTH,LEN=2,  X07320800
               LOC=RPL6VAOL-ISTRPL6X,BASEREG=14                    @D1A 07339200
.*                                     PUT VECTOR AREA INPUT LENGTH     07357600
.*                                     IN APPC AREA                @D1A 07376000
         AGO   .TEREQ                                              @D1A 07394400
.TEERVO  ANOP                                                      @D1A 07412800
         IST6MCER 13,VTROUTA,VTROUTL   ONLY ONE PARM CODED         @D1A 07431200
.TEREQ   ANOP                       ** MACRO REQUEST TYPE **            07450000
         AIF   (NOT &ISTGEN).TEEXIT                                     07500000
         MVI   RPL6REQ-ISTRPL6X(14),RPL6TSTS                            07550000
*                                      PUT TESTSTAT REQUEST IN RPL6     07600000
.TEEXIT  MEND                                                           07650000
