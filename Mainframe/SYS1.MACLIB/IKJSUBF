         MACRO                                                                  
&LABEL   IKJSUBF &DUMMY                                                         
.**01* PROPRIETARY STATEMENT=                                         *         
.* **PROPRIETARY_STATEMENT*********************************************         
.*                                                                    *         
.*                                                                    *         
.*  LICENSED MATERIALS - PROPERTY OF IBM                              *         
.*  THIS MACRO IS "RESTRICTED MATERIALS OF IBM"                       *         
.*  5685-025 (C) COPYRIGHT IBM CORP. 1981, 1991                       *         
.*  SEE COPYRIGHT INSTRUCTIONS                                        *         
.*                                                                    *         
.*  STATUS= JTE23X2                                                   *         
.*                                                                    *         
.* **END_OF_PROPRIETARY_STATEMENT**************************************         
.**********************************************************************         
.*                                                                    *         
.* NAME = SUBFIELD (IKJSUBF) PARSE CONTROL ENTRY (PCE)                *         
.*                                                                    *         
.* FUNCTION =                                                         *         
.*                                                                    *         
.*   1) BUILDS THE IKJPARM PCE.                                       *         
.*      INDICATES TO PARSE THE BEGINNING OF A PCL.                    *         
.*                                                                    *         
.*   2) BUILDS THE IKJSUBF PCE.                                       *         
.*      INDICATES TO PARSE THAT A SUBFIELD FOLLOWS THIS PCE.          *         
.*                                                                    *         
.*   3) BUILDS THE IKJENDP PCE.                                       *         
.*      INDICATES TO PARSE THE END OF A PCL.                          *         
.*                                                                    *         
.* CHANGE ACTIVITY =                                                  *         
.*                                                                    *         
.*   CREATED FOR TSO/E RELEASE 2.0 HBB2102                            *         
.*                                                                    *         
.*                                                                    *         
.*   JTE23X2 - TSO/E 2.3.1 EXTENDED IKJUNFLD SUPPORT        @E23X2C6  *         
.*                                                                    *         
.*                                                                    *         
.**********************************************************************         
.* RELEASE 20 SUPPORT CODE -- 20035                                             
         GBLC  &IKJCSNM,&IKJKWST                                                
         LCLB  &FENDBIT                                                         
&FENDBIT SETB  0                                                                
         AIF   (N'&SYSLIST(1) EQ 0).GOOD                     @ZA65509           
.*                                                                              
.* THE ONLY POSITIONAL PARAMETER ALLOWED ON IKJSUBF MACRO IS NOCSECT            
.* WHICH IS USED ONLY WHEN INTERNALLY CALLED FROM IKJPARM.                      
.*                                                                              
         AIF   ('&DUMMY' EQ 'NOCSECT').NOCSECT                                  
         MNOTE 4,'IKJSUBF SHOULD HAVE NO POSITIONAL PARAMETERS'                 
.GOOD    AIF   ('&LABEL' EQ '').ERROR1                                          
         AGO   .GOOD1                                                           
.ERROR1  ANOP                                                                   
         MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'                         
&FENDBIT SETB  1                                                                
.GOOD1   AIF   ('&IKJCSNM' NE '').GOOD2                                         
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'             
.END     MEXIT                                                                  
.GOOD2   AIF   (&FENDBIT).END                                                   
         GBLA  &UNKSUB       NEEDED FOR CHECKING UNKNOWN EXT @E23X2C6           
&UNKSUB  SETA  &SYSNDX          SET FOR CHECKING UNKNOWN EXT @E23X2C6           
&IKJCSNM CSECT ,                       RESUME PCL CSECT                         
.NOCSECT AIF   ('&LABEL' EQ '').NOENDF IF INTERNAL CALL BRANCH                  
         AIF   ('&IKJKWST' EQ '').NOEQU IS KEYWORD SET                          
&IKJKWST EQU   *-&IKJCSNM              OFFSET TO END OF FIELD                   
.NOEQU   ANOP                                                                   
         DC    AL1(0)                  END OF FIELD                             
.NOENDF  ANOP                                                                   
&IKJKWST SETC  'IKJ@&SYSNDX'           GENERATE SYMBOL FOR FIRST KEYWD          
&LABEL   DC    AL2(&IKJKWST)           OFFSET TO FIRST KEYWORD OR END           
&SYSECT  CSECT ,                       RESUME USERS CSECT                       
         MEND                                                                   
