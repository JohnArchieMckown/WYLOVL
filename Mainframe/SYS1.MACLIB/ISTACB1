        MACRO                                                           00050000
&NAME    ISTACB1 &DDNAME=,&EXLST=0,&MACRF=,                    @ZA34940C00100000
               &JFCB=0,&BUFND=0,&BUFNI=0,&PASSWD=0,                    C00150000
               &BLKSIZE=0,&LRECL=0,&BUFSP=0,                           C00200000
               &CCTYPE=,&STRNO=1,&CATALOG=NO,                  @ZA07566C00250000
               &BSTRNO=0,&MAREA=0,&MLEN=0,&CRA=,                X04SVHSC00300000
               &AM=,&APPLID=0,&USERPTR=0,                      @ZA34940C00350000
               &PARMS=,&PARM2=,&PARM3=                         @ZA34940 00400000
.*                                                                      00450000
.* ******************************************************************** 00500000
.*                                                                    * 00550000
.* MACRO NAME: ISTACB1                                                * 00600000
.*                                                                    * 00650000
.* DESCRIPTIVE NAME: BUILD ACB FOR AM=VTAM                            * 00700000
.*                                                                    * 00750000
.* MACRO-COPYRIGHT: LICENSED MATERIALS - PROPERTY OF IBM                00800000
.*                                                                      00850000
.*                  THIS PRODUCT CONTAINS "RESTRICTED MATERIALS OF IBM" 00900000
.*                                                                      00950000
.*                  5695-117 (C) COPYRIGHT IBM CORP. 1991, 1998         01000000
.*                  ALL RIGHTS RESERVED.                                01050000
.*                                                                      01100000
.*                  U.S. GOVERNMENT USERS RESTRICTED RIGHTS -           01150000
.*                  USE, DUPLICATION OR DISCLOSURE RESTRICTED BY        01200000
.*                  GSA ADP SCHEDULE CONTRACT WITH IBM CORP.            01250000
.*                                                                      01300000
.*                  SEE COPYRIGHT INSTRUCTIONS.                         01350000
.*                                                                      01400000
.* FUNCTION:   THE FUNCTION OF THIS MACRO IS TO BUILD,         @ZA34940 01450000
.*             AT ASSEMBLY TIME, AN ACB FOR A VTAM USER.       @ZA34940 01500000
.*             ISTACB1 IS INVOKED BY THE MACRO ACB WHEN        @ZA34940 01550000
.*             A USER ISSUES AN ACB MACRO WITH 'AM=VTAM'       @ZA34940 01600000
.*             SPECIFIED.                                      @ZA34940 01650000
.*                                                             @ZA34940 01700000
.* CHANGE ACTIVITY:                                            @ZA34940 01750000
.*      CBMM SPLIT SUPPORT                                     @ZA34940 01800000
.*      SNA4 SUPPORT                                           @G69AKJP 01850000
.*      DCR 4790.00Z                                           @D5P4790 01900000
.*      DCR 4791.00                                            @D5K4791 01950000
.*      DCR 4792.00                                            @D5K4792 02000000
.*      PTM P008914                                            @P5K8914 02050000
.*      PTM P008915                                            @P5K8915 02100000
.*      PTM P008979                                            @P5K8979 02150000
.*                                                                      02200000
.* $MAC(ISTACB1),COMP(O/C),PROD(VTAM): VTAM ACB MACRO PROCESSOR         02250000
.*                                                                      02300000
.* FLAG REASON   RELEASE DATE   ORIGIN  FLAG DESCRIPTORS                02350000
.* ---- -------- ------- ------ ------  ---------------------------     02400000
.* $B1= AP@@@CLD HVT3401 890314 496698: Persistent LU-LU Sessions       02450000
.* $B2= AA@@@MST HVT3401 891018 042845: CROSS-MEMORY API SUPPORT        02500000
.* $V1= P076950  HVT3401 891129 042845: COMMENT CHANGES                 02550000
.* $KF= NQNSTG2  NQNAME2 910605 457572: ENHANCED NQN SUPPORT            02600000
.* $T1= P099597  NQNAME2 920206 457572: FORMAT ASSEMBLER OUTPUT         02650000
.* $I1= FF@@@MST APPCFDX 911116 879315: APPC Full-Duplex support        02700000
.* $T2= P100577  NQNAME2 920224 879315: Missing LCLC causes AsmXF error 02750000
.* $L1= FO@@@MST PERFMON 920609 043006: Performance Monitor             02800000
.* $L2= KA@@@PLD APPCRR  930812 576146: DCE Security Services           02850000
.* $U2= P105579  KEEPFRR 931008 879315: New ACB keyword                 02900000
.* $U3= P109285  KEEPFRR 931104 879315: Implement FZKDR001 - remove     02950000
.*                                      keyword added by P105579        03000000
.* $N1= P126308  POLARIS 970918 879315: EXITFRR= on ACB PARMS           03025000
.* ******************************************************************** 03050000
.*                                                                      03100000
.*                                                             @G69AKJP 03150000
.* GLOBALS                                                      X04SVHS 03200000
         GBLC  &IDAMAC1,&IDAMAC2,&IDAMAC3 MACRF CODES           X04SVHS 03250000
         GBLC  &IDAIFL1                 CATALOG/CRA CODES       X04SVHS 03300000
         GBLA  &IDAENO                  NUMBER OF ELEMENTS     @ZA06420 03350000
.* LOCALS                                                       X04SVHS 03400000
         LCLA  &VTMEXT                  LENGTH OF VTAM EXT     X03004HS 03450000
         LCLA  &LEN                     SUBPARAMETER ACB LENGTH  X03004 03500000
         LCLA  &STYPE                   ACCESS METHOD ID        X04SVHS 03550000
         LCLA  &I                       LOOP COUNTER           @G69AKJP 03600000
         LCLA  &L                       LENGTH(KEYWORD VALUE)  @G69AKJP 03650000
         LCLA  &ACBFXD                  USER FIELD             @D5K4792 03700000
         LCLB  &A,&M                    CCTYPE BITS                     03750000
         LCLB  &F,&N,&S,&SS             KEEPFRR, NQNAMES, SRBEXIT,      03800000
.*                                      AND WHETHER                     03850000
.*                                      SRBEXIT WAS SPECIFIED @B2A @KFA 03900000
         LCLB  &PSC                     PERSISTENT CAPABLE BIT     @T1C 03950000
         LCLB  &FD                      APPC FULL-DUPLEX BIT       @I1A 04000000
         LCLB  &PM                      PERFORMANCE MONITOR BIT    @L1A 04050000
         LCLB  &XF                      EXITFRR BIT                @N1A 04075000
         LCLC  &NIB                     NIB ADDRESS            @G69AKJP 04100000
         LCLC  &APVTR                   APPLVCTR ADDRESS           @L2A 04150000
         LCLC  &USE                     USER FIELD             @D5K4791 04200000
         LCLC  &USED                    USER DEFAULT FIELD     @D5K4791 04250000
         LCLC  &USERFLD                 USER FIELD             @D5K4791 04300000
         LCLC  &PERSIST                 PERSISTENT CAPABLE FIELD   @B1A 04350000
         LCLC  &FDX                     APPC FULL-DUPLEX VALUE     @I1A 04400000
         LCLC  &PERFMON                 PERFORMANCE MONITOR VALUE  @L1A 04450000
         LCLC  &CHAR1,&CHAR2,&CHAR3,&CHAR4      USER FIELD     @D5K4791 04500000
         LCLC  &FRR,&SRB                CHARACTER INPUT            @B2A 04550000
         LCLC  &NQN                     NQNAMES INPUT VALUE        @T2A 04600000
         LCLC  &XFRR                    EXITFRR INPUT VALUE        @N1A 04625000
.*                                                                      04650000
.*             INITIALIZE GLOBALS TO ZERO                               04700000
&IDAMAC1 SETC  '00000000'                                       X04SVHS 04750000
&IDAMAC2 SETC  '00000000'                                       X04SVHS 04800000
&IDAMAC3 SETC  '00000000'                                       X04SVHS 04850000
&IDAIFL1 SETC  '00000000'                                       X04SVHS 04900000
&IDAENO  SETA  0                                               @ZA06420 04950000
.*                                                                      05000000
.*       DIAGNOSE ALL KEYWORDS IN PARMS SUBLIST FOR            @G69AKJP 05050000
.*       VTAM VALIDITY                                         @G69AKJP 05100000
.*                                                             @G69AKJP 05150000
.DOPARMS ANOP                                                  @G69AKJP 05200000
&NIB     SETC  '0'                      INITIALIZE NIB ADDRESS @G69AKJP 05250000
&APVTR   SETC  '0'                      INITIALIZE APPLVCTR ADDRESS     05300000
.*                                                                 @L2A 05350000
&USE     SETC  'F''0'''                 INITIALIZE USER FIELD  @D5K4791 05400000
&USED    SETC  'F''0'''                 USER FIELD DEFAULT     @D5K4791 05450000
&I       SETA  0                        INITIALIZE COUNTER     @G69AKJP 05500000
&SS      SETB  0                        INITIALIZE TO SRBEXIT NOT       05550000
.*                                      CODED                      @V1C 05600000
         AIF   ('&PARMS' EQ '').P0DONE                         @G69AKJP 05650000
.PARMS   ANOP                                                  @G69AKJP 05700000
&I       SETA  &I+1                                            @G69AKJP 05750000
         AIF   (&I GT N'&PARMS).P0DONE  BR IF NO MORE PARMS    @G69AKJP 05800000
         AIF   ('&PARMS(&I)'(1,4) EQ 'NIB=').NIBAD             @G69AKJP 05850000
         AIF   ('&PARMS(&I)'(1,8) EQ 'USERFLD=').USERAD        @D5K4791 05900000
         AIF   ('&PARMS(&I)'(1,8) EQ 'KEEPFRR=').FRRPRM            @B2A 05950000
         AIF   ('&PARMS(&I)'(1,8) EQ 'PERSIST=').PERST             @B1A 06000000
         AIF   ('&PARMS(&I)'(1,8) EQ 'SRBEXIT=').SRBPRM            @B2A 06050000
         AIF   ('&PARMS(&I)'(1,8) EQ 'NQNAMES=').NQNPRM            @KFA 06100000
         AIF   ('&PARMS(&I)'(1,4) EQ 'FDX=').AFDXPRM               @I1A 06150000
         AIF   ('&PARMS(&I)'(1,8) EQ 'PERFMON=').PFMNPRM           @L1A 06200000
         AIF   ('&PARMS(&I)'(1,9) EQ 'APPLVCTR=').APVTRAD          @L2A 06250000
         AIF   ('&PARMS(&I)'(1,8) EQ 'EXITFRR=').EXFRR             @N1A 06275000
         ISTERMAC 11,&PARMS(&I),PARMS                          @G69AKJP 06300000
         AGO   .PARMS                                          @G69AKJP 06350000
.NIBAD   ANOP                                                  @G69AKJP 06400000
&L       SETA  K'&PARMS(&I)-4           LENGTH OF CHAR STRING  @G69AKJP 06450000
         AIF   (&L EQ 0).PARMSND        TEST FOR NULL          @G69AKJP 06500000
&NIB     SETC  '&PARMS(&I)'(5,&L)       &NIB = CHAR STRING     @G69AKJP 06550000
.PARMSND ANOP                                                  @G69AKJP 06600000
         AGO   .PARMS                                          @G69AKJP 06650000
.*                                                                 @B2A 06700000
.* **************** TEST KEEPFRR OPERAND ********************      @B2A 06750000
.FRRPRM  ANOP                                                      @B2A 06800000
&L       SETA  K'&PARMS(&I)-8           GET LENGTH OF PARAMETER    @B2A 06850000
         AIF   (&L EQ 0).FRRNO          IF OMITTED, DEFAULT IS NO  @B2A 06900000
&FRR     SETC  '&PARMS(&I)'(9,&L)       GET VALUE SUPPLIED         @B2A 06950000
         AIF   ('&FRR' EQ 'NO').FRRNO   IF NO, PROCESS SETTING     @B2A 07000000
         AIF   ('&FRR' EQ 'YES').FRRYES IF YES, PROCESS SETTING    @B2A 07050000
         MNOTE 12,'INVALID KEYWORD VALUE KEEPFRR=&FRR'                  07100000
.*                                      INVALID VALUE SPECIFIED    @B2A 07150000
.FRRNO   ANOP                           KEEPFRR=NO OR OMITTED      @B2A 07200000
&F       SETB  0                        SET FLAG BIT VARIABLE OFF  @B2A 07250000
         AGO   .FRRDONE                 FINISH PROCESSING          @B2A 07300000
.FRRYES  ANOP                           KEEPFRR=YES                @B2A 07350000
&F       SETB  1                        SET FLAG BIT VARIABLE ON   @B2A 07400000
.FRRDONE ANOP                           FINSIHED PROCESSING        @B2A 07450000
         AGO   .PARMS                   GET NEXT KEYWORD           @B2A 07500000
.*                                                                 @B2A 07550000
.* **************** TEST SRBEXIT OPERAND ********************      @B2A 07600000
.*                                                                 @B2A 07650000
.SRBPRM  ANOP                                                      @B2A 07700000
&SS      SETB  1                        INDICATE 'SRBEXIT' CODED   @V1C 07750000
&L       SETA  K'&PARMS(&I)-8           GET LENGTH OF PARAMETER    @B2A 07800000
         AIF   (&L EQ 0).SRBNO          IF OMITTED, DEFAULT IS NO  @B2A 07850000
&SRB     SETC  '&PARMS(&I)'(9,&L)       GET VALUE SUPPLIED         @B2A 07900000
         AIF   ('&SRB' EQ 'NO').SRBNO   IF NO, PROCESS SETTING     @B2A 07950000
         AIF   ('&SRB' EQ 'YES').SRBYES IF YES, PROCESS SETTING    @B2A 08000000
         MNOTE 12,'INVALID KEYWORD VALUE SRBEXIT=&SRB'                  08050000
.*                                      INVALID VALUE SPECIFIED    @B2A 08100000
.SRBNO   ANOP                           SRBEXIT SET TO NO OR OMITTED    08150000
.*                                                                 @B2A 08200000
&S       SETB  0                        SET FLAG BIT TO 0 FOR LATER     08250000
.*                                      EXPANSION                  @B2A 08300000
         AGO   .SRBDONE                 FINISH PROCESSING          @B2A 08350000
.SRBYES  ANOP                           SRBEXIT SET TO YES         @B2A 08400000
&S       SETB  1                        SET FLAG BIT TO 1 FOR LATER     08450000
.*                                      EXPANSION                  @B2A 08500000
.SRBDONE ANOP                           FINSIHED PROCESSING        @B2A 08550000
         AGO   .PARMS                   GET NEXT KEYWORD           @B2A 08600000
.*                                                                 @KFA 08650000
.*****************TEST NQNAMES OPERAND **************************  @KFA 08700000
.*                                                                 @KFA 08750000
.NQNPRM  ANOP                                                      @KFA 08800000
&L       SETA  K'&PARMS(&I)-8           GET LENGTH OF PARAMETER    @KFA 08850000
         AIF   (&L EQ 0).NQNNO          IF OMITTED, DEFAULT IS NO  @KFA 08900000
&NQN     SETC  '&PARMS(&I)'(9,&L)       GET SUPPLIED VALUE         @KFA 08950000
         AIF   ('&NQN'(1,&L) EQ 'NO').NQNNO                        @KFA 09000000
.*                                      NQNAMES=NO WAS CODED       @KFA 09050000
         AIF   ('&NQN'(1,&L) EQ 'YES').NQNYES                      @KFA 09100000
.*                                      NQNAMES=YES WAS CODED      @KFA 09150000
         MNOTE 12,'INVALID KEYWORD VALUE NQNAMES=&NQN'             @KFA 09200000
.*                                      INVALID STRING SUPPLIED    @KFA 09250000
.NQNNO   ANOP                           NQNAMES=NO OR OMITTED      @KFA 09300000
.*                                                                 @KFA 09350000
&N       SETB  0                        SET FLAG BIT TO 0 FOR LATER     09400000
.*                                      EXPANSION                  @KFA 09450000
         AGO   .NQNDONE                 FINISH PROCESSING          @KFA 09500000
.NQNYES  ANOP                           NQNAMES=YES SPECIFIED      @KFA 09550000
&N       SETB  1                        SET FLAG BIT TO 1 FOR LATER     09600000
.*                                      EXPANSION                  @KFA 09650000
.NQNDONE ANOP                           FINISHED PROCESSING        @KFA 09700000
         AGO   .PARMS                   GET NEXT KEYWORD           @KFA 09750000
.*                                                                 @KFA 09800000
.*****************TEST PERSIST OPERAND **************************  @KFA 09850000
.*                                                                 @KFA 09900000
.PERST   ANOP                                                      @B1A 09950000
&L       SETA  K'&PARMS(&I)-8    LENGTH OF CHAR STRING             @B1A 10000000
         AIF   (&L EQ 0).PRSTNO  FIELD MISSING-DEFAULT TO NO       @B1A 10050000
&PERSIST SETC  '&PARMS(&I)'(9,&L)  &PERSIST = CHAR STRING          @B1A 10100000
         AIF   ('&PERSIST'(1,&L) EQ 'YES').PRSTYS                  @B1A 10150000
         AIF   ('&PERSIST'(1,&L) EQ 'NO').PRSTNO                   @B1A 10200000
.*                                                                 @B1A 10250000
         MNOTE 12,'INVALID KEYWORD VALUE PERSIST=&PERSIST'         @B1A 10300000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT        @B1A 10350000
.PRSTYS  ANOP                                                      @B1A 10400000
&PSC     SETB  1                 PERSISTENT CAPABLE           @B1A @T1C 10450000
         AGO   .PARMS                                              @B1A 10500000
.PRSTNO  ANOP                                                      @B1A 10550000
&PSC     SETB  0                 NOT PERSISTENT CAPABLE       @B1A @T1C 10600000
         AGO   .PARMS                                              @B1A 10650000
.*                                                                 @I1A 10700000
.***************** TEST FDX    OPERAND **************************  @I1A 10750000
.AFDXPRM ANOP                                                      @I1A 10800000
&L       SETA  K'&PARMS(&I)-4    LENGTH OF CHAR STRING             @I1A 10850000
         AIF   (&L EQ 0).FDXNO   FIELD NULL - DEFAULT TO NO        @I1A 10900000
&FDX     SETC  '&PARMS(&I)'(5,&L)  &FDX = CHAR STRING              @I1A 10950000
         AIF   ('&FDX'(1,&L) EQ 'YES').FDXYES                      @I1A 11000000
         AIF   ('&FDX'(1,&L) EQ 'NO').FDXNO                        @I1A 11050000
.*                                                                 @I1A 11100000
         MNOTE 12,'INVALID KEYWORD VALUE FDX=&FDX'                 @I1A 11150000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT        @I1A 11200000
.FDXYES  ANOP                                                      @I1A 11250000
&FD      SETB  1                 FDX=YES SPECIFIED ON ACB          @I1A 11300000
         AGO   .PARMS                                              @I1A 11350000
.FDXNO   ANOP                                                      @I1A 11400000
&FD      SETB  0                 FDX=NO SPECIFIED ON ACB (DEFAULT) @I1A 11450000
         AGO   .PARMS                                              @I1A 11500000
.*                                                                 @L1A 11550000
.*****************TEST PERFMON OPERAND **************************  @L1A 11600000
.PFMNPRM ANOP                                                      @L1A 11650000
&L       SETA  K'&PARMS(&I)-8    LENGTH OF CHAR STRING             @L1A 11700000
         AIF   (&L EQ 0).PFMNNO  FIELD NULL - DEFAULT TO NO        @L1A 11750000
&PERFMON SETC  '&PARMS(&I)'(9,&L)  &PERFMON = CHAR STRING          @L1A 11800000
         AIF   ('&PERFMON'(1,&L) EQ 'YES').PFMNYES                 @L1A 11850000
         AIF   ('&PERFMON'(1,&L) EQ 'NO').PFMNNO                   @L1A 11900000
.*                                                                 @L1A 11950000
         MNOTE 12,'INVALID KEYWORD VALUE PERFMON=&PERFMON'         @L1A 12000000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT        @L1A 12050000
.PFMNYES ANOP                                                      @L1A 12100000
&PM      SETB  1                 PERFMON=YES SPECIFIED ON ACB      @L1A 12150000
         AGO   .PARMS                                              @L1A 12200000
.PFMNNO  ANOP                                                      @L1A 12250000
&PM      SETB  0                 PERFMON=NO SPECIFIED  (DEFAULT)   @L1A 12300000
         AGO   .PARMS                                              @L1A 12350000
.*                                                                      12400000
.*****************TEST APPLVCTR OPERAND *************************  @L2A 12450000
.*                                                                 @L2A 12500000
.APVTRAD ANOP                                                      @L2A 12550000
&L       SETA  K'&PARMS(&I)-9           GET LENGTH OF PARAMETER    @L2A 12600000
         AIF   (&L EQ 0).APVDONE        IF OMITTED, TAKE DEFAULT   @L2A 12650000
&APVTR   SETC  '&PARMS(&I)'(10,&L)      GET SUPPLIED VALUE         @L2A 12700000
.APVDONE ANOP                           FINISHED PROCESSING        @L2A 12750000
         AGO   .PARMS                   GET NEXT KEYWORD           @L2A 12800000
.*                                                                 @L2A 12850000
.* **************** TEST EXITFRR OPERAND ********************      @N1A 12858300
.EXFRR   ANOP                                                      @N1A 12866600
&L       SETA  K'&PARMS(&I)-8           GET LENGTH OF PARAMETER    @N1A 12874900
         AIF   (&L EQ 0).XFRRNO         IF OMITTED, DEFAULT IS NO  @N1A 12883200
&XFRR    SETC  '&PARMS(&I)'(9,&L)       GET VALUE SUPPLIED         @N1A 12891500
         AIF   ('&XFRR' EQ 'NO').XFRRNO   IF NO, PROCESS SETTING   @N1A 12899800
         AIF   ('&XFRR' EQ 'YES').XFRRYES IF YES, PROCESS SETTING  @N1A 12908100
         MNOTE 12,'INVALID KEYWORD VALUE EXITFRR=&XFRR'                 12916400
.*                                      INVALID VALUE SPECIFIED    @N1A 12924700
.XFRRNO  ANOP                           EXITFRR=NO OR OMITTED      @N1A 12933000
&XF      SETB  0                        SET FLAG BIT VARIABLE OFF  @N1A 12941300
         AGO   .XFRDONE                 FINISH PROCESSING          @N1A 12949600
.XFRRYES ANOP                           EXITFRR=YES                @N1A 12957900
&XF      SETB  1                        SET FLAG BIT VARIABLE ON   @N1A 12966200
.XFRDONE ANOP                           FINISHED PROCESSING        @N1A 12974500
         AGO   .PARMS                   GET NEXT KEYWORD           @N1A 12982800
.*                                                                 @N1A 12991100
.* **************** TEST USER FIELD OPERAND *****************  @D5K4791 13000000
.USERAD  ANOP                                                  @D5K4791 13025000
.*                                                             @D5K4791 13050000
&ACBFXD  SETA  K'&PARMS(&I)-8      LENGTH OF CHAR STRING       @D5K4791 13100000
         AIF   (&ACBFXD EQ 0).ACBOUT    .  FIELD MISSING       @D5K4791 13150000
&USE     SETC  '&PARMS(&I)'(9,&ACBFXD)   &USE = CHAR STRING    @D5K4791 13200000
         AIF   ('&USE'(1,2) EQ 'C''').ACBCKP                   @D5K4791 13250000
         AIF   ('&USE'(1,2) EQ 'X''').ACBCKP                   @D5K4791 13300000
         AIF   ('&USE'(1,2) EQ 'F''').ACBCKP                   @D5K4791 13350000
         AIF   ('&USE'(1,2) EQ 'A(').ACBCKQ                    @D5K4791 13400000
         AIF   ('&USE'(1,2) EQ 'V(').ACBCKQ                    @D5K4791 13450000
         AGO   .ACBERQ                                         @D5K4791 13500000
.ACBCKP  AIF   ('&USE'(&ACBFXD,1) EQ '''').ACBGOP              @D5K4791 13550000
         AGO   .ACBERP                                         @P5K8914 13600000
.ACBCKQ  AIF   ('&USE'(&ACBFXD,1) EQ ')').ACBGOP               @D5K4791 13650000
.ACBERP  MNOTE 0,'CHECK FOR UNBALANCED PARENTHESES OR APOSTROPHES'      13700000
.*                                                             @P5K8914 13750000
.ACBERQ  MNOTE 12,'INVALID KEYWORD VALUE USERFLD=&USERFLD'     @D5K4791 13800000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT    @P5K8915 13850000
.ACBOUT  ANOP                                                  @D5K4791 13900000
&USE     SETC  '&USED'           USER FIELD DEFAULTED          @D5K4791 13950000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT    @P5K8915 14000000
.ACBGOP  ANOP                                                  @D5K4791 14050000
&USE     SETC  '&USE'(1,1)'L4'.'&USE'(2,&ACBFXD-1)             @D5K4791 14100000
         AGO   .PARMS            CHECK FOR ADDITIONAL INPUT    @P5K8915 14150000
.P0DONE  ANOP                                                  @P5P8802 14200000
         ISTCBACB MACRF=&MACRF,CATALOG=&CATALOG,CRA=&CRA,      @ZA34940*14250000
               AM=&AM,CALLER=ACB        SET VALUES FOR BIT     @ZA34940 14300000
.*                                      KEYWORDS IN GLOBALS     X04SVHS 14350000
.*            ISTCBACB IS CALLED TO PROCESS BIT KEYWORDS MACRF,@ZA34940 14400000
.*             CATALOG, AND CRA.  IT WILL VALIDITY CHECK OPTIONS        14450000
.*             SPECIFIED, MUTUALLY EXCLUSIVE CHECK WHERE APPLICABLE,    14500000
.*             AND SET GLOBALS WITH THE VALUES OF THE KEYWORDS.         14550000
.*                                                                      14600000
&LEN     SETA  76                       SET FOR COMMON LENGTH    X03004 14650000
         AIF   ('&AM' EQ '').SETLEN     IF AM IS NULL           X04SVHS 14700000
.*                                      DEFAULT LEN TO VSAM,    X04SVHS 14750000
.*                                      AND SUBTYPE TO 0        X04SVHS 14800000
.*                                                                      14850000
&STYPE   SETA  X'10'                    SET VSAM SUBTYPE        X04SVHS 14900000
         AIF   ('&AM' EQ 'VSAM').SETLEN IS IT VSAM              X04SVHS 14950000
&VTMEXT  SETA  32                       NO, SET VTAM EXT LEN   @D5P4790 15000000
&STYPE   SETA  X'20'                    SET VTAM SUBTYPE        X04SVHS 15050000
         AIF   ('&AM' EQ 'VTAM').SETLEN IF VTAM SET VTAM LEN    X04SVHS 15100000
.*                                      AND SUBTYPE             X04SVHS 15150000
.*                                      ELSE ERROR              X04SVHS 15200000
&STYPE   SETA  0                        ZERO SUBTYPE            X04SVHS 15250000
&VTMEXT  SETA  0                        GENERATE NORMAL LEN ACB X04SVHS 15300000
         ISTERMAC 3,AM,&AM              ISSUE ERROR MSG        @ZA34940 15350000
.SETLEN  ANOP                                                   X04SVHS 15400000
&NAME    DS    0F                                                       15450000
         DC    X'A0'                    ACB IDENTIFICATION              15500000
         DC    AL1(&STYPE)              ACB SUBTYPE             X04SVHS 15550000
.*             X'11' - X'1F' ARE RESERVED FOR VSAM              X04SVHS 15600000
.*             X'21' - X'2F' ARE RESERVED FOR VTAM              X04SVHS 15650000
         DC    AL2(&LEN+&VTMEXT)        ACB LENGTH             X03004HS 15700000
         DC    A(0)                     AMB LIST POINTER                15750000
         AIF   ('&AM' NE 'VTAM').SETIRR IF NOT VTAM ACBINRTN = ZERO     15800000
.*                                                             X03004HS 15850000
         DC    A(*+&LEN-8)              PTR TO VTAM EXTENSION  X03004HS 15900000
         AGO   .MACRF1                                         X03004HS 15950000
.SETIRR  ANOP                                                           16000000
         DC    A(0)                     INTERFACE ROUTINE POINTER       16050000
.*       MACRF PROCESSING                                               16100000
.MACRF1  ANOP                                                   X04SVHS 16150000
         DC    B'&IDAMAC1'              MACRF(1)                X04SVHS 16200000
         DC    B'&IDAMAC2'              MACRF(2)                X04SVHS 16250000
         DC    AL1(&BSTRNO)             NO OF CONCURRENT        X04SVHS 16300000
*                                       STRINGS FOR AIX PATH    X04SVHS 16350000
         DC    AL1(&STRNO)              NUMBER OF STRINGS       X04SVHS 16400000
         DC    AL2(&BUFND)              NUMBER OF DATA BUFFERS          16450000
         DC    AL2(&BUFNI)              NUMBER OF INDEX BUFFERS         16500000
         DC    B'&IDAMAC3'              MACRF(3)                X04SVHS 16550000
         DC    B'00000000'              MACRF(4)                X04SVHS 16600000
         DC    AL2(0)                   JES BUFFER POOL/NUMBER  X04SVHS 16650000
*                                       OF JOURNAL BUFFERS              16700000
         DC    X'80'                    RECFM=A                         16750000
         AIF   ('&CCTYPE' EQ '').CCSET                                  16800000
&A       SETB  ('&CCTYPE' EQ 'A')                                       16850000
&M       SETB  ('&CCTYPE' EQ 'M')                                       16900000
         AIF   (&A OR &M).CCSET                                         16950000
         ISTERMAC 5,CCTYPE,&CCTYPE                             @ZA34940 17000000
.CCSET   DC    B'00000&A&M.0'           CONTROL CHARACTER TYPE          17050000
         DC    X'0008'                  DSORG=ACB                       17100000
         AIF   ('&MAREA' EQ '0').GENM0 IF &MAREA = 0 BYPASS     X04SVHS 17150000
.*                                      CHKING FOR AM=VSAM      X04SVHS 17200000
         AIF   ('&AM' EQ '' OR '&AM' EQ 'VSAM').GENMA           X04SVHS 17250000
.*                                      IF NOT VSAM ACB, ERROR  X04SVHS 17300000
         ISTERMAC 24,MAREA              ISSUE ERR MSG          @ZA34940 17350000
         AGO   .GENM0                                           X04SVHS 17400000
.GENMA   ANOP                                                   X04SVHS 17450000
         DC    A(&MAREA)                MESSAGE AREA            X04SVHS 17500000
         AGO   .GPSWD                                           X04SVHS 17550000
.GENM0   ANOP                                                   X04SVHS 17600000
         DC    A(0)                                             X04SVHS 17650000
.GPSWD   ANOP                                                   X04SVHS 17700000
         DC    A(&PASSWD)               PASSWORD POINTER                17750000
         DC    A(&EXLST)                EXIT LIST POINTER               17800000
         AIF   ('&DDNAME' NE '' AND '&AM' NE 'VTAM').DDNMCHK   @ZA31869 17850000
         AIF   ('&DDNAME' EQ '').VTAM   NO DDNAME SPECIFIED?     X03004 17900000
         ISTERMAC 24,DDNAME             NOT A VTAM KEYWORD     @ZA34940 17950000
         AGO   .ACBB2                                            X03004 18000000
.VTAM    ANOP                                                    X03004 18050000
         AIF   ('&AM' NE 'VTAM').NONAME TRY LABEL IF NO VTAM     X03004 18100000
         DC    X'FF'                    INDICATE VTAM            X03004 18150000
         DC    XL7'00'                  PAD WITH ZEROS           X03004 18200000
         AGO   .ACBB3                                            X03004 18250000
.NONAME  ANOP                                                    X03004 18300000
         AIF   ('&NAME' EQ '').BLANK    IS THERE A LABEL                18350000
         DC    CL8'&NAME'               DDNAME                          18400000
         AGO   .ACBB3                                                   18450000
.BLANK   DC    CL8' '                   DDNAME                          18500000
         AGO   .ACBB3                                                   18550000
.DDNMCHK ANOP                                                  @ZA31869 18600000
         AIF   (K'&DDNAME GT 8 OR '&DDNAME'(1,1) LT 'A' OR             C18650000
               '&DDNAME'(1,1) GT 'Z').DDNMERR  CHECK FOR VALID @ZA31869 18700000
         AGO   .ACBB2                          DDNAME          @ZA31869 18750000
.DDNMERR ISTERMAC 3,DDNAME,&DDNAME      INVALID DDNAME         @ZA34940 18800000
.ACBB2   DC    CL8'&DDNAME'             DDNAME                          18850000
.ACBB3   DC    X'02'                    OFLAGS                          18900000
         DC    AL1(0)                   ERFLAGS                         18950000
         DC    B'&IDAIFL1'              INFLGS(1)               X04SVHS 19000000
         DC    B'00000000'              INFLGS(2)               X04SVHS 19050000
         DC    A(&JFCB)                 OPENJ JFCB POINTER              19100000
         DC    A(&BUFSP)                BUFFER SPACE                    19150000
         AIF   ('&BLKSIZE' EQ '0').CHKMLN                       X04SVHS 19200000
         AIF   ('&MLEN' EQ '0').BLKSET                          X04SVHS 19250000
.*             IF BLKSIZE AND MLEN ARE BOTH SPECIFIED           X04SVHS 19300000
.*             IT IS AN ERROR                                   X04SVHS 19350000
         ISTERMAC 19,BLKSIZE,MLEN                              @ZA34940 19400000
.BLKSET  ANOP                                                   X04SVHS 19450000
         DC    AL2(&BLKSIZE)            BLOCK SIZE                      19500000
         AGO   .LRECLST                                         X04SVHS 19550000
.CHKMLN  ANOP                                                   X04SVHS 19600000
         AIF   ('&MLEN' EQ '0').BLKSET                          X04SVHS 19650000
.*                                      IF MLEN IS SPEC,        X04SVHS 19700000
.*                                      CHECK FOR VSAM ACB      X04SVHS 19750000
         AIF   ('&AM' NE '' AND '&AM' NE 'VSAM').MLERR          X04SVHS 19800000
.*                                      IF NOT VSAM ACB, ERROR  X04SVHS 19850000
         DC    AL2(&MLEN)               MESSAGE LENGTH          X04SVHS 19900000
         AGO   .LRECLST                                         X04SVHS 19950000
.MLERR   ISTERMAC 24,MLEN               ISSUE ERR MSG          @ZA34940 20000000
         AGO   .BLKSET                                          X04SVHS 20050000
.LRECLST ANOP                                                   X04SVHS 20100000
         DC    AL2(&LRECL)              RECORD SIZE                     20150000
         AIF   ('&USERPTR' EQ '0').UPTR  NO USER PTR PROVIDED           20200000
         DC    A(&USERPTR)              ADDRESS OF USER WORKAREA        20250000
         AGO   .CBMWP                                                   20300000
.UPTR    DC    A(0)                     USER WORKAREA POINTER           20350000
.CBMWP   DC    A(0)                     CONTROL BLOCK MANIPULATION      20400000
*                                       WORKAREA POINTER                20450000
         AIF   ('&AM' EQ 'VTAM').SETAPP  WAS VTAM SPECIFIED      X03004 20500000
         AIF   ('&APPLID' EQ '0').NONAPP  WAS APPLID SPECIFIED   X03004 20550000
         ISTERMAC 18,APPLID                                    @ZA34940 20600000
.NONAPP  ANOP                           IGNORE APPLID            X03004 20650000
         DC    A(0)                     PTR TO APPL NAME         X03004 20700000
         MEXIT                          END THE MACRO            X03004 20750000
.SETAPP  ANOP                           VTAM SPECIFIED           X03004 20800000
         DC    A(&APPLID)               PTR TO APPL NAME         X03004 20850000
         LA    15,32                    SET RETURN CODE(VTAM)    X03004 20900000
         BR    14                       RETURN TO CALLER(VTAM)   X03004 20950000
         DC    B'&F.&S.&SS.&PSC.0&N.00' ACBOPT1 FLAGS         @KFA @T1C 21000000
         DC    B'&FD.&PM.0&XF.0000'     ACBOPT2 FLAGS              @N1C 21050000
         DC    F'0'                     RESERVED                 X03004 21100000
         DC    A(&NIB)                  NIB ADDRESS            @G69AKJP 21150000
         DC    A(0)                     AMSI ADDRESS           @D5K4791 21200000
         DC    A(0)                     RIVL ADDRESS           @D5K4791 21250000
         DC    &USE                     USER FIELD             @D5K4791 21300000
         DC    A(&APVTR)                APPLVCTR ADDRESS           @L2A 21350000
.OUT     MEND                                                           21400000
